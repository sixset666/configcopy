// Модуль документа ВозвратПоставщикуАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка наличия резервов на автомобиль
//
// Параметры
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль()
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("МетаданныеИмя",ЭтотОбъект.Метаданные().Имя);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Если ТипЗнч(РезультатОбработки) = Тип("ОбработкаОбъект.ИнформационноеПоле") Тогда
		СтрокаСообщений="";
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,СтрокаСообщений);
		Если НЕ ПустаяСтрока(СтрокаСообщений) Тогда
			Для НомерСтроки=1 По СтрЧислоСтрок(СтрокаСообщений) Цикл
				СообщениеПользователю=СтрПолучитьСтроку(СтрокаСообщений,НомерСтроки);
				РезультатОбработки.ДобавитьСтрокуСообщения(СообщениеПользователю,"Важное",Неопределено,Неопределено);
			КонецЦикла; 
		КонецЕсли; 
	Иначе
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,РезультатОбработки);
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("ИдентификаторАвтомобиля",1);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	ОбязательныеАвтомобили.Вставить("Партия",?(обПраво("РежимСписанияПриВозвратеПоставщику",Права,,ЭтотОбъект)=Перечисления.РежимыСписанияПриВозврате.ТолькоПоУказаннойПартии,3,2));
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("ИдентификаторАвтомобиля",3);
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Если ДокументОснование.Контрагент<>Контрагент Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Контрагент документа основания отличается от контрагента текущего документа ");
		КонецЕсли;
		
		Если ДокументОснование.ДоговорВзаиморасчетов<>ДоговорВзаиморасчетов Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Договор взаиморасчетов документа основания отличается от договора взаиморасчетов текущего документа ");
		КонецЕсли;
	КонецЕсли; 	
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);   		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;  	
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВозвратАвтомобилейПоставщику","Возврат автомобилей поставщику",Истина);
	СписокХозОпераций.Добавить("ВозвратАвтомобилейПоставщикуКомиссия","Возврат автомобилей поставщику (комиссия)");
	СписокХозОпераций.Добавить("ВозвратАвтомобилейСОтветственногоХранения","Возврат автомобилей поставщику (ответхранение)");
	Возврат СписокХозОпераций;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.СуммаДокумента КАК СуммаДокумента
	|
	|ИЗ
	|	Документ.ВозвратПоставщикуАвтомобилей КАК Док
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// Определим необходимость корректирующих проводок для поддержания баланса по подразделениям.
	ПодразделениеСклад                 = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны     = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Если было отложенное проведение по партиям, то :
	//Очистим возможные движения по регистру комплектации автомобилей 
	НаборЗаписейПартионногоРегистра=РегистрыНакопления.КомплектацияАвтомобилей.СоздатьНаборЗаписей();
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейПартионногоРегистра.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	
	//Спишем оборудование автомобиля
	НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ВозвратПоставщикуАвтомобилейАвтомобили.Автомобиль,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.ИдентификаторАвтомобиля,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.Количество,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.Цена,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.Сумма,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.СуммаВсего,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.СтавкаНДС,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.СуммаНДС,
	|	ВозвратПоставщикуАвтомобилейАвтомобили.Партия
	|ИЗ
	|	Документ.ВозвратПоставщикуАвтомобилей.Автомобили КАК ВозвратПоставщикуАвтомобилейАвтомобили
	|ГДЕ
	|	ВозвратПоставщикуАвтомобилейАвтомобили.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	РезультатАвтомобили=Запрос.Выполнить();
	ВыборкаАвтомобили=РезультатАвтомобили.Выбрать();
	Пока ВыборкаАвтомобили.Следующий() Цикл
		//Спишем оборудование по документу
		НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ВыборкаАвтомобили.Автомобиль;
		НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
		НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
		НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Номенклатура";
		НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
		//Спишем опции, установленные производителем
		НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейКомплектацияАвтомобилей.Автомобиль=ВыборкаАвтомобили.Автомобиль;
		НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
		НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
		НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Опции";
		НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
	КонецЦикла;
	Если НЕ Отказ Тогда
		НаборЗаписейКомплектацияАвтомобилей.Записать();
	КонецЕсли; 
	
	// двигаем границу последовательности комплектаций автомобилей
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); АвтомобильБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				АвтомобильБыл = Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("АвтомобильБыл",АвтомобильБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаАвтомобилей = Движения.КомплектацияАвтомобилей.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыКомплектация.Автомобиль = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыКомплектация.МоментВремени  = ШапкаДокумента.Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль		 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени	 = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;
	
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	СУММА(ВзаиморасчетыКомпании.СуммаУпр) КАК СуммаУпр
					 |ИЗ
					 |	РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
					 |ГДЕ
					 |	ВзаиморасчетыКомпании.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.СуммаУпр<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				НаборЗаписейДоходыИРасходы.Доход=Выборка.СуммаУпр;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр), 0) КАК СуммаУпр
					 |ИЗ
					 |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
					 |ГДЕ
					 |	ОстаткиАвтомобилей.Регистратор = &Регистратор
					 |	И ОстаткиАвтомобилей.СтатусПартии = &СтатусПартии";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварКупленный);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.СуммаУпр<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				НаборЗаписейДоходыИРасходы.Доход=Выборка.СуммаУпр;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр), 0) КАК СуммаУпр
					 |ИЗ
					 |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
					 |ГДЕ
					 |	КомплектацияАвтомобилей.Регистратор = &Регистратор
					 |	И КомплектацияАвтомобилей.СтатусПартии = &СтатусПартии";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартий.ТоварКупленный);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.СуммаУпр<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				НаборЗаписейДоходыИРасходы.Расход=Выборка.СуммаУпр;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 
		Возврат НЕ Отказ;
	КонецЕсли;
	
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(); 
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл,ШапкаДокумента.МоментВремени);
	КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
	Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.МоментВремени);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсУпр = ШапкаДокумента.КурсВалютыУпр;
	КонецЕсли;	
	
	// спишем реализованное оборудование
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	КомплектацияАвтомобилей.Период,
	|	КомплектацияАвтомобилей.Регистратор,
	|	КомплектацияАвтомобилей.НомерСтроки КАК НомерСтроки,
	|	КомплектацияАвтомобилей.Активность,
	|	КомплектацияАвтомобилей.ВидДвижения,
	|	КомплектацияАвтомобилей.Автомобиль,
	|	КомплектацияАвтомобилей.СкладКомпании,
	|	КомплектацияАвтомобилей.Номенклатура,
	|	КомплектацияАвтомобилей.ХарактеристикаНоменклатуры,
	|	КомплектацияАвтомобилей.СтатусПартии,
	|	КомплектацияАвтомобилей.Партия,
	|	КомплектацияАвтомобилей.ГТД,
	|	КомплектацияАвтомобилей.Количество,
	|	КомплектацияАвтомобилей.Сумма,
	|	КомплектацияАвтомобилей.СуммаНДС,
	|	КомплектацияАвтомобилей.СуммаУпр,
	|	КомплектацияАвтомобилей.СуммаПродажи,
	|	КомплектацияАвтомобилей.СуммаПродажиУпр,
	|	КомплектацияАвтомобилей.ХозОперация,
	|	КомплектацияАвтомобилей.СтавкаНДС
	|ИЗ
	|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
	|ГДЕ
	|	КомплектацияАвтомобилей.Регистратор = &Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("Регистратор", ШапкаДокумента.Ссылка);
	РезультатКомплектацияАвтомобилей = Запрос.Выполнить();
	ВыборкаКомплектацияАвтомобилей = РезультатКомплектацияАвтомобилей.Выбрать();
	НаборЗаписейРеализованныеТовары = Движения.РеализованныеТовары;
	НаборЗаписейПродажи = Движения.Продажи;
	Пока ВыборкаКомплектацияАвтомобилей.Следующий() Цикл
		Если ТипЗнч(ВыборкаКомплектацияАвтомобилей.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			Если ВыборкаКомплектацияАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия Тогда
				НоваяЗапись = НаборЗаписейРеализованныеТовары.Добавить();
				НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период                     = ШапкаДокумента.Дата;
				НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
				НоваяЗапись.Контрагент                 = ШапкаДокумента.Контрагент;
				НоваяЗапись.ДоговорВзаиморасчетов      = ШапкаДокумента.ДоговорВзаиморасчетов;
				НоваяЗапись.Автомобиль                 = ВыборкаКомплектацияАвтомобилей.Автомобиль;
				НоваяЗапись.Номенклатура               = ВыборкаКомплектацияАвтомобилей.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = ВыборкаКомплектацияАвтомобилей.ХарактеристикаНоменклатуры;
				НоваяЗапись.ДокументПередачи           = ВыборкаКомплектацияАвтомобилей.Партия;
				НоваяЗапись.ГТД                        = ВыборкаКомплектацияАвтомобилей.ГТД;
				НоваяЗапись.Количество                 = ВыборкаКомплектацияАвтомобилей.Количество;
				НоваяЗапись.СуммаУпр                   = ВыборкаКомплектацияАвтомобилей.СуммаУпр;
				НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
			КонецЕсли;
			НоваяЗапись = НаборЗаписейПродажи.Добавить();
			НоваяЗапись.Период                     = ШапкаДокумента.Дата;
			НоваяЗапись.Регистратор                = ШапкаДокумента.Ссылка;
			НоваяЗапись.ПодразделениеКомпании      = ШапкаДокумента.ПодразделениеКомпании;
			НоваяЗапись.Автомобиль                 = ВыборкаКомплектацияАвтомобилей.Автомобиль;
			НоваяЗапись.Номенклатура               = ВыборкаКомплектацияАвтомобилей.Номенклатура;
			НоваяЗапись.ДокументПродажи			   = ШапкаДокумента.Ссылка;
			НоваяЗапись.Поставщик                  = ВыборкаКомплектацияАвтомобилей.Партия.Контрагент;
			НоваяЗапись.Покупатель                 = ШапкаДокумента.Контрагент;
			НоваяЗапись.СтатусПартии               = ВыборкаКомплектацияАвтомобилей.СтатусПартии;
			НоваяЗапись.ХозОперация                = ШапкаДокумента.ХозОперация;
			НоваяЗапись.ДоговорВзаиморасчетов      = ШапкаДокумента.ДоговорВзаиморасчетов;
			НоваяЗапись.ХарактеристикаНоменклатуры = ВыборкаКомплектацияАвтомобилей.ХарактеристикаНоменклатуры;
			НоваяЗапись.СкладКомпании              = ШапкаДокумента.СкладКомпании;
			ВыборкаАвтомобили.Сбросить();
			Если ВыборкаАвтомобили.НайтиСледующий(Новый Структура("Автомобиль", ВыборкаКомплектацияАвтомобилей.Автомобиль)) Тогда
				НоваяЗапись.СтавкаНДС              = ВыборкаАвтомобили.СтавкаНДС;
			КонецЕсли; 
			НоваяЗапись.Партия                     = ВыборкаКомплектацияАвтомобилей.Партия;
			НоваяЗапись.ГТД                        = ВыборкаКомплектацияАвтомобилей.ГТД;
			НоваяЗапись.Количество                 = ВыборкаКомплектацияАвтомобилей.Количество;
			НоваяЗапись.Сумма                      = ВыборкаКомплектацияАвтомобилей.СуммаПродажи;
			НоваяЗапись.СуммаНДС                   = Окр(НоваяЗапись.Сумма*НоваяЗапись.СтавкаНДС.Ставка/100, 2);
			НоваяЗапись.СуммаУпр                   = ВыборкаКомплектацияАвтомобилей.СуммаПродажиУпр;
			НоваяЗапись.СебестоимостьУпр           = ВыборкаКомплектацияАвтомобилей.СуммаУпр;
			НоваяЗапись.Себестоимость              = Окр(обПересчет(ВыборкаКомплектацияАвтомобилей.СуммаУпр,ВалютаУпр,КурсУпр,ВалютаРегл,КурсРегл),2);
			НоваяЗапись.СуммаНДСВходящий           = ВыборкаКомплектацияАвтомобилей.СуммаНДС;
		КонецЕсли;
	КонецЦикла; 
	
	Если ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщику Тогда
		Если (НЕ БалансВедетсяПоПодразделениям ИЛИ НЕ БалансовыеПодразделенияНеРавны) Тогда	
			// Движения по Доходам и Расходам
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			|	ЕСТЬNULL(СУММА(ВозвратПоставщикуАвтомобилейТовары.СуммаВсего),0) КАК СуммаВсего
			|ИЗ
			|	Документ.ВозвратПоставщикуАвтомобилей.Товары КАК ВозвратПоставщикуАвтомобилейТовары
			|ГДЕ
			|	ВозвратПоставщикуАвтомобилейТовары.Ссылка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СуммаОборудования=Выборка.СуммаВсего;
			Иначе
				СуммаОборудования=0;
			КонецЕсли; 
			СуммаАвтомобилей = РезультатАвтомобили.Выгрузить().Итог("СуммаВсего");
			СуммаАвтомобилей = СуммаАвтомобилей + СуммаОборудования;
			СуммаАвтомобилей = обПересчет(СуммаАвтомобилей,ШапкаДокумента.ВалютаДокумента,ШапкаДокумента.КурсДокумента,ВалютаУпр,КурсУпр);
			
			СебестоимостьАвтомобилей = 0;
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			|	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр),0) КАК СуммаУпр
			|ИЗ
			|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
			|ГДЕ
			|	ОстаткиАвтомобилей.Регистратор = &Регистратор";
			Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СебестоимостьАвтомобилей = СебестоимостьАвтомобилей + (-Выборка.СуммаУпр);
			КонецЕсли; 
			Запрос=Новый Запрос;
			Запрос.Текст="ВЫБРАТЬ
			|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр),0) КАК СуммаУпр
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			|ГДЕ
			|	КомплектацияАвтомобилей.Регистратор = &Регистратор";
			Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СебестоимостьАвтомобилей = СебестоимостьАвтомобилей + Выборка.СуммаУпр;
			КонецЕсли; 
			
			СуммаДоходовИРасходов = СуммаАвтомобилей - СебестоимостьАвтомобилей;
			Если СуммаДоходовИРасходов<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.ОтклонениеСтоимостиВозврата;
				Если БалансВедетсяПоПодразделениям Тогда
					НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
				КонецЕсли;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
				НаборЗаписейДоходыИРасходы.Доход=СуммаДоходовИРасходов;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 
	Иначе
		Запрос.Текст="ВЫБРАТЬ
		             |	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр), 0) КАК СуммаУпр
		             |ИЗ
		             |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		             |ГДЕ
		             |	КомплектацияАвтомобилей.Регистратор = &Регистратор
		             |	И КомплектацияАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СебестоимостьКомплектации = Выборка.СуммаУпр;
			Если СебестоимостьКомплектации<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.ОтклонениеСтоимостиВозврата;
				НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
				НаборЗаписейДоходыИРасходы.Расход=СебестоимостьКомплектации;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции

//Добавим оборудование автомобиля
Процедура ДобавитьОборудованиеАвтомобиля(Знач Автомобиль,ИзменятьЦену)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("Дата",Дата);
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.Выгрузить());
	ДокументОбъектСтруктура.Вставить("Товары",Товары.Выгрузить());
	зфЗащищенныеФункцииСервер.РеализацияАвтомобилейДобавитьОборудованиеАвтомобиля(ДокументОбъектСтруктура,Автомобиль);
	
	Если НЕ ДокументОбъектСтруктура.Свойство("ТаблицаТоваров") Тогда
		Возврат;
	КонецЕсли; 
	
	Товары.Очистить();
	Товары.Загрузить(ДокументОбъектСтруктура.Товары);
	
	Если ИзменятьЦену Тогда
		ВалютаРегламентированногоУчетаОрганизаций=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		Если ВалютаДокумента<>ВалютаРегламентированногоУчетаОрганизаций Тогда
			ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
			Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
				СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
				КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			Иначе
				КурсУпр        = КурсВалютыУпр;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	Для каждого СтрокаТоваров Из ДокументОбъектСтруктура.ТаблицаТоваров Цикл
		СтрокиОборудования=Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля,Номенклатура,ХарактеристикаНоменклатуры",ДокументОбъектСтруктура.ИдентификаторАвтомобиля,СтрокаТоваров.Номенклатура,СтрокаТоваров.ХарактеристикаНоменклатуры));
		Если СтрокиОборудования.Количество()=0 Тогда 
			НоваяСтрокаТоваров=Товары.Добавить();
			НоваяСтрокаТоваров.ИдентификаторАвтомобиля=ДокументОбъектСтруктура.ИдентификаторАвтомобиля;
			НоваяСтрокаТоваров.Номенклатура=СтрокаТоваров.Номенклатура;
			НоваяСтрокаТоваров.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
			ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрокаТоваров);
		Иначе
			НоваяСтрокаТоваров=СтрокиОборудования[0];
		КонецЕсли; 
		НоваяСтрокаТоваров.Количество = СтрокаТоваров.Количество/?(обЗначениеНеЗаполнено(НоваяСтрокаТоваров.Коэффициент),1,НоваяСтрокаТоваров.Коэффициент);
		Если ИзменятьЦену Тогда
			Если ВалютаДокумента=ВалютаРегламентированногоУчетаОрганизаций Тогда
				НоваяСтрокаТоваров.СуммаВсего=СтрокаТоваров.СуммаПродажи;
			Иначе
				НоваяСтрокаТоваров.СуммаВсего=обПересчет(СтрокаТоваров.СуммаПродажиУпр,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
			КонецЕсли; 
		КонецЕсли; 
		ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрокаТоваров);
	КонецЦикла;
КонецПроцедуры

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Автомобили.Итог("СуммаВсего")+Товары.Итог("СуммаВсего");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//   								  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ВозвратАвтомобилейПоставщикуКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
		КонецЕсли; 
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщикуКомиссия Тогда
			Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомитентом И
				 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
				 #Если Клиент Тогда
				 Предупреждение("Необходимо выбрать договор комиссии!",10);
				 #КонецЕсли
				 ДоговорВзаиморасчетов=Неопределено;
				 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				 Возврат Ложь;
			КонецЕсли; 
		Иначе
			Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Покупка И
				 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
				 #Если Клиент Тогда
				 Предупреждение("Необходимо выбрать договор поставки!",10);
				 #КонецЕсли
				 ДоговорВзаиморасчетов=Неопределено;
				 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				 Возврат Ложь;
			КонецЕсли; 
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладКомпании = Неопределено;
			КонецЕсли; 
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		//Проставим количество
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
		// Заполним ставку НДС
		Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			Если НЕ обЗначениеНеЗаполнено(Контрагент) И Контрагент.ОсвобожденОтНДС Тогда
				Запрос=Новый Запрос("
				|ВЫБРАТЬ ПЕРВЫЕ 1 СпрСтавкиНДС.Ссылка КАК СтавкаБезНДС
				|ИЗ Справочник.СтавкиНДС КАК СпрСтавкиНДС
				|ГДЕ СпрСтавкиНДС.Ставка=0");
				РезультатСтавкаБезНДС=Запрос.Выполнить();
				Если РезультатСтавкаБезНДС.Пустой() Тогда
					ТекСтрока.СтавкаНДС=Неопределено;
				Иначе
					ТекСтрока.СтавкаНДС=РезультатСтавкаБезНДС.Выгрузить().Получить(0).СтавкаБезНДС;
				КонецЕсли;
			Иначе
				ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(ТекСтрока.Партия) Тогда
			ТекСтрока.Партия=ДокументОснование;
		КонецЕсли; 
		Рез=ОбработкаРеквизита("Автомобили.Партия",ТекСтрока,ЭтаФорма,ДопПараметры);
		ДобавитьОборудованиеАвтомобиля(ТекСтрока.Автомобиль,Истина);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Автомобили.Количество" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли;
		Попытка
			НеРассчитыватьСуммуВсего=ДопПараметры.НеРассчитыватьСуммуВсего;
		Исключение
			НеРассчитыватьСуммуВсего=Ложь;
		КонецПопытки; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			//Сумма всего равна сумме
			ТекСтрока.СуммаНДС=Окр((ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
			Если НЕ НеРассчитыватьСуммуВсего Тогда
				ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
			КонецЕсли; 
		Иначе
			//НДС в цену не включен
			//Получим сумму НДС
			ТекСтрока.СуммаНДС=Окр((ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100,2);
			Если НЕ НеРассчитыватьСуммуВсего Тогда
				ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		Если ТекСтрока.Количество<>0 Тогда
			ТекСтрока.Цена=Окр(ТекСтрока.Сумма/ТекСтрока.Количество,2);
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаВсего" Тогда
		//Получим новую сумму НДС как процент от суммы всего
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			ТекСтрока.Сумма=ТекСтрока.СуммаВсего;
		Иначе
			//НДС в цену не включен
			ТекСтрока.Сумма=Окр((100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		КонецЕсли;
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры=Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("НеРассчитыватьСуммуВсего",Истина);
		Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаНДС" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Партия" Тогда
		// Поищем автомобиль в документе покупки по строке
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Партия) Тогда
			СтрокаДокументаОснования=ТекСтрока.Партия.Автомобили.Найти(ТекСтрока.Автомобиль,"Автомобиль");
			Если СтрокаДокументаОснования<>Неопределено Тогда
				ТекСтрока.Цена=обПересчет(СтрокаДокументаОснования.Цена,ТекСтрока.Партия.ВалютаДокумента,ТекСтрока.Партия.КурсДокумента,ВалютаДокумента,КурсДокумента);
				Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		//Поищем автомобиль в документе продажи из шапки
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			СтрокаДокументаОснования=ДокументОснование.Автомобили.Найти(ТекСтрока.Автомобиль,"Автомобиль");
			Если СтрокаДокументаОснования<>Неопределено Тогда
				ТекСтрока.Партия=ДокументОснование;
				ТекСтрока.Цена=СтрокаДокументаОснования.Цена;
				ТекСтрока.Цена=обПересчет(СтрокаДокументаОснования.Цена,ДокументОснование.ВалютаДокумента,ДокументОснование.КурсДокумента,ВалютаДокумента,КурсДокумента);
				Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		СтрокаАвтомобиля=Автомобили.Найти(ТекСтрока.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаАвтомобиля=Неопределено Тогда
			Возврат Ложь;
		КонецЕсли; 
		Автомобиль=СтрокаАвтомобиля.Автомобиль;
		//А вот тут мы загрузим те цены, которые указаны в регистре комплектации
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	СУММА(КомплектацияАвтомобилейОстатки.КоличествоОстаток) КАК Количество,
					 |	СУММА(КомплектацияАвтомобилейОстатки.СуммаПродажиОстаток) КАК СуммаПродажи,
					 |	СУММА(КомплектацияАвтомобилейОстатки.СуммаПродажиУпрОстаток) КАК СуммаПродажиУпр
					 |ИЗ
					 |	РегистрНакопления.КомплектацияАвтомобилей.Остатки(
					 |		&НаДату,
					 |		Автомобиль = &Автомобиль
					 |		    И Номенклатура = &Номенклатура
					 |		    И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК КомплектацияАвтомобилейОстатки";
		Запрос.УстановитьПараметр("НаДату",?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),Новый Граница(Дата,ВидГраницы.Исключая)));
		Запрос.УстановитьПараметр("Автомобиль",Автомобиль);
		Запрос.УстановитьПараметр("Номенклатура",ТекСтрока.Номенклатура);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ТекСтрока.ХарактеристикаНоменклатуры);
		ОстаткиОборудования=Запрос.Выполнить().Выгрузить();
		ТекСтрока.Количество=ОстаткиОборудования.Итог("Количество");
		Если обЗначениеНеЗаполнено(ТекСтрока.Количество) Тогда ТекСтрока.Количество=0; КонецЕсли; 
		ВалютаРегламентированногоУчетаОрганизаций=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		Если ВалютаДокумента=ВалютаРегламентированногоУчетаОрганизаций Тогда
			ОстатокСуммы=ОстаткиОборудования.Итог("СуммаПродажи");
		Иначе
			ОстатокСуммы=ОстаткиОборудования.Итог("СуммаПродажиУпр");
			ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить(); 
			Если обЗначениеНеЗаполнено(КурсВалютыУпр) Тогда
				СтруктураКурса = обКурсДляВалюты(ВалютаУпр,Дата);
				КурсУпр		   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			Иначе
				КурсУпр        = КурсВалютыУпр;
			КонецЕсли;
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(ОстатокСуммы) Тогда ОстатокСуммы=0; КонецЕсли; 
		Если ВалютаДокумента<>ВалютаРегламентированногоУчетаОрганизаций Тогда
			ОстатокСуммы=обПересчет(ОстатокСуммы,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если НЕ ЦенаВключаетНДС Тогда
			//НДС в цену не включен
			ОстатокСуммы=(100*ОстатокСуммы)/(100+ТекСтрока.СтавкаНДС.Ставка);
		КонецЕсли; 
		ТекСтрока.Сумма=ОстатокСуммы;
		Рез=ОбработкаРеквизита("Товары.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ВозвратПоставщикуАвтомобилей - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Для контрагентов из ЕАЭС другая форма отчетности
	Если ПрослеживаемостьТоваровСервер.СтранаУчастникЕАЭС(Контрагент.СтранаРегистрации) Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ОсвобождентОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Автомобили.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ОстаткиАвтомобилей.Автомобиль КАК Номенклатура,
	|	ОстаткиАвтомобилей.ГТД КАК РНПТ,
	|	СУММА(ОстаткиАвтомобилей.Количество) КАК КоличествоПрослеживаемости,
	|	СУММА(ОстаткиАвтомобилей.Сумма - ОстаткиАвтомобилей.СуммаНДС) КАК СуммаБезНДС,
	|	ОстаткиАвтомобилей.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор = &Ссылка
	|	И ОстаткиАвтомобилей.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАвтомобилей.Автомобиль,
	|	ОстаткиАвтомобилей.ГТД,
	|	ОстаткиАвтомобилей.СтавкаНДС";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим, что в документе есть РНПТ
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = Запрос.Выполнить().Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	КодОперации = Справочники.КодыОперацийПрослеживаемости.ПолученнаяКорректировочнаяСчетФактураУменьшение;
	НомерДокумента = дкПолучитьНомерДляПечати(Ссылка);
	
	// Для пересчета валюты в рубли
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
	
	// Сформируем таблицу
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.КодОперации = КодОперации;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = Дата;
		НоваяСтрока.Контрагент = Контрагент;
		
		Если ВалютаНеРегл Тогда
			НоваяСтрока.СуммаБезНДС = Окр(обПересчет(НоваяСтрока.СуммаБезНДС,ВалютаДокумента,КурсДокумента,ВалютаРегл,Дата), 2);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА


// Формирует печатную форму "ВозвратПоставщикуАвтомобилей"
// Возвращает сформированный табличный документ:
Функция ПечатьВозвратПоставщикуАвтомобилей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВозвратПоставщикуАвтомобилей");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект, Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = "Накладная возврата (поставщику) № "+НомерДляПечати+" от "+Формат(ЭтотОбъект.Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОтправителя	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	ОбластьМакета.Параметры.ПредставлениеПолучателя		= спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	//подготовим дополнительную таблицу
	ТаблицаТоваровПоАвтомобилям = ЭтотОбъект.Товары.Выгрузить();
	ТаблицаТоваровПоАвтомобилям.Свернуть("ИдентификаторАвтомобиля","Сумма,СуммаНДС,СуммаВсего");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили.Выгрузить();
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка
		СтрокаТабличнойЧастиТовары = ТаблицаТоваровПоАвтомобилям.Найти(СтрокаТабличнойЧасти.ИдентификаторАвтомобиля,"ИдентификаторАвтомобиля");
		Если СтрокаТабличнойЧастиТовары <> Неопределено Тогда
			СтрокаТабличнойЧасти.Сумма		= СтрокаТабличнойЧасти.Сумма		+ СтрокаТабличнойЧастиТовары.Сумма;
			СтрокаТабличнойЧасти.СуммаНДС	= СтрокаТабличнойЧасти.СуммаНДС		+ СтрокаТабличнойЧастиТовары.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаВсего	= СтрокаТабличнойЧасти.СуммаВсего	+ СтрокаТабличнойЧастиТовары.СуммаВсего;
		КонецЕсли;
		//заполняем данные строки
		СтруктураСтроки = орПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;

	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ТОРГ-12"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(
		ОбластьМакета.Параметры.ПодразделениеКомпании, СтруктураПредставления2, ДополнительныеПараметры);
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикОрганизация",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления1);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП	= ЭтотОбъект.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
	ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
	ОбластьМакета.Параметры.ОснованиеДата = "";
	ОбластьМакета.Параметры.ОснованиеНомер = ""; 
	
	ОбластьМакета.Параметры.Номер = НомерДляПечати;	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	// в новой ПФ не выводится
	//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
	ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");		
	
	НомерСтраницы	= 1;
	ОбластьЗаголовокТаблицы.Параметры.Валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	КоличествоСтрокНаТекущейСтранице=0;
	ИспользоватьРазбиениеНаСтраницы = обПраво("ИспользоватьРазбиениеНаСтраницы", Права,,ЭтотОбъект);

	ВыборкаСтрок = ЭтотОбъект.Автомобили;
	КоличествоСтрок = ВыборкаСтрок.Количество();
	
	// инициализация итогов по странице
	ИтогоКоличествоНаСтранице = 0;
	ИтогоСуммаНаСтранице      = 0;
	ИтогоНДСНаСтранице        = 0;
	ИтогоСуммаСНДСНаСтранице  = 0;

	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		// Для переноса последней страницы необходимо посчитать количество итогов по НДС
		СуммаВсегоАвтомобиля=СтрокаТоваров.СуммаВсего;
		СуммаНДСАвтомобиля=СтрокаТоваров.СуммаНДС;
		Для каждого СтрокаТЧ Из Товары Цикл
			Если СтрокаТЧ.ИдентификаторАвтомобиля=СтрокаТоваров.ИдентификаторАвтомобиля Тогда
				СуммаВсегоАвтомобиля=СуммаВсегоАвтомобиля+СтрокаТЧ.СуммаВсего;
				СуммаНДСАвтомобиля=СуммаНДСАвтомобиля+СтрокаТЧ.СуммаНДС;
			КонецЕсли;
			// итоги по ставкам НДС
			Если НЕ обЗначениеНеЗаполнено(СтрокаТЧ.СтавкаНДС) Тогда
				Если СоответствиеИтоговПоставкамНДС[СтрокаТЧ.СтавкаНДС]=Неопределено Тогда
					СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТЧ.СтавкаНДС,0);
				КонецЕсли;
				СоответствиеИтоговПоставкамНДС[СтрокаТЧ.СтавкаНДС]=СоответствиеИтоговПоставкамНДС[СтрокаТЧ.СтавкаНДС]+СтрокаТЧ.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС]=Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС]=СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС]+СуммаНДСАвтомобиля;
		КонецЕсли;
		
		Если ИспользоватьРазбиениеНаСтраницы И КоличествоСтрокНаТекущейСтранице>0 Тогда
			// Проверим, помещаются ли нужные области на странице
			Если СтрокаТоваров.НомерСтроки = КоличествоСтрок Тогда
				// только на последнем шаге, когда нужно вывести все
				МассивОбластей = Новый Массив();
				МассивОбластей.Добавить(ОбластьСтрока);
				МассивОбластей.Добавить(ОбластьИтоги);
				МассивОбластей.Добавить(ОбластьВсего);
				//Для Каждого СтрокаИтоговНДС Из СоответствиеИтоговПоставкамНДС Цикл
				//	МассивОбластей.Добавить(ОбластьВсегоСтавкаНДС);				
				//КонецЦикла;
	            МассивОбластей.Добавить(ОбластьПодвал);
			Иначе
				МассивОбластей = Новый Массив();			
				МассивОбластей.Добавить(ОбластьСтрока);
				МассивОбластей.Добавить(ОбластьИтоги);
			КонецЕсли; 			
			
			Попытка
				Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
					ОбластьИтоги.Параметры.ИтогКоличествоПоСтранице = ИтогоКоличествоНаСтранице;
					ОбластьИтоги.Параметры.ИтогСуммыПоСтранице      = ИтогоСуммаНаСтранице;
					ОбластьИтоги.Параметры.ИтогНДСПоСтранице        = ИтогоНДСНаСтранице;
					ОбластьИтоги.Параметры.ИтогСуммыСНДСПоСтранице  = ИтогоСуммаСНДСНаСтранице;
					ТабДокумент.Вывести(ОбластьИтоги);

					// очистим итоги по странице
					ИтогоКоличествоНаСтранице = 0;
					ИтогоСуммаНаСтранице      = 0;
					ИтогоНДСНаСтранице        = 0;
					ИтогоСуммаСНДСНаСтранице  = 0;

					НомерСтраницы = НомерСтраницы + 1;
					ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
					ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
					КоличествоСтрокНаТекущейСтранице=0;
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;

		//заполняем данные строки
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.Номенклатура=СтрокаТоваров.Автомобиль;
		ОбластьСтрока.Параметры.ТоварНаименование=спПолучитьНаименование(СтрокаТоваров.Автомобиль);
		ОбластьСтрока.Параметры.Код = СтрокаТоваров.Автомобиль.VIN;
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование="шт";
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ="шт";
		ОбластьСтрока.Параметры.КоличествоВОдномМесте=1;
		ОбластьСтрока.Параметры.КоличествоМест=1;
		ОбластьСтрока.Параметры.СтавкаНДС = СтрокаТоваров.СтавкаНДС;
		
		// Выполним перерасчет суммы и цены по регламентированной валюте
		СуммаВсегоАвтомобиля = Окр(обПересчет(СуммаВсегоАвтомобиля, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДСАвтомобиля   = Окр(обПересчет(СуммаНДСАвтомобиля, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		
		СуммаАвтомобиляБезНДС=СуммаВсегоАвтомобиля - СуммаНДСАвтомобиля;
		ОбластьСтрока.Параметры.ЦенаБезНДС = СуммаАвтомобиляБезНДС;
		ОбластьСтрока.Параметры.СуммаБезНДС = СуммаАвтомобиляБезНДС;
		ОбластьСтрока.Параметры.СуммаНДС = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", СуммаНДСАвтомобиля);
		ОбластьСтрока.Параметры.СуммаВсего = СуммаВсегоАвтомобиля;
		
		ТабДокумент.Вывести(ОбластьСтрока);
		КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;

		// увеличим итоги по странице
		ИтогоКоличествоНаСтранице = ИтогоКоличествоНаСтранице + СтрокаТоваров.Количество;
		ИтогоСуммаНаСтранице      = ИтогоСуммаНаСтранице      + СуммаАвтомобиляБезНДС;
		ИтогоНДСНаСтранице        = ИтогоНДСНаСтранице        + СуммаНДСАвтомобиля;
		ИтогоСуммаСНДСНаСтранице  = ИтогоСуммаСНДСНаСтранице  + СуммаВсегоАвтомобиля;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаАвтомобиляБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДСАвтомобиля;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсегоАвтомобиля;
		
	КонецЦикла;
	
	// Выводим итоги по последней странице
	ОбластьИтоги.Параметры.ИтогКоличествоПоСтранице = ИтогоКоличествоНаСтранице;
	ОбластьИтоги.Параметры.ИтогСуммыПоСтранице      = ИтогоСуммаНаСтранице;
	ОбластьИтоги.Параметры.ИтогНДСПоСтранице        = ИтогоНДСНаСтранице;
	ОбластьИтоги.Параметры.ИтогСуммыСНДСПоСтранице  = ИтогоСуммаСНДСНаСтранице;
	ТабДокумент.Вывести(ОбластьИтоги);
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = ВыборкаСтрок.Итог("Количество");
	ОбластьВсего.Параметры.ИтогСуммы      = ИтогоСумма;
	ОбластьВсего.Параметры.ИтогНДС        = ИтогоНДС;
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = ИтогоСуммаСНДС;
	ТабДокумент.Вывести(ОбластьВсего);
	
	//// выведем итоги по ставкам НДС
	//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
	//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "+дкПолучитьПредставлениеСтавкиНДС(ЭлементСоответствия.Ключ,"%");
	//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = ЭлементСоответствия.Значение;
	//	ТабДокумент.Вывести(ОбластьВсегоСтавкаНДС);
	//КонецЦикла;
	
	// Выводим подвал документа
	ОбластьМакета = ОбластьПодвал;
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);

	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьМакета.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьМакета.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьМакета.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьМакета.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьМакета.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли; 
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьМакета.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

//Функция печати Акт о возврате ТМЦ
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьМХ3(ТабДокумент) Экспорт
	
	Макет = ПолучитьОбщийМакет("МХ3");
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.НомерДокумента = Номер;
	ОбластьЗаголовок.Параметры.ДатаДокумента  = Дата;
	ОбластьЗаголовок.Параметры.Заполнить(ЭтотОбъект);
	ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО				= Организация.КодПоОКПО;
	ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП	= Организация.КодПоОКДП;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		ОбластьЗаголовок.Параметры.ПредставлениеВладельца = спПолучитьПредставление(Контрагент);
		Если Тип("СправочникСсылка.СкладыКомпании") = ТипЗнч(Контрагент) Тогда
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = Контрагент.Организация.КодПоОКПО
		Иначе
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = Контрагент.КодПоОКПО;
		КонецЕсли;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьЗаголовок);
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьШапка);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ВыборкаСтрок = Автомобили.Выгрузить();
	ВыборкаСтрок.Колонки.Автомобиль.Имя = "Номенклатура";
	Оборудование = Товары.Выгрузить();
	Оборудование.Свернуть("ИдентификаторАвтомобиля", "Цена,Сумма,СуммаВсего,СуммаНДС");
	Для Каждого Строка Из ВыборкаСтрок Цикл
		ОборудованиеАвтомобиля = Оборудование.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля", Строка.ИдентификаторАвтомобиля));
		Если ОборудованиеАвтомобиля.Количество() > 0 Тогда
			Строка.Цена       = Строка.Цена +       ОборудованиеАвтомобиля[0].Цена;
			Строка.Сумма      = Строка.Сумма +      ОборудованиеАвтомобиля[0].Сумма;
			Строка.СуммаВсего = Строка.СуммаВсего + ОборудованиеАвтомобиля[0].СуммаВсего;
			Строка.СуммаНДС   = Строка.СуммаНДС +   ОборудованиеАвтомобиля[0].СуммаНДС;
		КонецЕсли;
	КонецЦикла;
	
	НомерСтроки = 1;
	ВыводитьКод = обПраво("ВыводитьКодВПечатныхФормах",Права,ЭтотОбъект);
	
	ЕстьЦена = НЕ (ВыборкаСтрок.Колонки.Найти("Цена") = Неопределено);
	ЕстьСуммаВсего = НЕ (ВыборкаСтрок.Колонки.Найти("СуммаВсего") = Неопределено);
	
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
		ОбластьСтрока.Параметры.Номенклатура = Строка(СтрокаТоваров.Номенклатура);
		Если Тип("СправочникСсылка.Номенклатура") = ТипЗнч(СтрокаТоваров.Номенклатура) Тогда
			Если ВыводитьКод Тогда
				ОбластьСтрока.Параметры.Код = Строка(СтрокаТоваров.Номенклатура.Артикул);
			КонецЕсли;
			ОбластьСтрока.Параметры.Характеристика 	 = Строка(СтрокаТоваров.ХарактеристикаНоменклатуры);
			ОбластьСтрока.Параметры.ЕдиницаИзмерения = СтрокаТоваров.ЕдиницаИзмерения;
			ОбластьСтрока.Параметры.КодОКЕИ          = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ИначеЕсли Тип("СправочникСсылка.Автомобили") = ТипЗнч(СтрокаТоваров.Номенклатура) Тогда
			ОбластьСтрока.Параметры.Код 			 = СтрокаТоваров.Номенклатура.VIN;
			ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(796);
			ОбластьСтрока.Параметры.ЕдиницаИзмерения = ?(ЕдиницаИзмерения = Неопределено, "", ЕдиницаИзмерения);
			ОбластьСтрока.Параметры.КодОКЕИ          = ?(ЕдиницаИзмерения = Неопределено, "", ЕдиницаИзмерения.Код);
		КонецЕсли;
		ОбластьСтрока.Параметры.Количество = Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена       =  ?(ЕстьЦена, Формат(СтрокаТоваров.Цена,ФорматВыводаСуммы), "");
		ОбластьСтрока.Параметры.СуммаВсего =  ?(ЕстьСуммаВсего, Формат(СтрокаТоваров.СуммаВсего,ФорматВыводаСуммы), "");
		ТабДокумент.Вывести(ОбластьСтрока);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"),ФорматВыводаКоличества);
	ОбластьИтого.Параметры.Цена       = ?(ЕстьЦена, Формат(ВыборкаСтрок.Итог("Цена"),ФорматВыводаСуммы), "");
	ОбластьИтого.Параметры.СуммаВсего = ?(ЕстьСуммаВсего, Формат(ВыборкаСтрок.Итог("СуммаВсего"),ФорматВыводаСуммы), "");
	ТабДокумент.Вывести(ОбластьИтого);
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(ОбластьПодвал);
	Возврат ТабДокумент;
КонецФункции // ПечатьМХ3(ТабДокумент)

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если обЗначениеНеЗаполнено(ХозОперация) Тогда
		ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщику;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеАвтомобилей") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщикуКомиссия;
		ИначеЕсли Основание.ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейОтветственноеХранение Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейСОтветственногоХранения;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВводОстатковАвтомобилей") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковАвтомобилейПринятыхНаКомиссию Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщикуКомиссия;
		ИначеЕсли Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковАвтомобилейНаОтветственномХранении Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейСОтветственногоХранения;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	// определим ХО.
	Если Основание=Неопределено Тогда
		ТипЦен=обПраво("ОсновнойТипЦенЗакупкиАвтомобилей",Права,,ЭтотОбъект);
	Иначе
		// проставим партию
		Для Каждого СтрокаТЧ Из Автомобили Цикл
			СтрокаТЧ.Партия=Основание;
			Сумма		= 0;
			СуммаВсего	= 0;
			СуммаНДС	= 0;
			ОпцииАвтомобиля = Основание.Опции.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля", СтрокаТЧ.ИдентификаторАвтомобиля));
			Для Каждого ТекСтрока Из ОпцииАвтомобиля Цикл
				Сумма		= Сумма+ТекСтрока.Сумма;
				СуммаВсего	= СуммаВсего+ТекСтрока.СуммаВсего;
				СуммаНДС	= СуммаНДС+ТекСтрока.СуммаНДС;
			КонецЦикла;
			СтрокаТЧ.Цена		= СтрокаТЧ.Цена+Сумма;
			СтрокаТЧ.Сумма		= СтрокаТЧ.Сумма+Сумма;
			СтрокаТЧ.СуммаВсего	= СтрокаТЧ.СуммаВсего+СуммаВсего;
			СтрокаТЧ.СуммаНДС	= СтрокаТЧ.СуммаНДС+СуммаНДС;
		КонецЦикла;
	КонецЕсли; 
	
	Товары.Очистить();
	Для каждого СтрокаАвтомобиля Из Автомобили Цикл
		ДобавитьОборудованиеАвтомобиля(СтрокаАвтомобиля.Автомобиль,Истина);
	КонецЦикла; 
	
	// изменим итоговую сумму документа
	СуммаДокумента=РассчитатьСуммуВсего();
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	//Обновим оборудование автомобилей
	Для каждого СтрокаАвтомобилей Из Автомобили Цикл
		ДобавитьОборудованиеАвтомобиля(СтрокаАвтомобилей.Автомобиль,Ложь);
	КонецЦикла; 
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	//Проверим корректность склада
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 	
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// Определим необходимость корректирующих проводок, для поддержания баланса по подразделениям.
	ПодразделениеСклад                 = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны     = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	
	// проведем взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		НаборЗаписейВзаиморасчеты.Сделка=ДокументОснование;
	КонецЕсли;
	НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
	Если ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщику Тогда
		//По взаиморасчетам всю сумму документа
		НаборЗаписейВзаиморасчеты.Сумма=Автомобили.Итог("СуммаВсего")+Товары.Итог("СуммаВсего");
	Иначе
		//Иначе "продадим" поставщику то оборудование, которое мы установили
		НаборЗаписейВзаиморасчеты.Сумма=Товары.Итог("СуммаВсего");
	КонецЕсли;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
	Отказ=НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
	Если НЕ Отказ Тогда
		НаборЗаписейВзаиморасчеты.Записать();
	КонецЕсли; 
	// доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
	НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
	// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
	Если БалансВедетсяПоПодразделениям Тогда
		НаборЗаписейДоходыИРасходы.Подразделение=ДоговорВзаиморасчетов.Подразделение;
	КонецЕсли;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
	НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
	НаборЗаписейДоходыИРасходы.Доход=Товары.Итог("СуммаВсего");
	Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	
	Отказ = ПроверитьЗаказыНаАвтомобиль() ИЛИ Отказ;
	
	НаборЗаписейОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
	НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладКомпании;
	НаборЗаписейОстаткиАвтомобилей.Партия="Партия";
	//НаборЗаписейОстаткиАвтомобилей.Сторно=Истина;
	Если ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщикуКомиссия Тогда
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейСОтветственногоХранения Тогда
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварОтветственноеХранение;
	Иначе
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	КонецЕсли; 
	Отказ=НЕ НаборЗаписейОстаткиАвтомобилей.ВозвратАвтомобилей(ВидДвиженияНакопления.Приход) ИЛИ Отказ;
	НаборЗаписейОстаткиАвтомобилей.Записать();
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// способ ведения не по подразделениям или балансовые подразделения равны то ничего не будем корректировать
	Если БалансВедетсяПоПодразделениям И БалансовыеПодразделенияНеРавны Тогда
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ОбъединенныйЗапрос.СуммаУпр),0) КАК СуммаУпр
		|ИЗ (
		|ВЫБРАТЬ
		|	ЕСТЬNULL(-СУММА(ОстаткиАвтомобилей.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		|ГДЕ
		|	ОстаткиАвтомобилей.Регистратор = &Регистратор
		|	И ОстаткиАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр),0)
		|ИЗ
		|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
		|ГДЕ
		|	КомплектацияАвтомобилей.Регистратор = &Регистратор
		//|	И (НЕ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Номенклатура)
		|	И КомплектацияАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
		|) КАК ОбъединенныйЗапрос";
		Запрос.УстановитьПараметр("Регистратор",Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СуммаАвтомобилей = Выборка.СуммаУпр;
		Иначе
			СуммаАвтомобилей = 0;
		КонецЕсли; 
		//СуммаАвтомобилей = -(Движения.ОстаткиАвтомобилей.Итог("СуммаУпр")-
		//Движения.КомплектацияАвтомобилей.Итог("СуммаУпр"));
		Если СуммаАвтомобилей<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение = СкладКомпании.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СуммаАвтомобилей;
			Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;	
		КонецЕсли;
		
		Если ХозОперация=Справочники.ХозОперации.ВозвратАвтомобилейПоставщику Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Ложь;
			НаборЗаписейДиР.Доход = СуммаДокумента;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
		КонецЕсли;
	КонецЕсли;	
	
	// Проверим наличие прослеживаемых товаров, которые были возвращены из розницы
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности автомобилей
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

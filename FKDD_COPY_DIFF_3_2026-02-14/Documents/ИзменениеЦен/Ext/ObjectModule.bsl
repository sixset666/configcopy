// Модуль документа

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ТипЦенСтарый Экспорт;         // Предыдущий тип цен
Перем ОтменитьПроверкуДоступностиСкладов Экспорт; // Булево - отменяет проверку доступности склада в дкДокументы.дкПроверитьДоступностьСкладовКомпании()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Процедура - Добавить строку ТЧ
//
// Параметры:
//  НоваяСТрокаТЧ	 - ДокументТабличнаяЧастьСтрока	 - Строка для заполнения.
//  СтрокаОснования	 - ДокументТабличнаяЧастьСтрока	 - Строка из которой будет заполнение.
//  МассивРеквизитов - Массив						 - Массив реквизитов для заполнения в строковом виде.
//
Процедура ДобавитьСтрокуТЧ(НоваяСТрокаТЧ, СтрокаОснования, МассивРеквизитов, ИмяобъектОснования)
	Для Каждого Реквизит Из МассивРеквизитов Цикл
		Если дкДокументЕстьРеквизитТабЧасти(Реквизит, ИмяобъектОснования, "Товары") Тогда 
			НоваяСТрокаТЧ[Реквизит] = СтрокаОснования[Реквизит];
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Процедура - Заполнить табличную часть товары по основанию
//
// Параметры:
//  ТЧОбъект	 - ДокументТабличнаяЧасть	 - ТЧ которую будем заполнять.
//  ТЧОснование	 - ДокументТабличнаяЧасть	 - ТЧ с которой будем заполнять.
//  Реквизиты	 - Строка					 - Перечень реквизитов, через запятую, которые необходимо скопировать.
//  					Реквизиты "ХарактеристикаНоменклатуры" и "ЕдиницаИзмерения" указывать не нужно.
//  Алгоритм	 - ПеречислениеСсылка		 - Алгоритм получения цены.
//
Процедура ЗаполнитьТабличнуюЧастьТоварыПоОснованию(ТЧОбъект, ТЧОснование, Реквизиты, Алгоритм, ИмяобъектОснования)
	
	// дополним перечень реквизитов
	Если Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре Тогда
		МассивРеквизитов = СтрРазделить(Реквизиты, ",");
	ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
		МассивРеквизитов = СтрРазделить(Реквизиты + ",ХарактеристикаНоменклатуры", ",");
	ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
		МассивРеквизитов = СтрРазделить(Реквизиты + ",ЕдиницаИзмерения", ",");
	Иначе
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОснования Из ТЧОснование Цикл
		// Если ведется учет по номенклатуре
		Если Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре Тогда
			СтрокаПоНоменклатуре = ТЧОбъект.Найти(СтрокаОснования.Номенклатура, "Номенклатура");
			Если СтрокаПоНоменклатуре = Неопределено Тогда
				ДобавитьСтрокуТЧ(ТЧОбъект.Добавить(), СтрокаОснования, МассивРеквизитов, ИмяобъектОснования);
			КонецЕсли;
		ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
			// Найдем строку с номенклатурой и такой же характеристикой
			ПараметрыОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаОснования);
			НайденныеСтроки = ТЧОбъект.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				ДобавитьСтрокуТЧ(ТЧОбъект.Добавить(), СтрокаОснования, МассивРеквизитов, ИмяобъектОснования);
			КонецЕсли;
		ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
			// Найдем строку с номенклатурой и такой же единицей измерения
			ПараметрыОтбора = Новый Структура("Номенклатура, ЕдиницаИзмерения");
			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаОснования);
			НайденныеСтроки = ТЧОбъект.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				ДобавитьСтрокуТЧ(ТЧОбъект.Добавить(), СтрокаОснования, МассивРеквизитов, ИмяобъектОснования);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьТоварыПоОснованиюДокументаКомплектация(Основание, Алгоритм)
	
	Если Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре Тогда
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура =  Основание.Комплект;
	ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура =  Основание.Комплект;
		НоваяСтрока.ХарактеристикаНоменклатуры = Основание.ХарактеристикаКомплекта;
	ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура =  Основание.Комплект;
		НоваяСтрока.ЕдиницаИзмерения = Основание.КомплектЕдиницаИзмерения;
	КонецЕсли;
	
КонецПроцедуры
// заполнение из прайс-листа контрагента
Процедура ЗаполнениеПоПрайсЛистуКонтрагента(ЗаполнитьНоменклатуройПоПрайсЛисту) Экспорт
	
	//Если ПрайсЛистКонтрагента.ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ВебПрайсЛист Тогда
	//	// Это Веб-сервис.
	//	// Мы не заполняем цены по нему
	//	#Если Клиент Тогда
	//		Предупреждение("По веб прайс-листу заполнение цен не возможно!");
	//	#КонецЕсли
	//	Возврат;
	//КонецЕсли;
	
	Если ПрайсЛистКонтрагента.ХранитьДанныеЛокально
		ИЛИ ПрайсЛистКонтрагента.ФайлИсточникДанных
		ИЛИ ПрайсЛистКонтрагента.ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ВебПрайсЛист Тогда
		//Проверим есть ли загруженный ПЛ
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПрайсЛистыКонтрагентов.ПрайсЛист
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
		|ГДЕ
		|	ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист";
		Запрос.УстановитьПараметр("ПрайсЛист",ПрайсЛистКонтрагента);
		
		Выборка=Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество()=0 Тогда
			#Если Клиент Тогда
				Предупреждение("Прайс-лист не загружен в базу. Перед использованием его необходимо загрузить!");
			#КонецЕсли
			Возврат;
		КонецЕсли;
		
		//Грузим из регистра
		Запрос=Новый Запрос;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	Спр.Ссылка КАК Номенклатура,
		|	ПрайсЛистыКонтрагентов.Цена КАК Цена,
		|	ПрайсЛистыКонтрагентов.Валюта КАК Валюта,
		|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
		|	ПрайсЛистыКонтрагентов.ТегПозиции КАК ТегПозиции,
		|	ПрайсЛистыКонтрагентов.Производитель КАК Производитель
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Спр
		|		ПО ПрайсЛистыКонтрагентов.АртикулДляПоиска = Спр.АртикулДляПоиска
		|			И ПрайсЛистыКонтрагентов.Производитель = Спр.Производитель
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи КАК НедоступныеЗаписи
		|		ПО ПрайсЛистыКонтрагентов.ПрайсЛист = НедоступныеЗаписи.ПрайсЛист
		|			И ПрайсЛистыКонтрагентов.ДатаЗаписи = НедоступныеЗаписи.ДатаЗаписи
		|ГДЕ
		|	ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист
		|	И ПрайсЛистыКонтрагентов.АртикулДляПоиска <> """"
		|	И НедоступныеЗаписи.ПрайсЛист ЕСТЬ NULL 
		|	"+?(ЗаполнитьНоменклатуройПоПрайсЛисту, "", "И Спр.Ссылка В (&Товары)")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура),
		|	ПрайсЛистыКонтрагентов.Цена,
		|	ПрайсЛистыКонтрагентов.Валюта,
		|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
		|	ПрайсЛистыКонтрагентов.ТегПозиции КАК ТегПозиции,
		|	ПрайсЛистыКонтрагентов.Производитель КАК Производитель
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
		|		ПО (ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика = (ВЫРАЗИТЬ(ПравилаЗагрузки.ОбъектПравила КАК СТРОКА(32))))
		|			И (ПрайсЛистыКонтрагентов.ПрайсЛист = ПравилаЗагрузки.ПрайсЛист)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи КАК НедоступныеЗаписи
		|		ПО (ПрайсЛистыКонтрагентов.ПрайсЛист = НедоступныеЗаписи.ПрайсЛист)
		|			И (ПрайсЛистыКонтрагентов.ДатаЗаписи = НедоступныеЗаписи.ДатаЗаписи)
		|ГДЕ
		|	ПравилаЗагрузки.ПрайсЛист = &ПрайсЛист
		|	И ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки)
		|	И ПравилаЗагрузки.ИмяРеквизитаПрайсЛиста = ""Номенклатура""
		|	И ПравилаЗагрузки.Значение ССЫЛКА Справочник.Номенклатура
		|	"+?(ЗаполнитьНоменклатуройПоПрайсЛисту, "", "И ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура).Ссылка В (&Товары)")+"
		|	И НедоступныеЗаписи.ПрайсЛист ЕСТЬ NULL 
		|";
		
		Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистКонтрагента);
		Если Не ЗаполнитьНоменклатуройПоПрайсЛисту Тогда
			СписокНоменклатуры = Новый СписокЗначений;
			СписокНоменклатуры.ЗагрузитьЗначения(Товары.ВыгрузитьКолонку("Номенклатура"));
			Запрос.УстановитьПараметр("Товары", СписокНоменклатуры);
		КонецЕсли;
		
		ТЗПрайсЛиста = Запрос.Выполнить().Выгрузить();
		
		Счетчик = 0;
		КоличествоСтрок = ТЗПрайсЛиста.Количество();
		Если КоличествоСтрок = 0 Тогда
			#Если Клиент Тогда
				Если ЗаполнитьНоменклатуройПоПрайсЛисту Тогда
					Предупреждение("Товары прайс-листа " + СокрЛП(Строка(ПрайсЛистКонтрагента)) + " не загружены в справочник ""Номенклатура""!");
				Иначе
					Если Товары.Количество() > 0 Тогда
						Предупреждение("Номенклатура из документа в прайс-листе " + СокрЛП(Строка(ПрайсЛистКонтрагента)) + " не найдена!");
					КонецЕсли;
				КонецЕсли;
			#КонецЕсли
		КонецЕсли;
		
		ДопПараметры = Новый Структура("ПолучитьБазовуюЦену");
		
		Для Каждого СтрокаПрайсЛиста Из ТЗПрайсЛиста Цикл
			
			НоваяСтрокаТоваров = Неопределено;
			СтрокиНоменклатуры = Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрокаПрайсЛиста.Номенклатура));
			Если СтрокиНоменклатуры.Количество() = 0 Тогда
				Если ЗаполнитьНоменклатуройПоПрайсЛисту Тогда
					НоваяСтрокаТоваров = Товары.Добавить();
					НоваяСтрокаТоваров.Номенклатура = СтрокаПрайсЛиста.Номенклатура;
				КонецЕсли; 
			Иначе
				НоваяСтрокаТоваров = СтрокиНоменклатуры[0];
			КонецЕсли;
			
			Если НоваяСтрокаТоваров <> Неопределено Тогда
				ДопПараметры.Вставить("ПолучитьБазовуюЦену", Ложь);
				ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрокаТоваров,, ДопПараметры);
				ЦенаВВалютеДокумента = обПересчет(СтрокаПрайсЛиста.Цена, СтрокаПрайсЛиста.Валюта, Дата, ВалютаДокумента, КурсДокумента);
				ДанныеЦены = Справочники.ПрайсЛистыКонтрагентов.РассчитатьЦеныПрайсЛиста(ПрайсЛистКонтрагента, Истина, Истина, ТипЦен, ПодразделениеКомпанииПолучатель, Дата, ЦенаВВалютеДокумента, СтрокаПрайсЛиста.Номенклатура, СтрокаПрайсЛиста.ТегПозиции, СтрокаПрайсЛиста.Производитель);
				НоваяСтрокаТоваров.ЦенаБазовая = ДанныеЦены.ЦенаПокупки;
				НоваяСтрокаТоваров.Цена = ДанныеЦены.ЦенаПродажи;
				ОбработкаРеквизита("Товары.Цена", НоваяСтрокаТоваров,, ДопПараметры);
			КонецЕсли;
			
			#Если Клиент Тогда
				Состояние("Загрузка цен по прайс-листу контрагента - " + Строка(Цел(Счетчик*100/КоличествоСтрок))+" %");
			#КонецЕсли
			
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
		
	Иначе
		ЗаполнитьБазовыеЦеныПоПрайсЛисту(ЗаполнитьНоменклатуройПоПрайсЛисту);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры", 2);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения", 2);
	//ОбязательныеТовары.Вставить("Цена",1);
	 	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпанииПолучатель",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("ДатаНачалаДействия",1);
	Если ХозОперация=Справочники.ХозОперации.УстановкаЦенКонтрагента Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
	Конецесли;
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	// Обязательные поля таблицы прайс-листов
	Если ХозОперация = Справочники.ХозОперации.УстановкаСкидокПрайсЛиста ИЛИ ХозОперация = Справочники.ХозОперации.УстановкаНаценокПрайсЛиста Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеПрайсЛисты = Новый Структура();
		ОбязательныеПрайсЛисты.Вставить("ПрайсЛист", 3);
		ОбязательныеПрайсЛисты.Вставить("ТегПозиции", 2);
		ОбязательныеПрайсЛисты.Вставить("Производитель", 2);
		ОбязательныеРеквизиты.Вставить("ПрайсЛисты",ОбязательныеПрайсЛисты);
	Конецесли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации
	// будем только в случае если все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если  Результат и СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
	
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
	КонецЕсли;
	
	// Проверяем заполненность некоторых реквизитов табличной части.
	// Отметим поля, необходимые для заполнения.
	УчетЦенПоХарактеристикам = (ТипЦен.АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоХарактеристике);
	УчетПоЕдиницамИзмерения  = (ТипЦен.АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения);
	
	Если УчетПоЕдиницамИзмерения Или УчетЦенПоХарактеристикам Тогда
		Для каждого ТекСтрокаТЧ Из Товары Цикл
			Если УчетЦенПоХарактеристикам Тогда
				ТекТипНоменклатуры = ТекСтрокаТЧ.Номенклатура.ТипНоменклатуры;
				Если ТекСтрокаТЧ.ХарактеристикаНоменклатуры.Пустая() И ТекТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров Тогда
					Если ТекТипНоменклатуры.ИспользованиеХарактеристик<>3 Тогда
						ТекстОшибки = "Строка №" + ТекСтрокаТЧ.НомерСтроки + " - не выбрана характеристика номенклатуры.";
						дкДобавитьОшибку(Ошибки, ТекстОшибки);
						Результат = Ложь;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли УчетПоЕдиницамИзмерения Тогда
				ТекТипНоменклатуры = ТекСтрокаТЧ.Номенклатура.ТипНоменклатуры;
				Если ТекСтрокаТЧ.ЕдиницаИзмерения.Пустая() И ТекТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров Тогда
					ТекстОшибки = "Строка №" + ТекСтрокаТЧ.НомерСтроки + " - не выбрана единица измерения номенклатуры.";
					дкДобавитьОшибку(Ошибки, ТекстОшибки);
					Результат = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого ТекСтрокаТЧ Из ПрайсЛисты Цикл
		Если ТекСтрокаТЧ.ПрайсЛист.Владелец <> Контрагент Тогда
			ТекстОшибки = "Строка №" + ТекСтрокаТЧ.НомерСтроки + " - прайс-лист "+ТекСтрокаТЧ.ПрайсЛист+" не принадлежит "+Контрагент+".";
			дкДобавитьОшибку(Ошибки, ТекстОшибки);
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Проверяет корректность заполнения характеристики номенклатуры
//                                                                
// Параметры:
//  СтрокаТовары - строка табличной части документа - ссылка на строку.
//  Ошибки       - строка - содержит описание ошибки.
//
// Возвращаемое значение:
//  Булево 
//
Функция ПроверитьЗаполнениеХарактеристики(СтрокаТовары, Ошибки="") Экспорт
	// Отключаем вызов исключения в дкПроверитьКорректность()
	Возврат Истина;
КонецФункции // ПроверитьЗаполнениеХарактеристики()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УстановкаЦенКомпании","Изменение цен компании",Истина);
	СписокХозОпераций.Добавить("УстановкаЦенКонтрагента","Изменение цен контрагента");
	СписокХозОпераций.Добавить("УстановкаНаценокПрайсЛиста","Изменение наценок прайс-листа");
	СписокХозОпераций.Добавить("УстановкаСкидокПрайсЛиста","Изменение скидок прайс-листа");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

//Заполнения базового типа цен в зависимости от источника базовых цен
//
// Параметры;
//  СтрокаТоваров - СтрокаТабличнойЧасти – Ссылка на строку табличной части документа.
//
Процедура ЗаполнитьБазовуюЦену(СтрокаТоваров, НаценкаДляПрайсЛиста=Неопределено) Экспорт
	
	Если обЗначениеНеЗаполнено(ДатаНачалаДействия) Тогда
		ДатаНачалаДействия = Дата;
	КонецЕсли;
	
	Момент = ?(Ссылка.Пустая(), Новый МоментВремени(КонецДня(ДатаНачалаДействия)), Новый Граница(Новый МоментВремени(ДатаНачалаДействия, Ссылка), ВидГраницы.Исключая));
	Если РасчетЦенОт = 0 Тогда
		//Расчет от базового типа цен
		СтрокаТоваров.ЦенаБазовая = обПолучитьЦену(БазовыйТипЦен, СтрокаТоваров.Номенклатура, Момент, ?(ХозОперация=Справочники.ХозОперации.УстановкаЦенКонтрагента,Контрагент,Справочники.Контрагенты.ПустаяСсылка()),ВалютаДокумента,КурсДокумента, СтрокаТоваров.ХарактеристикаНоменклатуры, СтрокаТоваров.ЕдиницаИзмерения, ПодразделениеКомпанииПолучатель, Ложь);
	ИначеЕсли РасчетЦенОт = 1 Тогда
		//Расчет от документа основания
		Попытка
			ИмяобъектОснования = ДокументОснование.Метаданные().Имя;
			Если дкДокументЕстьРеквизитШапки("ТипЦен", ИмяобъектОснования) Тогда
				Алгоритм = ДокументОснование.ТипЦен.АлгоритмПолученияЦены;
			Иначе
				Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре;
			КонецЕсли;
			
			Если Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре Тогда
				СтрокаТоваровОснования = ДокументОснование.Товары.Найти(СтрокаТоваров.Номенклатура,"Номенклатура");
			ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
				// Найдем строку с номенклатурой и такой же характеристикой
				ПараметрыОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
				ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаТоваров);
				НайденныеСтроки = ДокументОснование.Товары.НайтиСтроки(ПараметрыОтбора);
				Если НайденныеСтроки.Количество() = 0 Тогда
					СтрокаТоваровОснования = Неопределено;
				Иначе
					СтрокаТоваровОснования = НайденныеСтроки[0];
				КонецЕсли;
			ИначеЕсли Алгоритм = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
				// Найдем строку с номенклатурой и такой же единицей измерения
				ПараметрыОтбора = Новый Структура("Номенклатура, ЕдиницаИзмерения");
				ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаТоваров);
				НайденныеСтроки = ДокументОснование.НайтиСтроки(ПараметрыОтбора);
				Если НайденныеСтроки.Количество() = 0 Тогда
					СтрокаТоваровОснования = Неопределено;
				Иначе
					СтрокаТоваровОснования = НайденныеСтроки[0];
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаТоваровОснования = Неопределено Тогда
				СтрокаТоваров.ЦенаБазовая = 0;
			Иначе
				СтрокаТоваров.ЦенаБазовая = обПересчет(СтрокаТоваровОснования.Цена, ДокументОснование.ВалютаДокумента, ДокументОснование.КурсДокумента, ВалютаДокумента,КурсДокумента);
			КонецЕсли; 
		Исключение
			СтрокаТоваров.ЦенаБазовая = 0;
		КонецПопытки; 
	ИначеЕсли РасчетЦенОт = 2 Тогда
		//От цен поставщика
		СтрокаТоваров.ЦенаБазовая = обПолучитьЦену(ТипЦен, СтрокаТоваров.Номенклатура, Момент, Поставщик, ВалютаДокумента, КурсДокумента, СтрокаТоваров.ХарактеристикаНоменклатуры, СтрокаТоваров.ЕдиницаИзмерения, ПодразделениеКомпанииПолучатель, Ложь);
	ИначеЕсли РасчетЦенОт = 3 ИЛИ РасчетЦенОт = 5 ИЛИ РасчетЦенОт = 8 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК Количество,
		|	ПартииТоваровКомпанииОстатки.Сумма"+?(РасчетЦенОт=5, "Упр", "")+"Остаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&НаМомент, Номенклатура=&Номенклатура"+?(обЗначениеНеЗаполнено(СкладКомпании) ИЛИ РасчетЦенОт = 8,""," И СкладКомпании=&СкладКомпании")+?(РасчетЦенОт = 8, " И Партия=&Партия", "")+") КАК ПартииТоваровКомпанииОстатки";
		Запрос.УстановитьПараметр("НаМомент", Момент);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("Партия",        Партия);
		Запрос.УстановитьПараметр("Номенклатура", СтрокаТоваров.Номенклатура);
		ВыборкаСебестоимости = Запрос.Выполнить().Выбрать();
		Если ВыборкаСебестоимости.Следующий() Тогда
			Если ВыборкаСебестоимости.Количество=NULL ИЛИ ВыборкаСебестоимости.Количество=0 Тогда
				НоваяБазоваяЦена = 0;
			Иначе
				НоваяБазоваяЦена = ВыборкаСебестоимости.Сумма / ВыборкаСебестоимости.Количество;
			КонецЕсли;
			
			Если РасчетЦенОт = 3 ИЛИ РасчетЦенОт = 8 Тогда
				ВалютаСебестоимости = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			Иначе
				ВалютаСебестоимости = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			КонецЕсли;
			СтрокаТоваров.ЦенаБазовая = обПересчет(НоваяБазоваяЦена, ВалютаСебестоимости, Дата, ВалютаДокумента, КурсДокумента);
		Иначе
			СтрокаТоваров.ЦенаБазовая = 0;
		КонецЕсли;
	ИначеЕсли РасчетЦенОт = 4 Тогда
		//От прайс-листа контрагента
		Если ЗначениеЗаполнено(ПрайсЛистКонтрагента) Тогда
			СтрокаПрайсЛиста = ПрайсЛистКонтрагента.ПолучитьОбъект().ПолучитьПараметрыНоменклатуры(СтрокаТоваров.Номенклатура,,, ТипЦен, ПодразделениеКомпанииПолучатель, Дата);
			Если СтрокаПрайсЛиста = Неопределено Тогда
				СтрокаТоваров.ЦенаБазовая = 0;
			Иначе
				СтрокаТоваров.ЦенаБазовая = обПересчет(СтрокаПрайсЛиста.ЦенаПокупки, СтрокаПрайсЛиста.Валюта, Дата, ВалютаДокумента, КурсДокумента);
				НаценкаДляПрайсЛиста = СтрокаПрайсЛиста.Наценка;
			КонецЕсли;
		Иначе
			СтрокаТоваров.ЦенаБазовая = 0;
		КонецЕсли;
		
	ИначеЕсли  РасчетЦенОт = 6 Тогда
		//Расчет от базового типа цен
		СтрокаТоваров.ЦенаБазовая = обПолучитьЦену(ТипЦен, СтрокаТоваров.Номенклатура, Момент, ?(ХозОперация=Справочники.ХозОперации.УстановкаЦенКонтрагента, Контрагент, Справочники.Контрагенты.ПустаяСсылка()),ВалютаДокумента,КурсДокумента, СтрокаТоваров.ХарактеристикаНоменклатуры, СтрокаТоваров.ЕдиницаИзмерения,БазовоеПодразделение, Ложь);
	ИначеЕсли  РасчетЦенОт = 7 Тогда
		//Расчет от базовой единицы измерения
		СтрокаТоваров.ЦенаБазовая = обПолучитьЦену(ТипЦен, СтрокаТоваров.Номенклатура, Момент, ?(ХозОперация=Справочники.ХозОперации.УстановкаЦенКонтрагента, Контрагент, Справочники.Контрагенты.ПустаяСсылка()),ВалютаДокумента,КурсДокумента, СтрокаТоваров.ХарактеристикаНоменклатуры, СтрокаТоваров.БазоваяЕдиницаИзмерения, ПодразделениеКомпанииПолучатель, Ложь);
	КонецЕсли; 
КонецПроцедуры // ЗаполнитьБазовуюЦену()

//Заполнения базового типа цен в зависимости от источника базовых цен
//
Процедура ЗаполнитьБазовыеЦены() Экспорт
	
	// Запоминаем новый тип цен.
	ТипЦенСтарый = ТипЦен;
	
	//Заполнение базовых цен в валюте установленного типа цен и
	//заполнение ими табличной части
	ВалютаСтарая = ВалютаДокумента;
	КурсСтарый   = КурсДокумента;
	
	Если Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТипЦен.ВВалютеУчета Тогда
		ВалютаДокумента = обВалютаТипаЦены(Неопределено, ТипЦен, Ложь);
		СтруктураКурса  = обКурсДляВалюты(ВалютаДокумента,Дата);
		КурсДокумента   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;
	
	ИзмениласьВалюта = (ВалютаСтарая<>ВалютаДокумента);
	
	Если РасчетЦенОт = 4 Тогда
		//От прайс-листа контрагента
		//Будем заполнять по собственному алгоритму
		//Сначала очистим все базовые цены и пересчитаем цену при необходимости
		Для Каждого ТекСтрока Из Товары Цикл
			Если ТекСтрока.Цена <> 0 И ИзмениласьВалюта Тогда
				ТекСтрока.Цена = обПересчет(ТекСтрока.Цена, ВалютаСтарая, КурсСтарый, ВалютаДокумента, КурсДокумента);
			КонецЕсли;
			ТекСтрока.ЦенаБазовая = 0;
			ТекСтрока.ПроцентНаценки = 0;
			ТекСтрока.СуммаНаценки = 0;
		КонецЦикла;
		// Затем установим цены по прайс-листу
		Если ЗначениеЗаполнено(ПрайсЛистКонтрагента) Тогда
			ЗаполнениеПоПрайсЛистуКонтрагента(Ложь);
		КонецЕсли;
	Иначе
		Для Каждого ТекСтрока Из Товары Цикл
			// Получим базовую цену.
			ЗаполнитьБазовуюЦену(ТекСтрока);
			Если ТекСтрока.Цена <> 0 Тогда
				Если ИзмениласьВалюта Тогда
					ТекСтрока.Цена = обПересчет(ТекСтрока.Цена, ВалютаСтарая, КурсСтарый, ВалютаДокумента, КурсДокумента);
				КонецЕсли;
				Если ТекСтрока.ЦенаБазовая = 0 Тогда
					ТекСтрока.ПроцентНаценки = 0;
					ТекСтрока.СуммаНаценки = 0;
				Иначе
					ТекСтрока.ПроцентНаценки = ((ТекСтрока.Цена-ТекСтрока.ЦенаБазовая)/ТекСтрока.ЦенаБазовая)*100;
					ТекСтрока.СуммаНаценки = ТекСтрока.Цена-ТекСтрока.ЦенаБазовая;
				КонецЕсли;
			Иначе
				// Если цена была равной нулю, то пытаемся автоматически рассчитать цену от базовой цены.
				// Рассчитываем процент наценки.
				Если ПроцентНаценкиИзНоменклатуры И ТекСтрока.Номенклатура.ПроцентНаценки<>0 Тогда
					ТекСтрока.ПроцентНаценки = ТекСтрока.Номенклатура.ПроцентНаценки;
				ИначеЕсли (НЕ ПроцентНаценкиИзНоменклатуры) И ПроцентНаценки<>0 Тогда
					ТекСтрока.ПроцентНаценки = ПроцентНаценки;
				Иначе
					ТекСтрока.ПроцентНаценки = БазовыйТипЦен.ПроцентСкидкиНаценки;
				КонецЕсли;
				ОбработкаРеквизита("Товары.ПроцентНаценки", ТекСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьБазовыеЦены()

// Процедура заполнения базовых цен по прайс-листу контрагента
Процедура ЗаполнитьБазовыеЦеныПоПрайсЛисту(ЗагрузитьНоменклатуру)
	Попытка
		Connection = Новый COMОбъект("ADODB.Connection");
		Коннект = Connection.Open(ПрайсЛистКонтрагента.СтрокаПодключения);
	Исключение
		Сообщить(ОписаниеОшибки(),СтатусСообщения.Внимание); 
		Возврат;
	КонецПопытки;
	
	Command = Новый  COMОбъект("ADODB.Command");
	Recordset = Новый  COMОбъект("ADODB.Recordset");
	Recordset.ActiveConnection = Connection;
	
	СтрокаРеквизита = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Артикул", "ИмяРеквизитаПрайсЛиста");
	Если СтрокаРеквизита = Неопределено ИЛИ ПустаяСтрока(СтрокаРеквизита.ИмяПоляФайла) Тогда
		Возврат;
	КонецЕсли;
	ИмяПоляАртикул = СтрокаРеквизита.ИмяПоляФайла;
	
	СтрокаРеквизита = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Цена", "ИмяРеквизитаПрайсЛиста");
	Если СтрокаРеквизита = Неопределено ИЛИ ПустаяСтрока(СтрокаРеквизита.ИмяПоляФайла) Тогда
		Возврат;
	КонецЕсли;
	ИмяПоляЦены = СтрокаРеквизита.ИмяПоляФайла;
	
	СтрокаРеквизита = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("ТегПозиции", "ИмяРеквизитаПрайсЛиста");
	Если СтрокаРеквизита = Неопределено ИЛИ ПустаяСтрока(СтрокаРеквизита.ИмяПоляФайла) Тогда
		Возврат;
	Иначе
		ИмяПоляТегПозиции = СтрокаРеквизита.ИмяПоляФайла;
	КонецЕсли;
	
	ИмяТаблицы = ПрайсЛистКонтрагента.ИмяТаблицы;
	
	Если ЗагрузитьНоменклатуру Тогда
		Запрос = Новый Запрос;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.ЭтоГруппа = ЛОЖЬ
		|	И Номенклатура.ПометкаУдаления = ЛОЖЬ
		|	И Номенклатура.АртикулДляПоиска <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура.Ссылка";
		тзТовары = Запрос.Выполнить().Выгрузить();
	Иначе
		тзТовары = Товары;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ПолучитьБазовуюЦену", Ложь);
	
	Для каждого СтрокаТоваров Из тзТовары Цикл
		//Формируем запрос по поиску номенклатуры во внешнем источнике
		ТекстЗап="
		|SELECT TOP 1 "+ИмяТаблицы+".["+ИмяПоляЦены+"]" + ?(ИмяПоляТегПозиции=Неопределено, "", ", "+ИмяТаблицы+".["+ИмяПоляТегПозиции+"] ") + "
		|FROM "+ИмяТаблицы+"
		|WHERE (("+ИмяТаблицы+".["+ИмяПоляАртикул+"]) = '"+СтрокаТоваров.Номенклатура.Артикул+"')";
		Command.CommandText = ТекстЗап;
		Command.ActiveConnection = Connection;
		Попытка
			Recordset = Command.Execute();
		Исключение
			Сообщить(ОписаниеОшибки()+"
				|Обращайтесь к администратору базы данных!",СтатусСообщения.Внимание);
			Возврат;
		КонецПопытки;
		Попытка
			Цена = Число(Recordset.Fields(0).Value);
			Если ЗагрузитьНоменклатуру Тогда
				СтрокаТЧ = Товары.Добавить();
				СтрокаТЧ.Номенклатура = СтрокаТоваров.Номенклатура;
				ОбработкаРеквизита("Товары.Номенклатура", СтрокаТЧ,, ДопПараметры);
			Иначе
				СтрокаТЧ = СтрокаТоваров;
			КонецЕсли; 
		Исключение
			Цена = 0;
			Если ЗагрузитьНоменклатуру Тогда
				СтрокаТЧ = Неопределено;
			Иначе
				СтрокаТЧ = СтрокаТоваров;
			КонецЕсли; 
		КонецПопытки;
		ТегПозиции = "";
		Если ИмяПоляТегПозиции <> Неопределено Тогда
			Попытка
				ТегПозиции = Строка(Recordset.Fields(1).Value);
			Исключение
			КонецПопытки;
		КонецЕсли;
		Если СтрокаТЧ <> Неопределено Тогда
			ЦенаВВалютеДокумента = обПересчет(Цена, ПрайсЛистКонтрагента.Валюта, Дата, ВалютаДокумента, КурсДокумента);
			ДанныеЦены = Справочники.ПрайсЛистыКонтрагентов.РассчитатьЦеныПрайсЛиста(ПрайсЛистКонтрагента, Истина, Истина, ТипЦен, ПодразделениеКомпанииПолучатель, Дата, ЦенаВВалютеДокумента, СтрокаТЧ.Номенклатура, ТегПозиции, СтрокаТЧ.Номенклатура.Производитель);
			СтрокаТЧ.ЦенаБазовая = ДанныеЦены.ЦенаПокупки;
			СтрокаТЧ.Цена = ДанныеЦены.ЦенаПродажи;
			ОбработкаРеквизита("Товары.Цена", СтрокаТЧ,, ДопПараметры);
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	Если Имя="ТипЦен" Тогда
		Если ТипЦен.Рассчитывается Тогда
			Сообщить("Тип цен не должен быть рассчитываемым");
			ТипЦен = ТипЦенСтарый;
			Возврат Ложь;
		КонецЕсли;
		
		// Если сменился тип цены, то придется пересчитать табличную часть.
		Если ТипЦенСтарый<>ТипЦен Тогда
			// Если новый тип цен не поддерживает учет цен по характеристиками, то очистим данный реквизит в ТЧ.
			Если ТипЦен.АлгоритмПолученияЦены<>Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
				Для Каждого ТекСтрока Из Товары Цикл
					Если Не ТекСтрока.ХарактеристикаНоменклатуры.Пустая() Тогда
						ТекСтрока.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			ЗаполнитьБазовыеЦены();
		КонецЕсли;
		
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
				ЭтаФорма.Обновить();
			КонецЕсли; 
		#КонецЕсли
		
	ИначеЕсли Имя="ПодразделениеКомпанииПолучатель" Тогда
		Для Каждого ТекСтрока Из Товары Цикл
			Если ТекСтрока.Цена<>0 Тогда
				//Установка процента наценки
				Если ТекСтрока.ЦенаБазовая=0 Тогда
					ТекСтрока.ПроцентНаценки=0;
				Иначе
					ТекСтрока.ПроцентНаценки=((ТекСтрока.Цена-ТекСтрока.ЦенаБазовая)/ТекСтрока.ЦенаБазовая)*100;
				КонецЕсли;
				ОбработкаРеквизита("Товары.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
			Иначе
				// Если цена была равной нулю, то пытаемся автоматически рассчитать цену от базовой цены.
				// Получим базовую цену.
				НаценкаДляПрайсЛиста = Неопределено;
				ЗаполнитьБазовуюЦену(ТекСтрока, НаценкаДляПрайсЛиста);
				// Рассчитываем процент наценки.
				Если НаценкаДляПрайсЛиста = Неопределено Тогда
					Если ПроцентНаценкиИзНоменклатуры И ТекСтрока.Номенклатура.ПроцентНаценки<>0 Тогда
						ТекСтрока.ПроцентНаценки = ТекСтрока.Номенклатура.ПроцентНаценки;
					ИначеЕсли (НЕ ПроцентНаценкиИзНоменклатуры) И ПроцентНаценки<>0 Тогда
						ТекСтрока.ПроцентНаценки = ПроцентНаценки;
					Иначе
						ТекСтрока.ПроцентНаценки = БазовыйТипЦен.ПроцентСкидкиНаценки;
					КонецЕсли;
				Иначе
					ТекСтрока.ПроцентНаценки = НаценкаДляПрайсЛиста;
				КонецЕсли;
				ОбработкаРеквизита("Товары.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		КонецЦикла;
		
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
				ЭтаФорма.Обновить();
			КонецЕсли; 
		#КонецЕсли
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ" 	
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если ТекСтрока.Номенклатура.Пустая() Тогда 
			ТекСтрока.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			Возврат Ложь;
		КонецЕсли;
		Результат = Истина;
		
		// проверим а не набор ли у нас...
		Если ТекСтрока.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Набор Тогда
			// имеем дело с набором, разложим его...
			Набор=ТекСтрока.Номенклатура;
			// удалим строку из табличной части
			Товары.Удалить(ТекСтрока);
			// теперь будем рекурсивно добавлять состав набора
			Для Каждого ТоварИзНабора Из Набор.СоставНабора Цикл
				Если ТоварИзНабора.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Набор Тогда
					//Вот это да, набор в наборе ... игнорируем
					#Если Клиент Тогда
						Сообщить("В наборе """+СокрЛП(Набор)+""" присутствует набор """+СокрЛП(ТоварИзНабора.Номенклатура)+""". Добавление набора """+СокрЛП(ТоварИзНабора.Номенклатура)+""" отменено.",СтатусСообщения.Внимание);
					#КонецЕсли
				Иначе
					// поищем номенклатуру из набора в таблице товаров
					МассивСтрок=Товары.НайтиСтроки(Новый Структура("Номенклатура",ТоварИзНабора.Номенклатура));
					Если МассивСтрок.Количество()=0 Тогда
						СтрокаТЧ=ЭтотОбъект.Товары.Добавить();
						СтрокаТЧ.Номенклатура=ТоварИзНабора.Номенклатура;
						Если (НЕ ДопПараметры = Неопределено) И ДопПараметры.Свойство("ПолучитьБазовуюЦену") Тогда
							ДопПараметры.Вставить("ПолучитьБазовуюЦену", Истина);
						КонецЕсли;
						Результат = ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",СтрокаТЧ,ЭтаФорма,ДопПараметры);
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла;
			Возврат Результат;
		КонецЕсли;
		
		// заполним единицу измерения
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			Если ТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
				Если ТекСтрока.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
					Владелец = ТекСтрока.Номенклатура.ТипНоменклатуры;
				Иначе
					Владелец = ТекСтрока.Номенклатура;
				КонецЕсли;
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЕдиницыИзмерения.Ссылка
				|ИЗ
				|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
				|ГДЕ
				|	ЕдиницыИзмерения.Коэффициент = 1
				|	И ЕдиницыИзмерения.Владелец = &Владелец";
				
				Запрос.УстановитьПараметр("Владелец", Владелец);
				Результат = Запрос.Выполнить().Выбрать();
				
				Если Результат.Следующий() Тогда
					ТекСтрока.ЕдиницаИзмерения = Результат.Ссылка;
				КонецЕсли;
			Иначе
				ТекСтрока.ЕдиницаИзмерения = ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
			КонецЕсли;
		КонецЕсли;
		ТекСтрока.Коэффициент = 1;
		Результат = ЭтотОбъект.ОбработкаРеквизита("Товары.ХарактеристикаНоменклатуры", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Возврат ЭтотОбъект.ОбработкаРеквизита("Товары.ЕдиницаИзмерения", ТекСтрока, ЭтаФорма, ДопПараметры);
	
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		ТекСтрока.Коэффициент = ТекСтрока.ЕдиницаИзмерения.Коэффициент;
		НаценкаДляПрайсЛиста = Неопределено;
		// Получим базовую цену.
		Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("ПолучитьБазовуюЦену") Тогда
			Если ДопПараметры.ПолучитьБазовуюЦену Тогда
				ЗаполнитьБазовуюЦену(ТекСтрока, НаценкаДляПрайсЛиста);
			КонецЕсли;
		Иначе
			ЗаполнитьБазовуюЦену(ТекСтрока, НаценкаДляПрайсЛиста);
		КонецЕсли;
		// Рассчитываем процент наценки.
		Если НаценкаДляПрайсЛиста = Неопределено Тогда
			Если ПроцентНаценкиИзНоменклатуры И ТекСтрока.Номенклатура.ПроцентНаценки<>0 Тогда
				ТекСтрока.ПроцентНаценки = ТекСтрока.Номенклатура.ПроцентНаценки;
			ИначеЕсли (НЕ ПроцентНаценкиИзНоменклатуры) И ПроцентНаценки <> 0 Тогда
				ТекСтрока.ПроцентНаценки = ПроцентНаценки;
			Иначе
				ТекСтрока.ПроцентНаценки = БазовыйТипЦен.ПроцентСкидкиНаценки;
			КонецЕсли;
		Иначе
			ТекСтрока.ПроцентНаценки = НаценкаДляПрайсЛиста;
		КонецЕсли;
		ОбработкаРеквизита("Товары.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		ТекСтрока.Цена = ТекСтрока.ЦенаБазовая+((ТекСтрока.ЦенаБазовая*ТекСтрока.ПроцентНаценки)/100);
		ТекСтрока.СуммаНаценки = ТекСтрока.Цена-ТекСтрока.ЦенаБазовая;
		
		//округляем
		НоваяЦена = ТекСтрока.Цена;
		ДельтаОкругления = ?(ОкруглятьДо=0,0,НоваяЦена/ОкруглятьДо);
		ДельтаОкругленияЦел = Цел(ДельтаОкругления);
		Если ДельтаОкругления<>ДельтаОкругленияЦел Тогда
			НоваяЦена = (ДельтаОкругленияЦел+1)*ОкруглятьДо;
		КонецЕсли; 
		Если НоваяЦена <> ТекСтрока.Цена Тогда
			ТекСтрока.Цена = НоваяЦена;
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаНаценки" Тогда
		ТекСтрока.Цена = ТекСтрока.ЦенаБазовая+ТекСтрока.СуммаНаценки;
		ТекСтрока.ПроцентНаценки = ?(ТекСтрока.ЦенаБазовая=0,0,((ТекСтрока.Цена - ТекСтрока.ЦенаБазовая)/ТекСтрока.ЦенаБазовая)*100);

		//округляем
		НоваяЦена = ТекСтрока.Цена;
		ДельтаОкругления = ?(ОкруглятьДо=0,0,НоваяЦена/ОкруглятьДо);
		ДельтаОкругленияЦел = Цел(ДельтаОкругления);
		Если ДельтаОкругления<>ДельтаОкругленияЦел Тогда
			НоваяЦена = (ДельтаОкругленияЦел+1)*ОкруглятьДо;
		КонецЕсли; 
		Если НоваяЦена <> ТекСтрока.Цена Тогда
			ТекСтрока.Цена = НоваяЦена;
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		ТекСтрока.ПроцентНаценки=?(ТекСтрока.ЦенаБазовая=0,0,((ТекСтрока.Цена-ТекСтрока.ЦенаБазовая)/ТекСтрока.ЦенаБазовая)*100);
		//в "ОбработкаТабличнойЧастиТовары" нет реквизита "СуммаНаценки" !
		Попытка
			ТекСтрока.СуммаНаценки = ТекСтрока.Цена-ТекСтрока.ЦенаБазовая;
		Исключение
		КонецПопытки;
		Возврат Истина;
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда //Стандартный пересчет не пересчитал базовые цены
		Для Каждого ТекСтрока Из Товары Цикл
			ЗаполнитьБазовуюЦену(ТекСтрока);
			ОбработкаРеквизита("Товары.ПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЦикла;
		Возврат Истина;
		
	ИначеЕсли Имя = "КурсДокумента" Тогда //Тоже самое
		ОбработкаРеквизита("ВалютаДокумента");
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания  
КонецПроцедуры

Функция НетВПрайсЛисте(ПрайсЛист, ТегПозиции, Производитель = Неопределено) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПозицииПрайсЛиста.ПрайсЛист
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК ПозицииПрайсЛиста
	|ГДЕ
	|	ПозицииПрайсЛиста.ПрайсЛист = &ПрайсЛист
	|	"+?(ЗначениеЗаполнено(ТегПозиции), "И ПозицииПрайсЛиста.ТегПозиции = &ТегПозиции", "")+"
	|	"+?(ЗначениеЗаполнено(Производитель), "И ПозицииПрайсЛиста.Производитель = &Производитель", "")+"
	|	";
	
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛист);
	Запрос.УстановитьПараметр("ТегПозиции", ТегПозиции);
	Запрос.УстановитьПараметр("Производитель", Производитель);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции //ЕстьВПрайсЛисте()

//Получает последнюю действующую наценку для подразделения 
Функция ПолучитьНаценку(ПрайсЛист, ТегПозиции = Неопределено, Производитель = Неопределено) Экспорт 
	
	Подразделение = ПодразделениеКомпанииПолучатель;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Наценки.ПодразделениеКомпании КАК Подразделение,
	|	Наценки.ТегПозиции,
	|	Наценки.Производитель,
	|	Наценки.АлгоритмРасчетаЦены,
	|	Наценки.Наценка,
	|	Наценки.ОкруглятьДо
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|			&ДатаКон,
	|			ТипЦен = &ТипЦен
	|				И ПрайсЛист = &ПрайсЛист) КАК Наценки
	|ГДЕ
	|	НЕ Наценки.Отменена
	|	И (Наценки.ТегПозиции = &ТегПозиции
	|				И Наценки.Производитель = &Производитель
	|			ИЛИ Наценки.ТегПозиции = """"
	|				И Наценки.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка))
	|";
	
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛист);
	Запрос.УстановитьПараметр("ТегПозиции", ?(ТегПозиции = Неопределено, """", ТегПозиции));
	Запрос.УстановитьПараметр("Производитель", ?(Производитель = Неопределено, Справочники.Производители.ПустаяСсылка(), Производитель));
	
	ДатаКон = ?(ЗначениеЗаполнено(ДатаНачалаДействия), ДатаНачалаДействия, Дата);
	Если ЭтоНовый() Тогда
		Запрос.УстановитьПараметр("ДатаКон", Новый Граница(Новый МоментВремени(ДатаКон, Ссылка), ВидГраницы.Включая));
	Иначе
		Запрос.УстановитьПараметр("ДатаКон", Новый Граница(Новый МоментВремени(ДатаКон, Ссылка), ВидГраницы.Исключая));
	КонецЕсли;
	
	ТабНаценок = Запрос.Выполнить().Выгрузить();
	ТабНаценок.Индексы.Добавить("Подразделение");
	
	Пока Истина Цикл
		СтрокаНаценки = ТабНаценок.Найти(Подразделение, "Подразделение");
		Если СтрокаНаценки = Неопределено Тогда
			Если Подразделение.Родитель.Пустая() Тогда
				Прервать;
			Иначе
				Подразделение = Подразделение.Родитель;
			КонецЕсли;
		Иначе
			Если ТегПозиции <> Неопределено ИЛИ Производитель <> Неопределено Тогда
				СтрокиНаценки = ТабНаценок.НайтиСтроки(Новый Структура("Подразделение, ТегПозиции, Производитель", Подразделение, ?(ТегПозиции = Неопределено, """", ТегПозиции), ?(Производитель = Неопределено, Справочники.Производители.ПустаяСсылка(), Производитель)));
				Если СтрокиНаценки.Количество() > 0 Тогда
					СтруктураНаценки = Новый Структура("Наценка, АлгоритмРасчетаЦены, ОкруглятьДо", СтрокиНаценки[0].Наценка, СтрокиНаценки[0].АлгоритмРасчетаЦены, СтрокиНаценки[0].ОкруглятьДо);
				КонецЕсли;
			Иначе
				СтруктураНаценки = Новый Структура("Наценка, АлгоритмРасчетаЦены, ОкруглятьДо", СтрокаНаценки.Наценка, СтрокаНаценки.АлгоритмРасчетаЦены, СтрокаНаценки.ОкруглятьДо);
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураНаценки;
	
КонецФункции //ПолучитьНаценку()

//Получает последнюю действующую скидку
Функция ПолучитьСкидку(ПрайсЛист, ТегПозиции = Неопределено, Производитель = Неопределено)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Скидки.Скидка
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(&ДатаКон, ПрайсЛист = &ПрайсЛист) КАК Скидки
	|ГДЕ
	|	НЕ Скидки.Отменена
	|	И (Скидки.ТегПозиции = &ТегПозиции
	|				И Скидки.Производитель = &Производитель
	|			ИЛИ Скидки.ТегПозиции = """"
	|				И Скидки.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка))";
	
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛист);
	Запрос.УстановитьПараметр("ТегПозиции", ?(ТегПозиции = Неопределено, """", ТегПозиции));
	Запрос.УстановитьПараметр("Производитель", ?(Производитель = Неопределено, Справочники.Производители.ПустаяСсылка(), Производитель));
	
	ДатаКон = ?(ЗначениеЗаполнено(ДатаНачалаДействия), ДатаНачалаДействия, Дата);
	Если ЭтоНовый() Тогда
		Запрос.УстановитьПараметр("ДатаКон", Новый Граница(Новый МоментВремени(ДатаКон, Ссылка), ВидГраницы.Включая));
	Иначе
		Запрос.УстановитьПараметр("ДатаКон", Новый Граница(Новый МоментВремени(ДатаКон, Ссылка), ВидГраницы.Исключая));
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Скидка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции //ПолучитьСкидку()

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ИзменениеЦен"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИзменениеЦен(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ИзменениеЦен");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеТипаЦен = спПолучитьНаименование(ТипЦен);
	ОбластьМакета.Параметры.ПодразделениеКомпанииПолучательПредставление = спПолучитьНаименование(ПодразделениеКомпанииПолучатель);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	СтруктураИтоговПоСтранице = Новый Структура("Пустой",0);
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		// найдём старую цену
		МоментВремени = ?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(ДатаНачалаДействия)),Новый Граница(?(обЗначениеНеЗаполнено(ДатаНачалаДействия),МоментВремени(), Новый МоментВремени(ДатаНачалаДействия, Ссылка)),ВидГраницы.Исключая));
		ЦенаСтарая    = обПолучитьЦену(ТипЦен,СтрокаТабличнойЧасти.Номенклатура,МоментВремени,?(ХозОперация=Справочники.ХозОперации.УстановкаЦенКомпании ИЛИ обЗначениеНеЗаполнено(Контрагент),Справочники.Контрагенты.ПустаяСсылка(),Контрагент),ВалютаДокумента,КурсДокумента, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, СтрокаТабличнойЧасти.ЕдиницаИзмерения, ПодразделениеКомпанииПолучатель, Ложь);
		СтруктураСтроки.Вставить("ЦенаСтарая",Формат(ЦенаСтарая,ФорматВыводаСуммы));
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;		
		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы,, ЭтотОбъект, мсвДопОбластиПодвала);		
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;                                                             
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество();
	
	// Выводим представления и расшифровки подписей
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ПредседательКомиссии.ПредседательКомиссииПредставление = ?(обЗначениеНеЗаполнено(ПредседательКомиссии.ПредседательКомиссии),"","/ "+ ПредседательКомиссии.ПредседательКомиссииПредставление);
	ОбластьПодвал.Параметры.Заполнить(ПредседательКомиссии);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/ "+ ГлавныйБухгалтер.ГлавныйБухгалтерПредставление);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьИзменениеЦен()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,, Ложь) Тогда Возврат; КонецЕсли;
	
	Если Основание <> Неопределено Тогда 
		ИмяобъектОснования = Основание.Метаданные().Имя;
		// Заполним комплектом
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.Комплектация") Тогда
			Если дкДокументЕстьРеквизитШапки("ТипЦен", ИмяобъектОснования) Тогда
				ЗаполнитьТабличнуюЧастьТоварыПоОснованиюДокументаКомплектация(Основание, Основание.ТипЦен.АлгоритмПолученияЦены);
			Иначе
				ЗаполнитьТабличнуюЧастьТоварыПоОснованиюДокументаКомплектация(Основание, Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре);
			КонецЕсли;
		Иначе
			//Заполним тч по основанию
			ИмяобъектОснования = Основание.Метаданные().Имя;
			Если дкДокументЕстьРеквизитШапки("ТипЦен", ИмяобъектОснования) Тогда
				ЗаполнитьТабличнуюЧастьТоварыПоОснованию(ЭтотОбъект.Товары, Основание.Товары, "Номенклатура", Основание.ТипЦен.АлгоритмПолученияЦены, ИмяобъектОснования);  
			Иначе
				ЗаполнитьТабличнуюЧастьТоварыПоОснованию(ЭтотОбъект.Товары, Основание.Товары, "Номенклатура", Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре, ИмяобъектОснования);
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

	Если ЭтоНовый() Тогда
		Если обЗначениеНеЗаполнено(РасчетЦенОт) Тогда
			РасчетЦенОт=0;
		КонецЕсли;
		ОкруглятьДо=1;
	КонецЕсли; 
	
	Если обЗначениеНеЗаполнено(ПодразделениеКомпанииПолучатель) Тогда
		ПодразделениеКомпанииПолучатель=Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
	КонецЕсли; 
	
	ДатаНачалаДействия=Дата;
	Если Основание<>Неопределено И 
		 (ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ
		  ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковТоваров") ИЛИ
		  ТипЗнч(Основание)=Тип("ДокументСсылка.АвансовыйОтчет") ИЛИ
		  ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковТоваровВПроизводстве"))Тогда
		ХозОперация=Справочники.ХозОперации.УстановкаЦенКомпании;
		Контрагент=Справочники.Контрагенты.ПустаяСсылка();
		Если Основание.Метаданные().Реквизиты.Найти("СкладКомпании")=Неопределено Тогда
			ТипЦен=обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);
		Иначе
			Если обЗначениеНеЗаполнено(Основание.СкладКомпании.ТипЦенРозничнойТорговли) Тогда
				ТипЦен=обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);
			Иначе
				ТипЦен=Основание.СкладКомпании.ТипЦенРозничнойТорговли;
			КонецЕсли; 
		КонецЕсли; 
		Если ТипЦен.Рассчитывается Тогда
			ПроцентНаценки=ТипЦен.ПроцентСкидкиНаценки;
		КонецЕсли; 
		РасчетЦенОт=1;
		БазовыйТипЦен=Основание.ТипЦен;
		ЗаполнитьБазовыеЦены();
		ДатаНачалаДействия=Основание.Дата;
	КонецЕсли;
	
	Если Основание<>Неопределено И ТипЗнч(Основание)=Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		ХозОперация=Справочники.ХозОперации.УстановкаЦенКомпании;
		Контрагент=Справочники.Контрагенты.ПустаяСсылка();
		ТипЦен=Основание.ТипЦен;
		РасчетЦенОт=6;
		Если ТипЦен.Рассчитывается Тогда
			ПроцентНаценки=ТипЦен.ПроцентСкидкиНаценки;
		КонецЕсли;
		Если ТипЗнч(Основание.СкладПолучатель)=Тип("СправочникСсылка.СкладыКомпании") Тогда
			ПодразделениеКомпанииПолучатель=Основание.СкладПолучатель.Подразделение;
		Иначе
			ПодразделениеКомпанииПолучатель=Основание.СкладПолучатель;
		КонецЕсли;
		Если ТипЗнч(Основание.СкладКомпании)=Тип("СправочникСсылка.СкладыКомпании") Тогда
			БазовоеПодразделение=Основание.СкладКомпании.Подразделение;
		Иначе
			БазовоеПодразделение=Основание.СкладКомпании;
		КонецЕсли;
		ЗаполнитьБазовыеЦены();
		ДатаНачалаДействия=Основание.Дата;
		Для Каждого СтрокаТЧ Из Товары Цикл
			Если НЕ обЗначениеНеЗаполнено(СтрокаТЧ.ЦенаБазовая) Тогда
				СтрокаТЧ.Цена = СтрокаТЧ.ЦенаБазовая + СтрокаТЧ.ЦенаБазовая*СтрокаТЧ.ПроцентНаценки/100;
			Иначе
				СтрокаТЧ.ЦенаБазовая = СтрокаТЧ.Цена;
				СтрокаТЧ.Цена = СтрокаТЧ.Цена + СтрокаТЧ.Цена*СтрокаТЧ.ПроцентНаценки/100;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если (ХозОперация = Справочники.ХозОперации.УстановкаЦенКомпании) И 
		(НЕ обЗначениеНеЗаполнено(Контрагент)) Тогда
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли; 
	Если ХозОперация = Справочники.ХозОперации.УстановкаСкидокПрайсЛиста
		ИЛИ ХозОперация = Справочники.ХозОперации.УстановкаНаценокПрайсЛиста Тогда
		Если РасчетЦенОт <> 4 Тогда
			РасчетЦенОт = 4;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ПрайсЛистКонтрагента)Тогда
			ПрайсЛистКонтрагента = Справочники.ПрайсЛистыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
		КоличествоСтрокТЧ = ПрайсЛисты.Количество();
		Если ХозОперация = Справочники.ХозОперации.УстановкаСкидокПрайсЛиста Тогда
			ТипЦенЗакупки = обПраво("ОсновнойТипЦенЗакупки",Права,,ЭтотОбъект);
			ОсновноеПодразделение = Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
			Если ТипЦен <> ТипЦенЗакупки Тогда
				ТипЦен = ТипЦенЗакупки;
			КонецЕсли;
			Если ПодразделениеКомпанииПолучатель <> ОсновноеПодразделение Тогда
				ПодразделениеКомпанииПолучатель = ОсновноеПодразделение;
			КонецЕсли;
			//если заполнены наценки, то очистим их
			МассивСтрокНаценок = ПрайсЛисты.НайтиСтроки(Новый Структура("Наценка", 0));
			Если МассивСтрокНаценок.Количество() <> КоличествоСтрокТЧ Тогда
				Для Каждого СтрокаТЧ ИЗ ПрайсЛисты Цикл
					СтрокаТЧ.Наценка = 0;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ХозОперация = Справочники.ХозОперации.УстановкаНаценокПрайсЛиста Тогда
			//если заполнены скидки, то очистим их
			МассивСтрокСкидок = ПрайсЛисты.НайтиСтроки(Новый Структура("Скидка", 0));
			Если МассивСтрокСкидок.Количество() <> КоличествоСтрокТЧ Тогда
				Для Каждого СтрокаТЧ ИЗ ПрайсЛисты Цикл
					СтрокаТЧ.Скидка = 0;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		// Очистим ТЧ товаров
		Если Товары.Количество() > 0 Тогда
			Товары.Очистить();
		КонецЕсли;
	Иначе
		// Очистим ТЧ Прайс-листов
		Если ПрайсЛисты.Количество() > 0 Тогда
			ПрайсЛисты.Очистить();
		КонецЕсли;
	КонецЕсли;
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОтменитьПроверкуДоступностиСкладов = Истина;
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	Отказ = ТипЦен.Рассчитывается;
	Если Отказ Тогда
		РезультатОбработки = "Тип цен не должен быть рассчитываемым!";
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	КонецЕсли;
	УслКонтрагент = ?(ХозОперация=Справочники.ХозОперации.УстановкаЦенКонтрагента,Контрагент,Справочники.Контрагенты.ПустаяСсылка());
		
	Если ХозОперация=Справочники.ХозОперации.УстановкаЦенКонтрагента
		ИЛИ ХозОперация=Справочники.ХозОперации.УстановкаЦенКомпании Тогда
		// если надо переоценим все склады
		Если ПодразделениеКомпанииПолучатель.ПереоценкаРозницаПоПриходу Тогда
			// склады
			ЗапросСклады=Новый Запрос("ВЫБРАТЬ
			|	СкладыКомпании.Ссылка КАК СкладКомпании
			|ИЗ
			|	Справочник.СкладыКомпании КАК СкладыКомпании
			|ГДЕ
			|	СкладыКомпании.Розничный
			|	И СкладыКомпании.ТипЦенРозничнойТорговли = &ТипЦен
			|	И СкладыКомпании.Подразделение = &Подразделение");
			ЗапросСклады.УстановитьПараметр("ТипЦен",ТипЦен);
			ЗапросСклады.УстановитьПараметр("Подразделение",ПодразделениеКомпанииПолучатель);
			
			ВыборкаСклады = ЗапросСклады.Выполнить().Выбрать();
			// товары
			ЗапросТовары=Новый Запрос("
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура КАК Номенклатура,
			|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
			|	0 КАК Количество,
			|	ДокументТовары.Цена КАК ЦенаРозничная,
			|	0 КАК Резерв
			|ИЗ
			|	Документ.ИзменениеЦен.Товары КАК ДокументТовары
			|ГДЕ
			|	  ДокументТовары.Ссылка=&Ссылка
			|");
			ЗапросТовары.УстановитьПараметр("Ссылка",Ссылка);
			ЗапросТовары.УстановитьПараметр("ПустаяХарактеристика",Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
			РезультатЗапросаПоТоварам=ЗапросТовары.Выполнить();
			// переоцениваем
			НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
			Пока ВыборкаСклады.Следующий() Цикл
				НаборЗаписейОстатки.РежимПроведения=Режим;
				НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейОстатки.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
				НаборЗаписейОстатки.СкладКомпании=ВыборкаСклады.СкладКомпании;
				Отказ=НЕ НаборЗаписейОстатки.Переоценка(ХозОперация,Истина,Истина) ИЛИ Отказ;
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			// устанавливаем цены
			НаборЗаписейЦены = Движения.Цены;
			НаборЗаписейЦены.ДокументОбъект 		= ЭтотОбъект;
			НаборЗаписейЦены.Контрагент				= ?(ХозОперация=Справочники.ХозОперации.УстановкаЦенКонтрагента,Контрагент,Справочники.Контрагенты.ПустаяСсылка());
			НаборЗаписейЦены.ТипЦен					= ТипЦен;
			НаборЗаписейЦены.ПодразделениеКомпании	= ПодразделениеКомпанииПолучатель;
			НаборЗаписейЦены.ИмяРеквизитаЦена		= "Цена";
			НаборЗаписейЦены.УстанавливатьЦеныУслуг	= Истина;
			НаборЗаписейЦены.ДатаНачалаДействия		= ДатаНачалаДействия;
			НаборЗаписейЦены.МожноСделатьОтменуЦены = Истина;
			Отказ=НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
		КонецЕсли;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.УстановкаНаценокПрайсЛиста Тогда
		// переоцениваем
		НаборЗаписейНаценки = Движения.ПрайсЛистыКонтрагентовНаценки;
		
		НаборЗаписейНаценки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейНаценки.ДатаНачалаДействия = ДатаНачалаДействия;
		НаборЗаписейНаценки.ТипЦен = ТипЦен;
		НаборЗаписейНаценки.ПодразделениеКомпании = ПодразделениеКомпанииПолучатель;
		
		Отказ = НЕ НаборЗаписейНаценки.УстановитьНаценки() ИЛИ Отказ;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.УстановкаСкидокПрайсЛиста Тогда
		НаборЗаписейСкидки = Движения.ПрайсЛистыКонтрагентовСкидки;
		НаборЗаписейСкидки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСкидки.ДатаНачалаДействия = ДатаНачалаДействия;
		
		Отказ = НЕ НаборЗаписейСкидки.УстановитьСкидки() ИЛИ Отказ;
		
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ТипЦенСтарый = Ссылка.ТипЦен;	

// Модуль документа ЗакрытиеСмены

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	            // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;   // Хранится результат обработки
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

//Выборка чеков по кассе
Перем ЧекиВыборка; // Список чеков из запроса

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("КассаККМ",1);
	
	Если ХозОперация = Справочники.ХозОперации.АктОРеализации Тогда
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	КонецЕсли;
	
	ОбязательныеБонусы = Новый Структура;
	ОбязательныеБонусы.Вставить("БонуснаяКарта", 3);
	ОбязательныеБонусы.Вставить("БонуснаяПрограмма", 1);
	
	ОбязательныеРеквизиты.Вставить("Бонусы", ОбязательныеБонусы);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("КассаККМ");
			КонецЕсли;
			
			Если ХозОперация = Справочники.ХозОперации.АктОРеализации Тогда				
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			Иначе
				КонтролируемыеРеквизитыТовары = Новый Структура();
				КонтролируемыеРеквизитыТовары.Вставить("МестоРазмещения", КонтрольПоПодразделению);
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыТовары);
				ТабличныеЧасти.Вставить("Возвраты",КонтролируемыеРеквизитыТовары);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);	
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	//Документ считаем корректно заполненным, независимо от заполненности табличных частей
	Возврат Истина;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗакрытиеСмены","Закрытие кассовой смены",Истина);
	СписокХозОпераций.Добавить("АктОРеализации","Акт о реализации");
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "СуммаВсего".
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Товары.Итог("СуммаВсего")-Возвраты.Итог("СуммаВсего")+Оплаты.Итог("СуммаОплата")-Оплаты.Итог("СуммаВозвратОплата")-ВозвратОплаты.Итог("СуммаОплата");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  ЭтотОбъект	– ДокументСсылка	– Ссылка на документ, реквизит которого обрабатывается.
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	РезультатОбработки = Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СкладКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании"
		Если СкладКомпании.Пустая() Тогда
		ИначеЕсли (ХозОперация = Справочники.ХозОперации.АктОРеализации) И (НЕ СкладКомпании.Розничный) Тогда
			#Если Клиент Тогда
			Предупреждение("Выбранный склад: " + СкладКомпании + " не соответствует хоз. операции!
			|Выберите розничный склад",10);
			#КонецЕсли
			СкладКомпании = Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Организация" Тогда
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаСкидки" Тогда
		ОбработкаРеквизита("Товары.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,"Товары.Номенклатура",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Возвраты.Номенклатура" Тогда
		Если ДопПараметры = Неопределено Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		ДопПараметры.Вставить("ТабличнаяЧастьТовары", Возвраты);
		ДопПараметры.Вставить("ИмяТаблицы", "Возвраты");
		Возврат дкОбработкаРеквизита(ЭтотОбъект,"Товары.Номенклатура",ТекСтрока,ЭтаФорма,ДопПараметры);
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат РезультатОбработки;
КонецФункции // ОбработкаРеквизита()

//Выборка и загрузка чеков
//
Процедура ЗагрузитьЧекиПоКассеККМ() Экспорт
	//Очистим ТЧ документа
	ПолученоНал=0;
	ПолученоБезнал=0;
	ВозвратНал=0;
	ВозвратБезнал=0;
	Товары.Очистить();
	Возвраты.Очистить();
	Скидки.Очистить();
	Контрагенты.Очистить();
	Оплаты.Очистить();
	ВозвратОплаты.Очистить();
	Свойства.Очистить();
	Бонусы.Очистить();
	
	ТаблицаКодовМаркировки = Новый ТаблицаЗначений;
	ТаблицаКодовМаркировки.Колонки.Добавить("ИдентификаторТовара");
	ТаблицаКодовМаркировки.Колонки.Добавить("КодМаркировки");
	ТаблицаКодовМаркировки.Колонки.Добавить("КодМаркировкиBase64");
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ДокументЧек.Ссылка КАК Чек
	             |ИЗ
	             |	Документ.Чек КАК ДокументЧек
	             |ГДЕ
	             |	ДокументЧек.Дата <= &ДатаДокумента
	             |	И ДокументЧек.КассаККМ = &КассаККМ
	             |	И ДокументЧек.ПометкаУдаления = ЛОЖЬ
	             |	И ДокументЧек.Проведен = ИСТИНА
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ДокументЧек.Дата";
	Запрос.УстановитьПараметр("ДатаДокумента",Дата);
	Запрос.УстановитьПараметр("КассаККМ",КассаККМ);
	ЧекиВыборка=Запрос.Выполнить().Выбрать();
	Пока ЧекиВыборка.Следующий() Цикл
		//Перебор чеков для свертки
		#Если Клиент Тогда
		Состояние("Загрузка чека <"+СокрЛП(ЧекиВыборка.Чек)+"> ...");
	#КонецЕсли
		ЗагрузитьЧек(ЧекиВыборка.Чек, ТаблицаКодовМаркировки);
	КонецЦикла;
	
	//Определим последний документ закрытия смены по данной кассе
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
	             |	ЗакрытиеСмены.ДатаПоследнегоДокумента КАК ДатаПоследнегоДокумента
	             |ИЗ
	             |	Документ.ЗакрытиеСмены КАК ЗакрытиеСмены
	             |ГДЕ
	             |	ЗакрытиеСмены.КассаККМ = &КассаККМ
	             |	И ЗакрытиеСмены.ДатаПоследнегоДокумента < &ДатаПоследнегоДокумента
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ДатаПоследнегоДокумента УБЫВ";
	Запрос.УстановитьПараметр("КассаККМ",КассаККМ);
	Запрос.УстановитьПараметр("ДатаПоследнегоДокумента",Дата);
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатаНачало=Выборка.ДатаПоследнегоДокумента;
	Иначе
		ДатаНачало=Неопределено;
	КонецЕсли; 
	//Выборка чеков на оплату по кассе ККМ
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ДокументЧекНаОплату.Ссылка КАК ЧекНаОплату
	             |ИЗ
	             |	Документ.ЧекНаОплату КАК ДокументЧекНаОплату
	             |ГДЕ
	             |	ДокументЧекНаОплату.Дата <= &ДатаКонец
	             |	"+?(ДатаНачало=Неопределено,"","И ДокументЧекНаОплату.Дата > &ДатаНачало")+"
	             |	И ДокументЧекНаОплату.КассаККМ = &КассаККМ
	             |	И ДокументЧекНаОплату.ПометкаУдаления = ЛОЖЬ
	             |	И ДокументЧекНаОплату.Проведен = ИСТИНА
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ДокументЧекНаОплату.Дата";
	Запрос.УстановитьПараметр("ДатаКонец",Дата);
	Запрос.УстановитьПараметр("ДатаНачало",ДатаНачало);
	Запрос.УстановитьПараметр("КассаККМ",КассаККМ);
	ЧекиНаОплатуВыборка=Запрос.Выполнить().Выбрать();
	Пока ЧекиНаОплатуВыборка.Следующий() Цикл
		//Перебор чеков на оплату для свертки
		#Если Клиент Тогда
		Состояние("Загрузка чека на оплату <"+СокрЛП(ЧекиНаОплатуВыборка.ЧекНаОплату)+"> ...");
		#КонецЕсли
		ЗагрузитьЧекНаОплату(ЧекиНаОплатуВыборка.ЧекНаОплату);
	КонецЦикла;
	
	СвернутьЧеки(ТаблицаКодовМаркировки);
	
	// Поищем последний ПКО по данной кассе, вдруг он был после последнего чека?
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
					|	ДокументПКО.Ссылка КАК ПКО
					|ИЗ
					|	Документ.ПриходныйКассовыйОрдер КАК ДокументПКО
					|ГДЕ
					|	ДокументПКО.Дата <= &ДатаКонец
					|	"+?(ДатаНачало=Неопределено,"","И ДокументПКО.Дата > &ДатаНачало")+"
					|  И ДокументПКО.КассаККМ = &КассаККМ
	                |  И ДокументПКО.ПометкаУдаления = ЛОЖЬ
					|  И ДокументПКО.Проведен = ИСТИНА
					|
					|УПОРЯДОЧИТЬ ПО
					|	ДокументПКО.Дата УБЫВ";
	Запрос.УстановитьПараметр("ДатаКонец",Дата);
	Запрос.УстановитьПараметр("ДатаНачало",ДатаНачало);
	Запрос.УстановитьПараметр("КассаККМ",КассаККМ);
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда // В смене были ПКО, проверим последний и скорректируем ДатаПоследнегоДокумента
		ДатаПоследнегоДокумента=?(ДатаПоследнегоДокумента<Выборка.ПКО.Дата,Выборка.ПКО.Дата,ДатаПоследнегоДокумента);
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьЧекиПоКассеККМ()
 
//Добавляет ТЧ документа чека в ТЧ-и закрытия смены
//
// Параметры:
//  Чек - ДокументСсылка - Документ.Чек.
//
Процедура ЗагрузитьЧек(Чек, ТаблицаКодовМаркировки = Неопределено) Экспорт
	//Проверим что чек проведен
	Если НЕ Чек.Проведен Тогда Возврат; КонецЕсли; 
	ХозОперацияЧека=Чек.ХозОперация;
	Если ХозОперацияЧека<>Справочники.ХозОперации.Чек И ХозОперацияЧека<>Справочники.ХозОперации.ЧекНаВозврат Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПоследнегоДокумента=?(ДатаПоследнегоДокумента<Чек.Дата,Чек.Дата,ДатаПоследнегоДокумента);
	
	ДокументПродажи=?(ХозОперацияЧека=Справочники.ХозОперации.Чек,Истина,Ложь);
	Если ДокументПродажи Тогда
		Если Чек.ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет ИЛИ
			 Чек.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ПолученоНал=ПолученоНал+(Чек.СуммаДокумента-Чек.ПолученоБезнал);
		КонецЕсли;
		Если Чек.ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет ИЛИ
			 Чек.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ПолученоБезнал=ПолученоБезнал+Чек.ПолученоБезнал;
		КонецЕсли;
	Иначе
		Если Чек.ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет ИЛИ
			 Чек.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ВозвратНал=ВозвратНал+(Чек.СуммаДокумента-Чек.ПолученоБезнал);
		КонецЕсли;
		Если Чек.ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет ИЛИ
			 Чек.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ВозвратБезнал=ВозвратБезнал+Чек.ПолученоБезнал;
		КонецЕсли;
	КонецЕсли; 
	
	//Получим табличную часть оплат документа чек
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ЧекОплаты.Ссылка.КассаККМ,
	             |	ЧекОплаты.ТипОплаты,
	             |	ЧекОплаты.ТипПлатежнойКарты,
	             |	ЧекОплаты.Карточка,
	             |	ЧекОплаты.Контрагент,
	             |	ЧекОплаты.ДоговорВзаиморасчетов,
	             |	ЧекОплаты.Сумма КАК Сумма
	             |ИЗ
	             |	Документ.Чек.Оплаты КАК ЧекОплаты
	             |ГДЕ
	             |	ЧекОплаты.Ссылка = &Чек";
	Запрос.УстановитьПараметр("Чек",Чек);
	Выборка=Запрос.Выполнить().Выбрать();
	
	//Перебираем строки ТЧ чека
	Пока Выборка.Следующий() Цикл
		//Добавим новую строку в ТЧ оплаты
		НоваяСтрока						= ?(ДокументПродажи, Оплаты.Добавить(), ВозвратОплаты.Добавить());
		НоваяСтрока.Касса 				= Выборка.КассаККМ;
		НоваяСтрока.ТипОплаты 			= Выборка.ТипОплаты;
		НоваяСтрока.ТипПлатежнойКарты 	= Выборка.ТипПлатежнойКарты;
		НоваяСтрока.Карточка 			= Выборка.Карточка;
		НоваяСтрока.Контрагент 			= Выборка.Контрагент;
		НоваяСтрока.ДоговорВзаиморасчетов= Выборка.ДоговорВзаиморасчетов;
		НоваяСтрока.Сумма 				= Выборка.Сумма;
		НоваяСтрока.КоличествоЧеков 	= 1;
	КонецЦикла;
	
	ДатаПродажи = Неопределено;
	НомерФискальногоЧека = Неопределено;
	
	Если НЕ ДокументПродажи Тогда
		ДатаПродажи = ?(ЗначениеЗаполнено(Чек.ДокументОснование) И ТипЗнч(Чек.ДокументОснование) = Тип("ДокументСсылка.Чек"), Чек.ДокументОснование.Дата, Неопределено);
		НомерФискальногоЧека = Чек.НомерЧека;
	КонецЕсли;
	
	Коэффициент = ?(ДокументПродажи, 1, -1);
	
	//Получим табличную часть документа чек
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ЧекТовары.Ссылка.Контрагент КАК Контрагент,
	             |	ЧекТовары.Ссылка.Карточка КАК Карточка,
	             |	ЧекТовары.Ссылка.ПлатежнаяКарта КАК ПлатежнаяКарта,
	             |	ЧекТовары.Ссылка.СкидкаНаценка КАК СкидкаНаценка,
	             |	ЧекТовары.НомерСтроки КАК НомерСтроки,
	             |	ЧекТовары.Номенклатура КАК Номенклатура,
	             |	ЧекТовары.Количество КАК Количество,
	             |	ЧекТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	             |	ЧекТовары.Коэффициент КАК Коэффициент,
	             |	ЧекТовары.СтавкаНДС КАК СтавкаНДС,
	             |	ЧекТовары.СкидкаНаТовар КАК СкидкаНаТовар,
	             |	ЧекТовары.Цена КАК Цена,
	             |	ЧекТовары.Сумма КАК Сумма,
	             |	ЧекТовары.СуммаНДС КАК СуммаНДС,
	             |	ЧекТовары.СуммаСкидки КАК СуммаСкидки,
	             |	ЧекТовары.СуммаСкидкиСтроки КАК СуммаСкидкиСтроки,
	             |	ЧекТовары.СуммаВсего КАК СуммаВсего,
	             |	ЧекТовары.Количество * ЧекТовары.Коэффициент КАК КоличествоНоменклатуры,
	             |	ЧекТовары.МестоРазмещения КАК МестоРазмещения,
	             |	ЧекТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	             |	ЧекТовары.ИдентификаторТовара КАК ИдентификаторТовара
	             |ИЗ
	             |	Документ.Чек.Товары КАК ЧекТовары
	             |ГДЕ
	             |	ЧекТовары.Ссылка = &Чек
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ЧекТовары.НомерСтроки
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ЧекКодыМаркировки.ИдентификаторТовара КАК ИдентификаторТовара,
	             |	ЧекКодыМаркировки.КодМаркировки КАК КодМаркировки,
	             |	ЧекКодыМаркировки.КодМаркировкиBase64 КАК КодМаркировкиBase64
	             |ИЗ
	             |	Документ.Чек.КодыМаркировки КАК ЧекКодыМаркировки
	             |ГДЕ
	             |	ЧекКодыМаркировки.Ссылка = &Чек";
	Запрос.УстановитьПараметр("Чек",Чек);
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	Выборка=ПакетЗапроса[0].Выбрать();
	СписокКодовМаркировки = ПакетЗапроса[1].Выгрузить();
	СтруктураПоиска = Новый Структура("ИдентификаторТовара");
	//Перебираем строки ТЧ чека
	КоличествоЧеков = 1;
	Пока Выборка.Следующий() Цикл
		//Добавим новую строку в ТЧ товаров
		НоваяСтрока=?(ДокументПродажи, Товары.Добавить(), Возвраты.Добавить());
		НоваяСтрока.Номенклатура=Выборка.Номенклатура;
		НоваяСтрока.Количество=Выборка.Количество;
		НоваяСтрока.ЕдиницаИзмерения=Выборка.ЕдиницаИзмерения;
		НоваяСтрока.Коэффициент=Выборка.Коэффициент;
		НоваяСтрока.Цена=Выборка.Цена;
		НоваяСтрока.Сумма=Выборка.Сумма;
		НоваяСтрока.СтавкаНДС = Выборка.СтавкаНДС;
		НоваяСтрока.СуммаНДС=Выборка.СуммаНДС;
		НоваяСтрока.СуммаСкидки=Выборка.СуммаСкидки+Выборка.СуммаСкидкиСтроки;
		НоваяСтрока.СуммаВсего=Выборка.СуммаВсего;
		НоваяСтрока.МестоРазмещения=Выборка.МестоРазмещения;
		НоваяСтрока.КоличествоЧеков=1;
		НоваяСтрока.ХарактеристикаНоменклатуры=Выборка.ХарактеристикаНоменклатуры;
		НоваяСтрока.ИдентификаторТовара    = Выборка.ИдентификаторТовара;
		
		Если НЕ ДокументПродажи Тогда
			НоваяСтрока.ДатаРеализации=ДатаПродажи;
			НоваяСтрока.НомерФискальногоЧека=НомерФискальногоЧека;
		КонецЕсли;
		
		//Добавим новую строку в ТЧ скидок
		Если НЕ Выборка.СкидкаНаценка.Пустая() тогда
			НоваяСтрока=Скидки.Добавить();
			НоваяСтрока.СкидкаНаценка=Выборка.СкидкаНаценка;
			НоваяСтрока.Сумма=Коэффициент * Выборка.Сумма;
			НоваяСтрока.СуммаСкидки=Коэффициент * Выборка.СуммаСкидки;
			НоваяСтрока.СуммаВсего=Коэффициент * Выборка.СуммаВсего;
			НоваяСтрока.КоличествоЧеков=КоличествоЧеков;
			Если КоличествоЧеков = 1 Тогда КоличествоЧеков = 0; КонецЕсли;
		КонецЕсли;
		Если НЕ Выборка.СкидкаНаТовар.Пустая() тогда
			НоваяСтрока=Скидки.Добавить();
			НоваяСтрока.СкидкаНаценка=Выборка.СкидкаНаТовар;
			НоваяСтрока.Сумма=Коэффициент * Выборка.Сумма;
			НоваяСтрока.СуммаСкидки=Коэффициент * Выборка.СуммаСкидкиСтроки;
			НоваяСтрока.СуммаВсего=Коэффициент * Выборка.СуммаВсего;
			НоваяСтрока.КоличествоЧеков=1;
		КонецЕсли;
		//Добавим новую строку в ТЧ контрагентов
		НоваяСтрока=Контрагенты.Добавить();
		НоваяСтрока.Контрагент             = Выборка.Контрагент;
		НоваяСтрока.Карточка               = Выборка.Карточка;
		НоваяСтрока.ПлатежнаяКарта         = Выборка.ПлатежнаяКарта;
		НоваяСтрока.Сумма                  = Коэффициент * Выборка.Сумма;
		НоваяСтрока.КоличествоНоменклатуры = Коэффициент * Выборка.КоличествоНоменклатуры;
		НоваяСтрока.СуммаСкидки            = Коэффициент * (Выборка.СуммаСкидки+Выборка.СуммаСкидкиСтроки);
		НоваяСтрока.СуммаВсего             = Коэффициент * Выборка.СуммаВсего;
		НоваяСтрока.КоличествоЧеков        = ?(ДокументПродажи, 1, -1);
		
		// Заполним ТЧ Маркировки
		Если ТаблицаКодовМаркировки <> Неопределено Тогда
			СтруктураПоиска.ИдентификаторТовара = Выборка.ИдентификаторТовара;
			НайденныеСтроки = СписокКодовМаркировки.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
				
				НоваяСтрокаМаркировки = ТаблицаКодовМаркировки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаМаркировки, Выборка);
				НоваяСтрокаМаркировки.КодМаркировки = ТекущаяСтрока.КодМаркировки;
				НоваяСтрокаМаркировки.КодМаркировкиBase64 = ТекущаяСтрока.КодМаркировкиBase64;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	// Свойства чека
	Запрос=Новый Запрос;
	Запрос.Текст="	
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Сумма,
	|	ВложенныйЗапрос.СуммаВсего,
	|	ВложенныйЗапрос.СуммаСкидки,
	|	ЗначенияСвойствОбъектов.Значение
	|ИЗ
	|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СУММА(ЧекТовары.Сумма) КАК Сумма,
	|			СУММА(ЧекТовары.СуммаВсего) КАК СуммаВсего,
	|			СУММА(ЧекТовары.СуммаСкидки + ЧекТовары.СуммаСкидкиСтроки) КАК СуммаСкидки,
	|			ЧекТовары.Ссылка КАК Ссылка
	|		ИЗ
	|			Документ.Чек.Товары КАК ЧекТовары
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ЧекТовары.Ссылка) КАК ВложенныйЗапрос
	|		ПО ЗначенияСвойствОбъектов.Объект = ВложенныйЗапрос.Ссылка
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Объект = &Чек";
	
	Запрос.УстановитьПараметр("Чек",Чек);
	Выборка=Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		//Добавим новую строку в ТЧ свойства
		НоваяСтрока				= Свойства.Добавить();
		НоваяСтрока.Касса		= Чек.КассаККМ;
		НоваяСтрока.Свойство	= Выборка.Значение;
		НоваяСтрока.Сумма		= Выборка.Сумма;
		НоваяСтрока.СуммаСкидки	= Выборка.СуммаСкидки;
		НоваяСтрока.СуммаВсего	= Выборка.СуммаВсего;
		НоваяСтрока.КоличествоЧеков=1;
	КонецЦикла;
	
	// Бонусы
	Если (ЗначениеЗаполнено(Чек.КоличествоКНачислению) ИЛИ ЗначениеЗаполнено(Чек.КоличествоКСписанию)) И ЗначениеЗаполнено(Чек.Карточка) Тогда
		Если Бонусы.Найти(Чек.Карточка, "БонуснаяКарта") = Неопределено Тогда
			НоваяСтрока = Бонусы.Добавить();
			НоваяСтрока.БонуснаяКарта                = Чек.Карточка;
			НоваяСтрока.БонуснаяПрограмма            = Чек.Карточка.БонуснаяПрограмма;
			НоваяСтрока.КоличествоНачисленныхБонусов = Чек.КоличествоКНачислению;
			НоваяСтрока.КоличествоСписанныхБонусов   = Чек.КоличествоКСписанию;
		Иначе
			НоваяСтрока = Бонусы.Найти(Чек.Карточка, "БонуснаяКарта");
			НоваяСтрока.БонуснаяКарта                = Чек.Карточка;
			НоваяСтрока.БонуснаяПрограмма            = Чек.Карточка.БонуснаяПрограмма;
			НоваяСтрока.КоличествоНачисленныхБонусов = НоваяСтрока.КоличествоНачисленныхБонусов + Чек.КоличествоКНачислению;
			НоваяСтрока.КоличествоСписанныхБонусов   = НоваяСтрока.КоличествоСписанныхБонусов + Чек.КоличествоКСписанию;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьЧек()

//Добавляет ТЧ документа чека на оплату в ТЧ-и закрытия смены
//
// Параметры:
//  ЧекНаОплату - ДокументСсылка - Документ.ЧекНаОплату.
//
Процедура ЗагрузитьЧекНаОплату(ЧекНаОплату) Экспорт
	//Проверим что чек на оплату проведен
	Если НЕ ЧекНаОплату.Проведен Тогда Возврат; КонецЕсли; 
	ХозОперацияЧека=ЧекНаОплату.ХозОперация;
	
	ДатаПоследнегоДокумента=?(ДатаПоследнегоДокумента<ЧекНаОплату.Дата,ЧекНаОплату.Дата,ДатаПоследнегоДокумента);
	
	ДокументПродажи=?(ХозОперацияЧека=Справочники.ХозОперации.ЧекНаОплату ИЛИ ХозОперацияЧека=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат,Истина,Ложь);
	Если ДокументПродажи Тогда
		Если ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет ИЛИ
			 ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ПолученоНал=ПолученоНал+(ЧекНаОплату.СуммаДокумента-ЧекНаОплату.ПолученоБезнал);
		КонецЕсли;
		Если ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет ИЛИ
			 ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ПолученоБезнал=ПолученоБезнал+ЧекНаОплату.ПолученоБезнал;
		КонецЕсли;
	Иначе
		Если ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет ИЛИ
			 ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ВозвратНал=ВозвратНал+(ЧекНаОплату.СуммаДокумента-ЧекНаОплату.ПолученоБезнал);
		КонецЕсли;
		Если ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет ИЛИ
			 ЧекНаОплату.ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата Тогда
			 ВозвратБезнал=ВозвратБезнал+ЧекНаОплату.ПолученоБезнал;
		КонецЕсли;
	КонецЕсли; 
	
	//Получим табличную часть оплат документа чек на оплату
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
				 |	ЧекОплаты.Ссылка.КассаККМ,
				 |	ЧекОплаты.ТипОплаты,
				 |	ЧекОплаты.ТипПлатежнойКарты,
				 |	ЧекОплаты.Карточка,
				 |	ЧекОплаты.Контрагент,
				 |	ЧекОплаты.ДоговорВзаиморасчетов,
				 |	ЧекОплаты.Сумма КАК Сумма
				 |ИЗ
				 |	Документ.ЧекНаОплату.Оплаты КАК ЧекОплаты
				 |ГДЕ
				 |	ЧекОплаты.Ссылка = &ЧекНаОплату";
	Запрос.УстановитьПараметр("ЧекНаОплату",ЧекНаОплату);
	Запрос.УстановитьПараметр("ХозОперацияЧек",Справочники.ХозОперации.ЧекНаОплату);
	Выборка=Запрос.Выполнить().Выбрать();
	//Перебираем строки ТЧ чека на оплату
	Пока Выборка.Следующий() Цикл
		//Добавим новую строку в ТЧ оплаты
		НоваяСтрока						  = ?(ДокументПродажи, Оплаты.Добавить(), ВозвратОплаты.Добавить());
		НоваяСтрока.Касса 				  = Выборка.КассаККМ;
		НоваяСтрока.ТипОплаты 			  = Выборка.ТипОплаты;
		НоваяСтрока.ТипПлатежнойКарты	  = Выборка.ТипПлатежнойКарты;
		НоваяСтрока.Карточка 			  = Выборка.Карточка;
		НоваяСтрока.Контрагент 			  = Выборка.Контрагент;
		НоваяСтрока.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
		НоваяСтрока.СуммаОплата			  = Выборка.Сумма;
		НоваяСтрока.КоличествоЧековОплата = 1;
	КонецЦикла;
	
КонецПроцедуры // ЗагрузитьЧекНаОплату()

//Сворачивает табличные части документа
//
Процедура СвернутьЧеки(ТаблицаКодовМаркировки = Неопределено) Экспорт
	#Если Клиент Тогда
	Состояние("Свертка чеков ...");
	#КонецЕсли

	СписокИдентификаторов = Товары.Выгрузить(, "ИдентификаторТовара,Номенклатура,ЕдиницаИзмерения,Коэффициент,МестоРазмещения,ХарактеристикаНоменклатуры");
	СтуркутраПоиска = Новый Структура("Номенклатура,ЕдиницаИзмерения,Коэффициент,МестоРазмещения,ХарактеристикаНоменклатуры");
	
	Товары.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,МестоРазмещения,ХарактеристикаНоменклатуры,Цена,СтавкаНДС","КоличествоПродано,КоличествоВозврат,Количество,Сумма,СуммаНДС,СуммаСкидки,СуммаВсего,КоличествоЧеков");
	Для каждого СтрокаТоваров Из Товары Цикл
		СтрокаТоваров.ИдентификаторТовара = Новый УникальныйИдентификатор;
		СтрокаТоваров.Цена=?(СтрокаТоваров.Количество=0,0,СтрокаТоваров.Сумма/СтрокаТоваров.Количество);
		
		// Заполним коды маркировки
		Если НЕ ТаблицаКодовМаркировки = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтуркутраПоиска, СтрокаТоваров);
			НайденныеСтроки = СписокИдентификаторов.НайтиСтроки(СтуркутраПоиска);
			Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
				НайденнаяМаркировка = ТаблицаКодовМаркировки.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекущаяСтрока.ИдентификаторТовара));
				
				Для Каждого ТекущаяМаркировка Из НайденнаяМаркировка Цикл
					НоваяСтрока = КодыМаркировки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяМаркировка);
					НоваяСтрока.ИдентификаторТовара = СтрокаТоваров.ИдентификаторТовара;
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла; 
	
	Товары.Сортировать("Номенклатура Возр");
	
	СписокИдентификаторов = Возвраты.Выгрузить(, "ИдентификаторТовара,Номенклатура,ЕдиницаИзмерения,Коэффициент,МестоРазмещения,ХарактеристикаНоменклатуры");
	Возвраты.Свернуть("Номенклатура,ЕдиницаИзмерения,Коэффициент,МестоРазмещения,ХарактеристикаНоменклатуры,Цена,СтавкаНДС,ДатаРеализации,НомерФискальногоЧека","Количество,Сумма,СуммаНДС,СуммаСкидки,СуммаВсего,КоличествоЧеков");
	Для каждого СтрокаТоваров Из Возвраты Цикл
		СтрокаТоваров.ИдентификаторТовара = Новый УникальныйИдентификатор;
		СтрокаТоваров.Цена=?(СтрокаТоваров.Количество=0,0,СтрокаТоваров.Сумма/СтрокаТоваров.Количество);
		
		// Заполним коды маркировки
		Если НЕ ТаблицаКодовМаркировки = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтуркутраПоиска, СтрокаТоваров);
			НайденныеСтроки = СписокИдентификаторов.НайтиСтроки(СтуркутраПоиска);
			Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
				НайденнаяМаркировка = ТаблицаКодовМаркировки.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекущаяСтрока.ИдентификаторТовара));
				
				Для Каждого ТекущаяМаркировка Из НайденнаяМаркировка Цикл
					НоваяСтрока = КодыМаркировки.Добавить();
					НоваяСтрока.ИдентификаторТовара = СтрокаТоваров.ИдентификаторТовара;
					НоваяСтрока.КодМаркировки = ТекущаяМаркировка.КодМаркировки;
					НоваяСтрока.КодМаркировкиBase64 = ТекущаяМаркировка.КодМаркировкиBase64;
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла; 
	Возвраты.Сортировать("Номенклатура Возр");
	Скидки.Свернуть("СкидкаНаценка","Сумма,СуммаСкидки,СуммаВсего,КоличествоЧеков");
	Скидки.Сортировать("СкидкаНаценка Возр");
	Контрагенты.Свернуть("Контрагент,ДоговорВзаиморасчетов,Карточка,ПлатежнаяКарта","Сумма,СуммаСкидки,СуммаВсего,КоличествоЧеков, КоличествоНоменклатуры");
	Контрагенты.Сортировать("Контрагент Возр,ДоговорВзаиморасчетов Возр,Карточка Возр,ПлатежнаяКарта Возр");
	Оплаты.Свернуть("Касса,ТипОплаты,ТипПлатежнойКарты,Карточка,Контрагент,ДоговорВзаиморасчетов","Сумма,СуммаВозврат,КоличествоЧеков,СуммаОплата,СуммаВозвратОплата,КоличествоЧековОплата");
	Оплаты.Сортировать("Касса Возр,ТипОплаты Возр,ТипПлатежнойКарты Возр,Карточка Возр,ДоговорВзаиморасчетов Возр");
	ВозвратОплаты.Свернуть("Касса,ТипОплаты,ТипПлатежнойКарты,Карточка,Контрагент,ДоговорВзаиморасчетов","Сумма,КоличествоЧеков,СуммаОплата,КоличествоЧековОплата");
	ВозвратОплаты.Сортировать("Касса Возр,ТипОплаты Возр,ТипПлатежнойКарты Возр,Карточка Возр,ДоговорВзаиморасчетов Возр");
	Свойства.Свернуть("Касса, Свойство","Сумма, СуммаСкидки, СуммаВсего,КоличествоЧеков");
	Свойства.Сортировать("Касса Возр, Свойство Возр");
	СуммаДокумента=РассчитатьСуммуВсего();
КонецПроцедуры // СвернутьЧеки()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.Ссылка КАК ДокументПродажи,
	|	Док.КассаККМ КАК КассаККМ,
	|	ЕСТЬNULL(ДокТовары.СуммаВсего,0) КАК СуммаВсего,
	|	Док.ПолученоНал КАК ПолученоНал,
	|	Док.ПолученоБезнал КАК ПолученоБезнал,
	|	Док.ВозвратНал КАК ВозвратНал,
	|	Док.ВозвратБезнал КАК ВозвратБезнал,
	|	ЕСТЬNULL(ДокТовары.СуммаВсего,0) КАК СуммаВсегоВозвраты
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ СУММА(СуммаВсего) КАК СуммаВсего ИЗ Документ."+Метаданные().Имя+".Товары ГДЕ Ссылка=&Ссылка) КАК ДокТовары
	|	ПО Истина
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ СУММА(СуммаВсего) КАК СуммаВсего ИЗ Документ."+Метаданные().Имя+".Возвраты ГДЕ Ссылка=&Ссылка) КАК ДокВозвраты
	|	ПО Истина
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента = ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
		НаборЗаписейДоходыИРасходы.Доход=ШапкаДокумента.ПолученоНал+ШапкаДокумента.ПолученоБезнал-ШапкаДокумента.ВозвратНал-ШапкаДокумента.ВозвратБезнал;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		Возврат НЕ Отказ;
	КонецЕсли;
	
	//Сформируем структуру ТЗ для записи товаров по складам
	ТоварыПоСкладу=Новый ТаблицаЗначений;
	ТоварыПоСкладу.Колонки.Добавить("Номенклатура");
	ТоварыПоСкладу.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТоварыПоСкладу.Колонки.Добавить("Количество");
	ТоварыПоСкладам=Новый Соответствие;
	ТоварыПоСкладуВозвраты=Новый ТаблицаЗначений;
	ТоварыПоСкладуВозвраты.Колонки.Добавить("Номенклатура");
	ТоварыПоСкладуВозвраты.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТоварыПоСкладуВозвраты.Колонки.Добавить("Количество");
	ТоварыПоСкладуВозвраты.Колонки.Добавить("ДокументПродажи");
	ТоварыПоСкладуВозвраты.Колонки.Добавить("Себестоимость");
	ТоварыПоСкладуВозвраты.Колонки.Добавить("СтавкаНДС");
	ТоварыПоСкладамВозвраты = Новый Соответствие;
	
	ЗапросТовары=Новый Запрос("ВЫБРАТЬ
	                          |	ВЫБОР
	                          |		КОГДА &АктОРеализации
	                          |			ТОГДА &СкладКомпании
	                          |		ИНАЧЕ ЗакрытиеСменыТовары.МестоРазмещения
	                          |	КОНЕЦ КАК СкладКомпании,
	                          |	ЗакрытиеСменыТовары.Номенклатура КАК Номенклатура,
	                          |	ЗакрытиеСменыТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                          |	СУММА(ЗакрытиеСменыТовары.Количество * ЗакрытиеСменыТовары.Коэффициент) КАК Количество
	                          |ИЗ
	                          |	Документ.ЗакрытиеСмены.Товары КАК ЗакрытиеСменыТовары
	                          |ГДЕ
	                          |	ЗакрытиеСменыТовары.Ссылка = &Ссылка
	                          |	И ЗакрытиеСменыТовары.Номенклатура.ВидНоменклатуры <> &ВидНоменклатурыУслуга
	                          |
	                          |СГРУППИРОВАТЬ ПО
	                          |	ВЫБОР
	                          |		КОГДА &АктОРеализации
	                          |			ТОГДА &СкладКомпании
	                          |		ИНАЧЕ ЗакрытиеСменыТовары.МестоРазмещения
	                          |	КОНЕЦ,
	                          |	ЗакрытиеСменыТовары.Номенклатура,
	                          |	ЗакрытиеСменыТовары.ХарактеристикаНоменклатуры
	                          |ИТОГИ ПО
	                          |	СкладКомпании
	                          |;
	                          |
	                          |////////////////////////////////////////////////////////////////////////////////
	                          |ВЫБРАТЬ
	                          |	ВЫБОР
	                          |		КОГДА &АктОРеализации
	                          |			ТОГДА &СкладКомпании
	                          |		ИНАЧЕ ЗакрытиеСменыВозвраты.МестоРазмещения
	                          |	КОНЕЦ КАК СкладКомпании,
	                          |	ЗакрытиеСменыВозвраты.Номенклатура КАК Номенклатура,
	                          |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                          |	СУММА(ЗакрытиеСменыВозвраты.Количество * ЗакрытиеСменыВозвраты.Коэффициент) КАК Количество,
	                          |	ЗакрытиеСменыВозвраты.Ссылка КАК ДокументПродажи,
	                          |	&Партия КАК Партия,
	                          |	СУММА(ВЫБОР
	                          |			КОГДА ЗакрытиеСменыВозвраты.Количество * ЗакрытиеСменыВозвраты.Коэффициент = 0
	                          |				ТОГДА 0
	                          |			ИНАЧЕ ВЫРАЗИТЬ(ЗакрытиеСменыВозвраты.СуммаВсего
	                          |				/ (ЗакрытиеСменыВозвраты.Количество * ЗакрытиеСменыВозвраты.Коэффициент) КАК ЧИСЛО(15, 2))
	                          |		КОНЕЦ) КАК Себестоимость,
	                          |	МАКСИМУМ(ЗакрытиеСменыВозвраты.СтавкаНДС) КАК СтавкаНДС
	                          |ИЗ
	                          |	Документ.ЗакрытиеСмены.Возвраты КАК ЗакрытиеСменыВозвраты
	                          |ГДЕ
	                          |	ЗакрытиеСменыВозвраты.Ссылка = &Ссылка
	                          |	И ЗакрытиеСменыВозвраты.Номенклатура.ВидНоменклатуры <> &ВидНоменклатурыУслуга
	                          |
	                          |СГРУППИРОВАТЬ ПО
	                          |	ВЫБОР
	                          |		КОГДА &АктОРеализации
	                          |			ТОГДА &СкладКомпании
	                          |		ИНАЧЕ ЗакрытиеСменыВозвраты.МестоРазмещения
	                          |	КОНЕЦ,
	                          |	ЗакрытиеСменыВозвраты.Номенклатура,
	                          |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры,
	                          |	ЗакрытиеСменыВозвраты.Ссылка
	                          |ИТОГИ ПО
	                          |	СкладКомпании");
	ЗапросТовары.УстановитьПараметр("АктОРеализации",(ШапкаДокумента.ХозОперация=Справочники.ХозОперации.АктОРеализации));
	ЗапросТовары.УстановитьПараметр("СкладКомпании",ШапкаДокумента.СкладКомпании);
	ЗапросТовары.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	ЗапросТовары.УстановитьПараметр("ВидНоменклатурыУслуга",Перечисления.ВидыНоменклатуры.Услуга);
	ЗапросТовары.УстановитьПараметр("Партия",  Константы.ПартияТоваровОтрицательныхОстатков.Получить());
	ПакетЗапроса = ЗапросТовары.ВыполнитьПакет();
	ВыборкаСклады=ПакетЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"СкладКомпании");
	Пока ВыборкаСклады.Следующий() Цикл
		ТоварыПоТекущемуСкладу=ТоварыПоСкладу.Скопировать();
		ВыборкаТовары=ВыборкаСклады.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			СтрокаТоваровПоСкладу=ТоварыПоТекущемуСкладу.Добавить();
			СтрокаТоваровПоСкладу.Номенклатура=ВыборкаТовары.Номенклатура;
			СтрокаТоваровПоСкладу.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры;
			СтрокаТоваровПоСкладу.Количество=ВыборкаТовары.Количество;
		КонецЦикла;
		ТоварыПоСкладам.Вставить(ВыборкаСклады.СкладКомпании,ТоварыПоТекущемуСкладу);
	КонецЦикла;
	
	ВыборкаСкладыВозврат = ПакетЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"СкладКомпании");
	
	Пока ВыборкаСкладыВозврат.Следующий() Цикл
		ТоварыПоТекущемуСкладу=ТоварыПоСкладуВозвраты.Скопировать();
		ВыборкаТовары=ВыборкаСкладыВозврат.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			СтрокаТоваровПоСкладу=ТоварыПоТекущемуСкладу.Добавить();
			СтрокаТоваровПоСкладу.Номенклатура=ВыборкаТовары.Номенклатура;
			СтрокаТоваровПоСкладу.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры;
			СтрокаТоваровПоСкладу.Количество=ВыборкаТовары.Количество;
			СтрокаТоваровПоСкладу.Себестоимость=ВыборкаТовары.Себестоимость;
			СтрокаТоваровПоСкладу.СтавкаНДС=ВыборкаТовары.СтавкаНДС;
		КонецЦикла;
		ТоварыПоСкладамВозвраты.Вставить(ВыборкаСкладыВозврат.СкладКомпании,ТоварыПоТекущемуСкладу);
	КонецЦикла;
	
	// проведем по партиям товаров
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	
	// проведем по партиям товаров возврата
	Для каждого Склад Из ТоварыПоСкладам Цикл
		НаборЗаписейПартии.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейПартии.ПоБазовомуКоличеству      = Ложь; //По фактическому количеству
		НаборЗаписейПартии.СтатусПартии              = Неопределено; //Продаем все товары
		НаборЗаписейПартии.ЗаписыватьДвижения        = Истина;
		НаборЗаписейПартии.СкладКомпании             = Склад.Ключ;
		НаборЗаписейПартии.ШапкаДокумента            = ШапкаДокумента;
		НаборЗаписейПартии.РезультатЗапросаПоТоварам = Склад.Значение;
		НаборЗаписейПартии.ТаблицаВозвратов          = ТоварыПоСкладамВозвраты.Получить(Склад.Ключ);
		Отказ = НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	КонецЦикла;
	
	ПартияОтрицательныхОстатков = Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	Для каждого Склад Из ТоварыПоСкладамВозвраты Цикл
		
		Если Склад.Значение.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Склад.Значение.Колонки.Добавить("Партия");
		
		Для Каждого ТекущаяСтрока Из Склад.Значение Цикл
			ТекущаяСтрока.Партия = ПартияОтрицательныхОстатков;
		КонецЦикла;
		
		НаборЗаписейПартии.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейПартии.ПоБазовомуКоличеству      = Ложь; //По фактическому количеству
		НаборЗаписейПартии.СтатусПартии              = Неопределено; //Продаем все товары
		НаборЗаписейПартии.Сторно                    = Истина;
		НаборЗаписейПартии.ИмяРеквизитаДокумент      = "Ссылка";
		НаборЗаписейПартии.СкладКомпании             = Склад.Ключ;
		НаборЗаписейПартии.ШапкаДокумента            = ШапкаДокумента;
		НаборЗаписейПартии.РезультатЗапросаПоТоварам = Склад.Значение;
		Отказ = НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	КонецЦикла;
	
	// продажи
	Если НЕ Отказ Тогда
		НаборЗаписейПродажи=Движения.Продажи;
		НаборЗаписейПродажи.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейПродажи.СкладКомпании             = ?(ХозОперация=Справочники.ХозОперации.АктОРеализации,СкладКомпании,Неопределено);
		НаборЗаписейПродажи.ДокументПродажи			  = ШапкаДокумента.ДокументПродажи;
		НаборЗаписейПродажи.Сторно                    = Ложь;
		НаборЗаписейПродажи.РезультатЗапросаПоТоварам = ЗапросПоПроданнымТоварам(ШапкаДокумента.Ссылка);
		НаборЗаписейПродажи.Покупатель                = Неопределено;
		НаборЗаписейПродажи.ДоговорВзаиморасчетов     = Неопределено;
		НаборЗаписейПродажи.ПодразделениеКомпании     = ШапкаДокумента.ПодразделениеКомпании;
		НаборЗаписейПродажи.ШапкаДокумента            = ШапкаДокумента;
		НаборЗаписейПродажи.Комиссия                  = Ложь;
		НаборЗаписейПродажи.ПоБазовомуКоличеству      = Ложь;
		НаборЗаписейПродажи.РезультатЗапросаПоТоварам = НаборЗаписейПродажи.РезультатЗапросаПоТоварам.Выгрузить();
		ПустыеСтрокиТабличнойЧасти=НаборЗаписейПродажи.РезультатЗапросаПоТоварам.НайтиСтроки(Новый Структура("Количество",0));
		Для Каждого ПустаяСтрока Из ПустыеСтрокиТабличнойЧасти Цикл
			НаборЗаписейПродажи.РезультатЗапросаПоТоварам.Удалить(ПустаяСтрока);
		КонецЦикла;
		Отказ = НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			МассивСкладов = Новый Массив;
			Для Каждого Склад Из ТоварыПоСкладам Цикл
				МассивСкладов.Добавить(Склад.Ключ);
			КонецЦикла;
			НаборЗаписейГраницыПартий.СкладКомпании		= МассивСкладов;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;

	// спишем реализованные комиссионные товары
	НаборЗаписейРеализованныеТовары = Движения.РеализованныеТовары;
	НаборЗаписейРеализованныеТовары.ДокументОбъект       = ЭтотОбъект;
	НаборЗаписейРеализованныеТовары.ШапкаДокумента       = ШапкаДокумента;
	НаборЗаписейРеализованныеТовары.ПоБазовомуКоличеству = Ложь;
	Отказ = НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
	
	// возврат продажи
	Если НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗакрытиеСменыВозвраты.Номенклатура,
		               |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры,
		               |	ЗакрытиеСменыВозвраты.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		               |	ВЫБОР
		               |		КОГДА &АктОРеализации
		               |			ТОГДА &СкладКомпании
		               |		ИНАЧЕ ЗакрытиеСменыВозвраты.МестоРазмещения
		               |	КОНЕЦ КАК СкладКомпании,
		               |	СУММА(ЗакрытиеСменыВозвраты.Количество * ЗакрытиеСменыВозвраты.Коэффициент) КАК Количество,
		               |	СУММА(ЗакрытиеСменыВозвраты.СуммаНДС) КАК СуммаНДС,
		               |	СУММА(ЗакрытиеСменыВозвраты.СуммаСкидки) КАК СуммаСкидки,
		               |	СУММА(ЗакрытиеСменыВозвраты.СуммаВсего) КАК Сумма,
		               |	ЗакрытиеСменыВозвраты.СтавкаНДС
		               |ИЗ
		               |	Документ.ЗакрытиеСмены.Возвраты КАК ЗакрытиеСменыВозвраты
		               |ГДЕ
		               |	ЗакрытиеСменыВозвраты.Ссылка = &Ссылка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВЫБОР
		               |		КОГДА &АктОРеализации
		               |			ТОГДА &СкладКомпании
		               |		ИНАЧЕ ЗакрытиеСменыВозвраты.МестоРазмещения
		               |	КОНЕЦ,
		               |	ЗакрытиеСменыВозвраты.Номенклатура,
		               |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры,
		               |	ЗакрытиеСменыВозвраты.Номенклатура.ВидНоменклатуры,
		               |	ЗакрытиеСменыВозвраты.СтавкаНДС";
		Запрос.УстановитьПараметр("Ссылка",     ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("АктОРеализации",(ШапкаДокумента.ХозОперация=Справочники.ХозОперации.АктОРеализации));
		Запрос.УстановитьПараметр("СкладКомпании",ШапкаДокумента.СкладКомпании);
		
		ТаблицаПартий = НаборЗаписейПартии.Выгрузить();
		СтрокиУдалить = Новый Массив;
		
		РезультатЗапросаПоТоварам = Запрос.Выполнить().Выгрузить();
		СтруктураОтбора = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
		
		Для Каждого ТекущаяСтрока Из РезультатЗапросаПоТоварам Цикл
			ТаблицаВозвратов = ТоварыПоСкладамВозвраты.Получить(ТекущаяСтрока.СкладКомпании);
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТекущаяСтрока);
			
			Если ТаблицаВозвратов = Неопределено Тогда
				СтрокиУдалить.Добавить(ТекущаяСтрока);
				Продолжить;
			КонецЕсли;
			
			НайденныеСтроки = ТаблицаВозвратов.НайтиСтроки(СтруктураОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				СтрокиУдалить.Добавить(ТекущаяСтрока);
				Продолжить;
			КонецЕсли;
			КоличествоВозврата = 0;
			Для Каждого ТекущийВозврат Из НайденныеСтроки Цикл
				КоличествоВозврата = КоличествоВозврата + ТекущийВозврат.Количество;
			КонецЦикла;
			Если ТекущаяСтрока.Количество <> КоличествоВозврата Тогда
				ТекущаяСтрока.СуммаНДС = Окр(ТекущаяСтрока.СуммаНДС/ТекущаяСтрока.Количество * КоличествоВозврата, 2);
				ТекущаяСтрока.СуммаСкидки = Окр(ТекущаяСтрока.СуммаСкидки/ТекущаяСтрока.Количество * КоличествоВозврата, 2);
				ТекущаяСтрока.Сумма = Окр(ТекущаяСтрока.Сумма/ТекущаяСтрока.Количество * КоличествоВозврата, 2);
				ТекущаяСтрока.Количество = КоличествоВозврата;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаУдалить Из СтрокиУдалить Цикл
			РезультатЗапросаПоТоварам.Удалить(СтрокаУдалить);
		КонецЦикла;
		
		СтрокиУдалить = Новый Массив;
		
		Для Каждого ТекущаяСтрока Из ТаблицаПартий Цикл
			Если ЗначениеЗаполнено(ТекущаяСтрока.ДокументПродажи) Тогда
				СтрокиУдалить.Добавить(ТекущаяСтрока);
			КонецЕсли;
			ТекущаяСтрока.ДокументПродажи = ШапкаДокумента.Ссылка;
		КонецЦикла;
		
		Для Каждого СтрокаУдалить Из СтрокиУдалить Цикл
			ТаблицаПартий.Удалить(СтрокаУдалить);
		КонецЦикла;
		
		НаборЗаписейПродажи=Движения.Продажи;
		НаборЗаписейПродажи.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейПродажи.СкладКомпании             = ?(ХозОперация=Справочники.ХозОперации.АктОРеализации,СкладКомпании,Неопределено);
		НаборЗаписейПродажи.ДокументПродажи			  = ШапкаДокумента.Ссылка;
		НаборЗаписейПродажи.Сторно                    = Истина;
		НаборЗаписейПродажи.РезультатЗапросаПоПартиям = ТаблицаПартий;
		НаборЗаписейПродажи.Покупатель                = Неопределено;
		НаборЗаписейПродажи.ДоговорВзаиморасчетов     = Неопределено;
		НаборЗаписейПродажи.ПодразделениеКомпании     = ШапкаДокумента.ПодразделениеКомпании;
		НаборЗаписейПродажи.ШапкаДокумента            = ШапкаДокумента;
		НаборЗаписейПродажи.Комиссия                  = Ложь;
		НаборЗаписейПродажи.ПоБазовомуКоличеству      = Ложь;
		НаборЗаписейПродажи.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
		ПустыеСтрокиТабличнойЧасти=НаборЗаписейПродажи.РезультатЗапросаПоТоварам.НайтиСтроки(Новый Структура("Количество",0));
		Для Каждого ПустаяСтрока Из ПустыеСтрокиТабличнойЧасти Цикл
			НаборЗаписейПродажи.РезультатЗапросаПоТоварам.Удалить(ПустаяСтрока);
		КонецЦикла;
		Отказ = НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	//Доходы и расходы.
	
	// Подготовим таблицу движений в разрезе подразделений только собственных списанных партий.
	ТаблицаСписанийПартий = Движения.ПартииТоваровКомпании.Выгрузить();
	ТаблицаСписанийПартий.Свернуть("СкладКомпании, СтатусПартии","СуммаУпр");
	
	СтруктураОтбора      = Новый Структура("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	МассивНайденныхСтрок = ТаблицаСписанийПартий.НайтиСтроки(СтруктураОтбора);
	Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
		ТаблицаСписанийПартий.Удалить(МассивНайденныхСтрок[Сч]);
	КонецЦикла;
	
	ТаблицаСписанийПартий.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));	
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартий Цикл
		Если БалансВедетсяПоПодразделениям Тогда
			СтрокаСписания.Подразделение = СтрокаСписания.СкладКомпании.Подразделение;
		Иначе
			СтрокаСписания.Подразделение = ШапкаДокумента.ПодразделениеКомпании;
		КонецЕсли;
	КонецЦикла;	
	ТаблицаСписанийПартий.Свернуть("Подразделение", "СуммаУпр");
	
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартий Цикл
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСписания.Подразделение;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.Расход                 = СтрокаСписания.СуммаУпр;	
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЦикла;
	
	// Подготовим таблицу движений в разрезе подразделений комиссионных товаров.
	ТаблицаСписанийПартийРеализованных = Движения.РеализованныеТовары.Выгрузить();
	ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов","СуммаУпр");
	
	ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
		Если БалансВедетсяПоПодразделениям Тогда
			СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
		Иначе
			СтрокаСписания.Подразделение = ШапкаДокумента.ПодразделениеКомпании;
		КонецЕсли;
	КонецЦикла;	
	ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение", "СуммаУпр");
	
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСписания.Подразделение;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.Расход                 = СтрокаСписания.СуммаУпр;	
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЦикла;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Функция получения результата запроса для формирования проданной номенклатуры в закрытой смене.
//
Функция ЗапросПоПроданнымТоварам(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗакрытиеСменыТовары.Номенклатура КАК Номенклатура,
	               |	ЗакрытиеСменыТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗакрытиеСменыТовары.Номенклатура.ВидНоменклатуры КАК НоменклатураВидНоменклатуры,
	               |	ЗакрытиеСменыТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	               |	ЗакрытиеСменыТовары.МестоРазмещения КАК МестоРазмещения,
	               |	СУММА(ЗакрытиеСменыТовары.Количество * ЗакрытиеСменыТовары.Коэффициент) КАК Количество,
	               |	СУММА(ЗакрытиеСменыТовары.СуммаВсего) КАК СуммаВсего,
	               |	СУММА(ЗакрытиеСменыТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ЗакрытиеСменыТовары.СуммаСкидки) КАК СуммаСкидки
	               |ПОМЕСТИТЬ ТаблицаТоваров
	               |ИЗ
	               |	Документ.ЗакрытиеСмены.Товары КАК ЗакрытиеСменыТовары
	               |ГДЕ
	               |	ЗакрытиеСменыТовары.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗакрытиеСменыТовары.Номенклатура,
	               |	ЗакрытиеСменыТовары.ХарактеристикаНоменклатуры,
	               |	ЗакрытиеСменыТовары.Номенклатура.ВидНоменклатуры,
	               |	ЗакрытиеСменыТовары.МестоРазмещения,
	               |	ЗакрытиеСменыТовары.Номенклатура.СтавкаНДС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗакрытиеСменыВозвраты.Номенклатура КАК Номенклатура,
	               |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗакрытиеСменыВозвраты.МестоРазмещения КАК МестоРазмещения,
	               |	СУММА(ЗакрытиеСменыВозвраты.СуммаВсего) КАК СуммаВсего,
	               |	СУММА(ЗакрытиеСменыВозвраты.СуммаСкидки) КАК СуммаСкидки,
	               |	СУММА(ЗакрытиеСменыВозвраты.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ЗакрытиеСменыВозвраты.Количество * ЗакрытиеСменыВозвраты.Коэффициент) КАК Количество
	               |ПОМЕСТИТЬ Возвраты
	               |ИЗ
	               |	Документ.ЗакрытиеСмены.Возвраты КАК ЗакрытиеСменыВозвраты
	               |ГДЕ
	               |	ЗакрытиеСменыВозвраты.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗакрытиеСменыВозвраты.Номенклатура,
	               |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры,
	               |	ЗакрытиеСменыВозвраты.МестоРазмещения
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	               |	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаТоваров.НоменклатураВидНоменклатуры КАК ВидНоменклатуры,
	               |	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	               |	ТаблицаТоваров.МестоРазмещения КАК СкладКомпании,
	               |	ТаблицаТоваров.Количество - ЕСТЬNULL(Возвраты.Количество, 0) КАК Количество,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(Возвраты.СуммаВсего, 0) > ТаблицаТоваров.СуммаВсего
	               |			ТОГДА 0
	               |		ИНАЧЕ ТаблицаТоваров.СуммаВсего - ЕСТЬNULL(Возвраты.СуммаВсего, 0)
	               |	КОНЕЦ КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(Возвраты.СуммаНДС, 0) > ТаблицаТоваров.СуммаНДС
	               |			ТОГДА 0
	               |		ИНАЧЕ ТаблицаТоваров.СуммаНДС - ЕСТЬNULL(Возвраты.СуммаНДС, 0)
	               |	КОНЕЦ КАК СуммаНДС,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(Возвраты.СуммаСкидки, 0) > ТаблицаТоваров.СуммаСкидки
	               |			ТОГДА 0
	               |		ИНАЧЕ ТаблицаТоваров.СуммаСкидки - ЕСТЬNULL(Возвраты.СуммаСкидки, 0)
	               |	КОНЕЦ КАК СуммаСкидки
	               |ИЗ
	               |	ТаблицаТоваров КАК ТаблицаТоваров
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Возвраты КАК Возвраты
	               |		ПО ТаблицаТоваров.Номенклатура = Возвраты.Номенклатура
	               |			И ТаблицаТоваров.ХарактеристикаНоменклатуры = Возвраты.ХарактеристикаНоменклатуры
	               |			И ТаблицаТоваров.МестоРазмещения = Возвраты.МестоРазмещения
	               |ГДЕ
	               |	ТаблицаТоваров.Количество - ЕСТЬNULL(Возвраты.Количество, 0) > 0";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Возврат Запрос.Выполнить();
	
КонецФункции // ЗапросПоПроданнымТоварам()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ЗакрытиеСмены - Документ, на основании которого произошла операция с товаром
//  ДвиженияПоГТД	 - ТаблицаЗначений - Движения документа по регистру ГТД партии товаров компании
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Найдем РНПТ
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ГТДПартийТоваровКомпании.Количество > 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РозничнаяПродажа)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратРозница)
	|	КОНЕЦ КАК КодОперации,
	|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД КАК ГТД,
	|	СУММА(ГТДПартийТоваровКомпании.Количество * ВЫБОР
	|			КОГДА ГТДПартийТоваровКомпании.Количество < 0
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Количество,
	|	ВЫБОР
	|		КОГДА ГТДПартийТоваровКомпании.Количество > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПродажа,
	|	ГТДПартийТоваровКомпании.Партия КАК Партия
	|ПОМЕСТИТЬ РНПТДокумента
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
	|ГДЕ
	|	ГТДПартийТоваровКомпании.Регистратор = &Регистратор
	|	И ГТДПартийТоваровКомпании.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпании.Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД,
	|	ГТДПартийТоваровКомпании.Партия,
	|	ВЫБОР
	|		КОГДА ГТДПартийТоваровКомпании.Количество > 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РозничнаяПродажа)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратРозница)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ГТДПартийТоваровКомпании.Количество > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПартииТоваровКомпании.Количество > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРасход,
	|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ПартииТоваровКомпании.Партия КАК Партия,
	|	СУММА(ПартииТоваровКомпании.Количество * ВЫБОР
	|			КОГДА ПартииТоваровКомпании.Количество < 0
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Количество,
	|	СУММА((ПартииТоваровКомпании.Сумма - ПартииТоваровКомпании.СуммаНДС) * ВЫБОР
	|			КОГДА ПартииТоваровКомпании.Количество < 0
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК СуммаБезНДС
	|ПОМЕСТИТЬ СуммаНоменклатуры
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ПартииТоваровКомпании.Количество > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПартииТоваровКомпании.Номенклатура,
	|	ПартииТоваровКомпании.Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РНПТДокумента.КодОперации КАК КодОперации,
	|	РНПТДокумента.Номенклатура КАК Номенклатура,
	|	РНПТДокумента.ГТД КАК РНПТ,
	|	СУММА(РНПТДокумента.Количество) КАК КоличествоПрослеживаемости,
	|	СУММА(ЕСТЬNULL(СуммаНоменклатуры.Количество, 0)) КАК КоличествоПартии,
	|	СУММА(ЕСТЬNULL(СуммаНоменклатуры.СуммаБезНДС, 0)) КАК СуммаПартии
	|ИЗ
	|	РНПТДокумента КАК РНПТДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммаНоменклатуры КАК СуммаНоменклатуры
	|		ПО РНПТДокумента.Номенклатура = СуммаНоменклатуры.Номенклатура
	|			И РНПТДокумента.ЭтоПродажа = СуммаНоменклатуры.ЭтоРасход
	|			И РНПТДокумента.Партия = СуммаНоменклатуры.Партия
	|
	|СГРУППИРОВАТЬ ПО
	|	РНПТДокумента.КодОперации,
	|	РНПТДокумента.Номенклатура,
	|	РНПТДокумента.ГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗакрытиеСмены.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ЗакрытиеСмены.Дата, КВАРТАЛ) КАК ПериодОтчета,
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗакрытиеСмены.Ссылка КАК Документ,
	|	ЗакрытиеСмены.Дата КАК ДатаДокумента,
	|	ЗакрытиеСмены.Номер КАК НомерДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента
	|ИЗ
	|	Документ.ЗакрытиеСмены КАК ЗакрытиеСмены
	|ГДЕ
	|	ЗакрытиеСмены.Ссылка = &Регистратор";
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	Если ПакетЗапросов[2].Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ВыборкаНоменклатуры = ПакетЗапросов[2].Выбрать();
	ШапкаДокумента = ПакетЗапросов[3].Выбрать();
	ШапкаДокумента.Следующий();
	НомерДокумента = дкПолучитьНомерДляПечати(ШапкаДокумента.Документ);
	
	Пока ВыборкаНоменклатуры.Следующий() Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ШапкаДокумента);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНоменклатуры);
		НоваяСтрока.НомерДокумента = НомерДокумента;
		
		// Получим сумму без НДС
		Если ВыборкаНоменклатуры.КоличествоПрослеживаемости = ВыборкаНоменклатуры.КоличествоПартии Тогда
			НоваяСтрока.СуммаБезНДС = ВыборкаНоменклатуры.СуммаПартии;
		Иначе
			НоваяСтрока.СуммаБезНДС = ?(ВыборкаНоменклатуры.КоличествоПартии = 0,
				ВыборкаНоменклатуры.СуммаПартии,
				Окр(ВыборкаНоменклатуры.СуммаПартии / ВыборкаНоменклатуры.КоличествоПартии
					* ВыборкаНоменклатуры.КоличествоПрослеживаемости, 2));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ЗакрытиеСмены"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЗакрытиеСмены(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ЗакрытиеСмены");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
			
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = ЭтотОбъект.ХозОперация.Наименование+" № "+дкПолучитьНомерДляПечати(ЭтотОбъект)+" от "+Формат(ЭтотОбъект.Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подзаголовок");
	ОбластьМакета.Параметры.ИмяТабличнойЧасти = "Товары и услуги, реализованные розничному покупателю";
	ТабДокумент.Вывести(ОбластьМакета);
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 	
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ОбластьПодвалТаблицы.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвалТаблицы.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвалТаблицы.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") Тогда
		СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
		ОбластьПодвалТаблицы.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвалТаблицы.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	ТабДокумент.Вывести(ОбластьПодвалТаблицы);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подзаголовок");
	ОбластьМакета.Параметры.ИмяТабличнойЧасти = "Товары, возвращенные розничными покупателями";
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = "";
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "";
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 	
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Возвраты;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ОбластьПодвалТаблицы.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвалТаблицы.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвалТаблицы.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") Тогда
		СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
		ОбластьПодвалТаблицы.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвалТаблицы.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	ТабДокумент.Вывести(ОбластьПодвалТаблицы);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЗакрытиеСмены()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(КассаККМ) Тогда
		Организация           = КассаККМ.Организация;
		ПодразделениеКомпании = КассаККМ.Подразделение;
	КонецЕсли; 
	
	ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КурсДокумента   = 1;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	дкПриКопировании(ЭтотОбъект, ОбъектКопирования);
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Если обЗначениеНеЗаполнено(ДатаПоследнегоДокумента) Тогда
			ДатаПоследнегоДокумента=Дата;
		КонецЕсли; 
	КонецЕсли;
	
	дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	дкПриЗаписи(ЭтотОбъект, Отказ);
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	дкПередУдалением(ЭтотОбъект, Отказ);
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
	//Удалим накопление сумм
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейНакоплениеСумм.ОтменаПроведения();
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим); 	
	Отказ = Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	НачатьТранзакцию();
	
	//Если Акт о реализации - то не нужно удалять чеки, так как их нет
	Если НЕ ХозОперация = Справочники.ХозОперации.АктОРеализации Тогда
		
		//Получим право на удаление чеков после свертки
		ЧекиПослеСвертки=обПраво("ЧекиПослеСвертки",Права,,ЭтотОбъект);
		Если ТипЗнч(ЧекиВыборка)=Тип("ВыборкаИзРезультатаЗапроса") Тогда
			ЧекиВыборка.Сбросить();
			Пока ЧекиВыборка.Следующий() Цикл
				//Перебор чеков для удаления
				ОбъектЧек=ЧекиВыборка.Чек.ПолучитьОбъект();
				Если ЧекиПослеСвертки=Перечисления.ВариантыОтветов.Спрашивать Тогда
					#Если Клиент Тогда
						Состояние("Распроведение чека <"+СокрЛП(ОбъектЧек)+"> ...");
					#КонецЕсли
					Попытка
						ОбъектЧек.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					Исключение
						//При отмене проведения чека произошла ошибка
						Отказ=Истина;
						дкСообщитьРезультат("Ошибка отмены проведения чека <"+СокрЛП(ОбъектЧек)+">","Важное:",ЭтотОбъект);
					КонецПопытки; 
				ИначеЕсли ЧекиПослеСвертки=Перечисления.ВариантыОтветов.Да Тогда
					//Чеки удаляются совсем
					#Если Клиент Тогда
						Состояние("Удаление чека <"+СокрЛП(ОбъектЧек)+"> ...");
					#КонецЕсли
					ПодчиненныеДокументы=КритерииОтбора.ПодчиненныеДокументы.Найти(ЧекиВыборка.Чек);
					Попытка
						Если ПодчиненныеДокументы.Количество()=0 Тогда
							ОбъектЧек.Удалить();
						Иначе
							ОбъектЧек.УстановитьПометкуУдаления(Истина);
						КонецЕсли; 
					Исключение
						//При удалении чека произошла ошибка
						Отказ=Истина;
						дкСообщитьРезультат("Ошибка удаления чека <"+СокрЛП(ОбъектЧек)+">","Важное:",ЭтотОбъект);
					КонецПопытки; 
				ИначеЕсли ЧекиПослеСвертки=Перечисления.ВариантыОтветов.Нет Тогда
					//Чеки помечаются на удаление
					#Если Клиент Тогда
						Состояние("Пометка чека <"+СокрЛП(ОбъектЧек)+"> на удаление ...");
					#КонецЕсли
					Попытка
						ОбъектЧек.УстановитьПометкуУдаления(Истина);
					Исключение
						//При установке пометки удаления чека произошла ошибка
						Отказ=Истина;
						дкСообщитьРезультат("Ошибка установки пометки удаления чека <"+СокрЛП(ОбъектЧек)+">","Важное:",ЭтотОбъект);
					КонецПопытки; 
				КонецЕсли; 
				
				// Если в процессе обработки чеков возникли ошибки, то прекращаем проведение (транзакция все равно уже отбракована)
				Если Отказ Тогда
					дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
					ОтменитьТранзакцию();
					Возврат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли; 
	
	КонецЕсли;
	
	Если Возвраты.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗакрытиеСменыВозвраты.Номенклатура КАК Номенклатура,
		               |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ВЫБОР
		               |		КОГДА ЗакрытиеСменыВозвраты.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.АктОРеализации)
		               |			ТОГДА &СкладКомпании
		               |		ИНАЧЕ ЗакрытиеСменыВозвраты.МестоРазмещения
		               |	КОНЕЦ КАК СкладКомпании,
		               |	МАКСИМУМ(ЗакрытиеСменыВозвраты.Цена) КАК ЦенаРозничная,
		               |	МАКСИМУМ(ЗакрытиеСменыВозвраты.СуммаВсего) КАК СуммаРозничная,
		               |	МАКСИМУМ(ЗакрытиеСменыВозвраты.СуммаСкидки) КАК СуммаСкидки,
		               |	СУММА(ЗакрытиеСменыВозвраты.Количество * ЗакрытиеСменыВозвраты.Коэффициент) КАК Количество
		               |ИЗ
		               |	Документ.ЗакрытиеСмены.Возвраты КАК ЗакрытиеСменыВозвраты
		               |ГДЕ
		               |	ЗакрытиеСменыВозвраты.Ссылка = &Ссылка
		               |	И ЗакрытиеСменыВозвраты.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ЗакрытиеСменыВозвраты.Номенклатура,
		               |	ЗакрытиеСменыВозвраты.ХарактеристикаНоменклатуры,
		               |	ВЫБОР
		               |		КОГДА ЗакрытиеСменыВозвраты.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.АктОРеализации)
		               |			ТОГДА &СкладКомпании
		               |		ИНАЧЕ ЗакрытиеСменыВозвраты.МестоРазмещения
		               |	КОНЕЦ";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		
		НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения=Режим;
		НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам=Запрос.Выполнить().Выгрузить();
		//если АктОРеализации тогда склад берем из шапки документа
		НаборЗаписейОстатки.СкладКомпании = ?(ХозОперация = Справочники.ХозОперации.АктОРеализации,СкладКомпании,Неопределено);
		НаборЗаписейОстатки.Приходовать=Истина;
		НаборЗаписейОстатки.ПоБазовомуКоличеству=Ложь;
		НаборЗаписейОстатки.Резервировать=Ложь;
		НаборЗаписейОстатки.ДвиженияПоРознице=Истина;
		НаборЗаписейОстатки.РазрешитьПереоценку = Ложь;
		НаборЗаписейОстатки.ЕстьРозничнаяСумма=Истина;
		НаборЗаписейОстатки.ИмяРеквизитаЦенаРозничная="Цена";
		НаборЗаписейОстатки.ИмяРеквизитаСуммаРозничная="СуммаВсего";
		Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
		Если НЕ Отказ Тогда
			НаборЗаписейОстатки.Записать();
		КонецЕсли;
	КонецЕсли;
	
	// проведем остатки товаров
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения=Режим;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=Неопределено;
	//если АктОРеализации тогда склад берем из шапки документа
	НаборЗаписейОстатки.СкладКомпании = ?(ХозОперация = Справочники.ХозОперации.АктОРеализации,СкладКомпании,Неопределено);
	НаборЗаписейОстатки.Приходовать=Истина;
	НаборЗаписейОстатки.ПоБазовомуКоличеству=Ложь;
	НаборЗаписейОстатки.Резервировать=Ложь;
	НаборЗаписейОстатки.ДвиженияПоРознице=Истина;
	НаборЗаписейОстатки.ИмяРеквизитаЦенаРозничная="Цена";
	НаборЗаписейОстатки.ИмяРеквизитаСуммаРозничная="СуммаВсего";
	НаборЗаписейОстатки.ГраницаРасчетаОстатков = Новый Граница(ЭтотОбъект.МоментВремени(), ВидГраницы.Включая);
	Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// ANDV Проведем деньги по кассе ККМ
	НаборЗаписейКассыККМ=Движения.КассыККМ;
	СуммаОплатыПоКассе =0;
	Для каждого СтрокаОплаты из Оплаты Цикл
		
		Если СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
			
			Если (СтрокаОплаты.Сумма-СтрокаОплаты.СуммаВозврат)<>0 Тогда
				НаборЗаписейКассыККМ.ДокументОбъект 	= ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения 	= Режим;
				НаборЗаписейКассыККМ.КассаККМ 			= СтрокаОплаты.Касса;
				НаборЗаписейКассыККМ.СпособОплаты 		= СтрокаОплаты.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма 				= (СтрокаОплаты.Сумма-СтрокаОплаты.СуммаВозврат);
				Отказ = НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
			КонецЕсли;
		Иначе
			Если СтрокаОплаты.Сумма<>0 Тогда
				НаборЗаписейКассыККМ.ДокументОбъект 	= ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения 	= Режим;
				НаборЗаписейКассыККМ.КассаККМ 			= СтрокаОплаты.Касса;
				НаборЗаписейКассыККМ.СпособОплаты 		= СтрокаОплаты.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма 				= СтрокаОплаты.Сумма;
				Отказ = НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
			КонецЕсли;
			Если СтрокаОплаты.СуммаВозврат <> 0 Тогда
				НаборЗаписейКассыККМ.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения= Режим;
				НаборЗаписейКассыККМ.КассаККМ 		= СтрокаОплаты.Касса;
				НаборЗаписейКассыККМ.СпособОплаты 	= СтрокаОплаты.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма			= -СтрокаОплаты.СуммаВозврат;
				Отказ = НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Наличные ИЛИ
			СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Электронно Тогда
			 СуммаОплатыПоКассе = СуммаОплатыПоКассе+СтрокаОплаты.Сумма-СтрокаОплаты.СуммаВозврат;
		КонецЕсли;	 
	КонецЦикла;
	
	Для каждого СтрокаОплаты из ВозвратОплаты Цикл
		
		Если СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
			
			Если СтрокаОплаты.Сумма<>0 Тогда
				НаборЗаписейКассыККМ.ДокументОбъект 	= ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения 	= Режим;
				НаборЗаписейКассыККМ.КассаККМ 			= СтрокаОплаты.Касса;
				НаборЗаписейКассыККМ.СпособОплаты 		= СтрокаОплаты.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма 				= -СтрокаОплаты.Сумма;
				Отказ = НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
			КонецЕсли;
		Иначе
			Если СтрокаОплаты.Сумма <> 0 Тогда
				НаборЗаписейКассыККМ.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения= Режим;
				НаборЗаписейКассыККМ.КассаККМ 		= СтрокаОплаты.Касса;
				НаборЗаписейКассыККМ.СпособОплаты 	= СтрокаОплаты.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма			= -СтрокаОплаты.Сумма;
				Отказ = НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Наличные ИЛИ
			СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Электронно Тогда
			 СуммаОплатыПоКассе = СуммаОплатыПоКассе-СтрокаОплаты.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	// подготовим таблицу движений в разрезе подразделений по взаиморасчетам
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений();
	ТаблицаВзаиморасчетов.Колонки.Добавить("Сумма");
	ОписаниеТипов=Новый ОписаниеТипов;
	ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение",ОписаниеТипов);
	
	// проведем взаиморасчеты
	Для каждого СтрокаОплаты из Оплаты Цикл
		Если (СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Предоплата ИЛИ
			 СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Постоплата) И
			 НЕ СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.ЗаСчетЗаведения Тогда
			
			 Если (СтрокаОплаты.Сумма > 0) Тогда
				НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
				НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
				НаборЗаписейВзаиморасчеты.Контрагент			= СтрокаОплаты.Контрагент;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = СтрокаОплаты.ДоговорВзаиморасчетов;
				НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
				НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
				НаборЗаписейВзаиморасчеты.Сумма = СтрокаОплаты.Сумма;
				НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
				
				// доходы и расходы по суммовым разницам
				СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
				Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					Если БалансВедетсяПоПодразделениям Тогда
						НаборЗаписейДиР.Подразделение  = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
					КонецЕсли;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
					НаборЗаписейДиР.ВУпрВалюте = Истина;
					Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
						НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
					Иначе
						НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
					КонецЕсли;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
				КонецЕсли;
			КонецЕсли;
			Если (СтрокаОплаты.СуммаВозврат > 0) Тогда
				НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
				НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
				НаборЗаписейВзаиморасчеты.Контрагент			= СтрокаОплаты.Контрагент;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = СтрокаОплаты.ДоговорВзаиморасчетов;
				НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
				НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
				НаборЗаписейВзаиморасчеты.Сумма = -СтрокаОплаты.СуммаВозврат;
				НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
				
				// доходы и расходы по суммовым разницам
				СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
				Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					Если БалансВедетсяПоПодразделениям Тогда
						НаборЗаписейДиР.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
					КонецЕсли;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
					НаборЗаписейДиР.ВУпрВалюте = Истина;
					Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
						НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
					Иначе
						НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
					КонецЕсли;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
			СтрокаВзаиморасчетов.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
			СтрокаВзаиморасчетов.Сумма = СтрокаОплаты.Сумма-СтрокаОплаты.СуммаВозврат;
			
			Если Отказ Тогда Прервать; КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаОплаты из ВозвратОплаты Цикл
		Если (СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Предоплата ИЛИ
			 СтрокаОплаты.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Постоплата) И
			 НЕ СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.ЗаСчетЗаведения Тогда
			
			Если (СтрокаОплаты.Сумма > 0) Тогда
				НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
				НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
				НаборЗаписейВзаиморасчеты.Контрагент			= СтрокаОплаты.Контрагент;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = СтрокаОплаты.ДоговорВзаиморасчетов;
				НаборЗаписейВзаиморасчеты.Сделка = Неопределено;
				НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Истина;
				НаборЗаписейВзаиморасчеты.Сумма = -СтрокаОплаты.Сумма;
				НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
				
				// доходы и расходы по суммовым разницам
				СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
				Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					Если БалансВедетсяПоПодразделениям Тогда
						НаборЗаписейДиР.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
					КонецЕсли;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
					НаборЗаписейДиР.ВУпрВалюте = Истина;
					Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
						НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
					Иначе
						НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
					КонецЕсли;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаВзаиморасчетов = ТаблицаВзаиморасчетов.Добавить();
			СтрокаВзаиморасчетов.Подразделение = СтрокаОплаты.ДоговорВзаиморасчетов.Подразделение;
			СтрокаВзаиморасчетов.Сумма = -СтрокаОплаты.Сумма;
			
			Если Отказ Тогда Прервать; КонецЕсли;
		КонецЕсли;
	КонецЦикла;		
	
	
	//Проведем сумму выручки по доходам и расходам
	
	ТаблицаВзаиморасчетов.Свернуть("Подразделение","Сумма");
	Для Каждого СтрокаСписания Из ТаблицаВзаиморасчетов Цикл
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = СтрокаСписания.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.Доход                  = СтрокаСписания.Сумма;	
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЦикла;
	
	Если СуммаОплатыПоКассе<>0 Тогда
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = КассаККМ.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = (ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Выручка;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.Доход                  = СуммаОплатыПоКассе;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// Формирование накопления для скидок
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.Отбор.Документ.Значение=Ссылка;
	НаборЗаписейНакоплениеСумм.Отбор.Документ.Использование=Истина;
	ВалютаНакопительныхСумм=Константы.ВалютаНакопительныхСумм.Получить();
	КонтрагентыСуммы=Контрагенты.Выгрузить();
	КонтрагентыСуммы.Свернуть("Контрагент,Карточка","Сумма,СуммаСкидки,СуммаВсего,КоличествоЧеков, КоличествоНоменклатуры");
	Для каждого Контрагент Из КонтрагентыСуммы Цикл
		Если (НЕ обЗначениеНеЗаполнено(Контрагент.Контрагент)) ИЛИ (НЕ обЗначениеНеЗаполнено(Контрагент.Карточка)) Тогда
			НоваяЗапись=НаборЗаписейНакоплениеСумм.Добавить();
			НоваяЗапись.Контрагент=Контрагент.Контрагент;
			НоваяЗапись.Карточка=Контрагент.Карточка;
			НоваяЗапись.ПериодНакопления=Дата;
			НоваяЗапись.Подразделение=ПодразделениеКомпании;
			НоваяЗапись.Документ=Ссылка;
			НоваяЗапись.Сумма=обПересчет(Контрагент.СуммаВсего,ВалютаДокумента,КурсДокумента,ВалютаНакопительныхСумм,Дата);
			НоваяЗапись.КоличествоЧеков=Контрагент.КоличествоЧеков;
			НоваяЗапись.КоличествоНоменклатуры=Контрагент.КоличествоНоменклатуры;
			Попытка
				НаборЗаписейНакоплениеСумм.Записать();
			Исключение
				дкСообщитьРезультат("Ошибка записи накопительной суммы","Важное&Накопительные суммы:",ЭтотОбъект);
				Отказ=Истина;
			КонецПопытки; 
		КонецЕсли; 
	КонецЦикла; 
	
	// Изменение состояния кодов
	НаборЗаписейНакоплениеСумм = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.Отбор.ДокументОснование.Значение = Ссылка;
	НаборЗаписейНакоплениеСумм.Отбор.ДокументОснование.Использование = Истина;
	ТаблицаЗаписей = НаборЗаписейНакоплениеСумм.Выгрузить();
	ТаблицаЗаписей.Очистить();
	
	// Сворачиваем товары по кодам маркировки
	ТаблицаКодовМаркировки = Новый ТаблицаЗначений();
	ТаблицаКодовМаркировки.Колонки.Добавить("Номенклатура");
	ТаблицаКодовМаркировки.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТаблицаКодовМаркировки.Колонки.Добавить("КодМаркировки");
	ТаблицаКодовМаркировки.Колонки.Добавить("КоличествоПродаж");
	ТаблицаКодовМаркировки.Колонки.Добавить("КоличествоВозврат");
	
	Для Каждого ТекущаяСтрока Из Товары Цикл
		
		// Найдем коды
		НайденныеСтроки = КодыМаркировки.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекущаяСтрока.ИдентификаторТовара));
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ТаблицаКодовМаркировки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			НоваяСтрока.КодМаркировки = НайденнаяСтрока.КодМаркировки;
			НоваяСтрока.КоличествоПродаж = 1;
			НоваяСтрока.КоличествоВозврат = 0;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из Возвраты Цикл
		
		// Найдем коды
		НайденныеСтроки = КодыМаркировки.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекущаяСтрока.ИдентификаторТовара));
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ТаблицаКодовМаркировки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			НоваяСтрока.КодМаркировки = НайденнаяСтрока.КодМаркировки;
			НоваяСтрока.КоличествоПродаж = 0;
			НоваяСтрока.КоличествоВозврат = 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаКодовМаркировки.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,КодМаркировки", "КоличествоПродаж,КоличествоВозврат");
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	Для Каждого ТекущаяСтрока Из ТаблицаКодовМаркировки Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			дкСообщитьРезультат("Код маркировки <"+СокрЛП(ТекущаяСтрока.КодМаркировки)+"> не разобран. Проверьте его корректность.","Важное&Состояние кодов маркировки:", ЭтотОбъект);
			Отказ = Истина;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаЗаписей.Добавить();
		НоваяСтрока.Период = Дата;
		НоваяСтрока.ДокументОснование = Ссылка;
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.Номенклатура = ТекущаяСтрока.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатуры;
		НоваяСтрока.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		НоваяСтрока.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		НоваяСтрока.Состояние = ?(ТекущаяСтрока.КоличествоПродаж > ТекущаяСтрока.КоличествоВозврат,
			Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаРозничнаяПродажа,
			Перечисления.СостоянияКодовМаркировки.ВведенВОборотПриВозврате);
		НоваяСтрока.КодМаркировки = ТекущаяСтрока.КодМаркировки;
	КонецЦикла;
	
	НаборЗаписейНакоплениеСумм.Загрузить(ТаблицаЗаписей);
	
	Попытка
		НаборЗаписейНакоплениеСумм.Записать();
	Исключение
		дкСообщитьРезультат("Ошибка записи состояния кодов маркировки","Важное&Состояние кодов маркировки:",ЭтотОбъект);
		Отказ = Истина;
	КонецПопытки; 
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// бонусы
	НаборБонусныеПрограммы = Движения.БонусныеБаллы;
	Для Каждого Бонус Из Бонусы Цикл
		// бонусные баллы
		Если Бонус.КоличествоНачисленныхБонусов > 0 Тогда
			НаборБонусныеПрограммы.Карта             = Бонус.БонуснаяКарта;
			НаборБонусныеПрограммы.БонуснаяПрограмма = Бонус.БонуснаяПрограмма;
			НаборБонусныеПрограммы.ХозОперация       = ХозОперация;
			НаборБонусныеПрограммы.КоличетсвоБаллов  = Бонус.КоличествоНачисленныхБонусов;
			НаборБонусныеПрограммы.Регистратор       = Ссылка;
			НаборБонусныеПрограммы.ДокДата           = Дата;
			
			Отказ = НЕ НаборБонусныеПрограммы.Приход() ИЛИ Отказ;
			
			Если НЕ Отказ тогда
				НаборБонусныеПрограммы.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
		Если Бонус.КоличествоСписанныхБонусов > 0 Тогда
			НаборБонусныеПрограммы.Карта             = Бонус.БонуснаяКарта;
			НаборБонусныеПрограммы.БонуснаяПрограмма = Бонус.БонуснаяПрограмма;
			НаборБонусныеПрограммы.ХозОперация       = ХозОперация;
			НаборБонусныеПрограммы.КоличетсвоБаллов  = Бонус.КоличествоСписанныхБонусов;
			НаборБонусныеПрограммы.Регистратор       = Ссылка;
			НаборБонусныеПрограммы.ДокДата           = Дата;
			НаборБонусныеПрограммы.ПериодДвижения    = Новый Граница(МоментВремени(), ВидГраницы.Включая);
			
			Отказ = НЕ НаборБонусныеПрограммы.Расход() ИЛИ Отказ;
		КонецЕсли;
	КонецЦикла;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	// Проверим наличие прослеживаемых товаров, которые были возвращены из розницы
	Если НЕ Отказ Тогда
		Движения.ГТДПартийТоваровКомпании.Записать();
		Движения.ПартииТоваровКомпании.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ЧекиВыборка=Неопределено;

// Модуль документа Инкассация

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем СообщениеОбОшибке Экспорт;	// Переменная для накопления результатов записи. Если = неопределено, то вывод будет на экран, иначе тут сообщения об ошибках.
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

//Остатки по кассе ККМ
Перем СуммаККМНаличные Экспорт;           // Сумма кассы наличными
Перем СуммаККМБезналичныеПриход Экспорт;  // Сумма кассы без наличный приход
Перем СуммаККМБезналичныеВозврат Экспорт; // Сумма кассы без наличный возврат


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("КассаККМ",1);
	ОбязательныеРеквизиты.Вставить("ДатаФР",1);
	
	Если СуммаНал>0 Тогда
		ОбязательныеРеквизиты.Вставить("Инкассатор",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетовИнкассатор",1);
	КонецЕсли; 
	Если СуммаБезналПолучено>0 ИЛИ СуммаБезналВозврат>0 Тогда
		ОбязательныеРеквизиты.Вставить("ПлатежнаяСистема",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетовПлатежнаяСистема",1);
	КонецЕсли; 
	
	Для Каждого СтрокаОплаты Из Оплаты Цикл
		Если СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные И СтрокаОплаты.Сумма > 0 Тогда
			ОбязательныеРеквизиты.Вставить("Инкассатор",1);
			ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетовИнкассатор",1);
		ИначеЕсли  СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Безналичные И СтрокаОплаты.Сумма > 0  ИЛИ СтрокаОплаты.СуммаВозврат > 0 Тогда
			ОбязательныеРеквизиты.Вставить("ПлатежнаяСистема",1);
			ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетовПлатежнаяСистема",1);
		КонецЕсли;
	КонецЦикла;
		
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
		
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты      = Новый Структура();  		
			КонтролируемыеРеквизитыШапки = Новый Структура();
									
			Если НЕ ДоговорВзаиморасчетовИнкассатор.Пустая() Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетовИнкассатор", КонтрольПоПодразделению);	
			КонецЕсли;
			
			Если НЕ ДоговорВзаиморасчетовПлатежнаяСистема.Пустая() Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетовПлатежнаяСистема", КонтрольПоПодразделению);	
			КонецЕсли;
			           						
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("КассаККМ");
			КонецЕсли;
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки); 			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
			
    	КонецЕсли; 
		
		Если ВалютаДокумента<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+"). Ввод документа разрешен только в валюте регламентированного учета";
			дкДобавитьОшибку(Ошибки, ТекстОшибки);
			Результат = Ложь;
		КонецЕсли;
 
		Если КассаККМ.ВалютаДенежныхСредств<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			ТекстОшибки = "Валюта кассы ККМ (" + КассаККМ.ВалютаДенежныхСредств + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+").";
			дкДобавитьОшибку(Ошибки, ТекстОшибки);
			Результат = Ложь;
		КонецЕсли;
		
		ФлагБезналичныйРасчет = Ложь;
		Для Каждого ТекСтрока Из Оплаты Цикл
			Если ТекСтрока.ТипОплаты.ВидОплаты = Перечисления.ВидыОплаты.БезналичныйРасчет Тогда
				ФлагБезналичныйРасчет = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
   		Если ВалютаДокумента <> ДоговорВзаиморасчетовИнкассатор.ВалютаВзаиморасчетов И Не ФлагБезналичныйРасчет Тогда
			ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте договора взаиморасчетов с инкассатором ("+ДоговорВзаиморасчетовИнкассатор.ВалютаВзаиморасчетов+").";
			дкДобавитьОшибку(Ошибки, ТекстОшибки);
			Результат = Ложь;
		КонецЕсли;
		
 	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	//Документ считаем корректно заполненным, независимо от заполненности табличных частей
	Возврат Истина;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВнесениеВКассуККМ","Внесение в кассу ККМ",Истина);
	СписокХозОпераций.Добавить("ИзъятиеИзКассыККМ","Изъятие из кассы ККМ");
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает рассчитанную сумму документа.
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Оплаты.Итог("Сумма") - Оплаты.Итог("СуммаВозврат");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="ХозОперация" Тогда
		Если ХозОперация=Справочники.ХозОперации.ВнесениеВКассуККМ Тогда
			Если НЕ обЗначениеНеЗаполнено(ПлатежнаяСистема) Тогда
				ПлатежнаяСистема=Справочники.Контрагенты.ПустаяСсылка();
				ОбработкаРеквизита("ПлатежнаяСистема",,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетовПлатежнаяСистема) Тогда
				ДоговорВзаиморасчетовПлатежнаяСистема=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				ОбработкаРеквизита("ДоговорВзаиморасчетовПлатежнаяСистема",,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
			Если СуммаБезналПолучено<>0 Тогда
				СуммаБезналПолучено=0;
				ОбработкаРеквизита("СуммаБезналПолучено",,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
			Если СуммаБезналВозврат<>0 Тогда
				СуммаБезналВозврат=0;
				ОбработкаРеквизита("СуммаБезналВозврат",,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ИзъятиеИзКассыККМ Тогда
		КонецЕсли; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.УправлениеДиалогом();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="ВалютаДокумента" ИЛИ Имя="КурсДокумента" Тогда
		#Если Клиент Тогда
			ЭтаФорма.ОтображениеВалютыДокумента();
		#КонецЕсли
		
	ИначеЕсли Имя="КассаККМ" Тогда
			
		Если КассаККМ.Пустая() Тогда
			СуммаККМНаличные			= 0;
			СуммаККМБезналичныеПриход	= 0;
			СуммаККМБезналичныеВозврат	= 0;
			Возврат Истина;
		КонецЕсли;
		
		// Установим валюту документа
		Если КассаККМ.ВалютаДенежныхСредств<>ВалютаДокумента Тогда
			// Определим курс валюты денежных средств
			СтруктураКурса            = обКурсДляВалюты(КассаККМ.ВалютаДенежныхСредств,Дата);
			КурсВалютыДенежныхСредств = ?(СтруктураКурса.Кратность = 0, СтруктураКурса.Курс, СтруктураКурса.Курс / СтруктураКурса.Кратность);
			
			ВыполнитьПересчет = Ложь;
			Если ТипЗнч(ДопПараметры)=Тип("Структура") Тогда
				Если ДопПараметры.Свойство("ВыполнитьПересчет") Тогда
					ВыполнитьПересчет = ДопПараметры.ВыполнитьПересчет;	
				КонецЕсли;					
			КонецЕсли;
			
			Если ВыполнитьПересчет Тогда
				// Пересчитаем суммы документа в табличной части.
				Коэффициент	= обПересчет(1, ВалютаДокумента, КурсДокумента, КассаККМ.ВалютаДенежныхСредств, КурсВалютыДенежныхСредств);
				Для Каждого СтрокаОстатокККМ Из Оплаты Цикл
					СтрокаОстатокККМ.Сумма 		  = СтрокаОстатокККМ.Сумма        * Коэффициент;
					СтрокаОстатокККМ.СуммаВозврат = СтрокаОстатокККМ.СуммаВозврат * Коэффициент;
				КонецЦикла; 
			КонецЕсли;
			
			// Установим валюту документа как КассаККМ.ВалютаДенежныхСредств
			ВалютаДокумента = КассаККМ.ВалютаДенежныхСредств;
			ОбработкаРеквизита("ВалютаДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
			
			КурсДокумента	= КурсВалютыДенежныхСредств;
			ОбработкаРеквизита("КурсДокумента",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		// Проверим договоры на соответствие
		ДопПараметры = Новый Структура("ОтображатьПредупреждение",ЛОЖЬ);
		ОбработкаРеквизита("ДоговорВзаиморасчетовИнкассатор",		ТекСтрока,ЭтаФорма,ДопПараметры);
		ОбработкаРеквизита("ДоговорВзаиморасчетовПлатежнаяСистема",	ТекСтрока,ЭтаФорма,ДопПараметры);
		
		// Оповестим пользователя о произошедшем
		Если ДопПараметры.Свойство("ДоговорВзаиморасчетовИнкассатор") И ДопПараметры.Свойство("ДоговорВзаиморасчетовПлатежнаяСистема") Тогда
			ТекстСообщения = "Валюты договоров взаиморасчетов инкассатора и платежной системы не соответствует валюте кассы ККМ.";
		ИначеЕсли ДопПараметры.Свойство("ДоговорВзаиморасчетовИнкассатор") ИЛИ ДопПараметры.Свойство("ДоговорВзаиморасчетовПлатежнаяСистема") Тогда
			ТекстСообщения = "Валюта договора взаиморасчетов "+?(ДопПараметры.Свойство("ДоговорВзаиморасчетовИнкассатор"),"инкассатора","платежной системы")+" не соответствует валюте кассы ККМ.";
		КонецЕсли;
		Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
			Сообщить(ТекстСообщения, СтатусСообщения.Важное);
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя="Инкассатор" Тогда
		Если обЗначениеНеЗаполнено(Инкассатор) Тогда 
			// если контрагент не указан то очистим договор
			ДоговорВзаиморасчетовИнкассатор=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовИнкассатор",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			//Если договор уже выбран, тогда ничего не делаем
			Если (НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетовИнкассатор)) И 
					ДоговорВзаиморасчетовИнкассатор.Владелец = Инкассатор И
					ДоговорВзаиморасчетовИнкассатор.ПолучитьОбъект().СоответствуетХозОперации(ХозОперация) Тогда
					Возврат Истина;
			КонецЕсли;
			ДопПараметры = ?(ДопПараметры=Неопределено, Новый Структура, ДопПараметры);
			ВидДоговора = Перечисления.ВидыДоговоров.Прочее;
			ДопПараметры.Вставить("ВидДоговора",ВидДоговора);
			ДопПараметры.Вставить("ВалютаВзаиморасчетов",	ВалютаДокумента);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ОтображатьПредупреждение",ЛОЖЬ);
			ДоговорВзаиморасчетовИнкассатор = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Инкассатор,ВидДоговора,ЭтотОбъект,ДопПараметры);
			Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовИнкассатор",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетовИнкассатор" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетовИнкассатор) Тогда
			//Проверим на соответствие ХозОперации
			Если НЕ ДоговорВзаиморасчетовИнкассатор.ПолучитьОбъект().СоответствуетХозОперации(ХозОперация) Тогда
				#Если Клиент Тогда
				Предупреждение("Вид договора инкассатора не соответствует Хоз.операции.",обПраво("ТаймаутДиалогов",Права,,ЭтотОбъект));
				#КонецЕсли
				ДоговорВзаиморасчетовИнкассатор=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовИнкассатор",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
			
			// Проверим договор на соответствие валюте документа
			Если ВалютаДокумента <> ДоговорВзаиморасчетовИнкассатор.ВалютаВзаиморасчетов Тогда
				ДопПараметры = ?(ТипЗнч(ДопПараметры)=Тип("Структура"), ДопПараметры, Новый Структура);	ОтображатьПредупреждение = ЛОЖЬ;
				Если НЕ ДопПараметры.Свойство("ОтображатьПредупреждение", ОтображатьПредупреждение) ИЛИ ОтображатьПредупреждение = ИСТИНА Тогда
					#Если Клиент Тогда
					Предупреждение("Валюта взаиморасчетов договора инкассатора не соответствует валюте" + ?(обЗначениеНеЗаполнено(КассаККМ)," документа", " кассы ККМ."), обПраво("ТаймаутДиалогов",Права,,ЭтотОбъект));
					#КонецЕсли
					ДопПараметры.Вставить("ОтображатьПредупреждение",ЛОЖЬ);
				КонецЕсли;
				ДопПараметры.Вставить("ДоговорВзаиморасчетовИнкассатор",ИСТИНА);
				ДоговорВзаиморасчетовИнкассатор = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовИнкассатор",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		КонецЕсли;
		
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ВывестиЗаголовокВзаиморасчеты(ЭтаФорма.ЭлементыФормы.тВзаиморасчетыИнкассатор);
		КонецЕсли; 
		#КонецЕсли
		
	ИначеЕсли Имя="ПлатежнаяСистема" Тогда
		Если обЗначениеНеЗаполнено(ПлатежнаяСистема) Тогда 
			// если контрагент не указан то очистим договор
			ДоговорВзаиморасчетовПлатежнаяСистема=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовПлатежнаяСистема",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			//Если договор уже выбран, тогда ничего не делаем
			Если (НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетовПлатежнаяСистема)) И 
					ДоговорВзаиморасчетовПлатежнаяСистема.Владелец=ПлатежнаяСистема И
					ДоговорВзаиморасчетовПлатежнаяСистема.ПолучитьОбъект().СоответствуетХозОперации(ХозОперация) Тогда
					Возврат Истина;
			КонецЕсли;
			ДопПараметры = ?(ДопПараметры=Неопределено, Новый Структура, ДопПараметры);
			ВидДоговора = Перечисления.ВидыДоговоров.Прочее;
			ДопПараметры.Вставить("ВидДоговора",ВидДоговора);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ОтображатьПредупреждение",ЛОЖЬ);
			ДоговорВзаиморасчетовПлатежнаяСистема = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ПлатежнаяСистема,ВидДоговора,ЭтотОбъект,ДопПараметры);
			Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовПлатежнаяСистема",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетовПлатежнаяСистема" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетовПлатежнаяСистема) Тогда
			//Проверим на соответствие ХозОперации
			Если НЕ ДоговорВзаиморасчетовПлатежнаяСистема.ПолучитьОбъект().СоответствуетХозОперации(ХозОперация) Тогда
				#Если Клиент Тогда
				Предупреждение("Вид договора платежной системы не соответствует Хоз.операции.",обПраво("ТаймаутДиалогов",Права,,ЭтотОбъект));
				#КонецЕсли
				ДоговорВзаиморасчетовПлатежнаяСистема=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовПлатежнаяСистема",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
			
			// Проверим договор на соответствие валюте документа
			Если ВалютаДокумента <> ДоговорВзаиморасчетовПлатежнаяСистема.ВалютаВзаиморасчетов Тогда
				ДопПараметры = ?(ТипЗнч(ДопПараметры)=Тип("Структура"), ДопПараметры, Новый Структура);	ОтображатьПредупреждение = ЛОЖЬ;
				Если НЕ ДопПараметры.Свойство("ОтображатьПредупреждение", ОтображатьПредупреждение) ИЛИ ОтображатьПредупреждение = ИСТИНА Тогда
					#Если Клиент Тогда
					Предупреждение("Валюта взаиморасчетов договора платежной системы не соответствует валюте" + ?(обЗначениеНеЗаполнено(КассаККМ)," документа", " кассы ККМ."), обПраво("ТаймаутДиалогов",Права,,ЭтотОбъект));
					#КонецЕсли
					ДопПараметры.Вставить("ОтображатьПредупреждение",ЛОЖЬ);
				КонецЕсли;
				ДопПараметры.Вставить("ДоговорВзаиморасчетовПлатежнаяСистема",ИСТИНА);
				ДоговорВзаиморасчетовПлатежнаяСистема = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				Возврат ОбработкаРеквизита("ДоговорВзаиморасчетовПлатежнаяСистема",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		КонецЕсли;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ВывестиЗаголовокВзаиморасчеты(ЭтаФорма.ЭлементыФормы.тВзаиморасчетыПлатежнаяСистема);
		КонецЕсли; 
		#КонецЕсли
	
	ИначеЕсли Имя="СуммаНал" ИЛИ Имя="СуммаБезналПолучено" ИЛИ Имя="СуммаБезналВозврат" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			СтруктураОбязательныхРеквизитов=ПолучитьОбязательныеРеквизиты();
			удРасставитьАвтоотметкиНезаполненного(ЭтаФорма,СтруктураОбязательныхРеквизитов,Истина);
		КонецЕсли; 
		#КонецЕсли
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "КМ-6"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьКМ6(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("КМ6");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Организация = Организация;
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация,Новый Структура("Наименование,АдресЮридический,ТелефонРабочий"));
	ОбластьМакета.Параметры.ОКПО = Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ИНН = Организация.ИНН;
	ОбластьМакета.Параметры.ОКДП = Организация.КодПоОКДП;
	ОбластьМакета.Параметры.КассаККМ = КассаККМ.Наименование;
	ОбластьМакета.Параметры.ИдентификаторПрограммы = Метаданные.Синоним;
	ОбластьМакета.Параметры.ДокументНомер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.ДокументДата = Формат(Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.ВалютаНаименование = ВалютаДокумента.Наименование;
	ТабДокумент.Вывести(ОбластьМакета);
	
	СуммаНаличных = 0;
	Для Каждого СтрокаОплат Из Оплаты Цикл
		Если СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
			СуммаНаличных = СуммаНаличных + СтрокаОплат.Сумма;
		КонецЕсли;
	КонецЦикла;

	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ОбластьМакета.Параметры.Сумма = Формат(СуммаНаличных,ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Сумма = Формат(СуммаНаличных,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(СуммаНаличных,ЭтотОбъект.ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьМакета = Макет.ПолучитьОбласть("Оборот");
	// ответственные лица
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	
	Кассир = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Кассир);
	ОбластьМакета.Параметры.Заполнить(Кассир);
	
	КассирОперационист = дкОтветственноеЛицо(ЭтотОбъект,"КассирОперационист");
	ОбластьМакета.Параметры.Заполнить(КассирОперационист);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьКМ6()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// ИЗМЕНЕНО
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(КассаККМ) Тогда
		Организация=КассаККМ.Организация;
		ПодразделениеКомпании=КассаККМ.Подразделение;
	КонецЕсли; 
	
	Если Основание = Неопределено Тогда
		Если обЗначениеНеЗаполнено(Инкассатор) Тогда
			Инкассатор = обПраво("ОсновнойИнкассатор",Права);
			ОбработкаРеквизита("Инкассатор");
		КонецЕсли; 
		Если ХозОперация = Справочники.ХозОперации.ИзъятиеИзКассыККМ И обЗначениеНеЗаполнено(ПлатежнаяСистема) Тогда
			ПлатежнаяСистема = обПраво("ОсновнаяПлатежнаяСистема",Права);
			ОбработкаРеквизита("ПлатежнаяСистема");
		КонецЕсли; 
	Иначе
		Инкассатор						= Основание.Контрагент;
		ДоговорВзаиморасчетовИнкассатор	= Основание.ДоговорВзаиморасчетов;
		ОбработкаРеквизита("Инкассатор");
		
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
			ХозОперация = Справочники.ХозОперации.ИзъятиеИзКассыККМ;
			ОбработкаРеквизита("ХозОперация");
			СуммаНал = обПересчет(Основание.СуммаДокумента,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,КурсДокумента);
			ОбработкаРеквизита("СуммаНал");
			
			// Заполним оплаты
			НоваяСтрока = Оплаты.Добавить();
			НоваяСтрока.ТипОплаты	= Справочники.ТипыОплат.Наличные;
			НоваяСтрока.Сумма		= СуммаНал;
			
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			ХозОперация = Справочники.ХозОперации.ВнесениеВКассуККМ;
			ОбработкаРеквизита("ХозОперация");
			СуммаНал = обПересчет(Основание.СуммаДокумента,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,КурсДокумента);
			ОбработкаРеквизита("СуммаНал");
			
			// Заполним оплаты
			НоваяСтрока = Оплаты.Добавить();
			НоваяСтрока.ТипОплаты	= Справочники.ТипыОплат.Наличные;
			НоваяСтрока.Сумма		= СуммаНал;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	
	// необходимость формирования корректирующих проводок по подразделениям
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	Если ХозОперация=Справочники.ХозОперации.ВнесениеВКассуККМ Тогда
		Для Каждого СтрокаОплат Из Оплаты Цикл 
			Если СтрокаОплат.Сумма>0 И СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
				//Проведем вносимую сумму по взаиморасчетам с инкассатором
				НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
				НаборЗаписейВзаиморасчеты.ДокументОбъект        = ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.РежимПроведения       = Режим;
				НаборЗаписейВзаиморасчеты.Контрагент            = Инкассатор;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетовИнкассатор;
				НаборЗаписейВзаиморасчеты.Сделка                = Неопределено;
				НаборЗаписейВзаиморасчеты.Сумма                 = СтрокаОплат.Сумма;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
				
				// приходуем деньги в кассу ККМ по налу
				НаборЗаписейКассыККМ = Движения.КассыККМ;
				НаборЗаписейКассыККМ.ДокументОбъект  = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения = Режим;
				НаборЗаписейКассыККМ.КассаККМ        = КассаККМ;
				НаборЗаписейКассыККМ.СпособОплаты    = СтрокаОплат.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма           = СтрокаОплат.Сумма;
				Отказ = НЕ НаборЗаписейКассыККМ.Приход() ИЛИ Отказ;
				
				ПодразделениеКассаККМ              = обПолучитьБалансовоеПодразделение(КассаККМ.Подразделение);
				ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетовИнкассатор.Подразделение);
				
				Если ВедетсяБалансПоПодразделению И (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеКассаККМ) Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетовИнкассатор.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Ложь;
					НаборЗаписейДиР.Расход     = СтрокаОплат.Сумма;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
					
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = КассаККМ.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Ложь;
					НаборЗаписейДиР.Доход      = СтрокаОплат.Сумма;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
				КонецЕсли;	
			КонецЕсли; 
		КонецЦикла;
		
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ИзъятиеИзКассыККМ Тогда
		Для Каждого СтрокаОплат Из Оплаты Цикл 
			Если СтрокаОплат.Сумма>0 И СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
				// извлечем деньги из кассы ККМ по налу
				НаборЗаписейКассыККМ = Движения.КассыККМ;
				НаборЗаписейКассыККМ.ДокументОбъект  = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения = Режим;
				НаборЗаписейКассыККМ.КассаККМ		 = КассаККМ;
				НаборЗаписейКассыККМ.СпособОплаты	 = СтрокаОплат.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма			 = СтрокаОплат.Сумма;
				Отказ = НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
				
				//Проведем извлекаемую сумму по взаиморасчетам с инкассатором
				НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
				НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
				НаборЗаписейВзаиморасчеты.Контрагент			= Инкассатор;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетовИнкассатор;
				НаборЗаписейВзаиморасчеты.Сделка				= Неопределено;
				НаборЗаписейВзаиморасчеты.Сумма					= СтрокаОплат.Сумма;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
				
				ПодразделениеКассаККМ              = обПолучитьБалансовоеПодразделение(КассаККМ.Подразделение);
				ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетовИнкассатор.Подразделение);
				
				Если ВедетсяБалансПоПодразделению И (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеКассаККМ) Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетовИнкассатор.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Ложь;
					НаборЗаписейДиР.Доход      = СтрокаОплат.Сумма;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
					
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = КассаККМ.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте = Ложь;
					НаборЗаписейДиР.Доход      = СтрокаОплат.Сумма;
					Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
				КонецЕсли;	
			КонецЕсли;
				
			Если СтрокаОплат.Сумма>0 И СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Безналичные Тогда
				// извлечем деньги из кассы ККМ по безналу на приход
				НаборЗаписейКассыККМ=Движения.КассыККМ;
				НаборЗаписейКассыККМ.ДокументОбъект  = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения = Режим;
				НаборЗаписейКассыККМ.КассаККМ		 = КассаККМ;
				НаборЗаписейКассыККМ.СпособОплаты	 = СтрокаОплат.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма			 = СтрокаОплат.Сумма;
				Отказ = НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
				
				//Проведем извлекаемую сумму по взаиморасчетам с платежной системой
				НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
				НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
				НаборЗаписейВзаиморасчеты.Контрагент			= ПлатежнаяСистема;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетовПлатежнаяСистема;
				НаборЗаписейВзаиморасчеты.Сделка				= Неопределено;
				НаборЗаписейВзаиморасчеты.Сумма					= СтрокаОплат.Сумма;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
				
				ПодразделениеКассаККМ              = обПолучитьБалансовоеПодразделение(КассаККМ.Подразделение);
				ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетовПлатежнаяСистема.Подразделение);
				
				Если ВедетсяБалансПоПодразделению И (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеКассаККМ) Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетовПлатежнаяСистема.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте             = Ложь;
					НаборЗаписейДиР.Доход 				   = СтрокаОплат.Сумма;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
					
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = КассаККМ.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте 			   = Ложь;
					НаборЗаписейДиР.Доход 				   = СтрокаОплат.Сумма;
					Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
				КонецЕсли;	
			КонецЕсли;
			
			Если СтрокаОплат.СуммаВозврат > 0 И СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Безналичные Тогда
				// извлечем деньги из кассы ККМ по безналу на расход
				НаборЗаписейКассыККМ = Движения.КассыККМ;
				НаборЗаписейКассыККМ.ДокументОбъект  = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения = Режим;
				НаборЗаписейКассыККМ.КассаККМ		 = КассаККМ;
				НаборЗаписейКассыККМ.СпособОплаты 	 = СтрокаОплат.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма           = -СтрокаОплат.СуммаВозврат;
				Отказ = НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
				
				//Проведем извлекаемую сумму по взаиморасчетам с платежной системой
				НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
				НаборЗаписейВзаиморасчеты.ДокументОбъект		= ЭтотОбъект;
				НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
				НаборЗаписейВзаиморасчеты.Контрагент			= ПлатежнаяСистема;
				НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетовПлатежнаяСистема;
				НаборЗаписейВзаиморасчеты.Сделка				= Неопределено;
				НаборЗаписейВзаиморасчеты.Сумма					= СтрокаОплат.СуммаВозврат;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
				
				
				ПодразделениеКассаККМ              = обПолучитьБалансовоеПодразделение(КассаККМ.Подразделение);
				ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетовПлатежнаяСистема.Подразделение);
				
				Если ВедетсяБалансПоПодразделению И (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеКассаККМ) Тогда
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = ДоговорВзаиморасчетовПлатежнаяСистема.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте             = Ложь;
					НаборЗаписейДиР.Расход 				   = СтрокаОплат.СуммаВозврат;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
					
					НаборЗаписейДиР = Движения.ДоходыИРасходы;
					НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
					НаборЗаписейДиР.Подразделение  = КассаККМ.Подразделение;
					НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
					НаборЗаписейДиР.ВУпрВалюте			   = Ложь;
					НаборЗаписейДиР.Доход 				   = СтрокаОплат.СуммаВозврат;
					Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
				КонецЕсли;	
			КонецЕсли;
			
			Если СтрокаОплат.Сумма > 0 И (СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.КлубнаяКарта
									  ИЛИ СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Кредит
									  ИЛИ СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Талон
									  ИЛИ СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.ЗаСчетЗаведения)Тогда
				// извлечем деньги из кассы ККМ по безналу на расход
				НаборЗаписейКассыККМ = Движения.КассыККМ;
				НаборЗаписейКассыККМ.ДокументОбъект  = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения = Режим;
				НаборЗаписейКассыККМ.КассаККМ     	 = КассаККМ;
				НаборЗаписейКассыККМ.СпособОплаты 	 = СтрокаОплат.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма		  	 = СтрокаОплат.Сумма;
				Отказ = НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
			КонецЕсли;
			
				
			Если СтрокаОплат.СуммаВозврат > 0 И (СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.КлубнаяКарта
									  		ИЛИ СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Кредит
									  		ИЛИ СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Талон
									  		ИЛИ СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.ЗаСчетЗаведения) Тогда
				// извлечем деньги из кассы ККМ по безналу на расход
				НаборЗаписейКассыККМ = Движения.КассыККМ;
				НаборЗаписейКассыККМ.ДокументОбъект	 = ЭтотОбъект;
				НаборЗаписейКассыККМ.РежимПроведения = Режим;
				НаборЗаписейКассыККМ.КассаККМ		 = КассаККМ;
				НаборЗаписейКассыККМ.СпособОплаты	 = СтрокаОплат.ТипОплаты;
				НаборЗаписейКассыККМ.Сумма			 = -СтрокаОплат.СуммаВозврат;
				Отказ = НЕ НаборЗаписейКассыККМ.Расход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	Если ВалютаДокумента<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта документа (" + ВалютаДокумента + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+"). Ввод документа разрешен только в валюте регламентированного учета";
		СообщениеОбОшибке=дкДобавитьОшибку(?(СообщениеОбОшибке=Неопределено,"",СообщениеОбОшибке), ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
 
	Если КассаККМ.ВалютаДенежныхСредств<>Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		ТекстОшибки = "Валюта кассы ККМ (" + КассаККМ.ВалютаДенежныхСредств + ") не соответствует валюте регламентированного учета ("+Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()+").";
		СообщениеОбОшибке=дкДобавитьОшибку(?(СообщениеОбОшибке=Неопределено,"",СообщениеОбОшибке), ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	Если СообщениеОбОшибке=Неопределено Тогда
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	Иначе
		Если ТипЗнч(РезультатОбработки)=Тип("Строка") Тогда
			СообщениеОбОшибке=РезультатОбработки;
		Иначе
			Для Каждого СтрокаОшибки Из РезультатОбработки.Сообщения Цикл
				СообщениеОбОшибке=СообщениеОбОшибке+СтрокаОшибки.Сообщение+"
				|";
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

СуммаККМНаличные			= 0;
СуммаККМБезналичныеПриход	= 0;
СуммаККМБезналичныеВозврат	= 0;

СообщениеОбОшибке = Неопределено;
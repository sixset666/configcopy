// Модуль документа ПланФакт

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Получает параметр плана из ТЧ ПараметрыПлана документа ПланФакт
//
// Параметры:
//  НаименованиеПараметра - строка - наименование параметра плана.
//
Функция ПолучитьПараметрПлана(НаименованиеПараметра) Экспорт
	СтрокаПараметра=ПараметрыПлана.Найти(ПланыВидовХарактеристик.ПараметрыПланирования.НайтиПоНаименованию(НаименованиеПараметра).Ссылка, "ВидПараметра");
	Если СтрокаПараметра<>Неопределено Тогда
		ЗначениеПараметра=СтрокаПараметра.ЗначениеПараметра;
		Если НЕ обЗначениеНеЗаполнено(ЗначениеПараметра) Тогда
			Возврат ЗначениеПараметра;
		КонецЕсли;   
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции // ПолучитьПараметрПлана()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВидПлана",1);
	ОбязательныеРеквизиты.Вставить("ДатаНачала",1);
	ОбязательныеРеквизиты.Вставить("ДатаКонца",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("План","План",Истина);
	СписокХозОпераций.Добавить("Факт","Факт");
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Устанавливает границы периода планировании в соответствии со значением
// ПериодичностьПланирования только что выбранного реквизита документа ВидПлана.
// Не подлежит экспорту
//
Процедура ПолучитьПериодПланирования() Экспорт
	Если ВидПлана=Неопределено Тогда
		ДатаНачала=Неопределено;
		ДатаКонца=Неопределено;
	Иначе
		Если ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.День Тогда
			ДатаНачала=НачалоДня(Дата);
			ДатаКонца=КонецДня(Дата);
		ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Неделя Тогда
			ДатаНачала=НачалоНедели(Дата);
			ДатаКонца=КонецНедели(Дата);
		ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Месяц Тогда
			ДатаНачала=НачалоМесяца(Дата);
			ДатаКонца=КонецМесяца(Дата);
		ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Квартал Тогда
			ДатаНачала=НачалоКвартала(Дата);
			ДатаКонца=КонецКвартала(Дата);
		ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Год Тогда
			ДатаНачала=НачалоГода(Дата);
			ДатаКонца=КонецГода(Дата);
		Иначе
			ДатаНачала=Неопределено;
			ДатаКонца=Неопределено;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПолучитьПериодПланирования()

// Возвращает интервал
//
Функция ПолучитьИнтервалПрошлогоПериода()
	ДатаНачалаПланирования = ДатаНачала;
	ДатаКонцаПланирования = ДатаКонца;
	Если ВидПлана=Неопределено Тогда
		ДатаНачала_=Неопределено;
		ДатаКонца_=Неопределено;
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.День Тогда
		ДатаНачала_ = НачалоДня(ДатаНачалаПланирования-1);
		ДатаКонца_  = КонецДня(ДатаКонцаПланирования-1);
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Неделя Тогда
		ДатаНачала_ = НачалоНедели(ДатаНачалаПланирования-7);
		ДатаКонца_  = КонецНедели(ДатаКонцаПланирования-7);
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Месяц Тогда
		ДатаНачала_ = НачалоМесяца(ДобавитьМесяц(ДатаНачалаПланирования,-1));
		ДатаКонца_  = КонецМесяца(ДобавитьМесяц(ДатаКонцаПланирования,-1));
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Квартал Тогда
		ДатаНачала_ = НачалоКвартала(ДобавитьМесяц(ДатаНачалаПланирования,-3));
		ДатаКонца_  = КонецКвартала(ДобавитьМесяц(ДатаКонцаПланирования,-3));
	ИначеЕсли ВидПлана.ПериодичностьПланирования=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Год Тогда
		ДатаНачала_ = НачалоГода(ДобавитьМесяц(ДатаНачалаПланирования,-12));
		ДатаКонца_  = КонецГода(ДобавитьМесяц(ДатаКонцаПланирования,-12));
	КонецЕсли;
	
	Возврат Новый Структура("ДатаНачала, ДатаКонца", ДатаНачала_, ДатаКонца_)
КонецФункции // ПолучитьИнтервалПрошлогоПериода()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВидПлана" Тогда
		//проверим вид плана на работу с выбранным в документе подразделением
		ЭтотОбъект.Показатели.Очистить();
		Если ВидПлана.Подразделения.Найти(ПодразделениеКомпании,"Подразделение") = Неопределено Тогда
			Сообщить("Выбранный вид плана не рассчитан для работы с выбранным подразделением.");
			ВидПлана = Справочники.ВидыПлановКомпании.ПустаяСсылка();
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ВидПлана) Тогда
			ПолучитьПериодПланирования();
		КонецЕсли;
        #Если Клиент Тогда
		НастройкаФормы(ЭтаФорма);
		#КонецЕсли
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
Процедура ЗаполнитьПланФактИзМакета(НаименованиеОбработки)
	
КонецПроцедуры // ЗаполнитьПланФактИзМакета()

// Заполнение плана / факта из файла
//
// Параметры:
//  НаименованиеОбработки - строка - содержит путь к обработке и наименование.
//
Функция ЗаполнитьПланФактИзФайла(НаименованиеОбработки)
	//период планирования
	Если ХозОперация = Справочники.ХозОперации.План Тогда
		СтруктураПериода = ПолучитьИнтервалПрошлогоПериода();
		ДатаНачала_ = СтруктураПериода.ДатаНачала;
		ДатаКонца_  = СтруктураПериода.ДатаКонца;
	Иначе
		ДатаНачала_ = ДатаНачала;
		ДатаКонца_  = ДатаКонца;
	КонецЕсли;
		
	//читаем файл с текстом запроса
	ТекстЗапроса = Новый ТекстовыйДокумент;
	ТекстЗапроса.Прочитать(НаименованиеОбработки,КодировкаТекста.ANSI);
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса.ПолучитьТекст();
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала_);
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца_);
	Запрос.УстановитьПараметр("ВидПлана", ВидПлана);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("МинимальныйОстатокСсылка", Перечисления.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток);
	Если обЗначениеНеЗаполнено(ОбъектПланирования) Тогда
	Иначе
		Попытка	Запрос.УстановитьПараметр(ОбъектПланирования.Метаданные().Имя, ОбъектПланирования);	Исключение; КонецПопытки;
	КонецЕсли;
	
	//проходим по параметрам плана из ТЧ
	Для Каждого ТекПарам Из ПараметрыПлана Цикл
		Запрос.УстановитьПараметр(ТекПарам.ВидПараметра.Наименование, ТекПарам.ЗначениеПараметра);
	КонецЦикла;
	//выполняем запрос
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	//загружаем в ТЧ результат
	Показатели.Загрузить(РезультатЗапроса);
КонецФункции // ЗаполнитьПланФактИзФайла()

//Изменяем заголовки ТЧ "Показатели" и содержимое ТЧ "ПараметрыПлана"
//
// Параметры:
//  ЭтаФорма - форма - ссылка на форму документа.
//
Процедура НастройкаФормы(ЭтаФорма) Экспорт
	Если обЗначениеНеЗаполнено(ВидПлана) Тогда
		ЭтаФорма.ЭлементыФормы.ВыводПериодаПланирования.Заголовок="";
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования1.ТекстШапки="Вид аналитики планирования 1";
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования1.Видимость = Истина;
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования2.ТекстШапки="Вид аналитики планирования 2";
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования2.Видимость = Истина;
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана1.Видимость = Истина;
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана2.Видимость = Истина;
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана3.Видимость = Истина;
		// ПараметрыПлана оставляем как есть
	Иначе
		ЭтоПлан = (ХозОперация = Справочники.ХозОперации.План);
		//заполним период планирования для плана
		ЭтаФорма.ЭлементыФормы.ВыводПериодаПланирования.Заголовок="Период планирования: "+ЭтотОбъект.ВидПлана.ПериодичностьПланирования;
		Если обЗначениеНеЗаполнено(ВидПлана.ВидАналитикиПланирования1) Тогда
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования1.Видимость = Ложь;
			ЭтаФорма.ЭлементыФормы.ОбъектПланирования.ОграничениеТипа = Неопределено;
		Иначе
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования1.ТекстШапки=ВидПлана.ВидАналитикиПланирования1.Наименование;
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования1.Видимость = Истина;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ВидПлана.ВидАналитикиПланирования2) Тогда
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования2.Видимость = Ложь;
		Иначе
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования2.ТекстШапки=ВидПлана.ВидАналитикиПланирования2.Наименование;
		ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ВидАналитикиПланирования2.Видимость = Истина;
		КонецЕсли;
		Если ПустаяСтрока(ВидПлана.ПоказательПлана1) Тогда
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана1.Видимость = Ложь;
		Иначе
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана1.ТекстШапки=ВидПлана.ПоказательПлана1;
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана1.Видимость = Истина;
		КонецЕсли;
		Если ПустаяСтрока(ВидПлана.ПоказательПлана2) Тогда
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана2.Видимость = Ложь;
		Иначе
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана2.ТекстШапки=ВидПлана.ПоказательПлана2;
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана2.Видимость = Истина;
		КонецЕсли;
		Если ПустаяСтрока(ВидПлана.ПоказательПлана3) Тогда
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана3.Видимость = Ложь;
		Иначе
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана3.ТекстШапки=ВидПлана.ПоказательПлана3;
			ЭтаФорма.ЭлементыФормы.Показатели.Колонки.ПоказательПлана3.Видимость = Истина;
		КонецЕсли;
		
		//КоманднаяПанельПоказатели
		КнопкаОчистить = ЭтаФорма.ЭлементыФормы.КоманднаяПанельПоказатели.Кнопки.Заполнение.Кнопки.Найти("Очистить");
		ЭтаФорма.ЭлементыФормы.КоманднаяПанельПоказатели.Кнопки.Заполнение.Кнопки.Очистить();
		ЭтаФорма.ЭлементыФормы.КоманднаяПанельПоказатели.Кнопки.Заполнение.Кнопки.Добавить(КнопкаОчистить.Имя,КнопкаОчистить.ТипКнопки,КнопкаОчистить.Текст,КнопкаОчистить.Действие);
		Для Каждого ТекстОбработка Из ВидПлана.ОтчетыИОбработки Цикл
			//если это обработка по заполнению, то добавим в заполнение
			Если (ЭтоПлан) И (ТекстОбработка.ФлагЗаполненияПлана) Тогда
				ЭтаФорма.ЭлементыФормы.КоманднаяПанельПоказатели.Кнопки.Заполнение.Кнопки.Добавить(ТекстОбработка.Наименование,ТипКнопкиКоманднойПанели.Действие,ТекстОбработка.Наименование,Новый Действие("КоманднаяПанельПоказателиВыбор"));
			ИначеЕсли ТекстОбработка.ФлагЗаполненияФакта Тогда
				ЭтаФорма.ЭлементыФормы.КоманднаяПанельПоказатели.Кнопки.Заполнение.Кнопки.Добавить(ТекстОбработка.Наименование,ТипКнопкиКоманднойПанели.Действие,ТекстОбработка.Наименование,Новый Действие("КоманднаяПанельПоказателиВыбор"));
			КонецЕсли;
		КонецЦикла;
		// Получаем параметры плана из ТЧ справочника
		Для Каждого ПараметрПлана Из ВидПлана.ПараметрыПлана Цикл
			Если ПараметрыПлана.Найти(ПараметрПлана.ВидПараметра, "ВидПараметра")<>Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НовыйПараметрПлана=ПараметрыПлана.Добавить();
			НовыйПараметрПлана.ВидПараметра=ПараметрПлана.ВидПараметра;
			НовыйПараметрПлана.ЗначениеПараметра=ПараметрПлана.ВидПараметра.ТипЗначения.ПривестиЗначение(НовыйПараметрПлана.ЗначениеПараметра);
		КонецЦикла;			
		дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		Если НЕ обЗначениеНеЗаполнено(ВидПлана.ВидОбъектаПланирования) Тогда
			ОбъектПланирования=ВидПлана.ВидОбъектаПланирования.ТипЗначения.ПривестиЗначение(ОбъектПланирования);
			ЭтаФорма.ЭлементыФормы.ОбъектПланирования.Доступность=Истина;
		Иначе
			ЭтаФорма.ЭлементыФормы.ОбъектПланирования.Доступность=Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // НастройкаФормы()

//Заполняет показатели с помощью запроса из внешнего файла
//НаименованиеОбработки - путь и имя файла запроса
//Возвращает Ложь если запрос не выполнился, иначе возвращает Истина
//
// Параметры:
//  НаименованиеОбработки - строка - содержит путь и наименование обработки.
//
Функция ЗаполнитьПоказатели(НаименованиеОбработки) Экспорт
	Если Показатели.Количество()<>0 Тогда
		Если Вопрос("Перед заполнением таблица показатели будет очищена
				|Продолжить ?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет)<>КодВозвратаДиалога.Да Тогда
			Возврат Ложь;
		Иначе
			Показатели.Очистить();
		КонецЕсли;
	КонецЕсли;		
	ВидОбъекта=0;
	ВидОбъекта=ВидПлана.ОтчетыИОбработки.Найти(НаименованиеОбработки,"ПутьКОбъекту").ВидОбъекта;
	ЭтоПлан = (ХозОперация = Справочники.ХозОперации.План);
	Если ВидОбъекта=1 Тогда
		//Встроенный запрос из текстового макета
		Попытка
			ЗаполнитьПланФактИзМакета(НаименованиеОбработки);
	    Исключение
			Возврат Ложь;
		КонецПопытки;
	ИначеЕсли ВидОбъекта=2 Тогда
		//Внешний запрос из текстового файла
		Попытка
			ЗаполнитьПланФактИзФайла(НаименованиеОбработки);
	    Исключение
			Возврат Ложь;
		КонецПопытки;
	ИначеЕсли ВидОбъекта=3 Тогда
	//Встроенная обработка
		Попытка
			ОбработкаЗаполненияПоказателей = Обработки[НаименованиеОбработки].Создать();
			ОбработкаЗаполненияПоказателей.ДокументОбъект = ЭтотОбъект;
			Если (ЭтоПлан) Тогда
				ОбработкаЗаполненияПоказателей.ЗаполнитьПлан();
			Иначе
				ОбработкаЗаполненияПоказателей.ЗаполнитьФакт();
			КонецЕсли;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	ИначеЕсли ВидОбъекта=4 Тогда
	//Внешняя обработка
		Попытка
			ОбработкаЗаполненияПоказателей = ВнешниеОбработки.Создать(НаименованиеОбработки);
			ОбработкаЗаполненияПоказателей.ДокументОбъект = ЭтотОбъект;
			Если (ЭтоПлан) Тогда
				ОбработкаЗаполненияПоказателей.ЗаполнитьПлан();
			Иначе
				ОбработкаЗаполненияПоказателей.ЗаполнитьФакт();
			КонецЕсли;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	Возврат Истина;
КонецФункции // ЗаполнитьПоказатели()
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПланФакт"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПланФакт(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПланФакт");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.Подразделение = ПодразделениеКомпании;
	ОбластьМакета.Параметры.ВидПлана = ВидПлана;
	ОбластьМакета.Параметры.ПериодПланирования = "("+ВидПлана.ПериодичностьПланирования+") с: "+Формат(ДатаНачала,"ДФ=dd.MM.yy")+" по: "+Формат(ДатаКонца,"ДФ=dd.MM.yy");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Если НЕ обЗначениеНеЗаполнено(ОбъектПланирования) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ОбъектПланирования");
		ОбластьМакета.Параметры.ОбъектПланирования = ОбъектПланирования;
	    ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	Если ПараметрыПлана.Количество()>0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ПараметрыПлана");
	    ТабДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("ПараметрыПланаСтрока");
		Для Каждого ПараметрПлана Из ПараметрыПлана Цикл
			ОбластьМакета.Параметры.НомерСтроки = ПараметрПлана.НомерСтроки;
			ОбластьМакета.Параметры.ВидПараметра = ПараметрПлана.ВидПараметра;
			ОбластьМакета.Параметры.ЗначениеПараметра = ПараметрПлана.ЗначениеПараметра;
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;
	
	Если Показатели.Количество()>0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаЗаголовок");
		ТабДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПлана|Номер");
		ТабДокумент.Вывести(ОбластьМакета);
		Если НЕ обЗначениеНеЗаполнено(ВидПлана.ВидАналитикиПланирования1) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПлана|АналитикаПланирования1");
			ОбластьМакета.Параметры.ВидАналитикиШапка1 = ВидПлана.ВидАналитикиПланирования1;
			ТабДокумент.Присоединить(ОбластьМакета);
		Иначе
			ОбластьАналитика1 = Макет.Область("АналитикаПланирования2");
			ОбластьАналитика2 = Макет.Область("АналитикаПланирования1");
			ОбластьАналитика2.ШиринаКолонки = ОбластьАналитика2.ШиринаКолонки + ОбластьАналитика1.ШиринаКолонки;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ВидПлана.ВидАналитикиПланирования2) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПлана|АналитикаПланирования2");
			ОбластьМакета.Параметры.ВидАналитикиШапка2 = ВидПлана.ВидАналитикиПланирования2;
			ТабДокумент.Присоединить(ОбластьМакета);
		Иначе
			ОбластьАналитика2 = Макет.Область("АналитикаПланирования2");
			ОбластьАналитика1 = Макет.Область("АналитикаПланирования1");
			ОбластьАналитика1.ШиринаКолонки = ОбластьАналитика1.ШиринаКолонки + ОбластьАналитика2.ШиринаКолонки;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ВидПлана.ПоказательПлана1) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПлана|ПоказательПлана1");
			ОбластьМакета.Параметры.ПоказательПланаШапка1 = ВидПлана.ПоказательПлана1;
			ТабДокумент.Присоединить(ОбластьМакета);
		Иначе
			ОбластьПоказатель1 = Макет.Область("ПоказательПлана1");
			ОбластьПоказатель2 = Макет.Область("ПоказательПлана2");
			ОбластьПоказатель3 = Макет.Область("ПоказательПлана3");
			ОбластьПоказатель2.ШиринаКолонки = ОбластьПоказатель2.ШиринаКолонки + ОбластьПоказатель1.ШиринаКолонки/2;
			ОбластьПоказатель3.ШиринаКолонки = ОбластьПоказатель3.ШиринаКолонки + ОбластьПоказатель1.ШиринаКолонки/2;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ВидПлана.ПоказательПлана2) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПлана|ПоказательПлана2");
			ОбластьМакета.Параметры.ПоказательПланаШапка2 = ВидПлана.ПоказательПлана2;
			ТабДокумент.Присоединить(ОбластьМакета);
		Иначе
			ОбластьПоказатель1 = Макет.Область("ПоказательПлана1");
			ОбластьПоказатель2 = Макет.Область("ПоказательПлана2");
			ОбластьПоказатель3 = Макет.Область("ПоказательПлана3");
			ОбластьПоказатель1.ШиринаКолонки = ОбластьПоказатель1.ШиринаКолонки + ОбластьПоказатель2.ШиринаКолонки/2;
			ОбластьПоказатель3.ШиринаКолонки = ОбластьПоказатель3.ШиринаКолонки + ОбластьПоказатель2.ШиринаКолонки/2;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ВидПлана.ПоказательПлана3) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПлана|ПоказательПлана3");
			ОбластьМакета.Параметры.ПоказательПланаШапка3 = ВидПлана.ПоказательПлана3;
			ТабДокумент.Присоединить(ОбластьМакета);
		Иначе
			ОбластьПоказатель1 = Макет.Область("ПоказательПлана1");
			ОбластьПоказатель2 = Макет.Область("ПоказательПлана2");
			ОбластьПоказатель3 = Макет.Область("ПоказательПлана3");
			ОбластьПоказатель1.ШиринаКолонки = ОбластьПоказатель1.ШиринаКолонки + ОбластьПоказатель3.ШиринаКолонки/2;
			ОбластьПоказатель2.ШиринаКолонки = ОбластьПоказатель2.ШиринаКолонки + ОбластьПоказатель3.ШиринаКолонки/2;
		КонецЕсли;
		ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПлана|ОкончаниеСтроки");
		ТабДокумент.Присоединить(ОбластьМакета);
		
		
		Для Каждого Показатель Из Показатели Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаСтрока|Номер");
			ОбластьМакета.Параметры.НомерСтроки = Показатель.НомерСтроки;
			ТабДокумент.Вывести(ОбластьМакета);
			Если НЕ обЗначениеНеЗаполнено(ВидПлана.ВидАналитикиПланирования1) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаСтрока|АналитикаПланирования1");
				ОбластьМакета.Параметры.ВидАналитикиСтрока1 = Показатель.ВидАналитикиПланирования1;
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ВидПлана.ВидАналитикиПланирования2) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаСтрока|АналитикаПланирования2");
				ОбластьМакета.Параметры.ВидАналитикиСтрока2 = Показатель.ВидАналитикиПланирования2;
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ВидПлана.ПоказательПлана1) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаСтрока|ПоказательПлана1");
				ОбластьМакета.Параметры.ПоказательПланаСтрока1 = Формат(Показатель.ПоказательПлана1,"ЧЦ=10; ЧДЦ=3");
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ВидПлана.ПоказательПлана2) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаСтрока|ПоказательПлана2");
				ОбластьМакета.Параметры.ПоказательПланаСтрока2 = Формат(Показатель.ПоказательПлана2,"ЧЦ=10; ЧДЦ=3");
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ВидПлана.ПоказательПлана3) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаСтрока|ПоказательПлана3");
				ОбластьМакета.Параметры.ПоказательПланаСтрока3 = Формат(Показатель.ПоказательПлана3,"ЧЦ=10; ЧДЦ=3");
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
			ОбластьМакета = Макет.ПолучитьОбласть("ПоказателиПланаСтрока|ОкончаниеСтроки");
			ТабДокумент.Присоединить(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции // ПечатьПланФакт()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	//если План, будем вводить факт
	Если (ТипЗнч(Основание) = Тип("ДокументСсылка.ПланФакт")) и (ХозОперация = Справочники.ХозОперации.План) Тогда
		ХозОперация = Справочники.ХозОперации.Факт;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	Если ДатаНачала>ДатаКонца Тогда
		Отказ = Истина;
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
		Сообщить("Дата начала периода планирования должна быть меньше, чем дата конца", СтатусСообщения.Важное);
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	НаборЗаписейПланФакт=Движения.Планирование;
	НаборЗаписейПланФакт.ДокументОбъект=ЭтотОбъект;
    НаборЗаписейПланФакт.ВидПлана=ВидПлана;
	НаборЗаписейПланФакт.ПодразделениеКомпании=ПодразделениеКомпании;
	НаборЗаписейПланФакт.ДатаНачала=ДатаНачала;     
	НаборЗаписейПланФакт.ДатаКонца=ДатаКонца;
	НаборЗаписейПланФакт.Дата=Дата;
	
	Если ХозОперация.Наименование = "План" Тогда
		НаборЗаписейПланФакт.План();
		#Если Клиент Тогда
		Оповестить("ДокументПлан",""+Номер+Дата,);
		#КонецЕсли
	ИначеЕсли ХозОперация.Наименование = "Факт" Тогда
		НаборЗаписейПланФакт.Факт();
		#Если Клиент Тогда
		Оповестить("ДокументФакт",""+Номер+Дата);
		#КонецЕсли
	Иначе
		Сообщить("Ошибка! Неверная хозяйственная операция!", СтатусСообщения.Важное); 
		Возврат;		
	КонецЕсли;
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
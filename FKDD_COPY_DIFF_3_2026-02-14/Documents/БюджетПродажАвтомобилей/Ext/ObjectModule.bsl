// Модуль документа Бюджет продаж автомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Имя реквизита кода
Перем ВалютаУпр;                    // Валюта управленческого учета
Перем ВалютаРег;                    // Валюта регламентированного учета
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОсновнаяСтавкаНДС Экспорт;                // Основная ставка НДС

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	ОбязательныеРеквизитыТЧ = Новый Структура();
	ОбязательныеРеквизитыТЧ.Вставить("Модель",3);
	Если ХозОперация = Справочники.ХозОперации.БюджетПродажПоВариантамКомплектации Тогда
		ОбязательныеРеквизитыТЧ.Вставить("ВариантКомплектации", 2);
	КонецЕсли;
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("СценарийПланирования",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);  	
	ОбязательныеРеквизиты.Вставить("Автомобили", ОбязательныеРеквизитыТЧ);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Результат = Истина;	
	
	// Запретим запись с невыбранным сценарием планирования
	Если обЗначениеНеЗаполнено(СценарийПланирования) ИЛИ обЗначениеНеЗаполнено(СценарийПланирования.Периодичность) Тогда
		дкДобавитьОшибку(Ошибки, "Не выбран сценарий планирования, либо у выбранного сценария не выбрана периодичность планирования.");		
		Результат = Ложь;		
	КонецЕсли;
	
	// проверим правильность введенного подразделения хоз.операции.
	Результат = плПроверитьКорректностьВыбораПодразделенияИХозОперации(ХозОперация, ПодразделениеКомпании) И Результат;
	Если НЕ Результат Тогда
		дкДобавитьОшибку(Ошибки, "Для данного подразделения нельзя вводить данный вид бюджета.");		
	КонецЕсли;  
	
	Результат = (дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина) И Результат);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	
	Возврат Автомобили.Итог("СуммаВсегоУпр");
	
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СценарийПланирования" Тогда
		
		Параметры = Новый Структура;
		Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
		Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
		Параметры.Вставить("Действие", 0);
		плПолучитьДатыПланируемогоПериода(Параметры); 	
		ДатаПланирования = Параметры.ДатаНачала;  		
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Модель" Тогда
		
		ТекСтрока.Цена = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль,ВариантКомплектации", ТекСтрока.Модель, ТекСтрока.ВариантКомплектации), Дата, Справочники.Контрагенты.ПустаяСсылка(), Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		ОбработкаРеквизита("Автомобили.Цена", ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя="Автомобили.ВариантКомплектации" Тогда
		
		ТекСтрока.Цена = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль,ВариантКомплектации", ТекСтрока.Модель, ТекСтрока.ВариантКомплектации), Дата, Справочники.Контрагенты.ПустаяСсылка(), Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		ОбработкаРеквизита("Автомобили.Цена", ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя="Автомобили.Цена" Тогда
		
		СтараяСуммаУпр = ТекСтрока.СуммаВсегоУпр;
		НоваяСуммаУпр  = ТекСтрока.Количество * ТекСтрока.Цена;
		
		ТекСтрока.СуммаВсегоУпр = НоваяСуммаУпр;
		
		// Получим курсы упр. и рег. валюты на дату документа
		КоэфПересчета      = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * ОсновнаяСтавкаНДС/100;
		
	ИначеЕсли Имя="Автомобили.Количество" Тогда
		
		ОбработкаРеквизита("Автомобили.Цена"           , ТекСтрока, ЭтаФорма);
		ОбработкаРеквизита("Автомобили.НормативнаяЦена", ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя="Автомобили.СуммаВсегоУпр" Тогда
		
		КоэфПересчета       = плПолучитьКоэффициентПересчетаВалют(Дата);
		ТекСтрока.СуммаНДС  = ТекСтрока.СуммаВсегоУпр * КоэфПересчета * ОсновнаяСтавкаНДС/100;
		ТекСтрока.Цена 		= ?(ТекСтрока.Количество=0,ТекСтрока.СуммаВсегоУпр,ТекСтрока.СуммаВсегоУпр/ТекСтрока.Количество);
		
	ИначеЕсли Имя="Автомобили.СебестоимостьУпр" Тогда
		
		ТекСтрока.НормативнаяЦена = ?(ТекСтрока.Количество=0,ТекСтрока.СебестоимостьУпр,ТекСтрока.СебестоимостьУпр/ТекСтрока.Количество);
		
	ИначеЕсли Имя="Автомобили.НормативнаяЦена" Тогда
		ТекСтрока.СебестоимостьУпр = ТекСтрока.НормативнаяЦена * ТекСтрока.Количество;
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("БюджетПродажПоМоделям"   ,"По моделям",Истина);
	СписокХозОпераций.Добавить("БюджетПродажПоВариантамКомплектации","По вариантам комплектации",Ложь);	
	Возврат СписокХозОпераций;
	
КонецФункции // ПолучитьСписокХозОпераций()

// Процедура формирует строку с данными последнего заполнения
Процедура СформироватьСтрокуПоследнегоЗаполнения(ЭтаФорма, СпособЗаполнения=-1) Экспорт
	
	Если СпособЗаполнения=-1 Тогда
		СпособЗаполнения = СпособПоследнегоЗаполнения;
	Иначе
		СпособПоследнегоЗаполнения = СпособЗаполнения;
	КонецЕсли;	
	
	Если СпособЗаполнения=-2 Тогда
		ЭтаФорма.ЭлементыФормы.НадписьПараметры.Заголовок = "";	
		Возврат;
	КонецЕсли;
	
	Текст = "";
	
	Если СпособЗаполнения=0 Тогда
		
		Текст = "Тип анализа: " + СокрЛП(ТипАнализа);
		Текст = Текст + " (Коэф.роста=" + СокрЛП(КоэффициентРоста) + ";  Коэф.сез.=" + СокрЛП(КоэффициентСезонности) + ")" + Символы.ПС;
		Текст = Текст + "Показатель планирования: " + ?(ПоказательПланирования, "Количество", "Сумма (упр.)") + Символы.ПС;
		Текст = Текст + "Периоды планирования: " + плВывестиПредставлениеПериода(ЭтаФорма, СценарийПланирования.Периодичность, ДатаПланирования);
		
	ИначеЕсли СпособЗаполнения=1 Тогда
		
		Текст = Текст + "Модель: " + СокрЛП(МодельПрогнозирования);
		
		Если МодельПрогнозирования=Перечисления.МоделиПрогнозирования.Сглаживанием Тогда
			
			Текст = Текст + "; Постоянная уровня: " + СокрЛП(Параметр1) + "; Постоянная тренда: "+ СокрЛП(Параметр2);
			Если ( (СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Месяц) И
				(КоличествоПериодов >= 24) ) 
				ИЛИ				
				( (СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Квартал) И
				(КоличествоПериодов >= 8)   ) Тогда  
				Текст = Текст + "; Постоянная сезонности: " + СокрЛП(Параметр3);
			КонецЕсли;
			
		ИначеЕсли МодельПрогнозирования=Перечисления.МоделиПрогнозирования.Полиномиальный Тогда
			Текст = Текст + "; Степень полинома: " + СокрЛП(Параметр1);
		КонецЕсли;
		Текст = Текст + Символы.ПС + "Показатель планирования:  " + ?(ПоказательПланирования,"Количество","Сумма (упр.)");
		Текст = Текст + Символы.ПС + "Периоды планирования: " + плВывестиПредставлениеПериода(ЭтаФорма, СценарийПланирования.Периодичность, ДатаПланирования);
		
	ИначеЕсли СпособЗаполнения=2 Тогда		
		Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
			Текст = "";
		Иначе
			Текст = Текст + "Док.основание:  " + ДокументОснование;
			Если ХозОперация=Справочники.ХозОперации.БюджетПродажПоВариантамКомплектации И ДокументОснование.ХозОперация=Справочники.ХозОперации.БюджетПродажПоМоделям Тогда
				Текст = Текст + Символы.ПС + "Метод распределения суммы продаж категорий:  " + СокрЛП(МетодыРаспределенияКатегорий);
				Текст = Текст + Символы.ПС + "Изменяемый показатель:  " + ?(ПараметрУправленияРаспределением=0, "Количество", "Цена");
			КонецЕсли;
		КонецЕсли; 			
	КонецЕсли;
	
	ЭтаФорма.ЭлементыФормы.НадписьПараметры.Заголовок = Текст;
	
КонецПроцедуры // СформироватьСтрокуПоследнегоЗаполнения()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьБюджетПродажАвтомобилей(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("БюджетПродажАвтомобилей");
	Если НЕ ПланированиеСебестоимости Тогда
		ОбластьМодель = Макет.Область(1,Макет.Область("Модель").Право);
		ОбластьВариантКомплектации = Макет.Область(1,Макет.Область("ВариантКомплектации").Право);
		ОбластьСебестоимость = Макет.Область("Себестоимость");
		ШиринаКолонокСебестоимости = 0;
		Для Сч = ОбластьСебестоимость.Лево По ОбластьСебестоимость.Право-1 Цикл
			ШиринаКолонокСебестоимости = ШиринаКолонокСебестоимости + Макет.Область(1, Сч).ШиринаКолонки;
		КонецЦикла;
		ОбластьМодель.ШиринаКолонки = ОбластьМодель.ШиринаКолонки + ШиринаКолонокСебестоимости;
		ОбластьВариантКомплектации.ШиринаКолонки = ОбластьВариантКомплектации.ШиринаКолонки + Макет.Область(1, ОбластьСебестоимость.Право).ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьСебестоимость,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//ЗАГОЛОВОК
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПодразделениеКомпании = спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПодразделениеКомпании = ПодразделениеКомпании;
	ОбластьМакета.Параметры.ПредставлениеСценарийПланирования = спПолучитьНаименование(СценарийПланирования);
	ОбластьМакета.Параметры.СценарийПланирования = СценарийПланирования;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДатаИзПериода", ДатаПланирования);
	Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
	Параметры.Вставить("Действие", 0);
	ПредставлениеПериодаПланирования = плПолучитьДатыПланируемогоПериода(Параметры);		
	ОбластьМакета.Параметры.ПредставлениеПериодаПланирования = ПредставлениеПериодаПланирования;
	ТабДокумент.Вывести(ОбластьМакета);
	
	// установим следующий номер страницы
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//Получение областей макета
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//Обработка областей
	ПоМоделям = (ХозОперация = Справочники.ХозОперации.БюджетПродажПоМоделям);
	Если ПоМоделям Тогда
		ОбластьШапкаТаблицы.Область(2,3,2,4).Объединить();
		ОбластьСтрока.Область(1,3,1,4).Объединить();
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы	 = "Страница: " + НомерСтраницы;
	
	СтруктураИтоговПоСтранице = Новый Структура;
	СтруктураИтоговПоСтранице.Вставить("Количество", 0);
	СтруктураИтоговПоСтранице.Вставить("СуммаВсегоУпр", 0);
	СтруктураИтоговПоСтранице.Вставить("СуммаНДС", 0);
	Если ПланированиеСебестоимости Тогда
		СтруктураИтоговПоСтранице.Вставить("СебестоимостьУпр", 0);
		СтруктураИтоговПоСтранице.Вставить("МаржинальнаяПрибыль", 0);
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТаблицаАвтомобили.Модель КАК Модель,
	|	ТаблицаАвтомобили.ВариантКомплектации,
	|	ТаблицаАвтомобили.Цена КАК Цена,
	|	ТаблицаАвтомобили.Количество КАК Количество,
	|	ТаблицаАвтомобили.СуммаВсегоУпр КАК СуммаВсегоУпр,
	|	ТаблицаАвтомобили.СуммаВсегоУпр - ТаблицаАвтомобили.СебестоимостьУпр КАК МаржинальнаяПрибыль,
	|	ТаблицаАвтомобили.НормативнаяЦена КАК НормативнаяЦена,
	|	ТаблицаАвтомобили.СебестоимостьУпр КАК СебестоимостьУпр,
	|	ТаблицаАвтомобили.СуммаНДС КАК СуммаНДС
	|ИЗ
	|	Документ.БюджетПродажАвтомобилей.Автомобили КАК ТаблицаАвтомобили
	|ГДЕ
	|	ТаблицаАвтомобили.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаАвтомобили.НомерСтроки";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ВыборкаТабличнойЧасти = Запрос.Выполнить().Выбрать();
	
	КоличествоПозиций = ВыборкаТабличнойЧасти.Количество()-1;
	ИндексСтроки = 0;
	//перебор строк 	
	
	Пока ВыборкаТабличнойЧасти.Следующий() Цикл
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ИндексСтроки = КоличествоПозиций Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		ИндексСтроки = ИндексСтроки+1;
		ОбластьСтрока.Параметры.НомерСтроки = ИндексСтроки;
		ОбластьСтрока.Параметры.МодельНаименование = спПолучитьНаименование(ВыборкаТабличнойЧасти.Модель);
		ОбластьСтрока.Параметры.Модель = ВыборкаТабличнойЧасти.Модель;
		Если НЕ ПоМоделям Тогда
			ОбластьСтрока.Параметры.ВариантКомплектацииНаименование = спПолучитьНаименование(ВыборкаТабличнойЧасти.ВариантКомплектации);
			ОбластьСтрока.Параметры.ВариантКомплектации = ВыборкаТабличнойЧасти.ВариантКомплектации;
		КонецЕсли;
		ОбластьСтрока.Параметры.Количество	= Формат(ВыборкаТабличнойЧасти.Количество, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена		= Формат(ВыборкаТабличнойЧасти.Цена, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаУпр	= Формат(ВыборкаТабличнойЧасти.СуммаВсегоУпр, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС	= Формат(ВыборкаТабличнойЧасти.СуммаНДС, ФорматВыводаСуммы);
		Если ПланированиеСебестоимости Тогда
			ОбластьСтрока.Параметры.НормативнаяЦена		= Формат(ВыборкаТабличнойЧасти.НормативнаяЦена, ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.СебестоимостьУпр	= Формат(ВыборкаТабличнойЧасти.СебестоимостьУпр, ФорматВыводаСуммы);
			ОбластьСтрока.Параметры.МаржинальнаяПрибыль	= Формат(ВыборкаТабличнойЧасти.МаржинальнаяПрибыль, ФорматВыводаСуммы);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапкаТаблицы, ОбластьИтогоПоСтранице,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			// обнулим структуру
			Для каждого стрСтруктуры Из СтруктураИтоговПоСтранице Цикл
				СтруктураИтоговПоСтранице.Вставить(стрСтруктуры.Ключ, 0);		
			КонецЦикла;
			НомерСтраницыПред = НомерСтраницы;
			//заполним параметры шапки таблицы для следующего листа  
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(ВыборкаТабличнойЧасти, СтруктураИтоговПоСтранице);		
	КонецЦикла;  	
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтогоПоСтранице, СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// ИТОГ ПО ДОКУМЕНТУ
	ИтогоКоличество		= Автомобили.Итог("Количество");
	ИтогоСуммаВсегоУпр	= Автомобили.Итог("СуммаВсегоУпр");
	ИтогоСуммаНДС     	= Автомобили.Итог("СуммаНДС");
	
	ОбластьПодвал.Параметры.ИтогоКоличество = Формат(ИтогоКоличество, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ИтогоСуммаВсегоУпр = Формат(ИтогоСуммаВсегоУпр, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ИтогоСуммаНДС = Формат(ИтогоСуммаНДС, ФорматВыводаСуммы);
	
	Если ПланированиеСебестоимости Тогда
		ИтогоСебестоимостьУпр    = Автомобили.Итог("СебестоимостьУпр");
		ИтогоМаржинальнаяПрибыль = ИтогоСуммаВсегоУпр-ИтогоСебестоимостьУпр;
		ОбластьПодвал.Параметры.ИтогоСебестоимостьУпр = Формат(ИтогоСебестоимостьУпр, ФорматВыводаСуммы);
		ОбластьПодвал.Параметры.ИтогоМаржинальнаяПрибыль = Формат(ИтогоМаржинальнаяПрибыль, ФорматВыводаСуммы);
	КонецЕсли;
	
	Если ПоМоделям Тогда
		ОбластьПодвал.Параметры.СуммаПрописью = "Всего запланировано позиций моделей "+Строка(ИндексСтроки)+" на сумму " + обЧислоПрописью(ИтогоСуммаВсегоУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	Иначе
		ОбластьПодвал.Параметры.СуммаПрописью = "Всего запланировано позиций вариантов комплектации "+Строка(ИндексСтроки)+" на сумму " + обЧислоПрописью(ИтогоСуммаВсегоУпр, Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	КонецЕсли; 
	ОбластьПодвал.Параметры.ПредставлениеАвтор = спПолучитьНаименование(Автор);
	ОбластьПодвал.Параметры.Автор = Автор;
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);  	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьБюджетПродажАвтомобилей()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если обЗначениеНеЗаполнено(Основание) ИЛИ ТипЗнч(Основание) = Тип("Структура") Тогда
		дкОбработкаЗаполнения(ЭтотОбъект, Основание);	
		Возврат;
	КонецЕсли;
	
	ОснованиеПоВариантамКомплектации = Основание.ХозОперация=Справочники.ХозОперации.БюджетПродажПоВариантамКомплектации;
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.БюджетПродажАвтомобилей") Тогда
		Если ОснованиеПоВариантамКомплектации Тогда
			ХозОперация = Справочники.ХозОперации.БюджетПродажПоВариантамКомплектации;
		Иначе
			СписокХО = ПолучитьСписокХозОпераций();
			Для Каждого ХО Из СписокХО Цикл
				Попытка ХО.Представление = Справочники.ХозОперации[ХО.Значение].Наименование; Исключение КонецПопытки;
			КонецЦикла;
			
			ВыбХО = СписокХО.ВыбратьЭлемент("Выберите вид хозяйственной операции");
			
			// смотрим что выбрали
			Если ВыбХО=Неопределено Тогда 
				ХозОперация = Справочники.ХозОперации.БюджетПродажПоВариантамКомплектации;
			Иначе 
				ХозОперация = Справочники.ХозОперации[ВыбХО.Значение]		
			КонецЕсли;				
		КонецЕсли;  			
	Иначе
		Возврат;
	КонецЕсли;	
	
	// Стандартная процедура заполнения
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	ДокументПоВариантамКомплектации = (ХозОперация=Справочники.ХозОперации.БюджетПродажПоВариантамКомплектации);
	ДокументОснование          = Основание;
	СпособПоследнегоЗаполнения = 2;	
	ДатаПланирования           = НачалоМесяца(ТекущаяДата());
	ТипАнализа                 = Перечисления.ТипыАнализа.ПоФактическимДаннымПродаж;
	
	// очистим табличную часть, т.к. будет открыта форма заполнения по документу основанию.
	Автомобили.Очистить(); 
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)	
	СебестоимостьУпрИтого = Автомобили.Итог("СебестоимостьУпр");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , , ,"10111") Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли; 
	
	// Собственно само проведение
	НаборЗаписей = Движения.БюджетПродажАвтомобилей;
	НаборЗаписей.ПодразделениеКомпании = ПодразделениеКомпании;
	НаборЗаписей.ДокументОбъект        = Ссылка;
	НаборЗаписей.РежимПроведения       = Режим;
	
	Отказ = НЕ НаборЗаписей.Приход();
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;  
	
	дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);  	
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ
ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
ВалютаРег = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(); 
ОсновнаяСтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС.Ставка;

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

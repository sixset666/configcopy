// Модуль документа ПоступлениеДопРасходов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	
	Результат = Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Комплект,         0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Набор,            0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Тара,             0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Товар,            0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,     0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Диски,            0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.НомерныеАгрегаты, 0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Шины,             0);
	// MIKOLV
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ЛКМ,              0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Автомобили,       0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Опции,              0);
	// MIKOLV


	
	Возврат Результат;
	
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",1);
	ОбязательныеТовары.Вставить("Коэффициент",1);

	// Обязательные поля таблицы документов-основания
	ОбязательныеДокументыОснования=Новый Структура();
	ОбязательныеДокументыОснования.Вставить("ДокументОснование",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	//ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("ДокументыОснования",ОбязательныеДокументыОснования);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// У номенклатуры в табличной части, в обязательном порядке должен быть заполнен реквизит 
	// "СпособРаспределенияДопРасходов".
	Для Каждого ТекСтрока Из Товары Цикл  	
		Если ТекСтрока.Номенклатура.СпособРаспределенияДопРасходов.Пустая() Тогда
			дкДобавитьОшибку(Ошибки,"Таблица <Товары>. У номенклатуры не заполнен реквизит ""Способ распределения доп. расходов"". Строка: " + ТекСтрока.НомерСтроки);
			Результат = Ложь;	
		КонецЕсли;
	КонецЦикла;
	
	Если ДокументыОснования.Количество()=0 Тогда
		Результат = Ложь;
		дкДобавитьОшибку(Ошибки,"Таблица <Документы основания> не содержит данных!");
	КонецЕсли;
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДокументОснование");
			КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	Результат = Неопределено;
	
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПоступлениеДопРасходов","Поступление дополнительных расходов",Истина);
	СписокХозОпераций.Добавить("ПоступлениеДопРасходовВнутреннее","Поступление внутренних дополнительных расходов",Ложь);
	СписокХозОпераций.Добавить("ПоступлениеДопРасходовНаАвтомобили","Поступление дополнительных расходов на автомобили",Ложь);
	СписокХозОпераций.Добавить("ПоступлениеДопРасходовНаАвтомобилиВнутреннее","Поступление внутренних доп. расходов на автомобили",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Документ.Ссылка - Ссылка на документ для которого получаем шапку
//
// Возвращаемое значение:
//  Выборка - возвращает выборку по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.СуммаДокумента КАК СуммаДокумента
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

Процедура РассчетДополнительныхРасходовНаАвтомобили(ТаблицаАвтомобилей,Ссылка,Отказ)
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// Скопируем таблицу т\с
	ВремТабл = ТаблицаАвтомобилей.Скопировать();
	Попытка
		ВремТабл.Колонки.Добавить("ДопРасходы",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
		ВремТабл.Колонки.Добавить("НДСДопРасходов",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)));
		ВремТабл.Колонки.Добавить("СтавкаНДС",Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	Исключение
	КонецПопытки;
	// очистим таблицу
	ВремТабл.Очистить();
	
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоСумме ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоСумме,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоСумме ТОГДА ДокументТовары.СуммаНДС ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоСуммеНДС,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоКоличеству ТОГДА ДокументТовары.СуммаНДС ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоКоличествуНДС,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоКоличеству ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоКоличеству,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоВесу ТОГДА ДокументТовары.СуммаНДС ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоВесуНДС,
	|	СУММА(ЕСТЬNULL(ВЫБОР КОГДА ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов=&ПоВесу ТОГДА ДокументТовары.СуммаВсего ИНАЧЕ 0 КОНЕЦ,0)) КАК ПоВесу,
	|	ДокументТовары.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Документ."+Ссылка.Метаданные().Имя+".Товары КАК ДокументТовары
	|ГДЕ
	|	  ДокументТовары.Ссылка=&Ссылка
	|	И ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов<>&НаДоходыРасходы
	|	И ДокументТовары.Номенклатура.ВидНоменклатуры=&Услуга
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.СтавкаНДС
	|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("ПоСумме",Перечисления.СпособыРаспределенияДопРасходов.ПоСумме);
	Запрос.УстановитьПараметр("ПоКоличеству",Перечисления.СпособыРаспределенияДопРасходов.ПоКоличеству);
	Запрос.УстановитьПараметр("ПоВесу",Перечисления.СпособыРаспределенияДопРасходов.ПоВесу);
	Запрос.УстановитьПараметр("НаДоходыРасходы",Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы);
	Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
	РезультатДопРасходы=Запрос.Выполнить();
	
	Если РезультатДопРасходы.Пустой() Тогда 
		ТаблицаАвтомобилей=ВремТабл;
		Возврат;
	КонецЕсли;
	ВыборкаДопРасходы=РезультатДопРасходы.Выбрать();
	
	Пока ВыборкаДопРасходы.Следующий() Цикл
		
		ИтогДопРасходы=?(ВыборкаДопРасходы.ПоКоличеству=NULL,0,ВыборкаДопРасходы.ПоКоличеству)+?(ВыборкаДопРасходы.ПоСумме=NULL,0,ВыборкаДопРасходы.ПоСумме)+?(ВыборкаДопРасходы.ПоВесу=NULL,0,ВыборкаДопРасходы.ПоВесу);
		Если ИтогДопРасходы=0 Тогда ТаблицаАвтомобилей=ВремТабл; Возврат; КонецЕсли;
		ИтогДопРасходыНДС=?(ВыборкаДопРасходы.ПоКоличествуНДС=NULL,0,ВыборкаДопРасходы.ПоКоличествуНДС)+?(ВыборкаДопРасходы.ПоСуммеНДС=NULL,0,ВыборкаДопРасходы.ПоСуммеНДС)+?(ВыборкаДопРасходы.ПоВесуНДС=NULL,0,ВыборкаДопРасходы.ПоВесуНДС);
		
		Если ВыборкаДопРасходы.ПоВесу<>0 Тогда
			дкСообщитьРезультат("Поступление допрасходов по весу для автомобилей недопустимо.","Важное&Остатки автомобилей:",ЭтотОбъект);
			ТаблицаАвтомобилей=ВремТабл;
			Отказ=Истина; Возврат;
		КонецЕсли; 
		
		ИтогДокументКоличество=ТаблицаАвтомобилей.Итог("Количество");;
		ИтогДокументСумма=ТаблицаАвтомобилей.Итог("СуммаВсего"); 
		Для Каждого СтрокаАвтомобильИсх Из ТаблицаАвтомобилей Цикл
			СтрокаАвтомобиль = ВремТабл.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаАвтомобиль,СтрокаАвтомобильИсх);
			
			СтрокаАвтомобиль.СтавкаНДС=ВыборкаДопРасходы.СтавкаНДС;
			
			СуммаДопРасходов=0; СуммаНДСДопРасходов=0;
			
			СуммаДопРасходовК=ВыборкаДопРасходы.ПоКоличеству*?(ИтогДокументКоличество=0,0,СтрокаАвтомобильИсх.Количество/ИтогДокументКоличество);
			СуммаНДСДопРасходовК=ВыборкаДопРасходы.ПоКоличествуНДС*?(ИтогДокументКоличество=0,0,СтрокаАвтомобильИсх.Количество/ИтогДокументКоличество);
			
			СуммаДопРасходовС=ВыборкаДопРасходы.ПоСумме*?(СтрокаАвтомобиль.СуммаВсего=NULL ИЛИ ИтогДокументСумма=0,0,СтрокаАвтомобильИсх.СуммаВсего/ИтогДокументСумма);
			СуммаНДСДопРасходовС=ВыборкаДопРасходы.ПоСуммеНДС*?(СтрокаАвтомобиль.СуммаВсего=NULL ИЛИ ИтогДокументСумма=0,0,СтрокаАвтомобильИсх.СуммаВсего/ИтогДокументСумма);
			
			СтрокаАвтомобиль.ДопРасходы=СуммаДопРасходовК+СуммаДопРасходовС;
			СтрокаАвтомобиль.НДСДопРасходов=СуммаНДСДопРасходовК+СуммаНДСДопРасходовС;
			
			ИтогДопРасходы = ИтогДопРасходы-(СтрокаАвтомобиль.ДопРасходы);
			ИтогДопРасходыНДС = ИтогДопРасходыНДС-(СтрокаАвтомобиль.НДСДопРасходов);
		КонецЦикла;
		
		// если осталась сумма от округления то закинем ее на последний
		Если ИтогДопРасходы<>0 ИЛИ ИтогДопРасходыНДС<>0 Тогда
			Попытка
				СтрокаАвтомобиль.ДопРасходы=СтрокаАвтомобиль.ДопРасходы+ИтогДопРасходы;
				СтрокаАвтомобиль.НДСДопРасходов=СтрокаАвтомобиль.НДСДопРасходов+ИтогДопРасходыНДС;
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	ТаблицаАвтомобилей=ВремТабл;
КонецПроцедуры // РассчетДополнительныхРасходовНаАвтомобили()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим, ДокументСсылка) Экспорт
	
	Отказ = Ложь;
	НаборЗаписейДиР       = Движения.ДоходыИрасходы;
	СуммаДляРаспределения = 0; // Сумма для распределения по доходам и расходам.
	ВедетсяБалансПоПодразделению = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
	
	// получим шапку документа
	ШапкаДокумента = ПолучитьШапкуДокумента(ДокументСсылка);
	ТаблицаДокументыОснования = ШапкаДокумента.Ссылка.ДокументыОснования.Выгрузить();
	
	ФлагУпрВалюты = (ШапкаДокумента.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
		НаборЗаписейДоходыИРасходы.Расход = ШапкаДокумента.СуммаДокумента;
		НаборЗаписейДоходыИРасходы.Приход();
		Возврат НЕ Отказ;
	КонецЕсли;
	
	///////////////////////////////////////////////////////////
	// Секция Альфа-Авто: Начало
	///////////////////////////////////////////////////////////
	Если КонтрольОстаткаПартии Тогда
		//Для "новых" документов проверим остатки партии
		//Проверим, а есть ли услуги, распределяемые на себестоимость
		Запрос=Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПоступлениеДопРасходовТовары.Номенклатура
		|ИЗ
		|	Документ.ПоступлениеДопРасходов.Товары КАК ПоступлениеДопРасходовТовары
		|ГДЕ
		|	ПоступлениеДопРасходовТовары.Ссылка = &Ссылка
		|	И ПоступлениеДопРасходовТовары.Номенклатура.СпособРаспределенияДопРасходов <> ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияДопРасходов.НаДоходыИРасходы)";
		
		Запрос.УстановитьПараметр("Ссылка", ШапкаДокумента.Ссылка);
		
		ЕстьРаспределениеНаСебестоимость = НЕ Запрос.Выполнить().Пустой();
		Если ЕстьРаспределениеНаСебестоимость Тогда
			РаспределяемНаТовары = Ложь;
			ВариантУчетаПоПартиям = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
			
			Для каждого СтрокаДокументаОснования Из ТаблицаДокументыОснования Цикл
				//Проверим остатки партии
				Если ШапкаДокумента.ХозОперация = Справочники.ХозОперации.ПоступлениеДопРасходов ИЛИ
					ШапкаДокумента.ХозОперация = Справочники.ХозОперации.ПоступлениеДопРасходовВнутреннее Тогда
					
					ТекстЗапроса =
					"ВЫБРАТЬ
					|	ПартииОбороты.Номенклатура,
					|	ПартииОбороты.ХарактеристикаНоменклатуры,
					|	ПартииОбороты.СтатусПартии,
					|	ПартииОбороты.Партия,
					|	СУММА(ПартииОбороты.Количество) КАК КоличествоПриход
					|ПОМЕСТИТЬ
					|	ПартииОбороты
					|ИЗ
					|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииОбороты
					|ГДЕ
					//|	ПартииОбороты.Партия        = &Регистратор И
					|"+?(ВариантУчетаПоПартиям=Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя,"ПартииОбороты.Регистратор=&Регистратор И","ПартииОбороты.Партия=&Регистратор И")+"
					|	ПартииОбороты.СкладКомпании = &СкладКомпании И
					|	ПартииОбороты.ВидДвижения   = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И
					|	ПартииОбороты.МоментВремени	<= &НаМомент 
					|СГРУППИРОВАТЬ ПО
					|	ПартииОбороты.Номенклатура,
					|	ПартииОбороты.ХарактеристикаНоменклатуры,
					|	ПартииОбороты.СтатусПартии,
					|	ПартииОбороты.Партия
					|ИМЕЮЩИЕ
					|	СУММА(ПартииОбороты.Количество)<>0
					|;
					|
					|ВЫБРАТЬ
					|	ПартииОбороты.Номенклатура КАК Номенклатура,
					|	ПартииОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					|	ЕСТЬNULL(СУММА(ПартииОбороты.КоличествоПриход), 0) КАК КоличествоПриход,
					|	ЕСТЬNULL(СУММА(ПартииОстатки.КоличествоОстаток), 0) КАК КоличествоОстаток
					|ИЗ
					|	ПартииОбороты КАК ПартииОбороты
					|ЛЕВОЕ СОЕДИНЕНИЕ
					|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&НаМомент, СкладКомпании = &СкладКомпании И 
					|	(Номенклатура, ХарактеристикаНоменклатуры, СтатусПартии, Партия) В (ВЫБРАТЬ Номенклатура, ХарактеристикаНоменклатуры, СтатусПартии, Партия ИЗ ПартииОбороты)) КАК ПартииОстатки
					|ПО
					|	ПартииОбороты.Номенклатура = ПартииОстатки.Номенклатура И 
					|	ПартииОбороты.ХарактеристикаНоменклатуры = ПартииОстатки.ХарактеристикаНоменклатуры И 
					|	ПартииОбороты.СтатусПартии = ПартииОстатки.СтатусПартии И 
					|	ПартииОбороты.Партия = ПартииОстатки.Партия
					|СГРУППИРОВАТЬ ПО
					|	ПартииОбороты.Номенклатура,
					|	ПартииОбороты.ХарактеристикаНоменклатуры";
					
					РаспределяемНаТовары=Истина;
				ИначеЕсли ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили ИЛИ
					ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобилиВнутреннее Тогда
					
					ТекстЗапроса =
					"ВЫБРАТЬ
					|	ПартииОбороты.Автомобиль,
					|	ПартииОбороты.СтатусПартии,
					|	ПартииОбороты.Партия,
					|	ПартииОбороты.ГТД,
					|	СУММА(ПартииОбороты.Количество) КАК КоличествоПриход
					|ПОМЕСТИТЬ
					|	ПартииОбороты
					|ИЗ
					|	РегистрНакопления.ОстаткиАвтомобилей КАК ПартииОбороты
					|ГДЕ
					|	ПартииОбороты.Регистратор   = &Регистратор И 
					|	ПартииОбороты.СкладКомпании = &СкладКомпании И 
					|	ПартииОбороты.ВидДвижения   = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И 
					|	ПартииОбороты.Количество > 0 И
					|	ПартииОбороты.МоментВремени	<= &НаМомент 
					|СГРУППИРОВАТЬ ПО
					|	ПартииОбороты.Автомобиль,
					|	ПартииОбороты.СтатусПартии,
					|	ПартииОбороты.Партия,
					|	ПартииОбороты.ГТД
					|ИМЕЮЩИЕ
					|	СУММА(ПартииОбороты.Количество)<>0
					|;
					|
					|ВЫБРАТЬ
					|	ПартииОбороты.Автомобиль КАК Номенклатура,
					|	ЕСТЬNULL(СУММА(ПартииОбороты.КоличествоПриход), 0) КАК КоличествоПриход,
					|	ЕСТЬNULL(СУММА(ПартииОстатки.КоличествоОстаток), 0) КАК КоличествоОстаток
					|ИЗ
					|	ПартииОбороты КАК ПартииОбороты
					|ЛЕВОЕ СОЕДИНЕНИЕ
					|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&НаМомент, СкладКомпании = &СкладКомпании И 
					|	(Автомобиль, СтатусПартии, Партия, ГТД) В (ВЫБРАТЬ Автомобиль, СтатусПартии, Партия, ГТД ИЗ ПартииОбороты)) КАК ПартииОстатки
					|ПО
					|	ПартииОбороты.Автомобиль = ПартииОстатки.Автомобиль И 
					|	ПартииОбороты.СтатусПартии = ПартииОстатки.СтатусПартии И 
					|	ПартииОбороты.Партия = ПартииОстатки.Партия И
					|	ПартииОбороты.ГТД = ПартииОстатки.ГТД
					|СГРУППИРОВАТЬ ПО
					|	ПартииОбороты.Автомобиль";
					
				КонецЕсли;
				
				Запрос = Новый Запрос;
				Запрос.Текст = ТекстЗапроса;
				Запрос.УстановитьПараметр("НаМомент",    ШапкаДокумента.МоментВремени);
				
				Если ВариантУчетаПоПартиям = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
					//Запрос.УстановитьПараметр("Регистратор", Константы.ПартияТоваровОтрицательныхОстатков.Получить());
					Запрос.УстановитьПараметр("Регистратор", СтрокаДокументаОснования.ДокументОснование);
				Иначе
					Запрос.УстановитьПараметр("Регистратор", СтрокаДокументаОснования.ДокументОснование);
				КонецЕсли;
				
				Если ТипЗнч(СтрокаДокументаОснования.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеАвтомобилей") ИЛИ ТипЗнч(СтрокаДокументаОснования.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
					Запрос.УстановитьПараметр("СкладКомпании", СтрокаДокументаОснования.ДокументОснование.СкладПолучатель);
				Иначе
					Запрос.УстановитьПараметр("СкладКомпании", СтрокаДокументаОснования.ДокументОснование.СкладКомпании);
				КонецЕсли;
				
				Выборка=Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Если РаспределяемНаТовары И (ВариантУчетаПоПартиям = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя) Тогда
						Если Выборка.КоличествоОстаток=0 Тогда
							дкСообщитьРезультат("Дополнительные затраты, распределяемые на себестоимости могут быть введены только на номенклатуру имеющуюся на складе!","Важное&Партии товаров компании:",ЭтотОбъект);
							Отказ=Истина;
							Прервать;
						КонецЕсли;
					ИначеЕсли Выборка.КоличествоПриход<>Выборка.КоличествоОстаток Тогда
						дкСообщитьРезультат("Дополнительные затраты, распределяемые на себестоимости могут быть введены только ДО СПИСАНИЯ (ПЕРЕМЕЩЕНИЯ) партии <"+СтрокаДокументаОснования.ДокументОснование+">!","Важное&Партии товаров компании:",ЭтотОбъект);
						Отказ=Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат НЕ Отказ;
	КонецЕсли; 
	///////////////////////////////////////////////////////////
	// Секция Альфа-Авто: Конец
	///////////////////////////////////////////////////////////
	
	ТаблицаТоваров=Неопределено;
	Если ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходов ИЛИ ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовВнутреннее Тогда
		// Получим таблицу движения вида "Приход" регистра партий компании документов-оснований.
		// Если хотя бы одну услугу необходимо распределять по весу, то из табличной части документов-оснований
		// получим вес номенклатуры в соответствии с единицами измерения  или номенклатуры в зависимости от подчиненности единицы измерения.
		
		// Определим необходимость получения веса для номенклатуры.
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Номенклатура.Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.СпособРаспределенияДопРасходов = ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияДопРасходов.ПоВесу)
		|	И Номенклатура.Ссылка В(&СписокНоменклатуры)";
		Запрос.УстановитьПараметр("СписокНоменклатуры", ШапкаДокумента.Ссылка.Товары.ВыгрузитьКолонку("Номенклатура"));	
		НеобходимоПолучатьВес = НЕ Запрос.Выполнить().Пустой();
		
		Для каждого СтрокаДокументаОснования Из ШапкаДокумента.Ссылка.ДокументыОснования Цикл
			// Получаем движения по регистру "Партии компании".
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПартииТоваровКомпании.СкладКомпании КАК СкладКомпании,
			|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
			|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ПартииТоваровКомпании.Партия КАК Партия,
			|	ПартииТоваровКомпании.СтатусПартии КАК СтатусПартии,
			|	ПартииТоваровКомпании.СтавкаНДС КАК СтавкаНДС,
			|	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
			|	СУММА(ПартииТоваровКомпании.СуммаНДС) КАК СуммаНДС,
			|	СУММА(0) КАК Сумма,
			|	СУММА(ПартииТоваровКомпании.СуммаУпр) КАК СуммаВсего";
			Если НеобходимоПолучатьВес Тогда		
				Запрос.Текст = Запрос.Текст + ", СУММА(ПартииТоваровКомпании.Количество / ВложенныйЗапрос.Количество * ВложенныйЗапрос.Вес) КАК Вес";
			Иначе
				Запрос.Текст = Запрос.Текст + ", СУММА(0) КАК Вес";
			КонецЕсли;
			Запрос.Текст = Запрос.Текст + "
			|ИЗ
			|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании";
			Если НеобходимоПолучатьВес Тогда
				Запрос.Текст = Запрос.Текст + "
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ТабЧастьДокументаОснования.Номенклатура КАК Номенклатура,
				|			ТабЧастьДокументаОснования.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				|			ВЫБОР КОГДА ТабЧастьДокументаОснования.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1
				|				ТОГДА СУММА(ТабЧастьДокументаОснования.Номенклатура.Вес * ТабЧастьДокументаОснования.Количество)
				|				ИНАЧЕ ВЫБОР КОГДА ТабЧастьДокументаОснования.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2
				|					ТОГДА СУММА(ТабЧастьДокументаОснования.ЕдиницаИзмерения.Вес * ТабЧастьДокументаОснования.Количество)
				|					ИНАЧЕ 0 КОНЕЦ КОНЕЦ КАК Вес,
				|			СУММА(ТабЧастьДокументаОснования.Количество * ТабЧастьДокументаОснования.Коэффициент) КАК Количество
				|		ИЗ
				|			Документ." + СтрокаДокументаОснования.ДокументОснование.Метаданные().Имя + ".Товары КАК ТабЧастьДокументаОснования
				|		ГДЕ
				|			ТабЧастьДокументаОснования.Ссылка = &ДокументОснованиеСсылка
				|		
				|		СГРУППИРОВАТЬ ПО
				|			ТабЧастьДокументаОснования.Номенклатура,
				|			ТабЧастьДокументаОснования.ХарактеристикаНоменклатуры) КАК ВложенныйЗапрос
				|		ПО ПартииТоваровКомпании.Номенклатура = ВложенныйЗапрос.Номенклатура
				|			И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = ВложенныйЗапрос.ХарактеристикаНоменклатуры";
			КонецЕсли;
			Запрос.Текст = Запрос.Текст + " 
			|ГДЕ
			|	ПартииТоваровКомпании.Регистратор = &Регистратор
			|	И ПартииТоваровКомпании.ВидДвижения = &ВидДвиженияПриход
			|	И ПартииТоваровКомпании.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
			|
			|СГРУППИРОВАТЬ ПО
			|	ПартииТоваровКомпании.СкладКомпании,
			|	ПартииТоваровКомпании.Номенклатура,
			|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
			|	ПартииТоваровКомпании.Партия,
			|	ПартииТоваровКомпании.СтатусПартии,
			|	ПартииТоваровКомпании.СтавкаНДС";
			Запрос.УстановитьПараметр("Регистратор",             СтрокаДокументаОснования.ДокументОснование);
			Запрос.УстановитьПараметр("ВидДвиженияПриход",       ВидДвиженияНакопления.Приход);
			Запрос.УстановитьПараметр("ДокументОснованиеСсылка", СтрокаДокументаОснования.ДокументОснование);
			Если ТаблицаТоваров=Неопределено Тогда
				ТаблицаТоваров=Запрос.Выполнить().Выгрузить();
			Иначе
				ВыборкаЗапроса=Запрос.Выполнить().Выбрать();
				Пока ВыборкаЗапроса.Следующий() Цикл
					НоваяСтрокаТоваров=ТаблицаТоваров.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТоваров,ВыборкаЗапроса);
				КонецЦикла;
			КонецЕсли; 
		КонецЦикла; 
	
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили ИЛИ ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобилиВнутреннее Тогда
		Для каждого СтрокаДокументаОснования Из ШапкаДокумента.Ссылка.ДокументыОснования Цикл
			Запрос=Новый Запрос("ВЫБРАТЬ
			|	ОбъединенныйЗапрос.СкладКомпании КАК СкладКомпании,
			|	ОбъединенныйЗапрос.Автомобиль КАК Автомобиль,
			|	ОбъединенныйЗапрос.Партия КАК Партия,
			|	ОбъединенныйЗапрос.ГТД КАК ГТД,
			|	ОбъединенныйЗапрос.СтатусПартии КАК СтатусПартии,
			|	СУММА(ОбъединенныйЗапрос.Количество) КАК Количество,
			|	СУММА(ОбъединенныйЗапрос.СуммаНДС) КАК СуммаНДС,
			|	СУММА(0) КАК Сумма,
			|	СУММА(ОбъединенныйЗапрос.СуммаВсего) КАК СуммаВсего
			|ИЗ (
			|ВЫБРАТЬ
			|	ОстаткиАвтомобилей.СкладКомпании КАК СкладКомпании,
			|	ОстаткиАвтомобилей.Автомобиль КАК Автомобиль,
			|	ОстаткиАвтомобилей.Партия КАК Партия,
			|	ОстаткиАвтомобилей.СтатусПартии КАК СтатусПартии,
			|	ОстаткиАвтомобилей.ГТД КАК ГТД,
			|	ОстаткиАвтомобилей.Количество КАК Количество,
			|	ОстаткиАвтомобилей.СуммаНДС КАК СуммаНДС,
			|	0 КАК Сумма,
			|	ОстаткиАвтомобилей.СуммаУпр КАК СуммаВсего
			|ИЗ
			|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
			|ГДЕ
			|	ОстаткиАвтомобилей.Регистратор = &Регистратор
			|	И ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияПриход
			|	И ОстаткиАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
			|	И ОстаткиАвтомобилей.Количество > 0
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	КомплектацияАвтомобилей.СкладКомпании КАК СкладКомпании,
			|	КомплектацияАвтомобилей.Автомобиль КАК Автомобиль,
			|	КомплектацияАвтомобилей.Партия КАК Партия,
			|	КомплектацияАвтомобилей.СтатусПартии КАК СтатусПартии,
			|	КомплектацияАвтомобилей.ГТД КАК ГТД,
			|	КомплектацияАвтомобилей.Количество КАК Количество,
			|	КомплектацияАвтомобилей.СуммаНДС КАК СуммаНДС,
			|	0 КАК Сумма,
			|	КомплектацияАвтомобилей.СуммаУпр КАК СуммаВсего
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			|ГДЕ
			|	КомплектацияАвтомобилей.Регистратор = &Регистратор
			|	И КомплектацияАвтомобилей.ВидДвижения = &ВидДвиженияПриход
			|	И КомплектацияАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
			|) КАК ОбъединенныйЗапрос
			|СГРУППИРОВАТЬ ПО
			|	ОбъединенныйЗапрос.СкладКомпании,
			|	ОбъединенныйЗапрос.Автомобиль,
			|	ОбъединенныйЗапрос.Партия,
			|	ОбъединенныйЗапрос.СтатусПартии,
			|	ОбъединенныйЗапрос.ГТД
			|");
			Запрос.УстановитьПараметр("Регистратор",             СтрокаДокументаОснования.ДокументОснование);
			Запрос.УстановитьПараметр("ВидДвиженияПриход",       ВидДвиженияНакопления.Приход);
			Запрос.УстановитьПараметр("ДокументОснованиеСсылка", СтрокаДокументаОснования.ДокументОснование);
			Если ТаблицаТоваров=Неопределено Тогда
				ТаблицаТоваров=Запрос.Выполнить().Выгрузить();
			Иначе
				ВыборкаЗапроса=Запрос.Выполнить().Выбрать();
				Пока ВыборкаЗапроса.Следующий() Цикл
					НоваяСтрокаТоваров=ТаблицаТоваров.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТоваров,ВыборкаЗапроса);
				КонецЦикла;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	Если ТаблицаТоваров=Неопределено Тогда
		ВДокументеТолькоКомиссионныеТовары=Истина;
	ИначеЕсли ТаблицаТоваров.Количество()=0 Тогда
		ВДокументеТолькоКомиссионныеТовары=Истина;
	Иначе
		ВДокументеТолькоКомиссионныеТовары=Ложь;
	КонецЕсли;

	Если НЕ ВДокументеТолькоКомиссионныеТовары Тогда
		Если ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходов ИЛИ ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовВнутреннее Тогда
			НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
			НаборЗаписейПартии.ДокументОбъект  = ЭтотОбъект;
			НаборЗаписейПартии.ШапкаДокумента  = ШапкаДокумента;
			НаборЗаписейПартии.ЕстьСтавкаНДС   = Истина;
			НаборЗаписейПартии.РезультатЗапросаПоТоварам = ТаблицаТоваров;
			НаборЗаписейПартии.РежимДопРасходы = 2;
			Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили ИЛИ ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобилиВнутреннее Тогда
			НаборЗаписейПартии=Движения.ОстаткиАвтомобилей;
			НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейПартии.РезультатЗапросаПоАвтомобилям=ТаблицаТоваров;
			РассчетДополнительныхРасходовНаАвтомобили(НаборЗаписейПартии.РезультатЗапросаПоАвтомобилям,Ссылка,Отказ);
			НаборЗаписейПартии.РежимДопРасходы=2;
			НаборЗаписейПартии.Партия="Партия";
			Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
		КонецЕсли;
	Иначе
		// Значит в документе-основании присутствует только комиссионый товар или нет товара или перемещение в филиал, тогда
		// придется все списать на расходы
		// Вычислим сумму, которую необходимо списать.
		ТекстЗапроса = 
		"
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ДокументТовары.СуммаВсего),0) КАК СуммаВсего
		|ИЗ
		|	Документ.ПоступлениеДопРасходов.Товары КАК ДокументТовары
		|ГДЕ
		|	  ДокументТовары.Ссылка=&Ссылка
		|	И ДокументТовары.Номенклатура.СпособРаспределенияДопРасходов<>ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияДопРасходов.НаДоходыИРасходы)
		|	И ДокументТовары.Номенклатура.ВидНоменклатуры=ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|";
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		СуммаДляРаспределения = Выборка.СуммаВсего;
		
		Если СуммаДляРаспределения<>0 Тогда
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение      = ШапкаДокумента.ПодразделениеКомпании;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.ПрочиеРасходы;
			НаборЗаписейДиР.ВУпрВалюте             = ФлагУпрВалюты;
			НаборЗаписейДиР.Расход                 = СуммаДляРаспределения;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
		
	КонецЕсли;
	
	// если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		Результат = Запрос.Выполнить(); АвтомобильСкладКомпанииБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			АвтомобильСкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл",АвтомобильСкладКомпанииБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
	// двигаем границу последовательности автомобилей
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ОстаткиАвтомобилей.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ДвиженияОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей.Выгрузить();
			ДвиженияОстаткиАвтомобилей.Свернуть("СкладКомпании","");
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДвиженияОстаткиАвтомобилей.ВыгрузитьКолонку("СкладКомпании");
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	// если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
	// двигаем границу последовательности партий	
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ПартииТоваровКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ДвиженияПартииТоваровКомпании=Движения.ПартииТоваровКомпании.Выгрузить();
			ДвиженияПартииТоваровКомпании.Свернуть("СкладКомпании","");
			НаборЗаписейГраницыПартий.СкладКомпании		= ДвиженияПартииТоваровКомпании.ВыгрузитьКолонку("СкладКомпании");
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	БалансовоеПодразделениеДоговора = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение);
	БалансовоеПодразделениеШапки    = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеКомпании);
	
	// БАЛАНС: определим необходимость формирования корректирующих проводок по партиям.
	Если ВедетсяБалансПоПодразделению И Движения.ПартииТоваровКомпании.Количество()> 0 Тогда
		
		ДвиженияПартииТоваровКомпании = Движения.ПартииТоваровКомпании.Выгрузить(,"СкладКомпании, СуммаУпр");
		ДвиженияПартииТоваровКомпании.Свернуть("СкладКомпании", "СуммаУпр");
		ДвиженияПартииТоваровКомпании.Колонки.Добавить("Подразделение");
		
		Для Каждого ТекущаяСтрока Из ДвиженияПартииТоваровКомпании Цикл
			ТекущаяСтрока.Подразделение = ТекущаяСтрока.СкладКомпании.Подразделение;
		КонецЦикла;
		
		ДвиженияПартииТоваровКомпании.Свернуть("Подразделение", "СуммаУпр");
		
		СуммаКорректировки = 0;
		Для Каждого ТекущаяСтрока Из ДвиженияПартииТоваровКомпании Цикл
			
			Если ТекущаяСтрока.СуммаУпр = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ПодразделениеСклада = ТекущаяСтрока.Подразделение;
			БалансовоеПодразделениеСклада = обПолучитьБалансовоеПодразделение(ПодразделениеСклада);
			
			Если БалансовоеПодразделениеДоговора = БалансовоеПодразделениеСклада Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.Подразделение          = ПодразделениеСклада;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.Доход                  = ТекущаяСтрока.СуммаУпр;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			СуммаКорректировки = СуммаКорректировки + ТекущаяСтрока.СуммаУпр;
			
		КонецЦикла;
		
		// БАЛАНС: определим необходимость формирования корректирующих проводок по взаиморасчетам.
		Если (НЕ СуммаКорректировки = 0) Тогда
			
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.Подразделение          = ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.Расход                 = СуммаКорректировки;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
		КонецЕсли;
		
	// Корректирующие проводки Доходов и расходов для комиссионных товаров или перемещений в филиал.
	ИначеЕсли ВедетсяБалансПоПодразделению И (НЕ СуммаДляРаспределения = 0) И (НЕ БалансовоеПодразделениеШапки = БалансовоеПодразделениеДоговора) Тогда
		
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
		// Подразделение из шапки, т.к. не понятно как распределить сумму на доходы и расходы по разным основаниям.
		НаборЗаписейДиР.Подразделение          = ШапкаДокумента.ПодразделениеКомпании;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте             = ФлагУпрВалюты;
		НаборЗаписейДиР.Доход                  = СуммаДляРаспределения;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
		НаборЗаписейДиР.Подразделение          = ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте             = ФлагУпрВалюты;
		НаборЗаписейДиР.Расход                 = СуммаДляРаспределения;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
	КонецЕсли;
	
	//Услуги, распределяемые на доходы и расходы туда и поместим
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПоступлениеДопРасходовТовары.Номенклатура.СтатьяДопРасходов КАК СтатьяДопРасходов,
	|	СУММА(ПоступлениеДопРасходовТовары.СуммаВсего) КАК Сумма
	|ИЗ
	|	Документ.ПоступлениеДопРасходов.Товары КАК ПоступлениеДопРасходовТовары
	|ГДЕ
	|	ПоступлениеДопРасходовТовары.Ссылка = &Ссылка И
	|	ПоступлениеДопРасходовТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга) И
	|	ПоступлениеДопРасходовТовары.Номенклатура.СпособРаспределенияДопРасходов = ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияДопРасходов.НаДоходыИРасходы)
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеДопРасходовТовары.Номенклатура.СтатьяДопРасходов";
	
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	СуммаКорректировки = 0;
	Пока Выборка.Следующий() Цикл
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение      = ШапкаДокумента.ПодразделениеКомпании;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Выборка.СтатьяДопРасходов;
		НаборЗаписейДиР.ВУпрВалюте             = ФлагУпрВалюты;
		НаборЗаписейДиР.Расход                 = Выборка.Сумма;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
		// БАЛАНС: если нужно создаем корректирующие движения по доходам и расходам.
		Если ВедетсяБалансПоПодразделению И (НЕ БалансовоеПодразделениеДоговора = БалансовоеПодразделениеШапки) Тогда
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.Подразделение          = ШапкаДокумента.ПодразделениеКомпании;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте             = ФлагУпрВалюты;
			НаборЗаписейДиР.Доход                  = Выборка.Сумма;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			СуммаКорректировки = СуммаКорректировки + Выборка.Сумма;
		КонецЕсли;
		
	КонецЦикла;

	Если НЕ СуммаКорректировки = 0 Тогда
		
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
		НаборЗаписейДиР.Подразделение          = ДоговорВзаиморасчетов.Подразделение;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте             = ФлагУпрВалюты;
		НаборЗаписейДиР.Расход                 = СуммаКорректировки;
		
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
	КонецЕсли;

	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="Контрагент" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма);
				
		Возврат Рез;
			
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("Контрагент",Контрагент);
		КонецЕсли;
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма);
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ПоступлениеДопРасходов"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПоступлениеДопРасходов(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПоступлениеДопРасходов");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка			= ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(Контрагент);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего, ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований " + ВыборкаТабличнойЧасти.Количество()+ " на сумму "
		+ обЧислоПрописью(СуммаВсего, ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
	Отпустил.ОтпустилКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Отпустил.ОтпустилКонтрагент),"","/ "+ Отпустил.ОтпустилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПоступлениеДопРасходов()

// Формирует печатную форму "ТОРГ-12"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);	
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ЭтотОбъект.Контрагент,СтруктураПредставления1);
	ОбластьМакета.Параметры.ПодразделениеКомпании	= Неопределено;
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект,"ПоставщикКонтрагент",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.ПодразделениеКомпании);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(
		ОбластьМакета.Параметры.Грузополучатель, СтруктураПредставления2, ДополнительныеПараметры);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления2);
	
	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Контрагент.КодПоОКПО;
	Если ТипЗнч(ОбластьМакета.Параметры.Грузополучатель)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	КонецЕсли;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	Если ТипЗнч(ОбластьМакета.Параметры.Плательщик)=Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.Организация.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
	ОбластьМакета.Параметры.ДокументОснование = ДоговорВзаиморасчетов;
	ОбластьМакета.Параметры.ОснованиеДата = "";
	ОбластьМакета.Параметры.ОснованиеНомер = ""; 
	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);	
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;
	КоличествоСтрокНаТекущейСтранице=0;
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	//!!! TimR: В новой пф данное поле не выводится
	//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");
	
	СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);		
	
	ОбластьЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);	
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 

	ВыборкаСтрок = ЭтотОбъект.Товары;
	
	КоличествоСтрок = ВыборкаСтрок.Количество();

	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,ЭтотОбъект);
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		ОбластьСтрока.Параметры.Код							= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,, ЭтотОбъект);
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество					= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.ТоварНаименование           = СтруктураСтроки.ТоварНаименование + ?(ЗначениеЗаполнено(СтрокаТоваров.Номенклатура.Артикул), ", "+СтрокаТоваров.Номенклатура.Артикул, "");

		// Пересчитаем сумму в соответствии с регламентированной валютой
		СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДС   = Окр(обПересчет(СтрокаТоваров.СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаБезНДС = СуммаВсего - СуммаНДС;
		
		ОбластьСтрока.Параметры.СуммаВсего  = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС    = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", Формат(СуммаНДС, ФорматВыводаСуммы));
		ОбластьСтрока.Параметры.СуммаБезНДС = Формат(СуммаБезНДС, ФорматВыводаСуммы);
		
		// Рассчитаем цену.
		ОбластьСтрока.Параметры.ЦенаБезНДС = Формат(СуммаБезНДС / (СтрокаТоваров.Количество * ?(СтрокаТоваров.Коэффициент = 0, 1, СтрокаТоваров.Коэффициент)), ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(СтрокаТоваров) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьВсего);
			//!!! TimR: В новой пф данное поле не выводится
			//мсвДопОбластиПодвала.Добавить(ОбластьВсегоСтавкаНДС);
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		Если КоличествоСтрокНаТекущейСтранице=0 Тогда
			ТабДокумент.Вывести(ОбластьСтрока);
		Иначе
			//выводим строку, делая проверку попадания на лист		
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		КонецЕсли; 
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
		СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице = СтрокаТоваров.Количество;
		СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаБезНДС;
		СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
		СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			КоличествоСтрокНаТекущейСтранице=0;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Иначе
			КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
		КонецЕсли;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДС;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
		
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 1 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = Формат(ВыборкаСтрок.Итог("Количество"), ФорматВыводаКоличества);
	ОбластьВсего.Параметры.ИтогСуммы      = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогНДС        = Формат(ИтогоНДС, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = Формат(ИтогоСуммаСНДС, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьВсего);
	
	//!!! TimR: В новой пф данное поле не выводится
	//// выведем итоги по ставкам НДС
	//Для Каждого ЭлементСоответствия Из СоответствиеИтоговПоставкамНДС Цикл
	//	ОбластьВсегоСтавкаНДС.Параметры.СтавкаНДС = "Итого по ставке НДС: "
	//	+дкПолучитьПредставлениеСтавкиНДС(ЭлементСоответствия.Ключ,"%");
	//	ОбластьВсегоСтавкаНДС.Параметры.ИтогНДС = ЭлементСоответствия.Значение;
	//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьВсегоСтавкаНДС, , , НомерСтраницы,,ЭтотОбъект);
	//КонецЦикла;
	
	// Выводим подвал документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);
	
	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьПодвал.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьПодвал.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьПодвал.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли; 
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьПодвал.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
	Отпустил.Вставить("Отпустил",Отпустил.ОтпустилКонтрагент);
	Отпустил.Вставить("ОтпустилПредставление",Отпустил.ОтпустилКонтрагентПредставление);
	Отпустил.Вставить("ОтпустилДолжность",Отпустил.ОтпустилКонтрагентДолжность);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьТОРГ12()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если Основание<>Неопределено Тогда
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовВнутреннее;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеАвтомобилей")
			ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковАвтомобилей") Тогда
			ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ПеремещениеАвтомобилей") Тогда
			ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобилиВнутреннее;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.Комплектация")
			ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.Разукомплектация") Тогда
			ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовВнутреннее;
		ИначеЕсли обЭтотТип(Основание, "ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
			Если Основание.ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт Тогда
				ХозОперация = Справочники.ХозОперации.ПоступлениеДопРасходов;
			ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпортАвтомобилей Тогда
				ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ДокументыОснования.Очистить();
		//Если НЕ обЭтотТип(Основание, "ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
		НоваяСтрока                   = ДокументыОснования.Добавить();
		НоваяСтрока.ДокументОснование = ДокументОснование;
		//Иначе
		//	Если Основание.ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпорт Тогда
		//		Запрос = Новый Запрос;
		//		Запрос.Текст =
		//		"ВЫБРАТЬ
		//		|	ТаможеннаяДекларацияИмпортТовары.Партия КАК ДокументОснование
		//		|ИЗ
		//		|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаможеннаяДекларацияИмпортТовары
		//		|ГДЕ
		//		|	ТаможеннаяДекларацияИмпортТовары.Ссылка = &Основание
		//		|
		//		|СГРУППИРОВАТЬ ПО
		//		|	ТаможеннаяДекларацияИмпортТовары.Партия";
		//		
		//		Запрос.УстановитьПараметр("Основание", Основание);
		//		ДокументыОснования.Загрузить(Запрос.Выполнить().Выгрузить());
		//	ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.ТаможеннаяДекларацияИмпортАвтомобилей Тогда
		//		Запрос = Новый Запрос;
		//		Запрос.Текст =
		//		"ВЫБРАТЬ
		//		|	ОстаткиАвтомобилейОстатки.Партия КАК ДокументОснование
		//		|ИЗ
		//		|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(
		//		|			,
		//		|			Автомобиль В
		//		|				(ВЫБРАТЬ
		//		|					ТаможеннаяДекларацияИмпортАвтомобили.Автомобиль
		//		|				ИЗ
		//		|					Документ.ТаможеннаяДекларацияИмпорт.Автомобили КАК ТаможеннаяДекларацияИмпортАвтомобили
		//		|				ГДЕ
		//		|					ТаможеннаяДекларацияИмпортАвтомобили.Ссылка = &Основание)) КАК ОстаткиАвтомобилейОстатки
		//		|
		//		|СГРУППИРОВАТЬ ПО
		//		|	ОстаткиАвтомобилейОстатки.Партия";
		//		
		//		Запрос.УстановитьПараметр("Основание", Основание);
		//		ДокументыОснования.Загрузить(Запрос.Выполнить().Выгрузить());
		//	КонецЕсли;
		//КонецЕсли;
	КонецЕсли;
	
	
	// очистим таблицу товаров
	Товары.Очистить();
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СтрокиДляУдаления=Новый Массив;
	Если ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходов Тогда
		Для каждого СтрокаДокументаОснования Из ДокументыОснования Цикл
			Если ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.АвансовыйОтчет")
				 И ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
				 СтрокиДляУдаления.Добавить(СтрокаДокументаОснования);
			КонецЕсли; 		
		КонецЦикла; 
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовВнутреннее Тогда
		Для каждого СтрокаДокументаОснования Из ДокументыОснования Цикл
			Если ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.ПеремещениеТоваров")
				 И ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.Комплектация")
				 И ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.Разукомплектация") Тогда
				 СтрокиДляУдаления.Добавить(СтрокаДокументаОснования);
			КонецЕсли; 		
		КонецЦикла; 
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили Тогда
		Для каждого СтрокаДокументаОснования Из ДокументыОснования Цикл
			Если ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.ПоступлениеАвтомобилей")
				 И ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.ВводОстатковАвтомобилей")
				 И ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
				 СтрокиДляУдаления.Добавить(СтрокаДокументаОснования);
			КонецЕсли; 		
		КонецЦикла; 
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобилиВнутреннее Тогда
		Для каждого СтрокаДокументаОснования Из ДокументыОснования Цикл
			Если ТипЗнч(СтрокаДокументаОснования.ДокументОснование)<>Тип("ДокументСсылка.ПеремещениеАвтомобилей") Тогда
				 СтрокиДляУдаления.Добавить(СтрокаДокументаОснования);
			КонецЕсли; 		
		КонецЦикла; 
	КонецЕсли;
	Для каждого СтрокаУдаления Из СтрокиДляУдаления Цикл
		ДокументыОснования.Удалить(СтрокаУдаления);
	КонецЦикла; 
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	// Взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект        = ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения       = Режим;
	НаборЗаписейВзаиморасчеты.Контрагент            = Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.Сделка                = Неопределено;
	НаборЗаписейВзаиморасчеты.Сумма                 = СуммаДокумента;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц = 0;
	
	Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	
	// Доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц <> 0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение      = ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// проведем партии
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// установим цены контрагентов
	Если НЕ Отказ И ТипЦен.РегистрироватьЦеныПоПриходу И (НЕ обЗначениеНеЗаполнено(Контрагент)) И НЕ ТипЦен.Рассчитывается Тогда
		НаборЗаписейЦены = Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейЦены.Контрагент             = Контрагент;
		НаборЗаписейЦены.ТипЦен                 = ТипЦен;
		НаборЗаписейЦены.УстанавливатьЦеныУслуг = Истина;
		НаборЗаписейЦены.ИмяРеквизитаЦена       = "Цена";
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// установим закупочные цены компании
	Если НЕ Отказ И ПодразделениеКомпании.ФормироватьЗакупочнуюЦену И НЕ Справочники.ТипыЦен.ОсновнойТипЦенЗакупки.Рассчитывается Тогда
		НаборЗаписейЦены = Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейЦены.Контрагент             = Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаЦена       = "Цена";
		НаборЗаписейЦены.ТипЦен                 = Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
		НаборЗаписейЦены.УстанавливатьЦеныУслуг = Истина;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	//установим нормативные цены компании
	Если НЕ Отказ И ПодразделениеКомпании.ФормироватьНормативнуюЦену И НЕ Справочники.ТипыЦен.НормативнаяЦена.Рассчитывается Тогда
		НаборЗаписейЦены = Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейЦены.Контрагент             = Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаЦена       = "Цена";
		НаборЗаписейЦены.ТипЦен                 = Справочники.ТипыЦен.НормативнаяЦена;
		НаборЗаписейЦены.УстанавливатьЦеныУслуг = Истина;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль документа РасходныйСкладскойОрдер

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Если ХозОперация=Справочники.ХозОперации.РасходныйСкладскойОрдерШин Тогда
		Для каждого ВидНоменклатуры Из Метаданные.Перечисления.ВидыНоменклатуры.ЗначенияПеречисления Цикл
			ВидНоменклатурыСсылка=Перечисления.ВидыНоменклатуры[ВидНоменклатуры.Имя];
			Если ВидНоменклатурыСсылка<>Перечисления.ВидыНоменклатуры.Шины Тогда
				Результат.Вставить(ВидНоменклатурыСсылка,0);
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	КонецЕсли; 
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	Если Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
        Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
	    Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	    Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	    Возврат Рез;
        
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
        
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("РасходныйСкладскойОрдер", "Расходный складской ордер", Истина);
	СписокХозОпераций.Добавить("РасходныйСкладскойОрдерШин", "Расходный складской ордер шин", Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый Тогда
		ОбязательныеТовары.Вставить("Ячейка",3);
	КонецЕсли; 

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	Если ХозОперация = Справочники.ХозОперации.РасходныйСкладскойОрдерШин Тогда
		ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем только в случае если
	// все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
		
		ЗаявкаНаХранениеШин=Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(ДокументОснование);
		Если ХозОперация = Справочники.ХозОперации.РасходныйСкладскойОрдерШин Тогда
			Если ТипЗнч(ЗаявкаНаХранениеШин)<>Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" должен быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли; 
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			               |	РасходныйСкладскойОрдер.Ссылка КАК Ссылка
			               |ИЗ
			               |	Документ.РасходныйСкладскойОрдер КАК РасходныйСкладскойОрдер
			               |ГДЕ
			               |	РасходныйСкладскойОрдер.ДокументОснование = &ДокументОснование
			               |	И РасходныйСкладскойОрдер.Ссылка <> &Ссылка";
			Запрос.УстановитьПараметр("ДокументОснование",ДокументОснование);
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			Выборка=Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				дкДобавитьОшибку(Ошибки,"На основании <"+ДокументОснование+"> уже есть введенный документ <"+Выборка.Ссылка+">.");
				Результат=Ложь;
			КонецЕсли; 
		Иначе
			Если ТипЗнч(ЗаявкаНаХранениеШин)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" не может быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.Чек") И НЕ ДокументОснование.ХозОперация = Справочники.ХозОперации.Чек Тогда
		дкДобавитьОшибку(Ошибки, "Ввод расходного складского ордера на основании документа """+Метаданные.Документы.Чек+""" возможен только для хоз. операции ""Чек"".");
		Результат=Ложь;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "РасходныйСкладскойОрдер"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРасходныйСкладскойОрдер(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РасходныйСкладскойОрдер");
	
	//для начала настроим макет
	// Произведем преобразование макета в зависимости от типа ячеистого склада
	ЭтоЯчеистыйСклад = (СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый);
	Если НЕ ЭтоЯчеистыйСклад Тогда
		ОбластьТовар				= Макет.Область("Товар");
		ОбластьЯчейка				= Макет.Область("Ячейка");
		ОбластьТовар.ШиринаКолонки	= ОбластьТовар.ШиринаКолонки + ОбластьЯчейка.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЯчейка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьВладелецТовара = Макет.Область("Владелец");
		Макет.УдалитьОбласть(ОбластьВладелецТовара,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьДокументОснование = Макет.Область("ДокументОснование");
		Макет.УдалитьОбласть(ОбластьДокументОснование,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	//bz++
	ОбластьМакета.Параметры.ТекстЗаголовка				= ТекстЗаголовка;
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	КонецЕсли;	
	//ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	//
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	//bz--
	
	ОбластьМакета.Параметры.ПредставлениеСклада		 = СкладКомпании;
	
	Если НЕ обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьМакета.Параметры.ПредставлениеВладельца = спПолучитьПредставление(ВладелецТовара);
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета.Параметры.Заполнить(ДокументОснование);
		ОбластьМакета.Параметры.ПредставлениеДокументаОснования	= дкПолучитьПредставление(ДокументОснование);
		Попытка
			ОбластьМакета.Параметры.Контрагент 						= ДокументОснование.Контрагент;
			ОбластьМакета.Параметры.ПредставлениеКонтрагента		= спПолучитьПредставление(ДокументОснование.Контрагент);
			ОбластьМакета.Параметры.ДоговорВзаиморасчетов			= ДокументОснование.ДоговорВзаиморасчетов;
			ОбластьМакета.Параметры.ПредставлениеДоговора			= ДокументОснование.ДоговорВзаиморасчетов;
		Исключение
		КонецПопытки; 
	КонецЕсли;
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("Ячейка", СтрокаТабличнойЧасти.Ячейка);
		СтруктураСтроки.Вставить("КоличествоБазовое", Формат(СтрокаТабличнойЧасти.КоличествоБазовое,ФорматВыводаКоличества));
		СтруктураСтроки.Вставить("ЕдиницаИзмеренияБазовая", СтрокаТабличнойЧасти.Номенклатура.БазоваяЕдиницаИзмерения);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, ,ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьРасходныйСкладскойОрдер()

// Печатает отчет "Карта склада"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьКартаСклада(ТабДокумент) Экспорт
	ОтчетКартаСклада = Отчеты.КартаСклада.Создать();
	ОтчетКартаСклада.Склад = СкладКомпании;
	ОтчетКартаСклада.ИсточникМаршрута = ЭтотОбъект.Ссылка;
	ФормаКартаСклада = ОтчетКартаСклада.ПолучитьФорму();
	ФормаКартаСклада.РежимОтчетаПереданный = 1;
	ФормаКартаСклада.Открыть();
	ФормаКартаСклада.НарисоватьМаршрутДокумента(ЭтотОбъект);
	ТабДокумент = Неопределено;
	Возврат ТабДокумент;
КонецФункции // ПечатьКартаСклада()

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

//Функция печати Акт о возврате ТМЦ
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьМХ3(ТабДокумент) Экспорт
	
	Макет = ПолучитьОбщийМакет("МХ3");
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.НомерДокумента = Номер;
	ОбластьЗаголовок.Параметры.ДатаДокумента  = Дата;
	ОбластьЗаголовок.Параметры.Заполнить(ЭтотОбъект);
	ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО				= Организация.КодПоОКПО;
	
	//bz++
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	КонецЕсли;
	
	//ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП	= Организация.КодПоОКДП;
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	//bz--
	
	Если НЕ обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьЗаголовок.Параметры.ПредставлениеВладельца = спПолучитьПредставление(ВладелецТовара);
		Если Тип("СправочникСсылка.СкладыКомпании") = ТипЗнч(ВладелецТовара) Тогда
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ВладелецТовара.Организация.КодПоОКПО
		Иначе
			ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ВладелецТовара.КодПоОКПО;
		КонецЕсли;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьЗаголовок);
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьШапка);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ВыборкаСтрок = Товары.Выгрузить();
	НомерСтроки = 1;
	ВыводитьКод = обПраво("ВыводитьКодВПечатныхФормах",Права,ЭтотОбъект);
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
		Если ВыводитьКод Тогда
			ОбластьСтрока.Параметры.Код = Строка(СтрокаТоваров.Номенклатура.Артикул);
		КонецЕсли;
		ОбластьСтрока.Параметры.Номенклатура = Строка(СтрокаТоваров.Номенклатура);
		ОбластьСтрока.Параметры.Характеристика = Строка(СтрокаТоваров.ХарактеристикаНоменклатуры);
		ОбластьСтрока.Параметры.ЕдиницаИзмерения = СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.КодОКЕИ = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество = Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ТабДокумент.Вывести(ОбластьСтрока);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"),ФорматВыводаКоличества);
	ТабДокумент.Вывести(ОбластьИтого);
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(ОбластьПодвал);
	Возврат ТабДокумент;
КонецФункции // ПечатьМХ3(ТабДокумент)

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	ВычетыТЧВыполнены = Ложь;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПереразмещениеТоваров") И Основание.ХозОперация<>Справочники.ХозОперации.ПереразмещениеШин Тогда
		Товары.Очистить();
		Для Каждого СтрТовар Из Основание.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрТовар.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
			НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмеренияНовая;
			НоваяСтрока.Коэффициент = СтрТовар.ЕдиницаИзмеренияНовая.Коэффициент;
			НоваяСтрока.Количество = ?(СкладКомпании.УчетЕдиницИзмерения = Перечисления.ВидыУчетаЕдиницИзмерения.НеВедется, СтрТовар.Количество, СтрТовар.Количество * СтрТовар.ЕдиницаИзмерения.Коэффициент / СтрТовар.ЕдиницаИзмеренияНовая.Коэффициент);
			НоваяСтрока.КоличествоБазовое = СтрТовар.КоличествоБазовоеНовое;
			НоваяСтрока.Ячейка = СтрТовар.ЯчейкаНовая;
		КонецЦикла;
		//СтруктураПодчиненных = Новый Структура("РасходныйСкладскойОрдер", "Количество, ЕдиницаИзмерения, Ячейка");
		//дкЗаполнитьТабличнуюЧастьПоОснованию(ЭтотОбъект, Основание, "Количество,ЕдиницаИзмеренияНовая,ЯчейкаНовая" , СтруктураПодчиненных, Ложь);
		ВычетыТЧВыполнены = Истина;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Комплектация") Тогда
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	КомплектацияТовары.НомерСтроки КАК НомерСтроки,
		             |	КомплектацияТовары.Номенклатура,
		             |	КомплектацияТовары.ХарактеристикаНоменклатуры,
		             |	КомплектацияТовары.ЕдиницаИзмерения,
		             |	КомплектацияТовары.Коэффициент,
		             |	КомплектацияТовары.Количество * КомплектацияТовары.Ссылка.КоличествоКомплектов КАК Количество,
		             |	КомплектацияТовары.Количество * КомплектацияТовары.Ссылка.КоличествоКомплектов КАК КоличествоБазовое
		             |ИЗ
		             |	Документ.Комплектация.Товары КАК КомплектацияТовары
		             |ГДЕ
		             |	КомплектацияТовары.Ссылка = &Ссылка
		             |
		             |УПОРЯДОЧИТЬ ПО
		             |	НомерСтроки";
		Запрос.УстановитьПараметр("Ссылка",Основание);
		Выборка=Запрос.Выполнить().Выбрать();
		Товары.Очистить();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
			НоваяСтрока.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
			НоваяСтрока.Коэффициент = Выборка.Коэффициент;
			НоваяСтрока.Количество = Выборка.Количество;
			НоваяСтрока.КоличествоБазовое = Выборка.КоличествоБазовое;
		КонецЦикла;
		ВычетыТЧВыполнены = Истина;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Разукомплектация") Тогда
		Товары.Очистить();
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура = Основание.Комплект;
		НоваяСтрока.ХарактеристикаНоменклатуры = Основание.ХарактеристикаКомплекта;
		НоваяСтрока.ЕдиницаИзмерения = Основание.Комплект.ОсновнаяЕдиницаИзмерения;
		НоваяСтрока.Коэффициент = Основание.Комплект.ОсновнаяЕдиницаИзмерения.Коэффициент;
		НоваяСтрока.Количество = Основание.КоличествоКомплектов;
		НоваяСтрока.КоличествоБазовое = Основание.КоличествоКомплектов;
		Если обЗначениеНеЗаполнено(Основание.Ячейка) Тогда
			Если НЕ обЗначениеНеЗаполнено(НоваяСтрока.Номенклатура) Тогда
				НоваяСтрока.Ячейка = Справочники.Номенклатура.ПолучитьЯчейкуХранения(НоваяСтрока.Номенклатура,СкладКомпании);
			КонецЕсли;
		Иначе
			НоваяСтрока.Ячейка = Основание.Ячейка;
		КонецЕсли;
		ВычетыТЧВыполнены = Истина;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
		Товары.Очистить();
		Для Каждого СтрТовар Из Основание.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрТовар.НоменклатураРасход;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатурыРасход;
			НоваяСтрока.ЕдиницаИзмерения = СтрТовар.ЕдиницаИзмеренияРасход;
			НоваяСтрока.Коэффициент = СтрТовар.КоэффициентРасход;
			НоваяСтрока.Количество = СтрТовар.КоличествоРасход;
			НоваяСтрока.КоличествоБазовое = СтрТовар.КоличествоРасход;
		КонецЦикла;
		ВычетыТЧВыполнены = Истина;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВводВЭксплуатацию") Тогда
		ВычетыТЧВыполнены = Истина;
		
	ИначеЕсли обЭтотТип(Основание, "ДокументСсылка.Чек") Тогда
		
		Если Основание.ХозОперация = Справочники.ХозОперации.Чек Тогда
			
			Комментарий = "Введено на основании : " + Строка(Основание) + ".";
			
		Иначе
			
			Сообщить("Ввод расходного складского ордера возможен только для хоз. операции ""Чек""", СтатусСообщения.Важное);
			дкОтменаВводаДокумента(ЭтотОбъект);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		Если обЗначениеНеЗаполнено(СкладКомпании) Тогда
			СкладКомпании=обПраво("ОсновнойСкладКомпании",Права,,ЭтотОбъект);
		КонецЕсли;
		
	ИначеЕсли (ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаХранениеШин")) ИЛИ
			  (ТипЗнч(Основание) = Тип("ДокументСсылка.ПриходныйСкладскойОрдер") И Основание.ХозОперация=Справочники.ХозОперации.ПриходныйСкладскойОрдерШин) ИЛИ
			  (ТипЗнч(Основание) = Тип("ДокументСсылка.ПереразмещениеТоваров") И Основание.ХозОперация=Справочники.ХозОперации.ПереразмещениеШин) Тогда
		ХозОперация = Справочники.ХозОперации.РасходныйСкладскойОрдерШин;
		//Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПереразмещениеТоваров") Тогда
		//	СкладКомпании=Основание.СкладПолучатель;
		//КонецЕсли;
		СкладКомпании=Справочники.СкладыКомпании.ПустаяСсылка();
		ЗаявкаНаХранениеШин=Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(Основание);
		Товары.Очистить();
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ОстаткиТоваровОрдерныйСкладОстатки.СкладКомпании КАК СкладКомпании,
		               |	ОстаткиТоваровОрдерныйСкладОстатки.Ячейка КАК Ячейка,
		               |	ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура КАК Номенклатура,
		               |	ОстаткиТоваровОрдерныйСкладОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ОстаткиТоваровОрдерныйСкладОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		               |	ОстаткиТоваровОрдерныйСкладОстатки.ЗаявкаНаХранениеШин КАК ЗаявкаНаХранениеШин,
		               |	ОстаткиТоваровОрдерныйСкладОстатки.КоличествоОстаток КАК Количество
		               |ИЗ
		               |	РегистрНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(
		               |			&Момент,
		               |			//СкладКомпании = &СкладКомпании И
		               |				ЗаявкаНаХранениеШин = &ЗаявкаНаХранениеШин) КАК ОстаткиТоваровОрдерныйСкладОстатки
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	СкладКомпании,
		               |	Ячейка,
		               |	Номенклатура,
		               |	ХарактеристикаНоменклатуры";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Момент",?(ЭтоНовый(),Новый Граница(Новый МоментВремени(КонецДня(Дата)),ВидГраницы.Исключая), Новый Граница(Ссылка.МоментВремени(),ВидГраницы.Исключая)));
		//Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("ЗаявкаНаХранениеШин", ЗаявкаНаХранениеШин);
		ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаОстатков Из ТаблицаОстатков Цикл
			Если СкладКомпании<>Справочники.СкладыКомпании.ПустаяСсылка() И СкладКомпании<>СтрокаОстатков.СкладКомпании Тогда
				Продолжить;
			КонецЕсли; 
			СкладКомпании=СтрокаОстатков.СкладКомпании;
			СтрокаТЧ=Товары.Добавить();
			СтрокаТЧ.Номенклатура=СтрокаОстатков.Номенклатура;
			Если обЗначениеНеЗаполнено(СтрокаОстатков.ЕдиницаИзмерения) Тогда
				СтрокаТЧ.ЕдиницаИзмерения=СтрокаТЧ.Номенклатура.ОсновнаяЕдиницаИзмерения;
				СтрокаТЧ.Количество=СтрокаОстатков.Количество*СтрокаТЧ.ЕдиницаИзмерения.Коэффициент;
			Иначе
				СтрокаТЧ.ЕдиницаИзмерения=СтрокаТЧ.ЕдиницаИзмерения;
				СтрокаТЧ.Количество=СтрокаОстатков.Количество;
			КонецЕсли; 
			СтрокаТЧ.Коэффициент=СтрокаТЧ.ЕдиницаИзмерения.Коэффициент;
			СтрокаТЧ.КоличествоБазовое=СтрокаТЧ.Количество;
			СтрокаТЧ.Ячейка=СтрокаОстатков.Ячейка;
			СтрокаТЧ.ХарактеристикаНоменклатуры=СтрокаОстатков.ХарактеристикаНоменклатуры;
		КонецЦикла; 
		ВычетыТЧВыполнены=Истина;
		ВладелецТовара=ЗаявкаНаХранениеШин.Контрагент;
		Если СкладКомпании=Справочники.СкладыКомпании.ПустаяСсылка() Тогда
			Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПереразмещениеТоваров") Тогда
				СкладКомпании=Основание.СкладПолучатель;
			Иначе
				СкладКомпании=Основание.СкладКомпании;
			КонецЕсли;
		КонецЕсли; 
		Возврат;
		
	КонецЕсли;
	
	Попытка
		Если НЕ обЗначениеНеЗаполнено(Основание.Контрагент) Тогда
			ВладелецТовара = Основание.Контрагент;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Если НЕ ВычетыТЧВыполнены Тогда
		дкЗаполнитьТабличнуюЧастьПоОснованию(ЭтотОбъект, Основание, , , Ложь);
	КонецЕсли;	
	Для Каждого СтрокаТЧ Из Товары Цикл
		Если СтрокаТЧ.КоличествоБазовое=0 Тогда
			СтрокаТЧ.КоличествоБазовое=СтрокаТЧ.Количество*СтрокаТЧ.Коэффициент;
		КонецЕсли; 
	КонецЦикла; 
	
	Для Каждого СтрТовар Из Товары Цикл
		Если СтрТовар.Коэффициент = 0 Тогда
			СтрТовар.Коэффициент = ?(обЗначениеНеЗаполнено(СтрТовар.ЕдиницаИзмерения), 1, СтрТовар.ЕдиницаИзмерения.Коэффициент);
		КонецЕсли;	
	КонецЦикла;	
	
	// Произведем свертку.
	Товары.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Коэффициент", "Количество, КоличествоБазовое");
	
	Если Товары.Количество() > 0 И СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый Тогда
		
		ВедетсяУчетЕдиниц               = (НЕ СкладКомпании.УчетЕдиницИзмерения = Перечисления.ВидыУчетаЕдиницИзмерения.НеВедется);
		ТочныйПоискПоЕдиницам           = (СкладКомпании.УчетЕдиницИзмерения = Перечисления.ВидыУчетаЕдиницИзмерения.ВедетсяПоПриходуИОтгрузке);
		ПредпочтительныйПоискПоЕдиницам = (СкладКомпании.УчетЕдиницИзмерения = Перечисления.ВидыУчетаЕдиницИзмерения.ВедетсяПоПриходу);
		
		Запрос = Новый Запрос();
		ТекстЗапроса = "ВЫБРАТЬ
		|	ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровОрдерныйСкладОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровОрдерныйСкладОстатки.Ячейка КАК Ячейка,
		|	ОстаткиТоваровОрдерныйСкладОстатки.Ячейка.КоординатаX КАК КоординатаX,
		|	ОстаткиТоваровОрдерныйСкладОстатки.Ячейка.КоординатаY КАК КоординатаY,
		|	ОстаткиТоваровОрдерныйСкладОстатки.Ячейка.Уровень КАК Уровень" + ?(ВедетсяУчетЕдиниц,",
		|	ОстаткиТоваровОрдерныйСкладОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ОстаткиТоваровОрдерныйСкладОстатки.ЕдиницаИзмерения.Коэффициент КАК Коэффициент", "") + ",
		|	ОстаткиТоваровОрдерныйСкладОстатки.КоличествоОстаток КАК Количество
		|ИЗ 
		|	РегистрНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(, СкладКомпании = &СкладКомпании И Номенклатура В (&Номенклатура)) КАК ОстаткиТоваровОрдерныйСкладОстатки
		|";
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);
		ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
		ТаблицаОстатков.Индексы.Добавить("Номенклатура");
		Если ТочныйПоискПоЕдиницам Тогда
			ТаблицаОстатков.Индексы.Добавить("ЕдиницаИзмерения");
		КонецЕсли;
		
		ТаблицаЯчеек = Новый ТаблицаЗначений;
		ТаблицаЯчеек.Колонки.Добавить("Кратность");
		ТаблицаЯчеек.Колонки.Добавить("РасстояниеДоВхода");
		ТаблицаЯчеек.Колонки.Добавить("СтрокаОстаткаЯчейки");
		
		ТоварыДляЗаполнения = Товары.ВыгрузитьКолонки();
		ТоварыДляЗаполнения.Колонки.Добавить("РасстояниеДоВхода", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		
		НачалоОтрезкаX = СкладКомпании.ВходX;
		НачалоОтрезкаY = СкладКомпании.ВходY;
		НачалоОтрезкаZ = СкладКомпании.ВходУровень;
		
		Отбор = Новый Структура;
		Для Каждого СтрТовар Из Товары Цикл
			СтрТовар.Ячейка = "";
			Отбор.Вставить("Номенклатура", СтрТовар.Номенклатура);
			Если ТочныйПоискПоЕдиницам Тогда
				Отбор.Вставить("ЕдиницаИзмерения", СтрТовар.ЕдиницаИзмерения);
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(СтрТовар.ХарактеристикаНоменклатуры) Тогда
				Отбор.Вставить("ХарактеристикаНоменклатуры", СтрТовар.ХарактеристикаНоменклатуры);
			КонецЕсли;
			МассивСтрок = ТаблицаОстатков.НайтиСтроки(Отбор);
			
			ТаблицаЯчеек.Очистить();
			Для Каждого СтрОстаток Из МассивСтрок Цикл
				
				Кратность = 0;
				Если ПредпочтительныйПоискПоЕдиницам Тогда
					Если СтрТовар.ЕдиницаИзмерения = СтрОстаток.ЕдиницаИзмерения Тогда
						Кратность = 2;
					Иначе
						Если СтрТовар.Коэффициент > СтрОстаток.Коэффициент Тогда
							Кратность = СтрОстаток.Коэффициент/СтрТовар.Коэффициент;
						Иначе
							Кратность = СтрТовар.Коэффициент/СтрОстаток.Коэффициент;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ДлиннаОтрезкаПоX = СтрОстаток.КоординатаX - НачалоОтрезкаX;
				ДлиннаОтрезкаПоY = СтрОстаток.КоординатаY - НачалоОтрезкаY;
				ДлиннаОтрезкаПоZ = СтрОстаток.Уровень     - НачалоОтрезкаZ;
				
				РасстояниеДоВхода = Sqrt((ДлиннаОтрезкаПоX*ДлиннаОтрезкаПоX) + (ДлиннаОтрезкаПоY*ДлиннаОтрезкаПоY) + (ДлиннаОтрезкаПоZ*ДлиннаОтрезкаПоZ));
				
				НоваяСтрока = ТаблицаЯчеек.Добавить();
				НоваяСтрока.СтрокаОстаткаЯчейки = СтрОстаток;
				НоваяСтрока.Кратность           = Кратность;
				НоваяСтрока.РасстояниеДоВхода   = РасстояниеДоВхода;
				
			КонецЦикла;
			
			Если ТаблицаЯчеек.Количество() > 0 Тогда
				
				СтрокаСортировки = "";
				Если ПредпочтительныйПоискПоЕдиницам Тогда
					СтрокаСортировки = СтрокаСортировки + "Кратность Убыв,";
				КонецЕсли;
				СтрокаСортировки = СтрокаСортировки + "РасстояниеДоВхода Возр";
				ТаблицаЯчеек.Сортировать(СтрокаСортировки);
				
				ОсталосьРаспределить = СтрТовар.КоличествоБазовое;
				Для Каждого СтрокаЯчейки Из ТаблицаЯчеек Цикл
					
					ТекЯчейка = СтрокаЯчейки.СтрокаОстаткаЯчейки;
					
					Если ОсталосьРаспределить = 0 Тогда
						Прервать;
					КонецЕсли;
					
					Если ТекЯчейка.Количество = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					КоличествоСтроки = Мин(ОсталосьРаспределить, ТекЯчейка.Количество);
					
					НоваяСтрока = ТоварыДляЗаполнения.Добавить();
					НоваяСтрока.Номенклатура               = СтрТовар.Номенклатура;
					НоваяСтрока.ХарактеристикаНоменклатуры = ТекЯчейка.ХарактеристикаНоменклатуры;
					Если ВедетсяУчетЕдиниц Тогда
						НоваяСтрока.ЕдиницаИзмерения       = ТекЯчейка.ЕдиницаИзмерения;
						НоваяСтрока.Коэффициент            = ТекЯчейка.Коэффициент;
						НоваяСтрока.Количество             = ?(ТекЯчейка.Коэффициент>0,Окр(КоличествоСтроки/ТекЯчейка.Коэффициент, 3, 1), КоличествоСтроки);
					Иначе
						НоваяСтрока.ЕдиницаИзмерения       = СтрТовар.ЕдиницаИзмерения;
						НоваяСтрока.Коэффициент            = СтрТовар.Коэффициент;
						НоваяСтрока.Количество             = ?(СтрТовар.Коэффициент>0,Окр(КоличествоСтроки/СтрТовар.Коэффициент, 3, 1), КоличествоСтроки);
					КонецЕсли;
					НоваяСтрока.Ячейка                     = ТекЯчейка.Ячейка;
					НоваяСтрока.КоличествоБазовое          = КоличествоСтроки;
					НоваяСтрока.РасстояниеДоВхода          = СтрокаЯчейки.РасстояниеДоВхода;
					
					Если КоличествоСтроки = ТекЯчейка.Количество Тогда
						ТаблицаОстатков.Удалить(ТекЯчейка);
					Иначе
						ТекЯчейка.Количество = ТекЯчейка.Количество - КоличествоСтроки;
					КонецЕсли;
					
					ОсталосьРаспределить = ОсталосьРаспределить - КоличествоСтроки;
					
				КонецЦикла;
				
				Если ОсталосьРаспределить > 0 Тогда
					НоваяСтрока = ТоварыДляЗаполнения.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТовар);
					НоваяСтрока.КоличествоБазовое = ОсталосьРаспределить;
					НоваяСтрока.Количество        = ?(НоваяСтрока.Коэффициент>0,Окр(ОсталосьРаспределить/НоваяСтрока.Коэффициент, 3, 1), ОсталосьРаспределить);
					НоваяСтрока.РасстояниеДоВхода = 999999999999;
				КонецЕсли;
				
			Иначе
				НоваяСтрока = ТоварыДляЗаполнения.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТовар);
				НоваяСтрока.РасстояниеДоВхода = 999999999999;
			КонецЕсли;
		КонецЦикла;
		ТоварыДляЗаполнения.Сортировать("РасстояниеДоВхода");
		Товары.Загрузить(ТоварыДляЗаполнения);
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		БылиДвижения = Ложь;
		
		// ордерный склад
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиТоваровОрдерныйСклад ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	// на всякий случай проверим тип склада, его могли поменять в самом справочнике
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Обычный Тогда
		Отказ = Истина;
		РезультатОбработки = "Склад документа имеет вид """ + СкладКомпании.ВидСклада + """. Проведение возможно только для ""Ордерного"" или ""Ячеистого"" склада!";
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли; 
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	СписокТоваров = ЭтотОбъект.Товары.ВыгрузитьКолонку("Номенклатура");
	
	// проведем остатки товаров по регистру ОстаткиТоваровОрдерныйСклад
	НаборЗаписейОстатки = Движения.ОстаткиТоваровОрдерныйСклад;
	НаборЗаписейОстатки.РежимПроведения = Режим;
	НаборЗаписейОстатки.ДокументОбъект  = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании   = СкладКомпании;
	Если ХозОперация=Справочники.ХозОперации.РасходныйСкладскойОрдерШин Тогда
		ЗаявкаНаХранениеШин=Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(ДокументОснование);
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=ЗаявкаНаХранениеШин;	
	Иначе
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=Неопределено;	
	КонецЕсли; 
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = Неопределено;
	НаборЗаписейОстатки.Приходовать     = Истина;
	НаборЗаписейОстатки.ПоБазовомуКоличеству = Истина;
	Отказ = НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	
	Если (НЕ Отказ) И (ХозОперация=Справочники.ХозОперации.РасходныйСкладскойОрдерШин) Тогда
		//Контроль остатка шин на хранении
		НаборЗаписейОстатки.Записать();
		ТаблицаОстатковТоваров=РегистрыНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(
			Новый Граница(МоментВремени(),ВидГраницы.Включая),
			Новый Структура("ЗаявкаНаХранениеШин",ЗаявкаНаХранениеШин),
			"СкладКомпании,Номенклатура,Ячейка",
			"Количество"
		);
		Если ТаблицаОстатковТоваров.Количество()>0 Тогда
			дкСообщитьРезультат("Выдача шин с хранения должна быть осуществлена в полном объеме.","Важное&Остатки товаров ордерного склада:",ЭтотОбъект,Неопределено);
			Отказ=Истина;
		КонецЕсли; 
		Для каждого СтрокаОстатков Из ТаблицаОстатковТоваров Цикл
			СтрРезультат = "Склад """+СтрокаОстатков.СкладКомпании+""" ["+СокрЛП(СтрокаОстатков.Номенклатура.Код)+"] товар """+СокрЛП(СтрокаОстатков.Номенклатура)+""". Остаток";
			Если СтрокаОстатков.СкладКомпании.ВидСклада=Перечисления.ВидыСкладов.Ячеистый Тогда
				СтрРезультат=СтрРезультат+" в ячейке """+СокрЛП(СтрокаОстатков.Ячейка)+"""";
			КонецЕсли; 
			СтрРезультат=СтрРезультат+" в количестве "+Формат(СтрокаОстатков.Количество,"ЧДЦ=0; ЧН=0")+".";
			дкСообщитьРезультат(СтрРезультат,"Важное&Остатки товаров ордерного склада:",ЭтотОбъект,СтрокаОстатков.Номенклатура);
		КонецЦикла; 
	КонецЕсли;
	
	// двигаем границу последовательности ордерного склада
	СвойствоСкладКомпанииБыл = неопределено;
	ДополнительныеСвойства.Свойство("СкладКомпанииБыл",СвойствоСкладКомпанииБыл);
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоСкладКомпанииБыл) Тогда
		НаборЗаписейГраницыОрдерныйСклад = РегистрыСведений.ГраницыОрдерныйСклад.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= Дата;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыОрдерныйСклад.ДокументСсылка	= Ссылка;
		НаборЗаписейГраницыОрдерныйСклад.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

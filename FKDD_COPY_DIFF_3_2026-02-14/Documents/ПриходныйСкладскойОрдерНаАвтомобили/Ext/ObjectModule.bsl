// Модуль документа ПриходныйСкладскойОрдерНаАвтомобили

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	Если Имя="" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПриходныйСкладскойОрдерНаАвтомобили", "Приходный складской ордер на автомобили", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый Тогда
		ОбязательныеАвтомобили.Вставить("Ячейка",1);
	КонецЕсли; 

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый И Уникальность Тогда
		
		СтруктураОтбора = Новый Структура;
		СписокНайденныхДублей = Новый СписокЗначений();
		Для Каждого СтрокаТаблицы Из Автомобили Цикл
			
			Если СписокНайденныхДублей.НайтиПоЗначению(СтрокаТаблицы) = Неопределено Тогда
				СтруктураОтбора.Вставить("Ячейка", СтрокаТаблицы.Ячейка);
				НайденныеСтроки = Автомобили.НайтиСтроки(СтруктураОтбора);
				Если НайденныеСтроки.Количество() > 1 Тогда
					// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
					Результат = Ложь;
					ДублирующиесяСтроки = "";
					// выведем строку сообщения...
					Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
						ДублирующиесяСтроки = ДублирующиесяСтроки + "," + СокрЛП(НайденнаяСтрока.НомерСтроки);
						// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
						СписокНайденныхДублей.Добавить(НайденнаяСтрока);
					КонецЦикла;
					дкДобавитьОшибку(Ошибки,"Таблица < Автомобили > значения колонки < Ячейка > не уникальны! Строки: " + Сред(ДублирующиесяСтроки, 2));
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);  	
			   							
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;

	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПриходныйСкладскойОрдерНаАвтомобили"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПриходныйСкладскойОрдерНаАвтомобили(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПриходныйСкладскойОрдерНаАвтомобили");
	
	//для начала настроим макет
	// Произведем преобразование макета в зависимости от типа ячеистого склада
	ЭтоЯчеистыйСклад = (СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый);
	Если НЕ ЭтоЯчеистыйСклад Тогда
		ОбластьАвтомобиль			= Макет.Область("Автомобиль");
		ОбластьЯчейка				= Макет.Область("Ячейка");
		ОбластьАвтомобиль.ШиринаКолонки	= ОбластьАвтомобиль.ШиринаКолонки + ОбластьЯчейка.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЯчейка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьВладелецТовара = Макет.Область("Владелец");
		Макет.УдалитьОбласть(ОбластьВладелецТовара,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьДокументОснование = Макет.Область("ДокументОснование");
		Макет.УдалитьОбласть(ОбластьДокументОснование,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	//bz++
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.ТекстЗаголовка				= ТекстЗаголовка;
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	КонецЕсли;
	ОбластьМакета.Параметры.ПредставлениеСклада			= СкладКомпании;
	//bz--
	
	Если НЕ обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьМакета.Параметры.ПредставлениеВладельца	= спПолучитьПредставление(ВладелецТовара);
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		ОбластьМакета.Параметры.Заполнить(ДокументОснование);
		ОбластьМакета.Параметры.ПредставлениеДокументаОснования	= дкПолучитьПредставление(ДокументОснование);
		Попытка
			ОбластьМакета.Параметры.Контрагент						= ДокументОснование.Контрагент;
			ОбластьМакета.Параметры.ПредставлениеКонтрагента		= спПолучитьПредставление(ДокументОснование.Контрагент);
			ОбластьМакета.Параметры.ДоговорВзаиморасчетов			= ДокументОснование.ДоговорВзаиморасчетов;
			ОбластьМакета.Параметры.ПредставлениеДоговора			= спПолучитьПредставление(ДокументОснование.ДоговорВзаиморасчетов);
		Исключение
		КонецПопытки; 
		ОбластьМакета.Параметры.ДокументОснование = ДокументОснование;
	КонецЕсли;
		
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Автомобили;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = Новый Структура;
		СтруктураСтроки.Вставить("НомерСтроки", СтрокаТабличнойЧасти.НомерСтроки);
		СтруктураСтроки.Вставить("Автомобиль", СтрокаТабличнойЧасти.Автомобиль);
		СтруктураСтроки.Вставить("АвтомобильНаименование", спПолучитьНаименование(СтрокаТабличнойЧасти.Автомобиль));
		СтруктураСтроки.Вставить("VIN", СтрокаТабличнойЧасти.Автомобиль.VIN);
		СтруктураСтроки.Вставить("Количество", СтрокаТабличнойЧасти.Количество);
		СтруктураСтроки.Вставить("Ячейка", СтрокаТабличнойЧасти.Ячейка);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы,,ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПриходныйСкладскойОрдерНаАвтомобили()

// Формирует печатную форму "Приходный ордер М-4"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьМ4(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("М4");
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// Выводим общие реквизиты шапки
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО		= Организация.КодПоОКПО;
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим заголовок документа
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокумента");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ДатаСоставления			= Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.МестоПриемки			= СкладКомпании;
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Попытка
			Контрагент										= ДокументОснование.Контрагент;
			ОбластьМакета.Параметры.ПоставщикНаименование	= спПолучитьНаименование(Контрагент);
			ОбластьМакета.Параметры.Поставщик				= Контрагент;
			ОбластьМакета.Параметры.ПоставщикКод			= Контрагент.Код;
		Исключение КонецПопытки;
	КонецЕсли;
	СтраховаяКомпания = дкОтветственноеЛицо(ЭтотОбъект,"СтраховаяКомпания");
	ОбластьМакета.Параметры.Заполнить(СтраховаяКомпания);
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.Валюта = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	СтрокНаСтранице = 23;
	СтрокШапки      = 9;
	СтрокПодвала    = 3;
	НомерСтраницы   = 1;

	КоличествоСтрок = ЭтотОбъект.Автомобили.Количество();
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// Инициализация итогов в документе
	ИтогоКоличествоПринято = 0;
	Ном = 0;
	ИтогоПоСтраницеКоличествоПринято = 0;
	
	ПодвалСтраницы=Макет.ПолучитьОбласть("ПодвалСтраницы");
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ВыборкаСтрокАвтомобили = ЭтотОбъект.Автомобили;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаСтрокАвтомобили Цикл
		Если обЗначениеНеЗаполнено(СтрокаТабличнойЧасти.Автомобиль) Тогда
			Сообщить("В одной из строк не заполнено значение автомобиля - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		Ном = Ном + 1;
		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;

		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 ИЛИ ((ПереноситьПоследнююСтроку = 1) И (Ном = КоличествоСтрок)) Тогда

			НомерСтраницы = НомерСтраницы + 1;
			ИтогоПоСтранице=Макет.ПолучитьОбласть("ИтогоПоСтранице");
			ИтогоПоСтранице.Параметры.ИтогоКоличествоПринято = ИтогоПоСтраницеКоличествоПринято;
			ИтогоПоСтраницеКоличествоПринято=0;
			ТабДокумент.Вывести(ИтогоПоСтранице);
			ТабДокумент.Вывести(ПодвалСтраницы);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;

		КоличествоПринято = СтрокаТабличнойЧасти.Количество;
		ОбластьМакета.Параметры.КоличествоПринято = Формат(КоличествоПринято,ФорматВыводаКоличества);
		//КоличествоПоДокументу берем из основания, если оно есть
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
			ТекАвтомобиль = СтрокаТабличнойЧасти.Автомобиль;
			// получаем список строк с данным автомобилем
			МассивНайденныхСтрок = ДокументОснование.Автомобили.НайтиСтроки(Новый Структура("Автомобиль",ТекАвтомобиль));
			СписокСтрокАвтомобилиОснования = Новый СписокЗначений; 
			КолвоИзОснования = 0;
			Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
				ТекСтрока=МассивНайденныхСтрок[Сч];
				КолвоИзОснования = ТекСтрока.Количество;
				Прервать;
			КонецЦикла;
			ОбластьМакета.Параметры.КоличествоПоДокументу = Формат(КолвоИзОснования,ФорматВыводаКоличества);
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.ТоварНаименование				= спПолучитьНаименование(СтрокаТабличнойЧасти.Автомобиль);
		ОбластьМакета.Параметры.ТоварКод						= СтрокаТабличнойЧасти.Автомобиль.VIN;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКод				= "796";
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование	= "Штука";
		ТабДокумент.Вывести(ОбластьМакета);
		ИтогоКоличествоПринято = ИтогоКоличествоПринято + КоличествоПринято;
		ИтогоПоСтраницеКоличествоПринято = ИтогоПоСтраницеКоличествоПринято + КоличествоПринято;
	КонецЦикла;

	//итоги по последней странице
	ИтогоПоСтранице=Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ИтогоПоСтранице.Параметры.ИтогоКоличествоПринято = Формат(ИтогоПоСтраницеКоличествоПринято,ФорматВыводаКоличества);
	ИтогоПоСтраницеКоличествоПринято=0;
	ТабДокумент.Вывести(ИтогоПоСтранице);
	
	// Выводим итоги по документу
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");

	ОбластьМакета.Параметры.ИтогоКоличествоПринято = Формат(ИтогоКоличествоПринято,ФорматВыводаКоличества);
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим итоги по документу
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху         = 0;
	ТабДокумент.ПолеСлева          = 0;
	ТабДокумент.ПолеСнизу          = 0;
	ТабДокумент.ПолеСправа         = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;
КонецФункции // ПечатьМ4()

// Печатает отчет "Карта склада"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьКартаСклада(ТабДокумент) Экспорт
	ОтчетКартаСклада = Отчеты.КартаСкладаАвтомобилей.Создать();
	ОтчетКартаСклада.Склад = СкладКомпании;
	ОтчетКартаСклада.ИсточникМаршрута = ЭтотОбъект.Ссылка;
	ФормаКартаСклада = ОтчетКартаСклада.ПолучитьФорму();
	ФормаКартаСклада.РежимОтчетаПереданный = 1;
	ФормаКартаСклада.Открыть();
	ФормаКартаСклада.НарисоватьМаршрутДокумента(ЭтотОбъект);
	ТабДокумент = Неопределено;
	Возврат ТабДокумент;
КонецФункции // ПечатьКартаСклада()

//Функция печати Акт о преме-передачи ТМЦ
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьМХ1(ТабДокумент) Экспорт

	Макет = ПолучитьОбщийМакет("МХ1");
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.НомерДокумента = Номер;
	ОбластьЗаголовок.Параметры.ДатаДокумента  = Дата;
	ОбластьЗаголовок.Параметры.Заполнить(ЭтотОбъект);
	ОбластьЗаголовок.Параметры.ОрганизацияПоОКПО                = Организация.КодПоОКПО;
	ОбластьЗаголовок.Параметры.ОрганизацияВидДеятельностиПоОКДП = Организация.КодПоОКДП;
	
	//bz++
	Если НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании) И НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании.КПП) Тогда
		СтрокаПредставления = спПолучитьПредставление(Организация);
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = СтрЗаменить(СтрокаПредставления, Организация.КПП, ПодразделениеКомпании.КПП);
	Иначе	
		ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	КонецЕсли;
	
	//ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	//ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	//ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	//ОбластьЗаголовок.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	//bz--
	
	Если НЕ обЗначениеНеЗаполнено(ВладелецТовара) Тогда
		ОбластьЗаголовок.Параметры.ПредставлениеВладельца = спПолучитьПредставление(ВладелецТовара);
		ОбластьЗаголовок.Параметры.ВладелецПоОКПО = ВладелецТовара.КодПоОКПО;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьЗаголовок);
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьШапка);
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");

	ВыборкаСтрок = Автомобили.Выгрузить();
	НомерСтроки = 1;
	Для Каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		ОбластьСтрока.Параметры.Заполнить(СтрокаТоваров);
		ОбластьСтрока.Параметры.НомерСтроки  = НомерСтроки;
		ОбластьСтрока.Параметры.Номенклатура = Строка(СтрокаТоваров.Автомобиль);
		ОбластьСтрока.Параметры.Количество =  Формат(1,ФорматВыводаКоличества);

		ТабДокумент.Вывести(ОбластьСтрока);

		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьИтого.Параметры.Количество = Формат(ВыборкаСтрок.Итог("Количество"),ФорматВыводаКоличества);
	ТабДокумент.Вывести(ОбластьИтого);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьМХ1(ТабДокумент)

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
			ВладелецТовара=ДокументОснование.Заказчик;
			Автомобили.Очистить();
			НоваяСтрокаТЧ=Автомобили.Добавить();
			НоваяСтрокаТЧ.Автомобиль=ДокументОснование.Автомобиль;
			НоваяСтрокаТЧ.Количество=1;
			ОбработкаРеквизита("Автомобили.Автомобиль",НоваяСтрокаТЧ);
		ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ИнвентаризацияАвтомобилей") Тогда
			Автомобили.Очистить();
			Для каждого СтрокаТЧ Из ДокументОснование.Автомобили Цикл
				Если СтрокаТЧ.КоличествоУчет>СтрокаТЧ.Количество Тогда
					НоваяСтрокаТЧ=Автомобили.Добавить();
					НоваяСтрокаТЧ.Автомобиль=СтрокаТЧ.Автомобиль;
					НоваяСтрокаТЧ.Количество=1;
					ОбработкаРеквизита("Автомобили.Автомобиль",НоваяСтрокаТЧ);
				КонецЕсли; 
			КонецЦикла; 
		ИначеЕсли ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПеремещениеАвтомобилей") Тогда
			СкладКомпании=ДокументОснование.СкладПолучатель;
			ОбработкаРеквизита("СкладКомпании");
		КонецЕсли; 
	КонецЕсли;	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// на всякий случай проверим тип склада, его могли поменять в самом справочнике
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Обычный Тогда
		Отказ = Истина;
		Сообщить("Склад документа имеет вид """ + СкладКомпании.ВидСклада + """. Проведение возможно только для ""Ордерного"" или ""Ячеистого"" склада!",СтатусСообщения.Важное);
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// ордерный склад
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилейОрдерныйСклад ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// проведем остатки автомобилей по регистру ОстаткиАвтомобилейОрдерныйСклад
	НаборЗаписейОстатки = Движения.ОстаткиАвтомобилейОрдерныйСклад;
	НаборЗаписейОстатки.РежимПроведения = Режим;
	НаборЗаписейОстатки.ДокументОбъект  = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании   = СкладКомпании;
	НаборЗаписейОстатки.РезультатЗапросаПоАвтомобилям = Неопределено;
	НаборЗаписейОстатки.Приходовать     = Истина;
	Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	
	// двигаем границу последовательности автомобилей на ордерном складе
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобилиОрдерныйСклад = РегистрыСведений.ГраницыАвтомобилиОрдерныйСклад.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.СкладКомпании    = СкладКомпании;
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.МоментВремени    = Дата;
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.СкладКомпанииБыл = ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.СкладКомпании    = ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.МоментВремени    = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.СкладКомпанииБыл = Неопределено;
			НаборЗаписейГраницыАвтомобилиОрдерныйСклад.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобилиОрдерныйСклад.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобилиОрдерныйСклад.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

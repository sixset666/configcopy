// Модуль документа ПеремещениеАвтомобилей

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Курс текущий валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка наличия резервов на автомобиль
//
// Параметры
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль()
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("МетаданныеИмя",ЭтотОбъект.Метаданные().Имя);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	ДокументОбъектСтруктура.Вставить("Автомобили",Автомобили.ВыгрузитьКолонку("Автомобиль"));
	Если ТипЗнч(РезультатОбработки) = Тип("ОбработкаОбъект.ИнформационноеПоле") Тогда
		СтрокаСообщений="";
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,СтрокаСообщений);
		Если НЕ ПустаяСтрока(СтрокаСообщений) Тогда
			Для НомерСтроки=1 По СтрЧислоСтрок(СтрокаСообщений) Цикл
				СообщениеПользователю=СтрПолучитьСтроку(СтрокаСообщений,НомерСтроки);
				РезультатОбработки.ДобавитьСтрокуСообщения(СообщениеПользователю,"Важное",Неопределено,Неопределено);
			КонецЦикла; 
		КонецЕсли; 
	Иначе
		Рез=зфЗащищенныеФункцииСервер.РезервыАвтомобиляПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,РезультатОбработки);
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("Количество",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("СкладПолучатель",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			Если ТипЗнч(СкладКомпании) = Тип("СправочникСсылка.СкладыКомпании") Тогда
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",   КонтрольПоПодразделению);
			КонецЕсли;
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки); 			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект, "Автомобили", "Автомобиль");
	
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПеремещениеАвтомобилей","Перемещение автомобилей",Истина);
	СписокХозОпераций.Добавить("ПеремещениеАвтомобилейВФилиал","Перемещение автомобилей в филиал",Ложь);
	СписокХозОпераций.Добавить("ПеремещениеАвтомобилейИзФилиала","Перемещение автомобилей из филиала",Ложь);
	Возврат СписокХозОпераций;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка) Экспорт
	
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	"+?(ТипЗнч(ДокументСсылка.СкладКомпании)=Тип("СправочникСсылка.СкладыКомпании"),"
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СкладКомпании.Подразделение КАК ПодразделениеОтправителя,","
	|	ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК СкладКомпании,
	|	Док.СкладКомпании КАК ПодразделениеОтправителя,")+
		?(ТипЗнч(ДокументСсылка.СкладПолучатель)=Тип("СправочникСсылка.СкладыКомпании"),"
	|	Док.СкладПолучатель КАК СкладПолучатель,
	|	Док.СкладПолучатель.Подразделение КАК ПодразделениеПолучателя,","
	|	ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК СкладПолучатель,
	|	Док.СкладПолучатель КАК ПодразделениеПолучателя,")+"
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ.ПеремещениеАвтомобилей КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Режим - режим проведения (оперативный/неоперативный)
// ДокументСсылка - ссылка на документ который надо допровести по партиям
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	//Если было отложенное проведение по партиям, то :
	//Очистим возможные движения по регистру комплектации автомобилей 
	НаборЗаписейПартионногоРегистра=РегистрыНакопления.КомплектацияАвтомобилей.СоздатьНаборЗаписей();
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Значение=ШапкаДокумента.Ссылка;
	НаборЗаписейПартионногоРегистра.Отбор.Регистратор.Использование=Истина;
	НаборЗаписейПартионногоРегистра.Записать();
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилей ИЛИ
		ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал Тогда
		//Спишем и оприходуем оборудование автомобиля
		НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ПеремещениеАвтомобилейАвтомобили.Автомобиль
		|ИЗ
		|	Документ.ПеремещениеАвтомобилей.Автомобили КАК ПеремещениеАвтомобилейАвтомобили
		|ГДЕ
		|	ПеремещениеАвтомобилейАвтомобили.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			//Спишем оборудование по документу
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=Выборка.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Номенклатура";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
			//Спишем опции, установленные производителем
			НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
			НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
			НаборЗаписейКомплектацияАвтомобилей.Автомобиль=Выборка.Автомобиль;
			НаборЗаписейКомплектацияАвтомобилей.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=ШапкаДокумента.МоментВремени;
			НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Опции";
			НаборЗаписейКомплектацияАвтомобилей.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
		КонецЦикла;
		Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилей Тогда
			СписаннаяКомплектацияАвтомобилей=НаборЗаписейКомплектацияАвтомобилей.Выгрузить();
			Для каждого СписаннаяОпция Из СписаннаяКомплектацияАвтомобилей Цикл
				НоваяЗапись=НаборЗаписейКомплектацияАвтомобилей.Добавить();
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.Период=СписаннаяОпция.Период;
				НоваяЗапись.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗапись.Автомобиль=СписаннаяОпция.Автомобиль;
				НоваяЗапись.СкладКомпании=ШапкаДокумента.СкладПолучатель;
				НоваяЗапись.Номенклатура=СписаннаяОпция.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры=СписаннаяОпция.ХарактеристикаНоменклатуры;
				НоваяЗапись.СтатусПартии=СписаннаяОпция.СтатусПартии;
				НоваяЗапись.Партия=СписаннаяОпция.Партия;
				НоваяЗапись.ГТД=СписаннаяОпция.ГТД;
				НоваяЗапись.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗапись.Количество=СписаннаяОпция.Количество;
				НоваяЗапись.Сумма=СписаннаяОпция.Сумма;
				НоваяЗапись.СуммаНДС=СписаннаяОпция.СуммаНДС;
				НоваяЗапись.СуммаУпр=СписаннаяОпция.СуммаУпр;
				НоваяЗапись.СуммаПродажи=СписаннаяОпция.СуммаПродажи;
				НоваяЗапись.СуммаПродажиУпр=СписаннаяОпция.СуммаПродажиУпр;
			КонецЦикла; 
		КонецЕсли;
		
		// БАЛАНС: Если происходит перемещение товаров между складами подразделений, принадлежащих
		// различным балансовым "веткам", то возможен разрыв баланса.
		Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилей Тогда
			ПодразделениеОтправителя      = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеОтправителя);
			ПодразделениеПолучатель       = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеПолучателя);
		    ДобавлятьКорректирующиеЗаписи = БалансВедетсяПоПодразделениям И (ПодразделениеОтправителя<>ПодразделениеПолучатель);
		Иначе
			ДобавлятьКорректирующиеЗаписи = Ложь;
		КонецЕсли; 
		
		// Доходы и расходы
		Если ДобавлятьКорректирующиеЗаписи ИЛИ (ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал) Тогда
			// У подразделения склада-отправителя возникает расход.
			// У подразделения склада-получателя - доход.
			// Если это перемещение в филиал, то движений "приход" не будет.
			НаборЗаписейКомплектацияАвтомобилей.Записать();
			СебестоимостьАвтомобилейУпр  = 0;
			СебестоимостьОборудованияУпр = 0;
			СебестоимостьПриход          = 0;
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
			Запрос.Текст="ВЫБРАТЬ
			             |	ОстаткиАвтомобилей.ВидДвижения КАК ВидДвижения,
			             |	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр), 0) КАК СуммаУпр
			             |ИЗ
			             |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
			             |ГДЕ
			             |	ОстаткиАвтомобилей.Регистратор = &Регистратор
			             |	И ОстаткиАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ОстаткиАвтомобилей.ВидДвижения";
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.ВидДвижения=ВидДвиженияНакопления.Расход Тогда
					СебестоимостьАвтомобилейУпр = СебестоимостьАвтомобилейУпр + Выборка.СуммаУпр;
				Иначе
					СебестоимостьПриход = СебестоимостьПриход + Выборка.СуммаУпр;
				КонецЕсли;
			КонецЦикла;
			Запрос.Текст="ВЫБРАТЬ
			             |	КомплектацияАвтомобилей.ВидДвижения КАК ВидДвижения,
			             |	ЕСТЬNULL(СУММА(КомплектацияАвтомобилей.СуммаУпр), 0) КАК СуммаУпр
			             |ИЗ
			             |	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			             |ГДЕ
			             |	КомплектацияАвтомобилей.Регистратор = &Регистратор
			             |	И КомплектацияАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	КомплектацияАвтомобилей.ВидДвижения";
			
			Выборка=Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
					СебестоимостьОборудованияУпр = СебестоимостьОборудованияУпр + Выборка.СуммаУпр;
				Иначе
					СебестоимостьПриход = СебестоимостьПриход + Выборка.СуммаУпр;
				КонецЕсли;
			КонецЦикла;
			Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
				СебестоимостьРасход = СебестоимостьАвтомобилейУпр + СебестоимостьОборудованияУпр;
				Если СебестоимостьРасход <> 0 Тогда
					НаборЗаписейДоходыРасходы = Движения.ДоходыИРасходы;
					НаборЗаписейДоходыРасходы.ДокументОбъект         = ЭтотОбъект;
					Если БалансВедетсяПоПодразделениям Тогда
						НаборЗаписейДоходыРасходы.Подразделение      = ШапкаДокумента.ПодразделениеОтправителя;
					КонецЕсли; 
					НаборЗаписейДоходыРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
					НаборЗаписейДоходыРасходы.Расход                 = СебестоимостьРасход;
					НаборЗаписейДоходыРасходы.ШапкаДокумента         = ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
				КонецЕсли;
			Иначе
				Если СебестоимостьАвтомобилейУпр <> 0 Тогда
					НаборЗаписейДоходыРасходы = Движения.ДоходыИРасходы;
					НаборЗаписейДоходыРасходы.ДокументОбъект         = ЭтотОбъект;
					Если БалансВедетсяПоПодразделениям Тогда
						НаборЗаписейДоходыРасходы.Подразделение      = ШапкаДокумента.ПодразделениеОтправителя;
					КонецЕсли; 
					НаборЗаписейДоходыРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
					НаборЗаписейДоходыРасходы.Расход                 = СебестоимостьАвтомобилейУпр;
					НаборЗаписейДоходыРасходы.ШапкаДокумента         = ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
				КонецЕсли;
				Если СебестоимостьОборудованияУпр <> 0 Тогда
					НаборЗаписейДоходыРасходы = Движения.ДоходыИРасходы;
					НаборЗаписейДоходыРасходы.ДокументОбъект         = ЭтотОбъект;
					Если БалансВедетсяПоПодразделениям Тогда
						НаборЗаписейДоходыРасходы.Подразделение      = ШапкаДокумента.ПодразделениеОтправителя;
					КонецЕсли; 
					НаборЗаписейДоходыРасходы.ВУпрВалюте             = Истина;
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
					НаборЗаписейДоходыРасходы.Расход                 = СебестоимостьОборудованияУпр;
					НаборЗаписейДоходыРасходы.ШапкаДокумента         = ШапкаДокумента;
					Отказ=НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
				КонецЕсли;
			КонецЕсли;
			
			Если СебестоимостьПриход<>0 Тогда
				НаборЗаписейДоходыРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыРасходы.ДокументОбъект=ЭтотОбъект;
				Если БалансВедетсяПоПодразделениям Тогда
					НаборЗаписейДоходыРасходы.Подразделение = ШапкаДокумента.ПодразделениеПолучателя;
				КонецЕсли; 
				НаборЗаписейДоходыРасходы.ВУпрВалюте=Истина;
				Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				Иначе
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				КонецЕсли;
				НаборЗаписейДоходыРасходы.Доход=СебестоимостьПриход;
				НаборЗаписейДоходыРасходы.ШапкаДокумента=ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 	
		
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
		// доходы и расходы
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ЕСТЬNULL(СУММА(ОстаткиАвтомобилей.СуммаУпр), 0) КАК СуммаУпр
		             |ИЗ
		             |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
		             |ГДЕ
		             |	ОстаткиАвтомобилей.Регистратор = &Регистратор
		             |	И ОстаткиАвтомобилей.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.СуммаУпр <> 0 Тогда
				НаборЗаписейДоходыРасходы = Движения.ДоходыИРасходы;
				НаборЗаписейДоходыРасходы.ДокументОбъект             = ЭтотОбъект;
				НаборЗаписейДоходыРасходы.ВУпрВалюте                 = Истина;
				Если БалансВедетсяПоПодразделениям Тогда
					НаборЗаписейДоходыРасходы.Подразделение          = ШапкаДокумента.ПодразделениеПолучателя;
				КонецЕсли; 
				Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				Иначе
					НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
				КонецЕсли;
				НаборЗаписейДоходыРасходы.Доход                      = Выборка.СуммаУпр;
				НаборЗаписейДоходыРасходы.ШапкаДокумента             = ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыРасходы.Приход() ИЛИ Отказ;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	// если идет допроведение, то надо получить те значения которые были раньше
	Если Режим<>РежимПроведенияДокумента.Оперативный И Ссылка<>ДокументСсылка Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		Результат = Запрос.Выполнить(); АвтомобильБыл=Неопределено;
		Если НЕ Результат.Пустой() Тогда
			АвтомобильБыл = Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("АвтомобильБыл",АвтомобильБыл);
		ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
	КонецЕсли;
	
	// двигаем границу последовательности комплектаций автомобилей
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.КомплектацияАвтомобилей.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		НаборЗаписейГраницыКомплектация = РегистрыСведений.ГраницыКомплектация.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаАвтомобилей = Движения.КомплектацияАвтомобилей.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыКомплектация.Автомобиль = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыКомплектация.МоментВремени  = ШапкаДокумента.Дата;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыКомплектация.Автомобиль		 = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыКомплектация.МоментВремени	 = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыКомплектация.АвтомобильБыл	 = Неопределено;
			НаборЗаписейГраницыКомплектация.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыКомплектация.ДокументСсылка = ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыКомплектация.ИзменитьГраницу();
	КонецЕсли;

	Возврат НЕ Отказ;
КонецФункции

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПеремещениеАвтомобилейАвтомобили.Ссылка КАК Ссылка,
	|	СУММА(ОстаткиАвтомобилей.СуммаУпр) КАК СуммаУпр,
	|	СУММА(КомплектацияАвтомобилейОстатки.СуммаУпрОстаток) КАК СуммаОпцийУпр
	|ИЗ
	|	Документ.ПеремещениеАвтомобилей.Автомобили КАК ПеремещениеАвтомобилейАвтомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|		ПО ПеремещениеАвтомобилейАвтомобили.Ссылка = ОстаткиАвтомобилей.Регистратор
	|			И ОстаткиАвтомобилей.Автомобиль = ПеремещениеАвтомобилейАвтомобили.Автомобиль
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Остатки(&НаДату) КАК КомплектацияАвтомобилейОстатки
	|		ПО ОстаткиАвтомобилей.Автомобиль = КомплектацияАвтомобилейОстатки.Автомобиль
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор = &ВыбРегистратор
	|	И ПеремещениеАвтомобилейАвтомобили.Ссылка = &ВыбРегистратор
	|	И ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияРегистра
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеАвтомобилейАвтомобили.Ссылка";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату",Дата);
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
		Запрос.УстановитьПараметр("ВидДвиженияРегистра",ВидДвиженияНакопления.Приход);	
	Иначе
		Запрос.УстановитьПараметр("ВидДвиженияРегистра",ВидДвиженияНакопления.Расход);	
	КонецЕсли;
	Запрос.УстановитьПараметр("ВыбРегистратор",ЭтотОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	ВалютаТек		= ЭтотОбъект.ВалютаДокумента;
	КурсТек			= ЭтотОбъект.КурсДокумента;
	ВалютаПересч	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
		СтруктураКурса	= обКурсДляВалюты(ВалютаПересч,Дата);
		КурсПересч		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсПересч		= ЭтотОбъект.КурсВалютыУпр;
	КонецЕсли;	
	Если Выборка.Следующий() Тогда
		Сумма = ?(Выборка.СуммаУпр=NULL,0,Выборка.СуммаУпр)+?(Выборка.СуммаОпцийУпр=NULL,0,Выборка.СуммаОпцийУпр);
		Сумма = обПересчет(Сумма, ВалютаПересч, КурсПересч, ВалютаТек, КурсТек);
	Иначе
		Сумма = 0;
	КонецЕсли;
	Возврат Сумма;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
		ДопПараметры.Вставить("СкладКомпании","СкладКомпании");
		Возврат ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="СкладПолучатель" Тогда
		Если ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("СкладКомпании","СкладПолучатель");
			Рез = ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			Рез = Истина;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		//Проверка корректности значения реквизита "СкладКомпании" И "СкладПолучатель"
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (НЕ обЗначениеНеЗаполнено(СкладПолучатель)) Тогда
			Если СкладКомпании=СкладПолучатель Тогда
				#Если Клиент Тогда
				Предупреждение("Склад получатель не может совпадать со складом отправителем!");
				#КонецЕсли
				Если ДопПараметры<>Неопределено И ТипЗнч(ДопПараметры)=Тип("Структура") Тогда
					РеквизитСклада=Неопределено;
					ДопПараметры.Свойство("СкладКомпании",РеквизитСклада);
					Если РеквизитСклада<>Неопределено И ТипЗнч(РеквизитСклада)=Тип("Строка") Тогда
						ЭтотОбъект[РеквизитСклада]=Неопределено;
						ОбработкаРеквизита(РеквизитСклада,ТекСтрока,ЭтаФорма,ДопПараметры);
					КонецЕсли; 
				КонецЕсли;
				Возврат Ложь;
			КонецЕсли; 
		КонецЕсли; 
		Если ТипЗнч(СкладКомпании) = Тип("СправочникСсылка.СкладыКомпании") И ЗначениеЗаполнено(СкладКомпании) Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладКомпании = Неопределено;
				дкОбработкаРеквизита(ЭтотОбъект,"СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
				Возврат Ложь;
			КонецЕсли; 
		КонецЕсли; 
		Если ТипЗнч(СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании") И ЗначениеЗаполнено(СкладПолучатель) Тогда
			Если СкладПолучатель.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладПолучатель + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				СкладПолучатель = Неопределено;
				дкОбработкаРеквизита(ЭтотОбъект,"СкладПолучатель",ТекСтрока,ЭтаФорма,ДопПараметры);
				Возврат Ложь;
			КонецЕсли; 
		КонецЕсли;
		Возврат Истина;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		// установим количество
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
		// Заполним ставку НДС
		ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
			//Для перемещения из филиала попробуем плучить себестоимость авто до перемещения
			Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ПеремещениеАвтомобилей") И
				 ЗначениеЗаполнено(ДокументОснование) И
				 ДокументОснование.ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал Тогда
				 Запрос=Новый Запрос;
				 Запрос.Текст="ВЫБРАТЬ
				              |	ОстаткиАвтомобилей.Автомобиль КАК Автомобиль,
				              |	ОстаткиАвтомобилей.СкладКомпании КАК СкладКомпании,
				              |	ОстаткиАвтомобилей.СтатусПартии КАК СтатусПартии,
				              |	ОстаткиАвтомобилей.Партия КАК Партия,
				              |	ОстаткиАвтомобилей.ГТД КАК ГТД,
				              |	СУММА(ОстаткиАвтомобилей.Количество) КАК Количество,
				              |	СУММА(ОстаткиАвтомобилей.Сумма) КАК Сумма,
				              |	СУММА(ОстаткиАвтомобилей.СуммаУпр) КАК СуммаУпр,
				              |	СУММА(ОстаткиАвтомобилей.СуммаНДС) КАК СуммаНДС
				              |ИЗ
				              |	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
				              |ГДЕ
				              |	ОстаткиАвтомобилей.Регистратор = &Регистратор
				              |	И ОстаткиАвтомобилей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
				              |	И ОстаткиАвтомобилей.Автомобиль = &Автомобиль
				              |
				              |СГРУППИРОВАТЬ ПО
				              |	ОстаткиАвтомобилей.Партия,
				              |	ОстаткиАвтомобилей.СтатусПартии,
				              |	ОстаткиАвтомобилей.Автомобиль,
				              |	ОстаткиАвтомобилей.СкладКомпании,
				              |	ОстаткиАвтомобилей.ГТД";
				 Запрос.УстановитьПараметр("Регистратор",ДокументОснование);
				 Запрос.УстановитьПараметр("Автомобиль",ТекСтрока.Автомобиль);
				 Выборка=Запрос.Выполнить().Выбрать();
				 Если Выборка.Следующий() Тогда
					 ТекСтрока.ГТД=Выборка.ГТД;
					 ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
					 СтруктураКурса=обКурсДляВалюты(ВалютаРегл,Дата);
					 КурсРегл=СтруктураКурса.Курс/?(СтруктураКурса.Кратность=0,1,СтруктураКурса.Кратность);
					 Если ВалютаДокумента=ВалютаРегл Тогда
					 	ТекСтрока.СуммаВсего=Выборка.Сумма;
					 Иначе
						ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
						СтруктураКурса=обКурсДляВалюты(ВалютаУпр,Дата);
						КурсУпр=СтруктураКурса.Курс/?(СтруктураКурса.Кратность=0,1,СтруктураКурса.Кратность);
					 	ТекСтрока.СуммаВсего=обПересчет(Выборка.Сумма,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсУпр);
					 КонецЕсли;
					 ОбработкаРеквизита("Автомобили.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры);
				 	 ТекСтрока.СуммаНДС=обПересчет(Выборка.СуммаНДС,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
				 Иначе
					 ТекСтрока.ГТД=Справочники.ГТД.ПустаяСсылка();
				 КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.Количество" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		Если ТекСтрока.Количество<>0 Тогда
			ТекСтрока.Цена=ТекСтрока.Сумма/ТекСтрока.Количество;
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС - Сумма всего равна сумме
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
			//Получим новую сумму НДС как процент от суммы всего
			СуммаБезНДС=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
			ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Иначе
			//НДС в цену не включен - Получим сумму НДС
			ТекСтрока.СуммаНДС=(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
			//Сумма всего равна сумма + сумма НДС
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Автомобили.СуммаВсего" Тогда
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			ТекСтрока.Сумма=ТекСтрока.СуммаВсего;
		Иначе
			//НДС в цену не включен
			ТекСтрока.Сумма=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаНДС" Тогда
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Накладная"
// Возвращает сформированный табличный документ:
Функция ПечатьПеремещениеАвтомобилей(ТабДокумент) Экспорт
	//Возврат ТабДокумент;
	Макет = ПолучитьМакет("ПеремещениеАвтомобилей");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
	ОбластьМакета.Параметры.ПредставлениеСкладаПолучателя = спПолучитьНаименование(СкладПолучатель);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
	// выборка запросом по движениям документа по регистру "ПартииТоваровКомпании"
	ТекстЗапроса = "ВЫБРАТЬ
	|	ОстаткиАвтомобилей.Автомобиль КАК Автомобиль,
	|	СУММА(ОстаткиАвтомобилей.СуммаУпр) КАК СуммаУпр,
	|	СУММА(КомплектацияАвтомобилейОстатки.СуммаУпрОстаток) КАК СуммаОпцийУпр
	|ИЗ
	|	Документ.ПеремещениеАвтомобилей.Автомобили КАК ПеремещениеАвтомобилейАвтомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|		ПО ПеремещениеАвтомобилейАвтомобили.Ссылка = ОстаткиАвтомобилей.Регистратор
	|			И ОстаткиАвтомобилей.Автомобиль = ПеремещениеАвтомобилейАвтомобили.Автомобиль
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Остатки(&НаДату) КАК КомплектацияАвтомобилейОстатки
	|		ПО ОстаткиАвтомобилей.Автомобиль = КомплектацияАвтомобилейОстатки.Автомобиль
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор = &ВыбРегистратор
	|	И ПеремещениеАвтомобилейАвтомобили.Ссылка = &ВыбРегистратор
	|	И ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияРегистра
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАвтомобилей.Автомобиль";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату",Дата);
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
		Запрос.УстановитьПараметр("ВидДвиженияРегистра",ВидДвиженияНакопления.Приход);	
	Иначе
		Запрос.УстановитьПараметр("ВидДвиженияРегистра",ВидДвиженияНакопления.Расход);	
	КонецЕсли;
	Запрос.УстановитьПараметр("ВыбРегистратор",ЭтотОбъект.Ссылка);
	ВыборкаТабличнойЧасти = Запрос.Выполнить().Выгрузить();
	
	ВалютаТек		= ЭтотОбъект.ВалютаДокумента;
	КурсТек			= ЭтотОбъект.КурсДокумента;
	ВалютаПересч	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
		СтруктураКурса	= обКурсДляВалюты(ВалютаПересч,Дата);
		КурсПересч		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсПересч		= ЭтотОбъект.КурсВалютыУпр;
	КонецЕсли;	
	
	Ном = 0;
	ИтогоСуммаДокумента	= 0;
	СтрокаТЧДляИтогов = Новый Структура("Сумма");	
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		Ном = Ном + 1;
		ОбластьМакета.Параметры.НомерСтроки=Ном;
		ОбластьМакета.Параметры.Автомобиль=СтрокаТабличнойЧасти.Автомобиль;
		ОбластьМакета.Параметры.АвтомобильНаименование=спПолучитьНаименование(СтрокаТабличнойЧасти.Автомобиль);		
		ОбластьМакета.Параметры.VIN=СтрокаТабличнойЧасти.Автомобиль.VIN;
		ОбластьМакета.Параметры.НомерКузова=СтрокаТабличнойЧасти.Автомобиль.НомерКузова;
		ОбластьМакета.Параметры.НомерДвигателя=СтрокаТабличнойЧасти.Автомобиль.НомерДвигателя;
		Сумма = ?(СтрокаТабличнойЧасти.СуммаУпр=NULL,0,СтрокаТабличнойЧасти.СуммаУпр)+?(СтрокаТабличнойЧасти.СуммаОпцийУпр=NULL,0,СтрокаТабличнойЧасти.СуммаОпцийУпр);
		Сумма = обПересчет(Сумма, ВалютаПересч, КурсПересч, ВалютаТек, КурсТек);
		ОбластьМакета.Параметры.Сумма = Формат(Сумма,ФорматВыводаСуммы);
		ИтогоСуммаДокумента=ИтогоСуммаДокумента+Сумма;
		СтрокаТЧДляИтогов.Сумма = Сумма;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТЧДляИтогов,СтруктураИтоговПоСтранице);
	КонецЦикла;
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.СуммаВсего = Формат(ИтогоСуммаДокумента,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(ИтогоСуммаДокумента,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ТОРГ13"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ13(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ТОРГ13");
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ЭтотОбъект.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ОтправительПодразделение	= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.ПолучательПодразделение		= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.СкладПолучатель);
	ОбластьМакета.Параметры.Номер						= дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	
	СтрокНаСтранице = 25;
	СтрокШапки      = 12;
	СтрокПодвала    = 5;
	НомерСтраницы   = 1;

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ВалютаРегл;
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ВыборкаСтрокТовары = ЭтотОбъект.Автомобили;
	
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();
    Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку	= 0;
	Иначе
		ЦелыхСтраницСПодвалом		= Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала		= Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку	= ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// инициализация итогов по странице
	ИтогКоличествоМестПоСтранице = 0;
	ИтогСуммыПоСтранице          = 0;

	// инициализация итогов по документу
	ИтогоКоличество  = 0;
	ИтогоСумма       = 0;
    Ном = 0;

	// Выводим многострочную часть документа
	ОбластьИтоговПоСтранице	= Макет.ПолучитьОбласть("ИтогиПоСтранице");
	ОбластьМакета			= Макет.ПолучитьОбласть("Строка");

	// выборка запросом по движениям документа по регистру "ПартииТоваровКомпании"
	ТекстЗапроса = "ВЫБРАТЬ
	|	ОстаткиАвтомобилей.Автомобиль КАК Автомобиль,
	|	СУММА(ОстаткиАвтомобилей.Сумма) КАК Сумма,
	|	СУММА(ОстаткиАвтомобилей.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ОстаткиАвтомобилей.СуммаНДС) КАК СуммаНДС,
	|	СУММА(КомплектацияАвтомобилейОстатки.СуммаОстаток) КАК СуммаОпций,
	|	СУММА(КомплектацияАвтомобилейОстатки.СуммаУпрОстаток) КАК СуммаОпцийУпр,
	|	СУММА(КомплектацияАвтомобилейОстатки.СуммаНДСОстаток) КАК СуммаОпцийНДС
	|ИЗ
	|	Документ.ПеремещениеАвтомобилей.Автомобили КАК ПеремещениеАвтомобилейАвтомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
	|		ПО ПеремещениеАвтомобилейАвтомобили.Ссылка = ОстаткиАвтомобилей.Регистратор
	|			И (ОстаткиАвтомобилей.Автомобиль = ПеремещениеАвтомобилейАвтомобили.Автомобиль)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Остатки(&НаДату, ) КАК КомплектацияАвтомобилейОстатки
	|		ПО (ОстаткиАвтомобилей.Автомобиль = КомплектацияАвтомобилейОстатки.Автомобиль)
	|ГДЕ
	|	ОстаткиАвтомобилей.Регистратор = &ВыбРегистратор
	|	И ПеремещениеАвтомобилейАвтомобили.Ссылка = &ВыбРегистратор
	|	И ОстаткиАвтомобилей.ВидДвижения = &ВидДвиженияРегистра
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАвтомобилей.Автомобиль";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату",Дата);
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
		Запрос.УстановитьПараметр("ВидДвиженияРегистра",ВидДвиженияНакопления.Приход);	
	Иначе
		Запрос.УстановитьПараметр("ВидДвиженияРегистра",ВидДвиженияНакопления.Расход);	
	КонецЕсли;
	Запрос.УстановитьПараметр("ВыбРегистратор",ЭтотОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВалютаТек		= ВалютаРегл;
	КурсТек			= ЭтотОбъект.КурсДокумента;
	ВалютаПересч	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
		СтруктураКурса	= обКурсДляВалюты(ВалютаПересч,Дата);
		КурсПересч		= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Иначе
		КурсПересч		= ЭтотОбъект.КурсВалютыУпр;
	КонецЕсли;	
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
	КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Пока Выборка.Следующий() Цикл
		Ном = Ном + 1;
		ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;
		
		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
			или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда
			ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
			ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			// инициализация итогов по странице
			ИтогКоличествоМестПоСтранице = 0;
			ИтогСуммыПоСтранице          = 0;
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
		КонецЕсли;

		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.Номенклатура				= Выборка.Автомобиль;
		ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(Выборка.Автомобиль) + ", " + спПолучитьНаименование(Выборка.Автомобиль.Модель);
		ОбластьМакета.Параметры.ТоварКод					= Выборка.Автомобиль.VIN;
		ОбластьМакета.Параметры.ЕдиницаИзмерения			= "шт";
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ	= "шт";
		ОбластьМакета.Параметры.КоличествоВОдномМесте		= 1;
		ОбластьМакета.Параметры.КоличествоМест 				= 1;
		Сумма = ?(Выборка.Сумма=NULL,0,Выборка.Сумма)+?(Выборка.СуммаОпций=NULL,0,Выборка.СуммаОпций);
		СуммаНДС = ?(Выборка.СуммаНДС=NULL,0,Выборка.СуммаНДС)+?(Выборка.СуммаОпцийНДС=NULL,0,Выборка.СуммаОпцийНДС);
		Сумма = Сумма - СуммаНДС;
		ОбластьМакета.Параметры.Сумма 						= Сумма;
		ОбластьМакета.Параметры.Цена						= Сумма;
		ТабДокумент.Вывести(ОбластьМакета);

		// Обновим итоги по странице
		ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице + 1;
		ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице          + Сумма;

		// Обновим итоги по документу
		ИтогоКоличество  = ИтогоКоличество  + 1;
		ИтогоСумма       = ИтогоСумма       + Сумма;
    КонецЦикла;

	ОбластьИтоговПоСтранице.Параметры.ИтогКоличествоМестПоСтранице = ИтогКоличествоМестПоСтранице;
	ОбластьИтоговПоСтранице.Параметры.ИтогСуммыПоСтранице          = ИтогСуммыПоСтранице;
    ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

	// Выводим итоги по документу в целом
	ОбластьМакета = Макет.ПолучитьОбласть("Всего");
	ОбластьМакета.Параметры.ИтогоКоличествоМест = ИтогоКоличество;
	ОбластьМакета.Параметры.ИтогоСумма          = ИтогоСумма;
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ИтогоСуммаПрописью = обЧислоПрописью(ИтогоСумма,ВалютаРегл);

	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 15;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму Т-1 "Товарно-транспортная накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТ1(ТабДокумент) Экспорт
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТТН");
	
	//дальше заполнить Шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	//ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления2=Новый Структура(
	"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с ");
	 
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.НомерДокумента                = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДокумента                 = Формат(Дата,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.Грузоотправитель			  = обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.СкладКомпании);
	ОбластьМакета.Параметры.Грузополучатель				  = обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.СкладПолучатель);
	
	Если ТипЗнч(ОбластьМакета.Параметры.Грузоотправитель) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОбластьМакета.Параметры.ГрузоотправительПредставление = спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,СтруктураПредставления2);
		ОбластьМакета.Параметры.ГрузополучательПоОКПО		  = ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузоотправительПредставление = ОбластьМакета.Параметры.Грузоотправитель.Наименование;
	КонецЕсли;
	
	Если ТипЗнч(ОбластьМакета.Параметры.Грузополучатель) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОбластьМакета.Параметры.ГрузополучательПредставление  = спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
		ОбластьМакета.Параметры.ГрузоотправительПоОКПО		  = ОбластьМакета.Параметры.Грузоотправитель.КодПоОКПО;
	Иначе
		ОбластьМакета.Параметры.ГрузополучательПредставление  = ОбластьМакета.Параметры.Грузополучатель.Наименование;
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Выводим заголовок таблицы
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьЗаголовокТаблицы.Параметры.Валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;	
	
	ОбластьСтрока	= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги	= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего	= Макет.ПолучитьОбласть("Всего");
	ОбластьПодвал	= Макет.ПолучитьОбласть("Подвал");
	
	// инициализация итогов по странице
	СтруктураИтоговПоСтранице = Новый Структура("Количество, СуммаВсего",0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы  = "Страница: " + НомерСтраницы;
	ОбластьЗаголовокТаблицы.Параметры.ТекстЗаголовка = "Товарно-транспортная накладная товарный раздел";
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПеремещениеАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
	|	СУММА(ПеремещениеАвтомобилейАвтомобили.Количество) КАК Количество,
	|	ЕСТЬNULL(СУММА(РегистрОстатковАвтомобилей.Сумма),0) КАК СуммаАвтомобиля,
	|	ЕСТЬNULL(СУММА(РегистрОстатковКомплектации.Сумма),0) КАК СуммаКомплектации
	|ИЗ
	|	Документ.ПеремещениеАвтомобилей.Автомобили КАК ПеремещениеАвтомобилейАвтомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей КАК РегистрОстатковАвтомобилей
	|		ПО ПеремещениеАвтомобилейАвтомобили.Ссылка = РегистрОстатковАвтомобилей.Регистратор
	|			И ПеремещениеАвтомобилейАвтомобили.Автомобиль = РегистрОстатковАвтомобилей.Автомобиль
	|			"+?(Проведен,"И РегистрОстатковАвтомобилей.ВидДвижения = &ВидДвиженияПриход","")+"
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей КАК РегистрОстатковКомплектации
	|		ПО ПеремещениеАвтомобилейАвтомобили.Ссылка = РегистрОстатковКомплектации.Регистратор
	|			И ПеремещениеАвтомобилейАвтомобили.Автомобиль = РегистрОстатковКомплектации.Автомобиль
	|			"+?(Проведен,"И РегистрОстатковКомплектации.ВидДвижения = &ВидДвиженияПриход","")+"
	|ГДЕ
	|	ПеремещениеАвтомобилейАвтомобили.Ссылка = &ВыбРегистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеАвтомобилейАвтомобили.Автомобиль
	|";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВыбРегистратор",ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВидДвиженияПриход",?(ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал,ВидДвиженияНакопления.Расход,ВидДвиженияНакопления.Приход));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// инициализация итогов по документу
	Ном             = 0;
	ИтогоМест       = 0;
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	
	КоличествоСтрок = Выборка.Количество();
	
	//доп. области
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьВсего);
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	
	Пока Выборка.Следующий() Цикл                    
		//заполняем данные строки
		Ном = Ном + 1;
		
		Сумма=Выборка.СуммаАвтомобиля+Выборка.СуммаКомплектации;
		
		ОбластьСтрока.Параметры.КодПродукции				= Выборка.Автомобиль.VIN;
		//ОбластьСтрока.Параметры.Артикул 					= ;
		ОбластьСтрока.Параметры.Количество					= Формат(Выборка.Количество, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Цена						= Формат(Сумма, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.ТоварНаименование			= спПолучитьНаименование(Выборка.Автомобиль);
		ОбластьСтрока.Параметры.Номенклатура				= Выборка.Автомобиль;
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= спПолучитьНаименование(Справочники.КлассификаторЕдиницИзмерения.шт);
		ОбластьСтрока.Параметры.ВидУпаковки					= Справочники.КлассификаторЕдиницИзмерения.шт;
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(Выборка.Количество, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.СуммаВсего					= Формат(Сумма, ФорматВыводаСуммы);

		ИтогоМест		= ИтогоМест + Выборка.Количество;
		ИтогоКоличество	= ИтогоКоличество + Выборка.Количество;
		ИтогоСумма		= ИтогоСумма + Сумма;
		
		//выводим строку, делая проверку попадания на лист		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, ?(Ном=КоличествоСтрок,мсвДопОбластиПодвала,Неопределено));
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("Количество, СуммаВсего",0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//обновим итоги по странице
		СтруктураИтоговПоСтранице.Количество = СтруктураИтоговПоСтранице.Количество + Выборка.Количество;
		СтруктураИтоговПоСтранице.СуммаВсего = СтруктураИтоговПоСтранице.СуммаВсего + Сумма;
	КонецЦикла;
	
	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.Количество = Формат(ИтогоКоличество, ФорматВыводаКоличества);
	ОбластьВсего.Параметры.СуммаВсего = Формат(ИтогоСумма, ФорматВыводаСуммы);
	
	ТабДокумент.Вывести(ОбластьВсего);
	
	// Выводим подвал документа
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьПодвал.Параметры.Заполнить(Получил);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок,,",,,,,,,,0");
	ОбластьПодвал.Параметры.КоличествоСтрок         = КоличествоСтрок;
	ОбластьПодвал.Параметры.ОтпущеноНаСуммуПрописью = обЧислоПрописью(ИтогоСумма, ВалютаРегл);
	ОбластьПодвал.Параметры.ВсегоКОплате			 = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ВсегоМестПрописью       = ЧислоПрописью(КоличествоСтрок,,",,,,,,,,0");
	ОбластьПодвал.Параметры.ВсегоНаименований       = ЧислоПрописью(КоличествоСтрок,,",,,,,,,,0");
	
	ТабДокумент.Вывести(ОбластьПодвал);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьМакета   = Макет.ПолучитьОбласть("ТранспортныйРаздел");
	
	ЛицензионнаяКарточка 		= обПолучитьЗначениеСвойства(ЭтотОбъект, "ЛицензионнаяКарточка");
	СрокДоставки 				= обПолучитьЗначениеСвойства(ЭтотОбъект, "СрокДоставки");
	Перевозчик 					= обПолучитьЗначениеСвойства(ЭтотОбъект, "ОрганизацияПеревозчик");
	МаркаАвтомобиля     	    = обПолучитьЗначениеСвойства(ЭтотОбъект, "МаркаАвтомобиля");
	ГосНомерАвтомобиля	        = обПолучитьЗначениеСвойства(ЭтотОбъект, "ГосНомерАвтомобиля");
	Заказчик	 				= обПолучитьЗначениеСвойства(ЭтотОбъект, "ОрганизацияЗаказчик");
	Водитель					= обПолучитьЗначениеСвойства(ЭтотОбъект, "ФИОВодителя");
	ВодительскоеУдостоверение	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ВодительскоеУдостоверение");
	ВидПеревозки	            = обПолучитьЗначениеСвойства(ЭтотОбъект, "ВидПеревозки");
	ПунктПогрузки             	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ПунктПогрузки");
	ПунктРазгрузки            	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ПунктРазгрузки");
	МаркаПрицепа 			  	= обПолучитьЗначениеСвойства(ЭтотОбъект, "Прицеп");
	ГосНомерПрицепа	           	= обПолучитьЗначениеСвойства(ЭтотОбъект, "ГосНомерПрицепа");
	
	Если ТипЗнч(ЛицензионнаяКарточка) = Тип("Булево") Тогда
		ШрифтСтандарт   = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , , НЕ ЛицензионнаяКарточка);
		ШрифтОграничено = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , , ЛицензионнаяКарточка);
		ОбластьМакета.Области.Стандарт.Шрифт   = ШрифтСтандарт;
		ОбластьМакета.Области.Ограничено.Шрифт = ШрифтОграничено;
	КонецЕсли;
	
	ОбластьМакета.Параметры.СрокДоставки              = СрокДоставки;
	ОбластьМакета.Параметры.Номер                     = НомерДляПечати;
	ОбластьМакета.Параметры.ОрганизацияПеревозчик     = Перевозчик;
	ОбластьМакета.Параметры.МаркаАвтомобиля           = МаркаАвтомобиля;
	ОбластьМакета.Параметры.ГосНомерАвтомобиля        = ГосНомерАвтомобиля;
	ОбластьМакета.Параметры.ОрганизацияЗаказчик       = Заказчик;
	ОбластьМакета.Параметры.ФИОВодителя               = Водитель;
	ОбластьМакета.Параметры.ВодительскоеУдостоверение = ВодительскоеУдостоверение;
	ОбластьМакета.Параметры.ВидПеревозки              = ВидПеревозки;
	ОбластьМакета.Параметры.ПунктПогрузки             = ПунктПогрузки;
	ОбластьМакета.Параметры.ПунктРазгрузки            = ПунктРазгрузки;
	ОбластьМакета.Параметры.Прицеп                    = МаркаПрицепа;
	ОбластьМакета.Параметры.ГосНомерПрицепа           = ГосНомерПрицепа;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СведенияОГрузе");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалСведенийОГрузе");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПогрузочныеОперации");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ПрочиеСведения");
	ОбластьМакета.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции //ПечатьТ1()

// Формирует печатную форму Приложение №4 "Транспортная накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПриложение4(ТабДокумент) Экспорт
	ТабДокумент = дкПечатьНовойТТН(ЭтотОбъект,ТабДокумент);
	Возврат ТабДокумент;
КонецФункции // ПечатьПриложение4()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание, ТекстЗаполнения, СтандартнаяОбработка)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПеремещениеАвтомобилей") Тогда
		Если Основание.ХозОперация = Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал Тогда
			ХозОперация = Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала;
			Для Каждого СтрокаТЧ Из Автомобили Цикл
				ОбработкаРеквизита("Автомобили.Автомобиль",СтрокаТЧ);
			КонецЦикла;
		КонецЕсли;
		СкладКомпании=Основание.СкладПолучатель;
		СкладПолучатель=Основание.СкладКомпании;
	ИначеЕсли обЭтотТип(Основание, "ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
		// проверим хоз. операцию
		Если Основание.ХозОперация <> Справочники.ХозОперации.ТаможеннаяДекларацияИмпортАвтомобилей Тогда
			Сообщить(НСтр("ru = 'Ввод возможен только на основании документа с хоз. операцией Таможенная декларация(импорт автомобилей)'"));
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		Автомобили.Очистить();
		
		// заполняем табличную часть автомобилей
		Для Каждого Автомобиль Из Основание.Автомобили Цикл
			НоваяСтрока = Автомобили.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Автомобиль);
			НоваяСтрока.Количество = 1;
		КонецЦикла;
	КонецЕсли;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//БИЗТЕХ МАМОНОВ 21.11.2025 ++
	//Запрещаем запись, если больше 1 авто в ТЧ. Исключение право "БТ_ВозмещениеАвтомобилейЛогист" и роль "ПолныеПрава"
	Если Автомобили.Количество() > 1 Тогда
		ПравоЛогист = ПланыВидовХарактеристик.ПраваИНастройки.НайтиПоКоду("49190");
		Если НЕ(обПраво(ПравоЛогист,,,ЭтотОбъект) ИЛИ РольДоступна("ПолныеПрава")) Тогда
			#Если Клиент Тогда
				Предупреждение("Запрещено перемещать более 1 авто в документе");
			#Иначе
				Сообщить("Запрещено перемещать более 1 авто в документе");	
			#КонецЕсли
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	//БИЗТЕХ МАМОНОВ 21.11.2025 --
	
	ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	//Проверим корректность склада
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И ТипЗнч(СкладКомпании)=Тип("СправочникСсылка.СкладыКомпании") Тогда
			Если СкладКомпании.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладКомпании + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 	
	Если НЕ Отказ Тогда
		Если НЕ обЗначениеНеЗаполнено(СкладПолучатель) И ТипЗнч(СкладПолучатель)=Тип("СправочникСсылка.СкладыКомпании") Тогда
			Если СкладПолучатель.Розничный Тогда
				#Если Клиент Тогда
				Предупреждение("Выбранный склад: """ + СкладПолучатель + """ является розничным!
				|Выберите оптовый склад",10);
				#КонецЕсли
				Отказ=Истина; Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 	
	
	Если ХозОперация<>Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
		Для каждого СтрокаАвтомобилей Из Автомобили Цикл
			Если СтрокаАвтомобилей.Цена<>0 Тогда
				СтрокаАвтомобилей.Цена=0;
				ОбработкаРеквизита("Автомобили.Цена",СтрокаАвтомобилей);
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(СтрокаАвтомобилей.ГТД) Тогда
				СтрокаАвтомобилей.ГТД=Справочники.ГТД.ПустаяСсылка();
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильСкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// комплектация
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Автомобиль ИЗ РегистрНакопления.КомплектацияАвтомобилей ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// проверим, а не равны ли склады
	Если СкладПолучатель=СкладКомпании Тогда
		Сообщить("Склад-отправитель равен складу-получателю! Движения бессмысленны!"); Отказ=Истина;
		Возврат;
	КонецЕсли;
	
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал Тогда
		Отказ = ПроверитьЗаказыНаАвтомобиль() ИЛИ Отказ;
	КонецЕсли;
	
	НаборЗаписейОстаткиАвтомобилей=Движения.ОстаткиАвтомобилей;
	Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилей Тогда
		//Спишем автомобиль со склада отправителя
		НаборЗаписейОстаткиАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстаткиАвтомобилей.РезультатЗапросаПоАвтомобилям=Неопределено;
		НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладКомпании;
		НаборЗаписейОстаткиАвтомобилей.СкладПолучатель=СкладПолучатель;
		Отказ=НЕ НаборЗаписейОстаткиАвтомобилей.Переместить() ИЛИ Отказ;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал Тогда
		//Спишем автомобиль со склада отправителя
		НаборЗаписейОстаткиАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстаткиАвтомобилей.РезультатЗапросаПоАвтомобилям=Неопределено;
		НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладКомпании;
		Отказ=НЕ НаборЗаписейОстаткиАвтомобилей.Расход() ИЛИ Отказ;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
		//Оприходуем на склад получатель
		НаборЗаписейОстаткиАвтомобилей.РежимПроведения=Режим;
		НаборЗаписейОстаткиАвтомобилей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейОстаткиАвтомобилей.РезультатЗапросаПоАвтомобилям=Неопределено;
		НаборЗаписейОстаткиАвтомобилей.СкладКомпании=СкладПолучатель;
		НаборЗаписейОстаткиАвтомобилей.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
		НаборЗаписейОстаткиАвтомобилей.Партия=Ссылка;
		Отказ=НЕ НаборЗаписейОстаткиАвтомобилей.Приход() ИЛИ Отказ;
	КонецЕсли;
	Если НЕ Отказ Тогда
		НаборЗаписейОстаткиАвтомобилей.Записать();
	КонецЕсли; 
	
	// проведем партии товаров
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// двигаем границу последовательности автомобилей
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильСкладКомпанииБыл) Тогда
		// двигаем границу последовательности автомобилей
		НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			МассивСкладов = Новый Массив;
			Если ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилей Тогда
				МассивСкладов.Добавить(СкладКомпании);
				МассивСкладов.Добавить(СкладПолучатель);
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейВФилиал Тогда
				МассивСкладов.Добавить(СкладКомпании);
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ПеремещениеАвтомобилейИзФилиала Тогда
				МассивСкладов.Добавить(СкладПолучатель);
			КонецЕсли;
			НаборЗаписейГраницыАвтомобили = РегистрыСведений.ГраницыАвтомобили.СоздатьНаборЗаписей();
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= МассивСкладов;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= Дата;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыАвтомобили.СкладКомпании		= ДополнительныеСвойства.АвтомобильСкладКомпанииБыл;
			НаборЗаписейГраницыАвтомобили.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыАвтомобили.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыАвтомобили.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыАвтомобили.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыАвтомобили.ИзменитьГраницу();
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		СуммаДокумента=РассчитатьСуммуВсего();
		ЭтотОбъект.ОбменДанными.Загрузка=Истина;
		Записать();
		ЭтотОбъект.ОбменДанными.Загрузка=Ложь;
	КонецЕсли; 
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

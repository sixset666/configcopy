// Модуль документа УведомлениеОбОстаткахПрослеживаемыхТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ВыполнитьДвиженияПоГТД(Отказ)
	
	// Получим партии и ГТД текущих номенклатур
	Если ТипЗнч(ПервичныйДокумент) = Тип("ДокументСсылка.Инвентаризация") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
		|ПОМЕСТИТЬ ПрослеживаемыйТовар
		|ИЗ
		|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		|ГДЕ
		|	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
		|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровКомпанииОстатки.СтатусПартии КАК СтатусПартии,
		|	ПартииТоваровКомпанииОстатки.Партия КАК Партия,
		|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ПартииПрослеживаемыхТоваров
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						ПрослеживаемыйТовар.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ПрослеживаемыйТовар КАК ПрослеживаемыйТовар)
		|				И СкладКомпании = &СкладКомпании) КАК ПартииТоваровКомпанииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГТДПартийТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
		|	ГТДПартийТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ГТДПартийТоваровКомпанииОстатки.Партия КАК Партия,
		|	ГТДПартийТоваровКомпанииОстатки.ГТД КАК ГТД,
		|	ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ГТДПрослеживаемогоТовара
		|ИЗ
		|	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						ПрослеживаемыйТовар.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ПрослеживаемыйТовар КАК ПрослеживаемыйТовар)
		|				И СкладКомпании = &СкладКомпании) КАК ГТДПартийТоваровКомпанииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииПрослеживаемыхТоваров.СкладКомпании КАК СкладКомпании,
		|	ПартииПрослеживаемыхТоваров.Номенклатура КАК Номенклатура,
		|	ПартииПрослеживаемыхТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииПрослеживаемыхТоваров.Партия КАК Партия,
		|	ПартииПрослеживаемыхТоваров.КоличествоОстаток КАК КоличествоПартия,
		|	ЕСТЬNULL(ГТДПрослеживаемогоТовара.ГТД, ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)) КАК ГТД,
		|	ЕСТЬNULL(ГТДПрослеживаемогоТовара.КоличествоОстаток, 0) КАК КоличествоГТД,
		|	ВЫБОР
		|		КОГДА ГТДПрослеживаемогоТовара.ГТД ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ГТДПрослеживаемогоТовара.ГТД.РНПТ
		|	КОНЕЦ КАК ЭтоРНПТ
		|ПОМЕСТИТЬ ТаблицаГТДПартий
		|ИЗ
		|	ПартииПрослеживаемыхТоваров КАК ПартииПрослеживаемыхТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГТДПрослеживаемогоТовара КАК ГТДПрослеживаемогоТовара
		|		ПО ПартииПрослеживаемыхТоваров.Номенклатура = ГТДПрослеживаемогоТовара.Номенклатура
		|			И ПартииПрослеживаемыхТоваров.СкладКомпании = ГТДПрослеживаемогоТовара.СкладКомпании
		|			И ПартииПрослеживаемыхТоваров.ХарактеристикаНоменклатуры = ГТДПрослеживаемогоТовара.ХарактеристикаНоменклатуры
		|			И ПартииПрослеживаемыхТоваров.Партия = ГТДПрослеживаемогоТовара.Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПартииПрослеживаемыхТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ГТДПрослеживаемогоТовара
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрослеживаемыйТовар.Номенклатура КАК Номенклатура,
		|	ПрослеживаемыйТовар.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	ТаблицаГТДПартий.СкладКомпании КАК СкладКомпании,
		|	ТаблицаГТДПартий.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТаблицаГТДПартий.Партия КАК Партия,
		|	ЕСТЬNULL(ТаблицаГТДПартий.КоличествоПартия, 0) КАК КоличествоПоПартиям,
		|	ТаблицаГТДПартий.ГТД КАК ГТД,
		|	ЕСТЬNULL(ТаблицаГТДПартий.КоличествоГТД, 0) КАК КоличествоПоГТД,
		|	ТаблицаГТДПартий.ЭтоРНПТ КАК ЭтоРНПТ
		|ИЗ
		|	ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаГТДПартий КАК ТаблицаГТДПартий
		|		ПО ПрослеживаемыйТовар.Номенклатура = ТаблицаГТДПартий.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГТД УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(КоличествоПрослеживаемости),
		|	СУММА(КоличествоПоПартиям)
		|ПО
		|	Номенклатура";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("СкладКомпании", ПервичныйДокумент.СкладКомпании);
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		
		ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНоменклатуры.Следующий() Цикл
			
			КоличествоНоменклатуры = ВыборкаНоменклатуры.КоличествоПрослеживаемости;
			
			Выборка = ВыборкаНоменклатуры.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				КоличествоСписать = 0;
				СписатьГТД = Ложь;
				Если Выборка.КоличествоПоГТД <> 0 Тогда
					
					// У нас он уже корректно указан
					Если Выборка.ЭтоРНПТ Тогда
						Продолжить;
					КонецЕсли;
					
					СписатьГТД = Истина;
					Если Выборка.КоличествоПоГТД < КоличествоНоменклатуры Тогда
						КоличествоСписать = Выборка.КоличествоПоГТД;
						оличествоНоменклатуры = КоличествоНоменклатуры - КоличествоСписать;
					Иначе
						КоличествоСписать = КоличествоНоменклатуры;
						КоличествоНоменклатуры = 0;
					КонецЕсли;
					
				ИначеЕсли Выборка.КоличествоПоПартиям < КоличествоНоменклатуры Тогда
					КоличествоСписать = Выборка.КоличествоПоПартиям;
					КоличествоНоменклатуры = КоличествоНоменклатуры - КоличествоСписать;
				Иначе
					КоличествоСписать = КоличествоНоменклатуры;
					КоличествоНоменклатуры = 0;
				КонецЕсли;
				
				Если СписатьГТД Тогда
					
					НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
					НоваяЗапись.Регистратор = Ссылка;
					НоваяЗапись.ХозОперация = ХозОперация;
					НоваяЗапись.Период = Дата;
					НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
					НоваяЗапись.Количество = КоличествоСписать;
					
				КонецЕсли;
				
				НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
				НоваяЗапись.Регистратор = Ссылка;
				НоваяЗапись.ХозОперация = ХозОперация;
				НоваяЗапись.Период = Дата;
				НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				НоваяЗапись.Количество = КоличествоСписать;
				НоваяЗапись.ГТД = РНПТ;
				
				Если КоличествоНоменклатуры = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если КоличествоНоменклатуры > 0 Тогда
				дкСообщитьРезультат("РНПТ прослеживаемого товара " + ВыборкаНоменклатуры.Номенклатура + " была не полностью распределена.",, ЭтотОбъект);
				Отказ=Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
		               |	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
		               |ПОМЕСТИТЬ ТаблицаАвтомобилей
		               |ИЗ
		               |	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Товары КАК УведомлениеОбОстаткахПрослеживаемыхТоваровТовары
		               |ГДЕ
		               |	УведомлениеОбОстаткахПрослеживаемыхТоваровТовары.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТаблицаАвтомобилей.Номенклатура КАК Автомобиль,
		               |	ТаблицаАвтомобилей.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		               |	ОстаткиАвтомобилейОстатки.СкладКомпании КАК СкладКомпании,
		               |	ОстаткиАвтомобилейОстатки.СтатусПартии КАК СтатусПартии,
		               |	ОстаткиАвтомобилейОстатки.Партия КАК Партия,
		               |	ОстаткиАвтомобилейОстатки.ГТД КАК ГТД,
		               |	ЕСТЬNULL(ОстаткиАвтомобилейОстатки.КоличествоОстаток, 0) КАК Количество,
		               |	ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаОстаток, 0) КАК Сумма,
		               |	ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр,
		               |	ЕСТЬNULL(ОстаткиАвтомобилейОстатки.СуммаНДСОстаток, 0) КАК СуммаНДС
		               |ИЗ
		               |	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей.Остатки(
		               |				&МоментВремени,
		               |				Автомобиль В
		               |						(ВЫБРАТЬ
		               |							ТаблицаАвтомобилей.Номенклатура КАК Номенклатура
		               |						ИЗ
		               |							ТаблицаАвтомобилей КАК ТаблицаАвтомобилей)
		               |					И СкладКомпании = &СкладКомпании) КАК ОстаткиАвтомобилейОстатки
		               |		ПО ТаблицаАвтомобилей.Номенклатура = ОстаткиАвтомобилейОстатки.Автомобиль";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("СкладКомпании", ПервичныйДокумент.СкладКомпании);
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Количество = 0 Тогда
				дкСообщитьРезультат("Автомобиль " + Выборка.Автомобиль + " не найден на остатках! РНПТ не присвоен",, ЭтотОбъект);
				Продолжить;
			КонецЕсли;
			
			// Спишем автомобиль с текущей ГТД
			НоваяЗапись = Движения.ОстаткиАвтомобилей.Добавить();
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.ХозОперация = ХозОперация;
			НоваяЗапись.Период = Дата;
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			
			// Добавим заново автомобль с РНПТ
			НоваяЗапись = Движения.ОстаткиАвтомобилей.Добавить();
			НоваяЗапись.Регистратор = Ссылка;
			НоваяЗапись.ХозОперация = ХозОперация;
			НоваяЗапись.Период = Дата;
			НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Приход;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			НоваяЗапись.ГТД = РНПТ;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("КоличествоПрослеживаемости",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ПервичныйДокумент",1);
	ОбязательныеРеквизиты.Вставить("Состояние",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("КодТНВЭД",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Если Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.ПолученОтвет Тогда
		ОбязательныеРеквизиты.Вставить("РНПТ",1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УведомлениеОбОстаткахПрослеживаемыхТоваров",		"Уведомление об остатках прослеживаемых товаров",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.КодОКПД2) Тогда
			ТекСтрока.КодОКПД2 = ТекСтрока.Номенклатура.КодОКПД2;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры	= Новый Структура();
		КонецЕсли; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.КоличествоПрослеживаемости = ТекСтрока.Количество * ?(НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) ИЛИ ЕдиницаИзмерения.Коэффициент = 0, 1, ЕдиницаИзмерения.Коэффициент);
		Возврат Рез;
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
	// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
	//
	// Параметры:
	//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
	//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
	//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
	//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
	//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
	//
	// Возвращаемое значение:
	//   ТабличныйДокумент	- Сформированная печатная форма
	//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Формирует печатную форму "Чек"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьУведомлениеОбОстатках(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("УведомлениеОбОстаткахПрослеживаемыхТоваров");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	
	ОбластьМакета.Параметры.НомерУведомления = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ОбластьМакета.Параметры.ДатаУведомления = Формат(Дата, "ДЛФ=D");
	ОбластьМакета.Параметры.НомерКорректировки = "0";
	ОбластьМакета.Параметры.НаимОрг = СокрЛП(Организация.НаименованиеПолное);
	ОбластьМакета.Параметры.ИНН = Организация.ИНН;
	ОбластьМакета.Параметры.КПП = Организация.КПП;
	ОбластьМакета.Параметры.НаименованиеПервичногоДокумента = ПервичныйДокумент.ХозОперация.Наименование;
	ОбластьМакета.Параметры.НомерУчетногоДокумента = дкПолучитьНомерДляПечати(ПервичныйДокумент);
	ОбластьМакета.Параметры.ДатаУчетногоДокумента = Формат(ПервичныйДокумент.Дата, "ДЛФ=D");
	
	// Товары
	НаименованиеТоваров = "";
	Если Товары.Количество() = 1 Тогда
		НаименованиеТоваров = СокрЛП(Товары[0].Номенклатура.Наименование);
	Иначе
		НаименованиеТоваров = СокрЛП(КодТНВЭД.Наименование);
	КонецЕсли;
	
	ОбластьМакета.Параметры.НаименовениеТоваров = НаименованиеТоваров;
	ОбластьМакета.Параметры.КодТНВЭД = КодТНВЭД.Код;
	ОбластьМакета.Параметры.КодОКПД2 = ?(ЗначениеЗаполнено(КодОКПД2), КодОКПД2.Код, "");
	ОбластьМакета.Параметры.КоличествоТовара = Формат(Товары.Итог("Количество"), "ЧЦ=19; ЧДЦ=6; ЧРД=.; ЧН=-");
	ОбластьМакета.Параметры.КодЕдиницыПервичногоДокумента = ?(ЗначениеЗаполнено(ЕдиницаИзмерения), ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код, "796");
	ОбластьМакета.Параметры.РНПТ = РНПТ;
	ОбластьМакета.Параметры.КодЕдиницыПроследиваемости = ?(ЗначениеЗаполнено(ЕдиницаИзмеренияПрослеживаемости), ЕдиницаИзмеренияПрослеживаемости.Код, "796");
	ОбластьМакета.Параметры.КоличествоТовараПрослеживаемости = Формат(Товары.Итог("КоличествоПрослеживаемости"), "ЧЦ=19; ЧДЦ=6; ЧРД=.; ЧН=-");
	ОбластьМакета.Параметры.Стоимость = Формат(Товары.Итог("Сумма"), "ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=-");
	
	СтруктураОтбора=Новый Структура("Организация,Объект", Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(РабочаяДата,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	
	Если ЗначениеЗаполнено(Руководитель) Тогда
		ОбластьМакета.Параметры.ПрПодп = "1";
		МассивФИО = СтрРазделить(Руководитель.Наименование, " ", Ложь);
		ОбластьМакета.Параметры.ОргПодписантФамилия = СокрЛП(МассивФИО[0]);
		ОбластьМакета.Параметры.ОргПодписантИмя = ?(МассивФИО.Количество() > 1, МассивФИО[1], "");
		Если МассивФИО.Количество() > 2 Тогда
			Отчество = "";
			Для Инд = 2 По МассивФИО.Количество() - 1 Цикл
				Отчество = Отчество + МассивФИО[Инд];
			КонецЦикла;
			ОбластьМакета.Параметры.ОргПодписантОтчество = Отчество;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ДатаПодписи = Формат(Дата, "ДЛФ=D");
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЧек()

#КонецЕсли

// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Рез=дкОбработкаЗаполнения(ЭтотОбъект, Основание,,ТипЗнч(Основание)<>Тип("ДокументСсылка.КорректировкаЗаказаПоставщику"));
	
	Если ЭтоНовый() Тогда
		Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.Новый;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	РНПТ = "";
	Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.Новый;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		//Проверим корректность видов введенной номенклатуры
		БлокироватьВидыНоменклатуры = ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
		Если БлокироватьВидыНоменклатуры<>Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры)=Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
			ЭтоИнвентаризацияАвтомобилей = (ТипЗнч(ПервичныйДокумент) = Тип("ДокументСсылка.ИнвентаризацияАвтомобилей"));
			Ошибки="";
			Для каждого СтрокаТоваров Из Товары Цикл
				Если ЭтоИнвентаризацияАвтомобилей И ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
					ИЛИ НЕ ЭтоИнвентаризацияАвтомобилей И ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Указанный тип товара не соответствует первичному документу.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если НЕ СтрокаТоваров.Номенклатура.Прослеживаемый Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Указанная номенклатура не является прослеживаемым товаром.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если СтрокаТоваров.Номенклатура.КодТНВЭД <> КодТНВЭД Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. Код ТН ВЭД номенклатуры не соответствует коду ТН ВЭД документа.";
					Отказ=Истина;
					Продолжить;
				КонецЕсли;
				Если ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
					Продолжить;
				КонецЕсли;
				ЕстьОшибка=(БлокироватьВидыНоменклатуры[СтрокаТоваров.Номенклатура.ВидНоменклатуры]<>Неопределено);
				Если ЕстьОшибка Тогда
					Ошибки=?(Ошибки="","",Ошибки+Символы.ПС);
					Ошибки=Ошибки+"Строка "+СтрокаТоваров.НомерСтроки+". Товар <"+СтрокаТоваров.Номенклатура+">. В таблицу нельзя вводить номенклатуру вида """+СокрЛП(СтрокаТоваров.Номенклатура.ВидНоменклатуры)+""".";
					Отказ=Истина;
				КонецЕсли;
			КонецЦикла; 
			Если Отказ Тогда
				#Если Клиент Тогда
				// Выведем все накопившиеся ошибки 
				Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
				Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
				Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное);
				#КонецЕсли
				дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если Состояние = Перечисления.СостоянияУведомленияПрослеживаемостиТоваров.ПолученОтвет Тогда
		ВыполнитьДвиженияПоГТД(Отказ);
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

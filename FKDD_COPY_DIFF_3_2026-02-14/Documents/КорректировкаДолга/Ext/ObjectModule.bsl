// Модуль документа КорректировкаДолга

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныйСостав=Новый Структура();
	ОбязательныйСостав.Вставить("ДоговорВзаиморасчетов",3);
	ОбязательныйСостав.Вставить("Сделка",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("СтатьяДохода",1);
	ОбязательныеРеквизиты.Вставить("СтатьяРасхода",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Состав",ОбязательныйСостав);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
		
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыСостава = Новый Структура();
			КонтролируемыеРеквизитыСостава.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);			
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Состав",КонтролируемыеРеквизитыСостава);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			                    			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("КорректировкаДолга","Корректировка долга",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="" Тогда
		//Для приватной обработки изменения реквизитов
		
	ИначеЕсли Имя="КурсДокумента" Тогда
		СуммаДокументаПриход = 0;
		СуммаДокументаРасход = 0;
		Для Каждого ТекСтрока Из Состав Цикл
			СуммаДокументаПриход = СуммаДокументаПриход + обПересчет(ТекСтрока.УвеличениеДолга, ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, КурсДокумента);
			СуммаДокументаРасход = СуммаДокументаРасход + обПересчет(ТекСтрока.УменьшениеДолга, ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата, ВалютаДокумента, КурсДокумента);
		КонецЦикла;
		Возврат ИСТИНА;
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		// пересчитаем курсы взаиморасчетов в табличной части "Состав"
		Для Каждого ТекСтрока Из Состав Цикл
			Если ТекСтрока.ДоговорВзаиморасчетов.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			спрСтрокаСоставВалютаВзаиморасчетов = ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			// если валюта взаиморасчета в строке и валюта документа совпадают,
			// установим курс взаиморасчета равным курсу документа
			Если спрСтрокаСоставВалютаВзаиморасчетов = ВалютаДокумента Тогда
				ТекСтрока.КурсВалютыВзаиморасчетов = КурсДокумента;
				ОбработкаРеквизита("Состав.КурсВалютыВзаиморасчетов", ТекСтрока, ЭтаФорма);
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Имя="Контрагент" Тогда
		Если обЗначениеНеЗаполнено(Контрагент) Тогда 
			// пустой договор
			ЭтаФорма.ЭлементыФормы.тВзаиморасчеты.Заголовок="<Контрагент не выбран>";
		Иначе
			// получим долг
			СтруктураОтбора=Новый Структура(); СтруктураОтбора.Вставить("Контрагент",Контрагент);
			тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"СуммаУпр,СуммаБаз");
			Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
				Долг=тзДолги.Итог("СуммаБаз");
			Иначе
				Долг=обПересчет(тзДолги.Итог("СуммаУпр"),Константы.ВалютаУправленческогоУчетаКомпании.Получить(),Дата,ВалютаДокумента,Дата);
			КонецЕсли; 
			// выведем на экран этот долг
			ЭтаФорма.ЭлементыФормы.тВзаиморасчеты.Заголовок="Долг контрагента составляет "+Формат(Долг,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")+" "+СокрЛП(ВалютаДокумента.Наименование);
		КонецЕсли;
		Если Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.КонтактноеЛицо Тогда
			#Если Клиент Тогда
				Сообщить("В документы запрешен ввод контрагентов с видом контактное лицо.",СтатусСообщения.Внимание);
				Контрагент = Справочники.Контрагенты.ПустаяСсылка();
				ОбработкаРеквизита("Контрагент",,ЭтаФорма);
			#КонецЕсли
		КонецЕсли;
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "КорректировкаДолга"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьКорректировкаДолга(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("КорректировкаДолга");

	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.Поставщик = Организация;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.Получатель = Контрагент;
	ОбластьМакета.Параметры.ПредставлениеПолучателя = спПолучитьПредставление(Контрагент);
	
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;	
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");;
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
    ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, УвеличениеДолга, УменьшениеДолга, Разница", ВалютаДокумента, 0, 0, 0);
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ВыборкаТабличнойЧасти = ЭтотОбъект.Состав;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ВалютаСтроки = СтрокаТабличнойЧасти.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
		ОбластьМакета.Параметры.УвеличениеДолга = Формат(СтрокаТабличнойЧасти.УвеличениеДолга,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.УменьшениеДолга = Формат(СтрокаТабличнойЧасти.УменьшениеДолга,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Разница         = Формат(СтрокаТабличнойЧасти.УвеличениеДолга - СтрокаТабличнойЧасти.УменьшениеДолга,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Валюта			= ВалютаСтроки;
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, УвеличениеДолга, УменьшениеДолга, Разница", ВалютаДокумента, 0, 0, 0);
		КонецЕсли;
		
		Если ВалютаДокумента=ВалютаСтроки Тогда
			УвеличениеДолга = СтрокаТабличнойЧасти.УвеличениеДолга;
			УменьшениеДолга = СтрокаТабличнойЧасти.УменьшениеДолга;
		Иначе
			УвеличениеДолга = обПересчет(СтрокаТабличнойЧасти.УвеличениеДолга, ВалютаСтроки, Дата, ВалютаДокумента, КурсДокумента);
			УменьшениеДолга = обПересчет(СтрокаТабличнойЧасти.УменьшениеДолга, ВалютаСтроки, Дата, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		СтруктураИтоговПоСтранице.Вставить("УвеличениеДолга", СтруктураИтоговПоСтранице.УвеличениеДолга+УвеличениеДолга);
		СтруктураИтоговПоСтранице.Вставить("УменьшениеДолга", СтруктураИтоговПоСтранице.УменьшениеДолга+УменьшениеДолга);
		СтруктураИтоговПоСтранице.Вставить("Разница", СтруктураИтоговПоСтранице.Разница+УвеличениеДолга-УменьшениеДолга);
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	ОбластьПодвал.Параметры.УвеличениеДолга = Формат(СуммаДокументаПриход, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.УменьшениеДолга = Формат(СуммаДокументаРасход, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.Разница			= Формат(СуммаДокументаПриход-СуммаДокументаРасход, ФорматВыводаСуммы);
	ИтогоРазница = СуммаДокументаПриход-СуммаДокументаРасход;
	
	Если ИтогоРазница>=0 Тогда
		ОбластьПодвал.Параметры.СуммаПрописью = "Увеличение долга на сумму " + обЧислоПрописью(ИтогоРазница, ВалютаДокумента);
	Иначе
		ОбластьПодвал.Параметры.СуммаПрописью = "Уменьшение долга на сумму " + обЧислоПрописью(-ИтогоРазница, ВалютаДокумента);
	КонецЕсли;
		
	ТабДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьКорректировкаДолга()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
			ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			
			СуммаСделки = 0;						
			Выборка  = РегистрыНакопления.ВзаиморасчетыКомпании.ВыбратьПоРегистратору(Основание);			
			Пока Выборка.Следующий() Цикл
				СуммаСделки = СуммаСделки + Выборка.Сумма;
			КонецЦикла;
			
			Состав.Очистить();
			НоваяСтрока = Состав.Добавить();
			НоваяСтрока.ДоговорВзаиморасчетов = Основание.ДоговорВзаиморасчетов;
			НоваяСтрока.КурсВалютыВзаиморасчетов = Основание.КурсВалютыВзаиморасчетов;
			Если обЗначениеНеЗаполнено(Основание.ДокументОснование) Тогда
				НоваяСтрока.Сделка = Основание;				
			Иначе
				НоваяСтрока.Сделка = Основание.ДокументОснование;				
			КонецЕсли;
			
			Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
				НоваяСтрока.УвеличениеДолга = 0;
				НоваяСтрока.УменьшениеДолга = СуммаСделки;
			ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
				НоваяСтрока.УвеличениеДолга = СуммаСделки;
				НоваяСтрока.УменьшениеДолга = 0;
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;	
	
	СуммаДокументаПриход=0; СуммаДокументаРасход=0;
	Для Каждого СтрокаТЧ Из Состав Цикл
		СуммаДокументаПриход=СуммаДокументаПриход+обПересчет(СтрокаТЧ.УвеличениеДолга,СтрокаТЧ.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,Дата,ВалютаДокумента,КурсДокумента);
		СуммаДокументаРасход=СуммаДокументаРасход+обПересчет(СтрокаТЧ.УменьшениеДолга,СтрокаТЧ.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,Дата,ВалютаДокумента,КурсДокумента);
	КонецЦикла;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	// итоговые суммы
	СуммаДокументаПриход=0; СуммаДокументаРасход=0;
	Для Каждого СтрокаТЧ Из Состав Цикл
		СуммаДокументаПриход=СуммаДокументаПриход+обПересчет(СтрокаТЧ.УвеличениеДолга,СтрокаТЧ.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,Дата,ВалютаДокумента,КурсДокумента);
		СуммаДокументаРасход=СуммаДокументаРасход+обПересчет(СтрокаТЧ.УменьшениеДолга,СтрокаТЧ.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,Дата,ВалютаДокумента,КурсДокумента);
	КонецЦикла;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделению = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проводим
	СуммаДоходаРасходаСуммовыхРазниц=0;
	НаборЗаписейВзаиморасчетыКомпании=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчетыКомпании.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчетыКомпании.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Истина;
	// идем по строкам
	Для Каждого СтрокаТЧ Из Состав Цикл
		НаборЗаписейВзаиморасчетыКомпании.ДокументОбъект        = ЭтотОбъект;
		НаборЗаписейВзаиморасчетыКомпании.ДоговорВзаиморасчетов = СтрокаТЧ.ДоговорВзаиморасчетов;
		НаборЗаписейВзаиморасчетыКомпании.КурсВзаиморасчетов    = СтрокаТЧ.КурсВалютыВзаиморасчетов;
		НаборЗаписейВзаиморасчетыКомпании.Сделка                = СтрокаТЧ.Сделка;
		НаборЗаписейВзаиморасчетыКомпании.Валюта                = СтрокаТЧ.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
		НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц = 0;
		НаборЗаписейВзаиморасчетыКомпании.АвтоЗакрытиеСделок    = Ложь;
		НаборЗаписейВзаиморасчетыКомпании.СписатьНераспределеннуюСуммуПоСделке = Истина;
		
		Долг=СтрокаТЧ.УвеличениеДолга-СтрокаТЧ.УменьшениеДолга;
		Если Долг=0 Тогда 
			// долг не изменяется
			Продолжить;
		ИначеЕсли Долг>0 Тогда
			// надо увеличить долг контрагента
			НаборЗаписейВзаиморасчетыКомпании.Сумма=Долг;
			Отказ=НЕ НаборЗаписейВзаиморасчетыКомпании.Приход() ИЛИ Отказ;
		Иначе
			// надо уменьшить долг контрагента
			НаборЗаписейВзаиморасчетыКомпании.Сумма=-Долг;
			Отказ=НЕ НаборЗаписейВзаиморасчетыКомпании.Расход() ИЛИ Отказ;
		КонецЕсли;
		
		// доходы и расходы по суммовым разницам
		СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц;
		Если СуммаДоходаРасходаСуммовыхРазниц <> 0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			Если БалансВедетсяПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение = СтрокаТЧ.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;	
	КонецЦикла;
	
	// Доходы и расходы
	ТаблицаДоходовРасходов=Движения.ВзаиморасчетыКомпании.Выгрузить();
	ТаблицаДоходовРасходов.Свернуть("ВидДвижения,ДоговорВзаиморасчетов,ВидОперации","СуммаУпр");
	ТаблицаДоходовРасходов.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	
	Для Каждого СтрокаТаблицаДоходовРасходов Из ТаблицаДоходовРасходов Цикл
		Если БалансВедетсяПоПодразделению Тогда
			СтрокаТаблицаДоходовРасходов.Подразделение = СтрокаТаблицаДоходовРасходов.ДоговорВзаиморасчетов.Подразделение;
		Иначе
			СтрокаТаблицаДоходовРасходов.Подразделение = ПодразделениеКомпании;	
		КонецЕсли;
	КонецЦикла;	
	ТаблицаДоходовРасходов.Свернуть("ВидДвижения,Подразделение,ВидОперации","СуммаУпр");
	
	НаборЗаписейДоходыРасходы = Движения.ДоходыИРасходы;
	Для Каждого СтрокаТаблицаДоходовРасходов Из ТаблицаДоходовРасходов Цикл
		
		//т.к. проведение было по строкам ТЧ, то курсовые разницы уже в ДиР, надо их пропустить
		Если СтрокаТаблицаДоходовРасходов.ВидОперации = Перечисления.ВидыОперацийВзаиморасчетов.СписаниеКурсовойРазницыУпрВалюты Тогда
			Продолжить;
		КонецЕсли;
		
		// расход
		Если (СтрокаТаблицаДоходовРасходов.ВидДвижения=ВидДвиженияНакопления.Приход) И (СтрокаТаблицаДоходовРасходов.СуммаУпр<>0) Тогда
			НаборЗаписейДоходыРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = СтатьяДохода;
			НаборЗаписейДоходыРасходы.Подразделение          = СтрокаТаблицаДоходовРасходов.Подразделение;
			НаборЗаписейДоходыРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыРасходы.Доход                  = СтрокаТаблицаДоходовРасходов.СуммаУпр;
			//<<Павличенко 08.02.18
			НаборЗаписейДоходыРасходы.ДоходБезНДС            = СтрокаТаблицаДоходовРасходов.СуммаУпр;
			//>>Павличенко 08.02.18
			НаборЗаписейДоходыРасходы.Расход                 = 0;
			//<<Павличенко 08.02.18
			НаборЗаписейДоходыРасходы.РасходБезНДС            = 0;
			//>>Павличенко 08.02.18
			НаборЗаписейДоходыРасходы.Приход();
		КонецЕсли;
		
		// доход
		Если (СтрокаТаблицаДоходовРасходов.ВидДвижения=ВидДвиженияНакопления.Расход) И (СтрокаТаблицаДоходовРасходов.СуммаУпр<>0) Тогда
			НаборЗаписейДоходыРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыРасходы.СтатьяДоходовИРасходов = СтатьяРасхода;
			НаборЗаписейДоходыРасходы.Подразделение			 = СтрокаТаблицаДоходовРасходов.Подразделение;
			НаборЗаписейДоходыРасходы.ВУпрВалюте			 = Истина;
			НаборЗаписейДоходыРасходы.Доход                  = 0;
			НаборЗаписейДоходыРасходы.Расход                 = СтрокаТаблицаДоходовРасходов.СуммаУпр;
			//<<Павличенко 08.02.18
			НаборЗаписейДоходыРасходы.ДоходБезНДС                  = 0;
			НаборЗаписейДоходыРасходы.РасходБезНДС                 = СтрокаТаблицаДоходовРасходов.СуммаУпр;
			//>>Павличенко 08.02.18
			НаборЗаписейДоходыРасходы.Приход();
		КонецЕсли;
	КонецЦикла;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей

Перем ТекВалютаДокумента Экспорт;   // Текущая валюта документа
Перем ТекКурсДокумента Экспорт;     // Текущий курс документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Заполняет суммы накопления
//
// Параметры:
//  ЭтаФорма - форма - содержит данную форму.
//
Процедура ЗаполнитьСуммыНакопления(ЭтаФорма) Экспорт
	Накопления.Очистить();
	КонстантаВалютаНакопления=Константы.ВалютаНакопительныхСумм.Получить();
	КонстантаПериодаНакопления=Константы.ПериодНакопительныхСкидок.Получить();
	Если КонстантаПериодаНакопления=Перечисления.ПериодичностьАнализаНакопительныхСкидок.День Тогда
		ПериодНакопления=НачалоДня(Дата)-((1-1)*24*60*60);
	ИначеЕсли КонстантаПериодаНакопления=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Неделя Тогда
		ПериодНакопления=НачалоДня(Дата)-((7-1)*24*60*60);
	ИначеЕсли КонстантаПериодаНакопления=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Месяц Тогда
		ПериодНакопления=НачалоДня(Дата)-((30-1)*24*60*60);
	ИначеЕсли КонстантаПериодаНакопления=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Квартал Тогда
		ПериодНакопления=НачалоДня(Дата)-((90-1)*24*60*60);
	ИначеЕсли КонстантаПериодаНакопления=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Год Тогда
		ПериодНакопления=НачалоДня(Дата)-((365-1)*24*60*60);
	ИначеЕсли КонстантаПериодаНакопления=Перечисления.ПериодичностьАнализаНакопительныхСкидок.Неограниченно Тогда
		ПериодНакопления=НачалоДня('00010101');
	Иначе
		//В противном случае накопления не ведутся
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НакоплениеСумм.Контрагент КАК Контрагент,
	               |	НакоплениеСумм.Карточка КАК Карточка,
	               |	НакоплениеСумм.ПериодНакопления КАК ПериодНакопления,
	               |	НакоплениеСумм.Подразделение КАК Подразделение,
	               |	НакоплениеСумм.Документ КАК Документ,
	               |	НакоплениеСумм.Сумма КАК Сумма,
	               |	НакоплениеСумм.КоличествоЧеков КАК КоличествоЧеков,
	               |	НакоплениеСумм.КоличествоНоменклатуры КАК КоличествоНоменклатуры
	               |ИЗ
	               |	РегистрСведений.НакоплениеСумм КАК НакоплениеСумм
	               |ГДЕ
	               |	(НЕ(НакоплениеСумм.Документ ССЫЛКА Документ.ЗакрытиеНакопительныхСумм)) И
	               |	НакоплениеСумм.ПериодНакопления < &Дата И
	               |	НакоплениеСумм.Подразделение = &Подразделение
	               |УПОРЯДОЧИТЬ ПО
	               |	ПериодНакопления";
	Запрос.УстановитьПараметр("Дата",ПериодНакопления);
	Запрос.УстановитьПараметр("Подразделение",ПодразделениеКомпании);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока=Накопления.Добавить();
		НоваяСтрока.Контрагент=Выборка.Контрагент;
		НоваяСтрока.Карточка=Выборка.Карточка;
		НоваяСтрока.ПериодНакопления=Выборка.ПериодНакопления;
		НоваяСтрока.Подразделение=Выборка.Подразделение;
		НоваяСтрока.Документ=СокрЛП(Выборка.Документ.УникальныйИдентификатор());
		НоваяСтрока.ДокументТип=Выборка.Документ.Метаданные().Имя;
		НоваяСтрока.ДокументПредставление=СокрЛП(Выборка.Документ);
		Если КонстантаВалютаНакопления=ВалютаДокумента Тогда
			НоваяСтрока.Сумма=Выборка.Сумма;
		Иначе
			НоваяСтрока.Сумма=обПересчет(Выборка.Сумма,КонстантаВалютаНакопления,Дата,ВалютаДокумента,КурсДокумента);
		КонецЕсли; 
		НоваяСтрока.КоличествоЧеков=Выборка.КоличествоЧеков;
		НоваяСтрока.КоличествоНоменклатуры=Выборка.КоличествоНоменклатуры;
	КонецЦикла;
	#Если Клиент Тогда
	дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
	#КонецЕсли
КонецПроцедуры // ЗаполнитьСуммыНакопления()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеНакопления=Новый Структура();
	//ОбязательныеТовары.Вставить("Номенклатура",3);
	//ОбязательныеТовары.Вставить("Количество",1);
	//ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	//ОбязательныеТовары.Вставить("Коэффициент",1);
	//ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Накопления",ОбязательныеНакопления);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗакрытиеНакопительныхСумм","Закрытие накопительных сумм",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "Сумма".
//
Функция РассчитатьСуммуВсего() Экспорт
	ВремСуммаДокумента=Накопления.Итог("Сумма");
	Если СуммаДокумента<>ВремСуммаДокумента Тогда
		СуммаДокумента=ВремСуммаДокумента;
	КонецЕсли;
	Возврат СуммаДокумента;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Дата" Тогда
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.КоманднаяПанельТоварыЗаполнение(ЭтаФорма.ЭлементыФормы.КоманднаяПанельНакопления.Кнопки.Заполнение.Кнопки.Заполнить);
		КонецЕсли; 
		#Иначе
			ЗаполнитьСуммыНакопления(ЭтаФорма);
		#КонецЕсли
		Возврат Истина;
	
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.КоманднаяПанельТоварыЗаполнение(ЭтаФорма.ЭлементыФормы.КоманднаяПанельНакопления.Кнопки.Заполнение.Кнопки.Заполнить);
		КонецЕсли; 
		#Иначе
			ЗаполнитьСуммыНакопления(ЭтаФорма);
		#КонецЕсли
		Возврат Рез;
	
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		//Пересчет сумм табличной части документа
		Для каждого СтрокаНакопления Из Накопления Цикл
			СтрокаНакопления.Сумма=обПересчет(СтрокаНакопления.Сумма,ТекВалютаДокумента,ТекКурсДокумента,ВалютаДокумента,КурсДокумента);
		КонецЦикла; 
		ТекВалютаДокумента=ВалютаДокумента;
		ТекКурсДокумента=КурсДокумента;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ЗакрытиеСмены"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЗакрытиеНакопительныхСумм(ТабДокумент) Экспорт
	Макет = Документы.ЗакрытиеНакопительныхСумм.ПолучитьМакет("ЗакрытиеНакопительныхСумм");
	
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПодразделения = ЭтотОбъект.ПодразделениеКомпании;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма,", ВалютаДокумента, 0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Накопления;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки 		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
						НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма", ВалютаДокумента, 0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице); 		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтогоПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма");
	ОбластьПодвал.Параметры.Сумма = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего по документу на сумму: " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;   
КонецФункции // ПечатьЗакрытиеСмены()    

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть  

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;

	ТребуетсяПересчет=Ложь;
	Для каждого СтрокаНакопления Из Накопления Цикл
		//Перебор строк табличной части для удаления строк с несуществующими документами
		Попытка
			СсылкаНаДокумент=Документы[СтрокаНакопления.ДокументТип].ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаНакопления.Документ));
			Если СсылкаНаДокумент<>Неопределено Тогда
				Если СсылкаНаДокумент.ПолучитьОбъект()=Неопределено Тогда
					СсылкаНаДокумент=Неопределено;
				Иначе
					Если НЕ СсылкаНаДокумент.Проведен Тогда
						СсылкаНаДокумент=Неопределено;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		Исключение
			СсылкаНаДокумент=Неопределено;
		КонецПопытки; 
		Если СсылкаНаДокумент=Неопределено Тогда
			Накопления.Удалить(СтрокаНакопления); ТребуетсяПересчет=Истина;
		КонецЕсли; 
	КонецЦикла;
	Если ТребуетсяПересчет Тогда РассчитатьСуммуВсего(); КонецЕсли; 
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	//Удалим свернутые накопления сумм
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.Отбор.Документ.Значение=Ссылка;
	НаборЗаписейНакоплениеСумм.Отбор.Документ.Использование=Истина;
	НаборЗаписейНакоплениеСумм.Записать();

	//Восстановим ранее свернутые записи
	Для каждого СтрокаНакопления Из Накопления Цикл
		//Перебор строк табличной части для удаления сворачиваемых записей
		Попытка
			СсылкаНаДокумент=Документы[СтрокаНакопления.ДокументТип].ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаНакопления.Документ));
			Если СсылкаНаДокумент<>Неопределено Тогда
				Если СсылкаНаДокумент.ПолучитьОбъект()=Неопределено Тогда
					СсылкаНаДокумент=Неопределено;
				Иначе
					Если НЕ СсылкаНаДокумент.Проведен Тогда
						СсылкаНаДокумент=Неопределено;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		Исключение
			СсылкаНаДокумент=Неопределено;
		КонецПопытки; 
		Если СсылкаНаДокумент<>Неопределено Тогда
			МенеджерЗаписиНакопленияСумм=РегистрыСведений.НакоплениеСумм.СоздатьМенеджерЗаписи();
			МенеджерЗаписиНакопленияСумм.Контрагент=СтрокаНакопления.Контрагент;
			МенеджерЗаписиНакопленияСумм.Карточка=СтрокаНакопления.Карточка;
			МенеджерЗаписиНакопленияСумм.ПериодНакопления=СтрокаНакопления.ПериодНакопления;
			МенеджерЗаписиНакопленияСумм.Подразделение=СтрокаНакопления.Подразделение;
			МенеджерЗаписиНакопленияСумм.Документ=СсылкаНаДокумент;
			МенеджерЗаписиНакопленияСумм.Сумма=СтрокаНакопления.Сумма;
			МенеджерЗаписиНакопленияСумм.КоличествоЧеков=СтрокаНакопления.КоличествоЧеков;
			МенеджерЗаписиНакопленияСумм.КоличествоНоменклатуры=СтрокаНакопления.КоличествоНоменклатуры;
			МенеджерЗаписиНакопленияСумм.Записать();
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Для каждого СтрокаНакопления Из Накопления Цикл
		//Перебор строк табличной части для удаления сворачиваемых записей
		Попытка
			СсылкаНаДокумент=Документы[СтрокаНакопления.ДокументТип].ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаНакопления.Документ));
			Если СсылкаНаДокумент<>Неопределено Тогда
				Если СсылкаНаДокумент.ПолучитьОбъект()=Неопределено Тогда
					СсылкаНаДокумент=Неопределено;
				Иначе
					Если НЕ СсылкаНаДокумент.Проведен Тогда
						СсылкаНаДокумент=Неопределено;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		Исключение
			СсылкаНаДокумент=Неопределено;
		КонецПопытки; 
		Если СсылкаНаДокумент<>Неопределено Тогда
			МенеджерЗаписиНакопленияСумм=РегистрыСведений.НакоплениеСумм.СоздатьМенеджерЗаписи();
			МенеджерЗаписиНакопленияСумм.Контрагент=СтрокаНакопления.Контрагент;
			МенеджерЗаписиНакопленияСумм.Карточка=СтрокаНакопления.Карточка;
			МенеджерЗаписиНакопленияСумм.ПериодНакопления=СтрокаНакопления.ПериодНакопления;
			МенеджерЗаписиНакопленияСумм.Подразделение=СтрокаНакопления.Подразделение;
			МенеджерЗаписиНакопленияСумм.Документ=СсылкаНаДокумент;
			МенеджерЗаписиНакопленияСумм.Удалить();
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ОчисткаСуммНакопления Тогда
		ВалютаНакопительныхСумм=Константы.ВалютаНакопительныхСумм.Получить();
		//Свертка таблицы для вычисления накопления
		ТабСвертки=Накопления.Выгрузить();
		ТабСвертки.Свернуть("Контрагент,Карточка,Подразделение","Сумма,КоличествоЧеков,КоличествоНоменклатуры");
		НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
		НаборЗаписейНакоплениеСумм.Отбор.Документ.Значение=Ссылка;
		НаборЗаписейНакоплениеСумм.Отбор.Документ.Использование=Истина;
		Для каждого СтрокаНакопления Из ТабСвертки Цикл
			//Для каждой свернутой строки создаем записи регистра
			НакопленияКонтрагента=Накопления.НайтиСтроки(Новый Структура("Контрагент,Карточка,Подразделение",СтрокаНакопления.Контрагент,СтрокаНакопления.Карточка,ПодразделениеКомпании));
			ДатаПоследнейЗаписи=НакопленияКонтрагента[НакопленияКонтрагента.Количество()-1].ПериодНакопления;
			НоваяЗапись=НаборЗаписейНакоплениеСумм.Добавить();
			НоваяЗапись.Контрагент=СтрокаНакопления.Контрагент;
			НоваяЗапись.Карточка=СтрокаНакопления.Карточка;
			НоваяЗапись.ПериодНакопления=ДатаПоследнейЗаписи;
			НоваяЗапись.Подразделение=ПодразделениеКомпании;
			НоваяЗапись.Документ=Ссылка;
			Если ВалютаНакопительныхСумм=ВалютаДокумента Тогда
				НоваяЗапись.Сумма=СтрокаНакопления.Сумма;
			Иначе
				НоваяЗапись.Сумма=обПересчет(СтрокаНакопления.Сумма,ВалютаДокумента,КурсДокумента,ВалютаНакопительныхСумм,Дата);
			КонецЕсли; 
			НоваяЗапись.КоличествоЧеков=СтрокаНакопления.КоличествоЧеков;
			НоваяЗапись.КоличествоНоменклатуры=СтрокаНакопления.КоличествоНоменклатуры;
		КонецЦикла;
		НаборЗаписейНакоплениеСумм.Записать();
	КонецЕсли; 
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ТекВалютаДокумента=Константы.ВалютаНакопительныхСумм.Получить();
СтруктураКурса = обКурсДляВалюты(ТекВалютаДокумента,Дата);
ТекКурсДокумента=СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
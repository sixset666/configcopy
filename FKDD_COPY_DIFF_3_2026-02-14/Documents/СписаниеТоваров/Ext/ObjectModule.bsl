// Модуль документа СписаниеТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ФлагПерерасчет;               // Переменная для обозначения того, что ведется перерасчет суммы документа
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ВедетсяБалансПоПодразделению Экспорт;		//Признак ведения баланса по подразделениям

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если ХозОперация=Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию Тогда
		ОбязательныеТовары.Вставить("ДокументПередачи",3);
	КонецЕсли;
	Если обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("Партия",2);
		ОбязательныеТовары.Вставить("ГТД",2);
	КонецЕсли;
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СтатьяСписанияТМЦ",1);
	Если ХозОперация=Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
    Иначе
		ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
		Если НЕ ВедетсяБалансПоПодразделению Тогда
			ОбязательныеРеквизиты.Вставить("ПодразделениеЗатрат",1);
		КонецЕсли; 
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
		// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();			
			КонтролируемыеРеквизитыШапки = Новый Структура();			
			Если ХозОперация=Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			Иначе
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонецЕсли;      			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыСостава = Новый Структура();
				Если ХозОперация=Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию Тогда
					КонтролируемыеРеквизитыСостава.Вставить("ДокументПередачи");
				ИначеЕсли обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
					КонтролируемыеРеквизитыСостава.Вставить("Партия");
				КонецЕсли;	
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Товары",КонтролируемыеРеквизитыСостава);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);   			
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки, Истина);
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СписаниеТоваров","Списание товаров",Истина);
	СписокХозОпераций.Добавить("СписаниеТоваровОтданныхНаКомиссию","Списание товаров отданных на комиссию");
	СписокХозОпераций.Добавить("СписаниеВПроизводство","Списание товаров в производство");
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  ШапкаДокумента - выборка.
//
Функция ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента)
    ТекстЗапроса="ВЫБРАТЬ
                 |	СписаниеТоваровТовары.Номенклатура КАК Номенклатура,
                 |	СписаниеТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
                 |	СписаниеТоваровТовары.ДокументПередачи КАК ДокументПередачи,
                 |	СписаниеТоваровТовары.Партия КАК Партия,
                 |	СписаниеТоваровТовары.ГТД КАК ГТД,
                 |	СписаниеТоваровТовары.ЦенаРозничная КАК ЦенаРозничная,
                 |	СписаниеТоваровТовары.Количество * СписаниеТоваровТовары.Коэффициент КАК Количество
                 |ИЗ
                 |	Документ.СписаниеТоваров.Товары КАК СписаниеТоваровТовары
                 |ГДЕ
                 |	СписаниеТоваровТовары.Ссылка = &Ссылка";

	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
	Запрос.Текст=ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка          КАК Ссылка,
	|	Док.Дата            КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента   КАК КурсДокумента,
	|	Док.ТипЦен          КАК ТипЦен,
	|	Док.ХозОперация     КАК ХозОперация,
	|	Док.КурсВалютыУпр   КАК КурсВалютыУпр,
	|	Док.МоментВремени   КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании               КАК СкладКомпании,
	|	Док.СкладКомпании.Подразделение КАК ПодразделениеСклада,	
	|	Док.ПодразделениеЗатрат			КАК ПодразделениеЗатрат,	
	|	Док.Контрагент                  КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов       КАК ДоговорВзаиморасчетов,
	|	Док.ДоговорВзаиморасчетов.Подразделение КАК ПодразделениеДоговора, 
	|	Док.ДокументОснование           КАК ДокументОснование,
	|	Док.СтатьяСписанияТМЦ           КАК СтатьяСписанияТМЦ
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// определим способ ведения баланса
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
	РезультатЗапросаПоТоварам=ПолучитьРезультатЗапросаПоТоварам(ШапкаДокумента);
	// определим вид проводки
	Если ШапкаДокумента.ХозОперация=Справочники.ХозОперации.СписаниеТоваров ИЛИ
		 ШапкаДокумента.ХозОперация=Справочники.ХозОперации.СписаниеВПроизводство Тогда
		 
		// проверяем, присутствуют ли партии в табличной части
		ЕстьПартии = Ложь;
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
				ЕстьПартии = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		// списываем
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		НаборЗаписейПартии.ДокументОбъект       = ЭтотОбъект;
		НаборЗаписейПартии.СкладКомпании        = ШапкаДокумента.СкладКомпании;
		НаборЗаписейПартии.ИмяРеквизитаДокумент = ?(ЕстьПартии,"Партия","");
		НаборЗаписейПартии.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
		НаборЗаписейПартии.ШапкаДокумента            = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
		Если НЕ Отказ Тогда НаборЗаписейПартии.Записать(); КонецЕсли; 
		
		// Доходы и расходы. Только по собственному товару.
		СуммаУпрИтого   = 0;  
		
		//<<Павличенко 12.01.18
		СуммаНСДИтого   = 0;
		//>>Павличенко 12.01.18
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("СтатусПартии", Перечисления.СтатусыПартий.ТоварКупленный);
		МассивСтрок = НаборЗаписейПартии.Выгрузить().НайтиСтроки(СтруктураПоиска);
		Для каждого ТекСтрока Из МассивСтрок Цикл  	
			СуммаУпрИтого = СуммаУпрИтого + ТекСтрока.СуммаУпр;
			
			//<<Павличенко 12.01.18
			СуммаНСДИтого = СуммаНСДИтого + ТекСтрока.СуммаНДС;			
			//>>Павличенко 12.01.18
			
		КонецЦикла;
		
		Если СуммаУпрИтого<>0 Тогда
			НаборЗаписейДиР 			   = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			// В случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.ПодразделениеСклада;
			Иначе 
				НаборЗаписейДиР.Подразделение = ШапкаДокумента.ПодразделениеЗатрат;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяСписанияТМЦ;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.Расход					= СуммаУпрИтого; 
			
			//<<Павличенко 12.01.18
			НаборЗаписейДиР.РасходБезНДС			= СуммаУпрИтого-СуммаНСДИтого;
			//>>Павличенко 12.01.18
			
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		// Зачитываем принятые на реализацию товары, которые были списаны
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеТовары.Списание=Истина;
		НаборЗаписейРеализованныеТовары.РезультатЗапросаПоТоварам=РезультатЗапросаПоТоварам;
		НаборЗаписейРеализованныеТовары.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
		
		// подготовим таблицу движений в разрезе подразделений комиссионных товаров
		ТаблицаСписанийПартийРеализованных = Движения.РеализованныеТовары.Выгрузить();
		ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов","СуммаУпр");
		
		ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
		Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
			СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
		КонецЦикла;	
		ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение","СуммаУпр");
		
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.Подразделение  = СтрокаСписания.Подразделение;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
				НаборЗаписейДоходыИРасходы.Расход                 = СтрокаСписания.СуммаУпр;	
				НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
				Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
			КонецЦикла;
		Иначе
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Расход                 = ТаблицаСписанийПартийРеализованных.Итог("СуммаУпр");
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
	Иначе
		// списываем товары отданные на комиссию
		НаборЗаписейПартииОтданные = Движения.ПартииТоваровОтданные;
		НаборЗаписейПартииОтданные.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейПартииОтданные.Контрагент     = ШапкаДокумента.Контрагент;
		НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов     = ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПартииОтданные.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
		НаборЗаписейПартииОтданные.ШапкаДокумента            = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейПартииОтданные.Расход() ИЛИ Отказ;
		Если НЕ Отказ Тогда НаборЗаписейПартииОтданные.Записать(); КонецЕсли; 
		
		// 3. Доходы и расходы
		ТаблицаЗначений = НаборЗаписейПартииОтданные.Выгрузить();
		ТаблицаЗначений.Свернуть("ВидДвижения","СуммаСебестоимостиУпр");
		
		СтрокаТЗ = ТаблицаЗначений.Найти(ВидДвиженияНакопления.Расход,"ВидДвижения");
		Если СтрокаТЗ <> Неопределено Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект;
			// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение           = ШапкаДокумента.ПодразделениеДоговора;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= ШапкаДокумента.СтатьяСписанияТМЦ;
			НаборЗаписейДиР.ВУпрВалюте				= Истина;
			НаборЗаписейДиР.ШапкаДокумента			= ШапкаДокумента;
			НаборЗаписейДиР.Расход					= СтрокаТЗ.СуммаСебестоимостиУпр;
			Отказ=НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли; 
		
	КонецЕсли;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		Если Режим<>РежимПроведенияДокумента.Оперативный И ЗначениеЗаполнено(ШапкаДокумента.СкладКомпании) Тогда
			НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
			НаборЗаписейГраницыПартий.ИзменитьГраницу();
		ИначеЕсли ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
			НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
			НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
			НаборЗаписейГраницыПартий.ИзменитьГраницу();
		КонецЕсли;
	КонецЕсли;
	
	//Все ОК
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	СуммаВсего = дкПолучитьСуммуСписания(ЭтотОбъект, ?(ХозОперация = Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию, "ПартииТоваровОтданные" , "ПартииТоваровКомпании"),тКэшСуммСписания);
	Возврат СуммаВсего;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);

		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
			Рез=ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		ПолучитьРасчетноеКоличество = Истина;
		Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("ПолучитьРасчетноеКоличество") Тогда
			//при загрузке из АРМ Корзина расчет количества не выполняется
			ДопПараметры.Свойство("ПолучитьРасчетноеКоличество",ПолучитьРасчетноеКоличество);		
		КонецЕсли;
		Если ПолучитьРасчетноеКоличество Тогда
			Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.ДокументПередачи" Тогда
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.Количество;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если ТекСтрока.Количество<>0 Тогда
			ТекСтрока.ЦенаРозничная=ТекСтрока.СуммаРозничная/ТекСтрока.Количество;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Ячейка" Тогда
		//Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) ИЛИ обЗначениеНеЗаполнено(СкладКомпании) Тогда
		//	ТекСтрока.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
		//	Возврат Истина;
		//КонецЕсли;
		//
		//ЯчейкаСсылка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.Номенклатура,СкладКомпании);
		//ТекСтрока.Ячейка=ЯчейкаСсылка;
		//Возврат Истина;
		
	ИначеЕсли Имя="ПолучитьРасчетноеКоличество" Тогда
		//подставляет расчетное количество в текущую строку
		Если ХозОперация<>Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию Тогда
			СтруктураОтбора=Новый Структура("СкладКомпании,Номенклатура");
			СтруктураОтбора.СкладКомпании=СкладКомпании;
			СтруктураОтбора.Номенклатура=ТекСтрока.Номенклатура;
			МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
			ТаблицаОстатков=РегистрыНакопления.ОстаткиТоваровКомпании.Остатки(МоментВремени,СтруктураОтбора,,"Количество,Резерв");
			Если ТаблицаОстатков.Количество()>0 Тогда
				КоличествоОстаток=ТаблицаОстатков.Итог("Количество");
				Если обЗначениеНеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
					КоличествоОстаток=КоличествоОстаток/ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
				Иначе
					КоличествоОстаток=КоличествоОстаток/ТекСтрока.ЕдиницаИзмерения.Коэффициент;
				КонецЕсли; 
				ТекСтрока.Количество=КоличествоОстаток;
			Иначе
				ТекСтрока.Количество=0;
			КонецЕсли;
		Иначе
			СтруктураОтбора=Новый Структура("Контрагент,ДоговорВзаиморасчетов,Автомобиль,Номенклатура,ХарактеристикаНоменклатуры");
			СтруктураОтбора.Контрагент=Контрагент;
			СтруктураОтбора.Номенклатура=ТекСтрока.Номенклатура;
			СтруктураОтбора.ХарактеристикаНоменклатуры=ТекСтрока.ХарактеристикаНоменклатуры;
			СтруктураОтбора.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
			СтруктураОтбора.Автомобиль = Справочники.Автомобили.ПустаяСсылка();
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДокументПередачи) Тогда
				СтруктураОтбора.Вставить("ДокументПередачи",ТекСтрока.ДокументПередачи);
			КонецЕсли;
			МоментВремени=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
			ТаблицаОстатков=РегистрыНакопления.ПартииТоваровОтданные.Остатки(МоментВремени,СтруктураОтбора,,"Количество");
			Если ТаблицаОстатков.Количество()>0 Тогда
				КоличествоОстаток=ТаблицаОстатков.Итог("Количество");
				Если обЗначениеНеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
					КоличествоОстаток=КоличествоОстаток/ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент;
				Иначе
					КоличествоОстаток=КоличествоОстаток/ТекСтрока.ЕдиницаИзмерения.Коэффициент;
				КонецЕсли; 
				ТекСтрока.Количество=КоличествоОстаток;
			Иначе
				ТекСтрока.Количество=0;
			КонецЕсли;
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Заполняет на основании заказа
//
// Параметры:
//  ЗаказПокупателя - ДокументСсылка.
//
Процедура ЗаполнитьТоварыОстатками(Основание) Экспорт
	
	ВыборочноеСписаниеПартий = обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект);
	
	Если Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваров 
		ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия 
		ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
							  |	ПоступлениеТоваровТовары.Номенклатура,
							  |	ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры,
							  |	ПоступлениеТоваровТовары.ЕдиницаИзмерения,
							  |	ПоступлениеТоваровТовары.Коэффициент,
							  |	ПоступлениеТоваровТовары.ЦенаРозничная,
							  |	ПоступлениеТоваровТовары.СуммаРозничная,
							  |	МАКСИМУМ(ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0)) КАК КоличествоОстаток
							  |ИЗ
							  |	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
							  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки(
							  |				"+?(Ссылка.Пустая(),"","&НаМомент")+",
							  |				СкладКомпании = &Склад
							  |					И Партия = &Основание) КАК ПартииТоваровКомпанииОстатки
							  |		ПО (ПартииТоваровКомпанииОстатки.Номенклатура = ПоступлениеТоваровТовары.Номенклатура)
							  |			И (ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры = ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры)
							  |			И (ПартииТоваровКомпанииОстатки.Партия = ПоступлениеТоваровТовары.Ссылка)
							  |ГДЕ
							  |	ПоступлениеТоваровТовары.Ссылка = &Основание
							  |
							  |СГРУППИРОВАТЬ ПО
							  |	ПоступлениеТоваровТовары.Номенклатура,
							  |	ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры,
							  |	ПоступлениеТоваровТовары.ЕдиницаИзмерения,
							  |	ПоступлениеТоваровТовары.Коэффициент,
							  |	ПоступлениеТоваровТовары.ЦенаРозничная,
							  |	ПоступлениеТоваровТовары.СуммаРозничная");
							  
	ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.ВводОстатковТоваров 
		ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
							  |	ВводОстатковТоваровТовары.Номенклатура,
							  |	ВводОстатковТоваровТовары.ХарактеристикаНоменклатуры,
							  |	ВводОстатковТоваровТовары.ЕдиницаИзмерения,
							  |	ВводОстатковТоваровТовары.Коэффициент,
							  |	ВводОстатковТоваровТовары.ЦенаРозничная,
							  |	ВводОстатковТоваровТовары.СуммаРозничная,
							  |	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
							  |ИЗ
							  |	Документ.ВводОстатковТоваров.Товары КАК ВводОстатковТоваровТовары
							  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки(
							  |				"+?(Ссылка.Пустая(),"","&НаМомент")+",
							  |				СкладКомпании = &Склад
							  |					И Партия = &Основание) КАК ПартииТоваровКомпанииОстатки
							  |		ПО (ПартииТоваровКомпанииОстатки.Номенклатура = ВводОстатковТоваровТовары.Номенклатура)
							  |			И (ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры = ВводОстатковТоваровТовары.ХарактеристикаНоменклатуры)
							  |			И (ПартииТоваровКомпанииОстатки.Партия = ВводОстатковТоваровТовары.Ссылка)
							  |ГДЕ
							  |	ВводОстатковТоваровТовары.Ссылка = &Основание
							  |
							  |УПОРЯДОЧИТЬ ПО
							  |	ВводОстатковТоваровТовары.НомерСтроки");
		
	ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
							  |	ВводОстатковТоваровТовары.Номенклатура,
							  |	ВводОстатковТоваровТовары.ХарактеристикаНоменклатуры,
							  |	ВводОстатковТоваровТовары.ЕдиницаИзмерения,
							  |	ВводОстатковТоваровТовары.Коэффициент,
							  |	ВводОстатковТоваровТовары.ЦенаРозничная,
							  |	ВводОстатковТоваровТовары.СуммаРозничная,
							  |	ЕСТЬNULL(ПартииТоваровОтданныеОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
							  |ИЗ
							  |	Документ.ВводОстатковТоваров.Товары КАК ВводОстатковТоваровТовары
							  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОтданные.Остатки(
							  |				"+?(Ссылка.Пустая(),"","&НаМомент")+",
							  |				 Партия = &Основание ) КАК ПартииТоваровОтданныеОстатки
							  |		ПО (ПартииТоваровОтданныеОстатки.Номенклатура = ВводОстатковТоваровТовары.Номенклатура)
							  |			И (ПартииТоваровОтданныеОстатки.ХарактеристикаНоменклатуры = ВводОстатковТоваровТовары.ХарактеристикаНоменклатуры)
							  |			И (ПартииТоваровОтданныеОстатки.Партия = ВводОстатковТоваровТовары.Ссылка)
							  |ГДЕ
							  |	ВводОстатковТоваровТовары.Ссылка = &Основание
							  |
							  |УПОРЯДОЧИТЬ ПО
							  |	ВводОстатковТоваровТовары.НомерСтроки");
	Иначе
		Возврат;
	КонецЕсли;
	Запрос.УстановитьПараметр("НаМомент",ТекущаяДата());
	Запрос.УстановитьПараметр("Основание",Основание);
	Запрос.УстановитьПараметр("Склад",СкладКомпании);
	ВыборкаНоменклатуры = Запрос.Выполнить().Выбрать();
	Товары.Очистить(); 
	Пока ВыборкаНоменклатуры.Следующий() Цикл
		Если ВыборкаНоменклатуры.КоличествоОстаток > 0 Тогда
			НоваяСтрока=Товары.Добавить();
			НоваяСтрока.Номенклатура				= ВыборкаНоменклатуры.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры	= ВыборкаНоменклатуры.ХарактеристикаНоменклатуры;
			НоваяСтрока.ЕдиницаИзмерения			= ВыборкаНоменклатуры.ЕдиницаИзмерения;
			НоваяСтрока.Коэффициент					= ВыборкаНоменклатуры.Коэффициент;
			НоваяСтрока.Количество					= ВыборкаНоменклатуры.КоличествоОстаток;
			НоваяСтрока.ЦенаРозничная				= ВыборкаНоменклатуры.ЦенаРозничная;
			НоваяСтрока.СуммаРозничная				= ВыборкаНоменклатуры.СуммаРозничная;
			НоваяСтрока.ДокументПередачи			= Основание;
			Если ВыборочноеСписаниеПартий Тогда
				НоваяСтрока.Партия					= Основание;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьОстаткамиПоПартии()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.СписаниеТоваров - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	|	СУММА(ПартииТоваровКомпании.Количество) КАК КоличествоОсталось,
	|	СУММА(ПартииТоваровКомпании.Сумма - ПартииТоваровКомпании.СуммаНДС) КАК СуммаОсталось
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.НедостачаТовараПоИнвентаризации) КАК КодОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД КАК РНПТ,
	|	СУММА(ГТДПартийТоваровКомпании.Количество) КАК КоличествоПрослеживаемости
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
	|ГДЕ
	|	ГТДПартийТоваровКомпании.Регистратор = &Ссылка
	|	И ГТДПартийТоваровКомпании.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпании.Номенклатура,
	|	ГТДПартийТоваровКомпании.ГТД";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	// Проверим есть РНПТ у документа
	Если ПакетЗапросов[1].Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаТоваров = ПакетЗапросов[0].Выгрузить();
	ТаблицаРНПТ = ПакетЗапросов[1].Выгрузить();
	
	// Зафиксируем данные для заполнения
	ПериодОтчета = НачалоКвартала(Дата);
	НомерДокумента = дкПолучитьНомерДляПечати(Ссылка);
	
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Ссылка;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = Дата;
		
		// Получим сумму товара
		КоличествоРНПТ = ТекущаяСтрока.КоличествоПрослеживаемости;
		НайденныеСтроки = ТаблицаТоваров.НайтиСтроки(Новый Структура("Номенклатура", ТекущаяСтрока.Номенклатура));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокаНоменклатуры = НайденныеСтроки[0];
		
		Если КоличествоРНПТ >= СтрокаНоменклатуры.КоличествоОсталось Тогда
			СуммаБезНДС = СтрокаНоменклатуры.СуммаОсталось;
		Иначе
			СуммаБезНДС = Окр(СтрокаНоменклатуры.СуммаОсталось /
				?(СтрокаНоменклатуры.КоличествоОсталось = 0, 1, СтрокаНоменклатуры.КоличествоОсталось)
				* КоличествоРНПТ, 2);
		КонецЕсли;
		НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
		
		СтрокаНоменклатуры.КоличествоОсталось = СтрокаНоменклатуры.КоличествоОсталось - КоличествоРНПТ;
		СтрокаНоменклатуры.СуммаОсталось = СтрокаНоменклатуры.СуммаОсталось - СуммаБезНДС;
		Если СтрокаНоменклатуры.КоличествоОсталось <= 0 Тогда
			ТаблицаТоваров.Удалить(СтрокаНоменклатуры);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "СписаниеТоваров"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьСписаниеТоваров(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("СписаниеТоваров");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы	= обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка				= ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПодразделения	= спПолучитьНаименование(ПодразделениеКомпании);
	Если ХозОперация = Справочники.ХозОперации.СписаниеТоваров ИЛИ
		 ХозОперация = Справочники.ХозОперации.СписаниеВПроизводство Тогда
		ОбластьМакета.Параметры.ПредставлениеСкладаКонтрагента	= спПолучитьНаименование(СкладКомпании);
	    ОбластьМакета.Параметры.тСкладКонтрагент = "Склад компании:";
	Иначе
		ОбластьМакета.Параметры.ПредставлениеСкладаКонтрагента	= спПолучитьНаименование(Контрагент);
	    ОбластьМакета.Параметры.тСкладКонтрагент = "Контрагент:";
	КОнецЕсли;
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ИмяРегистра = ?(ХозОперация = Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию, "ПартииТоваровОтданные", "ПартииТоваровКомпании");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		Сумма = дкПолучитьСуммуСписания(ЭтотОбъект, ИмяРегистра, тКэшСуммСписания, 
				СтрокаТабличнойЧасти.Номенклатура, 
				СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, 
				СтрокаТабличнойЧасти.НомерСтроки);
		Цена  = Сумма / СтрокаТабличнойЧасти.Количество / СтрокаТабличнойЧасти.Коэффициент;
		
		СтруктураСтроки.Вставить("Цена",		Формат(Цена,  ФорматВыводаСуммы));
		СтруктураСтроки.Вставить("СуммаВсего",	Формат(Сумма, ФорматВыводаСуммы));
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		СтруктураСтроки.Вставить("СуммаВсего", Сумма); // чтобы округление не сказалось на итоговой сумме
		дкДобавитьИтогиПоСтранице(СтруктураСтроки,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;	
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = дкПолучитьСуммуСписания(ЭтотОбъект, ИмяРегистра, тКэшСуммСписания);
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований " + ВыборкаТабличнойЧасти.Количество()
		+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьСписаниеТоваров()

// Формирует печатную форму "ТОРГ16"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьТОРГ16(ТабДокумент) Экспорт
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	Макет = ПолучитьОбщийМакет("ТОРГ16");

	СтруктураПредставления = Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с ");
	
	// Выводим общие реквизиты шапки
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(Организация, СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО		 = Организация.КодПоОКПО;
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);

	Если ХозОперация = Справочники.ХозОперации.СписаниеТоваров ИЛИ
		 ХозОперация = Справочники.ХозОперации.СписаниеВПроизводство Тогда
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПартииТоваровКомпании.НомерСтроки,
		|	ПартииТоваровКомпании.Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровКомпании.Номенклатура) КАК НоменклатураПредставление,
		|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
		|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровКомпании.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатурыПредставление,
		|	ПартииТоваровКомпании.Партия,
		|	ПартииТоваровКомпании.Количество,
		|	ПартииТоваровКомпании.Сумма,
		|	ПартииТоваровКомпании.СуммаУпр,
		|	ПартииТоваровКомпании.СуммаНДС,
		|	ЕСТЬNULL(ОстаткиТоваровКомпании.СуммаРозн, 0) КАК СуммаРозничная
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
		|		ПО ПартииТоваровКомпании.Регистратор = ОстаткиТоваровКомпании.Регистратор
		|			И ПартииТоваровКомпании.Номенклатура = ОстаткиТоваровКомпании.Номенклатура
		|			И ПартииТоваровКомпании.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры
		|ГДЕ
		|	ПартииТоваровКомпании.Регистратор = &Регистратор";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		ВыборкаСтрокТовары = Запрос.Выполнить().Выгрузить();
		ВыборкаСтрокТовары.Колонки.Добавить("Цена");
		ПростоеСписание = Истина;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию Тогда
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПартииТоваровОтданные.НомерСтроки,
		|	ПартииТоваровОтданные.Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровОтданные.Номенклатура) КАК НоменклатураПредставление,
		|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
		|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровОтданные.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатурыПредставление,
		|	ПартииТоваровОтданные.Партия,
		|	ПартииТоваровОтданные.ДокументПередачи,
		|	ПартииТоваровОтданные.Количество,
		|	ПартииТоваровОтданные.Сумма,
		|	ПартииТоваровОтданные.СуммаУпр,
		|	ПартииТоваровОтданные.СуммаНДС,
		|	ЕСТЬNULL(ОстаткиТоваровКомпании.СуммаРозн, 0) КАК СуммаРозничная
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
		|		ПО ПартииТоваровОтданные.Регистратор = ОстаткиТоваровКомпании.Регистратор
		|			И ПартииТоваровОтданные.Номенклатура = ОстаткиТоваровКомпании.Номенклатура
		|			И ПартииТоваровОтданные.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры
		|ГДЕ
		|	ПартииТоваровОтданные.Регистратор = &Регистратор";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		ВыборкаСтрокТовары = Запрос.Выполнить().Выгрузить();
		ВыборкаСтрокТовары.Колонки.Добавить("Цена");
		ПростоеСписание = Ложь;
		
	Иначе
		Сообщить("Неправильный вид хозяйственной операции.");
		Возврат Неопределено;
		
	КонецЕсли; 
	
	//определимся откуда будем брать многострочную часть документа
	//если движения есть, то из регистра иначе из табличной части документа
	ЕстьДвиженияПоРегистру = Ложь;
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();
	Если КоличествоСтрок > 0 Тогда
		ЕстьДвиженияПоРегистру = Истина;
	Иначе
		Сообщить("Документ не проведен по партионному регистру. Отображение партий невозможно.");
		ВыборкаСтрокТовары = Товары.Выгрузить();
		ВыборкаСтрокТовары.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число"));
		ВыборкаСтрокТовары.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
		ВыборкаСтрокТовары.Колонки.Добавить("НоменклатураПредставление", Новый ОписаниеТипов("Строка"));
		ВыборкаСтрокТовары.Колонки.Добавить("ХарактеристикаНоменклатурыПредставление", Новый ОписаниеТипов("Строка"));
	КонецЕсли; 
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицыПервогоЛиста");
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтраницеПервогоЛиста");
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	//таблица партий - преобразование
	тблПартий = ВыборкаСтрокТовары.Скопировать();
	Если ЕстьДвиженияПоРегистру И НЕ ПростоеСписание Тогда
		Попытка
			тблПартий.Колонки.Партия.Имя = "ПартияСтарый";
		Исключение КонецПопытки; 
		тблПартий.Колонки.ДокументПередачи.Имя = "Партия";
	КонецЕсли;
	тблПартий.Свернуть("Партия");
	тблПартий.Колонки.Добавить("ДатаПоступленияТовара", Новый ОписаниеТипов("Дата"));
	тблПартий.Колонки.Добавить("ТоварнаяНакладнаяНомер", Новый ОписаниеТипов("Строка"));
	тблПартий.Колонки.Добавить("ТоварнаяНакладнаяДата", Новый ОписаниеТипов("Дата"));
	тблПартий.Колонки.Добавить("ДатаСписанияТовара", Новый ОписаниеТипов("Дата"));
	Для Каждого ТекПартия Из тблПартий Цикл
		ТекПартия.ДатаСписанияТовара = Дата;
		Если ТекПартия.Партия <> Неопределено Тогда
			ТекПартия.ДатаПоступленияТовара = ТекПартия.Партия.Дата;
			ТекПартия.ТоварнаяНакладнаяНомер = ТекПартия.Партия.Номер;
			ТекПартия.ТоварнаяНакладнаяДата = ТекПартия.Партия.Дата; //OkoE - а так ли это? //тут вопрос о разнице ДатаПоступленияТовара и ТоварнаяНакладнаяДата KOZT
		КонецЕсли;
	КонецЦикла;
	тблПартий.Сортировать("ДатаПоступленияТовара, ДатаСписанияТовара, ТоварнаяНакладнаяДата");
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаПервогоЛиста");
	Для каждого СтрокаПартия Из тблПартий Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаПартия);

		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, , ЭтотОбъект);
			
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
	КонецЦикла;
	
	ТабДокумент.Вывести(ОбластьМакетаИтогоПоСтранице);
		
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицыВторогоЛиста");
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаВторогоЛиста");
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьМакетаИтого = Макет.ПолучитьОбласть("Итого");
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	СтруктураИтоговПоСтранице = Новый Структура("Сумма", 0);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	// Определим, что показывать в колонке кода - код или артикул
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	СуммаПечатногоДокумента = 0;
	
	ВыборкаСтрокТовары.Свернуть("Номенклатура, НоменклатураПредставление, ХарактеристикаНоменклатуры, ХарактеристикаНоменклатурыПредставление, СуммаРозничная",
	"Количество, Сумма, Цена");
	
	// Выводим многострочную часть документа
	Для Каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		Если ЕстьДвиженияПоРегистру Тогда
			Если ПростоеСписание Тогда
				Если СкладКомпании.Розничный Тогда
					СтрокаТовар.Сумма = СтрокаТовар.СуммаРозничная;
					СтрокаТовар.Цена = СтрокаТовар.СуммаРозничная / СтрокаТовар.Количество;
				Иначе
					СтрокаТовар.Цена = СтрокаТовар.Сумма / СтрокаТовар.Количество;
				КонецЕсли; 
			Иначе
				СтрокаТовар.Цена = СтрокаТовар.Сумма / СтрокаТовар.Количество;
			КонецЕсли; 
		Иначе
			СтрокаТовар.НоменклатураПредставление = Строка(СтрокаТовар.Номенклатура);
			СтрокаТовар.ХарактеристикаНоменклатурыПредставление = Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
			СтрокаТовар.Цена = СтрокаТовар.ЦенаРозничная;
			СтрокаТовар.Сумма = СтрокаТовар.ЦенаРозничная * СтрокаТовар.Количество;
			Если ВалютаДокумента <> ВалютаРегл Тогда
				СтрокаТовар.Сумма = обПересчет(СтрокаТовар.Сумма, ВалютаДокумента, Дата, ВалютаРегл, Дата);
				СтрокаТовар.Цена = обПересчет(СтрокаТовар.Цена, ВалютаДокумента, Дата, ВалютаРегл, Дата);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		ОбластьМакета.Параметры.ТоварНаименование = СтрокаТовар.НоменклатураПредставление
			+ ?(СтрокаТовар.ХарактеристикаНоменклатурыПредставление = "", "", ", " + СтрокаТовар.ХарактеристикаНоменклатурыПредставление);
		ОбластьМакета.Параметры.ТоварКод = дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура, ИмяКолонкиКода, ЭтотОбъект);
		ОбластьМакета.Параметры.КоличествоМест = Формат(СтрокаТовар.Количество,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.Цена = Формат(СтрокаТовар.Цена,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Сумма = Формат(СтрокаТовар.Сумма,ФорматВыводаСуммы);
		
		Если ЕстьДвиженияПоРегистру Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмерения = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения;	
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения.Код;
		Иначе
			ОбластьМакета.Параметры.ЕдиницаИзмерения = СтрокаТовар.ЕдиницаИзмерения;
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ = СтрокаТовар.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		КонецЕсли;
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрокТовары.Индекс(СтрокаТовар) = ВыборкаСтрокТовары.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьМакетаИтого);
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("Сумма", 0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТовар, СтруктураИтоговПоСтранице);
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтого, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в общем
	СуммаИтого = ВыборкаСтрокТовары.Итог("Сумма");
	ОбластьМакетаИтого.Параметры.Сумма = Формат(СуммаИтого, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьМакетаИтого);

	// Выводим подвал документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	// форма регламентированная, все суммы в руб., итого - тоже в руб.
	ОбластьПодвал.Параметры.СуммаСписанияПрописью = обЧислоПрописью(СуммаИтого, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
	
	// Комиссия
	ПредседательКомиссии= дкОтветственноеЛицо(ЭтотОбъект, "ПредседательКомиссии");	
	ЧленКомиссии1		= дкОтветственноеЛицо(ЭтотОбъект, "ЧленКомиссии1");
	ЧленКомиссии2		= дкОтветственноеЛицо(ЭтотОбъект, "ЧленКомиссии2");
	МОЛ					= дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.МОЛ);
	ОбластьПодвал.Параметры.Заполнить(ПредседательКомиссии);
	ОбластьПодвал.Параметры.Заполнить(ЧленКомиссии1);
	ОбластьПодвал.Параметры.Заполнить(ЧленКомиссии2);
	ОбластьПодвал.Параметры.Заполнить(МОЛ);
	
	ТабДокумент.Вывести(ОбластьПодвал);

	Возврат ТабДокумент;
КонецФункции // ПечатьТОРГ16()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если НЕ Основание = Неопределено Тогда
		Если Основание.ХозОперация = Справочники.ХозОперации.РасходныйСкладскойОрдер Тогда
			Для Каждого СтрТовар Из Товары Цикл
				ОбработкаРеквизита("Товары.Количество", СтрТовар);
			КонецЦикла;
		КонецЕсли;
		Если Основание.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
			СкладКомпании=Основание.СкладПолучатель;
		КонецЕсли;
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров") ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.ВводОстатковТоваров") Тогда
			Если Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваров 
				ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия 
				ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ПоступлениеБезвозмездное 
				ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ВводОстатковТоваров
				ИЛИ Основание.ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию Тогда
				ХозОперация = Справочники.ХозОперации.СписаниеТоваров;
				ЗаполнитьТоварыОстатками(Основание);
			ИначеЕсли Основание.ХозОперация = Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
				ХозОперация = Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию;
				ЗаполнитьТоварыОстатками(Основание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ВедетсяБалансПоПодразделению=(Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	Если ХозОперация=Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию ИЛИ
		 ВедетсяБалансПоПодразделению Тогда
		 ПодразделениеЗатрат=Справочники.ПодразделенияКомпании.ПустаяСсылка();
	Иначе
		 ПодразделениеЗатрат=ПодразделениеКомпании;
	КонецЕсли;
	Для Каждого СтрокаТовар Из Товары Цикл
		// Заполним доп. поля для товарной строки
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Результат = Истина;
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ ФлагПерерасчет Тогда
		Результат = дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	ФлагПерерасчет = Ложь;
	Если НЕ Результат Тогда Возврат; КонецЕсли; 
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Заказ  ИЗ РегистрНакопления.ЗаказыПокупателей ГДЕ Регистратор = &Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;   
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --
	
	//++СнимченкоАА_бизтех++
	
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	Если ХозОперация=Справочники.ХозОперации.СписаниеТоваров ИЛИ
		 ХозОперация=Справочники.ХозОперации.СписаниеВПроизводство Тогда
		// Спишем товар со склада
		РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаПоТоварам(ЭтотОбъект);
		НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
		НаборЗаписейОстатки.ДвиженияПоРознице         = СкладКомпании.Розничный;
		НаборЗаписейОстатки.Резервировать             = Истина;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
		
		Отказ= НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
		Если Отказ Тогда
			// выведем сообщения об ошибках и предупреждениях
			дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
			Возврат; // дальше смысла не имеет
		КонецЕсли;
		
		// Снимаем резервы по заказам (если таковые были)
		НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = НаборЗаписейОстатки.Выгрузить();
		НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
		Отказ=НЕ НаборЗаписейЗаказыПокупателей.СнятиеРезервовЗаказовПокупателей() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ЗаказыПокупателей.Количество()>0 И 
		(ХозОперация=Справочники.ХозОперации.СписаниеТоваров ИЛИ ХозОперация=Справочники.ХозОперации.СписаниеВПроизводство));
		 
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// проведем партии товаров
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// Изменим состояние маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
	НаборЗаписейСостоянияКодовМаркировки.ПроверятьВыводИзОборота = Истина;
	НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаВРезультатеСписания;
	НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
	Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
	
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		ФлагПерерасчет = Истина;
	КонецЕсли;
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	// Проверим налиие прослеживаемых товаров, которые были реализованы
	Если НЕ Отказ Тогда
		Движения.ПартииТоваровКомпании.Записать();
		Движения.ГТДПартийТоваровКомпании.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Количество = тс.Количество;  
				
				Отказ = (Отказ или НЕ ХватаетНаСкладе_ПроверкаМарок(СкладКомпании,тс.Номенклатура,Движение.GTIN,Движение.СерийныйНомер,СтрокаТаблицы.КодМаркировки,тс.Количество));
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

Функция ХватаетНаСкладе_ПроверкаМарок(Склад,Номенклатура,GTIN,СерийныйНомер,КодМаркировки,Количество)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыМаркировкиОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.КодыМаркировки.Остатки(&МомВрем, ) КАК КодыМаркировкиОстатки
	|ГДЕ
	|	КодыМаркировкиОстатки.Организация = &Организация
	|	И КодыМаркировкиОстатки.Склад = &Склад
	|	И КодыМаркировкиОстатки.Номенклатура = &Номенклатура
	|	И КодыМаркировкиОстатки.GTIN = &GTIN
	|	И КодыМаркировкиОстатки.СерийныйНомер = &СерийныйНомер";
	
	Запрос.УстановитьПараметр("МомВрем", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));  
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
		Возврат Ложь
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество Тогда    
			Сообщить("По номенклатуре "+Номенклатура+" по коду маркировки "+КодМаркировки+" на складе "+Склад+" нет достаточного количества для списания!");
			Возврат Ложь
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
//Права = глПрава;
#КонецЕсли
ФлагПерерасчет = Ложь;
ВедетсяБалансПоПодразделению=(Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);

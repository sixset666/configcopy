// Модуль документа ЗаявкаНаРемонт

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;                                    // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;                       // Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаРасчетСкидок Экспорт;                    // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
// свойства и методы для расчета скидок документа.
Перем ОбработкаРасчетСкидокАвторабот Экспорт;           // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
// свойства и методы для расчета скидок документа.
Перем ОбработкаЗначенияСвойствОбъектов Экспорт;         // Переменная для хранения значения свойств основания/объекта копирования
Перем ФорматПредставленияГодаВыпускаАвтомобиля Экспорт; //Переменная с форматом отображения года выпуска автомобиля
Перем ТекущаяВалютаДокумента Экспорт;                   // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;                     // Текущий курс валюты документа
Перем ИмяРеквизитаКода Экспорт;                         // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ТоварыБезНДС;
Перем РаботыБезНДС;

//++СнимченкоАА_бизтех++
Перем КешИнформацииЗН; // хранит кеш информации ЗН 
Перем КешИтоговойИнформацииЗН; // хранит кеш итоговой информации ЗН 
Перем КэшДанныхПоПеремещению Экспорт; // хранит кеш данных по перемещению  
Перем КэшОстатковНоменклатурыНаСкладе; // хранит кеш остатков номенклатуры на складе
//-СнимченкоАА_бизтех--

//Перем Нормочасы; // Список нормочасов

Перем СкладПоПодразделению Экспорт; //БИЗТЕХ 08.07.2025 КОВАЛЬ ++ //Кеш склада по подразделению

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Если Клиент Тогда
	// Заполнение табличных частей "Работы" и "Планирование" по виду ремонта
	// Параметры: РежимРаботы,РежимПланирование - обработка табличных частей
	// 0 - не обрабатывать, 
	// 1- заполнять без очистки, 
	// 2 - заполнять с очисткой, 
	// 3 - калькуляция, заполнять работы без исполнителей без очистки,
	// 4 - калькуляция, заполнять работы без исполнителей с очисткой
	Процедура ЗаполнитьРаботамиПоВидуРемонта(РежимРаботы=0, РежимПланирование=0, РаботыПоВидуРемонта, ЭтаФорма=Неопределено)  Экспорт
		
		Если РаботыПоВидуРемонта = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// Очистим табличные части в зависимости от режимов
		Если РежимРаботы = 2 Тогда
			ЭтотОбъект.Работы.Очистить();
			ЭтотОбъект.Исполнители.Очистить();
		КонецЕсли;
		Если РежимРаботы = 4 Тогда
			ЭтотОбъект.Работы.Очистить();
		КонецЕсли;
		Если РежимПланирование = 2 Тогда
			ЭтотОбъект.Планирование.Очистить();
		КонецЕсли;
		
		// Заполним  
		МассивДобавленныхРабот = Новый СписокЗначений;
		Для Каждого СтрокаРабот Из РаботыПоВидуРемонта Цикл
			ВидИспользованияАвтоработы = обПолучитьЗначениеСвойства(СтрокаРабот.Авторабота,ПланыВидовХарактеристик.СвойстваОбъектов.ВидИспользованияАвтоработы,Перечисления.ВидыИспользованияАвторабот.Производство);
			Если РежимРаботы > 0 И ВидИспользованияАвтоработы <> Перечисления.ВидыИспользованияАвторабот.Планирование Тогда
				// Поищем строку с такой работой в табличной части
				ТекСтрока = ЭтотОбъект.Работы.Найти(СтрокаРабот.Авторабота,"Работа");
				Если ТекСтрока <> Неопределено Тогда
					ТекСтрока.Количество = ТекСтрока.Количество + СтрокаРабот.Количество;
					ОбработкаРеквизита("Работы.Количество",ТекСтрока);
				Иначе
					ТекСтрока = ЭтотОбъект.Работы.Добавить();
					ТекСтрока.Работа = СтрокаРабот.Авторабота;
					ТекСтрока.Количество = СтрокаРабот.Количество;
					ОбработкаРеквизита("Работы.Работа",ТекСтрока);
					ТекСтрока.ИдентификаторРаботы = Новый УникальныйИдентификатор;
					Если ЭтаФорма<>Неопределено Тогда
						ЭтаФорма.ИдентификаторТекущейРаботы = ТекСтрока.ИдентификаторРаботы;
					КонецЕсли;
					МассивДобавленныхРабот.Добавить(ТекСтрока);
				КонецЕсли;
			КонецЕсли;
			Если РежимПланирование > 0 И ВидИспользованияАвтоработы <> Перечисления.ВидыИспользованияАвторабот.Производство Тогда
				НоваяСтрока = ЭтотОбъект.Планирование.Добавить();
				НоваяСтрока.Авторабота = СтрокаРабот.Авторабота;
				ОбработкаРеквизита("Планирование.Авторабота",НоваяСтрока);
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтаФорма<>Неопределено Тогда
			Если МассивДобавленныхРабот.Количество() > 0 И РежимРаботы < 3 Тогда
				зфЗНЗаполнениеИсполнителей(ЭтаФорма,МассивДобавленныхРабот);	
			КонецЕсли;
		КонецЕсли;
		
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				Если РежимРаботы > 1 Тогда
					ВывестиПодвалСкидокРабот(ЭтаФорма);
				КонецЕсли;
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			КонецЕсли;
		#КонецЕсли
		
	КонецПроцедуры
#КонецЕсли

#Если Клиент Тогда
	// Задать вопрос о заполнении табличных частей "Работы" и "Планирование" по виду ремонта
	//
	Процедура ПредложитьЗаполнитьРаботамиПоВидуРемонта(ЭтаФорма=Неопределено)  Экспорт
		Если обПраво("РедактированиеРаботЗаказНаряда",Права)
			И ЗначениеЗаполнено(ВидРемонта) 
			И ВидРемонта.Работы.Количество() > 0 Тогда
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ИмяФормы", Неопределено);
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ЭтаФорма);
			Если СтруктураПоиска.ИмяФормы <> Неопределено Тогда
				ИмяФормы = СтруктураПоиска.ИмяФормы;
			Иначе
				ИмяФормы = "";
			КонецЕсли;		
			
			Очищать = ИСТИНА;
			Заполнять = ИСТИНА;
			Если ЭтаФорма <> Неопределено Тогда
				
				Если ИмяФормы = "АРМКалькуляция" Тогда
					//Калькуляция
					Если ЭтотОбъект.Работы.Количество() > 0 Тогда
						ОтветПользователя = Вопрос("Табличная часть ""Работы"" не пустая! Заполнить работами по виду ремонта?
						|	ДА - Очистить и заполнить по виду ремонта;
						|	НЕТ - Заполнить по виду ремонта без очистки;
						|	Отмена - Не заполнять.",РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Отмена);
					Иначе
						ОтветПользователя = Вопрос("Заполнить работами по виду ремонта?",РежимДиалогаВопрос.ОКОтмена,,КодВозвратаДиалога.Отмена);					
					КонецЕсли;
				ИначеЕсли ИмяФормы = "АРМЗаписьНаРемонт" Тогда
					//Запись на ремонт
					Если ЭтотОбъект.Планирование.Количество() > 0 Тогда
						ОтветПользователя = Вопрос("Табличная часть ""Планирование"" не пустая! Заполнить работами по виду ремонта?
						|	ДА - Очистить и заполнить по виду ремонта;
						|	НЕТ - Заполнить по виду ремонта без очистки;
						|	Отмена - Не заполнять.",РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Отмена);
					Иначе
						ОтветПользователя = Вопрос("Заполнить работами по виду ремонта?",РежимДиалогаВопрос.ОКОтмена,,КодВозвратаДиалога.Отмена);					
					КонецЕсли;
				Иначе
					//форма документа "Заявка на ремонт"
					Если ЭтотОбъект.Работы.Количество() > 0 ИЛИ ЭтотОбъект.Планирование.Количество() > 0 Тогда
						ОтветПользователя = Вопрос("Табличные части не пусты! Заполнить работами по виду ремонта?
						|	ДА - Очистить и заполнить по виду ремонта;
						|	НЕТ - Заполнить по виду ремонта без очистки;
						|	Отмена - Не заполнять.",РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Отмена);
					Иначе
						ОтветПользователя = Вопрос("Заполнить работами по виду ремонта?",РежимДиалогаВопрос.ОКОтмена,,КодВозвратаДиалога.Отмена);
					КонецЕсли;	
				КонецЕсли;	
				
				Если ОтветПользователя = КодВозвратаДиалога.Отмена Тогда
					Возврат;
				КонецЕсли;
				
				Очищать = (ОтветПользователя = КодВозвратаДиалога.Да ИЛИ ОтветПользователя = КодВозвратаДиалога.ОК);
				Заполнять = НЕ (ОтветПользователя = КодВозвратаДиалога.Отмена);
				
			КонецЕсли;
			Если Заполнять Тогда
				Если ЭтаФорма<>Неопределено И ИмяФормы = "АРМКалькуляция" Тогда
					РежимОбработкиРаботы = ?(Очищать,4,3);
					РежимОбработкиПланирование = 0;
				ИначеЕсли ЭтаФорма<>Неопределено И ИмяФормы = "АРМЗаписьНаРемонт" Тогда
					РежимОбработкиРаботы = 0;
					РежимОбработкиПланирование = ?(Очищать,2,1);
				Иначе
					РежимОбработкиРаботы = ?(Очищать,2,1);
					Если ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ПланРемонта Тогда
						РежимОбработкиПланирование = РежимОбработкиРаботы;
					Иначе
						РежимОбработкиПланирование = 0;
					КонецЕсли;
				КонецЕсли;	
				
				//Предложим выбрать работы по виду ремонта (работы по умолчанию)
				ФормаВыбора=Обработки.ПодборРабот.ПолучитьФорму("ФормаПодбораСвязанныхРабот",ЭтаФорма);
				ФормаВыбора.ВидРемонта = ВидРемонта;
				ФормаВыбора.РежимВыбора=Истина;
				ТаблицаРабот = ФормаВыбора.ОткрытьМодально();
				
				ЗаполнитьРаботамиПоВидуРемонта(РежимОбработкиРаботы,РежимОбработкиПланирование,ТаблицаРабот,ЭтаФорма);
				
			КонецЕсли;
		КонецЕсли;
	КонецПроцедуры
#КонецЕсли

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	ВремСуммаНоменклатурыДокумента=Товары.Итог("СуммаВсего");
	ВремСуммаРаботДокумента=Работы.Итог("СуммаВсего");
	ВремСуммаДокумента=СуммаНоменклатурыДокумента+СуммаРаботДокумента;
	Если СуммаНоменклатурыДокумента<>ВремСуммаНоменклатурыДокумента Тогда
		СуммаНоменклатурыДокумента=ВремСуммаНоменклатурыДокумента;
	КонецЕсли; 
	Если СуммаРаботДокумента<>ВремСуммаРаботДокумента Тогда
		СуммаРаботДокумента=ВремСуммаРаботДокумента;
	КонецЕсли; 
	Если СуммаДокумента<>ВремСуммаДокумента Тогда
		СуммаДокумента=ВремСуммаДокумента;
	КонецЕсли;
	Возврат СуммаДокумента;
КонецФункции

// выводит в подвал информацию по скидкам работ
//
Процедура ВывестиПодвалСкидокРабот(ЭтаФорма) Экспорт
	Если ЭтаФорма<>Неопределено И
		ЭтаФорма.ЭлементыФормы.Найти("Работы")<>Неопределено И 
		ЭтаФорма.ЭлементыФормы.Работы.Колонки.Найти("Работа")<>Неопределено Тогда
		//Подписи таблицы работ
		ТекстСкидка = "Скидка: ";
		Если СкидкаНаценкаРаботы.Пустая() Тогда
			ТекстСкидка = ТекстСкидка + "<нет>";
		Иначе
			ТекстСкидка = ТекстСкидка + СокрЛП(СкидкаНаценкаРаботы.Наименование);
			ТекстСкидка = ТекстСкидка + " (" + ЗначениеСкидкиНаценкиРабот + " " + ?(СкидкаНаценкаРаботы.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная, "%", Строка(Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить())) + ")";
		КонецЕсли;			
		ЭтаФорма.ЭлементыФормы.Работы.Колонки.Работа.ТекстПодвала=ТекстСкидка;
	КонецЕсли; 
КонецПроцедуры

// Выполняет пересчет цен авторабот
//
Процедура ПерерасчетЦенАвторабот(ЗадаватьВопрос=Ложь,ДопПараметры) Экспорт
	ПерерасчетЦенРаботВыполнен=Ложь;
	Если ДопПараметры=Неопределено Тогда
		ДопПараметры=Новый Структура;
	Иначе
		Если ДопПараметры.Свойство("ПерерасчетЦенРаботВыполнен",ПерерасчетЦенРаботВыполнен) Тогда
			Если ПерерасчетЦенРаботВыполнен Тогда
				Возврат;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	ДопПараметры.Вставить("ПерерасчетЦенРаботВыполнен",Истина);
	
	Если Работы.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
		Если ЗадаватьВопрос Тогда
			Если Вопрос("Выполнить перерасчет цен авторабот?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да,"Изменение цен")<>КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
		КонецЕсли; 
	#КонецЕсли
	
	Для Каждого СтрокаРабот Из Работы Цикл
		//Если текущая работа не заполнена - больше ничего не делаем
		Если обЗначениеНеЗаполнено(СтрокаРабот.Работа) Тогда Продолжить; КонецЕсли;
		ОбработкаРеквизита("РасчетЦеныРаботы",СтрокаРабот,,ДопПараметры);
		ОбработкаРеквизита("Работы.Цена",СтрокаРабот,,ДопПараметры);
	КонецЦикла;
КонецПроцедуры 

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ++ Alexku 29.09.2016
	Если ДопПараметры=Неопределено Тогда
		ДопПараметры=Новый Структура;
	КонецЕсли;
	// -- Alexku 
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Дата" Тогда		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиРабот", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Возврат Рез;
		
		//KRAA ++
	ИначеЕсли Имя="ХозОперация" Тогда
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				ЭтаФорма.ЭлементыФормы.ОсновнаяПанель.Страницы.Планирование.Видимость = (ХозОперация = Справочники.ХозОперации.ПланРемонта);
			КонецЕсли;
		#КонецЕсли
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		//KRAA --
	ИначеЕсли Имя = "Состояние" Тогда
		Если Состояние = Перечисления.СостоянияЗаявкиНаРемонт.Отклонено И НЕ ЗначениеЗаполнено(ПричинаОтказа) Тогда
			#Если Клиент Тогда
				// вызываем форму заполнения "Причины отказа"
				ФормаВыбораПричины = Документы.ЗаявкаНаРемонт.ПолучитьФорму("ФормаЗаполненияПричиныОтказа", ЭтаФорма, ЭтаФорма);
				ФормаВыбораПричины.ЗакрыватьПриЗакрытииВладельца = Истина;
				РезультатВыбора = ФормаВыбораПричины.ОткрытьМодально();
				Если РезультатВыбора = Неопределено Тогда
					Состояние     = Перечисления.СостоянияЗаявкиНаРемонт.ПустаяСсылка();
					ПричинаОтказа = Неопределено;
				Иначе
					ПричинаОтказа = РезультатВыбора.ПричинаОтказа;
					Комментарий   = РезультатВыбора.КомментарийКОтказу;
				КонецЕсли;
			#Иначе
				ПричинаОтказа = Справочники.ПричиныОтказаОтОбслуживания.Другое;
			#КонецЕсли
		ИначеЕсли Состояние <> Перечисления.СостоянияЗаявкиНаРемонт.Отклонено И ЗначениеЗаполнено(ПричинаОтказа) Тогда
			ПричинаОтказа = Неопределено;
		КонецЕсли;
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		//Попытка
		//	ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		//Исключение
		//	ТребуетсяПересчет=Ложь;
		//КонецПопытки; 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		//Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
		//	Для Каждого СтрокаРабот Из Работы Цикл
		//		НоваяЦена=обПересчет(СтрокаРабот.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
		//		Если НоваяЦена<>СтрокаРабот.Цена Тогда
		//			СтрокаРабот.Цена=НоваяЦена;
		//			Рез=ОбработкаРеквизита("Работы.Цена",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		//		КонецЕсли;
		//	КонецЦикла; 
		//КонецЕсли;
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Рез=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиРабот",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Организация может является плательщиком НДС, а может и нет.. надо обработать таблицу работ
		ТаблицаРабот=ЭтотОбъект.Работы;
		ОсвобожденОтНДС=ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаРабот Из ТаблицаРабот Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаРабот.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаРабот.СтавкаНДС=СтрокаРабот.Работа.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			Рез=ЭтотОбъект.ОбработкаРеквизита("Работы.СтавкаНДС",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		КонецЦикла;
		Возврат Рез;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Подразделение может является плательщиком НДС, а может и нет.. надо обработать таблицу работ
		ТаблицаРабот=ЭтотОбъект.Работы;
		ОсвобожденОтНДС=ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаРабот Из ТаблицаРабот Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаРабот.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаРабот.СтавкаНДС=СтрокаРабот.Работа.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			Рез=ЭтотОбъект.ОбработкаРеквизита("Работы.СтавкаНДС",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		КонецЦикла;
		// Изменение подразделения приводит к необходимости пересчета скидок документа (как "шапочных", так и товарных).
		Рез=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиРабот", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Заказчик" Тогда
		Рез=ОбработкаРеквизита("УстановитьГарантийногоПлательщика",ТекСтрока,ЭтаФорма,ДопПараметры);
		
		#Если Клиент Тогда
			Если ДопПараметры <> Неопределено И ДопПараметры.Свойство("НеОбновлятьТелефон") Тогда
				ОбновитьТелефон = Ложь;
			Иначе
				ОбновитьТелефон = Истина;
			КонецЕсли;
			Если ОбновитьТелефон Тогда
				КодРегиона = "";
				КодГорода = "";
				НомерТелефона = "";
				ВнутреннийНомер = "";
				ПредставлениеТелефона = "";
				ВидТелефона = Неопределено;
				Если ЗначениеЗаполнено(Заказчик) Тогда
					Телефоны = киПолучитьСписокТелефоновОбъекта(Заказчик);
					Если Телефоны<>Неопределено И Телефоны.Количество() > 0 Тогда
						ЗаполнитьЗначенияСвойств(ЭтотОбъект,Телефоны[0].Значение);
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
		#КонецЕсли
		
		Если ТипЗнч(Заказчик) = Тип("СправочникСсылка.Контрагенты") Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	КонтактнаяИнформация.Представление КАК ПредставлениеАдресаЭлектроннойПочты
			               |ИЗ
			               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			               |ГДЕ
			               |	КонтактнаяИнформация.Объект = &Контрагент
			               |	И КонтактнаяИнформация.Тип = &Тип
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	КонтактнаяИнформация.ЗначениеПоУмолчанию УБЫВ";
			Запрос.УстановитьПараметр("Тип",Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Запрос.УстановитьПараметр("Контрагент",Заказчик);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если ЗначениеЗаполнено(Выборка.ПредставлениеАдресаЭлектроннойПочты) Тогда 
					АдресЭлектронойПочты = Выборка.ПредставлениеАдресаЭлектроннойПочты;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Автомобиль" Тогда
		Рез=Истина;
		Если (НЕ обЗначениеНеЗаполнено(Автомобиль)) И ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
			Хозяин = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин, Дата);
			VIN    = Автомобиль.VIN;
			Если (НЕ обЗначениеНеЗаполнено(Хозяин)) И (Заказчик<>Хозяин) Тогда
				Заказчик=Хозяин;
				Рез=ОбработкаРеквизита("Заказчик",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
			ЭтотОбъект.ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер, Дата);
		Иначе
			ЭтотОбъект.ГосНомер = "";	
		КонецЕсли;
		
		#Если Клиент Тогда
			// KRAA ++ Спрашиваем только если есть форма	
			ПерерасчетЦенАвторабот(ЭтаФорма<>Неопределено,ДопПараметры);
			// KRAA --
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Цех" Тогда
		Рез=Истина;
		Если (НЕ обЗначениеНеЗаполнено(Цех)) И (обЗначениеНеЗаполнено(Мастер)) Тогда
			Мастер=Цех.Мастер;
			Рез=ОбработкаРеквизита("Мастер",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		#Если Клиент Тогда
			// KRAA ++ Спрашиваем только если есть форма	
			ПерерасчетЦенАвторабот((ЭтаФорма<>Неопределено),ДопПараметры); // KRAA Не будем спрашивать, если форма еще не открыта
			// KRAA --
		#КонецЕсли
		Рез=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиРабот", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="ВидРемонта" Тогда
		Если обЗначениеНеЗаполнено(ВидРемонта) Тогда
			Возврат Истина;
		КонецЕсли;
		Рез=Истина;
		Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон") Тогда
			КомплектацияАвтомобиля=Справочники.ВидыРемонта.КомплектацияАвтомобиля;
			Если (КомплектацияАвтомобиля.Пустая()) ИЛИ (НЕ КомплектацияАвтомобиля.Предопределенный) Тогда
				КомплектацияАвтомобиля=Неопределено;
			КонецЕсли; 
		Иначе
			КомплектацияАвтомобиля=Неопределено;
		КонецЕсли; 
		
		Если ВидРемонта=КомплектацияАвтомобиля ИЛИ 
			ВидРемонта.ТипРемонта=Перечисления.ТипыРемонта.Бесплатный Тогда
			Если ЗначениеЗаполнено(Контрагент) Тогда
				Контрагент=Справочники.Контрагенты.ПустаяСсылка();
				Рез=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
			Если ЗначениеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ДоговорВзаиморасчетов=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				Рез=ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		КонецЕсли;
		
		Если ДопПараметры=Неопределено Тогда
			ДопПараметры=Новый Структура;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			ДопПараметры.Вставить("ПодставлятьКонтрагентаИзЗаказчика",Ложь);
		КонецЕсли;
		Рез=ОбработкаРеквизита("УстановитьГарантийногоПлательщика",ТекСтрока,ЭтаФорма,ДопПараметры);
		
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				ПредложитьЗаполнитьРаботамиПоВидуРемонта(ЭтаФорма);
			КонецЕсли;
			
			Если ДопПараметры=Неопределено Тогда
				ДопПараметры=Новый Структура("НеРассчитыватьСкидки",Истина);
			Иначе
				ДопПараметры.Вставить("НеРассчитыватьСкидки",Истина);
			КонецЕсли; 
			// KRAA ++ Спрашиваем только если есть форма
			ПерерасчетЦенАвторабот(ЭтаФорма<>Неопределено,ДопПараметры);
			// KRAA --
			ДопПараметры.Удалить("НеРассчитыватьСкидки");
		#КонецЕсли
		
		Если ВидРемонта=КомплектацияАвтомобиля ИЛИ
			ВидРемонта.ТипРемонта<>Перечисления.ТипыРемонта.Платный Тогда
			
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки          = Истина;
			ОбработкаРасчетСкидокАвторабот.НеРассчитыватьАвтоматическиеСкидки = Истина;
			
			Если ЗначениеЗаполнено(Карточка) Тогда
				Карточка=Справочники.Карточки.ПустаяСсылка();
				ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку",Истина);
				ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры);
				ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");	
			КонецЕсли; 
			Если ЗначениеЗаполнено(МаркетинговаяПрограмма) Тогда
				МаркетинговаяПрограмма=Справочники.МаркетинговыеПрограммы.ПустаяСсылка();
				ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку",Истина);
				ОбработкаРеквизита("МаркетинговаяПрограмма",ТекСтрока,ЭтаФорма,ДопПараметры);
				ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СкидкаНаценка) Тогда
				ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку", Истина);
				СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
				Рез = ОбработкаРеквизита("СкидкаНаценка", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
				ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
			КонецЕсли; 
			Если ЗначениеЗаполнено(СкидкаНаценкаРаботы) Тогда
				ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку", Истина);
				СкидкаНаценкаРаботы = Справочники.ТипыСкидок.ПустаяСсылка();
				Рез = ОбработкаРеквизита("СкидкаНаценкаРаботы", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
				ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку", Истина);
			Для Каждого СтрокаТЧ Из Товары Цикл
				Если ЗначениеЗаполнено(СтрокаТЧ.СкидкаНаТовар) Тогда
					СтрокаТЧ.СкидкаНаТовар=Справочники.ТипыСкидок.ПустаяСсылка();
					Рез=ОбработкаРеквизита("Товары.СкидкаНаТовар",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла;
			Для Каждого СтрокаТЧ Из Работы Цикл
				Если ЗначениеЗаполнено(СтрокаТЧ.СкидкаНаТовар) Тогда
					СтрокаТЧ.СкидкаНаТовар=Справочники.ТипыСкидок.ПустаяСсылка();
					Рез=ОбработкаРеквизита("Работы.СкидкаНаТовар",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла;
			ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
		Иначе
			
			ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки          = Ложь;
			ОбработкаРасчетСкидокАвторабот.НеРассчитыватьАвтоматическиеСкидки = Ложь;
			
			Если ДопПараметры<>Неопределено Тогда
				Если ДопПараметры.Свойство("НеРассчитыватьСкидки") Тогда
					ДопПараметры.Удалить("НеРассчитыватьСкидки");
				КонецЕсли;
			КонецЕсли;
			Если ДополнительныеСвойства.Свойство("НеЗаменятьПустуюСкидку") Тогда
				ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
			КонецЕсли;
			Рез=ОбработкаРеквизита("СкидкаНаценка",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			Рез=ОбработкаРеквизита("СкидкаНаценкаРаботы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		
		// Посмотрим, что со ставками НДС по Виду Ремонта
		// товары
		Если ТоварыБезНДС <> ВидРемонта.ОсвобожденОтНДС Тогда
			ТоварыБезНДС = ВидРемонта.ОсвобожденОтНДС;
			Для Каждого ТекСтрока Из Товары Цикл
				Если ТоварыБезНДС Тогда
					ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				Иначе
					ТекСтрока.СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС;
				КонецЕсли;
				ОбработкаРеквизита("Товары.СтавкаНДС",ТекСтрока);
			КонецЦикла;
		КонецЕсли;
		
		// работы
		Если РаботыБезНДС <> ВидРемонта.ОсвобожденОтНДСРаботы Тогда
			РаботыБезНДС = ВидРемонта.ОсвобожденОтНДСРаботы;
			Для Каждого ТекСтрока Из Работы Цикл
				Если РаботыБезНДС Тогда
					ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
				Иначе
					ТекСтрока.СтавкаНДС = ТекСтрока.Работа.Номенклатура.СтавкаНДС;
				КонецЕсли;
				ОбработкаРеквизита("Работы.СтавкаНДС",ТекСтрока);
			КонецЦикла;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Рез=Истина;
		// проверим вид договора
		Если (ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.Покупка) ИЛИ
			(ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом) Тогда
			#Если Клиент Тогда
				Предупреждение("Неправильный вид договора !");
			#КонецЕсли
			ДоговорВзаиморасчетов=Неопределено;
			Рез=ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			Возврат Ложь;
		КонецЕсли;
		#Если Клиент Тогда
			// KRAA ++ Спрашиваем только если есть форма	
			ПерерасчетЦенАвторабот(ЭтаФорма<>Неопределено,ДопПараметры);
			// KRAA --
		#КонецЕсли
		ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку",Ложь);
		// Если у документа есть реквизит СкидкаНаценка, заполним его из Договора 
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		ДоговорСкидкаРаботы = ЭтотОбъект.ДоговорВзаиморасчетов.ТипСкидкиНаценкиРабот;
		Если ЭтотОбъект.СкидкаНаценкаРаботы<>ДоговорСкидкаРаботы И НЕ обЗначениеНеЗаполнено(ДоговорСкидкаРаботы) Тогда
			СкидкаНаценкаРаботы=ЭтотОбъект.ДоговорВзаиморасчетов.ТипСкидкиНаценкиРабот;
			Рез=ОбработкаРеквизита("СкидкаНаценкаРаботы",,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;		
		ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
		Если обЗначениеНеЗаполнено(Заказчик) Тогда
			Заказчик=Контрагент;
			Рез=дкОбработкаРеквизита(ЭтотОбъект,"Заказчик",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
				ВывестиПодвалСкидокРабот(ЭтаФорма);
			КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Карточка" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиРабот", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="МаркетинговаяПрограмма" Тогда
		Рез=Истина;
		Если НЕ обЗначениеНеЗаполнено(МаркетинговаяПрограмма.СкидкаНаценка) Тогда
			СкидкаНаценка=МаркетинговаяПрограмма.СкидкаНаценка;
			Рез=ОбработкаРеквизита("СкидкаНаценка",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли; 
		Если НЕ обЗначениеНеЗаполнено(МаркетинговаяПрограмма.СкидкаНаценкаРаботы) Тогда
			СкидкаНаценкаРаботы=МаркетинговаяПрограмма.СкидкаНаценкаРаботы;
			Рез=ОбработкаРеквизита("СкидкаНаценкаРаботы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="СкидкаНаценка" Тогда
		Если НЕ ДополнительныеСвойства.Свойство("НеЗаменятьПустуюСкидку") Тогда
			ИзменилиСкидку=ОбработкаРасчетСкидок.ТекущаяСкидкаНаценка<>СкидкаНаценка;
			ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку",ИзменилиСкидку И обЗначениеНеЗаполнено(СкидкаНаценка));
		КонецЕсли; 
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
		Возврат Результат
		
	ИначеЕсли Имя="СкидкаНаценкаРаботы" Тогда
		// Смена скидки\наценки влияет на видимость некоторых колонок в табличной части.
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				дкУправлениеДоступностьюКолонокСкидок(ЭтаФорма,СкидкаНаценкаРаботы,"Работы");	
			КонецЕсли;
		#КонецЕсли
		Если НЕ ДополнительныеСвойства.Свойство("НеЗаменятьПустуюСкидку") Тогда
			ИзменилиСкидку=ОбработкаРасчетСкидокАвторабот.ТекущаяСкидкаНаценка<>СкидкаНаценкаРаботы;
			ДополнительныеСвойства.Вставить("НеЗаменятьПустуюСкидку",ИзменилиСкидку И обЗначениеНеЗаполнено(СкидкаНаценкаРаботы));
		КонецЕсли;
		Результат = ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиРабот", ТекСтрока, ЭтаФорма, ДопПараметры);
		ДополнительныеСвойства.Удалить("НеЗаменятьПустуюСкидку");
		Возврат Результат;
		
	ИначеЕсли Имя="УстановитьГарантийногоПлательщика" Тогда
		Рез=Истина;
		НовыйКонтрагент=Контрагент;
		НовыйДоговорВзаиморасчетов=Неопределено;
		Если обЗначениеНеЗаполнено(ВидРемонта) ИЛИ ВидРемонта.ТипРемонта=Перечисления.ТипыРемонта.Платный Тогда
			ВидРемонтаГарантия=Неопределено;
			Если ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
				ВидРемонтаГарантия=ВидРемонта.Гарантия.Найти(Автомобиль.Модель,"Модель");
			ИначеЕсли ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Модели") Тогда
				ВидРемонтаГарантия=ВидРемонта.Гарантия.Найти(Автомобиль,"Модель");
			КонецЕсли; 
			Если ВидРемонтаГарантия<>Неопределено Тогда
				НовыйКонтрагент=ВидРемонтаГарантия.Контрагент;
				НовыйДоговорВзаиморасчетов=ВидРемонтаГарантия.ДоговорВзаиморасчетов;
			Иначе
				ПодставлятьКонтрагентаИзЗаказчика=Истина;
				Если ДопПараметры<>Неопределено Тогда
					Если НЕ ДопПараметры.Свойство("ПодставлятьКонтрагентаИзЗаказчика",ПодставлятьКонтрагентаИзЗаказчика) Тогда
						ПодставлятьКонтрагентаИзЗаказчика=Истина;
					КонецЕсли;
				КонецЕсли; 
				Если ПодставлятьКонтрагентаИзЗаказчика Тогда
					Если ТипЗнч(Заказчик)=Тип("СправочникСсылка.Контрагенты") И Контрагент<>Заказчик Тогда
						
						НовыйКонтрагент = РаботаСКонтактнымиЛицами.ПолучитьВедущегоКонтрагента(Заказчик);
						
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
		Если (НЕ обЗначениеНеЗаполнено(НовыйКонтрагент)) И (Контрагент<>НовыйКонтрагент) ИЛИ
			(НЕ обЗначениеНеЗаполнено(НовыйДоговорВзаиморасчетов)) И (ДоговорВзаиморасчетов<>НовыйДоговорВзаиморасчетов) Тогда
			Если (НЕ обЗначениеНеЗаполнено(НовыйКонтрагент)) И (Контрагент<>НовыйКонтрагент) Тогда
				Контрагент=НовыйКонтрагент;
				ОбработкаРеквизитаКонтрагент = Истина;
				Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
					Если НЕ ДопПараметры.Свойство("ОбработкаРеквизитаКонтрагент",ОбработкаРеквизитаКонтрагент) Тогда
						ОбработкаРеквизитаКонтрагент = Истина;
					КонецЕсли; 	
				КонецЕсли;
				Если ОбработкаРеквизитаКонтрагент Тогда
					Рез=ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЕсли;
			Если (НЕ обЗначениеНеЗаполнено(НовыйДоговорВзаиморасчетов)) И (ДоговорВзаиморасчетов<>НовыйДоговорВзаиморасчетов) Тогда
				ДоговорВзаиморасчетов=НовыйДоговорВзаиморасчетов;
				Рез=ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
			#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено И НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
					Сообщить("Установлен новый плательщик: """+СокрЛП(Контрагент)+""" по договору """+СокрЛП(ДоговорВзаиморасчетов)+"""");
				КонецЕсли; 
			#КонецЕсли
		КонецЕсли; 
		Возврат Рез;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		ДопПараметры.Вставить("ПересчитатьЦену", Ложь); // Так как мы будем получать цены самостоятельно!
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		//А вот цену мы получим актуальную
		Попытка
			ВариантПоставки = Новый Структура;
			ВариантПоставки.Вставить("Поставщик",            ТекСтрока.Поставщик);
			ВариантПоставки.Вставить("Закупка",              Ложь);
			ВариантПоставки.Вставить("КлючСтрокиПоставщика", ТекСтрока.КлючСтрокиПоставщика);
			ВариантПоставки.Вставить("СрокПоставки",         ТекСтрока.СрокПоставкиВСтроке);
			ВариантПоставки.Вставить("СтавкаНДС",            ТекСтрока.СтавкаНДС);
			#Если Клиент Тогда
				ВариантПоставки.Вставить("ИнтерактивныйВвод", Истина);
			#Иначе
				ВариантПоставки.Вставить("ИнтерактивныйВвод", Ложь);
			#КонецЕсли
			
			ТекСтрока.Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, ТекущаяДата(),, ВалютаДокумента, КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании,, ВариантПоставки);
			Рез = ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
			
			ТекСтрока.Поставщик            = ВариантПоставки.Поставщик;
			ТекСтрока.КлючСтрокиПоставщика = ВариантПоставки.КлючСтрокиПоставщика;
			ТекСтрока.СрокПоставкиВСтроке  = ВариантПоставки.СрокПоставки;
		Исключение
		КонецПопытки;
		
		// БИЗТЕХ КОВАЛЬ 08.07.2025 ++ 
		// Склад подставлять по складу по подразделению
		
		Попытка
			Если обЗначениеНеЗаполнено(ТекСтрока.СкладКомпании) Тогда
				ТекСтрока.СкладКомпании = ?(ПолучитьСкладПоПодразделению()=Неопределено,обПраво("ОсновнойСкладКомпании",Права),ПолучитьСкладПоПодразделению());
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				ЭтаФорма.ОбновитьПодборЗамен();
			КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ПроцентСкидки" Тогда
		Если ТекСтрока.ПроцентСкидки > 0 И ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный Тогда
			ТекСтрока.ПроцентСкидки = 0;
		КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,"Товары.ПроцентСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.СкидкаНаТовар" Тогда
		Если ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный Тогда
			ТекСтрока.СкидкаНаТовар       = Справочники.ТипыСкидок.ПустаяСсылка();
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "Товары.ПроцентСкидкиСтроки" Тогда
		Если ТекСтрока.ПроцентСкидкиСтроки > 0 И ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный Тогда
			ТекСтрока.ПроцентСкидкиСтроки = 0;
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "РАБОТЫ"
	ИначеЕсли Имя="Работы.Работа" Тогда
		
		// Проверим вид использования автоработы
		Если обПолучитьЗначениеСвойства(ТекСтрока.Работа, "ВидИспользованияАвтоработы") = Перечисления.ВидыИспользованияАвторабот.Планирование Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить работы с видом использования ""Планирование""",5);
			#КонецЕсли
			ТекСтрока.Работа = Неопределено;
		КонецЕсли;
		
		Если (Не СкидкаНаценкаРаботы.Пустая()) И (СкидкаНаценкаРаботы.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная) Тогда
			ТекСтрока.ПроцентСкидки=ЗначениеСкидкиНаценкиРабот; 	
		Иначе	
			ТекСтрока.ПроцентСкидки=0;
		КонецЕсли; 
		// Сбросим значения скидки строки, если таковые имеются.
		ТекСтрока.СкидкаНаТовар=Справочники.ТипыСкидок.ПустаяСсылка(); 
		ТекСтрока.ПроцентСкидкиСтроки=0;
		ТекСтрока.СуммаСкидкиСтроки=0;
		//Проверим на повторение строки в таблице
		//Получим из формы последнюю текущую работу
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1; КонецЕсли; 
		//Перерассчитаем цену текущей работы
		ОбработкаРеквизита("РасчетЦеныРаботы",ТекСтрока,ЭтаФорма,ДопПараметры);
		//Заполнение ставок налогов
		Если ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС ИЛИ ЭтотОбъект.ВидРемонта.ОсвобожденОтНДСРаботы Тогда
			ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
		Иначе
			ТекСтрока.СтавкаНДС=ТекСтрока.Работа.Номенклатура.СтавкаНДС;
		КонецЕсли; 
		//Расчет сумм по строке
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Количество" Тогда
		Возврат ОбработкаРеквизита("Работы.Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Нормочас" Тогда
		ТекСтрока.Цена=обПересчет(ТекСтрока.Нормочас.Цена,ТекСтрока.Нормочас.Валюта,Дата,ВалютаДокумента,КурсДокумента);
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Коэффициент" Тогда
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Возврат ОбработкаРеквизита("РассчитатьСкидкиРабот",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество*ТекСтрока.Коэффициент=0,0,ТекСтрока.Сумма/(ТекСтрока.Количество*ТекСтрока.Коэффициент));
		Возврат ОбработкаРеквизита("РассчитатьСкидкиРабот",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.ПроцентСкидки" Тогда
		Рез=Истина;
		// Скидка не может быть выше 100%
		Если ТекСтрока.ПроцентСкидки>100 Тогда
			ТекСтрока.ПроцентСкидки = 100;	
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.СкидкаНаТовар) И ТекСтрока.СкидкаНаТовар.ФлагВытеснения Тогда
			ТекСтрока.ПроцентСкидки = 0;
		КонецЕсли;
		Если ТекСтрока.ПроцентСкидки > 0 И ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный Тогда
			ТекСтрока.ПроцентСкидки = 0;
		КонецЕсли;
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
		ТекСтрока_СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
		СуммаСкидки=Окр(ТекСтрока.ПроцентСкидки*ТекСтрока_СуммаБезСкидки/100,2);
		СуммаСкидки=дкОкруглитьСуммуСкидки(СуммаСкидки,СкидкаНаценкаРаботы);
		Если СуммаСкидки<>ТекСтрока.СуммаСкидки Тогда // Сумма скидки изменилась
			ТекСтрока.СуммаСкидки=СуммаСкидки;
		КонецЕсли; 
		ДопПараметры.Вставить("ПересчитыватьПроцентСкидки",Ложь);
		Рез=ОбработкаРеквизита("Работы.СуммаСкидки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Работы.СуммаСкидки" Тогда
		Попытка	// Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцентСкидки=ДопПараметры.ПересчитыватьПроцентСкидки;
		Исключение 
			ПересчитыватьПроцентСкидки=Истина;	
		КонецПопытки;
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.СкидкаНаТовар) И ТекСтрока.СкидкаНаТовар.ФлагВытеснения Тогда
			ТекСтрока.СуммаСкидки = 0;
		КонецЕсли;
		Если ПересчитыватьПроцентСкидки Тогда
			Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
			ТекСтрока_СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
			ТекСтрока.ПроцентСкидки=?(ТекСтрока_СуммаБезСкидки=0,0,Окр(ТекСтрока.СуммаСкидки*100/ТекСтрока_СуммаБезСкидки,2));
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиРабот=Работы.Итог("СуммаСкидки");
		СуммаСкидкиНаценкиРабот=СуммаСкидкиНаценкиРабот+Работы.Итог("СуммаСкидкиСтроки");
		// Ничего делать не будем - весь расчет от НДС зависит (включено или нет)
		Возврат ОбработкаРеквизита("Работы.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.СкидкаНаТовар" Тогда
		ОбработкаРасчетСкидокАвторабот.ПодобратьСтрочнуюСкидку(ЭтотОбъект,ТекСтрока);
		ТекСтрока.СкидкаНаТовар       = ОбработкаРасчетСкидокАвторабот.ТекущаяСкидкаСтроки;
		ТекСтрока.ПроцентСкидкиСтроки = ОбработкаРасчетСкидокАвторабот.ТекущийПроцентСкидкиСтроки;
		ТекСтрока.СуммаСкидкиСтроки   = ОбработкаРасчетСкидокАвторабот.ТекущаяСуммаСкидкиСтроки;
		Если ЗначениеЗаполнено(ТекСтрока.СкидкаНаТовар) И ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный Тогда
			ТекСтрока.СкидкаНаТовар       = Справочники.ТипыСкидок.ПустаяСсылка();
			ТекСтрока.ПроцентСкидкиСтроки = 0;
			ТекСтрока.СуммаСкидкиСтроки   = 0;
			СуммаСкидкиНаценкиРабот       = 0;
		Иначе
			// Пересчитываем общую сумму скидок документа.
			СуммаИтого              = Работы.Итог("СуммаСкидки");
			СуммаИтого              = СуммаИтого+Работы.Итог("СуммаСкидкиСтроки");
			СуммаСкидкиНаценкиРабот = СуммаИтого;
		КонецЕсли;
		// Пересчитываем общую сумму скидок документа.
		СуммаИтого=Работы.Итог("СуммаСкидки");
		СуммаИтого=СуммаИтого+Работы.Итог("СуммаСкидкиСтроки");
		СуммаСкидкиНаценкиРабот=СуммаИтого;
		Возврат ЭтотОбъект.ОбработкаРеквизита("Работы.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);			
		
	ИначеЕсли Имя="Работы.ПроцентСкидкиСтроки" Тогда
		// Если скидка на работу (строки) не выбрана, а пользователь пытается установить значение процента, 
		// то не дадим ему это сделать.
		Если ТекСтрока.СкидкаНаТовар.Пустая() Тогда
			ТекСтрока.ПроцентСкидкиСтроки = 0;
		КонецЕсли;
		// Скидка не может быть выше 100%
		Если ТекСтрока.ПроцентСкидкиСтроки>100 Тогда
			ТекСтрока.ПроцентСкидкиСтроки = 100;
		КонецЕсли;
		Если ТекСтрока.ПроцентСкидкиСтроки > 0 И ВидРемонта.ТипРемонта = Перечисления.ТипыРемонта.Бесплатный Тогда
			ТекСтрока.ПроцентСкидкиСтроки = 0;
		КонецЕсли;
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
		ТекСтрока_СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
		ТекСтрока.СуммаСкидкиСтроки=Окр(ТекСтрока_СуммаБезСкидки*ТекСтрока.ПроцентСкидкиСтроки/100,2);
		ТекСтрока.СуммаСкидкиСтроки=дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидкиСтроки,ТекСтрока.СкидкаНаТовар);
		// Запрещаем обратный пересчет процента.
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ПересчитыватьПроцентСкидкиСтроки",Ложь);
		Возврат ЭтотОбъект.ОбработкаРеквизита("Работы.СуммаСкидкиСтроки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.СуммаСкидкиСтроки" Тогда
		// Если скидка на работу (строки) не выбрана, а пользователь пытается установить сумму скидки,
		// то не дадим ему это сделать.
		Если ТекСтрока.СкидкаНаТовар.Пустая() Тогда
			ТекСтрока.СуммаСкидкиСтроки = 0;
		КонецЕсли;
		// Проверим дополнительные параметры, возможно пересчитывать процент не нужно.
		Попытка // Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцентСкидкиСтроки=ДопПараметры.ПересчитыватьПроцентСкидкиСтроки;
		Исключение 
			ПересчитыватьПроцентСкидкиСтроки=Истина;
		КонецПопытки;
		Если ПересчитыватьПроцентСкидкиСтроки Тогда
			Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
			ТекСтрока_СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
			ТекСтрока.ПроцентСкидкиСтроки=?(ТекСтрока_СуммаБезСкидки=0,0,Окр(ТекСтрока.СуммаСкидкиСтроки*100/ТекСтрока_СуммаБезСкидки,2));
		КонецЕсли;			
		// Выполним расчет итоговой суммы скидки на документ.
		// Вычисляем итоговое значение суммы скидки (скидка шапки + скидки строк).
		// Имя реквизита, в котором должна хранится итоговая сумма скидки, 
		// а также имя табличной части, по которой необходимо вычислить итоговую сумму скидки находятся в реквизитах
		// обработки.
		СуммаИтого=Работы.Итог("СуммаСкидки");
		СуммаИтого=СуммаИтого+Работы.Итог("СуммаСкидкиСтроки");
		СуммаСкидкиНаценкиРабот=СуммаИтого;
		Возврат ОбработкаРеквизита("Работы.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.СтавкаНДС" Тогда
		СуммаСкидки = ТекСтрока.СуммаСкидки;
		СуммаСкидки = СуммаСкидки + ТекСтрока.СуммаСкидкиСтроки;
		ЦенаВключаетНДС = ЭтотОбъект.ТипЦенРабот.Пустая() ИЛИ ЭтотОбъект.ТипЦенРабот.ЦенаВключаетНДС;
		СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
		Сумма = ТекСтрока.Сумма;
		
		Если НЕ ЦенаВключаетНДС Тогда
			Сумма = Сумма + Окр(Сумма*СтавкаНДС/100,2);
		КонецЕсли;
		
		СуммаСоСкидкой = Сумма - СуммаСкидки;
		
		ТекСтрока.СуммаНДС=Окр((СуммаСоСкидкой*СтавкаНДС)/(100+СтавкаНДС),2);
		ТекСтрока.СуммаВсего=СуммаСоСкидкой;
		Возврат Истина;
		
	ИначеЕсли Имя="Работы.СуммаНДС" Тогда
		//СуммаСкидки=ТекСтрока.СуммаСкидки+ТекСтрока.СуммаСкидкиСтроки;
		//СуммаСкидки=дкОкруглитьСуммуСкидки(СуммаСкидки,СкидкаНаценкаРаботы);
		//СуммаВсегоБезСкидки=ТекСтрока.Сумма-СуммаСкидки; // База для расчета НДС
		//// Определим каким образом у нас вычисляется НДС
		//ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		//Если ЦенаВключаетНДС Тогда
		//	ТекСтрока.СуммаВсего=СуммаВсегоБезСкидки;
		//Иначе
		//	ТекСтрока.СуммаВсего=СуммаВсегоБезСкидки+ТекСтрока.СуммаНДС;
		//КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Работы.СуммаВсего" Тогда
		// Определяем какая скидка установлена для строки.
		Если Не ТекСтрока.СкидкаНаТовар.Пустая() Тогда
			СкидкаСтрокиАбсолютная=(ТекСтрока.СкидкаНаТовар.СпособВычисления=Перечисления.СкидкиСпособВычисления.Абсолютная);
			Если СкидкаСтрокиАбсолютная Тогда
				ЗначениеСкидкиСтроки=ТекСтрока.СуммаСкидкиСтроки;	
			Иначе
				ЗначениеСкидкиСтроки=ТекСтрока.ПроцентСкидкиСтроки;
			КонецЕсли;
		Иначе
			СкидкаСтрокиАбсолютная=Истина;
			ЗначениеСкидкиСтроки=0;
		КонецЕсли;
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦенРабот) ИЛИ ЭтотОбъект.ТипЦенРабот.ЦенаВключаетНДС);
		
		// Определим, есть ли скидки.
		// Сначала шапочная скидка.
		Попытка   			
			ПроцентСкидки = ТекСтрока.ПроцентСкидки;
		Исключение 
			ПроцентСкидки = 0;
		КонецПопытки;
		
		// Теперь определяем какая скидка установлена для строки.
		СкидкаСтрокиАбсолютная = Истина;
		ЗначениеСкидкиСтроки = 0;
		Попытка
			Если Не ТекСтрока.СкидкаНаТовар.Пустая() Тогда
				СкидкаСтрокиАбсолютная = (ТекСтрока.СкидкаНаТовар.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная);
				Если СкидкаСтрокиАбсолютная Тогда
					ЗначениеСкидкиСтроки = ТекСтрока.СуммаСкидкиСтроки;
				Иначе
					ЗначениеСкидкиСтроки = ТекСтрока.ПроцентСкидкиСтроки;
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		//// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если ЦенаВКлючаетНДС Тогда
			Если СкидкаСтрокиАбсолютная Тогда
				СуммаДляРасчета    = ТекСтрока.СуммаВсего + ЗначениеСкидкиСтроки;
				ПроцентСкидкиВсего = ПроцентСкидки;
			Иначе
				СуммаДляРасчета = ТекСтрока.СуммаВсего;
				ПроцентСкидкиВсего = ПроцентСкидки + ЗначениеСкидкиСтроки;
			КонецЕсли;
		Иначе
			ВремСуммаНДС = Окр((ТекСтрока.СуммаВсего * ТекСтрока.СтавкаНДС.Ставка)/(100 + ТекСтрока.СтавкаНДС.Ставка), 2);
			Если СкидкаСтрокиАбсолютная Тогда
				СуммаДляРасчета    = ТекСтрока.СуммаВсего - ВремСуммаНДС + ЗначениеСкидкиСтроки;
				ПроцентСкидкиВсего = ПроцентСкидки;
			Иначе
				СуммаДляРасчета = ТекСтрока.СуммаВсего - ВремСуммаНДС;
				ПроцентСкидкиВсего = ПроцентСкидки + ЗначениеСкидкиСтроки;
			КонецЕсли;
		КонецЕсли;
		
		Если ПроцентСкидкиВсего >= 100 Тогда
			ТекСтрока.СуммаВсего = 0;
			ТекСтрока.Сумма = СуммаДляРасчета;
		Иначе
			ТекСтрока.Сумма = СуммаДляРасчета*100/(100 - ПроцентСкидкиВсего)
		КонецЕсли;
		
		// Пересчитываем цену.
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,?(ТекСтрока.Коэффициент=0,(ТекСтрока.Сумма/ТекСтрока.Количество),(ТекСтрока.Сумма/(ТекСтрока.Количество*ТекСтрока.Коэффициент))));
		// Вычисляем новую сумму "шапочной" скидки приходящейся на текущую строку.
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
		СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
		ТекСтрока.СуммаСкидки=Окр(СуммаБезСкидки*ТекСтрока.ПроцентСкидки/100,2);
		ТекСтрока.СуммаСкидки=дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценкаРаботы);
		// Вычисляем товарную скидку.		
		Если СкидкаСтрокиАбсолютная Тогда
			ТекСтрока.ПроцентСкидкиСтроки=?(СуммаБезСкидки=0,0,Окр(ТекСтрока.СуммаСкидкиСтроки*100/СуммаБезСкидки,2));
		Иначе
			ТекСтрока.СуммаСкидкиСтроки=Окр(СуммаБезСкидки*ТекСтрока.ПроцентСкидкиСтроки/100,2);
			ТекСтрока.СуммаСкидкиСтроки=дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидкиСтроки,ТекСтрока.СкидкаНаТовар);
		КонецЕсли;
		// Рассчитываем новую сумму НДС. 		
		СуммаВсегоБезСкидки=СуммаБезСкидки-ТекСтрока.СуммаСкидки-ТекСтрока.СуммаСкидкиСтроки;
		//Если ЦенаВключаетНДС Тогда			
		ТекСтрока.СуммаНДС=Окр((СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		//Иначе
		//	ТекСтрока.СуммаНДС=Окр(СуммаВсегоБезСкидки*ТекСтрока.СтавкаНДС.Ставка/100,2);
		//КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаИтого=Работы.Итог("СуммаСкидки");
		СуммаИтого=СуммаИтого+Работы.Итог("СуммаСкидкиСтроки");
		СуммаСкидкиНаценкиРабот=СуммаИтого;
		Возврат Рез;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="МатериалыЗаказчика.Номенклатура" Тогда
		Если НЕ ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ТабличнаяЧастьТовары", МатериалыЗаказчика);
		ДопПараметры.Вставить("ИмяТаблицы", "МатериалыЗаказчика");
		Рез=дкОбработкаРеквизита(ЭтотОбъект,"Товары.Номенклатура",ТекСтрока, ЭтаФорма, ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда
			Возврат Рез;
		КонецЕсли; //Это если это был набор и мы удалили эту строку
		Возврат Рез;
		
	ИначеЕсли Имя="РасчетЦеныРаботы" Тогда
		//Расчет цены работы
		Если обЗначениеНеЗаполнено(ТекСтрока.Работа) Тогда
			Возврат Ложь;
		КонецЕсли;
		//Если работа задана - получим ее цену согласно модели автомобиля (если автомобиль выбран)
		Если ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") И ЗначениеЗаполнено(Автомобиль) Тогда
			Если ЗначениеЗаполнено(Автомобиль.ВариантКомплектации) Тогда
				Модель = Автомобиль.ВариантКомплектации;
			Иначе
				Модель = Автомобиль.Модель;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Модели") Тогда
			Если ЗначениеЗаполнено(ВариантКомплектации) Тогда
				Модель = ВариантКомплектации;
			Иначе
				Модель = Автомобиль;
			КонецЕсли;
		Иначе
			Модель = Справочники.Модели.ПустаяСсылка();
		КонецЕсли; 
		ЦенаРаботы=орПолучитьЦенуАвтоработы(ТипЦенРабот, ТекСтрока.Работа,Модель,ДоговорВзаиморасчетов,Цех,ВидРемонта,,ВалютаДокумента,КурсДокумента);
		ТекСтрока.Нормочас=ЦенаРаботы.Нормочас;
		ТекСтрока.Коэффициент=?(обЗначениеНеЗаполнено(ЦенаРаботы.НормаВремени),ТекСтрока.Коэффициент,ЦенаРаботы.НормаВремени);
		ТекСтрока.Цена=ЦенаРаботы.Цена;
		Возврат Истина;
		
	ИначеЕсли Имя="РассчитатьСкидкиРабот" Тогда	
		Рез=Истина;
		Попытка	
			НеРассчитыватьСкидки = ДопПараметры.НеРассчитыватьСкидки;
		Исключение 
			НеРассчитыватьСкидки = Ложь;	
		КонецПопытки;
		
		Попытка
			флБлокироватьПерерасчетСкидок = ЭтотОбъект.БлокироватьПерерасчетСкидок;
		Исключение
			флБлокироватьПерерасчетСкидок = Ложь;
		КонецПопытки;
		
		ОбработкаРасчетСкидокАвторабот.Дата = Дата;	
		ОбработкаРасчетСкидокАвторабот.ПодразделениеКомпании = ПодразделениеКомпании;				
		ОбработкаРасчетСкидокАвторабот.Карточка = Карточка;				
		ОбработкаРасчетСкидокАвторабот.СкидкаНаценка = СкидкаНаценкаРаботы;
		ОбработкаРасчетСкидокАвторабот.СкладКомпании = Цех.СкладКомпании;				
		// Подбираем подходящую скидку. Если скидка не изменилась, то функция вернет Ложь.
		Если флБлокироватьПерерасчетСкидок Тогда
			Попытка
				Если обЗначениеНеЗаполнено(ЭтотОбъект[ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаСкидкаНаценка]) Тогда
					Для Каждого стрДок Из ЭтотОбъект[ОбработкаРасчетСкидокАвторабот.ИмяТабличнойЧасти] Цикл
						стрДок.ПроцентСкидки = 0;
						ЭтотОбъект.ОбработкаРеквизита(ОбработкаРасчетСкидокАвторабот.ИмяТабличнойЧасти + ".ПроцентСкидки", стрДок, ЭтаФорма);
					КонецЦикла;
				КонецЕсли;
			Исключение
			КонецПопытки;
			
			Если ТекСтрока = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ЭтотОбъект.ОбработкаРеквизита(ОбработкаРасчетСкидокАвторабот.ИмяТабличнойЧасти + ".ПроцентСкидки", ТекСтрока, ЭтаФорма);
			Попытка // на случай если в документе нет скидки строки
				ЭтотОбъект.ОбработкаРеквизита(ОбработкаРасчетСкидокАвторабот.ИмяТабличнойЧасти + ".ПроцентСкидкиСтроки", ТекСтрока, ЭтаФорма);
			Исключение
			КонецПопытки;
			Возврат Истина;
		ИначеЕсли (Не НеРассчитыватьСкидки) И ОбработкаРасчетСкидокАвторабот.ПодобратьСкидку(ЭтотОбъект) Тогда
			СкидкаНаценкаРаботы=ОбработкаРасчетСкидокАвторабот.ТекущаяСкидкаНаценка;
			ЗначениеСкидкиНаценкиРабот=ОбработкаРасчетСкидокАвторабот.ТекущееЗначениеСкидки;
			СуммаСкидкиНаценкиРабот=ОбработкаРасчетСкидокАвторабот.ТекущаяСуммаСкидки;
			// Подобралась другая "шапочная" скидка, значит, возможно, придется заблокировать работу с некоторыми колонками 
			// в зависимости от типа скидки.
			#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					дкУправлениеДоступностьюКолонокСкидок(ЭтаФорма,ОбработкаРасчетСкидокАвторабот.ТекущаяСкидкаНаценка,ОбработкаРасчетСкидокАвторабот.ИмяТабличнойЧасти);	
				КонецЕсли;
			#КонецЕсли
			// Применяем скидку к табличной части. Перерассчитываем строчные скидки.
			ОбработкаРасчетСкидокАвторабот.ПрименитьСкидку(ЭтотОбъект);
			СуммаИтого=Работы.Итог("СуммаСкидкиСтроки");
			СуммаСкидкиНаценкиРабот=СуммаСкидкиНаценкиРабот+СуммаИтого;
		Иначе // Просто пересчитываем значение суммы скидки для текущей строки.
			Если ТекСтрока<>Неопределено Тогда									
				// Подбираем скидку для текущей строки (скидка на автоработу).
				Если Не ОбработкаРасчетСкидокАвторабот.НеРассчитыватьСтрочныеСкидки Тогда
					Если НЕ НеРассчитыватьСкидки Тогда
						ОбработкаРасчетСкидокАвторабот.ПодобратьСтрочнуюСкидку(ЭтотОбъект,ТекСтрока);
						ТекСтрока.СкидкаНаТовар       = ОбработкаРасчетСкидокАвторабот.ТекущаяСкидкаСтроки;
						ТекСтрока.ПроцентСкидкиСтроки = ОбработкаРасчетСкидокАвторабот.ТекущийПроцентСкидкиСтроки;
						ТекСтрока.СуммаСкидкиСтроки   = ОбработкаРасчетСкидокАвторабот.ТекущаяСуммаСкидкиСтроки;
					КонецЕсли;
				КонецЕсли; 
				// Рассчитываем "шапочную" скидку для текущей строки. За одно рассчитаем общую сумму скидки на документ.
				Если СкидкаНаценкаРаботы.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					ОбработкаРеквизита("Работы.ПроцентСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
				Иначе
					ОбработкаРеквизита("Работы.СуммаСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			Иначе
				// Если произошла смена типа скидки, но значение скидки не изменилась, то пересчитывать ТЧ нет необходимости, но
				// сохранить новый тип скидки нужно.
				СкидкаНаценкаРаботы=ОбработкаРасчетСкидокАвторабот.ТекущаяСкидкаНаценка;
				СуммаИтого = 0;				
				СуммаИтого = Работы.Итог("СуммаСкидки");				
				СуммаИтого = СуммаИтого + Работы.Итог("СуммаСкидкиСтроки");
				СуммаСкидкиНаценкиРабот = СуммаИтого;					
			КонецЕсли;			
		КонецЕсли;
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				ВывестиПодвалСкидокРабот(ЭтаФорма);
			КонецЕсли;
		#КонецЕсли
		Возврат Рез;
		
		// Блок обработки изменения реквизитов табличной части "Планирование"
	ИначеЕсли Имя="Планирование.Работа" Тогда
		Если ТекСтрока = Неопределено ИЛИ обЗначениеНеЗаполнено(ТекСтрока.Авторабота) Тогда
			Возврат Истина;
		КонецЕсли;
		ЗначениеСвойства = обПолучитьЗначениеСвойства(ТекСтрока.Авторабота.ПолучитьОбъект(),ПланыВидовХарактеристик.СвойстваОбъектов.ВидИспользованияАвтоработы,Перечисления.ВидыИспользованияАвторабот.Производство);
		Если ЗначениеСвойства = Перечисления.ВидыИспользованияАвторабот.Производство Тогда
			#Если Клиент Тогда
				Сообщить("Внимание! Работа """ + Строка(ТекСтрока.Авторабота) +  """ не может использоваться при планировании!", СтатусСообщения.Важное);
			#КонецЕсли
			ТекСтрока.Авторабота = Справочники.Автоработы.ПустаяСсылка();
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Планирование.Исполнитель" Тогда
		Если ТекСтрока = Неопределено ИЛИ обЗначениеНеЗаполнено(ТекСтрока.Исполнитель) Тогда
			Возврат Истина;
		КонецЕсли;
		
		// заполнить рабочее место, если является исполнителем
		Если ТекСтрока.Исполнитель.Исполнитель Тогда
			ТекСтрока.РабочееМесто = ТекСтрока.Исполнитель.Цех;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекСтрока.Авторабота)
			И (ТекСтрока.Авторабота = Справочники.Автоработы.Выдача ИЛИ ТекСтрока.Авторабота = Справочники.Автоработы.Приемка)
			И ТекСтрока.Исполнитель.Должность = Справочники.Должности.Диспетчер 
			И обЗначениеНеЗаполнено(Диспетчер) Тогда
			Диспетчер = ТекСтрока.Исполнитель;			
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Планирование.НачалоВыполнения" Тогда
		Рез = Истина;
		Рез = ОбработкаРеквизита("Планирование.ОкончаниеВыполнения",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="Планирование.ОкончаниеВыполнения" Тогда
		Рез = Истина;
		Если ТекСтрока.НачалоВыполнения = '00010101' ИЛИ ТекСтрока.ОкончаниеВыполнения = '00010101' 
			ИЛИ ТекСтрока.НачалоВыполнения>ТекСтрока.ОкончаниеВыполнения Тогда
			ТекСтрока.Продолжительность = 0;
			Возврат Рез;
		КонецЕсли;
		ТекСтрока.Продолжительность = Окр((ТекСтрока.ОкончаниеВыполнения - ТекСтрока.НачалоВыполнения)/3600,2, РежимОкругления.Окр15как20);
		Возврат Рез;
		
	ИначеЕсли Имя = "СоздатьНапоминание" Тогда
		ДатаНапоминания=Неопределено;
		Если НЕ обЗначениеНеЗаполнено(ДатаОкончания) Тогда
			ДатаНапоминания=ДатаОкончания;
		КонецЕсли;
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры=Новый Структура;
		КонецЕсли; 
		Если ДатаНапоминания<>Неопределено Тогда
			ДопПараметры.Вставить("ДатаНачала",ДатаНапоминания);
			ДопПараметры.Вставить("РеальнаяДатаОповещения",ДатаНапоминания);
		КонецЕсли;
		Получатели=Новый СписокЗначений;
		Получатели.Добавить(Автор);
		ДопПараметры.Вставить("СписокПользователей", Получатели); 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе
		//ТекущийОбъект = дкПолучитьТекущийОбъект(ЭтотОбъект);
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,0);
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеРаботы=Новый Структура();
	ОбязательныеРаботы.Вставить("Работа",3);
	ОбязательныеРаботы.Вставить("ИдентификаторРаботы",1);
	ОбязательныеРаботы.Вставить("Количество",1);
	ОбязательныеРаботы.Вставить("Нормочас",1);
	ОбязательныеРаботы.Вставить("Коэффициент",1);
	ОбязательныеИсполнители=Новый Структура();
	ОбязательныеИсполнители.Вставить("ИдентификаторРаботы",3);
	ОбязательныеИсполнители.Вставить("Исполнитель",3);
	ОбязательныеПланирование=Новый Структура();
	ОбязательныеПланирование.Вставить("Авторабота",1);
	ОбязательныеПланирование.Вставить("РабочееМесто",2);
	ОбязательныеПланирование.Вставить("Исполнитель",2);
	ОбязательныеПланирование.Вставить("НачалоВыполнения",3);
	ОбязательныеПланирование.Вставить("ОкончаниеВыполнения",3);
	ОбязательныеПланирование.Вставить("Продолжительность",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ТипЦенРабот",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ВидРемонта",1);
	Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон") Тогда
		КомплектацияАвтомобиля=Справочники.ВидыРемонта.КомплектацияАвтомобиля;
		Если (КомплектацияАвтомобиля.Пустая()) ИЛИ (НЕ КомплектацияАвтомобиля.Предопределенный) Тогда
			КомплектацияАвтомобиля=Неопределено;
		КонецЕсли; 
	Иначе
		КомплектацияАвтомобиля=Неопределено;
	КонецЕсли; 
	Если ВидРемонта<>КомплектацияАвтомобиля Тогда
		ОбязательныеРеквизиты.Вставить("Заказчик",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Автомобиль",1);
	ОбязательныеРеквизиты.Вставить("Цех",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Работы",ОбязательныеРаботы);
	ОбязательныеРеквизиты.Вставить("Исполнители",ОбязательныеИсполнители);
	// KRAA ++
	ОбязательныеРеквизиты.Вставить("Планирование",ОбязательныеПланирование);
	// KRAA --
	Если Состояние = Перечисления.СостоянияЗаявкиНаРемонт.Отклонено Тогда
		ОбязательныеРеквизиты.Вставить("ПричинаОтказа", 1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("Цех",КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов",КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;	
	КонецЕсли;
	
	// KRAA++
	// Проверим заполненность дат планирования
	#Если Клиент Тогда
		//Для Каждого СтрокаПланирования Из Планирование Цикл 
		//	Если (СтрокаПланирования.НачалоВыполнения = '00010101') И (СтрокаПланирования.ОкончаниеВыполнения <> '00010101') Тогда
		//		дкДобавитьОшибку(Ошибки,"В строке " + Строка(СтрокаПланирования.НомерСтроки) + " таблицы ""Планирование"" не заполнена дата начала выполнения работ");	
		//		Результат = Ложь;	
		//	ИначеЕсли (СтрокаПланирования.НачалоВыполнения <> '00010101') И (СтрокаПланирования.ОкончаниеВыполнения = '00010101') Тогда
		//		дкДобавитьОшибку(Ошибки,"В строке " + Строка(СтрокаПланирования.НомерСтроки) + " таблицы ""Планирование"" не заполнена дата окончания выполнения работ");	
		//		Результат = Ложь;	
		//	КонецЕсли;
		//КонецЦикла;
		
		// Проверим наличие дублированных строк по реквизитам "РабочееМесто","НачалоВыполнения","ОкончаниеВыполнения"
		// при этом исключим из контроля строки, где эти реквизиты не заполнены
		
		// поищем строки удовлетворяющие структуре отбора
		//ТабличнаяЧасть = ЭтотОбъект.Планирование;
		//СписокНайденныхДублей = Новый СписокЗначений();
		//Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		//	// если пусто, то не проверяем уникальность
		//	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.РабочееМесто)
		//		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.НачалоВыполнения)
		//		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОкончаниеВыполнения) Тогда
		//		Продолжить;
		//	КонецЕсли;
		//	СтруктураОтбора = Новый Структура;
		//	СтруктураОтбора.Вставить("РабочееМесто",СтрокаТаблицы.РабочееМесто);
		//	СтруктураОтбора.Вставить("НачалоВыполнения",СтрокаТаблицы.НачалоВыполнения);
		//	СтруктураОтбора.Вставить("ОкончаниеВыполнения",СтрокаТаблицы.ОкончаниеВыполнения);
		//	НайденныеСтроки = ТабличнаяЧасть.НайтиСтроки(СтруктураОтбора);
		//	// если нашли и их больше 1, то строки не уникальные
		//	Если НайденныеСтроки.Количество() > 1 Тогда
		//		// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
		//		Результат = Ложь;
		//		ДублирующиесяСтроки = ""; 
		//		// выведем строку сообщения...
		//		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		//			ДублирующиесяСтроки = ДублирующиесяСтроки + "," + СокрЛП(НайденнаяСтрока.НомерСтроки);
		//			// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
		//			СписокНайденныхДублей.Добавить(НайденнаяСтрока);
		//		КонецЦикла;
		//		СтрокаРеквизитов = "";
		//		Для каждого РеквизитТаблицы Из СтруктураОтбора Цикл
		//			СтрокаРеквизитов = СтрокаРеквизитов + ?(ПустаяСтрока(СтрокаРеквизитов),"",",") + РеквизитТаблицы.Ключ;
		//		КонецЦикла;
		//		дкДобавитьОшибку(Ошибки,"Таблица ""Планирование""" + " значения колон" + ?(СтруктураОтбора.Количество() > 1,"ок","ки") + " < " + СтрокаРеквизитов + " > не уникальны ! Строки: " + Сред(ДублирующиесяСтроки,2));
		//	КонецЕсли;
		//КонецЦикла;
		
	#КонецЕсли
	// KRAA--
	
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	// KRAA ++ определим какая хоз. операция должна быть по умолчанию
	ИспользоватьПланирование = обПраво("ПланированиеПриОформленииЗаявкиНаРемонт");
	// KRAA --
	
	СписокХозОпераций = Новый СписокЗначений;
	// KRAA ++
	СписокХозОпераций.Добавить("ЗаявкаНаРемонт","Заявка на ремонт", НЕ ИспользоватьПланирование);
	СписокХозОпераций.Добавить("ПланРемонта","План ремонта",ИспользоватьПланирование);
	// KRAA --
	СписокХозОпераций.Добавить("Калькуляция","Калькуляция", Ложь);
	
	Возврат СписокХозОпераций;
КонецФункции

// Процедура формирования отчета "РекомендацииПоАвтомобилям"
//
Процедура ОтчетРекомендацииПоАвтомобилям(ДатаНачала = Неопределено, ДатаКонца=Неопределено) Экспорт
	
	Если Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли; 
	
	#Если Клиент Тогда
		
		Отчет = Отчеты.РекомендацииПоАвтомобилям.Создать();
		Отчет.ЗаполнитьНачальныеНастройки();
		
		МетаданныеОтчета  = Отчет.Метаданные();
		ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.ИсторияРекомендаций;
		НастройкиВарианта = ВариантОтчета.Настройки;
		
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(?(ДатаКонца=Неопределено,КонецМесяца(ТекущаяДата()),ДатаКонца)));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",  НачалоДня(?(ДатаНачала=Неопределено,НачалоМесяца(ТекущаяДата()),ДатаНачала)));
		
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ТипЦен", обПраво("ОсновнойТипЦенПродажиАвтомобилей"));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ТипЦенРабот",обПраво("ОсновнойТипЦенРабот"));
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ВалютаОтчета", Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
		
		СтруктураОтчета = НастройкиВарианта.Структура;
		СтруктураОтчета.Очистить();
		
		ТаблицаОтчета = СтруктураОтчета.Добавить(Тип("ТаблицаКомпоновкиДанных"));
		
		//добавляем измерения строки
		ГруппировкаТиповРекомендации = ОтчетыСервер.СКД_ДобавитьГруппировку(ТаблицаОтчета.Строки, "ТипРекомендации");
		ГруппировкаСтатус    = ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаТиповРекомендации.Структура, "Статус");
		//ГруппировкаАвтомобиль     = ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаСтатус.Структура, "Автомобиль");
		ГруппировкаРекомендация     = ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаСтатус.Структура, "Рекомендация");
		
		ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Автомобиль", Ссылка.Автомобиль, ВидСравненияКомпоновкиДанных.Равно);
		
		
		ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
		СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
		СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
		СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
		СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
		СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
		
		ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
		
	#КонецЕсли
	
КонецПроцедуры


#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "БланкЗаявки"
	// Возвращает сформированный табличный документ:
Функция ПечатьБланкЗаявки(ТабДокумент) Экспорт
		Макет = ПолучитьМакет("БланкЗаявки");
		
		НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
		
		//Вывод шапки документа
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
		ОбластьМакета.Параметры.ДатаСоздания = Формат(Дата, "ДФ = дд.ММ.гггг");
		Если ТипЗнч(Заказчик)=Тип("СправочникСсылка.Контрагенты") Тогда
			ОбластьМакета.Параметры.Заказчик = Заказчик;
			ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Заказчик);
			ОбластьМакета.Параметры.ЗаказчикАдресПочтовый = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
			ОбластьМакета.Параметры.ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
		Иначе
			ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = СокрЛП(Заказчик);
			//ОбластьМакета.Параметры.ЗаказчикАдресПочтовый     = КонтактнаяИнформация;
			ОбластьМакета.Параметры.ЗаказчикТелефоны           = ПредставлениеТелефона;
		КонецЕсли; 
		Если ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
			МодельАвтомобиля =  Автомобиль.модель;
			ОбластьМакета.Параметры.Модель = Автомобиль;
			ОбластьМакета.Параметры.АвтомобильМодель = Автомобиль.Наименование;
		ИначеЕсли ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Модели") Тогда
			МодельАвтомобиля =  Автомобиль;
			ОбластьМакета.Параметры.Модель = Автомобиль;
			ОбластьМакета.Параметры.АвтомобильМодель = Автомобиль.НаименованиеПолное;
		КонецЕсли; 
		
		Если НЕ обЗначениеНеЗаполнено(МодельАвтомобиля) Тогда
			СхемаТС = орПолучитьСхемуТС(МодельАвтомобиля,"СхемаТС");
			Если СхемаТС <> Неопределено Тогда
				ОбластьМакета.Рисунки.СхемаТС.Картинка = СхемаТС;				
				ОбластьМакета.Рисунки.СхемаТС.РазмерКартинки = РазмерКартинки.Пропорционально;
			КонецЕсли;
		КонецЕсли; 
		
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
			Если ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
				ОбластьМакета.Параметры.АвтомобильГосНомер   = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,Дата);
				ОбластьМакета.Параметры.АвтомобильКод        = Автомобиль.VIN;
				ОбластьМакета.Параметры.АвтомобильГодВыпуска = Формат(Автомобиль.ГодВыпуска, ФорматПредставленияГодаВыпускаАвтомобиля);
				ОбластьМакета.Параметры.АвтомобильПробег     = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег,Дата));
			КонецЕсли; 
		КонецЕсли; 
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		//Вывод подвала документа
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Мастер = Мастер;
		ОбластьМакета.Параметры.МастерНаименование = Мастер.Наименование;
		ТабДокумент.Вывести(ОбластьМакета);
		
		ТабДокумент.Автомасштаб = Истина;
		Возврат ТабДокумент;
	КонецФункции

Функция ПечатьАктОсмотра(ТабДокумент) Экспорт
		
		Макет = ПолучитьМакет("АктОсмотра");
		
		НомерДляПечати = дкПолучитьНомерДляПечати(ЭтотОбъект);
		
		// Вывод шапки документа
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Организация = Организация;
		ОбластьМакета.Параметры.ФирмаПолноеНаименование = спПолучитьНаименование(Организация);
		ОбластьМакета.Параметры.ФирмаАдресЮридический = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.ФирмаАдресЮридический)) Тогда
			ОбластьМакета.Параметры.ФирмаАдресЮридический = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		КонецЕсли;
		ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
		Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.ФирмаТелефоны)) Тогда
			ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
		КонецЕсли;
		ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
		ОбластьМакета.Параметры.ДатаДок = Формат(ДатаНачала, "ДФ = дд.ММ.гггг");
		ОбластьМакета.Параметры.Заказчик = Заказчик;
		Если ТипЗнч(Заказчик) = Тип("СправочникСсылка.Контрагенты") Тогда
			ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Заказчик);
			ОбластьМакета.Параметры.ЗаказчикАдресПочтовый = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
			ОбластьМакета.Параметры.ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
		Иначе
			ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = СокрЛП(Заказчик);
			ОбластьМакета.Параметры.ЗаказчикТелефоны = ПредставлениеТелефона;
		КонецЕсли;
		ОбластьМакета.Параметры.Контрагент = Контрагент;
		ОбластьМакета.Параметры.КонтрагентПолноеНаименование = спПолучитьНаименование(Контрагент);;
		ОбластьМакета.Параметры.КонтрагентИНН = Контрагент.ИНН;
		ОбластьМакета.Параметры.КонтрагентАдресЮридический = киПолучитьПредставлениеКИ(Контрагент, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		ОбластьМакета.Параметры.КонтрагентТелефоны = киПолучитьПредставлениеКИ(Контрагент, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
		
		Если НЕ обЗначениеНеЗаполнено(Автомобиль) И ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автомобили") Тогда
			ОбластьМакета.Параметры.Автомобиль = Автомобиль;
			ОбластьМакета.Параметры.АвтомобильКод = Автомобиль.VIN;
			ОбластьМакета.Параметры.АвтомобильМодель = Автомобиль.Модель;
			ОбластьМакета.Параметры.АвтомобильГодВыпуска = Формат(Автомобиль.ГодВыпуска, ФорматПредставленияГодаВыпускаАвтомобиля);
			ОбластьМакета.Параметры.АвтомобильНомерДвигателя = Автомобиль.НомерДвигателя;
			ОбластьМакета.Параметры.АвтомобильНомерШасси = Автомобиль.НомерШасси;
			ОбластьМакета.Параметры.АвтомобильНомерКузова = Автомобиль.НомерКузова;
			ОбластьМакета.Параметры.АвтомобильЦвет = Автомобиль.Цвет;
			
			ОбластьМакета.Параметры.АвтомобильГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,ДатаНачала);
			ОбластьМакета.Параметры.АвтомобильТехПаспорт = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ТехПаспорт,ДатаНачала));
			//ОбластьМакета.Параметры.АвтомобильПробег = Формат(Пробег,"ЧЦ=10");
			
			СхемаТС = ОбластьМакета.Рисунки["СхемаТС"];
			
			// добавим общюю картинку
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КартинкиИФайлы.Данные
			|ИЗ
			|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
			|ГДЕ
			|	КартинкиИФайлы.Объект = &Объект
			|	И КартинкиИФайлы.Идентификатор = &Идентификатор";
			Запрос.УстановитьПараметр("Объект", Ссылка);
			Запрос.УстановитьПараметр("Идентификатор", "DirectoryPhoto");
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СхемаТС.Картинка       = Новый Картинка(Выборка.Данные.Получить(), Ложь);
				СхемаТС.РазмерКартинки = РазмерКартинки.Пропорционально;
			Иначе
				СхемаТСДанные = орПолучитьСхемуТС(Автомобиль.Модель,"СхемаТС");
				Если СхемаТСДанные <> Неопределено Тогда
					СхемаТС.Картинка = СхемаТСДанные;
					СхемаТС.РазмерКартинки = РазмерКартинки.Пропорционально;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Вывод подвала документа
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Заказчик = Заказчик;
		ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = ?(ТипЗнч(Заказчик) = Тип("Строка"), Заказчик, спПолучитьНаименование(Заказчик));
		ОбластьМакета.Параметры.Контрагент = Контрагент;
		ОбластьМакета.Параметры.КонтрагентПолноеНаименование = спПолучитьНаименование(Контрагент);
		ОбластьМакета.Параметры.Мастер = Мастер;
		ОбластьМакета.Параметры.МастерПолноеНаименование = Мастер.Наименование;
		ОбластьМакета.Параметры.ДатаДок = Формат(ДатаНачала, "ДФ = ""дд ММММ гггг 'г.'""");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ТабДокумент.Автомасштаб = Истина;
		Возврат ТабДокумент;
		
	КонецФункции

// Формирует печатную форму "РабочаяЗаявка"
	// Возвращает сформированный табличный документ:
Функция ПечатьРабочаяЗаявка(ТабДокумент) Экспорт
		ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
		Если ВалютаПечатногоДокумента=Неопределено Тогда Возврат Неопределено; КонецЕсли; 
		ТаблицаТоваров=Товары.Выгрузить();
		зфПерерасчетТаблицыТоваров(ТаблицаТоваров,ЭтотОбъект,ВалютаПечатногоДокумента);
		ТаблицаРабот=Работы.Выгрузить();
		зфПерерасчетТаблицыТоваров(ТаблицаРабот,ЭтотОбъект,ВалютаПечатногоДокумента,Истина);
		
		Макет = ПолучитьМакет("РабочаяЗаявка");
		
		//зададим параметры макета
		ТабДокумент.ПолеСверху = 10; //поле равно высоте колонтитулов
		ТабДокумент.ПолеСлева  = 0;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ТолькоПросмотр = Истина;
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		ЕстьСкидкаПоДеталям = Ложь;
		Если Товары.Итог("СуммаСкидки")<>0 ИЛИ Товары.Итог("СуммаСкидкиСтроки")<>0 Тогда
			ЕстьСкидкаПоДеталям = Истина;
		КонецЕсли;
		
		ЕстьСкидкаПоРаботам = Ложь;
		Если Работы.Итог("СуммаСкидки")<>0 ИЛИ Работы.Итог("СуммаСкидкиСтроки")<>0 Тогда
			ЕстьСкидкаПоРаботам = Истина;
		КонецЕсли;
		
		ОбластьСкидка = Макет.Область("Скидка");
		
		ОбластьУслуги = Макет.ПолучитьОбласть("Услуги");
		ОбластьДетали = Макет.ПолучитьОбласть("Детали");
		
		ОбластьСтрокаДеталей	        = Макет.ПолучитьОбласть("СтрокаДеталей");
		ОбластьИтоговПоСтраницеДетали	= Макет.ПолучитьОбласть("ИтогиПоСтраницеДеталей");
		ОбластьПодвалДеталей	        = Макет.ПолучитьОбласть("ПодвалДетали");
		
		ОбластьСтрокаУслуг		        = Макет.ПолучитьОбласть("СтрокаРабот");
		ОбластьИтоговПоСтраницеУслуги	= Макет.ПолучитьОбласть("ИтогиПоСтраницеРабот");
		ОбластьПодвалУслуг		        = Макет.ПолучитьОбласть("ПодвалУслуги");
		
		БезЛинии 	 = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
		// Настроем колонки вывода деталей если не было скидок
		Если Не ЕстьСкидкаПоДеталям Тогда
			ПоложениеШапкиДеталей = ОбластьДетали.Область("ШапкаДеталей").Низ;
			ПозицияКолонки = ОбластьСкидка.Лево;
			Пока ПозицияКолонки > 5 Цикл
				ОбластьДетали.Область(ПоложениеШапкиДеталей-1,ПозицияКолонки,ПоложениеШапкиДеталей-1,ПозицияКолонки).Текст = ОбластьДетали.Область(ПоложениеШапкиДеталей-1,ПозицияКолонки-1,ПоложениеШапкиДеталей-1,ПозицияКолонки-1).Текст;
				ОбластьДетали.Область(ПоложениеШапкиДеталей,ПозицияКолонки,ПоложениеШапкиДеталей,ПозицияКолонки).Текст = ОбластьДетали.Область(ПоложениеШапкиДеталей,ПозицияКолонки-1,ПоложениеШапкиДеталей,ПозицияКолонки-1).Текст;
				
				ОбластьИсточник = ОбластьСтрокаДеталей.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
				ОбластьПриемник = ОбластьСтрокаДеталей.Область(1,ПозицияКолонки,1,ПозицияКолонки);
				ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
				ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
				ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
				ОбластьИсточник = ОбластьИтоговПоСтраницеДетали.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
				ОбластьПриемник = ОбластьИтоговПоСтраницеДетали.Область(1,ПозицияКолонки,1,ПозицияКолонки);
				ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
				ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
				ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
				ОбластьИсточник = ОбластьПодвалДеталей.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
				ОбластьПриемник = ОбластьПодвалДеталей.Область(1,ПозицияКолонки,1,ПозицияКолонки);
				ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
				ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
				ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
				ПозицияКолонки = ПозицияКолонки-1;
			КонецЦикла;
			
			ОбластьДетали.Область(ПоложениеШапкиДеталей-1, 4, ПоложениеШапкиДеталей-1, 5).Объединить();
			ОбластьДетали.Область(ПоложениеШапкиДеталей, 4, ПоложениеШапкиДеталей, 5).Объединить();
			ОбластьСтрокаДеталей.Область(1, 4, 1, 5).Объединить();
			
			ТекОбласть = ОбластьИтоговПоСтраницеДетали.Область(1, 2, 1, 8);
			ТекОбласть.Объединить();
			ТекОбласть.ГраницаСнизу = БезЛинии;
			
			ТекОбласть = ОбластьПодвалДеталей.Область(1, 2, 1, 8);
			ТекОбласть.Объединить();
			ТекОбласть.ГраницаСнизу = БезЛинии;
			
			ПозицияКолонки = ОбластьСкидка.Лево-1;
			Пока ПозицияКолонки <= ОбластьДетали.ШиринаТаблицы Цикл
				ОбластьДетали.Область(ПоложениеШапкиДеталей,ПозицияКолонки,ПоложениеШапкиДеталей,ПозицияКолонки).Текст = ПозицияКолонки-2;
				ПозицияКолонки = ПозицияКолонки + 1;
			КонецЦикла;
		КонецЕсли;
		
		ОбластьШапкаДеталей = ОбластьДетали.ПолучитьОбласть("ШапкаДеталей");
		
		// Настроем колонки вывода услуг если не было скидок
		Если Не ЕстьСкидкаПоРаботам Тогда
			ПоложениеШапкиУслуг = ОбластьУслуги.Область("ШапкаУслуг").Низ;
			ПозицияКолонки = ОбластьСкидка.Лево;
			Пока ПозицияКолонки > 5 Цикл
				ОбластьУслуги.Область(ПоложениеШапкиУслуг-1,ПозицияКолонки,ПоложениеШапкиУслуг-1,ПозицияКолонки).Текст = ОбластьУслуги.Область(ПоложениеШапкиУслуг-1,ПозицияКолонки-1,ПоложениеШапкиУслуг-1,ПозицияКолонки-1).Текст;
				ОбластьУслуги.Область(ПоложениеШапкиУслуг,ПозицияКолонки,ПоложениеШапкиУслуг,ПозицияКолонки).Текст = ОбластьУслуги.Область(ПоложениеШапкиУслуг,ПозицияКолонки-1,ПоложениеШапкиУслуг,ПозицияКолонки-1).Текст;
				
				ОбластьИсточник = ОбластьСтрокаУслуг.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
				ОбластьПриемник = ОбластьСтрокаУслуг.Область(1,ПозицияКолонки,1,ПозицияКолонки);
				ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
				ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
				ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
				ОбластьИсточник = ОбластьИтоговПоСтраницеУслуги.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
				ОбластьПриемник = ОбластьИтоговПоСтраницеУслуги.Область(1,ПозицияКолонки,1,ПозицияКолонки);
				ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
				ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
				ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
				ОбластьИсточник = ОбластьПодвалУслуг.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
				ОбластьПриемник = ОбластьПодвалУслуг.Область(1,ПозицияКолонки,1,ПозицияКолонки);
				ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
				ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
				ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
				ПозицияКолонки = ПозицияКолонки-1;
			КонецЦикла;
			
			ОбластьУслуги.Область(ПоложениеШапкиУслуг-1, 4, ПоложениеШапкиУслуг-1, 5).Объединить();
			ОбластьУслуги.Область(ПоложениеШапкиУслуг, 4, ПоложениеШапкиУслуг, 5).Объединить();
			ОбластьСтрокаУслуг.Область(1, 4, 1, 5).Объединить();
			
			ТекОбласть = ОбластьИтоговПоСтраницеУслуги.Область(1, 2, 1, 8);
			ТекОбласть.Объединить();
			ТекОбласть.ГраницаСнизу = БезЛинии;
			
			ТекОбласть = ОбластьПодвалУслуг.Область(1, 2, 1, 8);
			ТекОбласть.Объединить();
			ТекОбласть.ГраницаСнизу = БезЛинии;
			
			ПозицияКолонки = ОбластьСкидка.Лево-1;
			Пока ПозицияКолонки <= ОбластьУслуги.ШиринаТаблицы Цикл
				ОбластьУслуги.Область(ПоложениеШапкиУслуг,ПозицияКолонки,ПоложениеШапкиУслуг,ПозицияКолонки).Текст = ПозицияКолонки-2;
				ПозицияКолонки = ПозицияКолонки + 1;
			КонецЦикла;
		КонецЕсли;
		
		ОбластьШапкаУслуг = ОбластьУслуги.ПолучитьОбласть("ШапкаУслуг");
		
		ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
		ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
		ВыводитьПроизводителя = обПраво("ВыводитьПроизводителяВПечатныхФормах",Права,,ЭтотОбъект);
		
		НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
		
		//Вывод шапки документа
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Организация = Организация;
		ОбластьМакета.Параметры.ОрганизацияПолноеНаименование = спПолучитьНаименование(Организация);
		ОбластьМакета.Параметры.ОрганизацияАдресЮридический = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
		ОбластьМакета.Параметры.ДатаДок = Формат(Дата, "ДФ = дд.ММ.гггг");
		ОбластьМакета.Параметры.ДатаНачала = Формат(ДатаНачала, "ДФ = дд.ММ.гггг");
		Если ТипЗнч(Заказчик)=Тип("СправочникСсылка.Контрагенты") Тогда
			ОбластьМакета.Параметры.Заказчик = Заказчик;
			ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Заказчик);
			ОбластьМакета.Параметры.ЗаказчикАдресПочтовый = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
			ОбластьМакета.Параметры.ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
		Иначе
			ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = СокрЛП(Заказчик);
			//ОбластьМакета.Параметры.ЗаказчикАдресПочтовый     = КонтактнаяИнформация;
			ОбластьМакета.Параметры.ЗаказчикТелефоны           = ПредставлениеТелефона;
		КонецЕсли; 
		Если ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
			ОбластьМакета.Параметры.Автомобиль = Автомобиль;
			ОбластьМакета.Параметры.АвтомобильМодель = Автомобиль.Модель; 
			ОбластьМакета.Параметры.АвтомобильГосНомер = ?(НЕ обЗначениеНеЗаполнено(Автомобиль), Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер, Дата), ""); 
			ОбластьМакета.Параметры.АвтомобильКод = Автомобиль.VIN; 
			ОбластьМакета.Параметры.АвтомобильГодВыпуска = Формат(Автомобиль.ГодВыпуска,ФорматПредставленияГодаВыпускаАвтомобиля); 
			ОбластьМакета.Параметры.АвтомобильПробег = ?(НЕ обЗначениеНеЗаполнено(Автомобиль), СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег, Дата)), ""); 
		ИначеЕсли ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Модели") Тогда
			ОбластьМакета.Параметры.Автомобиль = Автомобиль;
			ОбластьМакета.Параметры.АвтомобильМодель = Автомобиль.НаименованиеПолное; 
		КонецЕсли; 
		ОбластьМакета.Рисунки.Принят.Параметр = "Принят:
		|" + Формат(Дата, "ДФ = дд.ММ.гггг");
		ОбластьМакета.Рисунки.ВидРемонта.Параметр = "Вид ремонта:
		|" + ВидРемонта;
		ОбластьМакета.Рисунки.Диспетчер.Параметр = "Диспетчер:
		|" + Автор;
		ОбластьМакета.Рисунки.ДатаНачала.Параметр = "Предв. дата начала:
		|" + Формат(ДатаНачала, "ДФ='дд.ММ.гггг ЧЧ:мм:сс'; ДП='Не задана'");
		ОбластьМакета.Рисунки.ДатаОкончания.Параметр = "Предв. дата окончания:
		|" + Формат(ДатаОкончания, "ДФ='дд.ММ.гггг ЧЧ:мм:сс'; ДП='Не задана'");
		Если ПустаяСтрока(ПричинаОбращения) Тогда
			ТекОбласть = ОбластьМакета.Область(16, , 16, );
			ТекОбласть.АвтовысотаСтроки = Ложь;
			ТекОбласть.ВысотаСтроки = 22;
		КонецЕсли;
		ОбластьМакета.Параметры.ПричинаОбращения = ПричинаОбращения;
		ТабДокумент.Вывести(ОбластьМакета);
		
		//Вывод шапки табличной части работ
		ОбластьУслуги.Параметры.НомерДок = НомерДляПечати;
		ОбластьУслуги.Параметры.ДатаДок = Формат(Дата,"ДФ = дд.ММ.гггг");
		КолонкаКода=дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект);
		ОбластьУслуги.Параметры.ИмяКолонкиКодов=КолонкаКода.Синоним;
		ТабДокумент.Вывести(ОбластьУслуги);
		
		//Временная таблица
		Табл = ТаблицаРабот.Скопировать();
		Для ДопСтр = 1 по 10 Цикл
			Табл.Добавить();
		КонецЦикла;
		КоличествоРабот = Табл.Количество();
		
		// инициализация итогов по документу
		ИтогоСуммаРабот	              = 0;
		ИтогоСуммаСкидкиРабот	      = 0;
		ИтогоСуммаДеталей	          = 0;
		ИтогоСуммаСкидкиДеталей	      = 0;
		
		НомерСтраницы = 2;
		НомерСтраницыПред = 2;
		ТекстЗаголовка = "Заявка на работы по заявке на ремонт № "+НомерДляПечати+" от "+Формат(Дата, "ДФ=дд.ММ.гггг");
		
		ОбластьШапкаУслуг.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаУслуг.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		ОбластьШапкаУслуг.Параметры.ИмяКолонкиКодов = КолонкаКода.Синоним;
		
		//Вывод табличной части работ
		Ном = 0;
		СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаСкидки", 0, 0);
		
		ОбластьИтоговПоСтраницеУслуги.Параметры.ТекстИтоговПоСтранице = "Итого по странице предварительная сумма работ:";
		МассивОбластиПодвала = Новый Массив;
		МассивОбластиПодвала.Добавить(ОбластьПодвалУслуг);
		Для Каждого СтрокаРабот из Табл Цикл
			Ном = Ном + 1;
			
			ОбластьСтрокаУслуг.Параметры.НомСтр = Ном;
			Если Ном > (КоличествоРабот-10) Тогда
				//заполним параметры
				ОбластьСтрокаУслуг.Параметры.Авторабота = "";
				ОбластьСтрокаУслуг.Параметры.Код = "";
				ОбластьСтрокаУслуг.Параметры.Наименование = "";
				ОбластьСтрокаУслуг.Параметры.Цена = "";
				ОбластьСтрокаУслуг.Параметры.Количество = "";
				ОбластьСтрокаУслуг.Параметры.Единица = "";
				ОбластьСтрокаУслуг.Параметры.Сумма = "";
				Если ЕстьСкидкаПоРаботам Тогда
					ОбластьСтрокаУслуг.Параметры.Скидка = "";
				КонецЕсли;
				ТекОбласть = ОбластьСтрокаУслуг.Область(1, , 1,);
				//установим для пустой строки высоту, равную двум строкам. Параметры не заполняем
				ТекОбласть.АвтовысотаСтроки = Ложь;
				ТекОбласть.ВысотаСтроки = 22;
			Иначе
				//заполним параметры
				ОбластьСтрокаУслуг.Параметры.Авторабота = СтрокаРабот.Работа;
				ОбластьСтрокаУслуг.Параметры.Код = дкПолучитьЗначениеКолонкиКода(СтрокаРабот.Работа,ИмяРеквизитаКода,ЭтотОбъект);
				ОбластьСтрокаУслуг.Параметры.Наименование = спПолучитьНаименование(СтрокаРабот.Работа);
				ОбластьСтрокаУслуг.Параметры.Цена = Формат(СтрокаРабот.Цена, ФорматВыводаСуммы);
				ОбластьСтрокаУслуг.Параметры.Количество = Формат(СтрокаРабот.Количество*СтрокаРабот.Коэффициент, ФорматВыводаКоличества);
				ОбластьСтрокаУслуг.Параметры.Единица = СтрокаРабот.Нормочас;
				ОбластьСтрокаУслуг.Параметры.Сумма = Формат(СтрокаРабот.СуммаВсего, ФорматВыводаСуммы);
				Если ЕстьСкидкаПоРаботам Тогда
					ОбластьСтрокаУслуг.Параметры.Скидка = Формат(СтрокаРабот.СуммаСкидки+СтрокаРабот.СуммаСкидкиСтроки, ФорматВыводаСуммы);
				КонецЕсли;
				// Обновим итоги по документу
				ИтогоСуммаРабот = ИтогоСуммаРабот + СтрокаРабот.СуммаВсего;
				ИтогоСуммаСкидкиРабот = ИтогоСуммаСкидкиРабот + СтрокаРабот.СуммаСкидки + СтрокаРабот.СуммаСкидкиСтроки;
			КонецЕсли;
			// Обновим итоги по странице
			
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрокаУслуг, ОбластьШапкаУслуг, ОбластьИтоговПоСтраницеУслуги, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект, ?(Ном=КоличествоРабот, МассивОбластиПодвала, Неопределено));
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаСкидки", 0, 0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаУслуг.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			
			//обновим итоги по странице
			СтруктураИтоговПоСтранице.СуммаВсего  = СтруктураИтоговПоСтранице.СуммаВсего + СтрокаРабот.СуммаВсего;
			СтруктураИтоговПоСтранице.СуммаСкидки = СтруктураИтоговПоСтранице.СуммаСкидки + СтрокаРабот.СуммаСкидки + СтрокаРабот.СуммаСкидкиСтроки;
		КонецЦикла; 
		
		//выводим последний итог по странице, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьИтоговПоСтраницеУслуги, СтруктураИтоговПоСтранице, ЭтотОбъект);
		КонецЕсли;
		
		ОбластьПодвалУслуг.Параметры.Сумма = Формат(ИтогоСуммаРабот, ФорматВыводаСуммы);
		Если ЕстьСкидкаПоРаботам Тогда
			ОбластьПодвалУслуг.Параметры.Скидка = Формат(ИтогоСуммаСкидкиРабот, ФорматВыводаСуммы);
		КонецЕсли;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалУслуг, , , НомерСтраницы,,ЭтотОбъект);
		
		// Формирование врем. таблиц товаров
		ТаблТовары = ТаблицаТоваров.Скопировать();
		ТаблТоварыКлиента = МатериалыЗаказчика.Выгрузить();
		
		Для ТекСтр = 1 по 10 Цикл
			ТаблТовары.Добавить();
			ТаблТоварыКлиента.Добавить();
		КонецЦикла;
		КоличествоДеталей = ТаблТовары.Количество();
		
		//Вывод шапки табличной части деталей
		ОбластьДетали.Параметры.НомерДок = НомерДляПечати;
		ОбластьДетали.Параметры.ДатаДок  = Формат(Дата, "ДФ = дд.ММ.гггг");
		// Если помещается только шапка, но ни одной строчки, то шапку переносим на след. страницу
		ТабДокВрем = Новый ТабличныйДокумент;
		ТабДокВрем.ПолеСверху = 10; ТабДокВрем.ПолеСнизу  = 0;
		ТабДокВрем.ПолеСлева  = 0; ТабДокВрем.ПолеСправа = 0;
		ОбластьДетали.Параметры.ИмяКолонкиКодов=КолонкаКода.Синоним;
		ТабДокВрем.Вывести(ОбластьДетали);
		ОбластьМакетаСтрокаВрем = Макет.ПолучитьОбласть("СтрокаДеталей");
		СтрокаДеталей = ТаблТовары[0];
		ТекОбласть = ОбластьМакетаСтрокаВрем.Область(1, , 1, );
		Если КоличествоДеталей = 10 Тогда
			//присутствуют только пустые строки
			ТекОбласть.АвтовысотаСтроки = Ложь;
			ТекОбласть.ВысотаСтроки = 22;
		Иначе
			//присутствуют заполненные строки (заполняем не все параметры - только наиболее вероятно длинные)
			ТекТовар = СтрокаДеталей.Номенклатура;
			ОбластьМакетаСтрокаВрем.Параметры.Номенклатура = ТекТовар;
			ОбластьМакетаСтрокаВрем.Параметры.Код=дкПолучитьЗначениеКолонкиКода(ТекТовар,ИмяРеквизитаКода,ЭтотОбъект,ВыводитьПроизводителя);
			ОбластьМакетаСтрокаВрем.Параметры.Наименование = ТекТовар.НаименованиеПолное;
		КонецЕсли;
		ТабДокВрем.Вывести(ОбластьМакетаСтрокаВрем);
		ТабДокВрем.Вывести(ОбластьИтоговПоСтраницеДетали);
		Попытка
			Если НЕ ТабДокумент.ПроверитьВывод(ТабДокВрем) Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
		Исключение
		КонецПопытки;
		ТабДокумент.Вывести(ОбластьДетали);
		
		НомерСтраницы = 2;
		НомерСтраницыПред = 2;
		ТекстЗаголовка = "Требование к заявке на ремонт № "+НомерДляПечати+" от "+Формат(Дата, "ДФ = дд.ММ.гггг");
		
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаДеталей.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаДеталей.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		ОбластьШапкаДеталей.Параметры.ИмяКолонкиКодов = КолонкаКода.Синоним;
		
		//Вывод табличной части деталей
		Ном = 0;
		СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаСкидки", 0, 0);
		
		ОбластьИтоговПоСтраницеДетали.Параметры.ТекстИтоговПоСтранице = "Итого по странице предварительная сумма материалов:";
		
		МассивОбластиПодвала = Новый Массив;
		МассивОбластиПодвала.Добавить(ОбластьПодвалДеталей);
		Для Каждого СтрокаДеталей из ТаблТовары Цикл
			Ном = Ном + 1;
			
			ОбластьСтрокаДеталей.Параметры.НомСтр = Ном;
			Если Ном > (КоличествоДеталей-10) Тогда
				ОбластьСтрокаДеталей.Параметры.Номенклатура = "";
				ОбластьСтрокаДеталей.Параметры.Код = "";
				ОбластьСтрокаДеталей.Параметры.Наименование = "";
				ОбластьСтрокаДеталей.Параметры.Цена = "";
				ОбластьСтрокаДеталей.Параметры.Количество = "";
				ОбластьСтрокаДеталей.Параметры.Единица = "";
				ОбластьСтрокаДеталей.Параметры.Сумма = "";
				Если ЕстьСкидкаПоДеталям Тогда
					ОбластьСтрокаДеталей.Параметры.Скидка = "";
				КонецЕсли;
				//установим для пустой строки высоту, равную двум строкам. Параметры не заполняем
				ТекОбласть = ОбластьСтрокаДеталей.Область(1, , 1,);
				ТекОбласть.АвтовысотаСтроки = Ложь;
				ТекОбласть.ВысотаСтроки = 22;
			Иначе
				//заполним параметры
				ТекТовар = СтрокаДеталей.Номенклатура;
				ОбластьСтрокаДеталей.Параметры.Номенклатура = ТекТовар;
				ОбластьСтрокаДеталей.Параметры.Код=дкПолучитьЗначениеКолонкиКода(СтрокаДеталей.Номенклатура,ИмяРеквизитаКода,ЭтотОбъект,ВыводитьПроизводителя);
				ОбластьСтрокаДеталей.Параметры.Наименование = СтрокаДеталей.Номенклатура.НаименованиеПолное;
				ОбластьСтрокаДеталей.Параметры.Цена = Формат(СтрокаДеталей.Цена, ФорматВыводаСуммы);
				ОбластьСтрокаДеталей.Параметры.Количество = Формат(СтрокаДеталей.Количество*СтрокаДеталей.Коэффициент, ФорматВыводаКоличества);
				ОбластьСтрокаДеталей.Параметры.Единица = СтрокаДеталей.ЕдиницаИзмерения;
				ОбластьСтрокаДеталей.Параметры.Сумма = Формат(СтрокаДеталей.СуммаВсего, ФорматВыводаСуммы);
				Если ЕстьСкидкаПоДеталям Тогда
					ОбластьСтрокаДеталей.Параметры.Скидка = Формат(СтрокаДеталей.СуммаСкидки+СтрокаДеталей.СуммаСкидкиСтроки, ФорматВыводаСуммы);
				КонецЕсли;
				// Обновим итоги по документу
				ИтогоСуммаДеталей = ИтогоСуммаДеталей + СтрокаДеталей.СуммаВсего;
				ИтогоСуммаСкидкиДеталей = ИтогоСуммаСкидкиДеталей + СтрокаДеталей.СуммаСкидки + СтрокаДеталей.СуммаСкидкиСтроки;
			КонецЕсли;
			// Обновим итоги по странице
			
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрокаДеталей, ОбластьШапкаДеталей, ОбластьИтоговПоСтраницеДетали, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект, ?(Ном=КоличествоДеталей, МассивОбластиПодвала, Неопределено));
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаСкидки", 0, 0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаДеталей.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			
			//обновим итоги по странице
			СтруктураИтоговПоСтранице.СуммаВсего  = СтруктураИтоговПоСтранице.СуммаВсего + СтрокаДеталей.СуммаВсего;
			СтруктураИтоговПоСтранице.СуммаСкидки = СтруктураИтоговПоСтранице.СуммаСкидки + СтрокаДеталей.СуммаСкидки + СтрокаДеталей.СуммаСкидкиСтроки;
		КонецЦикла; 
		
		//выводим последний итог по странице, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьИтоговПоСтраницеДетали, СтруктураИтоговПоСтранице, ЭтотОбъект);
		КонецЕсли;
		
		//Вывод подвала табличной части деталей
		ОбластьПодвалДеталей.Параметры.Сумма = Формат(ИтогоСуммаДеталей, ФорматВыводаСуммы);
		Если ЕстьСкидкаПоДеталям Тогда
			ОбластьПодвалДеталей.Параметры.Скидка = Формат(ИтогоСуммаСкидкиДеталей, ФорматВыводаСуммы);
		КонецЕсли;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалДеталей, , , НомерСтраницы,,ЭтотОбъект);
		
		//Вывод подвала документа
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Сумма = Формат(ИтогоСуммаРабот+ИтогоСуммаДеталей,"ЧЦ=15; ЧДЦ=2");
		ОбластьМакета.Параметры.Мастер = Мастер;
		ОбластьМакета.Параметры.МастерНаименование = Мастер.Наименование;
		Если ТипЗнч(Заказчик)=Тип("СправочникСсылка.Контрагенты") Тогда
			ОбластьМакета.Параметры.Заказчик = Заказчик;
			ОбластьМакета.Параметры.ЗаказчикНаименование = Заказчик.Наименование;
		Иначе
			ОбластьМакета.Параметры.ЗаказчикНаименование = СокрЛП(Заказчик);
		КонецЕсли; 
		ОбластьМакета.Параметры.ДатаДок = Формат(Дата,"ДФ = дд.ММ.гггг");
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
		
		ТабДокумент.ВерхнийКолонтитул.ВертикальноеПоложение=ВертикальноеПоложение.Низ;
		ТабДокумент.ВерхнийКолонтитул.Выводить=Истина;
		ТабДокумент.ВерхнийКолонтитул.НачальнаяСтраница=2;
		ТабДокумент.ВерхнийКолонтитул.ТекстСправа="Заявка на ремонт № "+НомерДляПечати+" от "+Формат(Дата,"ДФ = дд.ММ.гггг");
		ТабДокумент.ВерхнийКолонтитул.Шрифт = Новый Шрифт(ТабДокумент.ВерхнийКолонтитул.Шрифт,,,,Истина,Истина,);
		
		Возврат ТабДокумент;
	КонецФункции

Функция ПечатьАктОсмотраПровреждения(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("АктОсмотраМобКлиент");
	
	НомерДляПечати = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	// Вывод шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьМакета.Параметры.Организация             = Организация;
	ОбластьМакета.Параметры.ФирмаПолноеНаименование = спПолучитьНаименование(Организация);
	ОбластьМакета.Параметры.ФирмаАдресЮридический   = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	
	Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.ФирмаАдресЮридический)) Тогда
		ОбластьМакета.Параметры.ФирмаАдресЮридический = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	КонецЕсли;
	
	ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(ПодразделениеКомпании, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	
	Если ПустаяСтрока(СокрЛП(ОбластьМакета.Параметры.ФирмаТелефоны)) Тогда
		ОбластьМакета.Параметры.ФирмаТелефоны = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	КонецЕсли;
	ОбластьМакета.Параметры.НомерДок                     = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДок                      = Формат(ДатаНачала, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.Заказчик                     = Заказчик;
	Если ТипЗнч(Заказчик) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Заказчик);
		ОбластьМакета.Параметры.ЗаказчикАдресПочтовый = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
		ОбластьМакета.Параметры.ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
	Иначе
		ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = СокрЛП(Заказчик);
		ОбластьМакета.Параметры.ЗаказчикТелефоны = ПредставлениеТелефона;
	КонецЕсли;
		ОбластьМакета.Параметры.Контрагент                   = Контрагент;
		ОбластьМакета.Параметры.КонтрагентПолноеНаименование = спПолучитьНаименование(Контрагент);;
		ОбластьМакета.Параметры.КонтрагентИНН                = Контрагент.ИНН;
		ОбластьМакета.Параметры.КонтрагентАдресЮридический   = киПолучитьПредставлениеКИ(Контрагент, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
		ОбластьМакета.Параметры.КонтрагентТелефоны           = киПолучитьПредставлениеКИ(Контрагент, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
		
		Если ЗначениеЗаполнено(Автомобиль) И ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автомобили") Тогда
			ОбластьМакета.Параметры.Автомобиль               = Автомобиль;
			ОбластьМакета.Параметры.АвтомобильКод            = Автомобиль.VIN;
			ОбластьМакета.Параметры.АвтомобильМодель         = Автомобиль.Модель;
			ОбластьМакета.Параметры.АвтомобильГодВыпуска     = Формат(Автомобиль.ГодВыпуска, ФорматПредставленияГодаВыпускаАвтомобиля);
			ОбластьМакета.Параметры.АвтомобильНомерДвигателя = Автомобиль.НомерДвигателя;
			ОбластьМакета.Параметры.АвтомобильНомерШасси     = Автомобиль.НомерШасси;
			ОбластьМакета.Параметры.АвтомобильНомерКузова    = Автомобиль.НомерКузова;
			ОбластьМакета.Параметры.АвтомобильЦвет           = Автомобиль.Цвет;
			
			ОбластьМакета.Параметры.АвтомобильГосНомер   = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,ДатаНачала);
			ОбластьМакета.Параметры.АвтомобильТехПаспорт = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ТехПаспорт,ДатаНачала));
		КонецЕсли;
		
		СхемаТС = ОбластьМакета.Рисунки["СхемаТС"];
		
		// добавим общюю картинку
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КартинкиИФайлы.Данные,
		|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
		|	КартинкиИФайлы.ИмяФайла
		|ИЗ
		|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
		|ГДЕ
		|	КартинкиИФайлы.Объект = &Объект
		|	И КартинкиИФайлы.Идентификатор = &Идентификатор";
		Запрос.УстановитьПараметр("Объект", Ссылка);
		Запрос.УстановитьПараметр("Идентификатор", "DirectoryPhoto");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.ДанныеВоВнешнемФайле Тогда
				ДанныеКартинки = Новый Картинка(Выборка.ИмяФайла,Ложь);
			Иначе
				ДанныеКартинки = Выборка.Данные.Получить();
			КонецЕсли;
			
			Если ТипЗнч(ДанныеКартинки) = Тип("ДвоичныеДанные") Тогда
				СхемаТС.Картинка = Новый Картинка(ДанныеКартинки, Ложь);
			ИначеЕсли типЗнч(ДанныеКартинки) = Тип("Картинка") Тогда
				СхемаТС.Картинка = ДанныеКартинки;
			КонецЕсли;
			СхемаТС.РазмерКартинки = РазмерКартинки.Пропорционально;
		Иначе
			ТипАвтомобиля = ТипЗнч(Автомобиль); ПараметрПоиска = Неопределено;
			Если ТипАвтомобиля = Тип("СправочникСсылка.Автомобили") Тогда
				ПараметрПоиска = Автомобиль.Модель;
			Иначеесли ТипАвтомобиля = Тип("СправочникСсылка.Модели") Тогда
				ПараметрПоиска = Автомобиль;
			КонецЕсли;
			
			СхемаТСДанные = орПолучитьСхемуТССервер(ПараметрПоиска,"СхемаТС");
			Если СхемаТСДанные <> Неопределено Тогда
				СхемаТС.Картинка       = СхемаТСДанные;
				СхемаТС.РазмерКартинки = РазмерКартинки.Растянуть;
			КонецЕсли;
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		// выводим описание повреждений
		ОбластьМакетаШапкаПовреждения  = Макет.ПолучитьОбласть("ЗамечанияПриОсмотреШапка");
		ОбластьМакетаСтрокаПовреждения = макет.ПолучитьОбласть("ЗамечанияПриОсмотреСтрока");
		
		// получим данные из регистра ОписаниеПовреждений
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОписаниеПовреждений.ТипПовреждения,
		|	ОписаниеПовреждений.ОписаниеПовреждения,
		|	ОписаниеПовреждений.НомерПовреждения
		|ИЗ
		|	РегистрСведений.ОписаниеПовреждений КАК ОписаниеПовреждений
		|ГДЕ
		|	ОписаниеПовреждений.Объект = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерПовреждения";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатПоврежлдения = Запрос.Выполнить();
		
		// проверим а естьли что выводить
		Если НЕ РезультатПоврежлдения.Пустой() Тогда
			ТабДокумент.Вывести(ОбластьМакетаШапкаПовреждения);
			ВыборкаПовреждения = РезультатПоврежлдения.Выбрать();
			Пока ВыборкаПовреждения.Следующий() Цикл
				//заполним область
				ОбластьМакетаСтрокаПовреждения.Параметры.НомерСтроки         = ВыборкаПовреждения.НомерПовреждения;
				ОбластьМакетаСтрокаПовреждения.Параметры.ВидОтметки          = ВыборкаПовреждения.ТипПовреждения;
				ОбластьМакетаСтрокаПовреждения.Параметры.ОписаниеПовреждения = ВыборкаПовреждения.ОписаниеПовреждения;
				
				//проверим вывод
				МассивОбластей = Новый Массив;
				МассивОбластей.Добавить(ОбластьМакетаСтрокаПовреждения);
				
				Попытка
					Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
						ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ТабДокумент.Вывести(ОбластьМакетаШапкаПовреждения);
					КонецЕсли;
				Исключение
				КонецПопытки;
				ТабДокумент.Вывести(ОбластьМакетаСтрокаПовреждения);
			КонецЦикла;
		КонецЕсли;
		
		
		// получим заголовок и строку таблицы
		ОбластьМакетаШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьМакетаСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ОбластьМакетаПолвал        = Макет.ПолучитьОбласть("Подвал");
		
		Если НЕ РезультатПоврежлдения.Пустой() Тогда
			ТабДокумент.Вывести(Макет.ПолучитьОбласть("Отступ"));
		КонецЕсли;
		
		//проверим вывод
		МассивОбластей = Новый Массив;
		МассивОбластей.Добавить(ОбластьМакетаШапкаТаблицы);
		МассивОбластей.Добавить(ОбластьМакетаСтрокаТаблицы);
		
		Попытка
			Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда 
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ТабДокумент.Вывести(ОбластьМакетаШапкаТаблицы);
		
		// получим значениея чеклиста
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗначениеCheckList.CheckList КАК CheckList,
		|	ЗначениеCheckList.CheckList.Наименование КАК Наименование,
		|	ЗначениеCheckList.CheckList.Описание КАК Описание,
		|	ЗначениеCheckList.Значение
		|ИЗ
		|	РегистрСведений.ЗначениеCheckList КАК ЗначениеCheckList
		|ГДЕ
		|	ЗначениеCheckList.Объект = &Объект
		|	И ЗначениеCheckList.CheckList.ИспользоватьПриПечати";
		Запрос.УстановитьПараметр("Объект", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ОбластьМакетаСтрокаТаблицы.Параметры.ПунктПроверки = ?( ЗначениеЗаполнено(Выборка.Описание), Выборка.Описание, Выборка.Наименование);
		Если НЕ ЗначениеЗаполнено(ОбластьМакетаСтрокаТаблицы.Параметры.ПунктПроверки) Тогда
			ОбластьМакетаСтрокаТаблицы.Параметры.ПунктПроверки = "Элемент проверки";
		КонецЕсли;
		ОбластьМакетаСтрокаТаблицы.Параметры.Значение      = Выборка.Значение;
		
		//проверим вывод
		МассивОбластей = Новый Массив;
		МассивОбластей.Добавить(ОбластьМакетаСтрокаТаблицы);
		
		Попытка
			Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ОбластьМакетаШапкаТаблицы);
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		ТабДокумент.Вывести(ОбластьМакетаСтрокаТаблицы);

	КонецЦикла;
	
	
	// Вывод подвала документа
	ОбластьМакетаПолвал.Параметры.Заказчик                     = Заказчик;
	ОбластьМакетаПолвал.Параметры.ЗаказчикПолноеНаименование   = спПолучитьНаименование(Заказчик);
	ОбластьМакетаПолвал.Параметры.Контрагент                   = Контрагент;
	ОбластьМакетаПолвал.Параметры.КонтрагентПолноеНаименование = спПолучитьНаименование(Контрагент);
	ОбластьМакетаПолвал.Параметры.Мастер                       = Мастер;
	ОбластьМакетаПолвал.Параметры.МастерПолноеНаименование     = Мастер.Наименование;
	ОбластьМакетаПолвал.Параметры.ДатаДок                      = Формат(ДатаНачала, "ДФ = ""дд ММММ гггг 'г.'""");
	
	//проверим вывод
	МассивОбластей = Новый Массив;
	МассивОбластей.Добавить(ОбластьМакетаПолвал);
	
	Попытка
		Если НЕ ТабДокумент.ПроверитьВывод(МассивОбластей) Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ТабДокумент.Вывести(ОбластьМакетаПолвал);
	
	Возврат ТабДокумент;
	
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
	// экран или принтер, а также распечатать необходимое количество копий.
	// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
	//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено, ДопПараметры=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы  
	
	//<<ЧалапАЮ БизТех 21.12.2023   
	//ДатаЗапретаРедактирования = КонецДня(орПолучитьДатуЗапретаРедактирования(ЭтотОбъект));
	ПодразделениеО2Ли = Неопределено;
	ПодразделениеО2Ли = Справочники.БТ_СписокКонстантныхЗначений.ПодразделениеО2Ли(ЭтотОбъект.ПодразделениеКомпании);
	
	Если обПраво("Печать_ЗН_ЗакрытогоПериода") 
		ИЛИ РольДоступна("БТ_РасширенныйДоступ") 
		ИЛИ (РольДоступна("БТ_СотрудникО2") И ПодразделениеО2Ли = ИСТИНА) 
		ИЛИ ЭтотОбъект.Дата > обПолучитьПраваИНастройкиПользователя(ЭтотОбъект.Организация,"ДатаЗапретаРедактирования",ЭтотОбъект) тогда
		ТекущийОбъект = дкПолучитьТекущийОбъект(ЭтотОбъект);
		Возврат дкПечать(ТекущийОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ, ДопПараметры);
	иначе
		Сообщить("Запрещено печатать документы закрытого периода"); 
		Возврат неопределено;
	КонецЕсли;
	//ЧалапАЮ БизТех 21.12.2023>>

	//ТекущийОбъект = дкПолучитьТекущийОбъект(ЭтотОбъект);
	//Возврат дкПечать(ТекущийОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ, ДопПараметры);
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДЛЯ МОБИЛЬНОГО КЛИЕНТА

Процедура ПечатьАктОсмотраМобКлиент(УзелОбмена) Экспорт
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	ТабДокумент = ПечатьАктОсмотраПровреждения(ТабДокумент);
	
	ТабДокумент.ИмяПринтера = УзелОбмена.Принтер;
	
	ТабДокумент.Напечатать(НЕ ПустаяСтрока(ТабДокумент.ИмяПринтера));
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ТипЦен) Тогда
		ТипЦен=обПраво("ОсновнойТипЦенПродажи",Права,,ЭтотОбъект);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ТипЦенРабот) Тогда
		ТипЦенРабот = обПраво("ОсновнойТипЦенРабот",Права,,ЭтотОбъект);
	КонецЕсли;
	
	Если Метаданные.Документы.Найти("ЗаказНаАвтомобиль")<>Неопределено Тогда
		Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
			Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон") Тогда
				ВидРемонта=Справочники.ВидыРемонта.КомплектацияАвтомобиля;
			КонецЕсли; 
			Если обЗначениеНеЗаполнено(Автомобиль) Тогда
				Автомобиль = Основание.Автомобиль;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Событие") Тогда
		Заказчик = Основание.Контрагент;
		//ОбработкаРеквизита("Заказчик");
		Если Не ЗначениеЗаполнено(ПредставлениеТелефона) Тогда
			КодГорода             = Основание.КодГорода;
			КодРегиона            = Основание.КодРегиона;
			ВнутреннийНомер       = Основание.ВнутреннийНомер;
			ПредставлениеТелефона = Основание.ПредставлениеТелефона;
			НомерТелефона         = Основание.НомерТелефона;
			ВидТелефона           = Основание.ВидТелефона;
		КонецЕсли;
		Если Основание.Автомобили.Количество() > 0 Тогда
			Автомобиль = Основание.Автомобили[0].Автомобиль;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(Заказчик)=Тип("СправочникСсылка.Контрагенты") Тогда	
		ДопПараметры = Новый Структура;
		Если ЗначениеЗаполнено(ПредставлениеТелефона) Тогда
			ДопПараметры.Вставить("НеОбновлятьТелефон");
		КонецЕсли;
		ОбработкаРеквизита("Заказчик",,,ДопПараметры);
	КонецЕсли; 
	
	Если ТипЗнч(Основание)=Тип("СправочникСсылка.Контрагенты") Тогда
		Заказчик=Основание;
		Контрагент=Основание;
		ДоговорВзаиморасчетов=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
		ОбработкаРеквизита("Контрагент");
	КонецЕсли; 
	
	Если ЭтоНовый() Тогда
		Если обЗначениеНеЗаполнено(ВидРемонта) Тогда 
			ВидРемонта=обПраво("ВидРемонтаПоУмолчанию",Права,,ЭтотОбъект);
			// KRAA
			ОбработкаРеквизита("ВидРемонта");
		КонецЕсли; 
		Если обЗначениеНеЗаполнено(Цех) Тогда 
			Цех=обПраво("ОсновнойЦех",Права,,ЭтотОбъект);
			ОбработкаРеквизита("Цех");
		КонецЕсли; 
	КонецЕсли;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	//получим свойства объекта копирования и запишем их в переменную модуля объекта
	//ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов = Обработки.ЗначенияСвойствОбъекта.Создать();
	//ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ОбъектОтбораЗначений=ОбъектКопирования.Ссылка;
	//ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ПрочитатьЗаполнитьСвойстваИЗначения();
	//ЭтотОбъект.ОбработкаЗначенияСвойствОбъектов.ОбъектОтбораЗначений=ЭтотОбъект.Ссылка; 
	
	//<<ЧалапАЮ БизТех 06.10.2023    
	//Если ЗначениеЗаполнено(ОбъектКопирования.Заказчик.БТ_ПризнакЛояльности)  тогда          //Элемент.Значение.ПризнакЛояльности  
	//	//Сообщить("Контрагент " + ОбъектКопирования.Заказчик.Наименование +" имеет признак " + ОбъектКопирования.Заказчик.БТ_ПризнакЛояльности +", дальнейшие работы с ним запрещены, необходимо обратиться к руководителю отдела.");	
	//	//Заказчик = Справочники.Контрагенты.ПустаяСсылка(); 
	//	Предупреждение("Контрагент " + ОбъектКопирования.Заказчик.Наименование +" имеет признак " + ОбъектКопирования.Заказчик.БТ_ПризнакЛояльности); //ЧалапАю БизТех 10.01.2025
	//КонецЕсли; 
	//ЧалапАЮ БизТех 06.10.2023>>
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//если ХО "заявка на ремонт", очистим планирование
	Если ХозОперация <> Справочники.ХозОперации.ПланРемонта Тогда
		Планирование.Очистить();	
	КонецЕсли;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	Если Отказ Тогда Возврат; КонецЕсли;
	
	//БИЗТЕХ КОВАЛЬ 11.10.2025 О2 API ++
	#Если НЕ КлиентскоеПриложение Тогда
		ЕстьПодсистемаАвтосалон = Истина; 
	#Иначе
		ЕстьПодсистемаАвтосалон = зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон"); 
	#КонецЕсли
	//БИЗТЕХ КОВАЛЬ 11.10.2025 --

	
	Если ЕстьПодсистемаАвтосалон Тогда
		КомплектацияАвтомобиля=Справочники.ВидыРемонта.КомплектацияАвтомобиля;
		Если (КомплектацияАвтомобиля.Пустая()) ИЛИ (НЕ КомплектацияАвтомобиля.Предопределенный) Тогда
			КомплектацияАвтомобиля=Неопределено;
		КонецЕсли; 
	Иначе
		КомплектацияАвтомобиля=Неопределено;
	КонецЕсли; 
	Если ВидРемонта=КомплектацияАвтомобиля ИЛИ ВидРемонта.ТипРемонта=Перечисления.ТипыРемонта.Бесплатный Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Контрагент=Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
		Если ЗначениеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ДоговорВзаиморасчетов=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	//Если ТипЗнч(Заказчик) <> Тип("Строка") Тогда
	//	КодРегиона             = "";
	//	КодГорода              = "";
	//	НомерТелефона          = "";
	//	ВнутреннийНомер        = "";
	//	ПредставлениеТелефона = "";
	//	КонтактнаяИнформация   = "";
	//КонецЕсли;
	
	СуммаНоменклатурыДокумента=Товары.Итог("СуммаВсего");
	СуммаРаботДокумента=Работы.Итог("СуммаВсего");
	СуммаДокумента=СуммаНоменклатурыДокумента+СуммаРаботДокумента;
	
	НовоеВремяВыполненияРабот=0;
	Для Каждого СтрокаРабот Из Работы Цикл
		Если СтрокаРабот.Нормочас.Цена<>1 Тогда
			НовоеВремяВыполненияРабот=НовоеВремяВыполненияРабот+(СтрокаРабот.Коэффициент*СтрокаРабот.Количество);
		КонецЕсли; 
	КонецЦикла;
	Если НовоеВремяВыполненияРабот<>ВремяВыполненияРабот Тогда
		ВремяВыполненияРабот=НовоеВремяВыполненияРабот;
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ВремяНачалаПередЗаписью",Ссылка.ДатаНачала);
	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("Новый");
	КонецЕсли; 
	
	//<<БАА 13.10.24 
	Если видРемонта = Справочники.ВидыРемонта.НайтиПоКоду("ЦБ000037") и не ЗначениеЗаполнено(БТ_НомерТО)  тогда   //ЧалапАю БизТех 14.10.2024 ограничимся видом ремонта от греха
		БТ_НомерТО = БТ_СлужебныеФункции.ОпределитьНомерТО(ЭтотОбъект);
	КонецЕсли;
	//>>БАА 13.10.24

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Ошибки="";
	//Проверка корректности дат
	Если (НЕ обЗначениеНеЗаполнено(ДатаНачала)) И (НЕ обЗначениеНеЗаполнено(ДатаОкончания)) Тогда
		Если ДатаНачала>ДатаОкончания Тогда
			дкДобавитьОшибку(Ошибки,"Дата начала заявки на ремонт более даты его окончания.");
			Отказ=Истина;
		КонецЕсли;
	КонецЕсли; 
	Если Отказ Тогда
		// Выведем все накопившиеся ошибки 
		Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
		Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
		Сообщить("Из-за возникших ошибок операция записи <"+СокрЛП(ЭтотОбъект)+"> была отменена.",СтатусСообщения.Важное);
	КонецЕсли;
	Если Отказ Тогда Возврат; КонецЕсли;
	
	// попробуем сформировать список значений чек листа
	НаборЗначениеCheckList = РегистрыСведений.ЗначениеCheckList.СоздатьНаборЗаписей();
	НаборЗначениеCheckList.Отбор.Объект.Установить(Ссылка);
	НаборЗначениеCheckList.Прочитать();
	
	Если НаборЗначениеCheckList.Количество() = 0 Тогда
		Модель = Неопределено; ТипАвтомобиля = ТипЗнч(Автомобиль);
		Если ТипАвтомобиля = Тип("СправочникСсылка.Автомобили") Тогда
			Модель = Автомобиль.Модель;
		ИначеЕсли ТипАвтомобиля = Тип("СправочникСсылка.Модели") Тогда
			Модель = Автомобиль;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставCheckList.CheckList,
		|	МИНИМУМ(СоставCheckList.Порядок) КАК Порядок
		|ИЗ
		|	РегистрСведений.СоставCheckList КАК СоставCheckList
		|ГДЕ
		|	(СоставCheckList.Модель = &Модель
		|			ИЛИ СоставCheckList.Модель = ЗНАЧЕНИЕ(Справочник.Модели.пустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	СоставCheckList.CheckList
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
		Запрос.УстановитьПараметр("Модель", Модель);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЗаписьРегистра = НаборЗначениеCheckList.Добавить();
			
			// Измерения
			ЗаписьРегистра.Объект    = Ссылка;
			ЗаписьРегистра.CheckList = Выборка.CheckList;
			ЗаписьРегистра.GUID      = Новый УникальныйИдентификатор();
			
			// Ресурсы
			ЗаписьРегистра.Порядок   = Выборка.Порядок;
		КонецЦикла;
		
		Если НаборЗначениеCheckList.Количество() > 0 Тогда
			Попытка
				НаборЗначениеCheckList.Записать();
			Исключение КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ВремяНачалаПриЗаписи",Ссылка.ДатаНачала);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Очистим регистр ГрафикРаботыРесурсов
	НаборЗаписейГрафикРаботыРесурсов = РегистрыСведений.ГрафикРаботыРесурсов.СоздатьНаборЗаписей();
	НаборЗаписейГрафикРаботыРесурсов.ДокументОбъект = Ссылка;
	НаборЗаписейГрафикРаботыРесурсов.ОтменаПроведения();
	
	//Механизм отправки смс
	РегистрыСведений.НапоминаниеОЗаписиНаРемонт.УдалитьИзЗаявкиНаРемонт(Ссылка);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Перем ВремяПриЗаписи;
	Перем ВремяПередЗаписью;
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ И Состояние <> Перечисления.СостоянияЗаявкиНаРемонт.Отклонено Тогда
		Отказ = НЕ орГрафикРаботыРесурсовПоЗаказНаряду(Ссылка);
	ИначеЕсли Состояние = Перечисления.СостоянияЗаявкиНаРемонт.Отклонено Тогда
		ОбработкаУдаленияПроведения(Отказ);
	КонецЕсли; 
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	// Механизм отправки смс уведомлений Mikolv.
	ДополнительныеСвойства.Свойство("ВремяНачалаПриЗаписи",ВремяПриЗаписи);
	ДополнительныеСвойства.Свойство("ВремяНачалаПередЗаписью",ВремяПередЗаписью);
	// Проверим необходимо ли отправлять смс.
	Если ВидРемонта.ОтправлятьУведомления  Тогда
		Если ЭтотОбъект.Состояние=Перечисления.СостоянияЗаявкиНаРемонт.Отклонено ИЛИ ДатаНачала<ТекущаяДата() ИЛИ НЕ ЗначениеЗаполнено(ВидРемонта.ШаблонПриЗаписиНаРемонт) Тогда
			РегистрыСведений.НапоминаниеОЗаписиНаРемонт.УдалитьИзЗаявкиНаРемонт(Ссылка);
		Иначе
			// Проверим есть ли телефоны
			Если ТипЗнч(Заказчик) = Тип("Строка") Тогда
				Сообщить("Не удалось проверить у заказчика согласие на получение SMS.
					|Для получения SMS необходимо выбрать заказчика из справочника контрагентов.", СтатусСообщения.Информация);
			ИначеЕсли (ЗначениеЗаполнено(ПредставлениеТелефона)
				ИЛИ ЗначениеЗаполнено(кмПолучитьОсновнойМобильныйТелефонОбъекта(Заказчик)))
				И Заказчик.СогласиеНаПолучениеSMS Тогда
				Если ВремяПриЗаписи <> ВремяПередЗаписью ИЛИ ДополнительныеСвойства.Свойство("Новый")  Тогда
					РегистрыСведений.НапоминаниеОЗаписиНаРемонт.СоздатьИзменитьИзЗаявкиНаРемонт(Ссылка);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ДополнительныеСвойства.Удалить("Новый");
	
	//++СнимченкоАА_бизтех+      
	Если РезервироватьЗапчасти Тогда
		
		Для каждого тс Из Товары Цикл
		   Если НЕ ЗначениеЗаполнено(тс.СкладКомпании) Тогда
		       Сообщить(стршаблон("Склад в %1 строке не указан, резервирование товара %2 не будет выполнено",тс.НомерСтроки,тс.Номенклатура));
			   //Отказ = Истина;
		   КонецЕсли;
		КонецЦикла;
		
		// проводим заказ покупателя
		НаборЗаписейЗаказыПокупателей=Движения.ЗаказыПокупателей;
		НаборЗаписейЗаказыПокупателей.РежимПроведения=Режим;
		НаборЗаписейЗаказыПокупателей.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам=Неопределено;
		НаборЗаписейЗаказыПокупателей.Контрагент=Контрагент;
		НаборЗаписейЗаказыПокупателей.Заказ=Ссылка;   
		//НаборЗаписейЗаказыПокупателей.СкладКомпании=СкладКомпании;
		НаборЗаписейЗаказыПокупателей.Заказывать=Истина;
		НаборЗаписейЗаказыПокупателей.Резервировать=РезервироватьЗапчасти;
		Отказ=НЕ НаборЗаписейЗаказыПокупателей.Приход() ИЛИ Отказ;
		
		// резервирование заказа покупателя
		НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
		НаборЗаписейОстатки.РежимПроведения           = Режим;
		НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейОстатки.РезультатЗапросаПоТоварам = Неопределено;
		//НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
		НаборЗаписейОстатки.ДвиженияПоРознице         = Ложь;
		Отказ = НЕ НаборЗаписейОстатки.Зарезервировать() ИЛИ Отказ;  
		
		МоментВремениБыл = Неопределено; 
		ЗаказБыл =  Неопределено;
		
		// двигаем границу последовательности заказов
		Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ЗаказБыл) Тогда
			НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
			Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
				НаборЗаписейГраницыЗаказы.Заказ = Ссылка;
				НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
				НаборЗаписейГраницыЗаказы.ЗаказБыл = ЗаказБыл;
				НаборЗаписейГраницыЗаказы.МоментВремениБыл = МоментВремениБыл;
			Иначе
				НаборЗаписейГраницыЗаказы.Заказ = ЗаказБыл;
				НаборЗаписейГраницыЗаказы.МоментВремени = МоментВремениБыл;
				НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
				НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
			КонецЕсли;
			НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
			НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
		КонецЕсли;
		
	КонецЕсли;
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ОбработкаПроведения


//++СнимченкоАА_бизтех++
// перезаполнение кеша по перемещению
Процедура ПолучитьКэшДанныхПоПеремещению(ПоСкладу=Неопределено, Знач РезервыПоЗаказНаряду=Неопределено, СтруктураОтбора = Неопределено, ИсходнаяТаблица = Неопределено) Экспорт
	
	Если РезервыПоЗаказНаряду=Неопределено Тогда
		РезервыПоЗаказНаряду=Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстОтбораЗаказПокупателя = "";
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") И ЗначениеЗаполнено(ДокументОснование) Тогда
		ТекстОтбораЗаказПокупателя = " (Заказ=&ВыбЗаказПокупателя) ИЛИ ";
		Запрос.УстановитьПараметр("ВыбЗаказПокупателя", ДокументОснование);
	КонецЕсли;   
	
	//++СнимченкоАА_бизтех++
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявкаНаРемонт") Тогда
		ТекстОтбораЗаказПокупателя = " (Заказ=&ВыбЗаявкаНаРемонт) ИЛИ ";
		Запрос.УстановитьПараметр("ВыбЗаявкаНаРемонт", Ссылка);
	КонецЕсли;	   
	//--СнимченкоАА_бизтех--
	
	ОтборПоСтроке = Ложь;
	ТекстОтбора = "";
	ВиртТекстОтбора = "";
	СтруктураОтбораБезСклада = Новый Структура;
	Если ТипЗнч(КешИнформацииЗН) = Тип("ТаблицаЗначений") И ТипЗнч(СтруктураОтбора) = Тип("Структура") Тогда
		
		Для Каждого ТекЭлемент Из СтруктураОтбора Цикл
			ИмяЭлемента = ТекЭлемент.Ключ;
			ТекстОтбора = ТекстОтбора + ?(ТекстОтбора = "", "ГДЕ ", " И ") + "
			|	ЗаказНарядТовары."+ИмяЭлемента+" = &"+ИмяЭлемента;
			Если НЕ ИмяЭлемента = "СкладКомпании" Тогда
				ВиртТекстОтбора = ВиртТекстОтбора + " И 
				|	"+ИмяЭлемента+" = &"+ИмяЭлемента;
				СтруктураОтбораБезСклада.Вставить(ИмяЭлемента, ТекЭлемент.Значение);
			КонецЕсли;
			Запрос.УстановитьПараметр(ИмяЭлемента, ТекЭлемент.Значение);
			ОтборПоСтроке = Истина;
		КонецЦикла;
		
	КонецЕсли;
	
	// Таблица строк
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ЗаказНарядТовары.Номенклатура,
	|	ЗаказНарядТовары.ХарактеристикаНоменклатуры,
	|	ЗаказНарядТовары.СкладКомпании КАК СкладКомпании
	|ПОМЕСТИТЬ ЗаказНарядТовары
	|ИЗ
	|	&Товары КАК ЗаказНарядТовары
	|" + ТекстОтбора +"
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказНарядТовары.Номенклатура КАК Номенклатура,
	|	ЗаказНарядТовары.СкладКомпании КАК СкладКомпании,
	|	ЗаказНарядТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ
	|	ТаблицаТоваров
	|ИЗ
	|	ЗаказНарядТовары КАК ЗаказНарядТовары
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СкладКомпании
	|;
	|
	|ВЫБРАТЬ
	|	ПеремещениеТоваровВПроизводство.Ссылка КАК Регистратор
	|ПОМЕСТИТЬ
	|	ПеремещенияТоваровВПроизводство
	|ИЗ
	|	Документ.ПеремещениеТоваровВПроизводство КАК ПеремещениеТоваровВПроизводство
	|ГДЕ
	|	ПеремещениеТоваровВПроизводство.ДокументОснование = &ЗаказНаряд
	|;
	|
	|ВЫБРАТЬ
	|	ЗаказВнутренний.Ссылка КАК Регистратор
	|ПОМЕСТИТЬ
	|	ТаблицаЗаказов
	|ИЗ
	|	Документ.ЗаказВнутренний КАК ЗаказВнутренний
	|ГДЕ
	|	ЗаказВнутренний.ДокументОснование = &ЗаказНаряд
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.ДокументОснование = &ЗаказНаряд
	|;
	|
	|ВЫБРАТЬ
	|	ОбъединеннаяТаблица.Номенклатура КАК Номенклатура,
	|	ОбъединеннаяТаблица.СкладКомпании КАК СкладКомпании,
	|	ОбъединеннаяТаблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ОбъединеннаяТаблица.ПеремещеноСоСклада) КАК ПеремещеноСоСклада,
	|	СУММА(ОбъединеннаяТаблица.Зарезервировано) КАК Зарезервировано,
	|	СУММА(ОбъединеннаяТаблица.ОстатокНаСкладе)+СУММА(ОбъединеннаяТаблица.Зарезервировано) КАК КоличествоОстаток,
	|	СУММА(ОбъединеннаяТаблица.ОстатокНаСкладе) КАК ОстатокНаСкладе
	|ИЗ(
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.Номенклатура,
	|		ТаблицаТоваров.СкладКомпании,
	|		ТаблицаТоваров.ХарактеристикаНоменклатуры,
	|		0 КАК ПеремещеноСоСклада,
	|		0 КАК Зарезервировано,
	|		0 КАК ОстатокНаСкладе
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОстаткиТоваровКомпании.Номенклатура,
	|		ОстаткиТоваровКомпании.СкладКомпании,
	|		ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры,
	|		ОстаткиТоваровКомпании.Количество КАК ПеремещеноСоСклада,
	|		0 КАК Зарезервировано,
	|		0 КАК ОстатокНаСкладе
	|	ИЗ
	|		РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
	|	ГДЕ
	|		ОстаткиТоваровКомпании.Регистратор ССЫЛКА Документ.ПеремещениеТоваровВПроизводство И 
	|		ОстаткиТоваровКомпании.Регистратор В (ВЫБРАТЬ Регистратор ИЗ ПеремещенияТоваровВПроизводство)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаказыПокупателейОстатки.Номенклатура,
	|		ЗаказыПокупателейОстатки.СкладКомпании,
	|		ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	|		0,
	|		ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0),
	|		0
	|	ИЗ 
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(,
	|		"+?(РезервыПоЗаказНаряду, ТекстОтбораЗаказПокупателя+"Заказ В (ВЫБРАТЬ ТаблицаЗаказов.Регистратор ИЗ ТаблицаЗаказов КАК ТаблицаЗаказов)","
	|		("+ТекстОтбораЗаказПокупателя+"(Заказ В (ВЫБРАТЬ ТаблицаЗаказов.Регистратор ИЗ ТаблицаЗаказов КАК ТаблицаЗаказов)) ИЛИ (Контрагент=&Контрагент И (СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры) В 
	|			(ВЫБРАТЬ СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры ИЗ ТаблицаТоваров КАК ТаблицаТоваров)))")+"
	|		"+?(ПоСкладу=Неопределено,"","И (СкладКомпании=&ПоСкладу)")+") КАК ЗаказыПокупателейОстатки
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОстаткиТоваровКомпанииОстатки.Номенклатура,
	|		ОстаткиТоваровКомпанииОстатки.СкладКомпании,
	|		ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	|		0,
	|		0,
	|		ОстаткиТоваровКомпанииОстатки.КоличествоОстаток-ОстаткиТоваровКомпанииОстатки.РезервОстаток
	|	ИЗ
	|		РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, (СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры) В 
	|			(ВЫБРАТЬ СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры ИЗ ТаблицаТоваров КАК ТаблицаТоваров)) КАК ОстаткиТоваровКомпанииОстатки) КАК ОбъединеннаяТаблица
	|СГРУППИРОВАТЬ ПО
	|	ОбъединеннаяТаблица.Номенклатура,
	|	ОбъединеннаяТаблица.СкладКомпании,
	|	ОбъединеннаяТаблица.ХарактеристикаНоменклатуры
	|";
	
	Запрос.Текст = ТекстЗапроса;
	Если ИсходнаяТаблица = Неопределено Тогда
		Запрос.УстановитьПараметр("Товары", Товары);
	Иначе
		Запрос.УстановитьПараметр("Товары", ИсходнаяТаблица);
	КонецЕсли;
	Запрос.УстановитьПараметр("ЗаказНаряд", Ссылка);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ПоСкладу",   ПоСкладу);
	
	Если ОтборПоСтроке Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Номенклатура",               Выборка.Номенклатура);
			СтруктураПоиска.Вставить("СкладКомпании",              Выборка.СкладКомпании);
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
			НайденныеСтроки = КешИнформацииЗН.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество()>0 Тогда
				СтрокаКеша = НайденныеСтроки[0];
			Иначе
				СтрокаКеша = КешИнформацииЗН.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтрокаКеша, Выборка);
		КонецЦикла;
	Иначе
		КешИнформацииЗН = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ ТаблицаТоваровБезСкладов
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ОбъединенныйЗапрос.Номенклатура,
	|	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры,
	|	СУММА(ОбъединенныйЗапрос.ВПроизводстве) КАК ВПроизводстве,
	|	СУММА(ОбъединенныйЗапрос.Заказано) КАК Заказано,
	|	СУММА(ОбъединенныйЗапрос.ЗаказаноПодЗН) КАК ЗаказаноПодЗН,
	|	СУММА(ОбъединенныйЗапрос.ЗарезервированоПодЗН) КАК ЗарезервированоПодЗН
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		ТоварыВПроизводствеОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыВПроизводствеОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ТоварыВПроизводствеОстатки.КоличествоОстаток КАК ВПроизводстве,
	|		0 КАК Заказано,
	|		0 КАК ЗаказаноПодЗН,
	|		0 КАК ЗарезервированоПодЗН
	|	ИЗ
	|		РегистрНакопления.ТоварыВПроизводстве.Остатки(, ЗаказНаряд = &ЗаказНаряд"+ВиртТекстОтбора+") КАК ТоварыВПроизводствеОстатки
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
	|		ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		0,
	|		ЗаказыПокупателейОстатки.ЗаказаноОстаток-ЗаказыПокупателейОстатки.РезервОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(,
	|		"+?(РезервыПоЗаказНаряду, ТекстОтбораЗаказПокупателя+"Заказ В (ВЫБРАТЬ ТаблицаЗаказов.Регистратор ИЗ ТаблицаЗаказов КАК ТаблицаЗаказов)","
	|		("+ТекстОтбораЗаказПокупателя+"(Заказ В (ВЫБРАТЬ ТаблицаЗаказов.Регистратор ИЗ ТаблицаЗаказов КАК ТаблицаЗаказов)) ИЛИ (Контрагент=&Контрагент И (Номенклатура, ХарактеристикаНоменклатуры) В 
	|			(ВЫБРАТЬ Номенклатура, ХарактеристикаНоменклатуры ИЗ ТаблицаТоваровБезСкладов КАК ТаблицаТоваров)))")+") КАК ЗаказыПокупателейОстатки
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаказыПокупателейОстатки.Номенклатура,
	|		ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	|		0,
	|		0,
	|		ЗаказыПокупателейОстатки.ЗаказаноОстаток-ЗаказыПокупателейОстатки.РезервОстаток,
	|		ЗаказыПокупателейОстатки.РезервОстаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(,
	|		"+ТекстОтбораЗаказПокупателя+"Заказ В (ВЫБРАТЬ ТаблицаЗаказов.Регистратор ИЗ ТаблицаЗаказов КАК ТаблицаЗаказов)) КАК ЗаказыПокупателейОстатки) КАК ОбъединенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ОбъединенныйЗапрос.Номенклатура,
	|	ОбъединенныйЗапрос.ХарактеристикаНоменклатуры
	|	";
	
	Если ОтборПоСтроке Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Номенклатура",               Выборка.Номенклатура);
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
			НайденныеСтроки = КешИтоговойИнформацииЗН.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество()>0 Тогда
				СтрокаКеша = НайденныеСтроки[0];
			Иначе
				СтрокаКеша = КешИтоговойИнформацииЗН.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтрокаКеша, Выборка);
		КонецЦикла;
	Иначе 
		
		//++СнимченкоАА_бизтех++
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявкаНаРемонт")  Тогда
			Запрос.Текст = СтрЗаменить(запрос.Текст,"ЗаказыПокупателейОстатки.ЗаказаноОстаток-ЗаказыПокупателейОстатки.РезервОстаток","0");
		КонецЕсли;
		//--СнимченкоАА_бизтех--
		
		КешИтоговойИнформацииЗН = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Если ОтборПоСтроке Тогда
		ПересчитатьКеш(ПоСкладу, СтруктураОтбора, ИсходнаяТаблица);
	Иначе
		ПересчитатьКеш(ПоСкладу,, ИсходнаяТаблица);
	КонецЕсли;

КонецПроцедуры //ПолучитьКэшДанныхПоПеремещению()
//Процедура начального заполнения остатков номенклатуры на складах
Процедура ЗаполнитьОстаткиНоменклатурыНаСкладах() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток-ОстаткиТоваровКомпанииОстатки.РезервОстаток КАК Остаток
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки";
	Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Товары.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	КэшОстатковНоменклатурыНаСкладе=Запрос.Выполнить().Выгрузить();
КонецПроцедуры
// перезаполнение кеша
Процедура ПересчитатьКеш(ПоСкладу=Неопределено, СтруктураОтбора = Неопределено, ТаблицаТоваров = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(КэшДанныхПоПеремещению) = Тип("ТаблицаЗначений") Тогда
		ТипКоличества = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 3));
		КэшДанныхПоПеремещению = Новый ТаблицаЗначений;
		КэшДанныхПоПеремещению.Колонки.Добавить("Номенклатура",               Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		КэшДанныхПоПеремещению.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		КэшДанныхПоПеремещению.Колонки.Добавить("СкладКомпании",    Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
		КэшДанныхПоПеремещению.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	
		КэшДанныхПоПеремещению.Колонки.Добавить("Заказано",             ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("ЗаказаноПодЗН",        ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("ЗарезервированоПодЗН", ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("Зарезервировано",      ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("Коэффициент",          ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("ВПроизводстве",        ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("ОсталосьПереместить",  ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("ВозможноПереместить",  ТипКоличества);
		КэшДанныхПоПеремещению.Колонки.Добавить("ОстатокНаСкладе",      ТипКоличества);  
		КэшДанныхПоПеремещению.Колонки.Добавить("Ячейка",      Новый ОписаниеТипов("СправочникСсылка.ЯчейкиХранения"));
		
		//Индексы
		КэшДанныхПоПеремещению.Индексы.Добавить("Номенклатура");
		КэшДанныхПоПеремещению.Индексы.Добавить("ХарактеристикаНоменклатуры");   
		КэшДанныхПоПеремещению.Индексы.Добавить("Ячейка");
	КонецЕсли;
	
	Если ТипЗнч(СтруктураОтбора) = Тип("Структура") Тогда
		
		ТекОтбор = Новый Структура;
		Если СтруктураОтбора.Свойство("Номенклатура") Тогда
			ТекОтбор.Вставить("Номенклатура", СтруктураОтбора.Номенклатура);
		КонецЕсли;
		Если СтруктураОтбора.Свойство("ХарактеристикаНоменклатуры") Тогда
			ТекОтбор.Вставить("ХарактеристикаНоменклатуры", СтруктураОтбора.ХарактеристикаНоменклатуры);
		КонецЕсли;
		СтрокиДляУдаления = КэшДанныхПоПеремещению.НайтиСтроки(ТекОтбор);
		Для Каждого ТекСтрока Из СтрокиДляУдаления Цикл
			КэшДанныхПоПеремещению.Удалить(ТекСтрока);
		КонецЦикла;
		Если ТаблицаТоваров = Неопределено Тогда
			ИсходнаяТаблица = Товары.НайтиСтроки(ТекОтбор);
		Иначе
			ИсходнаяТаблица = ТаблицаТоваров.НайтиСтроки(ТекОтбор);
		КонецЕсли;
		ТаблицаПоЗН         = КешИнформацииЗН.Скопировать(КешИнформацииЗН.НайтиСтроки(ТекОтбор));
		ИтоговаяТаблицаПоЗН = КешИтоговойИнформацииЗН.Скопировать(КешИтоговойИнформацииЗН.НайтиСтроки(ТекОтбор));
		
		Если ТаблицаПоЗН.Количество() = 0 И ИтоговаяТаблицаПоЗН.Количество() = 0 И ИсходнаяТаблица.Количество()>0 Тогда
			ПолучитьКэшДанныхПоПеремещению(, , ТекОтбор, ТаблицаТоваров);
			Возврат;
		КонецЕсли;
	Иначе
		КэшДанныхПоПеремещению.Очистить();
		Если ТаблицаТоваров = Неопределено Тогда
			ИсходнаяТаблица = Товары;
		Иначе
			ИсходнаяТаблица = ТаблицаТоваров;
		КонецЕсли;
		ТаблицаПоЗН         = КешИнформацииЗН.Скопировать();
		ИтоговаяТаблицаПоЗН = КешИтоговойИнформацииЗН.Скопировать();
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из ИсходнаяТаблица Цикл
		
		СтруктураПоиска = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", СтрокаТЧ.Номенклатура, СтрокаТЧ.ХарактеристикаНоменклатуры);
		СтрокиПроизводства = ИтоговаяТаблицаПоЗН.НайтиСтроки(СтруктураПоиска);
		Если СтрокиПроизводства.Количество() = 0 Тогда
			ВПроизводстве = 0;
		Иначе
			ВПроизводстве = СтрокиПроизводства[0].ВПроизводстве;
		КонецЕсли;
		
		Заказано             = 0;
		ЗаказаноПодЗН        = 0;
		ЗарезервированоПодЗН = 0;
		СтрокиЗаказов = КешИтоговойИнформацииЗН.НайтиСтроки(СтруктураПоиска);
		Если СтрокиЗаказов.Количество()>0 Тогда
			ПерваяСтрока = СтрокиЗаказов[0];
			Заказано             = ПерваяСтрока.Заказано;
			ЗаказаноПодЗН        = ПерваяСтрока.ЗаказаноПодЗН;
			ЗарезервированоПодЗН = ПерваяСтрока.ЗарезервированоПодЗН;
		КонецЕсли;
		
		Если ПоСкладу = Неопределено Тогда
			СтруктураПоиска.Вставить("СкладКомпании", СтрокаТЧ.СкладКомпании);
		Иначе
			СтруктураПоиска.Вставить("СкладКомпании", ПоСкладу);
		КонецЕсли;
		
		РесурсыСтроки  = ТаблицаПоЗН.НайтиСтроки(СтруктураПоиска);
		Если РесурсыСтроки.Количество()=0 Тогда
			
			//Если ПоСкладу = Неопределено ИЛИ СтрокаТЧ.СкладКомпании = ПоСкладу Тогда
				НоваяСтрока = КэшДанныхПоПеремещению.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
				НоваяСтрока.ОсталосьПереместить  = СтрокаТЧ.Количество*СтрокаТЧ.Коэффициент;
				НоваяСтрока.Заказано             = Заказано;
				НоваяСтрока.ЗаказаноПодЗН        = ЗаказаноПодЗН;
				НоваяСтрока.ЗарезервированоПодЗН = ЗарезервированоПодЗН;
			//КонецЕсли;
			
		Иначе
			
			СтрокаРесурсов = РесурсыСтроки[0];
			Количество = СтрокаТЧ.Количество*СтрокаТЧ.Коэффициент;
			
			ВозможноПереместить      = СтрокаРесурсов.КоличествоОстаток;
			ВПроизводствеПоИсточнику = СтрокаРесурсов.ПеремещеноСоСклада;
			ВПроизводствеПоИсточнику = Мин(ВПроизводстве, ВПроизводствеПоИсточнику, Количество);
			ОсталосьПереместить      = Количество - ВПроизводствеПоИсточнику;
			ВозможноПереместить      = Мин(ОсталосьПереместить, ВозможноПереместить);
			
			СтрокаРесурсов.ПеремещеноСоСклада = СтрокаРесурсов.ПеремещеноСоСклада - ВПроизводствеПоИсточнику;
			СтрокаРесурсов.КоличествоОстаток  = СтрокаРесурсов.КоличествоОстаток - ВозможноПереместить;
			
			Если СтрокиПроизводства.Количество() > 0 Тогда
				Если ВПроизводстве = ВПроизводствеПоИсточнику Тогда
					ИтоговаяТаблицаПоЗН.Удалить(СтрокиПроизводства[0]);
				Иначе
					СтрокиПроизводства[0].ВПроизводстве = ВПроизводстве - ВПроизводствеПоИсточнику;
				КонецЕсли;
			КонецЕсли;
			
			//Если ПоСкладу = Неопределено ИЛИ СтрокаТЧ.СкладКомпании = ПоСкладу Тогда
				НоваяСтрока = КэшДанныхПоПеремещению.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРесурсов);
				НоваяСтрока.ЕдиницаИзмерения     = СтрокаТЧ.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент          = СтрокаТЧ.Коэффициент;
				НоваяСтрока.Заказано             = Заказано;
				НоваяСтрока.ЗаказаноПодЗН        = ЗаказаноПодЗН;
				НоваяСтрока.ЗарезервированоПодЗН = ЗарезервированоПодЗН;
				НоваяСтрока.ВПроизводстве        = ВПроизводствеПоИсточнику;
				НоваяСтрока.ОсталосьПереместить  = ОсталосьПереместить;
				НоваяСтрока.ВозможноПереместить  = ВозможноПереместить;
			//КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из ИтоговаяТаблицаПоЗН Цикл
		СтрокиКеша = КэшДанныхПоПеремещению.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", СтрокаТЧ.Номенклатура, СтрокаТЧ.ХарактеристикаНоменклатуры));
		Если СтрокиКеша.Количество()>0 Тогда
			ПерваяСтрокаКеша = СтрокиКеша[0];
			ВПроизводстве = СтрокаТЧ.ВПроизводстве;
			Для Каждого СтрокаКеша Из СтрокиКеша Цикл
				Если ВПроизводстве = 0 Тогда
					Прервать;
				КонецЕсли;
				Если СтрокаКеша.ОсталосьПереместить = 0 Тогда
					Продолжить;
				КонецЕсли;
				ОсталосьПереместить            = СтрокаКеша.ОсталосьПереместить;
				ДополнительноеПеремещение      = Мин(ОсталосьПереместить, ВПроизводстве);
				ОсталосьПереместить            = ОсталосьПереместить-ДополнительноеПеремещение;
				СтрокаКеша.ОсталосьПереместить = ОсталосьПереместить;
				СтрокаКеша.ВозможноПереместить = Мин(ОсталосьПереместить, СтрокаКеша.ВозможноПереместить);
				СтрокаКеша.ВПроизводстве       = СтрокаКеша.ВПроизводстве + ДополнительноеПеремещение;
				ВПроизводстве = ВПроизводстве  - ДополнительноеПеремещение;
			КонецЦикла;
			ПерваяСтрокаКеша.ВПроизводстве = ПерваяСтрокаКеша.ВПроизводстве + ВПроизводстве;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПересчитатьКеш()  
// возвращает строку кеша по параметрам
Функция ПолучитьСтрокуКешаПеремещения(Номенклатура, ХарактеристикаНоменклатуры, СкладКомпании, ЕдиницаИзмерения, ИсходнаяТаблица = Неопределено) Экспорт
	//Если выборка еще не осуществлялась - загрузим ее
	СтруктураОтбора = Новый Структура();
	СтруктураОтбораКеша   = Новый Структура();
	Если НЕ обЗначениеНеЗаполнено(Номенклатура) Тогда
		СтруктураОтбора.Вставить("Номенклатура", Номенклатура);
		СтруктураОтбораКеша.Вставить("Номенклатура", Номенклатура);
	КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры) Тогда
		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
		СтруктураОтбораКеша.Вставить("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
	КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
		СтруктураОтбора.Вставить("СкладКомпании", СкладКомпании);
		СтруктураОтбораКеша.Вставить("СкладКомпании", СкладКомпании);
	КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
		СтруктураОтбора.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	КонецЕсли;
	
	Результат = Новый Массив;
	Если СтруктураОтбора.Количество()=0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если КэшДанныхПоПеремещению=Неопределено Тогда
		ПолучитьКэшДанныхПоПеремещению(,,,ИсходнаяТаблица);
		Результат = КэшДанныхПоПеремещению.НайтиСтроки(СтруктураОтбора);
	Иначе
		Результат = КэшДанныхПоПеремещению.НайтиСтроки(СтруктураОтбора);
		Если Результат.Количество()=0 Тогда
			Результат = КэшДанныхПоПеремещению.НайтиСтроки(СтруктураОтбораКеша);
			Если Результат.Количество()=0 Тогда
				ПолучитьКэшДанныхПоПеремещению(,,СтруктураОтбораКеша, ИсходнаяТаблица);
			Иначе
				ПересчитатьКеш(, СтруктураОтбораКеша, ИсходнаяТаблица);
			КонецЕсли;
			Результат = КэшДанныхПоПеремещению.НайтиСтроки(СтруктураОтбора);
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции 
Функция ПолучитьОстаткиНоменклатурыНаСкладах(Знач СкладКомпании,Номенклатура,ХарактеристикаНоменклатуры) Экспорт
	//Будем считать что остаток нулевой
	ОстатокНоменклатуры=0;
	СтруктураОтбора = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",Номенклатура,ХарактеристикаНоменклатуры);
	Если Не ЗначениеЗаполнено(СкладКомпании) Тогда
		СкладКомпании=Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	СтруктураОтбора.Вставить("СкладКомпании", СкладКомпании);
	//Попробуем найти номенклатуру в кэше
	МассивСтрок=КэшОстатковНоменклатурыНаСкладе.НайтиСтроки(СтруктураОтбора);
	Если МассивСтрок.Количество()=0 Тогда
		//В кэше номенклатура не найдена - добавим остатки номенклатуры из регистра
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток-ОстаткиТоваровКомпанииОстатки.РезервОстаток КАК Остаток
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(,"+?(ЗначениеЗаполнено(СкладКомпании),"СкладКомпании=&СкладКомпании И ","")+" Номенклатура = &Номенклатура И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ОстаткиТоваровКомпанииОстатки
		|";
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
		ВыборкаОстатковНоменклатурыНаСкладах=Запрос.Выполнить().Выбрать();
		Если ВыборкаОстатковНоменклатурыНаСкладах.Следующий() Тогда
			ОстатокНоменклатуры=ВыборкаОстатковНоменклатурыНаСкладах.Остаток;
		КонецЕсли; 
		НоваяСтрокаКеша=КэшОстатковНоменклатурыНаСкладе.Добавить();
		НоваяСтрокаКеша.СкладКомпании=СкладКомпании;
		НоваяСтрокаКеша.Номенклатура=Номенклатура;
		НоваяСтрокаКеша.ХарактеристикаНоменклатуры=ХарактеристикаНоменклатуры;
		НоваяСтрокаКеша.Остаток = ОстатокНоменклатуры;
	Иначе
		ОстатокНоменклатуры = МассивСтрок[0].Остаток;
	КонецЕсли;

	Возврат ОстатокНоменклатуры;
КонецФункции
//--СнимченкоАА_бизтех--

//БИЗТЕХ КОВАЛЬ 08.07.2025 ++
Функция ПолучитьСкладПоПодразделению() Экспорт
	Если ЗначениеЗаполнено(СкладПоПодразделению) Тогда
		Возврат СкладПоПодразделению;
	Иначе
		СкладПоПодразделению = РегистрыСведений.СкладПоУмолчаниюПоПодразделению.ПолучитьСкладПоПодразделению(ПодразделениеКомпании);
		Возврат СкладПоПодразделению;
	КонецЕсли;
КонецФункции
//БИЗТЕХ КОВАЛЬ 08.07.2025 --

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

ОбработкаРасчетСкидок=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.ИмяРеквизитаСкладКомпании="Цех";
ОбработкаРасчетСкидокАвторабот=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаСкладКомпании="Цех";
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаСкидкаНаценка="СкидкаНаценкаРаботы";
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаЗначениеСкидкиНаценки="ЗначениеСкидкиНаценкиРабот";
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаСуммаСкидкиНаценки="СуммаСкидкиНаценкиРабот";
ОбработкаРасчетСкидокАвторабот.ИмяТабличнойЧасти="Работы";
ОбработкаРасчетСкидокАвторабот.СкидкаНаРаботы=Истина;

ФорматПредставленияГодаВыпускаАвтомобиля=орПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Права);

ТоварыБезНДС = Ложь;
РаботыБезНДС = Ложь;

// Модуль документа Выписка

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем СообщениеОбОшибке Экспорт;	// Переменная для накопления сообщений. Если равна "Неопределено", то сообщения выводятся на экран, иначе накапливаются в переменной
Перем ОбработкаРасчетСкидок Экспорт;              // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
Перем ЗначениеСкидкиНаценки Экспорт;
Перем СуммаСкидкиНаценки Экспорт;


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы состава
	ОбязательныеСостав=Новый Структура();
	ОбязательныеСостав.Вставить("СтатьяДДС",1);
	ОбязательныеСостав.Вставить("Контрагент",1);
	ОбязательныеСостав.Вставить("ДоговорВзаиморасчетов",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("БанковскийСчет",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	Если ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
		ОбязательныеРеквизиты.Вставить("СтатьяДДС",1);
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
		ОбязательныеРеквизиты.Вставить("СтавкаНДС",1);
		Если ДляПробитияНаФР Тогда
			ОбязательныеРеквизиты.Вставить("ТипСпособаРасчетаККТ",1);
			ОбязательныеРеквизиты.Вставить("ТипРасчета", 1);
		КонецЕсли;
		ОбязательныеТовары=Новый Структура();
		ОбязательныеТовары.Вставить("Номенклатура",3);
		ОбязательныеТовары.Вставить("Количество",1);
		ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
		ОбязательныеТовары.Вставить("Коэффициент",1);
		ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
		ОбязательныеТовары.Вставить("ГТД", 2);
		ОбязательныеТовары.Вставить("ДоговорВзаиморасчетов", 2);
		ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Состав",ОбязательныеСостав);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Если ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
		Если СуммаДокументаПриход<>Состав.Итог("СуммаПриход") Тогда
			дкДобавитьОшибку(Ошибки, "Сумма прихода документа не равна сумме прихода платежей.");
			Результат = Ложь;
		КонецЕсли;
		Если СуммаДокументаРасход<>Состав.Итог("СуммаРасход") Тогда
			дкДобавитьОшибку(Ошибки, "Сумма расхода документа не равна сумме расхода платежей.");
			Результат = Ложь;
		КонецЕсли;
		Если СуммаНДС<>Состав.Итог("СуммаНДС") Тогда
			дкДобавитьОшибку(Ошибки, "Сумма НДС документа не равна сумме НДС платежей.");
			Результат = Ложь;
		КонецЕсли;
		Если СуммаУслуг<>Состав.Итог("СуммаУслуг") Тогда
			дкДобавитьОшибку(Ошибки, "Сумма банковских услуг документа не равна сумме услуг платежей.");
			Результат = Ложь;
		КонецЕсли;
		
		Для Каждого стрСостав Из Состав Цикл
			Если обЗначениеНеЗаполнено(стрСостав.Сделка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если дкДокументЕстьРеквизитШапки("Контрагент", стрСостав.Сделка.Метаданные().Имя) И стрСостав.Контрагент <> стрСостав.Сделка.Контрагент Тогда
				дкДобавитьОшибку(Ошибки, "В строке №" + стрСостав.НомерСтроки + " контрагент сделки не совпадает с контрагентом документа.");
				Результат = Ложь;
			КонецЕсли;
			Если дкДокументЕстьРеквизитШапки("ДоговорВзаиморасчетов", стрСостав.Сделка.Метаданные().Имя) И стрСостав.ДоговорВзаиморасчетов <> стрСостав.Сделка.ДоговорВзаиморасчетов Тогда
				дкДобавитьОшибку(Ошибки, "В строке №" + стрСостав.НомерСтроки + " договор сделки не совпадает с договором документа.");
				Результат = Ложь;
			КонецЕсли; 
			//Аляль БизТех 08.09.2025г. добавляем проверку Договора КСО  <<
			Если дкДокументЕстьРеквизитШапки("ДоговорКСО", стрСостав.Сделка.Метаданные().Имя) И стрСостав.ДоговорВзаиморасчетов <> стрСостав.Сделка.ДоговорКСО Тогда
				дкДобавитьОшибку(Ошибки, "В строке №" + стрСостав.НомерСтроки + " договор сделки не совпадает с договором документа.");
				Результат = Ложь;
			КонецЕсли; 
			//>>	
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого стрСостав Из Состав Цикл
		Если стрСостав.СуммаПриход < стрСостав.СуммаУслуг И стрСостав.СуммаРасход < стрСостав.СуммаУслуг Тогда
			дкДобавитьОшибку(Ошибки, "В строке №" + стрСостав.НомерСтроки + " сумма услуг превышает сумму платежа.");
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыБанковскиеСчета = Новый Структура();
				КонтролируемыеРеквизитыБанковскиеСчета.Вставить("БанковскийСчет");
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыБанковскиеСчета",КонтролируемыеРеквизитыБанковскиеСчета);
			КонецЕсли;
			
			КонтролируемыеРеквизитыСостава = Новый Структура();
			КонтролируемыеРеквизитыСостава.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Состав",КонтролируемыеРеквизитыСостава);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти); 	
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Если Товары.Количество() > 0 И ДляПробитияНаФР Тогда
		
		Если (СуммаДокументаПриход + СуммаДокументаРасход) > 0 И Товары.Итог("СуммаОплаты") = 0 Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки, "Необходимо произвести пропорциональное распределение суммы вносимой оплаты между предметами платежа");
		ИначеЕсли (СуммаДокументаПриход + СуммаДокументаРасход) <> Товары.Итог("СуммаОплаты") Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки, "Неверно заполнена колонка ""Сумма оплаты"" в табличной части товары, необходимо распределить сумму оплаты");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	Результат = Новый СписокЗначений;
	Результат.Добавить(Метаданные.Документы.Выписка.ТабличныеЧасти.Состав);
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("БанковскаяВыписка","Банковская выписка",Истина);
	СписокХозОпераций.Добавить("СтрокаБанковскойВыписки","Строка банковской выписки",Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="ВалютаДокумента" Тогда
		// пересчитаем курсы взаиморасчетов в табличной части "Состав"
		Для Каждого ТекСтрока Из Состав Цикл
			Если обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
				Продолжить;
			КонецЕсли;
			спрСтрокаСоставВалютаВзаиморасчетов = ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			// если валюта взаиморасчета в строке и валюта документа совпадают,
			// установим курс взаиморасчета равным курсу документа
			Если спрСтрокаСоставВалютаВзаиморасчетов = ВалютаДокумента Тогда
				ТекСтрока.КурсВалютыВзаиморасчетов = КурсДокумента;
				
			КонецЕсли;
		КонецЦикла; 
		
	ИначеЕсли Имя="Организация" ИЛИ Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		//Проставим ставки НДС для исходящих платежей
		ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Если ОсвобожденОтНДС Тогда
			НоваяСтавкаНДС=Справочники.СтавкиНДС.БезНДС;
		Иначе
			НоваяСтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Для Каждого СтрокаТЧ Из Состав Цикл
			Если СтрокаТЧ.СуммаРасход<>0 И СтрокаТЧ.СтавкаНДС<>НоваяСтавкаНДС Тогда
				СтрокаТЧ.СтавкаНДС=НоваяСтавкаНДС;
				Рез=ОбработкаРеквизита("Состав.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЦикла;
		
		Если Имя="Организация" Тогда
			ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
			ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
			ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
			ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
			ЭтотОбъект.Патент = Неопределено;
			Если ВидимостьПатент Тогда
				ЭтаФорма.ИспользоватьПатент = Ложь;
				ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="СтатьяДДС" Тогда
		Рез=Истина;
		Если СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Приход Тогда
			Если СуммаДокументаРасход<>0 Тогда
				СуммаДокументаРасход=0;
				Если ДопПараметры=Неопределено Тогда
					ДопПараметры=Новый Структура;
				КонецЕсли; 
				ДопПараметры.Вставить("ИзменениеНаправленияПлатежа",Истина);
				Рез=ОбработкаРеквизита("СуммаДокументаРасход",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		ИначеЕсли СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Расход Тогда
			Если СуммаДокументаПриход<>0 Тогда
				СуммаДокументаПриход=0;
				Если ДопПараметры=Неопределено Тогда
					ДопПараметры=Новый Структура;
				КонецЕсли; 
				ДопПараметры.Вставить("ИзменениеНаправленияПлатежа",Истина);
				Рез=ОбработкаРеквизита("СуммаДокументаПриход",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли;
		Если СтатьяДДС = Справочники.СтатьиДДС.ОплатаБанковскихУслуг Тогда
			СуммаУслуг = 0;
		КонецЕсли;
		
		Для Каждого СтрокаТЧ Из Состав Цикл
		    Если СтрокаТЧ.СтатьяДДС<>СтатьяДДС Тогда
				СтрокаТЧ.СтатьяДДС=СтатьяДДС;
				Рез=ОбработкаРеквизита("Состав.СтатьяДДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЦикла; 
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		//Параметры для подстановки или создания договора контрагента
		Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
		Если СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Приход Тогда
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.Продажа);
		ИначеЕсли СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Расход Тогда
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.Покупка);
		Иначе
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.ПустаяСсылка());
		КонецЕсли;
		ДопПараметры.Вставить("ВалютаВзаиморасчетов",ВалютаДокумента);
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Для Каждого СтрокаТЧ Из Состав Цикл
		    Если СтрокаТЧ.Контрагент<>Контрагент Тогда
				СтрокаТЧ.Контрагент=Контрагент;
				Рез=ОбработкаРеквизита("Состав.Контрагент",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЦикла; 
		Возврат Рез;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		
		//<<Павличенко 21.07.2020
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов)
			И НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов.СтатьяДДС) И Ссылка.СуммаДокументаРасход > 0 Тогда
			СтатьяДДС = ДоговорВзаиморасчетов.СтатьяДДС;
			Рез=ОбработкаРеквизита("СтатьяДДС",СтрокаТЧ,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов)
			И НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов.СтатьяДДСПриход) И Ссылка.СуммаДокументаПриход > 0 Тогда
			СтатьяДДС = ДоговорВзаиморасчетов.СтатьяДДСПриход;
			Рез=ОбработкаРеквизита("СтатьяДДС",СтрокаТЧ,ЭтаФорма,ДопПараметры);
		КонецЕсли;	
		//>>Павличенко 21.07.2020
		
		Для Каждого СтрокаТЧ Из Состав Цикл
		    Если СтрокаТЧ.ДоговорВзаиморасчетов<>ДоговорВзаиморасчетов Тогда
				СтрокаТЧ.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
				Рез=ОбработкаРеквизита("Состав.ДоговорВзаиморасчетов",СтрокаТЧ,ЭтаФорма,ДопПараметры);
			КонецЕсли; 
		КонецЦикла;
		
		Если ДоговорВзаиморасчетов.ВидДоговора = Перечисления.ВидыДоговоров.Зарплата
			И СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Расход И Состав.Количество() = 0 Тогда
			
			СтруктураОтбора=Новый Структура();
			СтруктураОтбора.Вставить("Контрагент", Контрагент);
			СтруктураОтбора.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
			тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(,СтруктураОтбора,,"Сумма,СуммаУпр,СуммаБаз");
			ВалютаДоговора = ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			ВалютаБаз=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			Если ВалютаДокумента=ВалютаДоговора Тогда
				СуммаДокументаРасход = -тзДолги.Итог("Сумма");
			ИначеЕсли ВалютаДокумента=ВалютаБаз Тогда
				СуммаДокументаРасход = -тзДолги.Итог("СуммаБаз");
			ИначеЕсли ВалютаДокумента=ВалютаУпр Тогда
				СуммаДокументаРасход = -тзДолги.Итог("СуммаУпр");
			Иначе
				СтруктураКурса = обКурсДляВалюты(ВалютаДоговора,Дата);
				Курс           = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
				СуммаДокументаРасход = обПересчет(-тзДолги.Итог("Сумма"),ВалютаДоговора,Курс,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			Если СуммаДокументаРасход < 0 Тогда
				СуммаДокументаРасход = 0;
			КонецЕсли;
			ОбработкаРеквизита("СуммаДокументаРасход",ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаДокументаПриход" ИЛИ Имя="СуммаДокументаРасход" Тогда
		Рез=ОбработкаРеквизита("СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="СтавкаНДС" Тогда
		Рез=Истина;
		Сумма=СуммаДокументаПриход+СуммаДокументаРасход;
		СуммаНДС=Окр((Сумма*СтавкаНДС.Ставка)/(100+СтавкаНДС.Ставка),2);
		Если ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
			Для Каждого СтрокаТЧ Из Состав Цикл
			    Если СтрокаТЧ.СтавкаНДС<>СтавкаНДС Тогда
					СтрокаТЧ.СтавкаНДС=СтавкаНДС;
					Рез=ОбработкаРеквизита("Состав.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="СуммаНДС" Тогда
		Рез=Истина;
		Возврат Рез;
		
	ИначеЕсли Имя = "ДляПробитияНаФР" Тогда
		Рез = Истина;
		
		Если ДляПробитияНаФР И НЕ ЗначениеЗаполнено(ТипРасчета) Тогда
			ТипРасчета = дкТипРасчетаДокумента(ЭтотОбъект);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Состав.СтатьяДДС" Тогда
		Рез=Истина;
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.СтатьяДДС) Тогда
			Если ТекСтрока.СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Приход Тогда
				Если ТекСтрока.СуммаРасход<>0 Тогда
					ТекСтрока.СуммаРасход=0;
					Если ДопПараметры=Неопределено Тогда
                    	ДопПараметры=Новый Структура;
					КонецЕсли; 
					ДопПараметры.Вставить("ИзменениеНаправленияПлатежа",Истина);
					Рез=ОбработкаРеквизита("Состав.СуммаРасход",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			ИначеЕсли ТекСтрока.СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Расход Тогда
				Если ТекСтрока.СуммаПриход<>0 Тогда
					ТекСтрока.СуммаПриход=0;
					Если ДопПараметры=Неопределено Тогда
                    	ДопПараметры=Новый Структура;
					КонецЕсли; 
					ДопПараметры.Вставить("ИзменениеНаправленияПлатежа",Истина);
					Рез=ОбработкаРеквизита("Состав.СуммаПриход",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли; 
			КонецЕсли;
			Если ТекСтрока.СтатьяДДС=Справочники.СтатьиДДС.ОплатаБанковскихУслуг Тогда
				Если ТекСтрока.СуммаУслуг <> 0 Тогда
					ТекСтрока.СуммаУслуг=0;
					Рез=ОбработкаРеквизита("Состав.СуммаУслуг",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Состав.Контрагент" Тогда
		Рез=Истина;
		Если обЗначениеНеЗаполнено(ТекСтрока.Контрагент) Тогда 
			// если контрагент не указан то очистим договор
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
				ТекСтрока.ДоговорВзаиморасчетов=Неопределено;
			КонецЕсли; 
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ПлатежноеПоручениеОснование) Тогда
				ТекСтрока.ПлатежноеПоручениеОснование=Неопределено;
			КонецЕсли; 
		Иначе
			// установим договор по умолчанию
			ВидДоговора = Неопределено;
			Если СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Приход Тогда
				ВидДоговора = Перечисления.ВидыДоговоров.Продажа;
			ИначеЕсли СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Расход Тогда
				ВидДоговора = Перечисления.ВидыДоговоров.Покупка;
			КонецЕсли;
			
			ТекСтрока.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ТекСтрока.Контрагент, ВидДоговора, ЭтотОбъект, ДопПараметры);
			Рез = ОбработкаРеквизита("Состав.ДоговорВзаиморасчетов", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
			
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.ПлатежноеПоручениеОснование)) И
				 (ТипЗнч(ТекСтрока.ПлатежноеПоручениеОснование)=Тип("ДокументСсылка.ПлатежноеПоручение")) Тогда
				Если ТекСтрока.Контрагент<>ТекСтрока.ПлатежноеПоручениеОснование.Контрагент Тогда
					ТекСтрока.ПлатежноеПоручениеОснование=Неопределено;
					ТекСтрока.НазначениеПлатежа=Неопределено;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		//Проставим ставки НДС для входящих платежей
		ОсвобожденОтНДС=ТекСтрока.Контрагент.ОсвобожденОтНДС;
		Если ОсвобожденОтНДС Тогда
			НоваяСтавкаНДС=Справочники.СтавкиНДС.БезНДС;
		Иначе
			НоваяСтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Если ТекСтрока.СуммаПриход<>0 И ТекСтрока.СтавкаНДС<>НоваяСтавкаНДС Тогда
			ТекСтрока.СтавкаНДС=НоваяСтавкаНДС;
			Рез=ОбработкаРеквизита("Состав.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Иначе
			ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
			Если ОсвобожденОтНДС Тогда
				НоваяСтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				НоваяСтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Если ТекСтрока.СуммаРасход<>0 И ТекСтрока.СтавкаНДС<>НоваяСтавкаНДС Тогда
				ТекСтрока.СтавкаНДС=НоваяСтавкаНДС;
				Рез=ОбработкаРеквизита("Состав.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;
		КонецЕсли; 
		Возврат Рез;
		
	ИначеЕсли Имя="Состав.ДоговорВзаиморасчетов" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) Тогда
			ТекСтрока.АвтоЗакрытиеСделок=ТекСтрока.ДоговорВзаиморасчетов.АвтоЗакрытиеСделок;
			ТекСтрока.КурсВалютыВзаиморасчетов=дкПолучитьКурсВалютыВзаиморасчетов(ЭтотОбъект,ТекСтрока.ДоговорВзаиморасчетов);
			
			//<<Павличенко 21.07.2020
			//Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов.СтатьяДДС) Тогда
			//	ТекСтрока.СтатьяДДС = ТекСтрока.ДоговорВзаиморасчетов.СтатьяДДС;
			//	Рез=ОбработкаРеквизита("Состав.СтатьяДДС",ТекСтрока,ЭтаФорма,ДопПараметры);
			//КонецЕсли; 
			
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов.СтатьяДДС) 
				 И Ссылка.СуммаДокументаРасход > 0 Тогда
				ТекСтрока.СтатьяДДС = ТекСтрока.ДоговорВзаиморасчетов.СтатьяДДС;
				Рез=ОбработкаРеквизита("Состав.СтатьяДДС",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов.СтатьяДДС) 
				И Ссылка.СуммаДокументаПриход > 0 Тогда
				ТекСтрока.СтатьяДДС = ТекСтрока.ДоговорВзаиморасчетов.СтатьяДДСПриход;
				Рез=ОбработкаРеквизита("Состав.СтатьяДДС",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
			//>>Павличенко 21.07.2020   
			
		КонецЕсли;
		Если ТекСтрока.ДоговорВзаиморасчетов.ВидДоговора = Перечисления.ВидыДоговоров.Зарплата
			И ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Расход Тогда
			Если ЗначениеЗаполнено(ДокументОснование) Тогда
				ТекСтрока.Сделка = ДокументОснование;
			КонецЕсли;
			ТекСтрока.АвтоЗакрытиеСделок = Истина;
		КонецЕсли;
		Если (ТекСтрока.СуммаПриход = 0 И ТекСтрока.СуммаРасход = 0) ТОгда
			Рез=ОбработкаРеквизита("Состав.РассчитатьСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			Рез=Истина;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Состав.Сделка" Тогда
		Попытка
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Сделка) Тогда
				Если ХозОперация = Справочники.ХозОперации.БанковскаяВыписка Тогда
					ТекСтрока.Контрагент=ТекСтрока.Сделка.Контрагент;
					ОбработкаРеквизита("Состав.Контрагент",ТекСтрока, ЭтаФорма, ДопПараметры);
					ТекСтрока.ДоговорВзаиморасчетов=ТекСтрока.Сделка.ДоговорВзаиморасчетов;
					ОбработкаРеквизита("Состав.ДоговорВзаиморасчетов",ТекСтрока, ЭтаФорма, ДопПараметры);
				Иначе
					Контрагент=ТекСтрока.Сделка.Контрагент;
					ОбработкаРеквизита("Контрагент",ТекСтрока, ЭтаФорма, ДопПараметры);
					ДоговорВзаиморасчетов=ТекСтрока.Сделка.ДоговорВзаиморасчетов;
					ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока, ЭтаФорма, ДопПараметры);
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки;
		Рез=ОбработкаРеквизита("Состав.РассчитатьСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
		// Получим сумму НДС
		Если ЗначениеЗаполнено(текСтрока.Сделка)
			И (ТекСтрока.СуммаПриход > 0 ИЛИ ТекСтрока.СуммаРасход > 0) Тогда
			СтруктураСтроки = Новый Структура();
			СтруктураСтроки.Вставить("СтавкаНДС", ТекСтрока.СтавкаНДС);
			СтруктураСтроки.Вставить("СуммаНДС",  ТекСтрока.СуммаНДС);
			СтруктураСтроки.Вставить("ВалютаДокумента", ВалютаДокумента);
			СтруктураСтроки.Вставить("КурсДокумента", КурсДокумента);
			дкСформироватьСуммуНДСДокументаОтгрузки(СтруктураСтроки, ТекСтрока.Сделка, ?(ТекСтрока.СуммаПриход = 0, ТекСтрока.СуммаРасход, ТекСтрока.СуммаПриход));
			ТекСтрока.СуммаНДС = СтруктураСтроки.СуммаНДС;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Состав.СтатьяДДС" Тогда
		ТекСтрока.ПлатежноеПоручениеОснование = "";
		ТекСтрока.НазначениеПлатежа           = "";
		Возврат Истина;
		
	ИначеЕсли Имя="Состав.СуммаПриход" ИЛИ Имя="Состав.СуммаРасход" Тогда
		Попытка
			ИзменениеНаправленияПлатежа=ДопПараметры.ИзменениеНаправленияПлатежа;	
		Исключение
			ИзменениеНаправленияПлатежа=Ложь;
		КонецПопытки;
		Если ИзменениеНаправленияПлатежа Тогда
			Если ТекСтрока.СуммаПриход<>0 Тогда
				//Проставим ставку НДС для входящего платежа
				ОсвобожденОтНДС=ТекСтрока.Контрагент.ОсвобожденОтНДС;
				Если ОсвобожденОтНДС Тогда
					НоваяСтавкаНДС=Справочники.СтавкиНДС.БезНДС;
				Иначе
					НоваяСтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				КонецЕсли;
				Если ТекСтрока.СтавкаНДС<>НоваяСтавкаНДС Тогда
					ТекСтрока.СтавкаНДС=НоваяСтавкаНДС;
				КонецЕсли;
			ИначеЕсли ТекСтрока.СуммаРасход<>0 Тогда
				//Проставим ставку НДС для исходящего платежа
				ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
				Если ОсвобожденОтНДС Тогда
					НоваяСтавкаНДС=Справочники.СтавкиНДС.БезНДС;
				Иначе
					НоваяСтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				КонецЕсли;
				Если ТекСтрока.СтавкаНДС<>НоваяСтавкаНДС Тогда
					ТекСтрока.СтавкаНДС=НоваяСтавкаНДС;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если НЕ ТекСтрока.СтатьяДДС = Справочники.СтатьиДДС.ОплатаБанковскихУслуг Тогда
			СуммаПлатежа = ТекСтрока.СуммаРасход + ТекСтрока.СуммаПриход;
			Если СуммаПлатежа < ТекСтрока.СуммаУслуг Тогда
				ТекСтрока.СуммаУслуг = СуммаПлатежа;
				#Если Клиент Тогда
					Предупреждение("Сумма прихода/расхода не может быть меньше суммы комиссии банка!");
				#КонецЕсли
			КонецЕсли;
			ТекСтрока.ПроцентКомиссии = ?(ТекСтрока.СуммаУслуг = 0 ИЛИ СуммаПлатежа = 0, 0, ТекСтрока.СуммаУслуг / СуммаПлатежа * 100);
		КонецЕсли;
		ТекСтрока.СуммаРазница = ?(ТекСтрока.СуммаРасход <> 0 ИЛИ ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Расход, ТекСтрока.СуммаРасход, ТекСтрока.СуммаПриход) - ТекСтрока.СуммаУслуг;
		
		//KamikadZe begin add 22.11.2016 12:24:11
		Если обПраво("ЭквайрингПерерасчетКомиссии") Тогда
			Если ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Приход Тогда
				ТекСтрока.ПроцентКомиссии = ?(ТекСтрока.СуммаУслуг = 0 ИЛИ СуммаПлатежа = 0, 0, ТекСтрока.СуммаУслуг / (СуммаПлатежа + ТекСтрока.СуммаУслуг) * 100);
				ТекСтрока.СуммаРазница = ?(ТекСтрока.СуммаРасход <> 0 ИЛИ ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Расход, ТекСтрока.СуммаРасход, ТекСтрока.СуммаПриход) + ТекСтрока.СуммаУслуг;
			КонецЕсли;
		КонецЕсли;
		//KamikadZe end add
		
		Возврат ОбработкаРеквизита("Состав.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
	ИначеЕсли Имя="Состав.СуммаУслуг" Тогда
		СуммаПлатежа = ?(ТекСтрока.СуммаРасход = 0, ТекСтрока.СуммаПриход, ТекСтрока.СуммаРасход);
		Если СуммаПлатежа < ТекСтрока.СуммаУслуг Тогда
			ТекСтрока.СуммаУслуг = СуммаПлатежа;
			#Если Клиент Тогда
				Предупреждение("Сумма прихода/расхода не может быть меньше суммы комиссии банка!");
			#КонецЕсли
		КонецЕсли;
		ТекСтрока.СуммаРазница = СуммаПлатежа - ТекСтрока.СуммаУслуг;
		ТекСтрока.ПроцентКомиссии = ?(ТекСтрока.СуммаУслуг = 0 ИЛИ СуммаПлатежа = 0, 0, ТекСтрока.СуммаУслуг / СуммаПлатежа * 100);
		
		//KamikadZe begin add 22.11.2016 11:41:17
		Если обПраво("ЭквайрингПерерасчетКомиссии") Тогда
			Если ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Приход Тогда
				ТекСтрока.СуммаРазница = СуммаПлатежа + ТекСтрока.СуммаУслуг;
				ТекСтрока.ПроцентКомиссии = ?(ТекСтрока.СуммаУслуг = 0 ИЛИ СуммаПлатежа = 0, 0, ТекСтрока.СуммаУслуг / (СуммаПлатежа + ТекСтрока.СуммаУслуг) * 100);
			КонецЕсли;
			ЭтотОбъект.СуммаУслуг = ЭтотОбъект.Состав.Итог("СуммаУслуг");
		КонецЕсли;
		//KamikadZe end add		
		
		Возврат Истина;
	ИначеЕсли Имя="Состав.ПроцентКомиссии" Тогда
		Сумма=ТекСтрока.СуммаПриход+ТекСтрока.СуммаРасход;
		ТекСтрока.СуммаУслуг = Окр(Сумма * ТекСтрока.ПроцентКомиссии / 100, 2);
		ТекСтрока.СуммаРазница = Сумма - ТекСтрока.СуммаУслуг;
		
		//KamikadZe begin add 22.11.2016 11:41:17
		Если обПраво("ЭквайрингПерерасчетКомиссии") Тогда
			Если ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Приход Тогда
				ТекСтрока.СуммаУслуг = Окр(Сумма / (100-ТекСтрока.ПроцентКомиссии) * ТекСтрока.ПроцентКомиссии, 2);
				ТекСтрока.СуммаРазница = Сумма + ТекСтрока.СуммаУслуг;
			КонецЕсли;
			ЭтотОбъект.СуммаУслуг = ЭтотОбъект.Состав.Итог("СуммаУслуг");
		КонецЕсли;
		//KamikadZe end add		
		
		Возврат Истина;
	ИначеЕсли Имя="Состав.СуммаРазница" Тогда
		
		Если ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Расход Тогда
			ТекСтрока.СуммаРасход = ТекСтрока.СуммаРазница + ТекСтрока.СуммаУслуг;
			ОбработкаРеквизита("Состав.СуммаРасход",ТекСтрока, ЭтаФорма, ДопПараметры);
		ИначеЕсли ТекСтрока.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Приход Тогда
			ТекСтрока.СуммаПриход = ТекСтрока.СуммаРазница + ТекСтрока.СуммаУслуг; 
			
			//KamikadZe begin add 22.11.2016 12:11:39
			Если обПраво("ЭквайрингПерерасчетКомиссии") Тогда
				ТекСтрока.СуммаПриход = ТекСтрока.СуммаРазница - ТекСтрока.СуммаУслуг;
				ТекСтрока.ПроцентКомиссии = ?(ТекСтрока.СуммаУслуг = 0 ИЛИ Сумма = 0, 0, ТекСтрока.СуммаУслуг / ТекСтрока.СуммаРазница * 100);
			КонецЕсли;
			//KamikadZe end add			
			
			ОбработкаРеквизита("Состав.СуммаПриход",ТекСтрока, ЭтаФорма, ДопПараметры);
		Иначе
			Если ТекСтрока.СуммаРасход <> 0 Тогда
				ТекСтрока.СуммаРасход = ТекСтрока.СуммаРазница + ТекСтрока.СуммаУслуг;
				ОбработкаРеквизита("Состав.СуммаРасход",ТекСтрока, ЭтаФорма, ДопПараметры);
			Иначе
				ТекСтрока.СуммаПриход = ТекСтрока.СуммаРазница + ТекСтрока.СуммаУслуг;
				ОбработкаРеквизита("Состав.СуммаПриход",ТекСтрока, ЭтаФорма, ДопПараметры);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ТекСтрока.СтатьяДДС = Справочники.СтатьиДДС.ОплатаБанковскихУслуг Тогда
			Сумма=ТекСтрока.СуммаПриход+ТекСтрока.СуммаРасход;
			ТекСтрока.ПроцентКомиссии = ?(ТекСтрока.СуммаУслуг = 0 ИЛИ Сумма = 0, 0, ТекСтрока.СуммаУслуг / Сумма * 100);
		КонецЕсли;
		Возврат Истина;
	ИначеЕсли Имя="Состав.СтавкаНДС" Тогда
		Сумма=ТекСтрока.СуммаПриход+ТекСтрока.СуммаРасход;
		ТекСтрока.СуммаНДС=Окр((Сумма*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		Возврат Истина;
		
	ИначеЕсли Имя="Состав.СуммаНДС" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Состав.ПлатежноеПоручениеОснование" Тогда
		Рез=Истина;
		Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.ПлатежноеПоручениеОснование)) И
			 (ТипЗнч(ТекСтрока.ПлатежноеПоручениеОснование)=Тип("ДокументСсылка.ПлатежноеПоручение")) Тогда
			ИзмененКонтрагент=Ложь;
			ИзмененДоговорВзаиморасчетов=Ложь;
			Если ТекСтрока.Контрагент<>ТекСтрока.ПлатежноеПоручениеОснование.Контрагент Тогда
				ТекСтрока.Контрагент=ТекСтрока.ПлатежноеПоручениеОснование.Контрагент;
				ИзмененКонтрагент=Истина;
			КонецЕсли;
			Если ТекСтрока.ДоговорВзаиморасчетов<>ТекСтрока.ПлатежноеПоручениеОснование.ДоговорВзаиморасчетов Тогда
				ТекСтрока.ДоговорВзаиморасчетов=ТекСтрока.ПлатежноеПоручениеОснование.ДоговорВзаиморасчетов;
				ИзмененДоговорВзаиморасчетов=Истина;
			КонецЕсли;
			Если ИзмененКонтрагент Тогда
				Рез=ОбработкаРеквизита("Состав.Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			ИначеЕсли ИзмененДоговорВзаиморасчетов Тогда
				Рез=ОбработкаРеквизита("Состав.ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			Иначе
				Рез=ОбработкаРеквизита("Состав.РассчитатьСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
			ЗаполнитьНазначениеПлатежа=Истина;
			Если СокрЛП(ТекСтрока.НазначениеПлатежа)<>"" Тогда
				#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда
					Ответ=Вопрос("Заполнить назначение платежа из выбранного платежного поручения?",РежимДиалогаВопрос.ДаНет);
					Если Ответ=КодВозвратаДиалога.Нет Тогда
						ЗаполнитьНазначениеПлатежа=Ложь;
					КонецЕсли;
				КонецЕсли; 
				#КонецЕсли
			КонецЕсли;
			Если ЗаполнитьНазначениеПлатежа Тогда
				ТекСтрока.НазначениеПлатежа=""+ТекСтрока.ПлатежноеПоручениеОснование.НазначениеПлатежа;
			КонецЕсли;
		ИначеЕсли обЗначениеНеЗаполнено(ТекСтрока.ПлатежноеПоручениеОснование) Тогда
			#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено И СокрЛП(ТекСтрока.НазначениеПлатежа)<>"" Тогда
				Ответ=Вопрос("Очистить назначение платежа?",РежимДиалогаВопрос.ДаНет);
				Если Ответ=КодВозвратаДиалога.Да Тогда
					ТекСтрока.НазначениеПлатежа="";
				КонецЕсли;
			КонецЕсли; 
			#КонецЕсли
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Состав.РассчитатьСумму" Тогда
		Рез=Истина;
		ТребуетсяПерерасчет=Истина;
		Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.ПлатежноеПоручениеОснование)) И
			 (ТипЗнч(ТекСтрока.ПлатежноеПоручениеОснование)=Тип("ДокументСсылка.ПлатежноеПоручение")) Тогда
			 ОстатокПоСделке=ТекСтрока.ПлатежноеПоручениеОснование.СуммаДокумента;
			 ОстатокПоСделке=-обПересчет(ОстатокПоСделке,ТекСтрока.ПлатежноеПоручениеОснование.ВалютаДокумента,?(ЗначениеЗаполнено(ТекСтрока.КурсВалютыВзаиморасчетов),ТекСтрока.КурсВалютыВзаиморасчетов, Дата),ВалютаДокумента,КурсДокумента);
			 ТребуетсяПерерасчет=Ложь;
		Иначе
			ОстатокПоСделке=Неопределено;
		КонецЕсли; 
		Если ОстатокПоСделке=Неопределено Тогда
			Если ЗначениеЗаполнено(ТекСтрока.Сделка)
				И (ТипЗнч(ТекСтрока.Сделка) = Тип("ДокументСсылка.ЗаказНаАвтомобиль") ИЛИ ТипЗнч(ТекСтрока.Сделка) = Тип("ДокументСсылка.ЗаказПокупателя")) Тогда
				
				ДокументОбъектСтруктура=Новый Структура();
				ДокументОбъектСтруктура.Вставить("Дата",          ТекущаяДата());
				ДокументОбъектСтруктура.Вставить("МоментВремени", ЭтотОбъект.МоментВремени());
				ДокументОбъектСтруктура.Вставить("Проведен",      ЭтотОбъект.Проведен);
				
				ТаблицаРасчетов = зфЗащищенныеФункцииСервер.ПолучитьТаблицуРасчетовПоЗаказам(ДокументОбъектСтруктура, ТекСтрока.Сделка);
				
				ДолгПоПредоплате = 0;
				Долг = 0;
				Для Каждого СтрокаРасчетов Из ТаблицаРасчетов Цикл
					ДолгПоПредоплате = ДолгПоПредоплате + СтрокаРасчетов.ДолгПоПредоплате;
					Долг = Долг + СтрокаРасчетов.Долг;
				КонецЦикла;
				
				Если ДолгПоПредоплате>0 Тогда
					ОстатокПоСделке = ДолгПоПредоплате;
				Иначе
					ОстатокПоСделке = Долг;
				КонецЕсли;
				
			Иначе
				СтруктураОтбора=Новый Структура(); 
				СтруктураОтбора.Вставить("Контрагент",ТекСтрока.Контрагент);
				СтруктураОтбора.Вставить("ДоговорВзаиморасчетов",ТекСтрока.ДоговорВзаиморасчетов);
				Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Сделка) Тогда
					СтруктураОтбора.Вставить("Сделка",ТекСтрока.Сделка);
				КонецЕсли;
				тзДолги=РегистрыНакопления.ВзаиморасчетыКомпании.Остатки(МоментВремени(),СтруктураОтбора,,"Сумма");
				ОстатокПоСделке=тзДолги.Итог("Сумма");
			КонецЕсли;
		КонецЕсли;
		Если ТребуетсяПерерасчет Тогда
			ОстатокПоСделке=обПересчет(ОстатокПоСделке,ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,?(ЗначениеЗаполнено(ТекСтрока.КурсВалютыВзаиморасчетов),ТекСтрока.КурсВалютыВзаиморасчетов, Дата),ВалютаДокумента,КурсДокумента, 2);
		КонецЕсли; 
		Если ОстатокПоСделке>0 Тогда
			Если ТекСтрока.СтатьяДДС.ВидДвижения<>Перечисления.ВидыДвижений.Расход Тогда
				Если ТекСтрока.СуммаРасход<>0 Тогда
					ТекСтрока.СуммаРасход=0;
					Если ДопПараметры=Неопределено Тогда
						ДопПараметры=Новый Структура;
					КонецЕсли; 
					ДопПараметры.Вставить("ИзменениеНаправленияПлатежа",Истина);
				КонецЕсли;
				ТекСтрока.СуммаПриход=ОстатокПоСделке;
				Рез=ОбработкаРеквизита("Состав.СуммаПриход",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		Иначе
			Если ТекСтрока.СтатьяДДС.ВидДвижения<>Перечисления.ВидыДвижений.Приход Тогда
				Если ТекСтрока.СуммаПриход<>0 Тогда
					ТекСтрока.СуммаПриход=0;
					Если ДопПараметры=Неопределено Тогда
						ДопПараметры=Новый Структура;
					КонецЕсли; 
					ДопПараметры.Вставить("ИзменениеНаправленияПлатежа",Истина);
				КонецЕсли;
				ТекСтрока.СуммаРасход=-ОстатокПоСделке;
				Рез=ОбработкаРеквизита("Состав.СуммаРасход",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли;
		Возврат Рез;
	ИначеЕсли Имя = "РассчитатьСкидки" Тогда
		Возврат Истина;
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

Функция ПолучитьСделку(Знач Основание, ЭтоДебетоваяСделка)
	
	ДоступныеСделки = дкПолучитьРазрешенныеТипыСделок(Неопределено, ЭтоДебетоваяСделка, НЕ ЭтоДебетоваяСделка);
	
	Пока ЗначениеЗаполнено(Основание.ДокументОснование) И
		ДоступныеСделки.НайтиПоЗначению(Основание.ДокументОснование.Метаданные().Имя) <> Неопределено Цикл
		
		Основание = Основание.ДокументОснование;
		
	КонецЦикла;
	
	Возврат Основание;
	
КонецФункции


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Банковская выписка"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьБанковскаяВыписка(ТабДокумент) Экспорт
	//зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет("БанковскаяВыписка");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);	
		
	//определяем шапку ТЧ, которая будет повторяться на всех страницах документа
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	//реквизиты шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	//...
	ОбластьМакета.Параметры.Организация = Организация;
	ОбластьМакета.Параметры.ПредставлениеОрганизация = спПолучитьПредставление(ЭтотОбъект.Организация);
	//...
	ОбластьМакета.Параметры.Счет = БанковскийСчет;
	ОбластьМакета.Параметры.БанковскийСчет = спПолучитьНаименование(ЭтотОбъект.БанковскийСчет) ;
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ОбластьМакета.Параметры.Валюта = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьМакета);
	//шапка
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	//строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ВыборкаТабличнойЧасти = ЭтотОбъект.Состав;
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		
	СтруктураОтбора	= Новый Структура("СтруктурнаяЕдиница", БанковскийСчет);
	МоментВремени	= ?(ЭтоНовый(), Новый МоментВремени(КонецДня(Дата)), МоментВремени());
	тзОстатки	= РегистрыНакопления.ДенежныеСредстваКомпании.Остатки(МоментВремени,СтруктураОтбора,,"Сумма");
	Остаток		= тзОстатки.Итог("Сумма");
	СуммаНачОстаток = Остаток;
	ИтогиСуммаПриход = 0;
	
	СтруктураИтоговПоСтранице = Новый Структура("СуммаНачальныйОстаток,СуммаПриход,СуммаРасход,СуммаКонечныйОстаток",СуммаНачОстаток,0,0,СуммаНачОстаток);	
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.СуммаНачальныйОстаток = Формат(Остаток,ФорматВыводаСуммы);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 	
		
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		
		СтрокаТабличнойЧасти_СуммаПриходДС = 0;
		Если СтрокаТабличнойЧасти.СуммаПриход <> 0 Тогда
			СтрокаТабличнойЧасти_СуммаПриходДС = СтрокаТабличнойЧасти.СуммаПриход - СтрокаТабличнойЧасти.СуммаУслуг;		
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);		
		ОбластьМакета.Параметры.СуммаПриход = Формат(СтрокаТабличнойЧасти_СуммаПриходДС,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаРасход = Формат(СтрокаТабличнойЧасти.СуммаРасход,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаОстаток = Формат(Остаток,ФорматВыводаСуммы);
		СуммаКонОстаток = Остаток+СтрокаТабличнойЧасти_СуммаПриходДС-СтрокаТабличнойЧасти.СуммаРасход;
		ОбластьМакета.Параметры.СуммаКонОстаток = Формат(СуммаКонОстаток,ФорматВыводаСуммы);
		
		Документ = СтрокаТабличнойЧасти.ПлатежноеПоручениеОснование;
		Если ТипЗнч(Документ)=Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
			ОбластьМакета.Параметры.ПлатежноеПоручениеОснование = "№ " + СокрЛП(Документ.Номер) + " от " + Символы.ПС + Формат(Документ.Дата, "ДЛФ=DD") 
		КонецЕсли;
		
		ОбластьМакета.Параметры.НазначениеПлатежа = СтрЗаменить(СтрокаТабличнойЧасти.НазначениеПлатежа, Символы.ПС, " "); 
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//НомСтр  = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, , НомСтр,,ЭтотОбъект);
		Остаток = Остаток+СтрокаТабличнойЧасти_СуммаПриходДС-СтрокаТабличнойЧасти.СуммаРасход;
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			КонечныйОстаток = СтруктураИтоговПоСтранице.СуммаКонечныйОстаток;
			СтруктураИтоговПоСтранице = Новый Структура("СуммаНачальныйОстаток,СуммаПриход,СуммаРасход,СуммаКонечныйОстаток",КонечныйОстаток,0,0,КонечныйОстаток);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		ИтогиПоСтранице = Новый Структура("СуммаНачальныйОстаток,СуммаПриход,СуммаРасход,СуммаКонечныйОстаток",0,СтрокаТабличнойЧасти_СуммаПриходДС,СтрокаТабличнойЧасти.СуммаРасход,СтрокаТабличнойЧасти.СуммаПриход-СтрокаТабличнойЧасти.СуммаРасход);
		ИтогиСуммаПриход = ИтогиСуммаПриход + СтрокаТабличнойЧасти_СуммаПриходДС;
		дкДобавитьИтогиПоСтранице(ИтогиПоСтранице,СтруктураИтоговПоСтранице);		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;	
	
	//итоги
	СуммаПриход = ИтогиСуммаПриход;
	ОбластьПодвал.Параметры.ИтогоСуммаПриход = Формат(СуммаПриход,ФорматВыводаСуммы);
	СуммаРасход = Состав.Итог("СуммаРасход");
	ОбластьПодвал.Параметры.ИтогоСуммаРасход = Формат(СуммаРасход,ФорматВыводаСуммы);
	СуммаКонечныйОстаток = СуммаНачОстаток+СуммаПриход-СуммаРасход;
	ОбластьПодвал.Параметры.СуммаКонечныйОстаток = Формат(СуммаКонечныйОстаток,ФорматВыводаСуммы);
	//сумма прописью
	ОбластьПодвал.Параметры.СуммаПрописьюНачОстаток=обЧислоПрописью(тзОстатки.Итог("Сумма"),ЭтотОбъект.ВалютаДокумента);
	ОбластьПодвал.Параметры.СуммаПрописьюПриход = обЧислоПрописью(СуммаПриход,ЭтотОбъект.ВалютаДокумента);
	ОбластьПодвал.Параметры.СуммаПрописьюРасход = обЧислоПрописью(СуммаРасход,ЭтотОбъект.ВалютаДокумента);
	ОбластьПодвал.Параметры.СуммаПрописьюКонОстаток=обЧислоПрописью(Остаток,ЭтотОбъект.ВалютаДокумента);
	// Выводим представления и расшифровки подписей
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/ "+ ГлавныйБухгалтер.ГлавныйБухгалтерПредставление);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	ТабДокумент.Вывести(ОбластьПодвал);
	
	ТабДокумент.ТолькоПросмотр = Истина;
	Возврат ТабДокумент;
КонецФункции // ПечатьБанковскаяВыписка()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если обЗначениеНеЗаполнено(БанковскийСчет) Тогда 
		БанковскийСчет	= Организация.ОсновнойБанковскийСчет;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(БанковскийСчет) Тогда 	
		ВалютаДокумента	= Справочники.Валюты.ПустаяСсылка();
	Иначе
		ВалютаДокумента	= БанковскийСчет.ВалютаДенежныхСредств;
		СтруктураКурса	= обКурсДляВалюты(ВалютаДокумента,Дата);
		КурсДокумента	= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;
	
	//rarus zdoroe begin
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаРасходДС") ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ПланПоступленияДС") Тогда
		
		ОснованиеПлатежи = Основание.Платежи;
		Если ОснованиеПлатежи.Количество() > 0 Тогда
			Для Каждого Платеж Из ОснованиеПлатежи Цикл
				
				НоваяСтрока = Состав.Добавить();
				НоваяСтрока.СтатьяДДС             = Основание.СтатьяДДС;
				НоваяСтрока.Контрагент            = Основание.Контрагент;
				НоваяСтрока.ДоговорВзаиморасчетов = Основание.ДоговорВзаиморасчетов;  
				
				//НоваяСтрока.Сделка= ПолучитьСделку(Основание.ДокументОснование, ТипЗнч(Основание) = Тип("ДокументСсылка.ПланПоступленияДС"));

				Если ЗначениеЗаполнено(Основание.ДокументОснование) Тогда
					НоваяСтрока.Сделка                = ПолучитьСделку(Основание.ДокументОснование, ТипЗнч(Основание) = Тип("ДокументСсылка.ПланПоступленияДС"));
				КонецЕсли;	
				
				
				НоваяСтрока.ДокументПланирования  = Основание;
				
				ОбработкаРеквизита("Состав.Контрагент", НоваяСтрока);
				
				Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаРасходДС") Тогда
					НоваяСтрока.СуммаРасход = Платеж.Сумма;
				Иначе 
					НоваяСтрока.СуммаПриход = Платеж.Сумма;
				КонецЕсли;
				
				Если НоваяСтрока.СуммаПриход > 0 Тогда
					ОбработкаРеквизита("Состав.СуммаПриход", НоваяСтрока);
				ИначеЕсли 
					НоваяСтрока.СуммаРасход > 0 Тогда 
					ОбработкаРеквизита("Состав.СуммаРасход", НоваяСтрока);
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		//БизТех 21022022
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаявкаНаРасходДС") Тогда
			ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки;	
		КонецЕсли;
		//БизТех 21022022
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.НачислениеЗарплаты") Тогда
		ДокументОснование=Основание;
		ХозОперация = Справочники.ХозОперации.БанковскаяВыписка;
		//Вычислим сумму
		Состав.Очистить();
		//выберем всю дебиторскую задолженность по договорам, которые есть в начислении зп
		Запрос=Новый Запрос("ВЫБРАТЬ
		|	ВзаиморасчетыКомпанииОстатки.Контрагент,
		|	ВзаиморасчетыКомпанииОстатки.ДоговорВзаиморасчетов,
		|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК Сумма,
		|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр,
		|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаБазОстаток, 0) КАК СуммаБаз
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
		|			&МоментВремени,
		|			ДоговорВзаиморасчетов В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов
		|				ИЗ
		|					Документ.НачислениеЗарплаты.Сотрудники КАК НачислениеЗарплатыСотрудники
		|				ГДЕ
		|					НачислениеЗарплатыСотрудники.Ссылка = &ДокументОснование)) КАК ВзаиморасчетыКомпанииОстатки
		|ГДЕ
		|	ВзаиморасчетыКомпанииОстатки.СуммаОстаток < 0");
		Запрос.УстановитьПараметр("ДокументОснование",ДокументОснование);
		МоментВремени = ?(ТекущаяДата() > ДокументОснование.Дата, ТекущаяДата(), КонецДня(ДокументОснование.Дата));
		Запрос.УстановитьПараметр("МоментВремени",МоментВремени);
		РезультатЗапросаПоСотрудникам = Запрос.Выполнить();
		Если НЕ РезультатЗапросаПоСотрудникам.Пустой() Тогда
			Выборка = РезультатЗапросаПоСотрудникам.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = Состав.Добавить();
				НоваяСтрока.Контрагент = Выборка.Контрагент;
				НоваяСтрока.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
				ВалютаДоговора = Выборка.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
				ОбработкаРеквизита("Состав.ДоговорВзаиморасчетов", НоваяСтрока);
				НоваяСтрока.Сделка = ДокументОснование;
				НоваяСтрока.АвтоЗакрытиеСделок = Истина;
				Если НЕ ЗначениеЗаполнено(НоваяСтрока.СтатьяДДС) Тогда
					НоваяСтрока.СтатьяДДС				= Справочники.СтатьиДДС.ВыдачаЗП;
				КонецЕсли;
				Если ВалютаДокумента=ВалютаДоговора Тогда
					НоваяСтрока.СуммаРасход = -Выборка.Сумма;
				ИначеЕсли ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
					НоваяСтрока.СуммаРасход = -Выборка.СуммаБаз; 
				ИначеЕсли ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
					НоваяСтрока.СуммаРасход = -Выборка.СуммаУпр; 
				Иначе
					СтруктураКурса = обКурсДляВалюты(ВалютаДоговора,ДокументОснование.Дата);
					Курс           = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
					НоваяСтрока.СуммаРасход = обПересчет(-Выборка.Сумма,ВалютаДоговора,Курс,ВалютаДокумента,ДокументОснование.Дата);
				КонецЕсли;
				ОбработкаРеквизита("Состав.СуммаРасход", НоваяСтрока);
			КонецЦикла;
		КонецЕсли;
		СуммаДокумента=обПересчет(Основание.СуммаДокумента,Основание.ВалютаДокумента,Основание.КурсДокумента,ВалютаДокумента,Дата);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Документы.ПриходныйКассовыйОрдер.ОчиститьРеквизитыФискальногоРегистратора(ЭтотОбъект);
	
	Товары.Очистить();
	КодыМаркировки.Очистить();
	
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если ХозОперация<>Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
		СтатьяДДС=Справочники.СтатьиДДС.ПустаяСсылка();
		Контрагент=Справочники.Контрагенты.ПустаяСсылка();
		ДоговорВзаиморасчетов=Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
		СтавкаНДС=Справочники.СтавкиНДС.ПустаяСсылка();
		СуммаНДС=0;
	КонецЕсли;
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если ХозОперация<>Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
		СуммаДокументаПриход=Состав.Итог("СуммаПриход");
		СуммаДокументаРасход=Состав.Итог("СуммаРасход");
		СуммаУслуг=Состав.Итог("СуммаУслуг");
	КонецЕсли; 
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;	
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли; 
		
		БТ_ПробитНаККТ = ЗначениеЗаполнено(ДатаФР); //БизТех Аляль 24.11.2025г.
		
	КонецЕсли;  
		
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	// Ошибка, если счет не соответствует организации или подразделению
	Отказ = НЕ дкПроверитьКорректностьБанковскогоСчета(БанковскийСчет, Организация, ПодразделениеКомпании, ДополнительныеСвойства);
	Если Отказ Тогда
		РезультатОбработки = "Банковский счет не соответствует выбранной организации";
		дкВыводРезультатовОбработки(ЭтотОбъект, Отказ);
		Возврат;
	КонецЕсли;
	
	СуммаДоходаРасходаСуммовыхРазниц=0;
	
	// деньги
	НаборЗаписейДС=Движения.ДенежныеСредстваКомпании;
	НаборЗаписейДС.РежимПроведения=Режим;
	НаборЗаписейДС.СтруктурнаяЕдиница=БанковскийСчет;
	НаборЗаписейДС.Валюта=ВалютаДокумента;
	НаборЗаписейДС.Курс=КурсДокумента;
	
	// взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	ТипыСделокСПокупателем=орПолучитьТипыСделок(Истина);
	ТипыСделокСПоставщиком=орПолучитьТипыСделок(Ложь);
	
	// БАЛАНС: Подразделение банковского счета и подразделения договоров из табличной части могут
	// находится в разных балансовых "ветках". Поэтому, если в системе ведется баланс по подразделениям, то
	// возможен "разрыв" баланса. Следовательно, придется создавать корректирующие движения по регистру "Доходы и расходы".
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	ПодразделениеБанкСета         = обПолучитьБалансовоеПодразделение(БанковскийСчет.Подразделение);
	
	// Для накопления записей корректировок используем специальную таблицу
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений;
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение",  Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпрРасход", Новый ОписаниеТипов("Число"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпрПриход", Новый ОписаниеТипов("Число"));

	// Суммовые разницы возникают в рамках договора, поэтому их нельзя списывать одним движением.
	ТаблицаСуммовыхРазниц = Новый ТаблицаЗначений;
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммовыхРазниц.Колонки.Добавить("СуммаУпр",      Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТЧ Из Состав Цикл
		// Обнулим суммовые разницы 
		СуммаДоходаРасходаСуммовыхРазниц = 0;
		
		НаборЗаписейДС.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.Валюта = Неопределено;
		НаборЗаписейВзаиморасчеты.КурсВзаиморасчетов = СтрокаТЧ.КурсВалютыВзаиморасчетов;
		
		// деньги
		НаборЗаписейДС.СтатьяДДС=СтрокаТЧ.СтатьяДДС;
		
		// взаиморасчеты		
		НаборЗаписейВзаиморасчеты.Контрагент=СтрокаТЧ.Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=СтрокаТЧ.ДоговорВзаиморасчетов;
		НаборЗаписейВзаиморасчеты.Сделка=СтрокаТЧ.Сделка;
		ТипСделки=ТипЗнч(СтрокаТЧ.Сделка);
		Если ТипыСделокСПокупателем.СодержитТип(ТипСделки) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		ИначеЕсли ТипыСделокСПоставщиком.СодержитТип(ТипСделки) Тогда
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
		Иначе
			 НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Неопределено;
		КонецЕсли;
		Если Режим=РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейВзаиморасчеты.МоментВремени=Неопределено;
		Иначе
			Если СтрокаТЧ.НомерСтроки=1 Тогда
				НаборЗаписейВзаиморасчеты.МоментВремени=Новый Граница(МоментВремени(),ВидГраницы.Исключая);
			Иначе
				НаборЗаписейВзаиморасчеты.МоментВремени=Новый Граница(МоментВремени(),ВидГраницы.Включая);
			КонецЕсли;  
		КонецЕсли;  
		НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок=СтрокаТЧ.АвтоЗакрытиеСделок;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		Если (СтрокаТЧ.СуммаПриход<>0)И(СтрокаТЧ.СуммаРасход<>0) Тогда
			дкСообщитьРезультат("Приход и расход в одной строке "+СтрокаТЧ.НомерСтроки+" !","Важное&Выписка:",ЭтотОбъект,СтрокаТЧ.Контрагент);
			Отказ = Истина;	Продолжить;
		ИначеЕсли (СтрокаТЧ.СуммаПриход=0)И(СтрокаТЧ.СуммаРасход=0) Тогда
			дкСообщитьРезультат("Ни прихода ни расхода в строке "+СтрокаТЧ.НомерСтроки+" !","Важное&Выписка:",ЭтотОбъект,СтрокаТЧ.Контрагент);
			Отказ = Истина;	Продолжить;
		ИначеЕсли СтрокаТЧ.СуммаПриход<>0 Тогда
			// взаиморасчеты
			НаборЗаписейВзаиморасчеты.Сумма=СтрокаТЧ.СуммаПриход;
			
			//KamikadZe begin add 22.11.2016 12:33:32
			Если обПраво("ЭквайрингПерерасчетКомиссии") Тогда
				НаборЗаписейВзаиморасчеты.Сумма=СтрокаТЧ.СуммаПриход + СтрокаТЧ.СуммаУслуг;
			КонецЕсли;
			//KamikadZe end add 
			
			Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
			
			// деньги
			Если НЕ (СтрокаТЧ.СуммаПриход - СтрокаТЧ.СуммаУслуг) = 0 Тогда
				НаборЗаписейДС.Сумма=СтрокаТЧ.СуммаПриход - СтрокаТЧ.СуммаУслуг;    
				
				//KamikadZe begin add 22.11.2016 12:31:49
				Если обПраво("ЭквайрингПерерасчетКомиссии") Тогда
					НаборЗаписейДС.Сумма=СтрокаТЧ.СуммаПриход;
				КонецЕсли;
				//KamikadZe end add
				
				НаборЗаписейДС.СуммаВсего=СуммаДокументаПриход-СтрокаТЧ.СуммаПриход-СуммаДокументаРасход;
				Отказ=НЕ НаборЗаписейДС.Приход() ИЛИ Отказ;
			КонецЕсли;
		ИначеЕсли СтрокаТЧ.СуммаРасход<>0 Тогда
			// взаиморасчеты
			Если НЕ СтрокаТЧ.СтатьяДДС = Справочники.СтатьиДДС.ОплатаБанковскихУслуг Тогда
				НаборЗаписейВзаиморасчеты.Сумма=СтрокаТЧ.СуммаРасход - СтрокаТЧ.СуммаУслуг;
				Отказ=НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			КонецЕсли;
			
			// деньги
			НаборЗаписейДС.Сумма=СтрокаТЧ.СуммаРасход;
			НаборЗаписейДС.СуммаВсего=СуммаДокументаПриход+СтрокаТЧ.СуммаРасход-СуммаДокументаРасход;
			Отказ=НЕ НаборЗаписейДС.Расход() ИЛИ Отказ;
		КонецЕсли; 
		
		НаборЗаписейВзаиморасчеты.Записать();
		
		Если СтрокаТЧ.СуммаУслуг <> 0 ИЛИ СтрокаТЧ.СтатьяДДС = Справочники.СтатьиДДС.ОплатаБанковскихУслуг Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение  = БанковскийСчет.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.РасходыНаУслугиБанков;
			
			//KamikadZe begin add 07.02.2017 16:28:45
			Если НЕ обЗначениеНеЗаполнено(ЭтотОбъект.СтатьяДоходовИРасходов) Тогда
				НаборЗаписейДиР.СтатьяДоходовИРасходов = ЭтотОбъект.СтатьяДоходовИРасходов;
			КонецЕсли;	
			//KamikadZe end add			
			
			НаборЗаписейДиР.ВУпрВалюте = Ложь;
			НаборЗаписейДиР.Расход     = ?(СтрокаТЧ.СуммаУслуг = 0, СтрокаТЧ.СуммаРасход, СтрокаТЧ.СуммаУслуг);
			
			//<<Павличенко 08.02.18
			НаборЗаписейДиР.РасходБезНДС     = ?(СтрокаТЧ.СуммаУслуг = 0, СтрокаТЧ.СуммаРасход, СтрокаТЧ.СуммаУслуг);
			//>>Павличенко 08.02.18
			
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		
		// Заполняем таблицу суммовых разниц
		НоваяСтрока = ТаблицаСуммовыхРазниц.Добавить();
		Если БалансВедетсяПоПодразделениям Тогда
			НоваяСтрока.Подразделение = СтрокаТЧ.ДоговорВзаиморасчетов.Подразделение;
		Иначе
			НоваяСтрока.Подразделение = ПодразделениеКомпании; 
		КонецЕсли;
		НоваяСтрока.СуммаУпр = СуммаДоходаРасходаСуммовыхРазниц;
		
		// БАЛАНС: Если балансовые подразделения договора и банковского счета различны, то будет 
		// "разрыв" баланса. Необходимо добавить корректирующие движения по регистру "Доходы и Расходы"
		Если БалансВедетсяПоПодразделениям Тогда
			ПодразделениеДоговора = обПолучитьБалансовоеПодразделение(СтрокаТЧ.ДоговорВзаиморасчетов.Подразделение);
			Если ПодразделениеБанкСета<>ПодразделениеДоговора Тогда
				НоваяСтрока = ТаблицаВзаиморасчетов.Добавить();
				НоваяСтрока.Подразделение  = СтрокаТЧ.ДоговорВзаиморасчетов.Подразделение;
				НоваяСтрока.СуммаУпрРасход = СтрокаТЧ.СуммаРасход;
				НоваяСтрока.СуммаУпрПриход = СтрокаТЧ.СуммаПриход;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Создаем движения по суммовым разницам
	ТаблицаСуммовыхРазниц.Свернуть("Подразделение", "СуммаУпр");
	Если ТаблицаСуммовыхРазниц.Количество()<>0 Тогда
		Для Каждого ТекСтрока Из ТаблицаСуммовыхРазниц Цикл  	
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение          = ТекСтрока.Подразделение;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			Если ТекСтрока.СуммаУпр<0 Тогда
				НаборЗаписейДиР.Расход = -ТекСтрока.СуммаУпр;
			Иначе
				НаборЗаписейДиР.Доход  = ТекСтрока.СуммаУпр;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;			
		КонецЦикла;	
	КонецЕсли;
	
	// БАЛАНС: создаем корректирующие движения. Сумма в управленческой валюте будет рассчитана в 
	// модуле набора записей. Для этого указываем реквизит "ВУпрВалюте=Ложь";
	ТаблицаВзаиморасчетов.Свернуть("Подразделение", "СуммаУпрРасход, СуммаУпрПриход");
	Если ТаблицаВзаиморасчетов.Количество()<>0 Тогда
		Для Каждого ТекСтрока Из ТаблицаВзаиморасчетов Цикл  	
			
			Если ТекСтрока.СуммаУпрПриход<>0 Тогда
				// Корректируем движения денежных средств
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение  = БанковскийСчет.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте = Ложь;
				НаборЗаписейДиР.Доход      = ТекСтрока.СуммаУпрПриход;  
				
				//<<Павличенко 08.02.18
				НаборЗаписейДиР.ДоходБезНДС      = ТекСтрока.СуммаУпрПриход;
				//>>Павличенко 08.02.18
				
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
				
				// Корректируем движения взаиморасчетов.
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение  = ТекСтрока.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте = Ложь;
				НаборЗаписейДиР.Расход     = ТекСтрока.СуммаУпрПриход;   
				
				//<<Павличенко 08.02.18
				НаборЗаписейДиР.РасходБезНДС     = ТекСтрока.СуммаУпрПриход;
				//>>Павличенко 08.02.18		
				
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
			КонецЕсли;
			
			Если ТекСтрока.СуммаУпрРасход<>0 Тогда
				// Корректируем движения денежных средств
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение  = БанковскийСчет.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте = Ложь;   
				
				//<<Павличенко 08.02.18
				НаборЗаписейДиР.ДоходБезНДС      = ТекСтрока.СуммаУпрРасход;
				//>>Павличенко 08.02.18
				
				НаборЗаписейДиР.Доход      = ТекСтрока.СуммаУпрРасход;
				Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
				
				// Корректируем движения взаиморасчетов.
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				НаборЗаписейДиР.Подразделение  = ТекСтрока.Подразделение;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
				НаборЗаписейДиР.ВУпрВалюте = Ложь;
				НаборЗаписейДиР.Доход      = ТекСтрока.СуммаУпрРасход;  
				
				//<<Павличенко 08.02.18
				НаборЗаписейДиР.ДоходБезНДС      = ТекСтрока.СуммаУпрРасход;
				//>>Павличенко 08.02.18
				
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
			КонецЕсли;  					
		КонецЦикла;	
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	//проведение в Платежный календарь
	НаборЗаписей = Движения.ПлатежныйКалендарь;
	НаборЗаписей.ДокументОбъект = Ссылка;
	
	Отказ = НЕ НаборЗаписей.ФактВыписка() ИЛИ Отказ;
	
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки = Истина;
#КонецЕсли
СообщениеОбОшибке = Неопределено;
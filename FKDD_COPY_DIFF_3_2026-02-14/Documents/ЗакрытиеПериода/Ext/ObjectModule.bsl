
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеРеквизиты = Новый Структура();
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Возврат дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗакрытиеПериода","Закрытие периода",Истина);		
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="!!!" Тогда
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьРаспределениеСтатей(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("РаспределениеСтатей");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	НомерСтраницы     = 2;	
	НомерСтраницыПред = НомерСтраницы;
	// Выводим заголовок.
	ОбластьЗаголовка = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовка.Параметры.ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект); 
	ТабДокумент.Вывести(ОбластьЗаголовка);
	
	// Устанавливаем номер страницы для следующей страницы
	ОбластьШапки = Макет.ПолучитьОбласть("ОбластьШапки");
		
	// Получаем движения документа.
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаСтатейПолучателей.ПодразделениеКомпании КАК Подразделение,
	|	ТаблицаСтатейПолучателей.СтатьяРаспределения КАК СтатьяИсточник,
	|	ТаблицаСтатейПолучателей.СтатьяДоходовИРасходов КАК СтатьяПолучатель,
	|	ТаблицаСтатейПолучателей.ДоходУпр КАК ДоходУпр,
	|	ТаблицаСтатейПолучателей.РасходУпр КАК РасходУпр,
	|	ПорядокЗакрытияБалансовыхСтатей.Порядок КАК Порядок
	|ИЗ
	|	РегистрНакопления.ДоходыИРасходы КАК ТаблицаСтатейПолучателей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокЗакрытияБалансовыхСтатей КАК ПорядокЗакрытияБалансовыхСтатей
	|		ПО ТаблицаСтатейПолучателей.СтатьяРаспределения = ПорядокЗакрытияБалансовыхСтатей.Статья
	|ГДЕ
	|	ТаблицаСтатейПолучателей.ХозОперация = &ХозОперация
	|	И ТаблицаСтатейПолучателей.Регистратор = &Регистратор
	|	И ТаблицаСтатейПолучателей.ВидДвижения = &ВидДвиженияПриход
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделениеКомпании,
	|	Порядок
	|ИТОГИ
	|	СУММА(ДоходУпр),
	|	СУММА(РасходУпр)
	|ПО
	|	Подразделение,
	|	СтатьяИсточник";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ХозОперация",       Справочники.ХозОперации.РаспределениеДоходовИРасходов);
	Запрос.УстановитьПараметр("Регистратор",       Ссылка);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	
	ВыборкаПоПодразделениям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоПодразделениям.Следующий() Цикл
		// Выводим подразделение.
		ОбластьПодразделения = Макет.ПолучитьОбласть("СтрокаБалансовоеПодразделение");
		ОбластьПодразделения.Параметры.БалансовоеПодразделение = ВыборкаПоПодразделениям.Подразделение;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодразделения, ОбластьШапки, , НомерСтраницы, , ЭтотОбъект);
		Если НомерСтраницы<>НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		ТабДокумент.НачатьГруппуСтрок();
		
		// Спускаемся ниже
		ВыборкаПоСтатье = ВыборкаПоПодразделениям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСтатье.Следующий() Цикл 			
			ВыборкаПоЗаписям = ВыборкаПоСтатье.Выбрать();
			Пока ВыборкаПоЗаписям.Следующий() Цикл
				// Выводим записи
				ОбластьСтатьи = Макет.ПолучитьОбласть("СтрокаСтатьяДиР");
				ОбластьСтатьи.Параметры.РаспределяемаяСтатья = ВыборкаПоЗаписям.СтатьяИсточник;
				ОбластьСтатьи.Параметры.СтатьяПолучатель     = ВыборкаПоЗаписям.СтатьяПолучатель;
				ОбластьСтатьи.Параметры.СуммаДоходаУпр       = Формат(ВыборкаПоЗаписям.ДоходУпр,  ФорматВыводаСуммы);
                ОбластьСтатьи.Параметры.СуммаРасходаУпр      = Формат(ВыборкаПоЗаписям.РасходУпр, ФорматВыводаСуммы);
				
				НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтатьи, ОбластьШапки, , НомерСтраницы, , ЭтотОбъект);
				Если НомерСтраницы<>НомерСтраницыПред Тогда
					НомерСтраницыПред = НомерСтраницы;
					ОбластьШапки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		ТабДокумент.ЗакончитьГруппуСтрок(); 		
	КонецЦикла;                             	
	
	// Выводим подвал
	ОбластьПодвала = Макет.ПолучитьОбласть("Подвал");	
	ТабДокумент.Вывести(ОбластьПодвала);

	Возврат ТабДокумент;
КонецФункции // ПечатьРаспределениеСтатей()

// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОтчетПоОстаткам(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ОтчетПоОстаткам");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	НомерСтраницы     = 2;	
	НомерСтраницыПред = НомерСтраницы;
	// Выводим заголовок.
	ОбластьЗаголовка = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовка.Параметры.ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект); 
	ТабДокумент.Вывести(ОбластьЗаголовка);
	
	// Устанавливаем номер страницы для следующей страницы
	ОбластьШапки = Макет.ПолучитьОбласть("ОбластьШапки");
	
	// Получаем движения документа.
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НачальныеОстаткиИОбороты.Подразделение КАК Подразделение,
	|	НачальныеОстаткиИОбороты.Статья КАК Статья,
	|	НачальныеОстаткиИОбороты.ДоходПриход,
	|	НачальныеОстаткиИОбороты.РасходПриход,
	|	НачальныеОстаткиИОбороты.ДоходНачОст,
	|	НачальныеОстаткиИОбороты.РасходНачОст,
	|	ЕСТЬNULL(ДоходыИРасходыОстатки.ДоходУпрОстаток, 0) КАК ДоходКонОст,
	|	ЕСТЬNULL(ДоходыИРасходыОстатки.РасходУпрОстаток, 0) КАК РасходКонОст
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(ВложенныйЗапрос.Подразделение, ДоходыИРасходыОстатки.ПодразделениеКомпании) КАК Подразделение,
	|		ЕСТЬNULL(ВложенныйЗапрос.Статья, ДоходыИРасходыОстатки.СтатьяДоходовИРасходов) КАК Статья,
	|		ЕСТЬNULL(ВложенныйЗапрос.ДоходПриход, 0) КАК ДоходПриход,
	|		ЕСТЬNULL(ВложенныйЗапрос.РасходПриход, 0) КАК РасходПриход,
	|		ЕСТЬNULL(ДоходыИРасходыОстатки.ДоходУпрОстаток, 0) КАК ДоходНачОст,
	|		ЕСТЬNULL(ДоходыИРасходыОстатки.РасходУпрОстаток, 0) КАК РасходНачОст
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДоходыИРасходы.ПодразделениеКомпании КАК Подразделение,
	|			ДоходыИРасходы.СтатьяДоходовИРасходов КАК Статья,
	|			СУММА(ДоходыИРасходы.ДоходУпр) КАК ДоходПриход,
	|			СУММА(ДоходыИРасходы.РасходУпр) КАК РасходПриход
	|		ИЗ
	|			РегистрНакопления.ДоходыИРасходы КАК ДоходыИРасходы
	|		ГДЕ
	|			ДоходыИРасходы.ВидДвижения = &ВидДвиженияПриход
	|			И ДоходыИРасходы.Регистратор = &Регистратор
	|			И ДоходыИРасходы.ПодразделениеКомпании.Балансовое
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ДоходыИРасходы.ПодразделениеКомпании,
	|			ДоходыИРасходы.СтатьяДоходовИРасходов) КАК ВложенныйЗапрос
	|			ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы.Остатки(&МоментНаНачалоДокумента, ПодразделениеКомпании.Балансовое) КАК ДоходыИРасходыОстатки
	|			ПО ВложенныйЗапрос.Статья = ДоходыИРасходыОстатки.СтатьяДоходовИРасходов
	|				И ВложенныйЗапрос.Подразделение = ДоходыИРасходыОстатки.ПодразделениеКомпании) КАК НачальныеОстаткиИОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы.Остатки(&МоментНаКонецДокумента, ПодразделениеКомпании.Балансовое) КАК ДоходыИРасходыОстатки
	|		ПО НачальныеОстаткиИОбороты.Подразделение = ДоходыИРасходыОстатки.ПодразделениеКомпании
	|			И НачальныеОстаткиИОбороты.Статья = ДоходыИРасходыОстатки.СтатьяДоходовИРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	Статья
	|ИТОГИ ПО
	|	Подразделение";
			
	Запрос = Новый Запрос(ТекстЗапроса); 
	Момент = МоментВремени();
	Запрос.УстановитьПараметр("МоментНаНачалоДокумента", Новый Граница(Момент, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("МоментНаКонецДокумента",  Новый Граница(Момент, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Регистратор",             Ссылка);
	Запрос.УстановитьПараметр("ВидДвиженияПриход",       ВидДвиженияНакопления.Приход);
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	ВыборкаПоПодразделениям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоПодразделениям.Следующий() Цикл
		// Выводим подразделение.
		ОбластьПодразделения = Макет.ПолучитьОбласть("СтрокаБалансовоеПодразделение");
		ОбластьПодразделения.Параметры.БалансовоеПодразделение = ВыборкаПоПодразделениям.Подразделение;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодразделения, ОбластьШапки, , НомерСтраницы, , ЭтотОбъект);
		Если НомерСтраницы<>НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		ТабДокумент.НачатьГруппуСтрок();
		
		// Спускаемся ниже
		ВыборкаПоСтатье = ВыборкаПоПодразделениям.Выбрать();
		Пока ВыборкаПоСтатье.Следующий() Цикл 			
			// Выводим записи
			ОбластьСтатьи = Макет.ПолучитьОбласть("СтрокаСтатьяДиР");
			ОбластьСтатьи.Параметры.Статья = ВыборкаПоСтатье.Статья;
			ОбластьСтатьи.Параметры.СуммаДоходаНачОст  = Формат(ВыборкаПоСтатье.ДоходНачОст,  ФорматВыводаСуммы);
			ОбластьСтатьи.Параметры.СуммаРасходаНачОст = Формат(ВыборкаПоСтатье.РасходНачОст, ФорматВыводаСуммы);
			ОбластьСтатьи.Параметры.СуммаДоходаПриход  = Формат(ВыборкаПоСтатье.ДоходПриход,  ФорматВыводаСуммы);
			ОбластьСтатьи.Параметры.СуммаРасходаПриход = Формат(ВыборкаПоСтатье.РасходПриход, ФорматВыводаСуммы);
			ОбластьСтатьи.Параметры.СуммаДоходаКонОст  = Формат(ВыборкаПоСтатье.ДоходНачОст  + ВыборкаПоСтатье.ДоходПриход,  ФорматВыводаСуммы);
			ОбластьСтатьи.Параметры.СуммаРасходаКонОст = Формат(ВыборкаПоСтатье.РасходНачОст + ВыборкаПоСтатье.РасходПриход, ФорматВыводаСуммы);
			ОбластьСтатьи.Параметры.СуммаДоходаКонОстПосле  = Формат(ВыборкаПоСтатье.ДоходКонОст,  ФорматВыводаСуммы);
			ОбластьСтатьи.Параметры.СуммаРасходаКонОстПосле = Формат(ВыборкаПоСтатье.РасходКонОст, ФорматВыводаСуммы);
			
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтатьи, ОбластьШапки, , НомерСтраницы, , ЭтотОбъект);
			Если НомерСтраницы<>НомерСтраницыПред Тогда
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапки.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
		КонецЦикла;
		ТабДокумент.ЗакончитьГруппуСтрок(); 
	КонецЦикла;
	
	
	// Выводим подвал
	ОбластьПодвала = Макет.ПолучитьОбласть("Подвал");	
	ТабДокумент.Вывести(ОбластьПодвала);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьОтчетПоОстаткам()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	// Документ должен быть последним в течение дня.
	УстановитьВремя(РежимАвтоВремя.ТекущееИлиПоследним);
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
		
	// Действие №1: Производим перенос остатков с небалансовых подразделений на балансовые (или на корень).
	// Получим таблицу значений. Первая колонка - это подразделение, остатки которого необходимо перенести.
	// Вторая колонка - подразделение, куда переносим (это будет либо балансовое подразделение, либо корневое).
	
	// В случае ведения баланса по организациям, нельзя выполнять перенос остатков,
	// т.к. организации подразделений могут не совпадает.
	Если СпособВеденияБаланса<>Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		ТаблицаПереноса = обПолучитьСоответствияБалансовымПодразделениям((СпособВеденияБаланса=Перечисления.СпособВеденияБаланса.ПоПодразделению));     	
		Если ТаблицаПереноса.Количество()<>0 Тогда // Получаем остатки по подразделениям
			// Поместим таблицу во "Временную таблицу"
			МенеджерВТ = Новый МенеджерВременныхТаблиц;
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	Источник,
			|	Приемник
			|ПОМЕСТИТЬ ТаблицаСоответствий
			|ИЗ &ВнешнийИсточник КАК ВнешнийИсточник
			|ИНДЕКСИРОВАТЬ ПО Источник";
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
			Запрос.УстановитьПараметр("ВнешнийИсточник", ТаблицаПереноса);
			Запрос.Выполнить();
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ДоходыИРасходыОстатки.ПодразделениеКомпании КАК ПодразделениеКомпании,
			|	ДоходыИРасходыОстатки.СтатьяДоходовИРасходов КАК СтатьяДоходовИРасходов,
			|	ДоходыИРасходыОстатки.ДоходУпрОстаток,
			|	ДоходыИРасходыОстатки.РасходУпрОстаток, 
			|   ТаблицаСоответствий.Приемник
			|ИЗ
			|	РегистрНакопления.ДоходыИРасходы.Остатки(&МоментВремени, ПодразделениеКомпании В (&СписокПодразделений)) КАК ДоходыИРасходыОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСоответствий КАК ТаблицаСоответствий
			|		ПО ДоходыИРасходыОстатки.ПодразделениеКомпании = ТаблицаСоответствий.Источник
			|ДЛЯ ИЗМЕНЕНИЯ
			|	РегистрНакопления.ДоходыИРасходы.Остатки
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПодразделениеКомпании,
			|	СтатьяДоходовИРасходов"; 
			Запрос.Текст = ТекстЗапроса;
			Запрос.УстановитьПараметр("МоментВремени", ЭтотОбъект.МоментВремени());
			Запрос.УстановитьПараметр("СписокПодразделений", ТаблицаПереноса.ВыгрузитьКолонку("Источник"));
			
			// наложим блокировку на считываемые данные
			СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ДоходыИРасходы");
			ЗначенияБлокировки = Новый Соответствие;
			ЗначенияБлокировки.Вставить("Период",      Новый Диапазон(, Дата)); 
			СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаПереноса);
			ОписаниеИсточника = Новый Соответствие;
			ОписаниеИсточника.Вставить("ПодразделениеКомпании", "Источник");
			обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл						
				// Сначала выполняем движение расход по небалансовому подразделению.
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.Подразделение          = Выборка.ПодразделениеКомпании;
				НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДиР.ХозОперация            = Справочники.ХозОперации.ПереносОстатковДоходовИРасходовНаБалансовыйУровень;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Выборка.СтатьяДоходовИРасходов;
				НаборЗаписейДиР.ВУпрВалюте             = Истина;
				НаборЗаписейДиР.Расход                 = Выборка.РасходУпрОстаток;
				НаборЗаписейДиР.Доход                  = Выборка.ДоходУпрОстаток;
				Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ; 
				
				// Теперь делаем приход уже по балансовому подразделению.
				НаборЗаписейДиР.Подразделение          = Выборка.Приемник;
				НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
				НаборЗаписейДиР.ХозОперация            = Справочники.ХозОперации.ПереносОстатковДоходовИРасходовНаБалансовыйУровень;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Выборка.СтатьяДоходовИРасходов;
				НаборЗаписейДиР.ВУпрВалюте             = Истина;
				НаборЗаписейДиР.Расход                 = Выборка.РасходУпрОстаток;
				НаборЗаписейДиР.Доход                  = Выборка.ДоходУпрОстаток;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ; 	
				
				#Если Клиент Тогда			
					Состояние("Перенос остатков с подразделения <" + Выборка.ПодразделениеКомпании + "> на балансовое подразделение <" + Выборка.Приемник + ">");
				#КонецЕсли
			КонецЦикла;
			
			// Движения записываем сразу, т.к. они понадобятся нам далее.
			Движения.ДоходыИРасходы.Записать();
			МенеджерВТ.Закрыть();
		КонецЕсли;
	КонецЕсли;
	
	// Действие №2: Производим закрытие статей балансовых подразделений и корневого элемента.
	// Порядок закрытия статей содержится в регистре сведений "ПорядокЗакрытияБалансовыхСтатей". 
	// Если в регистре сведений не присутствует статья, значит она распределяться не будет.
	// В запросе нам необходимо получить все статьи, присутствующие в регистре сведений в разрезе балансовых
	// подразделений. Даже, если по статье нет остатков, он может появится в ходе распределения.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Подразделение КАК ПодразделениеКомпании,
	|	ВложенныйЗапрос.Порядок КАК Порядок,
	|	ВложенныйЗапрос.Статья,
	|   ВложенныйЗапрос.Статья.СтатьиРаспределения КАК СтатьиРаспределения,
	|	ЕСТЬNULL(ДоходыИРасходыОстатки.ДоходУпрОстаток, 0) КАК Доход,
	|	ЕСТЬNULL(ДоходыИРасходыОстатки.РасходУпрОстаток, 0) КАК Расход
	|ИЗ
	|	(ВЫБРАТЬ
	|			ПодразделенияКомпании.Ссылка КАК Подразделение,
	|			ПорядокЗакрытияБалансовыхСтатей.Статья КАК Статья,
	|			ПорядокЗакрытияБалансовыхСтатей.Порядок КАК Порядок
	|		ИЗ
	|			Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании,
	|			РегистрСведений.ПорядокЗакрытияБалансовыхСтатей КАК ПорядокЗакрытияБалансовыхСтатей
	|";
	
	Если СпособВеденияБаланса=Перечисления.СпособВеденияБаланса.ПоПодразделению Тогда
		ТекстЗапроса = ТекстЗапроса + "
		| ГДЕ ПодразделенияКомпании.Ссылка.Балансовое";
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапроса + "
	|    ) КАК ВложенныйЗапрос 	
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы.Остатки(
	|		&МоментВремени,	(НЕ ПодразделениеКомпании.ПометкаУдаления)";
	
	Если СпособВеденияБаланса=Перечисления.СпособВеденияБаланса.ПоПодразделению Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|  И (ПодразделениеКомпании.Балансовое)";
	КонецЕсли;    
	
	ТекстЗапроса = ТекстЗапроса + "
	|		) КАК ДоходыИРасходыОстатки
	|		ПО ВложенныйЗапрос.Подразделение = ДоходыИРасходыОстатки.ПодразделениеКомпании
	|			И ВложенныйЗапрос.Статья = ДоходыИРасходыОстатки.СтатьяДоходовИРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	Порядок";   	
	  	
	Запрос  = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(ЭтотОбъект.МоментВремени(), ВидГраницы.Включая));
	тзСтатьиРаспределения = Запрос.Выполнить().Выгрузить();
	
	Если тзСтатьиРаспределения.Количество()<>0 Тогда
		тзСтатьиРаспределения.Колонки.Добавить("Распределена", Новый ОписаниеТипов("Булево"));	
	КонецЕсли;
	
	// Выполняем распределение строго по порядку. После того как статья распределена, 
	// помечаем ее. Если в последующем некоторая статья будет пытаться распределиться на уже распределенную, то
	// это ошибка. В этом случае будем предупреждать пользователя и распределять на служебную статью.
	СтруктураПоиска = Новый Структура;
	
	// Получим служебную статью доходов и расходов. 
	СпецСтатьяДляРаспределения = обПраво("СлужебнаяСтатьяДляРаспределения", Права,,ЭтотОбъект);
	
	// Получаем набор записей.
	НаборЗаписейДиР = Движения.ДоходыИРасходы;	
	
	Для каждого ТекСтрока Из тзСтатьиРаспределения Цикл  	
		#Если Клиент Тогда
			Состояние("Распределение статьи: " + ТекСтрока.Статья);		
		#КонецЕсли
		
		Если (ТекСтрока.Расход=0) И (ТекСтрока.Доход=0) Тогда
			Продолжить;
		КонецЕсли;
		
		// Получаем табличную часть статьи.
		тзТабличнаяЧасть = ТекСтрока.СтатьиРаспределения;
		
		// Выполняем расход по текущей статье на общие суммы. 		
		НаборЗаписейДиР.Подразделение          = ТекСтрока.ПодразделениеКомпании;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.ХозОперация            = Справочники.ХозОперации.РаспределениеДоходовИРасходов;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = ТекСтрока.Статья;
		НаборЗаписейДиР.ВУпрВалюте             = Истина;
		НаборЗаписейДиР.Расход                 = ТекСтрока.Расход;
		НаборЗаписейДиР.Доход                  = ТекСтрока.Доход;
		НаборЗаписейДиР.СтатьяДиРИсточник      = Неопределено;
		Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ; 

		// Находим количество статей, на которые  необходимо распределять.
		КоличествоСтатей = тзТабличнаяЧасть.Количество();
		Если КоличествоСтатей=0 Тогда // Распределять некуда, а нужно. Поэтому распределяем на спец.статью.
			КоличествоСтатей = 1;
			НоваяСтрока = тзТабличнаяЧасть.Добавить();
			НоваяСтрока.СтатьяРаспределения = СпецСтатьяДляРаспределения;
		КонецЕсли;
		
		// Получим суммы, приходящиеся на каждую статью.  
		ВремРасход = Окр(ТекСтрока.Расход / КоличествоСтатей, 2);
		ВремДоход  = Окр(ТекСтрока.Доход  / КоличествоСтатей, 2); 
		      		
		// Проходим по табличной части распределяемой статьи.
		Для каждого СтрокаТабличнойЧасти Из тзТабличнаяЧасть Цикл  	
			ЭтоПоследняяСтрока  = (тзТабличнаяЧасть.Индекс(СтрокаТабличнойЧасти)=(КоличествоСтатей-1));
			СтатьяРаспределения = СтрокаТабличнойЧасти.СтатьяРаспределения;
			
			// Проверим, а не пытаемся ли мы распределить статью на уже распределенную статью. 			
			СтруктураПоиска.Вставить("ПодразделениеКомпании", ТекСтрока.ПодразделениеКомпании);
			СтруктураПоиска.Вставить("Статья",                СтатьяРаспределения);
			мсвСтрок = тзСтатьиРаспределения.НайтиСтроки(СтруктураПоиска);
			
			Если мсвСтрок.Количество()<>0 Тогда  				 
				Если мсвСтрок[0].Распределена Тогда // Придется распределять на спец. статью.
					СтатьяРаспределения = СпецСтатьяДляРаспределения;
					
					СтруктураПоиска.Вставить("ПодразделениеКомпании", ТекСтрока.ПодразделениеКомпании);
					СтруктураПоиска.Вставить("Статья",                СтатьяРаспределения);
					мсвСтрок = тзСтатьиРаспределения.НайтиСтроки(СтруктураПоиска);
					Если мсвСтрок.Количество()<>0 Тогда
						Если мсвСтрок[0].Распределена Тогда // Спец. статья уже сама распределена. Значит ошибка.
							Отказ = Истина;
							РезультатОбработки = РезультатОбработки + 
							" - Ошибка при распределении статьи <" + ТекСтрока.Статья + "> на статью <" + СтрокаТабличнойЧасти.СтатьяРаспределения + ">.
							|Нельзя производить распределение на уже распределенную статью.
							|Распределение на служебную статью <" + СпецСтатьяДляРаспределения  + "> невозможно." + Символы.ПС;
						Иначе // Спец. статья распределяемая, но еще не распределена
							РезультатОбработки = РезультатОбработки + 
							" - Ошибка при распределении статьи <" + ТекСтрока.Статья + "> на статью <" + СтрокаТабличнойЧасти.СтатьяРаспределения + ">.
							|Нельзя производить распределение на уже распределенную статью.
							|Произведено распределение на служебную статью <" + СпецСтатьяДляРаспределения  + ">." + Символы.ПС;
							
							мсвСтрок[0].Расход = мсвСтрок[0].Расход + ?(ЭтоПоследняяСтрока, ТекСтрока.Расход - ВремРасход*(КоличествоСтатей-1),  ВремРасход);
							мсвСтрок[0].Доход  = мсвСтрок[0].Доход  + ?(ЭтоПоследняяСтрока, ТекСтрока.Доход  - ВремДоход*(КоличествоСтатей-1),   ВремДоход); 	
						КонецЕсли;
					Иначе // Спец. статья НЕ распределяемая, поэтому смело распределяем на нее.
						РезультатОбработки = РезультатОбработки + 
						" - Ошибка при распределении статьи <" + ТекСтрока.Статья + "> на статью <" + СтрокаТабличнойЧасти.СтатьяРаспределения + ">.
						|Нельзя производить распределение на уже распределенную статью.
						|Произведено распределение на служебную статью <" + СпецСтатьяДляРаспределения  + ">." + Символы.ПС;
					КонецЕсли;
				Иначе
					// Статья еще не распределена. Значит увеличиваем ее ресурсы на распределяемую сумму.
					// Однако, возможен случай, когда статья распределяется на саму себя. Хотя такая ситуация
					// бессмысленна с прикладной точки зрения, но вполне возможна. Например, если пользователь указал, что
					// необходимо распределять статью (которая является спец. статьей), но не указал на какие статьи ее распределять.
					// В этом случае, будет задействована, опять-таки, спец. статья (т.к. распределять-то куда-то надо),
					// и в результате мы имеем, что спец. статья распределяется на спец. статью.
					Если ТекСтрока.Статья<>мсвСтрок[0].Статья Тогда
						мсвСтрок[0].Расход = мсвСтрок[0].Расход + ?(ЭтоПоследняяСтрока, ТекСтрока.Расход - ВремРасход*(КоличествоСтатей-1),  ВремРасход);
						мсвСтрок[0].Доход  = мсвСтрок[0].Доход  + ?(ЭтоПоследняяСтрока, ТекСтрока.Доход  - ВремДоход*(КоличествоСтатей-1),   ВремДоход); 
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;  			
			
			НаборЗаписейДиР.Подразделение          = ТекСтрока.ПодразделениеКомпании;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДиР.ХозОперация            = Справочники.ХозОперации.РаспределениеДоходовИРасходов;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = СтатьяРаспределения;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.СтатьяДиРИсточник      = ТекСтрока.Статья; // Статья, с которой производится распределение.
			
			// Если это последняя статья из табличной части, то списываем весь остаток, чтобы не оставались "копейки".
			НаборЗаписейДиР.Расход                 = ?(ЭтоПоследняяСтрока, ТекСтрока.Расход - ВремРасход*(КоличествоСтатей-1),  ВремРасход);
			НаборЗаписейДиР.Доход                  = ?(ЭтоПоследняяСтрока, ТекСтрока.Доход  - ВремДоход*(КоличествоСтатей-1),   ВремДоход);
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ; 			
		КонецЦикла;
		
		// Помечаем статью как распределенную.
		ТекСтрока.Распределена = Истина;
	КонецЦикла;
	
	
	// если модифицировали документ, то запишем его.
	// стандартное поведение
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли; 
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
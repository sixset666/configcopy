// Модуль документа ОбслуживаниеАктивов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ВалютаКурсДок Экспорт;        // Кэш валюты и курса документа
Перем СкладКомп Экспорт;            // Кэш склада компании
Перем РезультатЗапросаПоТоварам;	// выбора по расходным материалам
Перем РезультатЗапросаПоОприходованнымЦенностям; //выборка по оприходованным ценностям
Перем СуммаДок;                     // Сумма документа
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ФлагПерерасчет;               // Переменная для обозначения того, что ведется перерасчет суммы документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ШапкаДокумента;				// кэш реквизитов шапки документа
Перем ТребуетсяПересчет Экспорт;	// флаг необходимости пересчета ТЧ оприходованные ценности после выбора валюты / курса


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары = Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура", 3);
	ОбязательныеТовары.Вставить("Количество", 1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения", 3);
	ОбязательныеТовары.Вставить("Коэффициент", 1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры", 2);
	Если обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("Партия",2);
		ОбязательныеТовары.Вставить("ГТД",2);
	КонецЕсли;
	
	// Обязательные поля таблицы ОприходованныеЦенности
	ОбязательныеОприходованныеЦенности = Новый Структура();
	ОбязательныеОприходованныеЦенности.Вставить("Номенклатура", 3);
	ОбязательныеОприходованныеЦенности.Вставить("Количество", 1);
	ОбязательныеОприходованныеЦенности.Вставить("ЕдиницаИзмерения", 3);
	ОбязательныеОприходованныеЦенности.Вставить("Коэффициент", 1);
	//ОбязательныеОприходованныеЦенности.Вставить("ХарактеристикаНоменклатуры", 2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании", 1);
	ОбязательныеРеквизиты.Вставить("Автор", 1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента", 1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента", 1);
	ОбязательныеРеквизиты.Вставить("Актив", 1);	
	ОбязательныеРеквизиты.Вставить("ТипОбслуживания", 1);
	ОбязательныеРеквизиты.Вставить("ХозОперация", 1);
	Если ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаРасходыПодразделения Тогда
		ОбязательныеРеквизиты.Вставить("СтатьяРасходов", 1);
	КонецЕсли;
	Если СтоимостьСобственныхУслуг > 0 Тогда
		ОбязательныеРеквизиты.Вставить("ПодразделениеОказывающееУслугу", 1);
		ОбязательныеРеквизиты.Вставить("СтатьяДоходов", 1);
	КонецЕсли;
	Если СтоимостьСтороннихУслуг > 0 Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент", 1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов", 1);
	КонецЕсли; 
	ОбязательныеРеквизиты.Вставить("Товары", ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("ОприходованныеЦенности",ОбязательныеОприходованныеЦенности);
	Если ОприходованныеЦенности.Количество() > 0 Тогда
		ОбязательныеРеквизиты.Вставить("СтатьяДоходовПоОприходованнымЦенностям", 1);
	КонецЕсли; 
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения характеристики номенклатуры
//                                                                
// Параметры:
//  ИмяТабличнойЧасти - имя табличной части документ, для которой выполняется проверка.
//  СтрокаТовары - строка табличной части документа - ссылка на строку.
//  Ошибки       - строка - содержит описание ошибки.
//
// Возвращаемое значение:
//  Булево 
//
Функция ПроверитьЗаполнениеХарактеристики(ИмяТабличнойЧасти, СтрокаТовары, Ошибки="") Экспорт
	Если ИмяТабличнойЧасти = "ОприходованныеЦенности" Тогда
		УчетВедется = СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ  СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2;	  
		Если УчетВедется И обЗначениеНеЗаполнено(СтрокаТовары.ХарактеристикаНоменклатуры)  Тогда
	 		Возврат Ложь;
		КонецЕсли;	
	КонецЕсли;
	Возврат Истина;
КонецФункции // ПроверитьЗаполнениеХарактеристики()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			Если БалансВедетсяПоОрганизации И (СтоимостьСобственныхУслуг>0) Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ПодразделениеОказывающееУслугу");
			КонецЕсли; 			
			Если СтоимостьСтороннихУслуг>0 Тогда
				КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонецЕсли;
			Если НЕ СкладКомпании.Пустая() Тогда
				КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонецЕсли;
			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки", КонтролируемыеРеквизитыШапки); 			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ОбслуживаниеАктиваНаРасходыПодразделения", "Обслуживание на расходы подразделения", Истина);
	СписокХозОпераций.Добавить("ОбслуживаниеАктиваНаСтоимостьАктива", "Обслуживание на стоимость актива", Ложь);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  ШапкаДокумента - ДокументСсылка - этотобъект, выборка.
//
// Возвращаемое значение:
//  Возвращает результат выборки.
// 
Функция ПолучитьРезультатЗапросаПоТоварам()
	//ЕстьПартии = обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект);
	
	// проверяем, присутствуют ли партии в табличной части
		ЕстьПартии = Ложь;
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
				ЕстьПартии = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;

	ЕстьСуммаРозн = (ОприходованныеЦенности.Итог("СуммаРозничная") + Товары.Итог("СуммаРозничная")) > 0;
    ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбслуживаниеАктиваТовары.Номенклатура,
	|	ОбслуживаниеАктиваТовары.ХарактеристикаНоменклатуры,
	|	СУММА(ОбслуживаниеАктиваТовары.Количество*ОбслуживаниеАктиваТовары.Коэффициент) КАК Количество,
	|	СУММА(0) КАК Резерв";
	Если ЕстьПартии Тогда
		ТекстЗапроса = ТекстЗапроса + ", ОбслуживаниеАктиваТовары.Партия КАК Партия";
		ТекстЗапроса = ТекстЗапроса + ", ОбслуживаниеАктиваТовары.ГТД КАК ГТД";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + ", СУММА(ОбслуживаниеАктиваТовары.СуммаРозничная) КАК СуммаРозничная, ОбслуживаниеАктиваТовары.ЦенаРозничная";
	ТекстЗапроса = ТекстЗапроса + "	ИЗ
	|	Документ.ОбслуживаниеАктива.Товары КАК ОбслуживаниеАктиваТовары
	|ГДЕ
	|	ОбслуживаниеАктиваТовары.Ссылка = &Ссылка
	|	И ОбслуживаниеАктиваТовары.Номенклатура.ВидНоменклатуры <> &Услуга
	|СГРУППИРОВАТЬ ПО Номенклатура, ХарактеристикаНоменклатуры, ЦенаРозничная"+"
	|	"+?(ЕстьПартии,", Партия, ГТД","")+"
	|";

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ = Ложь;
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// определение необходимости формирования корректирующих проводок
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(ШапкаДокумента.СкладКомпании.Подразделение);
	ПодразделениеПодразделениеКомпании = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ПодразделениеКомпании);
	
	// проверяем, присутствуют ли партии в табличной части
		ЕстьПартии = Ложь;
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
				ЕстьПартии = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	
	// списываем с партий
	НаборЗаписейПартии = Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании             = ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.ШапкаДокумента			 = ШапкаДокумента;
	НаборЗаписейПартии.ИмяРеквизитаДокумент		 = ?(ЕстьПартии, "Партия", "");
	НаборЗаписейПартии.СтатусПартии				 = Неопределено;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
	Отказ = НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
	
	// Зачитываем принятые на реализацию товары, которые были списаны
	НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
	НаборЗаписейРеализованныеТовары.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейРеализованныеТовары.Списание                  = Истина;
	НаборЗаписейРеализованныеТовары.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
	НаборЗаписейРеализованныеТовары.ШапкаДокумента            = ШапкаДокумента;
	Отказ = НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
	
	
	// подготовим таблицу движений в разрезе подразделений только собственных списанных партий
	ТаблицаСписанийПартий = Движения.ПартииТоваровКомпании.Выгрузить();
	ТаблицаСписанийПартий.Свернуть("СтатусПартии","СуммаУпр");
	СтруктураОтбора=Новый Структура("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
	МассивНайденныхСтрок=ТаблицаСписанийПартий.НайтиСтроки(СтруктураОтбора);
	Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
		ТаблицаСписанийПартий.Удалить(МассивНайденныхСтрок[Сч]);
	КонецЦикла;
	
	СебестоимостьУпр = ТаблицаСписанийПартий.Итог("СуммаУпр");
	
	Если БалансВедетсяПоПодразделениям И (ПодразделениеСклад<>ПодразделениеПодразделениеКомпании) И СебестоимостьУпр<>0 Тогда
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = ШапкаДокумента.СкладКомпании.Подразделение;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.Доход                  = СебестоимостьУпр;	
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
	КонецЕсли;
	
	// подготовим таблицу движений в разрезе подразделений комиссионных товаров
	ТаблицаСписанийПартийРеализованных = Движения.РеализованныеТовары.Выгрузить();
	ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов","СуммаУпр");
	
	ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
		Если БалансВедетсяПоПодразделениям Тогда
			СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
		Иначе
			СтрокаСписания.Подразделение = ШапкаДокумента.ПодразделениеКомпании;
		КонецЕсли;
	КонецЦикла;	
	ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение","СуммаУпр");
	
	Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
		ПодразделениеДоговора = обПолучитьБалансовоеПодразделение(СтрокаСписания.Подразделение);
		Если БалансВедетсяПоПодразделениям И (ПодразделениеДоговора<>ПодразделениеПодразделениеКомпании) И СтрокаСписания.СуммаУпр<>0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСписания.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Расход                 = СтрокаСписания.СуммаУпр;	
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЦикла;
	
	Если ШапкаДокумента.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		СуммаДок = НаборЗаписейПартии.Итог("Сумма");
	Иначе
		СуммаДок = обПересчет(НаборЗаписейПартии.Итог("СуммаУпр"), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ?(обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.Дата, ШапкаДокумента.КурсВалютыУпр), ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента);
	КонецЕсли;
	
	Если БалансВедетсяПоПодразделениям И (ПодразделениеСклад<>ПодразделениеПодразделениеКомпании) Тогда
		// Корректируем списание на расходы нашего подразделения
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.Подразделение          = ШапкаДокумента.ПодразделениеКомпании;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
		НаборЗаписейДиР.ВУпрВалюте             = Ложь;
		НаборЗаписейДиР.Доход                  = СуммаДок;
		НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;	
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиямОприходованныеЦенности(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// посчитаем сумму товаров
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	СУММА(ДокументТовары.СуммаВсего) КАК СуммаВсего
	|ИЗ
	|	Документ.ОбслуживаниеАктива.ОприходованныеЦенности 	КАК ДокументТовары
	|
	|ГДЕ  
	|	ДокументТовары.Ссылка = &ДокументСсылка
	|");
	Запрос.УстановитьПараметр("ДокументСсылка",ШапкаДокумента.Ссылка);
	ДеревоДоходовИРасходов=Запрос.Выполнить().Выгрузить();
	
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		// Доходы и расходы на себестоимость не оприходованных партий
		Если ДеревоДоходовИРасходов.Количество() = 0 Тогда
			НайденнаяСтрокаТоваров = Неопределено;
		Иначе
			НайденнаяСтрокаТоваров = ДеревоДоходовИРасходов[0];
		КонецЕсли;
		СуммаВсего=?(НайденнаяСтрокаТоваров<>Неопределено,НайденнаяСтрокаТоваров.СуммаВсего,0);
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		// в случае если ведется баланс по подразделению передадим подразделение соответствующее корреспонденции
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.СкладКомпании.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
		НаборЗаписейДоходыИРасходы.Расход = СуммаВсего;
		НаборЗаписейДоходыИРасходы.Приход();
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// определим необходимость формирования корректирующих проводок
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.ЕстьСтавкаНДС=Истина;
	НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	НаборЗаписейПартии.ПоБазовомуКоличеству=Истина;
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
	НаборЗаписейПартии.РежимДопРасходы=0;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам = РезультатЗапросаПоОприходованнымЦенностям;
	Отказ=НЕ НаборЗаписейПартии.Приход() ИЛИ Отказ;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет общей балансовой стоимости активов
//
Функция РассчитатьСуммуВсего() Экспорт
	СуммаСписания = дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании",тКэшСуммСписания);
	СуммаВсего = СуммаСписания + СтоимостьСобственныхУслуг + СтоимостьСтороннихУслуг + ОприходованныеЦенности.Итог("Сумма");
	Возврат СуммаВсего;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя = "ВалютаДокумента" И (ВалютаКурсДок.Валюта <> ВалютаДокумента ИЛИ ВалютаКурсДок.Курс <> КурсДокумента) Тогда
		СтоимостьСобственныхУслуг = обПересчет(СтоимостьСобственныхУслуг, ВалютаКурсДок.Валюта, ВалютаКурсДок.Курс, ВалютаДокумента, КурсДокумента);
		СтоимостьСтороннихУслуг   = обПересчет(СтоимостьСтороннихУслуг, ВалютаКурсДок.Валюта, ВалютаКурсДок.Курс, ВалютаДокумента, КурсДокумента);
		Для каждого ТекСтрока Из ОприходованныеЦенности Цикл
			ТекСтрока.Цена = обПересчет(ТекСтрока.Цена, ВалютаКурсДок.Валюта, ВалютаКурсДок.Курс, ВалютаДокумента, КурсДокумента);
			ЭтотОбъект.ОбработкаРеквизита("Товары.Цена", ТекСтрока);
		КонецЦикла;
		ВалютаКурсДок.Валюта = ВалютаДокумента;
		ВалютаКурсДок.Курс   = КурсДокумента;
		
		Возврат Истина;
		
	ИначеЕсли Имя = "ТипЦен" Тогда
		ОбнулятьСуммовыеПоказатели = Неопределено;
		// если изменились, тип цен, валюта или курс, то получим цену
		Для Каждого ТекСтрока Из ОприходованныеЦенности Цикл
			ТекСтрока.Цена = обПолучитьЦену(ТипЦен,ТекСтрока.Номенклатура,?(ЭтотОбъект.ЭтоНовый(),ЭтотОбъект.Дата,ЭтотОбъект.МоментВремени()),,ВалютаДокумента,КурсДокумента,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.ЕдиницаИзмерения,ПодразделениеКомпании);
			#Если Клиент Тогда
			Если ТекСтрока.Цена=0 И ОбнулятьСуммовыеПоказатели=Неопределено Тогда // Если вопрос еще не задавался, то зададим его.
				Ответ = Вопрос("В табличной части присутствуют товары с нулевой ценой для выбранного типа цен <" + ТипЦен + ">.
				|Обнулить цены для данных позиций?", РежимДиалогаВопрос.ДаНет);
				Если Ответ=КодВозвратаДиалога.Нет Тогда
					ОбнулятьСуммовыеПоказатели = Ложь;
				Иначе
					ОбнулятьСуммовыеПоказатели = Истина;
				КонецЕсли;
			Иначе
				ОбнулятьСуммовыеПоказатели = Ложь;
			КонецЕсли;
			#Иначе
			    ОбнулятьСуммовыеПоказатели = Истина;
			#КонецЕсли
			Если ОбнулятьСуммовыеПоказатели Тогда
				ТекСтрока.Цена = 0;	
			КонецЕсли;
		    ЭтотОбъект.ОбработкаРеквизита("Товары.Цена", ТекСтрока);
		КонецЦикла;
		ВалютаКурсДок.Валюта = ВалютаДокумента;
		ВалютаКурсДок.Курс   = КурсДокумента;
		Возврат Истина;
	ИначеЕсли Имя = "ТипОбслуживания" Тогда
		// дефолтное заполнение статьи расходов
		Если ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаРасходыПодразделения Тогда
			СтатьяРасходов = ТипОбслуживания.СтатьяДоходовИРасходов;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		// отработаем специфику розничного склада
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) Тогда
			Если (СкладКомпании.Розничный) И (НЕ обЗначениеНеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Для Каждого СтрокаТоваров Из ЭтотОбъект.ОприходованныеЦенности Цикл
					Рез=ОбработкаРеквизита("ОприходованныеЦенности.УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма);
				КонецЦикла;
				Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
					Рез=ОбработкаРеквизита("Товары.УстановитьРозничнуюЦену",СтрокаТоваров,ЭтаФорма);
				КонецЦикла;				
			Иначе
				Для Каждого СтрокаТоваров Из ЭтотОбъект.ОприходованныеЦенности Цикл
					СтрокаТоваров.СуммаРозничная = 0;
					СтрокаТоваров.ЦенаРозничная = 0;
				КонецЦикла;
				Для Каждого СтрокаТоваров Из ЭтотОбъект.Товары Цикл
					СтрокаТоваров.СуммаРозничная = 0;
					СтрокаТоваров.ЦенаРозничная = 0;
				КонецЦикла;
            КонецЕсли;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Актив" Тогда
		Если НЕ обЗначениеНеЗаполнено(Актив) Тогда
			Если Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецодежда ИЛИ
				 Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка ИЛИ
				 Актив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Инструменты Тогда
				#Если Клиент Тогда
				Сообщить("В документ нельзя вводить активы вида """+СокрЛП(Актив.ВидПрочегоАктива)+"""");
				#КонецЕсли
				Актив = Справочники.ПрочиеАктивы.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;	
		Возврат Истина;	
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.ЕдиницаИзмерения,ПодразделениеКомпании);
			Рез=ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.Количество;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		Если ТекСтрока.Количество<>0 Тогда
			ТекСтрока.ЦенаРозничная=ТекСтрока.СуммаРозничная/ТекСтрока.Количество;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
				ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.ЕдиницаИзмерения,СкладКомпании.Подразделение);
			КонецЕсли;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	//обработка реквизитов ТЧ "Оприходованные ценности"	
	ИначеЕсли Имя="ОприходованныеЦенности.УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (НЕ обЗначениеНеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			Если (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура)) И (ТекСтрока.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга) Тогда
				ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.ЕдиницаИзмерения,СкладКомпании.Подразделение);
			КонецЕсли;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("ОприходованныеЦенности.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ОприходованныеЦенности.ЦенаРозничная" Тогда
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.КоличествоБазовое;
		Возврат Истина;
		
	ИначеЕсли Имя="ОприходованныеЦенности.СуммаРозничная" Тогда
		Если (ТекСтрока.Количество=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=?(ТекСтрока.КоличествоБазовое=0,0,ТекСтрока.СуммаРозничная/ТекСтрока.КоличествоБазовое);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="ОприходованныеЦенности.Количество" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,"Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат ОбработкаРеквизита("ОприходованныеЦенности.УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="ОприходованныеЦенности.Номенклатура" Тогда
		ДопПараметры = Новый Структура("ТабличнаяЧастьТовары",ОприходованныеЦенности);
		Рез=дкОбработкаРеквизита(ЭтотОбъект,"Товары.Номенклатура",ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			Рез = ОбработкаРеквизита("ОприходованныеЦенности.УстановитьРозничнуюЦену",ТекСтрока,ЭтаФорма,ДопПараметры);
			Рез = ОбработкаРеквизита("ОприходованныеЦенности.УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		Попытка
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ХарактеристикаНоменклатуры) Тогда
				Если (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура) И (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
						ТекСтрока.ХарактеристикаНоменклатуры=Неопределено;
						ЭтотОбъект.ОбработкаРеквизита("Товары.ХарактеристикаНоменклатуры",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			КонецЕсли; 
		Исключение КонецПопытки;
		//Обработка отображения интерфейса
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			// покажем если надо колонку "характеристика номенклатуры"
			ТипНоменклатуры = ТекСтрока.Номенклатура.ТипНоменклатуры;
			Если обПраво("АвтоматическиСкрыватьКолонкуХарактеристик",ЭтотОбъект.Права,,ЭтотОбъект) Тогда
				Если НЕ ЭтаФорма.ЭлементыФормы.ОприходованныеЦенности.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
					ЭтаФорма.ЭлементыФормы.ОприходованныеЦенности.Колонки.ХарактеристикаНоменклатуры.Видимость = (ТипНоменклатуры.ИспользованиеХарактеристик=1 ИЛИ ТипНоменклатуры.ИспользованиеХарактеристик=2);
				КонецЕсли; 
			КонецЕсли;
			//установим свойства колонки характеристик в зависимости от типа номенклатуры
			ЭтаФорма.ЭлементыФормы.ОприходованныеЦенности.Колонки.ХарактеристикаНоменклатуры.ЭлементУправления.ОтметкаНезаполненного = Истина;
		КонецЕсли;
		Если ЭтаФорма<>Неопределено Тогда
			// изменим сумму документа
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// возвращает результат запроса по таблице ОприходованныеЦенности
Функция ПолучитьРезультатЗапросаПоОприходованнымЦенностям()
	// для поступления таблицу товаров получаем из документа, 
	// для расхода получаем таблицу из движений по остаткам + из документа получаем партии и ставки НДС
	ИмяРеквизитаДокумент = Неопределено;
	ТекстЗапроса="
	|ВЫБРАТЬ
	|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,";
	ТекстЗапроса=ТекстЗапроса+"
		|	ДокТовары.СтавкаНДС КАК СтавкаНДС,";
	
	ТекстЗапроса=ТекстЗапроса+"
		|	СУММА(ДокТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ДокТовары.Сумма) КАК Сумма,
		|	СУММА(ДокТовары.СуммаВсего) КАК СуммаВсего,";

	ТекстЗапроса = ТекстЗапроса + "
			|	ВЫБОР КОГДА ДокТовары.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1
			|			ТОГДА СУММА(ДокТовары.Номенклатура.Вес * ДокТовары.Количество)
			|			ИНАЧЕ ВЫБОР КОГДА ДокТовары.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2
			|				ТОГДА СУММА(ДокТовары.ЕдиницаИзмерения.Вес * ДокТовары.Количество)
			|				ИНАЧЕ 0 КОНЕЦ КОНЕЦ КАК Вес,";	

	ТекстЗапроса=ТекстЗапроса+"
	|	СУММА(ДокТовары.Количество*ДокТовары.Коэффициент) КАК Количество,
	|	ДокТовары.Номенклатура КАК Номенклатура";
	
	Если СкладКомпании.Розничный Тогда
    	ТекстЗапроса = ТекстЗапроса + ", СУММА(ДокТовары.СуммаРозничная) КАК СуммаРозничная, ДокТовары.ЦенаРозничная";
	КонецЕсли;
	ТекстЗапроса=ТекстЗапроса+"
	|ИЗ
	|	Документ.ОбслуживаниеАктива.ОприходованныеЦенности КАК ДокТовары
	|ГДЕ
	|	  ДокТовары.Ссылка=&Ссылка
	|	И ДокТовары.Номенклатура.ВидНоменклатуры<>&Услуга
	|
	|СГРУППИРОВАТЬ ПО Номенклатура, ХарактеристикаНоменклатуры"+?(СкладКомпании.Розничный,",ЦенаРозничная","")+ ",СтавкаНДС" +"
	|
	|";
	ЗапросТовары=Новый Запрос(ТекстЗапроса);
	ЗапросТовары.УстановитьПараметр("Ссылка", Ссылка);
	ЗапросТовары.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
	
	// для прихода все просто
	Возврат ЗапросТовары.Выполнить();
	
КонецФункции

// Функция возвращает результат выполнения запроса по авктивам
Функция ПолучитьРезультатЗапросаПоАктивам(БалансоваяСтоимость, СуммаОбслуживания)
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ОбслуживаниеАктива.Актив,
	               |	&БалансоваяСтоимость КАК БалансоваяСтоимость,
	               |	&СуммаОбслуживания КАК СуммаОбслуживания,
	               |	0 КАК Амортизация,
	               |	ПрочиеАктивыВЭксплуатацииОстатки.МОЛ,
	               |	ПрочиеАктивыВЭксплуатацииОстатки.ТипЭксплуатации,
	               |	ПрочиеАктивыВЭксплуатацииОстатки.ПодразделениеКомпании,
				   |	ПрочиеАктивыВЭксплуатацииОстатки.Автомобиль
	               |ИЗ
	               |	Документ.ОбслуживаниеАктива КАК ОбслуживаниеАктива
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(
	               |				&Момент,
	               |				ПрочийАктив = &Актив
	               |					И ПодразделениеКомпании = &Подразделение) КАК ПрочиеАктивыВЭксплуатацииОстатки
	               |		ПО ОбслуживаниеАктива.Актив = ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив
	               |			И ОбслуживаниеАктива.ПодразделениеКомпании = ПрочиеАктивыВЭксплуатацииОстатки.ПодразделениеКомпании
	               |ГДЕ
	               |	ОбслуживаниеАктива.Ссылка = &Ссылка
	               |
	               |ДЛЯ ИЗМЕНЕНИЯ
	               |	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки";

	// наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ПрочиеАктивыВЭксплуатации");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, Дата)); 
	ЗначенияБлокировки.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ЗначенияБлокировки.Вставить("ПрочийАктив", Актив);
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
				   
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Момент", МоментВремени());
	Запрос.УстановитьПараметр("Актив", Актив);
	Запрос.УстановитьПараметр("Подразделение", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("БалансоваяСтоимость", БалансоваяСтоимость);
	Запрос.УстановитьПараметр("СуммаОбслуживания", СуммаОбслуживания);
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоАктивам()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Обслуживание актива"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОбслуживаниеАктива(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ОбслуживаниеАктива");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
    ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации			= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПодразделенияКомпании	= спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеАктива					= спПолучитьНаименование(Актив);
	ОбластьМакета.Параметры.ПредставлениеТипаОбслуживания		= спПолучитьНаименование(ТипОбслуживания);
	ОбластьМакета.Параметры.ПредставлениеСтатьиРасходов			= спПолучитьНаименование(СтатьяРасходов);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//вывод шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.СтоимостьСобственныхУслуг	= Формат(СтоимостьСобственныхУслуг,ФорматВыводаСуммы) + " "+ВалютаДокумента;
	ОбластьМакета.Параметры.СтоимостьСтороннихУслуг		= Формат(СтоимостьСтороннихУслуг,ФорматВыводаСуммы)   + " "+ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал"); // вдруг строк нет...
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда
		//теперь выводим шапку
		ОбластьШапкаТаблицы.Параметры.Товар = "Расходные материалы";
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
		//готовим области строки
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0);
		
		ОбластьИтог = Макет.ПолучитьОбласть("Итог");
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
			
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			
			Сумма = дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании", тКэшСуммСписания, СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры, СтрокаТабличнойЧасти.НомерСтроки);
			Цена  = Сумма / СтрокаТабличнойЧасти.Количество / СтрокаТабличнойЧасти.Коэффициент;
			
			СтруктураСтроки.Вставить("Цена",		Формат(Цена,  ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("СуммаВсего",	Формат(Сумма, ФорматВыводаСуммы));
			ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьИтог);
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
				Если ЭтотОбъект.ОприходованныеЦенности.Количество() > 0 Тогда
					мсвДопОбластиПодвала.Добавить(ОбластьШапкаТаблицы);
				КонецЕсли;
			КонецЕсли;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			//добавляем итоги
			СтруктураСтроки.Вставить("СуммаВсего", Сумма); // чтобы округление не сказалось на итоговой сумме
			дкДобавитьИтогиПоСтранице(СтруктураСтроки,СтруктураИтоговПоСтранице);
			
		КонецЦикла;

		//выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		ОбластьИтог.Параметры.ВалютаДокумента = ВалютаДокумента;
		СуммаВсего = дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании", тКэшСуммСписания);
		ОбластьИтог.Параметры.СуммаВсего = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьИтог.Параметры.СуммаПрописью = "Всего наименований " + ВыборкаТабличнойЧасти.Количество()
		+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
		ОбластьИтог.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьИтог, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.ОприходованныеЦенности;
	Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда
		//теперь выводим шапку
		Если НомерСтраницы <= 2 Тогда
			ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = "";
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "";
		КонецЕсли;
		ОбластьШапкаТаблицы.Параметры.Товар = "Оприходованные ценности";
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
		//готовим области строки
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0);
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			Цена  = СтрокаТабличнойЧасти.СуммаВсего / СтрокаТабличнойЧасти.Количество / СтрокаТабличнойЧасти.Коэффициент;
			СтруктураСтроки.Вставить("Цена",		Формат(Цена,  ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("СуммаВсего",Формат(СтрокаТабличнойЧасти.СуммаВсего,ФорматВыводаСуммы));		
			ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
						
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
			
		КонецЦикла;

		//выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		ОбластьИтог.Параметры.ВалютаДокумента = ВалютаДокумента;
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьИтог.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
		ОбластьИтог.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьИтог, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьОбслуживаниеАктива()

// Формирует печатную форму "ОС-3
// (Акт о приемке-сдаче отремонтированных, реконструированных, модернизированных объектов основных средств)"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОС3(ТабДокумент) Экспорт
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет("ОС3");
	
	//для начала настроим макет
    ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьПредставление(ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.КонтрагентПредставление = спПолучитьПредставление(ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО = ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.КонтрагентПоОКПО = ЭтотОбъект.Контрагент.КодПоОКПО;
	ОбластьМакета.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента;
	// Заполним информацию о руководителе организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	// Информация о договоре
	Если НЕ ОбЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
		ОбластьМакета.Параметры.ДатаДоговора = ДоговорВзаиморасчетов.ДатаНачала;
		ОбластьМакета.Параметры.НомерДоговора = ДоговорВзаиморасчетов.НомерДоговора;
	КонецЕсли; 
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Строка1 = Макет.ПолучитьОбласть("Строка1");
	Строка2 = Макет.ПолучитьОбласть("Строка2");
	Шапка2 = Макет.ПолучитьОбласть("Шапка2");
	Шапка2.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента;
	Подвал = Макет.ПолучитьОбласть("Подвал");
	Подвал.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента;
	
	ВРегВалюте = (ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
	ТекстСумм="";
	Если ВРегВалюте Тогда
		ТекстСумм=",
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатацииОстатки.БалансоваяСтоимостьОстаток, 0) - ЕСТЬNULL(ПрочиеАктивыВЭксплуатацииОстатки.СуммаАмортизацииОстаток, 0) КАК ОстаточнаяСтоимость,
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаОбслуживания, 0) КАК СуммаЗатрат";
	Иначе
		ТекстСумм=",
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатацииОстатки.БалансоваяСтоимостьУпрОстаток, 0) * &МножительКурса - ЕСТЬNULL(ПрочиеАктивыВЭксплуатацииОстатки.СуммаАмортизацииУпрОстаток, 0) * &МножительКурса КАК ОстаточнаяСтоимость,
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаОбслуживанияУпр, 0) * &МножительКурса КАК СуммаЗатрат";
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ОбслуживаниеАктива.Актив.Наименование КАК НаимОс,
	|	ОбслуживаниеАктива.Актив.ИнвентарныйНомер КАК ИнвНомер,
	|	ОбслуживаниеАктива.Актив.СерийныйНомер КАК ЗаводскойНомер,
	|	ОбслуживаниеАктива.Актив.СрокПолезногоИспользования КАК СрокЭкспл,
	|	ОбслуживаниеАктива.ТипОбслуживания КАК ВидРабот,
	|	ОбслуживаниеАктива.Актив"+ТекстСумм+"
	|ИЗ
	|	Документ.ОбслуживаниеАктива КАК ОбслуживаниеАктива
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	|		ПО ОбслуживаниеАктива.Актив = ПрочиеАктивыВЭксплуатации.ПрочийАктив
	|			И ОбслуживаниеАктива.Ссылка = ПрочиеАктивыВЭксплуатации.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(&Дата, ) КАК ПрочиеАктивыВЭксплуатацииОстатки
	|		ПО ОбслуживаниеАктива.ПодразделениеКомпании = ПрочиеАктивыВЭксплуатацииОстатки.ПодразделениеКомпании
	|			И ОбслуживаниеАктива.Актив = ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив
	|ГДЕ
	|	ОбслуживаниеАктива.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Дата",Дата);
	Если НЕ ВРегВалюте Тогда
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр) Тогда
			МножительКурса = обПересчет(1, ВалютаУправленческогоУчета, Дата , ВалютаДокумента, КурсДокумента);
		Иначе
			МножительКурса = обПересчет(1, ВалютаУправленческогоУчета, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
		КонецЕсли;
		Запрос.УстановитьПараметр("МножительКурса",МножительКурса);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий()Тогда
		Строка1.Параметры.Заполнить(Выборка);
		Строка2.Параметры.Заполнить(Выборка);
		Строка1.Параметры.ОстаточнаяСтоимость = Формат(Выборка.ОстаточнаяСтоимость,ФорматВыводаСуммы);
		СуммаЗатрат = Формат(Выборка.СуммаЗатрат,ФорматВыводаСуммы); 
		Строка2.Параметры.СуммаЗатрат = СуммаЗатрат;
		Подвал.Параметры.СуммаЗатрат = СуммаЗатрат;		
		Подвал.Параметры.СтоимостьКонечнаяПеч = Формат(?(ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаРасходыПодразделения,Выборка.ОстаточнаяСтоимость,Выборка.ОстаточнаяСтоимость + Выборка.СуммаЗатрат),ФорматВыводаСуммы);
	КонецЕсли;
	ТабДокумент.Вывести(Строка1);
	ТабДокумент.Вывести(Шапка2);
	ТабДокумент.Вывести(Строка2);
	ТабДокумент.Вывести(Подвал);
	
	// Печать оборотной стороны
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	ОбластьМакета = Макет.ПолучитьОбласть("ОборотнаяСторона");
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	ЧленКомиссии1 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	ЧленКомиссии2 = дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	ОбластьМакета.Параметры.Заполнить(Получил);
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	ТабДокумент.Вывести(ОбластьМакета);
	Возврат ТабДокумент;
КонецФункции // ПечатьОС3()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если Основание = Неопределено Тогда //ничего
	Иначе
		ТЧОснования = Основание[?(ТипЗнч(Основание) = Тип("ДокументСсылка.ВводВЭксплуатацию"), "Товары", "Активы")];
		СписокАктивов = Новый СписокЗначений;
		Для каждого ТекСтрокаТЧ Из ТЧОснования Цикл
			СписокАктивов.Добавить(ТекСтрокаТЧ[?(ТипЗнч(Основание) = Тип("ДокументСсылка.ВводВЭксплуатацию"), "Актив", "ПрочийАктив")]);
		КонецЦикла;
		Если СписокАктивов.Количество() = 0 Тогда //ничего
		ИначеЕсли СписокАктивов.Количество() = 1 Тогда //его и заполним
			Актив = СписокАктивов[0].Значение;
		Иначе //выберем из списка интерактивно
			ВыбЭлемент = СписокАктивов.ВыбратьЭлемент("Выберите обслуживаемый актив");
			Если ВыбЭлемент = Неопределено Тогда
			Иначе
				Актив = ВыбЭлемент.Значение;
			КонецЕсли;
		КонецЕсли;
		Товары.Очистить();
	КонецЕсли;
	
	Если ЭтоНовый() И ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаРасходыПодразделения Тогда
		СтатьяРасходов = Справочники.СтатьиДоходовИРасходов.РаботыПоОбслуживаниюАктивов;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Результат = Истина;
	Если НЕ ФлагПерерасчет Тогда
		Результат = дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	ФлагПерерасчет = Ложь;
	Если НЕ Результат Тогда
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли;
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// определение необходимости формирования корректирующих проводок
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	ПодразделениеПодразделениеОказывающееУслугу = обПолучитьБалансовоеПодразделение(ПодразделениеОказывающееУслугу);
	ПодразделениеПодразделениеКомпании = обПолучитьБалансовоеПодразделение(ПодразделениеКомпании);
			
	РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаПоТоварам();
	РезультатЗапросаПоОприходованнымЦенностям = ПолучитьРезультатЗапросаПоОприходованнымЦенностям();
	СуммаДок = 0;
	
	// 1) остатки товаров
	//списываем расходные материалы
	НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
	НаборЗаписейОстатки.ДвиженияПоРознице		  = СкладКомпании.Розничный;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам;
	Отказ = НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	
	//приходуем ценности
	НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
	НаборЗаписейОстатки.ДвиженияПоРознице		  = СкладКомпании.Розничный;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаПоОприходованнымЦенностям();
	Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;

	// 2) партии товаров
	// получим шапку документа
	ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
	//расходные материалы
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	СуммаДок = СуммаДок + СтоимостьСтороннихУслуг + СтоимостьСобственныхУслуг;
	// оприходованные ценности
	Отказ = НЕ ПровестиПоПартиямОприходованныеЦенности(Режим, Ссылка) ИЛИ Отказ;
	
	// 3) взаиморасчеты
	Если СтоимостьСтороннихУслуг > 0 Тогда
		НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании; 
		НаборЗаписейВзаиморасчеты.ДокументОбъект        	= ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения       	= Режим;
		НаборЗаписейВзаиморасчеты.Контрагент            	= Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов 	= ДоговорВзаиморасчетов;
		НаборЗаписейВзаиморасчеты.Сделка					= Неопределено;
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем = Неопределено;
		НаборЗаписейВзаиморасчеты.Сумма                 	= СтоимостьСтороннихУслуг;
		Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
		
		Если БалансВедетсяПоПодразделениям И  (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеПодразделениеКомпании) Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение = ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.Расход=СтоимостьСтороннихУслуг;	
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;	
	КонецЕсли;
	
	
	// 4) доход подразделению
	Если СтоимостьСобственныхУслуг > 0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДиР.Подразделение          = ПодразделениеОказывающееУслугу;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = СтатьяДоходов;
		НаборЗаписейДиР.ВУпрВалюте             = Ложь;
		НаборЗаписейДиР.Расход                  = СтоимостьСобственныхУслуг;
		Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		
		Если БалансВедетсяПоПодразделениям И (ПодразделениеПодразделениеОказывающееУслугу <> ПодразделениеПодразделениеКомпании) Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение = ПодразделениеОказывающееУслугу;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.Доход=СтоимостьСобственныхУслуг;	
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЕсли;	
	КонецЕсли;
	
	// 5) зависит от ХО
	СуммаКорректировки =0;
	Если БалансВедетсяПоПодразделениям И (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеПодразделениеКомпании) Тогда
		СуммаКорректировки = СуммаКорректировки+СтоимостьСтороннихУслуг;
	КонецЕсли;	
	
	Если БалансВедетсяПоПодразделениям И (ПодразделениеПодразделениеОказывающееУслугу<>ПодразделениеПодразделениеКомпании) Тогда
		СуммаКорректировки = СуммаКорректировки+СтоимостьСобственныхУслуг;
	КонецЕсли;	
	
	Если ХозОперация = Справочники.ХозОперации.ОбслуживаниеАктиваНаСтоимостьАктива Тогда
		// а) увеличение балансовой стоимости актива = сумме обслуживания = сумме документа
		БалансоваяСтоимостьАктиваПлюс = СуммаДок;
	Иначе
		// б) списываем на расходы нашего подразделения
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДиР.Подразделение = ПодразделениеКомпании;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = СтатьяРасходов;
		НаборЗаписейДиР.ВУпрВалюте             = Ложь;
		НаборЗаписейДиР.Расход                 = СуммаДок;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		
		Если СуммаКорректировки<>0 Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение = ПодразделениеКомпании;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.Доход=СуммаКорректировки;	
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;	
		
		БалансоваяСтоимостьАктиваПлюс = 0;
	КонецЕсли;
	
	РезультатЗапросаПоАктивам = ПолучитьРезультатЗапросаПоАктивам(БалансоваяСтоимостьАктиваПлюс, СуммаДок).Выгрузить();
			
	// 6) эксплуатация активов - начисляем сумму обслуживания и плюсуем балансовую стоимость если нужно
	Если РезультатЗапросаПоАктивам.Количество() = 0 Тогда
		#Если Клиент Тогда
			Сообщить("Прочий актив """ + Актив + """. Подразделение """ + ПодразделениеКомпании + """. Не обнаружен!", СтатусСообщения.Важное);
		#КонецЕсли
		Отказ = Истина;
	Иначе
		НаборЗаписейЭксплуатация = Движения.ПрочиеАктивыВЭксплуатации;
		НаборЗаписейЭксплуатация.ДокументОбъект            = ЭтотОбъект;
		НаборЗаписейЭксплуатация.ПодразделениеКомпании     = ПодразделениеКомпании;
		НаборЗаписейЭксплуатация.ТипОбслуживания           = ТипОбслуживания;
		НаборЗаписейЭксплуатация.ЭтоПервыйВвод             = Ложь;
		НаборЗаписейЭксплуатация.РезультатЗапросаПоАктивам = РезультатЗапросаПоАктивам;
		Отказ = НЕ НаборЗаписейЭксплуатация.Приход() ИЛИ Отказ;
		
		Если ХозОперация=Справочники.ХозОперации.ОбслуживаниеАктиваНаСтоимостьАктива И СуммаКорректировки <> 0 Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.Подразделение = ПодразделениеКомпании;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.Доход=СуммаКорректировки;	
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;	
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		ФлагПерерасчет = Истина;
		Записать();
		
		//движения появились после записи, нужно обновить сумму
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		Записать();
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// ДиР по оприходованным ценностям
	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
	НаборЗаписейДоходыИРасходы.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейДоходыИРасходы.Подразделение = СкладКомпании.Подразделение;
	НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = СтатьяДоходовПоОприходованнымЦенностям;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
	НаборЗаписейДоходыИРасходы.Доход = ОприходованныеЦенности.Итог("СуммаВсего");
	Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;

	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
ФлагПерерасчет = Ложь;

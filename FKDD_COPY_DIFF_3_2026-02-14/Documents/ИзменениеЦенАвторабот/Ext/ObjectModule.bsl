// Модуль документа

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ТипЦенСтарый Экспорт;         // Предыдущий тип цен
Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеАвтоработы=Новый Структура();
	ОбязательныеАвтоработы.Вставить("Авторабота",2);
	ОбязательныеАвтоработы.Вставить("Модель", 2);
	//ОбязательныеАвтоработы.Вставить("Нормочас",1);
	//ОбязательныеАвтоработы.Вставить("Цена",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",         1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",  1);
	ОбязательныеРеквизиты.Вставить("Автор",          1);
	ОбязательныеРеквизиты.Вставить("Организация",    1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("ДатаНачалаДействия",1);
	Если ХозОперация=Справочники.ХозОперации.УстановкаЦенАвтоработКонтрагента Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	Конецесли;
	ОбязательныеРеквизиты.Вставить("Автоработы",ОбязательныеАвтоработы);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации 
	// будем только в случае если все нормально с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если Результат И СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
	
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
	КонецЕсли;
	
	Возврат Результат
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("УстановкаЦенАвтоработКомпании","Изменение цен авторабот компании",Истина);
	СписокХозОпераций.Добавить("УстановкаЦенАвтоработКонтрагента","Установка цен авторабот контрагента");

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	Если Имя="ТипЦен" Тогда
		Если ТипЦен.Рассчитывается Тогда
			Сообщить("Тип цен не должен быть рассчитываемым");
			ТипЦен = ТипЦенСтарый;
			Возврат Ложь;
		КонецЕсли;
		
		ВалютаСтарая = ВалютаДокумента;
		КурсСтарый   = КурсДокумента;
		
		Если НЕ ТипЦен.ВВалютеУчета Тогда
			ВалютаДокумента = обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
			СтруктураКурса  = обКурсДляВалюты(ВалютаДокумента,Дата);
			КурсДокумента   = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		КонецЕсли;
		
		ИзмениласьВалюта = (ВалютаСтарая<>ВалютаДокумента);
		
		// Если сменился тип цены, то придется пересчитать табличную часть.
		Если ТипЦенСтарый<>ТипЦен Тогда
			
			Если ИзмениласьВалюта Тогда
				Для Каждого ТекСтрока Из Автоработы Цикл
					
					Если ЗначениеЗаполнено(ТекСтрока.Нормочас) Тогда
						Продолжить;
					КонецЕсли;
					
					Если ТекСтрока.Цена<>0 Тогда
						ТекСтрока.Цена = ОбПересчет(ТекСтрока.Цена, ВалютаСтарая, КурсСтарый, ВалютаДокумента, КурсДокумента);
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
			// Запоминаем новый тип цен.
			ТипЦенСтарый = ТипЦен;
		КонецЕсли;
		
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
				ЭтаФорма.Обновить();
			КонецЕсли; 
		#КонецЕсли
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента))Тогда
			Для Каждого ТекСтрока Из Автоработы Цикл
				
				Если ЗначениеЗаполнено(ТекСтрока.Нормочас) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ТекСтрока.Цена<>0 Тогда
					ТекСтрока.Цена = ОбПересчет(ТекСтрока.Цена, ТекущаяВалютаДокумента, ТекущийКурсДокумента, ВалютаДокумента, КурсДокумента);
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
	ИначеЕсли Имя="ВидРемонта" Тогда
		//Если НЕ обЗначениеНеЗаполнено(ВидРемонта) Тогда
		//	Если ВидРемонта.ТипРемонта=Перечисления.ТипыРемонта.Бесплатный Тогда
		//		#Если Клиент Тогда
		//		Сообщить("Тип бесплатного ремонта не может быть установлена цена на автоработы.",СтатусСообщения.Внимание);
		//		#КонецЕсли
		//		ВидРемонта=Справочники.ВидыРемонта.ПустаяСсылка();
		//	КонецЕсли;
		//КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Цех" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Автоработы.Нормочас" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Нормочас) Тогда
			ТекСтрока.Цена=0;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Автоработы.Цена" Тогда	
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Цена) Тогда
			ТекСтрока.Нормочас=Справочники.Нормочасы.ПустаяСсылка();
		КонецЕсли;
		Возврат Истина;
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ИзменениеЦенАвторабот"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИзменениеЦенАвторабот(ТабДокумент) Экспорт
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет("ИзменениеЦенАвторабот");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект, Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация);
	ОбластьМакета.Параметры.ПредставлениеТипаЦен	 = спПолучитьНаименование(ТипЦен);
	ОбластьМакета.Параметры.ПредставлениеКонтрагента = спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.ПредставлениеДоговора	 = спПолучитьНаименование(ДоговорВзаиморасчетов);
	ОбластьМакета.Параметры.ПредставлениеЦеха		 = спПолучитьНаименование(Цех);
	ОбластьМакета.Параметры.ПредставлениеВидаРемонта = спПолучитьНаименование(ВидРемонта);

	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	// Определим, что показывать в колонке кода - код или артикул
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	СтруктураИтоговПоСтранице = Новый Структура("Пустой",0);
	
	Запрос = Новый Запрос("ВЫБРАТЬ * ПОМЕСТИТЬ Автоработы ИЗ &ТЧАвтоработы КАК ТЧАвтоработы");
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТЧАвтоработы", Автоработы);
	Запрос.Выполнить();
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Автоработы.Авторабота,
	|	Автоработы.Модель,
	|	Автоработы.Нормочас,
	|	Автоработы.Цена,
	|	ЦеныАвтоработСрезПоследних.Нормочас КАК СтарыйНормочас,
	|	ВЫБОР КОГДА ЕСТЬNULL(ЦеныАвтоработСрезПоследних.Цена, 0) = 0 ТОГДА 0 
	|		  КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) = 0 ТОГДА КурсыВалютСрезПоследних.Курс/&КурсДокумента*ЦеныАвтоработСрезПоследних.Цена
	|	      ИНАЧЕ (КурсыВалютСрезПоследних.Курс/КурсыВалютСрезПоследних.Кратность/&КурсДокумента)*ЦеныАвтоработСрезПоследних.Цена КОНЕЦ КАК СтараяЦена
	|ИЗ
	|	Автоработы КАК Автоработы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныАвторабот.СрезПоследних(&Момент
	|				,
	|					Регистратор <> &ВыбРегистратор
	|					И ВидРемонта = &ВыбВидРемонта
	|					И Цех = &ВыбЦех
	|					И ДоговорВзаиморасчетов = &ВыбДоговорВзаиморасчетов
	|					"+?(обЗначениеНеЗаполнено(Контрагент),"", "И ДоговорВзаиморасчетов.Владелец = &ВыбКонтрагент")+"
	|					И (Авторабота, Модель) В (ВЫБРАТЬ Авторабота, Модель ИЗ Автоработы КАК Автоработы)
	|					) КАК ЦеныАвтоработСрезПоследних
	|		ПО Автоработы.Авторабота = ЦеныАвтоработСрезПоследних.Авторабота
	|			И Автоработы.Модель = ЦеныАвтоработСрезПоследних.Модель
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Момент, ) КАК КурсыВалютСрезПоследних
	|		ПО ЦеныАвтоработСрезПоследних.Валюта = КурсыВалютСрезПоследних.Валюта";
	
	Запрос.УстановитьПараметр("ВыбРегистратор", Ссылка);
	Запрос.УстановитьПараметр("ВыбВидРемонта", ВидРемонта);
	Запрос.УстановитьПараметр("ВыбДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВыбКонтрагент", Контрагент);
	Запрос.УстановитьПараметр("ВыбЦех", Цех);
	
	Момент=?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(ДатаНачалаДействия)), Новый Граница(Новый МоментВремени(ДатаНачалаДействия, Ссылка),ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Момент", Момент);
	Запрос.УстановитьПараметр("КурсДокумента", ?(КурсДокумента=0,1,КурсДокумента));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	//перебор строк
	НомерСтроки=1;
	КоличествоСтрок = Выборка.Количество();
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	Пока Выборка.Следующий() Цикл
		ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.АвтоработаНаименование = спПолучитьНаименование(Выборка.Авторабота);
		ОбластьМакета.Параметры.МодельНаименование     = спПолучитьНаименование(Выборка.Модель);
		ОбластьМакета.Параметры.Код                    = дкПолучитьЗначениеКолонкиКода(Выборка.Авторабота, ИмяКолонкиКода, ЭтотОбъект);
		ОбластьМакета.Параметры.СтараяЦена             = Формат(Выборка.СтараяЦена, ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Цена                   = Формат(Выборка.Цена, ФорматВыводаСуммы);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
						НомерСтраницы,, ЭтотОбъект, ?(НомерСтроки=КоличествоСтрок,мсвДопОбластиПодвала, Неопределено));
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;                                                             
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		НомерСтроки = НомерСтроки+1;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	//Итоги
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+КоличествоСтрок;
	
	// Выводим представления и расшифровки подписей
	ПредседательКомиссии = дкОтветственноеЛицо(ЭтотОбъект, "ПредседательКомиссии");
	ПредседательКомиссии.ПредседательКомиссииПредставление = ?(обЗначениеНеЗаполнено(ПредседательКомиссии.ПредседательКомиссии),"","/ "+ ПредседательКомиссии.ПредседательКомиссииПредставление);
	ОбластьПодвал.Параметры.Заполнить(ПредседательКомиссии);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ГлавныйБухгалтер.ГлавныйБухгалтерПредставление = ?(обЗначениеНеЗаполнено(ГлавныйБухгалтер.ГлавныйБухгалтер),"","/ "+ ГлавныйБухгалтер.ГлавныйБухгалтерПредставление);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьИзменениеЦенАвторабот()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если Основание=Неопределено Тогда
		ТипЦен = обПраво("ОсновнойТипЦенРабот",Права,,ЭтотОбъект);
	КонецЕсли;

	ДатаНачалаДействия=Дата;
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;

	ДатаНачалаДействия=ТекущаяДата();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ХозОперация<>Справочники.ХозОперации.УстановкаЦенАвтоработКонтрагента Тогда
		Контрагент            = Справочники.Контрагенты.ПустаяСсылка();
		ДоговорВзаиморасчетов =Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	КонецЕсли;
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// устанавливаем цены
	НаборЗаписейЦеныАвторабот = Движения.ЦеныАвторабот;
	НаборЗаписейЦеныАвторабот.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЦеныАвторабот.ДоговорВзаиморасчетов = ?(ХозОперация=Справочники.ХозОперации.УстановкаЦенАвтоработКонтрагента,ДоговорВзаиморасчетов,Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка());
	НаборЗаписейЦеныАвторабот.ТипЦен = ТипЦен;
	НаборЗаписейЦеныАвторабот.Цех = Цех;
	НаборЗаписейЦеныАвторабот.ВидРемонта = ВидРемонта;
	НаборЗаписейЦеныАвторабот.ДатаНачалаДействия = ДатаНачалаДействия;
	Отказ=НЕ НаборЗаписейЦеныАвторабот.УстановитьЦеныАвторабот() ИЛИ Отказ;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
ТипЦенСтарый = Ссылка.ТипЦен;
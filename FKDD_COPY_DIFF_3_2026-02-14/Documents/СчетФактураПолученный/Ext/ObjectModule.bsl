// Модуль документа СчетФактураПолученный

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД",2);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("Грузоотправитель",1);
	ОбязательныеРеквизиты.Вставить("ВхДокНомер",1);
	ОбязательныеРеквизиты.Вставить("ВхДокДата",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("СчетФактураПолученный","Счет-фактура полученный",Истина);
	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="" Тогда
		//Для приватной обработки изменения реквизитов
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
			//Если имеем дело с автомобилем - обработаем локально
			//Установим количество
			ТекСтрока.Количество=1;
			ТекСтрока.ЕдиницаИзмерения=Справочники.КлассификаторЕдиницИзмерения.шт;
			ТекСтрока.Коэффициент=1;
			// Заполним ставку НДС
			Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
				Если Контрагент.ОсвобожденОтНДС Тогда
					ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
				Иначе
					ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				КонецЕсли; 
			КонецЕсли; 
			ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации",ТекСтрока.Номенклатура,ТекСтрока.Номенклатура.ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			//Расчет сумм по строке
			Возврат ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			Если ТекСтрока.Номенклатура=Неопределено Тогда
				ТекСтрока.Номенклатура=Справочники.Номенклатура.ПустаяСсылка();
			КонецЕсли; 
			//В противном случае стандартная обработка
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
			//Если имеем дело с автомобилем. Количество всегда = 1
			ТекСтрока.Количество=1;
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
			ТекСтрока.ЕдиницаИзмерения=Справочники.КлассификаторЕдиницИзмерения.шт;
			ТекСтрока.Коэффициент=1;
			Возврат Истина;
		Иначе
			//В противном случае стандартная обработка
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="Товары.Коэффициент" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
			//Если имеем дело с автомобилем. Коэффициент всегда = 1
			ТекСтрока.Коэффициент=1;
		КонецЕсли;
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

Процедура ЗаполнитьНомера(Основание) Экспорт
	Если Основание = Неопределено ИЛИ НЕ ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	ЭтоИсправление = (Основание.ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияИсправлениеВПервичныхДокументах) ИЛИ (Основание.ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейИсправлениеВПервичныхДокументах);
	Корректировка     = Неопределено;
	ОригиналОснование = Неопределено;
	ПервоеИсправление = Неопределено;
	ИсправлениеОснования = Неопределено;
	//Получим документ оригинал и корректировку
	Попытка
		ДокДокументОснование = Основание.ДокументОснование;
	Исключение
		ДокДокументОснование = Неопределено;
	КонецПопытки;
	КорректировкаПоСогласованию = Ложь;
	НайденаКорректировка = Ложь;
	
	Пока Истина Цикл
		Если ДокДокументОснование = Неопределено Тогда
			Прервать;
		КонецЕсли;
		КорректировкаПоСогласованию = (ДокДокументОснование.ХозОперация =
				Справочники.ХозОперации.КорректировкаПоступленияКорректировкаПоСогласованиюСторон
			ИЛИ ДокДокументОснование.ХозОперация =
				Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейКорректировкаПоСогласованиюСторон);
		КорректировкаИсправления = (ДокДокументОснование.ХозОперация =
				Справочники.ХозОперации.КорректировкаПоступленияИсправлениеВПервичныхДокументах
			ИЛИ ДокДокументОснование.ХозОперация =
				Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейИсправлениеВПервичныхДокументах);
		НайденаКорректировка = НайденаКорректировка ИЛИ КорректировкаПоСогласованию;
		
		Если КорректировкаИсправления И ИсправлениеОснования = Неопределено Тогда
			ИсправлениеОснования = ДокДокументОснование;
		КонецЕсли;
		
		Если КорректировкаПоСогласованию И ЭтоИсправление И Корректировка = Неопределено Тогда
			ЕстьКорректировка = Истина;
			Корректировка = ДокДокументОснование;
		КонецЕсли;
		Если КорректировкаИсправления И ПервоеИсправление = Неопределено И НЕ НайденаКорректировка Тогда
			ПервоеИсправление = ДокДокументОснование;
			ЕстьИсправление = Истина;
		КонецЕсли;
		Если ОригиналОснование = Неопределено
			И Корректировка <> ДокДокументОснование
			И КорректировкаПоСогласованию
		Тогда
			ОригиналОснование = ДокДокументОснование;
		КонецЕсли;
		Если ТипЗнч(ДокДокументОснование) <> Тип("ДокументСсылка.КорректировкаПоступления") И ТипЗнч(ДокДокументОснование) <> Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
			Если ОригиналОснование = Неопределено Тогда
				ОригиналОснование = ДокДокументОснование;
			КонецЕсли;
			Прервать;
		КонецЕсли;
		
		Если ОригиналОснование = Неопределено И КорректировкаПоСогласованию Тогда
			ИсправлениеОснования = Неопределено;
		КонецЕсли;
		
		ДокДокументОснование = ДокДокументОснование.ДокументОснование;
	КонецЦикла;
	
	ЕстьОснование = (ОригиналОснование <> Неопределено);
	// получим счет фактуру для документов
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетФактураПолученный.Ссылка,
	|	СчетФактураПолученный.ДокументОснование
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ДокументОснование = &Док1
	|	ИЛИ СчетФактураПолученный.ДокументОснование = &Док2
	|	ИЛИ СчетФактураПолученный.ДокументОснование = &Док3
	|	ИЛИ СчетФактураПолученный.ДокументОснование = &Док4";
	Запрос.УстановитьПараметр("Док1",Корректировка);
	Запрос.УстановитьПараметр("Док2",ОригиналОснование);
	Запрос.УстановитьПараметр("Док3",ПервоеИсправление);
	Запрос.УстановитьПараметр("Док4",ИсправлениеОснования);
	ТаблСФ = Запрос.Выполнить().Выгрузить();
	
	Исправление = ЭтоИсправление;
	Если ЭтоИсправление Тогда
		Если ПервоеИсправление = Неопределено Тогда
			НомерИсправления = 1;
			УчитыватьИсправлениеИсходногоДокумента = Ложь;
		Иначе
			МассивСФ = ТаблСФ.НайтиСтроки(Новый Структура("ДокументОснование",ПервоеИсправление));
			Если МассивСФ.Количество() = 0 Тогда
				Если ЭтоИсправление Тогда
					НомерИсправления = 1;
				Иначе
					НомерИсправления = 0;
				КонецЕсли;
				УчитыватьИсправлениеИсходногоДокумента = Истина;
			Иначе
				УчитыватьИсправлениеИсходногоДокумента = Истина;
				НомерИсправления = МассивСФ[0].Ссылка.НомерИсправления + 1;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если ЕстьКорректировка Тогда
		МассивСФ = ТаблСФ.НайтиСтроки(Новый Структура("ДокументОснование",Корректировка));
		Если МассивСФ.Количество() > 0 Тогда
			НомерИсправляемогоКорректировочногоДокумента = ?(обЗначениеНеЗаполнено(МассивСФ[0].Ссылка.ВхДокНомер),МассивСФ[0].Ссылка.Номер,МассивСФ[0].Ссылка.ВхДокНомер);
			ДатаИсправляемогоКорректировочногоДокумента  = ?(обЗначениеНеЗаполнено(МассивСФ[0].Ссылка.ВхДокДата),МассивСФ[0].Ссылка.Дата,МассивСФ[0].Ссылка.ВхДокДата);
		КонецЕсли;
	КонецЕсли;
	
	Если ОригиналОснование <> Неопределено Тогда
		МассивСФ = ТаблСФ.НайтиСтроки(Новый Структура("ДокументОснование",ОригиналОснование));
		Если МассивСФ.Количество() > 0 Тогда
			НомерИсходногоДокумента = ?(обЗначениеНеЗаполнено(МассивСФ[0].Ссылка.ВхДокНомер),МассивСФ[0].Ссылка.Номер,МассивСФ[0].Ссылка.ВхДокНомер);
			ДатаИсходногоДокумента  = ?(обЗначениеНеЗаполнено(МассивСФ[0].Ссылка.ВхДокДата),МассивСФ[0].Ссылка.Дата,МассивСФ[0].Ссылка.ВхДокДата);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсправлениеОснования) Тогда
			МассивСФ = ТаблСФ.НайтиСтроки(Новый Структура("ДокументОснование",ИсправлениеОснования));
			Если МассивСФ.Количество() > 0 Тогда
				НомерИсправленияИсходногоДокумента = ?(обЗначениеНеЗаполнено(МассивСФ[0].Ссылка.ВхДокНомер),МассивСФ[0].Ссылка.НомерИсправления,МассивСФ[0].Ссылка.ВхДокНомер);
				ДатаИсправленияИсходногоДокумента = ?(обЗначениеНеЗаполнено(МассивСФ[0].Ссылка.ВхДокДата),МассивСФ[0].Ссылка.Дата,МассивСФ[0].Ссылка.ВхДокДата);
				УчитыватьИсправлениеИсходногоДокумента = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ТаблицаДвиженийПоГТД(Основание) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ГТДПартийТоваровКомпании.ГТД КАК ГТД,
		|	ГТДПартийТоваровКомпании.Партия КАК Партия,
		|	СУММА(ГТДПартийТоваровКомпании.Количество) КАК Количество
		|ИЗ
		|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
		|ГДЕ
		|	ГТДПартийТоваровКомпании.Регистратор = &Основание
		|
		|СГРУППИРОВАТЬ ПО
		|	ГТДПартийТоваровКомпании.Номенклатура,
		|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры,
		|	ГТДПартийТоваровКомпании.ГТД,
		|	ГТДПартийТоваровКомпании.Партия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПартииТоваровОтданные.Номенклатура,
		|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
		|	ПартииТоваровОтданные.ГТД,
		|	ПартииТоваровОтданные.Партия,
		|	СУММА(ПартииТоваровОтданные.Количество)
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
		|ГДЕ
		|	ПартииТоваровОтданные.Регистратор = &Основание
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваровОтданные.Номенклатура,
		|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
		|	ПартииТоваровОтданные.ГТД,
		|	ПартииТоваровОтданные.Партия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализованныеТовары.Номенклатура,
		|	РеализованныеТовары.ХарактеристикаНоменклатуры,
		|	РеализованныеТовары.ГТД,
		|	РеализованныеТовары.ДокументПередачи,
		|	СУММА(РеализованныеТовары.Количество)
		|ИЗ
		|	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
		|ГДЕ
		|	РеализованныеТовары.Регистратор = &Основание
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализованныеТовары.Номенклатура,
		|	РеализованныеТовары.ХарактеристикаНоменклатуры,
		|	РеализованныеТовары.ГТД,
		|	РеализованныеТовары.ДокументПередачи";
	
	Запрос.УстановитьПараметр("Основание", Основание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Процедура ЗаполнитьНомерИсходнойСтроки() Экспорт
	
	Если ТипЗнч(ЭтотОбъект.ДокументОснование)=Тип("ДокументСсылка.КорректировкаПоступления") ИЛИ 
		ТипЗнч(ЭтотОбъект.ДокументОснование)=Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей")Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураПолученныйТовары.Номенклатура КАК Номенклатура,
		|	СчетФактураПолученныйТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СчетФактураПолученныйТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СчетФактураПолученныйТовары.ГТД КАК ГТД,
		|	СчетФактураПолученныйТовары.НомерИсходнойСтроки КАК НомерИсходнойСтроки
		|ИЗ
		|	Документ.СчетФактураПолученный.Товары КАК СчетФактураПолученныйТовары
		|ГДЕ
		|	СчетФактураПолученныйТовары.Ссылка.ДокументОснование = &ДокументОснование";
		
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование.ДокументОснование);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ДанныеИсходнойСФ = РезультатЗапроса.Выгрузить();
			
			СтруктураПоиска = Новый Структура("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры,ГТД");
			
			Для каждого ТекущаяСтрока Из ЭтотОбъект.Товары Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
				
				НайденныеСтроки = ДанныеИсходнойСФ.НайтиСтроки(СтруктураПоиска);
				Если НайденныеСтроки.Количество() = 0 Тогда
					ТекущаяСтрока.НомерИсходнойСтроки = 0; 
					Продолжить;
				КонецЕсли;
				ТекущаяСтрока.НомерИсходнойСтроки = НайденныеСтроки[0].НомерИСходнойСтроки; 
				
			КонецЦикла;
			
			
		Иначе
			Для Каждого ТекущаяСтрока Из ЭтотОбъект.Товары Цикл
			
			ТекущаяСтрока.НомерИсходнойСтроки = 0;
			
		КонецЦикла;
		КонецЕсли;
	Иначе
		Для Каждого ТекущаяСтрока Из ЭтотОбъект.Товары Цикл
			
			ТекущаяСтрока.НомерИсходнойСтроки = ТекущаяСтрока.НомерСтроки;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоАвансовыйСчетФактура()
	
	СписокОснований = Новый Массив();
	СписокОснований.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	СписокОснований.Добавить(Тип("ДокументСсылка.Выписка"));
	СписокОснований.Добавить(Тип("ДокументСсылка.ЧекНаОплату"));
	
	Возврат СписокОснований.Найти(ТипЗнч(ДокументОснование)) <> Неопределено;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "СчетФактура"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьСчетФактура(ТабДокумент) Экспорт
	
	Если Дата >= Дата("20210701000000") Тогда
		Возврат ПечатьСчетФактуры2021(ТабДокумент);
	КонецЕсли;
	
	ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаТабличнойЧасти=ЭтотОбъект.Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ВыборкаТабличнойЧасти,ЭтотОбъект,ВалютаПечатногоДокумента);
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	Если ПустаяСтрока(ФорматВыводаСуммы) Тогда
		ФорматВыводаСуммы = ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00";
	КонецЕсли;
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	СтарыйМакет = Ложь;
	НовыйМакет = Ложь;
	ВыводитьКодВидаТовара = Ложь;
	Если Дата<Дата("20060601000000") Тогда
		Макет = ПолучитьОбщийМакет("СчетФактура_2006_06_01");
		СтарыйМакет = Истина;
	ИначеЕсли Дата>Дата("20120124000000") Тогда
		Макет = ПолучитьОбщийМакет("СчетФактура_24_01_2012");
		НовыйМакет = Истина;
		ОбластьКодВидаТовара = Макет.Область("КодВидаТовара");
		ВыводитьКодВидаТовара = (ЭтотОбъект.Дата >= Дата('20171001'));
	Иначе
		Макет = ПолучитьОбщийМакет("СчетФактура");
	КонецЕсли;
	ВыводитьКодТНВЭД = обПраво("ВыводитьКодТНВЭД");
	
	Если НЕ СтарыйМакет И НЕ НовыйМакет Тогда
		ОбластьНаимменованиеВалюты = Макет.Область("НаименованиеВалюты");
	КонецЕсли;
	ВставлятьНаменованиеВалюты = Ложь;
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КоличествоСтрок = ВыборкаТабличнойЧасти.Количество();
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	Если СокрЛП(ВхДокНомер)="" Тогда
		НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	Иначе
		НомерДляПечати=СокрЛП(ВхДокНомер);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ВхДокДата) Тогда
		ДатаДляПечати=Дата;
    Иначе
		ДатаДляПечати=ВхДокДата;
	КонецЕсли; 
	Если ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
		ТекстЗаголовкаОтчета    = "Счет-фактура № " + ?(ПустаяСтрока(ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект),СокрЛП(ВхДокНомер)) +" от " + Формат(?(обЗначениеНеЗаполнено(ВхДокДата),ЭтотОбъект.Дата,ВхДокДата), "ДФ=""дд ММММ гггг""");
		ТекстЗаголовкаИзменения = "Исправление  № " + "----" +" от " + "----";
	Иначе
		ТекстЗаголовкаИзменения = "Исправление  № " + ?(ЗначениеЗаполнено(ЭтотОбъект.НомерИсправления), ЭтотОбъект.НомерИсправления, "----") +" от " + Формат(ЭтотОбъект.Дата, "ДФ=""дд ММММ гггг""");
		// найдем изначальную счет фактуру
		ДокументОсн = ДокументОснование;
		Пока ТипЗнч(ДокументОсн) = Тип("ДокументСсылка.КорректировкаПоступления") Цикл
			ДокументОсн = ДокументОсн.ДокументОснование;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СчетФактураПолученный.Ссылка
		|ИЗ
		|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|ГДЕ
		|	СчетФактураПолученный.ДокументОснование = &ДокументОсн";
		Запрос.УстановитьПараметр("ДокументОсн",ДокументОсн);
		ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
		
		Если ВыборкаЗапроса.Следующий() Тогда 
			ТекстЗаголовкаОтчета    = "Счет-фактура № " + ?(ПустаяСтрока(ВыборкаЗапроса.Ссылка.ВхДокНомер),дкПолучитьНомерДляПечати(ВыборкаЗапроса.Ссылка.ПолучитьОбъект()),СокрЛП(ВыборкаЗапроса.Ссылка.ВхДокНомер)) +" от " + Формат(?(обЗначениеНеЗаполнено(ВыборкаЗапроса.Ссылка.ВхДокДата),ВыборкаЗапроса.Ссылка.Дата,ВыборкаЗапроса.Ссылка.ВхДокДата), "ДФ=""дд ММММ гггг""");
		Иначе
			ТекстЗаголовкаОтчета = "Счет-фактура  № " + "----" +" от " + "----";
		КонецЕсли;
	КонецЕсли;
	ОбластьМакета.Параметры.ТекстЗаголовка			  = ТекстЗаголовкаОтчета;
	Если НовыйМакет Тогда
		ОбластьМакета.Параметры.ТекстЗаголовкаИсправление = ТекстЗаголовкаИзменения;
	КонецЕсли;
	
	ОбластьМакета.Параметры.Поставщик				= ЭтотОбъект.Контрагент;
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,Новый Структура("Наименование","Продавец: "));
	
	ОбластьМакета.Параметры.АдресПоставщика	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,Новый Структура("АдресЮридический","Адрес: "));
	ОбластьМакета.Параметры.ИННпоставщика	= "ИНН/КПП продавца: " + ?(ОбластьМакета.Параметры.Поставщик.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение, ОбластьМакета.Параметры.Поставщик.ГоловнойКонтрагент.ИНН, ОбластьМакета.Параметры.Поставщик.ИНН) + "/" + ОбластьМакета.Параметры.Поставщик.КПП;
	
	ОбластьМакета.Параметры.Грузоотправитель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Грузоотправитель);
	Если ЭтотОбъект.Контрагент=ОбластьМакета.Параметры.Грузоотправитель Тогда
		ОбластьМакета.Параметры.ГрузоотправительПредставление	= "Грузоотправитель и его адрес: он же";
	Иначе
		ОбластьМакета.Параметры.ГрузоотправительПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,Новый Структура("Наименование,АдресФактический","Грузоотправитель и его адрес: ",""));
	КонецЕсли;
	Если НовыйМакет Тогда
		ОбластьМакета.Параметры.ВалютаШапка = "Валюта: наименование, код "+ ?(НЕ обЗначениеНеЗаполнено(ВалютаПечатногоДокумента.НаименованиеПолное), ВалютаПечатногоДокумента.НаименованиеПолное, ВалютаПечатногоДокумента.Наименование) + ", " + ВалютаПечатногоДокумента.Код;
	КонецЕсли;
	ОбластьМакета.Параметры.Грузополучатель					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.ПодразделениеКомпании);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ОбластьМакета.Параметры.ГрузополучательПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,Новый Структура("Наименование,АдресФактический","Грузополучатель и его адрес: ",""), ДополнительныеПараметры);
	
	Если КоличествоСтрок>0 Тогда
		ЕстьТовары=Ложь;
		Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
			Если ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
				Если СтрокаТоваров.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
					ЕстьТовары=Истина; Прервать;
				КонецЕсли; 
			ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
				ЕстьТовары=Истина; Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ЕстьТовары Тогда
			ОбластьМакета.Параметры.ГрузоотправительПредставление="Грузоотправитель и его адрес: ----";
			ОбластьМакета.Параметры.ГрузополучательПредставление="Грузополучатель и его адрес: ----";
		КонецЕсли; 
	КонецЕсли; 
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.Выписка") Тогда
		ОбластьМакета.Параметры.ПредставлениеПоДокументу	= "К платежно-расчетному документу № " + ДокументОснование.Номер + " от: " + Формат(ДокументОснование.Дата,"ДФ=dd.MM.yyyy");
		ОбластьМакета.Параметры.ПоДокументу					= ДокументОснование;
	Иначе
		ОбластьМакета.Параметры.ПредставлениеПоДокументу	= "К платежно-расчетному документу №          от:";
	КонецЕсли;
	
	ОбластьМакета.Параметры.Покупатель				= Организация;
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(ОбластьМакета.Параметры.Покупатель,Новый Структура("Наименование","Покупатель: "));
	
	ОбластьМакета.Параметры.АдресПокупателя   = спПолучитьПредставление(ПодразделениеКомпании,Новый Структура("АдресЮридический","Адрес: "));
		ОбластьМакета.Параметры.ИННПокупателя = "ИНН/КПП покупателя: " + ОбластьМакета.Параметры.Покупатель.ИНН+"/"+спПолучитьКППДляПечати(Организация, ПодразделениеКомпании);
	
	// если дата документа позднее 01.07.2017
	Если ВыводитьКодВидаТовара И НовыйМакет Тогда
		ОбластьМакета.Параметры.Редакция = НСтр("ru = '(в редакции постановления Правительства Российской Федерации от 19 августа 2017 № 981)'");
	ИначеЕсли ЭтотОбъект.Дата >= Дата('20170701') Тогда
		ОбластьМакета.Параметры.Редакция = НСтр("ru = '(в редакции постановления Правительства Российской Федерации от 25 мая 2017 г. № 625)'");
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// если дата документа позднее 01.07.2017
	Если ЭтотОбъект.Дата >= Дата('20170701') Тогда
		ОбластьШапкаИдГосКонтракта = Макет.ПолучитьОбласть("ШапкаИдГосКонтракта");
		ЗаголовокИдентификатораГосударственногоКонтракта = "Идентификатор государственного контракта, договора (соглашения)" + ?(ВыводитьКодВидаТовара, " (при наличии)", "")+": ";
		ОбластьШапкаИдГосКонтракта.Параметры.ИдентификаторГосударственногоКонтракта = ЗаголовокИдентификатораГосударственногоКонтракта + ЭтотОбъект.ИдентификаторГосударственногоКонтракта;
		ТабДокумент.Вывести(ОбластьШапкаИдГосКонтракта);
	КонецЕсли;
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	
	// вставить выкусывание области наименование номенклатуры
	Если НЕ ВставлятьНаменованиеВалюты И НЕ СтарыйМакет И НЕ НовыйМакет Тогда
		НовыйТекст = ОбластьШапкаТаблицы.Область(3,ОбластьНаимменованиеВалюты.Право+1,3,ОбластьНаимменованиеВалюты.Право+5).Текст;
		ОбластьВыреза = ОбластьШапкаТаблицы.Область(3,ОбластьНаимменованиеВалюты.Лево,3,ОбластьНаимменованиеВалюты.Право+5);
		ОбластьВыреза.Объединить();
		ОбластьВыреза.Текст = НовыйТекст;
		
		ОбластьВыреза = ОбластьШапкаТаблицы.Область(4,ОбластьНаимменованиеВалюты.Лево,4,ОбластьНаимменованиеВалюты.Право+5);
		ОбластьВыреза.Объединить();
		
		//перенумеруем колонки
		ОбластьШапкаТаблицы.Область(4,35,4,42).Текст = "5";
		ОбластьШапкаТаблицы.Область(4,43,4,46).Текст = "6";
		ОбластьШапкаТаблицы.Область(4,47,4,51).Текст = "7";
		ОбластьШапкаТаблицы.Область(4,52,4,56).Текст = "8";
		ОбластьШапкаТаблицы.Область(4,57,4,65).Текст = "9";
		ОбластьШапкаТаблицы.Область(4,66,4,71).Текст = "10";
		ОбластьШапкаТаблицы.Область(4,72,4,81).Текст = "11";
	КонецЕсли;
	
	// Уберем колонку, вывод которой утвержден с 1 октября 2017 г.
	Если НЕ ВыводитьКодВидаТовара И НовыйМакет Тогда
		
		НовыйТекст = ОбластьШапкаТаблицы.Область(2,ОбластьКодВидаТовара.Лево-11,3,ОбластьКодВидаТовара.Лево-1).Текст;
		
		ОбластьВыреза = ОбластьШапкаТаблицы.Область(2,ОбластьКодВидаТовара.Лево-11,3,ОбластьКодВидаТовара.Право);
		ОбластьВыреза.Объединить();
		ОбластьВыреза.Текст = НовыйТекст;
		
		ОбластьВыреза = ОбластьШапкаТаблицы.Область(4,ОбластьКодВидаТовара.Лево-11,4,ОбластьКодВидаТовара.Право);
		ОбластьВыреза.Объединить();
		
		ОбластьШапкаТаблицы.Параметры.ТекстНомерТаможеннойДекларации = "Номер таможенной декларации";
		
	ИначеЕсли НовыйМакет Тогда
		
		ОбластьШапкаТаблицы.Параметры.ТекстНомерТаможеннойДекларации = "Регистрационный номер таможенной декларации";
		
	КонецЕсли;
	
	Если НЕ НовыйМакет Тогда
		ОбластьШапкаТаблицы.Параметры.Валюта = ВалютаПечатногоДокумента;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("Сумма, СуммаНДС, СуммаВсего", 0, 0, 0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовкаОтчета;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы	 = "Страница: " + НомерСтраницы;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	// вставить выкусывание области наименование валюты
	Если НЕ ВставлятьНаменованиеВалюты И НЕ СтарыйМакет И НЕ НовыйМакет Тогда
		НовыйПараметр = ОбластьМакета.Область(1,ОбластьНаимменованиеВалюты.Право+1,1,ОбластьНаимменованиеВалюты.Право+5).Параметр;
		ОбластьВыреза = ОбластьМакета.Область(1,ОбластьНаимменованиеВалюты.Лево,1,ОбластьНаимменованиеВалюты.Право+5);
		ОбластьВыреза.Объединить();
		ОбластьВыреза.Параметр = НовыйПараметр;
		//ОбластьМакета.УдалитьОбласть(ОбластьВыреза, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	
	// Уберем колонку, вывод которой утвержден с 1 октября 2017 г.
	Если НЕ ВыводитьКодВидаТовара И НовыйМакет Тогда
		НовыйПараметр = ОбластьМакета.Область(1,ОбластьКодВидаТовара.Лево-11,1,ОбластьКодВидаТовара.Лево-1).Параметр;
		ОбластьВыреза = ОбластьМакета.Область(1,ОбластьКодВидаТовара.Лево-11,1,ОбластьКодВидаТовара.Право);
		ОбластьВыреза.Объединить();
		ОбластьВыреза.Параметр = НовыйПараметр;
	КонецЕсли;
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьИтого  = Макет.ПолучитьОбласть("Итого");
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	мсвДопОбластиПодвала.Добавить(ОбластьИтого);
	
	Ном=1;
	СуммаВсегоБезНДС = 0;
	Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТоваров);
		Если СтрокаТоваров.Номенклатура=Справочники.Номенклатура.Предоплата Тогда
			ОбластьМакета.Параметры.Количество = "----";	
		Иначе
			ОбластьМакета.Параметры.Количество = Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		КонецЕсли;
		Если ВставлятьНаменованиеВалюты Тогда
			ОбластьМакета.Параметры.НаименованиеВалюты = ?(НЕ обЗначениеНеЗаполнено(ВалютаПечатногоДокумента.НаименованиеПолное), ВалютаПечатногоДокумента.НаименованиеПолное, ВалютаПечатногоДокумента.Наименование);
		КонецЕсли;
		КоличествоТовара = СтрокаТоваров.Количество;
		ОбластьМакета.Параметры.ТоварНаименование	= спПолучитьНаименование(СтрокаТоваров.Номенклатура);
		Если СтрокаТоваров.Номенклатура=Справочники.Номенклатура.Предоплата Тогда 
			ОбластьМакета.Параметры.ЕдиницаИзмерения = "----";
			Если НовыйМакет Тогда
				ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "----";
			КонецЕсли;
			Если ВыводитьКодВидаТовара Тогда
				КодТНВЭД = СтрокаТоваров.Номенклатура.КодТНВЭД;
				Если ЗначениеЗаполнено(КодТНВЭД) И ВыводитьКодТНВЭД Тогда
					ОбластьМакета.Параметры.КодВидаТовара = СокрЛП(КодТНВЭД);
				Иначе
					ОбластьМакета.Параметры.КодВидаТовара = "----";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
			Если НовыйМакет Тогда
				ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = СтрокаТоваров.ЕдиницаИзмерения.Код;
				Если ВыводитьКодВидаТовара Тогда
					ОбластьМакета.Параметры.КодВидаТовара = "----";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура)<>Тип("СправочникСсылка.Номенклатура") Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмерения = "--";
			Если НовыйМакет Тогда
				ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
				Если ВыводитьКодВидаТовара Тогда
					ОбластьМакета.Параметры.КодВидаТовара = "--";
				КонецЕсли;
			КонецЕсли;
			Если ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
				КоличествоТовара = СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент;
				ОбластьМакета.Параметры.Количество = КоличествоТовара;
				ОбластьМакета.Параметры.ЕдиницаИзмерения = "ч";
				Если НовыйМакет Тогда
					ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "356";
				КонецЕсли;	
			КонецЕсли;
		ИначеЕсли НовыйМакет Тогда
			Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.ЕдиницаИзмерения) Тогда
				Если СтрокаТоваров.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
					ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
				Иначе
					ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
				КонецЕсли;
			КонецЕсли;
			Если ВыводитьКодВидаТовара Тогда
				КодТНВЭД = СтрокаТоваров.Номенклатура.КодТНВЭД;
				Если ЗначениеЗаполнено(КодТНВЭД) И ВыводитьКодТНВЭД Тогда
					ОбластьМакета.Параметры.КодВидаТовара = СокрЛП(КодТНВЭД);
				Иначе
					ОбластьМакета.Параметры.КодВидаТовара = "--";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ОбластьМакета.Параметры.СтавкаНДС			= дкПолучитьПредставлениеСтавкиНДС(СтрокаТоваров.СтавкаНДС,"%");
		
		Если обЗначениеНеЗаполнено(СтрокаТоваров.ГТД) Тогда
			ОбластьМакета.Параметры.ПредставлениеГТД	= "-----";
			ОбластьМакета.Параметры.ПредставлениеСтраны	= "-----";
			Если НовыйМакет Тогда
				ОбластьМакета.Параметры.ПредставлениеСтраныКод = "-----";
			КонецЕсли;
		Иначе
			ОбластьМакета.Параметры.ПредставлениеГТД	= спПолучитьНаименование(СтрокаТоваров.ГТД);
			ОбластьМакета.Параметры.ПредставлениеСтраны	= СтрокаТоваров.ГТД.Страна.Наименование;
			Если НовыйМакет Тогда
				ОбластьМакета.Параметры.ПредставлениеСтраныКод = СтрокаТоваров.ГТД.Страна.Код;
			КонецЕсли;
		КонецЕсли;
		
		СуммаБезНДС=СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС;
		ЦенаБезНДС=СуммаБезНДС/КоличествоТовара;
		ОбластьМакета.Параметры.Сумма = Формат(СуммаБезНДС,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.Цена = Формат(ЦенаБезНДС,ФорматВыводаСуммы);
		СуммаВсегоБезНДС = СуммаВсегоБезНДС + СуммаБезНДС;
		ОбластьМакета.Параметры.Акциз = "без акциза";
		ОбластьМакета.Параметры.СуммаНДС = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС,"Без НДС",Формат(СтрокаТоваров.СуммаНДС,ФорматВыводаСуммы));
		ОбластьМакета.Параметры.СуммаВсего = Формат(СтрокаТоваров.СуммаВсего,ФорматВыводаСуммы);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, ?(КоличествоСтрок=Ном, мсвДопОбластиПодвала, Неопределено));
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("Сумма,СуммаНДС,СуммаВсего",0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТоваров, СтруктураИтоговПоСтранице);
		СтруктураИтоговПоСтранице.Сумма = СтруктураИтоговПоСтранице.Сумма - СтрокаТоваров.Сумма + СуммаБезНДС;
		Ном = Ном + 1;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтогоПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;
	Если НовыйМакет Тогда
		ОбластьИтого.Параметры.ИтогоСумма	= Формат(СуммаВсегоБезНДС,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьИтого.Параметры.ИтогоСуммаНДС	= Формат(ВыборкаТабличнойЧасти.Итог("СуммаНДС"),ФорматВыводаСуммы);
	ОбластьИтого.Параметры.ИтогоВсего		= Формат(ВыборкаТабличнойЧасти.Итог("СуммаВсего"),ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьИтого);
	
	// Заполним информацию о руководителях организации
	Руководитель = обПолучитьЗначениеСвойства(ЭтотОбъект,"Руководитель");
	
	Если Контрагент.ФормаСобственности=Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ОбластьПодвал.Параметры.Руководитель=Руководитель;
		ОбластьПодвал.Параметры.РуководительПредставление=Руководитель;
		ГлавныйБухгалтер = обПолучитьЗначениеСвойства(ЭтотОбъект,"ГлавныйБухгалтер");
		ОбластьПодвал.Параметры.ГлавныйБухгалтер=ГлавныйБухгалтер;
		ОбластьПодвал.Параметры.ГлавныйБухгалтерПредставление=ГлавныйБухгалтер;
	КонецЕсли;
	
	Если Контрагент.ФормаСобственности=Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
		ОбластьПодвал.Параметры.ФИОПБОЮЛ=Руководитель;
		ОбластьПодвал.Параметры.Свидетельство = спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Свидетельство);
	КонецЕсли; 
	
	Если НовыйМакет Тогда
		ОбластьПодвал.Параметры.ТекстИндивидуальныйПредприниматель = ?(ВыводитьКодВидаТовара, "Индивидуальный предприниматель или иное уполномоченное лицо", "Индивидуальный предприниматель");
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьПодвал);
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьСчетФактура()

Функция ПечатьСчетФактуры2021(ТабДокумент)
	
	ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаТабличнойЧасти=ЭтотОбъект.Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ВыборкаТабличнойЧасти,ЭтотОбъект,ВалютаПечатногоДокумента);
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	Если ПустаяСтрока(ФорматВыводаСуммы) Тогда
		ФорматВыводаСуммы = ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00";
	КонецЕсли;
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	ЕстьМежценоваяРазница = Ложь;
	ЕстьПрослеживаемыйТовар = Ложь;
	
	// Проверим, что есть РНПТ в документе
	Для Каждого ТекущаяСтрока Из ВыборкаТабличнойЧасти Цикл
		Если ЗначениеЗаполнено(ТекущаяСтрока.ГТД) И ТекущаяСтрока.ГТД.РНПТ Тогда
			ЕстьПрослеживаемыйТовар = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Определим макет
	Если ЕстьПрослеживаемыйТовар Тогда
		Макет = ПолучитьОбщийМакет("СчетФактура_02_04_2021_Прослеж");
	Иначе
		Макет = ПолучитьОбщийМакет("СчетФактура_02_04_2021");
	КонецЕсли;
	
	ВыводитьКодТНВЭД = обПраво("ВыводитьКодТНВЭД");
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КоличествоСтрок = ВыборкаТабличнойЧасти.Количество();
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	Если СокрЛП(ВхДокНомер)="" Тогда
		НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	Иначе
		НомерДляПечати=СокрЛП(ВхДокНомер);
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ВхДокДата) Тогда
		ДатаДляПечати=Дата;
    Иначе
		ДатаДляПечати=ВхДокДата;
	КонецЕсли; 
	Если ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
		ОбластьМакета.Параметры.Номер = НомерДляПечати;
		ОбластьМакета.Параметры.Дата = Формат(ДатаДляПечати, "ДФ=""дд ММММ гггг""");
		ОбластьМакета.Параметры.НомерИсправления = "----";
		ОбластьМакета.Параметры.ДатаИсправления = "----";
		ТекстЗаголовкаОтчета    = "Счет-фактура № " + ?(ПустаяСтрока(ВхДокНомер),дкПолучитьНомерДляПечати(ЭтотОбъект),СокрЛП(ВхДокНомер)) +" от " + Формат(?(обЗначениеНеЗаполнено(ВхДокДата),ЭтотОбъект.Дата,ВхДокДата), "ДФ=""дд ММММ гггг""");
	Иначе
		
		ОбластьМакета.Параметры.НомерИсправления = ?(ЗначениеЗаполнено(ЭтотОбъект.НомерИсправления), ЭтотОбъект.НомерИсправления, "----");
		ОбластьМакета.Параметры.ДатаИсправления = Формат(ЭтотОбъект.Дата, "ДФ=""дд ММММ гггг""");
		
		// найдем изначальную счет фактуру
		ДокументОсн = ДокументОснование;
		Пока ТипЗнч(ДокументОсн) = Тип("ДокументСсылка.КорректировкаПоступления") Цикл
			ДокументОсн = ДокументОсн.ДокументОснование;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СчетФактураПолученный.Ссылка
		|ИЗ
		|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|ГДЕ
		|	СчетФактураПолученный.ДокументОснование = &ДокументОсн";
		Запрос.УстановитьПараметр("ДокументОсн",ДокументОсн);
		ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
		
		Если ВыборкаЗапроса.Следующий() Тогда
			ОбластьМакета.Параметры.Номер = ?(ПустаяСтрока(ВыборкаЗапроса.Ссылка.ВхДокНомер),дкПолучитьНомерДляПечати(ВыборкаЗапроса.Ссылка.ПолучитьОбъект()),СокрЛП(ВыборкаЗапроса.Ссылка.ВхДокНомер));
			ОбластьМакета.Параметры.Дата = Формат(?(обЗначениеНеЗаполнено(ВыборкаЗапроса.Ссылка.ВхДокДата),ВыборкаЗапроса.Ссылка.Дата,ВыборкаЗапроса.Ссылка.ВхДокДата), "ДФ=""дд ММММ гггг""");
			ТекстЗаголовкаОтчета    = "Счет-фактура № " + ОбластьМакета.Параметры.Номер +" от " + ОбластьМакета.Параметры.Дата;
		Иначе
			ОбластьМакета.Параметры.Номер = "----";
			ОбластьМакета.Параметры.Дата = "----";
			ТекстЗаголовкаОтчета = "Счет-фактура  № " + "----" +" от " + "----";
		КонецЕсли;
	КонецЕсли;
	ОбластьМакета.Параметры.Поставщик				= ЭтотОбъект.Контрагент;
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,Новый Структура("Наименование",""));
	
	ОбластьМакета.Параметры.АдресПоставщика	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,Новый Структура("АдресЮридический",""));
	ОбластьМакета.Параметры.ИННпоставщика	= ?(ОбластьМакета.Параметры.Поставщик.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение, ОбластьМакета.Параметры.Поставщик.ГоловнойКонтрагент.ИНН, ОбластьМакета.Параметры.Поставщик.ИНН) + "/" + ОбластьМакета.Параметры.Поставщик.КПП;
	
	ОбластьМакета.Параметры.Грузоотправитель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель",ЭтотОбъект.Грузоотправитель);
	Если ЭтотОбъект.Контрагент=ОбластьМакета.Параметры.Грузоотправитель Тогда
		ОбластьМакета.Параметры.ГрузоотправительПредставление	= "он же";
	Иначе
		ОбластьМакета.Параметры.ГрузоотправительПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузоотправитель,Новый Структура("Наименование,АдресФактический","",""));
	КонецЕсли;
	ОбластьМакета.Параметры.ВалютаШапка = ?(НЕ обЗначениеНеЗаполнено(ВалютаПечатногоДокумента.НаименованиеПолное), ВалютаПечатногоДокумента.НаименованиеПолное, ВалютаПечатногоДокумента.Наименование) + ", " + ВалютаПечатногоДокумента.Код;
	ОбластьМакета.Параметры.Грузополучатель					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.ПодразделениеКомпании);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ОбластьМакета.Параметры.ГрузополучательПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,Новый Структура("Наименование,АдресФактический","",""), ДополнительныеПараметры);
	
	Если КоличествоСтрок>0 Тогда
		ЕстьТовары=Ложь;
		Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
			Если ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
				Если СтрокаТоваров.Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
					ЕстьТовары=Истина; Прервать;
				КонецЕсли; 
			ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
				ЕстьТовары=Истина; Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ЕстьТовары Тогда
			ОбластьМакета.Параметры.ГрузоотправительПредставление="----";
			ОбластьМакета.Параметры.ГрузополучательПредставление="----";
		КонецЕсли; 
	КонецЕсли; 
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.Выписка") Тогда
		ОбластьМакета.Параметры.ПредставлениеПоДокументу	= "№ " + ДокументОснование.Номер + " от: " + Формат(ДокументОснование.Дата,"ДФ=dd.MM.yyyy");
		ОбластьМакета.Параметры.ПоДокументу					= ДокументОснование;
	Иначе
		ОбластьМакета.Параметры.ПредставлениеПоДокументу	= "          от: ";
	КонецЕсли;
	
	Попытка
		ДатаОснования = ?(ЗначениеЗаполнено(ДокументОснование.ВхДокДата), Формат(ДокументОснование.ВхДокДата, "ДФ=""дд.ММ.гггг"""), "");
		НомерОснования = ?(ЗначениеЗаполнено(ДокументОснование.ВхДокНомер), ДокументОснование.ВхДокНомер, "          ");
	Исключение
		ДатаОснования = ?(ЗначениеЗаполнено(ДокументОснование.Дата), Формат(ДокументОснование.Дата, "ДФ=""дд.ММ.гггг"""), "");
		НомерОснования = ?(ЗначениеЗаполнено(ДокументОснование.Номер), ДокументОснование.Номер, "          ");
	КонецПопытки;
	
	Если НЕ ЭтоАвансовыйСчетФактура() Тогда
		Если ВыборкаТабличнойЧасти.Количество() = 0 Тогда
			ОбластьМакета.Параметры.ДокументыОбОтгрузке = "№" + НомерОснования + " от " + ДатаОснования;
		ИначеЕсли ВыборкаТабличнойЧасти.Количество() = 1 Тогда
			ОбластьМакета.Параметры.ДокументыОбОтгрузке = "№ п/п 1 №" + НомерОснования + " от " + ДатаОснования;
		Иначе
			ОбластьМакета.Параметры.ДокументыОбОтгрузке = "№ п/п 1-" + ВыборкаТабличнойЧасти.Количество() +" №" + НомерОснования + " от " + ДатаОснования;
		КонецЕсли;
	КонецЕсли;
	ОбластьМакета.Параметры.Покупатель				= Организация;
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(ОбластьМакета.Параметры.Покупатель,Новый Структура("Наименование",""));
	
	ОбластьМакета.Параметры.АдресПокупателя   = спПолучитьПредставление(ПодразделениеКомпании,Новый Структура("АдресЮридический",""));
	ОбластьМакета.Параметры.ИННПокупателя = ОбластьМакета.Параметры.Покупатель.ИНН+"/"+спПолучитьКППДляПечати(Организация, ПодразделениеКомпании);
	
	ОбластьМакета.Параметры.ИдентификаторГосударственногоКонтракта = ИдентификаторГосударственногоКонтракта;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("Сумма, СуммаНДС, СуммаВсего", 0, 0, 0);
	СтруктураОбщихИтогов = Новый Структура("Сумма, СуммаНДС, СуммаВсего", 0, 0, 0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовкаОтчета;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы	 = "Страница: " + НомерСтраницы; 
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьИтого  = Макет.ПолучитьОбласть("Итого");
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	мсвДопОбластиПодвала.Добавить(ОбластьИтого);
	
	Если ЕстьПрослеживаемыйТовар Тогда
		ТаблицаРНПТ = ТаблицаДвиженийПоГТД(ДокументОснование);
	Иначе
		ТаблицаРНПТ = Неопределено;
	КонецЕсли;
	
	Ном=1;
	СуммаВсегоБезНДС = 0;
	ЕдиницаИзмеренияАвтоработВПечатныхФормах = ЭтотОбъект.ДоговорВзаиморасчетов.ЕдиницаИзмеренияАвтоработВПечатныхФормах;
	Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
		
		Если СтрокаТоваров.Номенклатура=Справочники.Номенклатура.Предоплата Или СтрокаТоваров.Номенклатура=Справочники.Номенклатура.КомиссионноеВознаграждение Тогда
			ЗаполнитьПрочерки = Истина;
		Иначе
			ЗаполнитьПрочерки = Ложь;
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТоваров);
		Если ЗаполнитьПрочерки Тогда
			ОбластьМакета.Параметры.Количество = "--";	
		Иначе
			ОбластьМакета.Параметры.Количество = Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		КонецЕсли;
		Наименование = спПолучитьНаименование(СтрокаТоваров.Номенклатура);
		ОбластьМакета.Параметры.ТоварНаименование	= Наименование;
		Если НЕ СтрокаТоваров.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() Тогда    
			ОбластьМакета.Параметры.ТоварНаименование = ОбластьМакета.Параметры.ТоварНаименование + ", " + СтрокаТоваров.ХарактеристикаНоменклатуры;	       
		КонецЕсли;
		КоличествоТовара = СтрокаТоваров.Количество;
		Если ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = СтрокаТоваров.ЕдиницаИзмерения.Код;
			ОбластьМакета.Параметры.КодВидаТовара = "--";
		ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура)<>Тип("СправочникСсылка.Номенклатура") Тогда
			Если ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автоработы") Тогда
				КоличествоТовара = СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент;
				ОбластьМакета.Параметры.Количество = КоличествоТовара;
				ОбластьМакета.Параметры.ЕдиницаИзмерения = ЕдиницаИзмеренияАвтоработВПечатныхФормах;
				Если ЕдиницаИзмеренияАвтоработВПечатныхФормах = Перечисления.ЕдиницаИзмеренияАвтоработВПечатныхФормах.ЕдиницаИзмеренияЧас Тогда
					ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "365";
				Иначе
					ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
				КонецЕсли;
				ОбластьМакета.Параметры.КодВидаТовара = "--";
			Иначе
				КоличествоТовара = СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент;
				ОбластьМакета.Параметры.Количество = КоличествоТовара;
				ОбластьМакета.Параметры.ЕдиницаИзмерения = "ч";
				ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "356";
				ОбластьМакета.Параметры.КодВидаТовара = "--";
			КонецЕсли;
		ИначеЕсли ЗаполнитьПрочерки Тогда 
			ОбластьМакета.Параметры.Количество = "--";
			ОбластьМакета.Параметры.ЕдиницаИзмерения = "--";
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
			ОбластьМакета.Параметры.КодВидаТовара = "--";
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.ЕдиницаИзмерения) Тогда
				Если СтрокаТоваров.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
					ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
				Иначе
					ОбластьМакета.Параметры.ЕдиницаИзмеренияКод = "--";
				КонецЕсли;
			КонецЕсли;
			КодТНВЭД = СтрокаТоваров.Номенклатура.КодТНВЭД;
			Если ЗначениеЗаполнено(КодТНВЭД) И ВыводитьКодТНВЭД Тогда
				ОбластьМакета.Параметры.КодВидаТовара = СокрЛП(КодТНВЭД);
			Иначе
				ОбластьМакета.Параметры.КодВидаТовара = "--";
			КонецЕсли;
		КонецЕсли;
		Если ЗаполнитьПрочерки Тогда
			ОбластьМакета.Параметры.СтавкаНДС = Строка(СтрокаТоваров.СтавкаНДС.Ставка) +"/" + Строка(СтрокаТоваров.СтавкаНДС.Ставка + 100);
		Иначе
			ОбластьМакета.Параметры.СтавкаНДС			= дкПолучитьПредставлениеСтавкиНДС(СтрокаТоваров.СтавкаНДС,"%");
		КонецЕсли;
		
		Если ЗаполнитьПрочерки Тогда
			ОбластьМакета.Параметры.ПредставлениеГТД = "--";
			ОбластьМакета.Параметры.ПредставлениеСтраны = "--";
			ОбластьМакета.Параметры.ПредставлениеСтраныКод = "--";
		ИначеЕсли обЗначениеНеЗаполнено(СтрокаТоваров.ГТД) Тогда
			ОбластьМакета.Параметры.ПредставлениеГТД	= "-----";
			ОбластьМакета.Параметры.ПредставлениеСтраны	= "-----";
			ОбластьМакета.Параметры.ПредставлениеСтраныКод = "-----";
		Иначе
			ОбластьМакета.Параметры.ПредставлениеГТД	= спПолучитьНаименование(СтрокаТоваров.ГТД);
			ОбластьМакета.Параметры.ПредставлениеСтраны	= СтрокаТоваров.ГТД.Страна.Наименование;
			ОбластьМакета.Параметры.ПредставлениеСтраныКод = СтрокаТоваров.ГТД.Страна.Код;
		КонецЕсли;
		
		СуммаБезНДС=СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС;
		ЦенаБезНДС=СуммаБезНДС/КоличествоТовара;
		Если ЗаполнитьПрочерки Тогда
			ОбластьМакета.Параметры.Сумма = "--";
			ОбластьМакета.Параметры.Цена = "--";
			ОбластьМакета.Параметры.Акциз = "--";
		Иначе
			ОбластьМакета.Параметры.Сумма = Формат(СуммаБезНДС,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.Цена = Формат(ЦенаБезНДС ,ФорматВыводаСуммы);
			СуммаВсегоБезНДС = СуммаБезНДС + СуммаВсегоБезНДС;
			ОбластьМакета.Параметры.Акциз = "без акциза";
		КонецЕсли;
		ОбластьМакета.Параметры.СуммаНДС = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС,"Без НДС",Формат(СтрокаТоваров.СуммаНДС,ФорматВыводаСуммы));
		ОбластьМакета.Параметры.СуммаВсего = Формат(СтрокаТоваров.СуммаВсего,ФорматВыводаСуммы);
		
		// Получим Количество прослеживаемого товара в единицах прослеживаемости
		Если ЕстьПрослеживаемыйТовар Тогда
			
			Если ЗначениеЗаполнено(СтрокаТоваров.ГТД) И СтрокаТоваров.ГТД.РНПТ Тогда
				Если ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Номенклатура")
					И ТаблицаРНПТ <> Неопределено Тогда
					СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД");
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТоваров);
					СрокиРНПТ = ТаблицаРНПТ.НайтиСтроки(СтруктураПоиска);
					Если СрокиРНПТ.Количество() > 0 Тогда
						ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКод = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код;
						ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослеж = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Наименование;
						ОбластьМакета.Параметры.КоличествоПрослеж = СрокиРНПТ[0].Количество * ?(СрокиРНПТ[0].Количество < 0, -1, 1);
					Иначе
						ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКод = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код;
						ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослеж = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Наименование;
						ОбластьМакета.Параметры.КоличествоПрослеж = Окр(СтрокаТоваров.Количество * СтрокаТоваров.Коэффициент, 3);
					КонецЕсли;
				ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКод = "796";
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослеж = "шт";
					ОбластьМакета.Параметры.КоличествоПрослеж = 1;
				Иначе
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКод = "--";
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослеж = "--";
					ОбластьМакета.Параметры.КоличествоПрослеж = "--";
				КонецЕсли;
			Иначе
				ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКод = "--";
				ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослеж = "--";
				ОбластьМакета.Параметры.КоличествоПрослеж = "--";
			КонецЕсли;
			
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, ?(КоличествоСтрок=Ном, мсвДопОбластиПодвала, Неопределено));
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураОбщихИтогов.Сумма = СтруктураОбщихИтогов.Сумма + СтруктураИтоговПоСтранице.Сумма;
			СтруктураОбщихИтогов.СуммаНДС = СтруктураОбщихИтогов.СуммаНДС + СтруктураИтоговПоСтранице.СуммаНДС;
			СтруктураОбщихИтогов.СуммаВсего = СтруктураОбщихИтогов.СуммаВсего + СтруктураИтоговПоСтранице.СуммаВсего;
			СтруктураИтоговПоСтранице = Новый Структура("Сумма, СуммаНДС,СуммаВсего",0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТоваров, СтруктураИтоговПоСтранице);
		СтруктураИтоговПоСтранице.Сумма = СтруктураИтоговПоСтранице.Сумма - СтрокаТоваров.Сумма + СуммаБезНДС;
		
		Ном = Ном + 1;
	КонецЦикла;
	
	СтруктураОбщихИтогов.Сумма = СтруктураОбщихИтогов.Сумма + СтруктураИтоговПоСтранице.Сумма;
	СтруктураОбщихИтогов.СуммаНДС = СтруктураОбщихИтогов.СуммаНДС + СтруктураИтоговПоСтранице.СуммаНДС;
	СтруктураОбщихИтогов.СуммаВсего = СтруктураОбщихИтогов.СуммаВсего + СтруктураИтоговПоСтранице.СуммаВсего;
	

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтогоПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;
	
	ОбластьИтого.Параметры.ИтогоСумма	= Формат(СтруктураОбщихИтогов.Сумма,ФорматВыводаСуммы);
	ОбластьИтого.Параметры.ИтогоСуммаНДС	= Формат(СтруктураОбщихИтогов.СуммаНДС,ФорматВыводаСуммы);
	ОбластьИтого.Параметры.ИтогоВсего		= Формат(СтруктураОбщихИтогов.СуммаВсего,ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьИтого);
	
	// Заполним информацию о руководителях организации
	Руководитель = обПолучитьЗначениеСвойства(ЭтотОбъект,"Руководитель");
	
	Если Контрагент.ФормаСобственности=Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ОбластьПодвал.Параметры.Руководитель=Руководитель;
		ОбластьПодвал.Параметры.РуководительПредставление=Руководитель;
		ГлавныйБухгалтер = обПолучитьЗначениеСвойства(ЭтотОбъект,"ГлавныйБухгалтер");
		ОбластьПодвал.Параметры.ГлавныйБухгалтер=ГлавныйБухгалтер;
		ОбластьПодвал.Параметры.ГлавныйБухгалтерПредставление=ГлавныйБухгалтер;
	КонецЕсли;
	
	Если Контрагент.ФормаСобственности=Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
		ОбластьПодвал.Параметры.ФИОПБОЮЛ=Руководитель;
		ОбластьПодвал.Параметры.Свидетельство = спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Свидетельство);
	КонецЕсли; 
	
	ТабДокумент.Вывести(ОбластьПодвал);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ПечатьКорректировочныйСчетФактура(ТабДокумент) Экспорт
	
	Если Дата >= Дата("20210701000000") Тогда
		Возврат ПечатьКорректировочныйСчетФактура2021(ТабДокумент);
	КонецЕсли;

	ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	Если ПустаяСтрока(ФорматВыводаСуммы) Тогда
		ФорматВыводаСуммы = ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00";
	КонецЕсли;
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	ВыводитьКодВидаТовара = (ЭтотОбъект.Дата >= Дата('20171001'));
	ВыводитьКодТНВЭД      = обПраво("ВыводитьКодТНВЭД");
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("КорректировочныйСчетФактура");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	//
	ОбластьМакета.Параметры.НомерИсправленияКорректировочного = ?(обЗначениеНеЗаполнено(НомерИсправления),"--",НомерИсправления);
	ОбластьМакета.Параметры.ДатаИсправленияКорректировочного  = ?(обЗначениеНеЗаполнено(НомерИсправления),"--",Формат(Дата,"ДЛФ=DD"));
	Если ДокументОснование.ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияКорректировкаПоСогласованиюСторон
		ИЛИ ДокументОснование.ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейКорректировкаПоСогласованиюСторон Тогда
		ОбластьМакета.Параметры.Номер = ?(обЗначениеНеЗаполнено(НомерИсправляемогоКорректировочногоДокумента),ВхДокНомер,НомерИсправляемогоКорректировочногоДокумента);
		ОбластьМакета.Параметры.Дата  = Формат(?(обЗначениеНеЗаполнено(ДатаИсправляемогоКорректировочногоДокумента),ВхДокДата,ДатаИсправляемогоКорректировочногоДокумента),"ДЛФ=DD");
		ОбластьМакета.Параметры.НомерИсправленияКорректировочного = "--";
		ОбластьМакета.Параметры.ДатаИсправленияКорректировочного = "--";
	Иначе
		ОбластьМакета.Параметры.Номер = ?(обЗначениеНеЗаполнено(НомерИсправляемогоКорректировочногоДокумента),ВхДокНомер,НомерИсправляемогоКорректировочногоДокумента);
		ОбластьМакета.Параметры.Дата  = Формат(?(обЗначениеНеЗаполнено(ДатаИсправляемогоКорректировочногоДокумента),ВхДокДата,ДатаИсправляемогоКорректировочногоДокумента),"ДЛФ=DD");
	КонецЕсли;
	ОбластьМакета.Параметры.НомерСчетаФактуры = ?(обЗначениеНеЗаполнено(НомерИсходногоДокумента),"--",НомерИсходногоДокумента);
	ОбластьМакета.Параметры.ДатаСчетаФактуры  = Формат(?(обЗначениеНеЗаполнено(ДатаИсходногоДокумента),"--",ДатаИсходногоДокумента),"ДЛФ=DD");
	ОбластьМакета.Параметры.НомерИсправления  = ?(обЗначениеНеЗаполнено(НомерИсправленияИсходногоДокумента),"--",НомерИсправленияИсходногоДокумента);
	ОбластьМакета.Параметры.ДатаИсправления   = Формат(?(обЗначениеНеЗаполнено(ДатаИсправленияИсходногоДокумента),"--",ДатаИсправленияИсходногоДокумента),"ДЛФ=DD");
	
	ОбластьМакета.Параметры.Покупатель               = ЭтотОбъект.Организация;
	ОбластьМакета.Параметры.ПредставлениеПокупатель = спПолучитьПредставление(ОбластьМакета.Параметры.Покупатель,Новый Структура("Наименование",""));
	ОбластьМакета.Параметры.АдресПокупателя         = спПолучитьПредставление(ЭтотОбъект.ПодразделениеКомпании,Новый Структура("АдресЮридический"," "));
	
	ОбластьМакета.Параметры.ИННПокупателя = Организация.ИНН + "/" + спПолучитьКППДляПечати(Организация, ПодразделениеКомпании);
	
	ОбластьМакета.Параметры.Поставщик               = Контрагент;
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,Новый Структура("Наименование",""));
	ОбластьМакета.Параметры.АдресПоставщика         = спПолучитьПредставление(Контрагент,Новый Структура("АдресЮридический",""));
	ОбластьМакета.Параметры.ИННПоставщика           = ?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение, Контрагент.ГоловнойКонтрагент.ИНН, Контрагент.ИНН) + "/" + Контрагент.КПП;
	
	ОбластьМакета.Параметры.Валюта = "Валюта: наименование, код " + 
		?(НЕ обЗначениеНеЗаполнено(ВалютаПечатногоДокумента.НаименованиеПолное), ВалютаПечатногоДокумента.НаименованиеПолное, ВалютаПечатногоДокумента.Наименование) 
		+ ", " + ВалютаПечатногоДокумента.Код;
	
	// если дата документа позднее 01.07.2017
	Если ВыводитьКодВидаТовара Тогда
		ОбластьМакета.Параметры.Редакция = НСтр("ru = '(в редакции постановления Правительства Российской Федерации от 19 августа 2017 № 981)'");
	ИначеЕсли ЭтотОбъект.Дата >= Дата('20170701') Тогда
		ОбластьМакета.Параметры.Редакция = НСтр("ru = '(в редакции постановления Правительства Российской Федерации от 25 мая 2017 г. № 625)'");
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// если дата документа позднее 01.07.2017
	Если ЭтотОбъект.Дата >= Дата('20170701') Тогда
		ОбластьШапкаИдГосКонтракта = Макет.ПолучитьОбласть("ШапкаИдГосКонтракта");
		ЗаголовокИдентификатораГосударственногоКонтракта = "Идентификатор государственного контракта, договора (соглашения)" + ?(ВыводитьКодВидаТовара, " (при наличии)", "")+": ";
		ОбластьШапкаИдГосКонтракта.Параметры.ИдентификаторГосударственногоКонтракта = ЗаголовокИдентификатораГосударственногоКонтракта + ЭтотОбъект.ИдентификаторГосударственногоКонтракта;
		ТабДокумент.Вывести(ОбластьШапкаИдГосКонтракта);
	КонецЕсли;
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	
	// Уберем колонку, вывод которой утвержден с 1 октября 2017 г.
	Если НЕ ВыводитьКодВидаТовара Тогда
		
		ОбластьКодВидаТовара = Макет.Область("КодВидаТовара");
		НовыйТекст = ОбластьШапкаТаблицы.Область(1,ОбластьКодВидаТовара.Лево-4,3,ОбластьКодВидаТовара.Лево-1).Текст;
		
		ОбластьВыреза = ОбластьШапкаТаблицы.Область(1,ОбластьКодВидаТовара.Лево-4,3,ОбластьКодВидаТовара.Право);
		ОбластьВыреза.Объединить();
		ОбластьВыреза.Текст = НовыйТекст;
		
		ОбластьВыреза = ОбластьШапкаТаблицы.Область(4,ОбластьКодВидаТовара.Лево-4,4,ОбластьКодВидаТовара.Право);
		ОбластьВыреза.Объединить();
		
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("Итого");
	СтруктураИтоговПоСтранице =
	Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение",0,0,0,0,0,0);
	СтруктураИтоговПоСтраницеИтого =
	Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение",0,0,0,0,0,0);
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	// Уберем колонку, вывод которой утвержден с 1 октября 2017 г.
	Если НЕ ВыводитьКодВидаТовара Тогда
		ОбластьКодВидаТовара = Макет.Область("СтрокаКодВидаТовара");
		НомерСтроки = 1;
		Пока НомерСтроки < 5 Цикл
			ТекстЯчейки = ОбластьМакета.Область(НомерСтроки,ОбластьКодВидаТовара.Лево-4,НомерСтроки,ОбластьКодВидаТовара.Лево-1).Текст;
			ОбластьВыреза = ОбластьМакета.Область(НомерСтроки,ОбластьКодВидаТовара.Лево-4,НомерСтроки,ОбластьКодВидаТовара.Право);
			ОбластьВыреза.Объединить();
			ОбластьВыреза.Текст = ТекстЯчейки;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьИтого  = Макет.ПолучитьОбласть("Итого");
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	мсвДопОбластиПодвала.Добавить(ОбластьИтого);
	
	Ном=1;
	СуммаВсегоБезНал = 0;
	ВыборкаТабличнойЧасти = Документы.СчетФактураПолученный.ПолучитьВыборку(ЭтотОбъект.ДокументОснование);
	зфПерерасчетТаблицыТоваров(ВыборкаТабличнойЧасти,ЭтотОбъект,ВалютаПечатногоДокумента);
	Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.НаименованиеНоменклатуры = спПолучитьНаименование(СтрокаТоваров.Номенклатура);
		
		Если ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Номенклатура") И СтрокаТоваров.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод          = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмерения = СтрокаТоваров.ЕдиницаИзмерения;
			Если ВыводитьКодВидаТовара Тогда
				Попытка
					КодТНВЭД = СтрокаТоваров.Номенклатура.КодТНВЭД;
				Исключение
					КодТНВЭД = Неопределено;
				Конецпопытки;
				Если ЗначениеЗаполнено(КодТНВЭД) И ВыводитьКодТНВЭД Тогда
					ОбластьМакета.Параметры.КодВидаТовара = СокрЛП(КодТНВЭД);
				Иначе
					ОбластьМакета.Параметры.КодВидаТовара = "--";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод          = "796";
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмерения = "шт";
			Если ВыводитьКодВидаТовара Тогда
				ОбластьМакета.Параметры.КодВидаТовара = "--";
			КонецЕсли;
		Иначе
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКод          = "--";
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмерения = "--";
			Если ВыводитьКодВидаТовара Тогда
				ОбластьМакета.Параметры.КодВидаТовара = "--";
			КонецЕсли;
		КонецЕсли;
		
		ОбластьМакета.Параметры.КоличествоПослеИзменения = Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.КоличествоДоИзменения    = Формат(СтрокаТоваров.КоличествоПоДокументуПоступления,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.СтавкаНДС = СтрокаТоваров.СтавкаНДС;
		
		
		ОбластьМакета.Параметры.СтоимостьСНДСДоИзменения    = Формат(СтрокаТоваров.СуммаВсегоПоДокументуПоступления,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СтоимостьСНДСПослеИзменения = Формат(СтрокаТоваров.СуммаВсего,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаСНДСУвеличение       = Формат(?(СтрокаТоваров.СуммаВсегоРазница >0,СтрокаТоваров.СуммаВсегоРазница,0),ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаСНДСУменьшение       = Формат(?(СтрокаТоваров.СуммаВсегоРазница <0,-СтрокаТоваров.СуммаВсегоРазница,0),ФорматВыводаСуммы);

		ОбластьМакета.Параметры.СуммаНДСДоИзменения     = Формат(СтрокаТоваров.СуммаНДСПоДокументуПоступления,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаНДСПослеИзменения  = Формат(СтрокаТоваров.СуммаНДС,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаНДСУвеличение    = Формат(?(СтрокаТоваров.СуммаНДСРазница >0,СтрокаТоваров.СуммаНДСРазница,0),ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаНДСУменьшение    = Формат(?(СтрокаТоваров.СуммаНДСРазница <0,-СтрокаТоваров.СуммаНДСРазница,0),ФорматВыводаСуммы);
		
		ОбластьМакета.Параметры.СтоимостьБезНДСДоИзменения    = Формат((СтрокаТоваров.СуммаВсегоПоДокументуПоступления - СтрокаТоваров.СуммаНДСПоДокументуПоступления),ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СтоимостьБезНДСПослеИзменения = Формат((СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС),ФорматВыводаСуммы);
		РазницаБезНДС = (СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС)-(СтрокаТоваров.СуммаВсегоПоДокументуПоступления - СтрокаТоваров.СуммаНДСПоДокументуПоступления);
		ОбластьМакета.Параметры.РазницаБезНДСУвеличение       = Формат(?(РазницаБезНДС >0,РазницаБезНДС,0),ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаБезНДСУменьшение       = Формат(?(РазницаБезНДС <0,-РазницаБезНДС,0),ФорматВыводаСуммы);
		
		Если НЕ СтрокаТоваров.КоличествоПоДокументуПоступления = 0 Тогда
			ОбластьМакета.Параметры.ЦенаДоИзменения    = Формат((СтрокаТоваров.СуммаВсегоПоДокументуПоступления - СтрокаТоваров.СуммаНДСПоДокументуПоступления)/СтрокаТоваров.КоличествоПоДокументуПоступления,ФорматВыводаСуммы);
		Иначе
			ОбластьМакета.Параметры.ЦенаДоИзменения    = Формат(0,ФорматВыводаСуммы);
		КонецЕсли;
		ОбластьМакета.Параметры.ЦенаПослеИзменения = Формат(?(СтрокаТоваров.Количество=0,0,(СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС)/СтрокаТоваров.Количество),ФорматВыводаСуммы);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице
			= Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение",0,0,0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			//ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		СтруктураИтоговПоСтранице.РазницаБезНДСУвеличение = СтруктураИтоговПоСтранице.РазницаБезНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаБезНДСУвеличение);
		СтруктураИтоговПоСтранице.РазницаБезНДСУменьшение = СтруктураИтоговПоСтранице.РазницаБезНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаБезНДСУменьшение);
		СтруктураИтоговПоСтранице.РазницаНДСУменьшение    = СтруктураИтоговПоСтранице.РазницаНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаНДСУменьшение);
		СтруктураИтоговПоСтранице.РазницаНДСУвеличение    = СтруктураИтоговПоСтранице.РазницаНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаНДСУвеличение);
		СтруктураИтоговПоСтранице.РазницаСНДСУменьшение   = СтруктураИтоговПоСтранице.РазницаСНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаСНДСУменьшение);
		СтруктураИтоговПоСтранице.РазницаСНДСУвеличение   = СтруктураИтоговПоСтранице.РазницаСНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаСНДСУвеличение);
		
		// итого
		СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУвеличение = СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаБезНДСУвеличение);
		СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУменьшение = СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаБезНДСУменьшение);
		СтруктураИтоговПоСтраницеИтого.РазницаНДСУменьшение    = СтруктураИтоговПоСтраницеИтого.РазницаНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаНДСУменьшение);
		СтруктураИтоговПоСтраницеИтого.РазницаНДСУвеличение    = СтруктураИтоговПоСтраницеИтого.РазницаНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаНДСУвеличение);
		СтруктураИтоговПоСтраницеИтого.РазницаСНДСУменьшение   = СтруктураИтоговПоСтраницеИтого.РазницаСНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаСНДСУменьшение);
		СтруктураИтоговПоСтраницеИтого.РазницаСНДСУвеличение   = СтруктураИтоговПоСтраницеИтого.РазницаСНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаСНДСУвеличение);
		Ном = Ном + 1;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтогоПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;
	
	//Если НовыйМакет Тогда
	//	ОбластьИтого.Параметры.ИтогоСумма	= Формат(СуммаВсегоБезНал,ФорматВыводаСуммы);
	//КонецЕсли;
	//ОбластьИтого.Параметры.ИтогоСуммаНДС	= Формат(ЭтотОбъект.Товары.Итог("СуммаНДС"),ФорматВыводаСуммы);
	//ОбластьИтого.Параметры.ИтогоВсего		= Формат(ЭтотОбъект.Товары.Итог("СуммаВсего"),ФорматВыводаСуммы);
	ОбластьИтого.Параметры.Заполнить(СтруктураИтоговПоСтраницеИтого);
	ТабДокумент.Вывести(ОбластьИтого);
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	
	Если Контрагент.ФормаСобственности=Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ОбластьПодвал.Параметры.Заполнить(Руководитель);
		ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
		ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	КонецЕсли;
	
	Если Организация.ФормаСобственности=Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
		ОбластьПодвал.Параметры.ФИОПБОЮЛ=Руководитель.Руководитель;
		//ОбластьПодвал.Параметры.ФИОПБОЮЛПредставление=Руководитель.РуководительПредставление;
		ОбластьПодвал.Параметры.Свидетельство = спПолучитьПодтверждающийДокументОбъектаПоВиду(Организация,Перечисления.ВидыДокументов.Свидетельство);
	КонецЕсли;
	
	ОбластьПодвал.Параметры.ТекстИндивидуальныйПредприниматель = ?(ВыводитьКодВидаТовара, "Индивидуальный предприниматель или иное уполномоченное лицо", "Индивидуальный предприниматель");
	
	ТабДокумент.Вывести(ОбластьПодвал);
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции

Функция ПечатьКорректировочныйСчетФактура2021(ТабДокумент) Экспорт
	
	ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	

	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	Если ПустаяСтрока(ФорматВыводаСуммы) Тогда
		ФорматВыводаСуммы = ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00";
	КонецЕсли;
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	ВыводитьКодТНВЭД      = обПраво("ВыводитьКодТНВЭД");
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ЕстьПрослеживаемыйТовар = Ложь;
	
	
	ВыборкаТабличнойЧасти = Документы.СчетФактураПолученный.ТоварыДляПечатиУКД(ЭтотОбъект.ДокументОснование);
	зфПерерасчетТаблицыТоваров(ВыборкаТабличнойЧасти,ЭтотОбъект,ВалютаПечатногоДокумента);

	// Проверим, что есть РНПТ в документе
	Для Каждого ТекущаяСтрока Из ВыборкаТабличнойЧасти Цикл
		Если ЗначениеЗаполнено(ТекущаяСтрока.ГТД) И ТекущаяСтрока.ГТД.РНПТ Тогда
			ЕстьПрослеживаемыйТовар = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Определим макет
	Если ЕстьПрослеживаемыйТовар Тогда
		Макет = ПолучитьОбщийМакет("КорректировочныйСчетФактура_02_04_2021_прослеж");
	Иначе
		Макет = ПолучитьОбщийМакет("КорректировочныйСчетФактура_02_04_2021");
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	//
	ОбластьМакета.Параметры.НомерИсправленияКорректировочного = ?(
						обЗначениеНеЗаполнено(НомерИсправления),"--",НомерИсправления);
	ОбластьМакета.Параметры.ДатаИсправленияКорректировочного  = ?(
						обЗначениеНеЗаполнено(НомерИсправления),"--",Формат(Дата,"ДЛФ=DD"));
	Если ДокументОснование.ХозОперация =
							Справочники.ХозОперации.КорректировкаПоступленияКорректировкаПоСогласованиюСторон
		ИЛИ ДокументОснование.ХозОперация =
							Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейКорректировкаПоСогласованиюСторон Тогда
							
		ОбластьМакета.Параметры.Номер = ?(обЗначениеНеЗаполнено(НомерИсправляемогоКорректировочногоДокумента),
										ВхДокНомер,НомерИсправляемогоКорректировочногоДокумента);
		ОбластьМакета.Параметры.Дата  = Формат(?
										(обЗначениеНеЗаполнено(ДатаИсправляемогоКорректировочногоДокумента),
										ВхДокДата, ДатаИсправляемогоКорректировочногоДокумента), "ДЛФ=DD");
		ОбластьМакета.Параметры.НомерИсправленияКорректировочного = "--";
		ОбластьМакета.Параметры.ДатаИсправленияКорректировочного = "--";
	Иначе
		ОбластьМакета.Параметры.Номер = ?(обЗначениеНеЗаполнено(НомерИсправляемогоКорректировочногоДокумента),
										ВхДокНомер,НомерИсправляемогоКорректировочногоДокумента);
		ОбластьМакета.Параметры.Дата  = Формат(?
										(обЗначениеНеЗаполнено(ДатаИсправляемогоКорректировочногоДокумента),
										ВхДокДата,ДатаИсправляемогоКорректировочногоДокумента),"ДЛФ=DD");
	КонецЕсли;
	ОбластьМакета.Параметры.НомерСчетаФактуры = ?(обЗначениеНеЗаполнено(НомерИсходногоДокумента),
												"--",НомерИсходногоДокумента);
	ОбластьМакета.Параметры.ДатаСчетаФактуры  = Формат(?(обЗначениеНеЗаполнено(ДатаИсходногоДокумента),
												"--",ДатаИсходногоДокумента),"ДЛФ=DD");
	ОбластьМакета.Параметры.НомерИсправления  = ?(обЗначениеНеЗаполнено(НомерИсправленияИсходногоДокумента),
												"--",НомерИсправленияИсходногоДокумента);
	ОбластьМакета.Параметры.ДатаИсправления   = Формат(?(обЗначениеНеЗаполнено(ДатаИсправленияИсходногоДокумента),
												"--",ДатаИсправленияИсходногоДокумента),"ДЛФ=DD");
	
	ОбластьМакета.Параметры.Покупатель               = ЭтотОбъект.Организация;
	ОбластьМакета.Параметры.ПредставлениеПокупатель = спПолучитьПредставление(ОбластьМакета.Параметры.Покупатель,
																			Новый Структура("Наименование",""));
	АдресПокупателя = спПолучитьПредставление(ЭтотОбъект.ПодразделениеКомпании,Новый Структура("АдресЮридический"," "));
 																		
	ОбластьМакета.Параметры.АдресПокупателя         =   ?(АдресПокупателя = "", "--", АдресПокупателя);

	ОбластьМакета.Параметры.ИННПокупателя = Организация.ИНН + "/" + 
											спПолучитьКППДляПечати(Организация, ПодразделениеКомпании);
	
	ОбластьМакета.Параметры.Поставщик               = Контрагент;
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик,
																			Новый Структура("Наименование",""));
	АдресПоставщика = спПолучитьПредставление(Контрагент,Новый Структура("АдресЮридический",""));																		
	ОбластьМакета.Параметры.АдресПоставщика         = ?(АдресПоставщика = "", "--", АдресПоставщика);
	ОбластьМакета.Параметры.ИННПоставщика           = ?(
										Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ОбособленноеПодразделение,
										Контрагент.ГоловнойКонтрагент.ИНН, Контрагент.ИНН) + "/" + Контрагент.КПП;
	
	ОбластьМакета.Параметры.Валюта = "Валюта: наименование, код " + 
		?(НЕ обЗначениеНеЗаполнено(ВалютаПечатногоДокумента.НаименованиеПолное),
		ВалютаПечатногоДокумента.НаименованиеПолное, ВалютаПечатногоДокумента.Наименование) 
		+ ", " + ВалютаПечатногоДокумента.Код;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьШапкаИдГосКонтракта = Макет.ПолучитьОбласть("ШапкаИдГосКонтракта");
	ОбластьШапкаИдГосКонтракта.Параметры.ИдентификаторГосударственногоКонтракта =
											ЭтотОбъект.ИдентификаторГосударственногоКонтракта;
	ТабДокумент.Вывести(ОбластьШапкаИдГосКонтракта);

	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("Итого");
	
	Если ЕстьПрослеживаемыйТовар Тогда
		СтруктураИтоговПоСтранице =
		Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение, КоличествоПрослежУвеличениеВсего,КоличествоПрослежУменьшениеВсего",0,0,0,0,0,0,0,0);
		СтруктураИтоговПоСтраницеИтого =
		Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение, КоличествоПрослежУвеличениеВсего, КоличествоПрослежУменьшениеВсего",0,0,0,0,0,0,0,0);
	ИНаче
		СтруктураИтоговПоСтранице =
		Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение",0,0,0,0,0,0);
		СтруктураИтоговПоСтраницеИтого =
		Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение",0,0,0,0,0,0);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьИтого  = Макет.ПолучитьОбласть("Итого");
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	мсвДопОбластиПодвала.Добавить(ОбластьИтого);
	
	Ном=1;
	СуммаВсегоБезНал = 0;                                                                                    
	ЭтоПрослеживаемыйТовар = Ложь;
	
	Для Каждого СтрокаТоваров Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.НомерСтроки = ?(СтрокаТоваров.НомерИсходнойСтроки = 0, "--", СтрокаТоваров.НомерИсходнойСтроки);
		ОбластьМакета.Параметры.НаименованиеНоменклатуры = спПолучитьНаименование(СтрокаТоваров.Номенклатура);
		
		Если ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Номенклатура") И 
			СтрокаТоваров.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
			
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодДо         = СтрокаТоваров.ЕдиницаИзмеренияПоДокументуПоступления.ЕдиницаПоКлассификатору.Код;
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмеренияДо = СтрокаТоваров.ЕдиницаИзмеренияПоДокументуПоступления;
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПосле         = СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмеренияПосле = СтрокаТоваров.ЕдиницаИзмерения;

			
			Попытка
				КодТНВЭД = СтрокаТоваров.Номенклатура.КодТНВЭД;
			Исключение
				КодТНВЭД = Неопределено;
			Конецпопытки;
			
			Если ЗначениеЗаполнено(КодТНВЭД) И ВыводитьКодТНВЭД Тогда
				ОбластьМакета.Параметры.КодВидаТовара = СокрЛП(КодТНВЭД);
			Иначе
				ОбластьМакета.Параметры.КодВидаТовара = "--";
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
			
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодДо          = "796";
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмеренияДо = "шт";
			ОбластьМакета.Параметры.КодВидаТовара = "--";
			
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПосле          = "796";
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмеренияПосле = "шт";
			ОбластьМакета.Параметры.КодВидаТовара = "--";

			
		Иначе
			
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодДо          = "--";
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмеренияДо = "--";
			ОбластьМакета.Параметры.КодВидаТовара = "--";
			ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПосле          = "--";
			ОбластьМакета.Параметры.НаименованиеЕдиницыИзмеренияПосле = "--";
			ОбластьМакета.Параметры.КодВидаТовара = "--";

			
		КонецЕсли;
		
		ОбластьМакета.Параметры.КоличествоПослеИзменения = Формат(СтрокаТоваров.Количество,
																ФорматВыводаКоличества);
		ОбластьМакета.Параметры.КоличествоДоИзменения    = Формат(СтрокаТоваров.КоличествоПоДокументуПоступления,
																ФорматВыводаКоличества);
			
		ОбластьМакета.Параметры.РазницаСНДСУвеличение       = Формат(?(СтрокаТоваров.СуммаВсегоРазница >0,
																	СтрокаТоваров.СуммаВсегоРазница,0),
																	ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаСНДСУменьшение       = Формат(?(СтрокаТоваров.СуммаВсегоРазница <0,
																	-СтрокаТоваров.СуммаВсегоРазница,0),
																	ФорматВыводаСуммы);

		ОбластьМакета.Параметры.РазницаНДСУвеличение    = Формат(?(СтрокаТоваров.СуммаНДСРазница >0,
																СтрокаТоваров.СуммаНДСРазница,0),
																ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаНДСУменьшение    = Формат(?(СтрокаТоваров.СуммаНДСРазница <0,
																-СтрокаТоваров.СуммаНДСРазница,0),
																ФорматВыводаСуммы);
		
		РазницаБезНДС = (СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС)-(СтрокаТоваров.СуммаВсегоПоДокументуПоступления - СтрокаТоваров.СуммаНДСПоДокументуПоступления);

		ОбластьМакета.Параметры.РазницаБезНДСУвеличение       = Формат(?(РазницаБезНДС >0,РазницаБезНДС,0),
																	ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РазницаБезНДСУменьшение       = Формат(?(РазницаБезНДС <0,-РазницаБезНДС,0),
																	ФорматВыводаСуммы);
		
																	
		Если НЕ СтрокаТоваров.КоличествоПоДокументуПоступления = 0 Тогда
			ОбластьМакета.Параметры.ЦенаДоИзменения    = Формат(
												(СтрокаТоваров.СуммаВсегоПоДокументуПоступления - СтрокаТоваров.СуммаНДСПоДокументуПоступления)
												/СтрокаТоваров.КоличествоПоДокументуПоступления,
												ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СуммаНДСДоИзменения     = Формат(СтрокаТоваров.СуммаНДСПоДокументуПоступления,
																ФорматВыводаСуммы);
		
			ОбластьМакета.Параметры.СтоимостьБезНДСДоИзменения    = Формат(
								(СтрокаТоваров.СуммаВсегоПоДокументуПоступления - СтрокаТоваров.СуммаНДСПоДокументуПоступления),ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СтоимостьСНДСДоИзменения    = Формат(СтрокаТоваров.СуммаВсегоПоДокументуПоступления, ФорматВыводаСуммы);
		  	ОбластьМакета.Параметры.СтавкаНДСДо = СтрокаТоваров.СтавкаНДС;

		Иначе
			ОбластьМакета.Параметры.ЦенаДоИзменения    = "--";
		    ОбластьМакета.Параметры.СтоимостьСНДСДоИзменения = "--";
			ОбластьМакета.Параметры.СтавкаНДСДо = "--";
			ОбластьМакета.Параметры.СуммаНДСДоИзменения = "--";
			ОбластьМакета.Параметры.СтоимостьБезНДСДоИзменения  = "--"

		КонецЕсли;
		
		Если Не СтрокаТоваров.Количество = 0 Тогда 
			
			ОбластьМакета.Параметры.ЦенаПослеИзменения = Формат((СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС)/СтрокаТоваров.Количество,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СуммаНДСПослеИзменения  = Формат(СтрокаТоваров.СуммаНДС,ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СтоимостьБезНДСПослеИзменения = Формат((СтрокаТоваров.СуммаВсего - СтрокаТоваров.СуммаНДС),ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СтоимостьСНДСПослеИзменения = Формат(СтрокаТоваров.СуммаВсего, ФорматВыводаСуммы);
			ОбластьМакета.Параметры.СтавкаНДСПосле = СтрокаТоваров.СтавкаНДС;
			ОбластьМакета.Параметры.ПредставлениеГТДДо	= спПолучитьНаименование(СтрокаТоваров.ГТД);
		    ОбластьМакета.Параметры.ПредставлениеСтраныДо	= СтрокаТоваров.ГТД.Страна.Наименование;
		    ОбластьМакета.Параметры.СтранаПроисхожденияКодДо = СтрокаТоваров.ГТД.Страна.Код;

			ОбластьМакета.Параметры.ПредставлениеГТДПосле	= спПолучитьНаименование(СтрокаТоваров.ГТД);
		    ОбластьМакета.Параметры.ПредставлениеСтраныПосле	= СтрокаТоваров.ГТД.Страна.Наименование;
		    ОбластьМакета.Параметры.СтранаПроисхожденияКодПосле = СтрокаТоваров.ГТД.Страна.Код;
								
		Иначе
			ОбластьМакета.Параметры.ЦенаПослеИзменения    = 0;
		    ОбластьМакета.Параметры.СтоимостьСНДСПослеИзменения = 0;
			ОбластьМакета.Параметры.СтавкаНДСПосле = 0;
			ОбластьМакета.Параметры.СуммаНДСПослеИзменения =0;
			ОбластьМакета.Параметры.СтоимостьБезНДСПослеИзменения  = 0;
			ОбластьМакета.Параметры.ПредставлениеГТДПосле	= "--";
		    ОбластьМакета.Параметры.ПредставлениеСтраныПосле	= "--";
		    ОбластьМакета.Параметры.СтранаПроисхожденияКодПосле = "--";
			ОбластьМакета.Параметры.ПредставлениеГТДДо	= "--";
		    ОбластьМакета.Параметры.ПредставлениеСтраныДо	= "--";
		    ОбластьМакета.Параметры.СтранаПроисхожденияКодДо = "--";

							
		КонецЕсли;
		
	 КоличествоПрослеж = 0;

	  Если ЕстьПрослеживаемыйТовар Тогда
			
			Если ЗначениеЗаполнено(СтрокаТоваров.ГТД) И СтрокаТоваров.ГТД.РНПТ Тогда
				Если ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодДо = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код;
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежДо = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Наименование;
					ОбластьМакета.Параметры.КоличествоПрослежДо = СтрокаТоваров.ГТДКоличествоДо;
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодПосле = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Код;
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежПосле = СтрокаТоваров.Номенклатура.БазоваяЕдиницаИзмерения.Наименование;
					ОбластьМакета.Параметры.КоличествоПрослежПосле =  СтрокаТоваров.ГТДКоличествоПосле;
					КоличествоПрослеж =СтрокаТоваров.ГТДКоличествоПосле -СтрокаТоваров.ГТДКоличествоДо ;
					ОбластьМакета.Параметры.КоличествоПрослежУвеличение = ?(КоличествоПрослеж > 0,  КоличествоПрослеж, 0);
					ОбластьМакета.Параметры.КоличествоПрослежУменьшение = ?(КоличествоПрослеж < 0,  - КоличествоПрослеж, 0);
					ЭтоПрослеживаемыйТовар = Истина;

				ИначеЕсли ТипЗнч(СтрокаТоваров.Номенклатура)=Тип("СправочникСсылка.Автомобили") Тогда
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодДо = "796";
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежДо = "шт";
					ОбластьМакета.Параметры.КоличествоПрослежДо = 1;
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодПосле = "796";
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежПосле = "шт";
					ОбластьМакета.Параметры.КоличествоПрослежПосле = 1;
							
				Иначе
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодДо = "--";
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежДо = "--";
					ОбластьМакета.Параметры.КоличествоПрослежДо = "--";
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодПосле = "--";
					ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежПосле = "--";
					ОбластьМакета.Параметры.КоличествоПрослежПосле = "--";
					ОбластьМакета.Параметры.КоличествоПрослежУвеличение = "--";
					ОбластьМакета.Параметры.КоличествоПрослежУменьшение = "--";
				КонецЕсли;
			Иначе
				ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодДо = "--";
				ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежДо = "--";
				ОбластьМакета.Параметры.КоличествоПрослежДо = "--";
				ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежКодПосле = "--";
				ОбластьМакета.Параметры.ЕдиницаИзмеренияПрослежПосле = "--";
				ОбластьМакета.Параметры.КоличествоПрослежПосле = "--";
				ОбластьМакета.Параметры.КоличествоПрослежУвеличение = "--";
				ОбластьМакета.Параметры.КоличествоПрослежУменьшение = "--";

			КонецЕсли;
		КонецЕсли;
		
		
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			Если ЕстьПрослеживаемыйТовар Тогда
				СтруктураИтоговПоСтранице =
				Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение, КоличествоПрослежУвеличениеВсего,КоличествоПрослежУменьшениеВсего",0,0,0,0,0,0,0,0);
			Иначе
				СтруктураИтоговПоСтранице
				= Новый Структура("РазницаБезНДСУвеличение,РазницаБезНДСУменьшение,РазницаНДСУменьшение,РазницаНДСУвеличение,РазницаСНДСУменьшение,РазницаСНДСУвеличение",0,0,0,0,0,0);
			КонецЕсли;

			НомерСтраницыПред = НомерСтраницы;
		КонецЕсли;
		
		//добавляем итоги
		СтруктураИтоговПоСтранице.РазницаБезНДСУвеличение =
					СтруктураИтоговПоСтранице.РазницаБезНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаБезНДСУвеличение);
		СтруктураИтоговПоСтранице.РазницаБезНДСУменьшение = 
					СтруктураИтоговПоСтранице.РазницаБезНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаБезНДСУменьшение);
		СтруктураИтоговПоСтранице.РазницаНДСУменьшение    =
					СтруктураИтоговПоСтранице.РазницаНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаНДСУменьшение);
		СтруктураИтоговПоСтранице.РазницаНДСУвеличение    = 
					СтруктураИтоговПоСтранице.РазницаНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаНДСУвеличение);
		СтруктураИтоговПоСтранице.РазницаСНДСУменьшение   = 
					СтруктураИтоговПоСтранице.РазницаСНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаСНДСУменьшение);
		СтруктураИтоговПоСтранице.РазницаСНДСУвеличение   = 
					СтруктураИтоговПоСтранице.РазницаСНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаСНДСУвеличение);
		
		// итого
		СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУвеличение = 
				СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаБезНДСУвеличение);
		СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУменьшение =
				СтруктураИтоговПоСтраницеИтого.РазницаБезНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаБезНДСУменьшение);
		СтруктураИтоговПоСтраницеИтого.РазницаНДСУменьшение    =
				СтруктураИтоговПоСтраницеИтого.РазницаНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаНДСУменьшение);
		СтруктураИтоговПоСтраницеИтого.РазницаНДСУвеличение    = 
				СтруктураИтоговПоСтраницеИтого.РазницаНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаНДСУвеличение);
		СтруктураИтоговПоСтраницеИтого.РазницаСНДСУменьшение   =
				СтруктураИтоговПоСтраницеИтого.РазницаСНДСУменьшение + Число(ОбластьМакета.Параметры.РазницаСНДСУменьшение);
		СтруктураИтоговПоСтраницеИтого.РазницаСНДСУвеличение   = 
				СтруктураИтоговПоСтраницеИтого.РазницаСНДСУвеличение + Число(ОбластьМакета.Параметры.РазницаСНДСУвеличение);
				
		Если ЭтоПрослеживаемыйТовар Тогда
			СтруктураИтоговПоСтранице.КоличествоПрослежУвеличениеВсего   = 
					СтруктураИтоговПоСтранице.КоличествоПрослежУвеличениеВсего + ?(КоличествоПрослеж > 0,  КоличествоПрослеж, 0);
					
			СтруктураИтоговПоСтранице.КоличествоПрослежУменьшениеВсего   = 
					СтруктураИтоговПоСтранице.КоличествоПрослежУменьшениеВсего + ?(КоличествоПрослеж < 0,  - КоличествоПрослеж, 0);

					
			СтруктураИтоговПоСтраницеИтого.КоличествоПрослежУвеличениеВсего   = 
					СтруктураИтоговПоСтраницеИтого.КоличествоПрослежУвеличениеВсего + ?(КоличествоПрослеж > 0,  КоличествоПрослеж, 0);
					
			СтруктураИтоговПоСтраницеИтого.КоличествоПрослежУменьшениеВсего   = 
					СтруктураИтоговПоСтраницеИтого.КоличествоПрослежУменьшениеВсего + ?(КоличествоПрослеж < 0,  - КоличествоПрослеж, 0);
													
		КонецЕсли;
				

		Ном = Ном + 1;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьМакетаИтогоПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;	
	
	ОбластьИтого.Параметры.Заполнить(СтруктураИтоговПоСтраницеИтого);
	ТабДокумент.Вывести(ОбластьИтого);
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	
	Если Контрагент.ФормаСобственности=Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ОбластьПодвал.Параметры.Заполнить(Руководитель);
		ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
		ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	КонецЕсли;
	
	Если Организация.ФормаСобственности=Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
		ОбластьПодвал.Параметры.ФИОПБОЮЛ=Руководитель.Руководитель;
		ОбластьПодвал.Параметры.Свидетельство = 
					спПолучитьПодтверждающийДокументОбъектаПоВиду(Организация,Перечисления.ВидыДокументов.Свидетельство);
	КонецЕсли;
		
	ТабДокумент.Вывести(ОбластьПодвал);
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "УКД"
// Возвращает сформированный табличный документ:
Функция ПечатьУКД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУКД(ЭтотОбъект, ТабДокумент);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьУКД

// Функция получения данных для УКД.
//
Функция ПолучитьДанныеДляПечатиУКД() Экспорт
	
	ДанныеОбъекта = Новый Структура();
	
	ТаблицаТоваров = ЭтотОбъект.ДокументОснование.ПолучитьОбъект().ПолучитьДанныеДляПечатиУКД().Товары;
	
	// Данные документа
	ДанныеОбъекта.Вставить("Дата"                                  , ЭтотОбъект.Дата);
	ДанныеОбъекта.Вставить("Номер"                                 , дкПолучитьНомерДляПечати(ЭтотОбъект));
	ДанныеОбъекта.Вставить("ХозОперация"                           , ЭтотОбъект.ХозОперация);
	ДанныеОбъекта.Вставить("ДокументОснование"                     , ЭтотОбъект.ДокументОснование);
	ДанныеОбъекта.Вставить("ВалютаДокумента"                       , ЭтотОбъект.ВалютаДокумента);
	ДанныеОбъекта.Вставить("Поставщик"                             , ЭтотОбъект.Контрагент);
	ДанныеОбъекта.Вставить("ПодразделениеКомпании"                 , ЭтотОбъект.ПодразделениеКомпании);
	ДанныеОбъекта.Вставить("ПлатежноРасчетныеДокументы"            , Неопределено);
	ДанныеОбъекта.Вставить("Покупатель"                            , ЭтотОбъект.Организация);
	ДанныеОбъекта.Вставить("Организация"                           , ЭтотОбъект.Организация);
	ДанныеОбъекта.Вставить("ДоговорВзаиморасчетов"                 , ?(ЗначениеЗаполнено(ЭтотОбъект.ДоговорВзаиморасчетов), 
																		ЭтотОбъект.ДоговорВзаиморасчетов, 
																		"--"));
	ДанныеОбъекта.Вставить("Товары"                                , ТаблицаТоваров);
	ДанныеОбъекта.Вставить("Статус"                                , 1);
	ДанныеОбъекта.Вставить("Исправление"                           , ЭтотОбъект.Исправление);
	ДанныеОбъекта.Вставить("НомерИсходногоДокумента"               , ?(ЗначениеЗаполнено(ЭтотОбъект.НомерИсходногоДокумента),
																		дкПолучитьНомерДляПечати(ЭтотОбъект,,"НомерИсходногоДокумента"),
																		"--"));
	ДанныеОбъекта.Вставить("ДатаИсходногоДокумента"   			   , ?(ЗначениеЗаполнено(ЭтотОбъект.ДатаИсходногоДокумента), 
																		ЭтотОбъект.ДатаИсходногоДокумента, 
																		"--"));
	ДанныеОбъекта.Вставить("НомерИсправленияИсходногоДокумента"	   , ?(ЗначениеЗаполнено(ЭтотОбъект.НомерИсправленияИсходногоДокумента), 
																		ЭтотОбъект.НомерИсправленияИсходногоДокумента, 
																		"--"));
	ДанныеОбъекта.Вставить("ДатаИсправленияИсходногоДокумента"     , ?(ЗначениеЗаполнено(ЭтотОбъект.ДатаИсправленияИсходногоДокумента), 
																		ЭтотОбъект.ДатаИсправленияИсходногоДокумента, 
																		"--"));
	ДанныеОбъекта.Вставить("Ссылка"                                , ЭтотОбъект.Ссылка);
	ДанныеОбъекта.Вставить("ИдентификаторГосударственногоКонтракта", ЭтотОбъект.ИдентификаторГосударственногоКонтракта);
	ДанныеОбъекта.Вставить("ДатаОтгрузки", Неопределено);
	
	// Свойства
	ДанныеОбъекта.Вставить("Руководитель"     		, дкОтветственноеЛицо(ЭтотОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.Руководитель));
	ДанныеОбъекта.Вставить("ГлавныйБухгалтер" 		, дкОтветственноеЛицо(ЭтотОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер));
	ДанныеОбъекта.Вставить("Менеджер" 				, дкОтветственноеЛицо(ЭтотОбъект.ДокументОснование, "Менеджер"));
	
	Возврат ДанныеОбъекта;
	
КонецФункции

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	ОснованиеОригинал=Основание;
	
	ВхДокНомерВрем=ВхДокНомер;
	ВхДокДатаВрем=ВхДокДата;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаПоступления") ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаПоступленияАвтомобилей") Тогда
		ЗаполнитьНомера(Основание);
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	// Для выписки получим строку, по которой будем вводить СФ
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Выписка") Тогда
		// заполнение таблицы приходов
		Состав = Основание.Состав;
		СписокРасходов = Новый ТаблицаЗначений;
		СписокРасходов.Колонки.Добавить("Контрагент",,"Контрагент",25);
		СписокРасходов.Колонки.Добавить("Договор",,"Договор",25);
		СписокРасходов.Колонки.Добавить("Сумма",,"Сумма аванса",12);
		СписокРасходов.Колонки.Добавить("СтавкаНДС",,"Ставка НДС",9);
		СписокРасходов.Колонки.Добавить("СуммаНДС",,"Сумма НДС",9);
		Для Каждого Строка Из Состав Цикл
			Если Строка.СуммаРасход > 0 Тогда
				СтрокаРасхода = СписокРасходов.Добавить();
				СтрокаРасхода.Контрагент = Строка.Контрагент;
				СтрокаРасхода.Договор    = Строка.ДоговорВзаиморасчетов;
				СтрокаРасхода.Сумма      = Строка.СуммаРасход;
				СтрокаРасхода.СуммаНДС   = Строка.СуммаНДС;
				СтрокаРасхода.СтавкаНДС  = Строка.СтавкаНДС;
			КонецЕсли; 
		КонецЦикла;  
		Если СписокРасходов.Количество() = 0 Тогда  
			#Если Клиент Тогда
				Предупреждение("Документ " + Основание + " не содержит авансов.");
			#КонецЕсли
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли; 
		Если СписокРасходов.Количество() > 1 Тогда  
			ВыбСтрока = СписокРасходов.ВыбратьСтроку("Выберите строку авансов для ввода СФ");
			Если ВыбСтрока = Неопределено Тогда
				дкОтменаВводаДокумента(ЭтотОбъект);
				Возврат;
			КонецЕсли;
		Иначе
			ВыбСтрока = СписокРасходов[0];
		КонецЕсли;
	КонецЕсли;
	
	// получим список уже введенных счетов-фактур
	Если ТипЗнч(ОснованиеОригинал) <> Тип("ДокументСсылка.Выписка") Тогда
		СчетФактура = Неопределено;
		МассивВидов = Новый Массив(1); 
		МассивВидов[0] = "СчетФактураПолученный";
		МассивПодчиненных = дкПолучитьМассивПодчиненных(ОснованиеОригинал, МассивВидов, Истина);
		Если НЕ обЗначениеНеЗаполнено(МассивПодчиненных) И МассивПодчиненных.Количество()>0 Тогда
			Если НЕ ТипЗнч(ОснованиеОригинал) = Тип("ДокументСсылка.Выписка") Тогда
				Если МассивПодчиненных[0].Ссылка<>Ссылка Тогда
					СчетФактура = МассивПодчиненных[0].Ссылка;
				КонецЕсли;
			Иначе
				Для ин = 0 По МассивПодчиненных.Количество()-1 Цикл
					СчетФактураТек = МассивПодчиненных[ин].Ссылка;
					Если ВыбСтрока.Контрагент = СчетФактураТек.Контрагент И ВыбСтрока.Договор = СчетФактураТек.ДоговорВзаиморасчетов И 
						ВыбСтрока.Сумма = СчетФактураТек.СуммаДокумента Тогда
						СчетФактура = СчетФактураТек;
						Прервать;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
		КонецЕсли;
		// откроем введенную ранее СФ, отменим ввод новой
		Если НЕ СчетФактура = Неопределено Тогда
			ФормаСФ = СчетФактура.ПолучитьФорму();
			Если ФормаСФ.Открыта() Тогда
				ФормаСФ.Активизировать();
			Иначе
				ФормаСФ.Открыть();
			КонецЕсли;
			дкОтменаВводаДокумента(ЭтотОбъект);
			Сообщить("На основании документа " + ОснованиеОригинал + " уже введен " + СчетФактура);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Основание<>Неопределено Тогда
		// Для выписки получим строку, по которой будем вводить СФ
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.Выписка") Тогда
			// заполним шапку СФ и одну строку
			Контрагент			  = ВыбСтрока.Контрагент;
			ДоговорВзаиморасчетов = ВыбСтрока.Договор; 
			ВалютаДокумента		  =	ВыбСтрока.Договор.ВалютаВзаиморасчетов;
			Если обЗначениеНеЗаполнено(Дата) Тогда
				НаДату = ТекущаяДата();
			Иначе
				НаДату = Дата;
			КонецЕсли; 
			СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
			КурсДокумента = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			СтрокаТоваров = Товары.Добавить();
			СтрокаТоваров.Номенклатура 		= Справочники.Номенклатура.Предоплата;
			СтрокаТоваров.Количество  		= 1;
			СтрокаТоваров.Сумма				= ВыбСтрока.Сумма;
			СтрокаТоваров.ЕдиницаИзмерения	= СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения;
			СтрокаТоваров.Коэффициент		= СтрокаТоваров.ЕдиницаИзмерения.Коэффициент;
			ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров,,);
			СтрокаТоваров.СтавкаНДС         = ВыбСтрока.СтавкаНДС;
			СтрокаТоваров.СуммаВсего		= ВыбСтрока.Сумма;
			ОбработкаРеквизита("Товары.СуммаВсего",СтрокаТоваров,,);
			СуммаДокумента    = Товары.Итог("СуммаВсего");
			СуммаНДСДокумента = Товары.Итог("СуммаНДС");
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетКомиссионера") ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетКомиссионераЗаАвтомобили") Тогда
			//Для отчета комиссионера и отчета комиссионера за автомобили только 1 строка с комиссионным вознаграждением
			Товары.Очистить();
			НоваяСтрока=Товары.Добавить();
			НоваяСтрока.Номенклатура=Справочники.Номенклатура.КомиссионноеВознаграждение;
			ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
			Если ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
				НоваяСтрока.СуммаВсего=Основание.Товары.Итог("Вознаграждение");
			Иначе
				НоваяСтрока.СуммаВсего=Основание.Товары.Итог("Вознаграждение")+Основание.Автомобили.Итог("Вознаграждение");
			КонецЕсли;
			ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			//При вводе на основании РКО добавляем только одну строку в ТЧ
			Товары.Очистить();
			СтрокаТоваров=Товары.Добавить();
			СтрокаТоваров.Номенклатура=Справочники.Номенклатура.Предоплата;
			СтрокаТоваров.Количество=1;
			СтрокаТоваров.ЕдиницаИзмерения=СтрокаТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения;
			СтрокаТоваров.Коэффициент=СтрокаТоваров.ЕдиницаИзмерения.Коэффициент;
			СтрокаТоваров.СтавкаНДС=Основание.СтавкаНДС;
			СтрокаТоваров.СуммаВсего=Основание.СуммаДокумента;
			ОбработкаРеквизита("Товары.СуммаВсего",СтрокаТоваров,,);
			СуммаДокумента    = Товары.Итог("СуммаВсего");
			СуммаНДСДокумента = Товары.Итог("СуммаНДС");
			СтрокаТоваров.СуммаНДС = Основание.СуммаНДС;
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ОтчетКомитенту") ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
			Товары.Очистить();
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Основание",Основание);
			
			Если ТипЗнч(Основание)=Тип("ДокументСсылка.ОтчетКомитенту") Тогда
				Запрос.Текст =
				"ВЫБРАТЬ
				|	ОтчетКомитентуТовары.Номенклатура,
				|	СУММА(ОтчетКомитентуТовары.Количество) КАК Количество,
				|	СУММА(ОтчетКомитентуТовары.СуммаВсего) КАК Сумма,
				|	ОтчетКомитентуТовары.СтавкаНДС,
				|	СУММА(ОтчетКомитентуТовары.СуммаВсего) КАК СуммаОсталось,
				|	СУММА(ОтчетКомитентуТовары.Количество) КАК КоличествоОсталось,
				|	СУММА(ОтчетКомитентуТовары.СуммаНДС) КАК СуммаНДС,
				|	СУММА(ОтчетКомитентуТовары.СуммаНДС) КАК СуммаНДСОсталось,
				|	ОтчетКомитентуТовары.ХарактеристикаНоменклатуры,
				|	ОтчетКомитентуТовары.ГТД
				|ИЗ
				|	Документ.ОтчетКомитенту.Товары КАК ОтчетКомитентуТовары
				|ГДЕ
				|	ОтчетКомитентуТовары.Ссылка = &Основание
				|
				|СГРУППИРОВАТЬ ПО
				|	ОтчетКомитентуТовары.Номенклатура,
				|	ОтчетКомитентуТовары.СтавкаНДС,
				|	ОтчетКомитентуТовары.ХарактеристикаНоменклатуры,
				|	ОтчетКомитентуТовары.ГТД";
				ТабТоваров = Запрос.Выполнить().Выгрузить();
				
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	РеализованныеТовары.Номенклатура,
				|	РеализованныеТовары.ХарактеристикаНоменклатуры,
				|	РеализованныеТовары.ГТД,
				|	СУММА(РеализованныеТовары.Количество) КАК Количество,
				|	СУММА(РеализованныеТовары.Количество) КАК КоличествоОсталось
				|ИЗ
				|	РегистрНакопления.РеализованныеТовары КАК РеализованныеТовары
				|ГДЕ
				|	РеализованныеТовары.Регистратор = &Основание
				|
				|СГРУППИРОВАТЬ ПО
				|	РеализованныеТовары.ГТД,
				|	РеализованныеТовары.ХарактеристикаНоменклатуры,
				|	РеализованныеТовары.Номенклатура";
				
				ТабГТД = Запрос.Выполнить().Выгрузить();
			Иначе
				Запрос.Текст =
				"ВЫБРАТЬ
				|	ВозвратОтПокупателяТовары.Номенклатура,
				|	СУММА(ВозвратОтПокупателяТовары.Количество) КАК Количество,
				|	СУММА(ВозвратОтПокупателяТовары.СуммаВсего) КАК Сумма,
				|	ВозвратОтПокупателяТовары.СтавкаНДС,
				|	СУММА(ВозвратОтПокупателяТовары.СуммаВсего) КАК СуммаОсталось,
				|	СУММА(ВозвратОтПокупателяТовары.Количество) КАК КоличествоОсталось,
				|	СУММА(ВозвратОтПокупателяТовары.СуммаНДС) КАК СуммаНДС,
				|	СУММА(ВозвратОтПокупателяТовары.СуммаНДС) КАК СуммаНДСОсталось,
				|	ВозвратОтПокупателяТовары.ХарактеристикаНоменклатуры,
				|	ВозвратОтПокупателяТовары.ГТД
				|ИЗ
				|	Документ.ВозвратОтПокупателя.Товары КАК ВозвратОтПокупателяТовары
				|ГДЕ
				|	ВозвратОтПокупателяТовары.Ссылка = &Основание
				|
				|СГРУППИРОВАТЬ ПО
				|	ВозвратОтПокупателяТовары.Номенклатура,
				|	ВозвратОтПокупателяТовары.СтавкаНДС,
				|	ВозвратОтПокупателяТовары.ХарактеристикаНоменклатуры,
				|	ВозвратОтПокупателяТовары.ГТД";
				ТабТоваров = Запрос.Выполнить().Выгрузить();
				
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ГТДПартийТоваровКомпании.Номенклатура,
				|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры,
				|	ГТДПартийТоваровКомпании.ГТД,
				|	СУММА(-ГТДПартийТоваровКомпании.Количество) КАК КоличествоОсталось,
				|	СУММА(-ГТДПартийТоваровКомпании.Количество) КАК Количество
				|ИЗ
				|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
				|ГДЕ
				|	ГТДПартийТоваровКомпании.Регистратор = &Основание
				|
				|СГРУППИРОВАТЬ ПО
				|	ГТДПартийТоваровКомпании.Номенклатура,
				|	ГТДПартийТоваровКомпании.ХарактеристикаНоменклатуры,
				|	ГТДПартийТоваровКомпании.ГТД";
				
				ТабГТД = Запрос.Выполнить().Выгрузить();
			КонецЕсли;
			Если НЕ обЗначениеНеЗаполнено(ТабТоваров) Тогда
				Для Каждого стрМассива Из ТабТоваров Цикл
					Если ТабГТД <> Неопределено Тогда
						Если стрМассива.КоличествоОсталось = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						Отбор = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",стрМассива.Номенклатура,стрМассива.ХарактеристикаНоменклатуры);
						МассивПоиска = ТабГТД.НайтиСтроки(Отбор);
						Для Каждого ТекСтрока Из МассивПоиска Цикл
							Если стрМассива.КоличествоОсталось = 0 Тогда
								Прервать;
							КонецЕсли;
							Если ТекСтрока.КоличествоОсталось = 0 Тогда
								Продолжить;
							КонецЕсли;
							СписываемКоличество = Мин(стрМассива.КоличествоОсталось,ТекСтрока.КоличествоОсталось);
							СписываемСумма      = ?(стрМассива.КоличествоОсталось = СписываемКоличество, стрМассива.СуммаОсталось, Окр((стрМассива.Сумма/стрМассива.Количество)*СписываемКоличество,2));
							СписываемСуммаНДС   = ?(стрМассива.КоличествоОсталось = СписываемКоличество, стрМассива.СуммаНДСОсталось, Окр((стрМассива.СуммаНДС/стрМассива.Количество)*СписываемКоличество,2));
							
							НоваяСтрока = ЭтотОбъект.Товары.Добавить();
							НоваяСтрока.Номенклатура = стрМассива.Номенклатура;
							ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
							
							НоваяСтрока.ХарактеристикаНоменклатуры = стрМассива.ХарактеристикаНоменклатуры;
							НоваяСтрока.Количество                 = СписываемКоличество;
							НоваяСтрока.ГТД                        = ТекСтрока.ГТД;
							НоваяСтрока.СтавкаНДС                  = стрМассива.СтавкаНДС;
							НоваяСтрока.СуммаВсего                 = СписываемСумма;
							ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
							
							НоваяСтрока.СуммаНДС                   = СписываемСуммаНДС;
							ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаНДС",НоваяСтрока);
							
							// уменьшим нераспределенное количество
							стрМассива.КоличествоОсталось = стрМассива.КоличествоОсталось - СписываемКоличество;
							ТекСтрока.КоличествоОсталось  = ТекСтрока.КоличествоОсталось - СписываемКоличество;
							стрМассива.СуммаОсталось      = стрМассива.СуммаОсталось - СписываемСумма;
							стрМассива.СуммаНДСОсталось   = стрМассива.СуммаНДСОсталось - СписываемСуммаНДС;
						КонецЦикла;
					КонецЕсли;
					
					// если осталось еще допишем без гтд
					Если стрМассива.КоличествоОсталось > 0 Тогда
						НоваяСтрока = ЭтотОбъект.Товары.Добавить();
						НоваяСтрока.Номенклатура = стрМассива.Номенклатура;
						ЭтотОбъект.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
						
						НоваяСтрока.ХарактеристикаНоменклатуры = стрМассива.ХарактеристикаНоменклатуры;
						НоваяСтрока.Количество                 = стрМассива.КоличествоОсталось;
						НоваяСтрока.ГТД                        = Справочники.ГТД.ПустаяСсылка();
						НоваяСтрока.СтавкаНДС                  = стрМассива.СтавкаНДС;
						НоваяСтрока.СуммаВсего                 = стрМассива.СуммаОсталось;
						ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
						
						НоваяСтрока.СуммаНДС                   = стрМассива.СуммаНДСОсталось;
						ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаНДС",НоваяСтрока);
						// если расхождения по сумма кинем их на последнюю строку
					ИначеЕсли стрМассива.СуммаОсталось <> 0 ИЛИ стрМассива.СуммаНДСОсталось <> 0 Тогда
						НоваяСтрока.СуммаВсего = НоваяСтрока.СуммаВсего + стрМассива.СуммаОсталось;
						СтараяСуммаНДС         = НоваяСтрока.СуммаНДС;
						ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
						
						НоваяСтрока.СуммаНДС   = СтараяСуммаНДС + стрМассива.СуммаНДСОсталось;
						ЭтотОбъект.ОбработкаРеквизита("Товары.СуммаНДС",НоваяСтрока);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			//Подсчет новой суммы документа назначения
			СуммаДокумента    = Товары.Итог("СуммаВсего");
			СуммаНДСДокумента = Товары.Итог("СуммаНДС");
		ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
			Товары.Очистить();
		Иначе
			Если Товары.Количество()>0 Тогда
				СуммаДокумента    = Товары.Итог("СуммаВсего");
				СуммаНДСДокумента = Товары.Итог("СуммаНДС");
			КонецЕсли; 
		КонецЕсли;
	
		Грузоотправитель=Контрагент;
	КонецЕсли;
	
	ИдентификаторГосударственногоКонтракта = ?(ЗначениеЗаполнено(ИдентификаторГосударственногоКонтракта),
		ИдентификаторГосударственногоКонтракта,
		ДоговорВзаиморасчетов.ИдентификаторГосударственногоКонтракта);
	
	ВхДокНомер=ВхДокНомерВрем;
	ВхДокДата=ВхДокДатаВрем;
	ЗаполнитьНомерИсходнойСтроки();

КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	СуммаНДСДокумента = Товары.Итог("СуммаНДС");
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);    
	
	//Если ЭтоНовый()и не отказ 
	Если (ТипЗНЧ(ДокументОснование) = тип("ДокументСсылка.ПоступлениеАвтомобилей") или ТипЗНЧ(ДокументОснование) = тип("ДокументСсылка.ПоступлениеТоваров"))  тогда 
		Об =   ДокументОснование.ПолучитьОбъект();
		Если Товары.найти(Справочники.СтавкиНДС.ОсновнаяСтавкаНДС,"СтавкаНДС")= Неопределено тогда    //без ндс
			//ДокументОснование.БТ_СтатусСЧФ = Строка("Не требуется"); 
			//ДокументОснование.БТ_НомерСЧФ = ВхДокНомер;     
			//ДокументОснование.БТ_ДатаСЧФ = Дата(1,1,1);
		иначе   //есть ставка 20%  
			//Об =   ДокументОснование.ПолучитьОбъект();
			Об.БТ_СтатусСЧФ= Строка("Проведен"); 
			Об.БТ_НомерСЧФ = ВхДокНомер;     
			Об.БТ_ДатаСЧФ = ВхДокДата;
		КонецЕсли;
		Об.БТ_СуммаНДС = Товары.Итог("СуммаНДС"); 
		Об.Записать();
	КонецЕсли;

	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли
// Модуль документа ИнвентаризацияОрдерногоСклада

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="СкладКомпании" Тогда
		Рез=Истина;
		Если Товары.Количество()>0 Тогда
			#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				Если Вопрос("Произвести пересчет расчетного количества номенклатуры ?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
					Для Каждого СтрокаТоваров Из Товары Цикл
						Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",СтрокаТоваров,ЭтаФорма) И Рез;
					КонецЦикла; 
				КонецЕсли; 
			Иначе
				Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",СтрокаТоваров,ЭтаФорма) И Рез;
			КонецЕсли; 
			#Иначе
			Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма) И Рез;
			#КонецЕсли
		КонецЕсли; 
		Возврат Рез;
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		// получим расчетное количество
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.КоличествоУчет" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// получим расчетное количество
		Рез=ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат ОбработкаРеквизита("РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	ИначеЕсли Имя="Товары.Ячейка" Тогда
		// получим расчетное количество
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		// получим расчетное количество
		Возврат ОбработкаРеквизита("ПолучитьРасчетноеКоличество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	// ПРОЧИЕ ОБРАБОТЧИКИ
	ИначеЕсли Имя="ПолучитьРасчетноеКоличество" Тогда
		// проверим
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда 
			Возврат Ложь; 
		КонецЕсли;
        СтруктураОтбора = Новый Структура("СкладКомпании,Номенклатура,ХарактеристикаНоменклатуры");
		СтруктураОтбора.СкладКомпании = СкладКомпании;
		СтруктураОтбора.Номенклатура  = ТекСтрока.Номенклатура;
		СтруктураОтбора.ХарактеристикаНоменклатуры=ТекСтрока.ХарактеристикаНоменклатуры;
		Если СкладКомпании.ВидСклада  = Перечисления.ВидыСкладов.Ячеистый Тогда
			СтруктураОтбора.Вставить("Ячейка", ТекСтрока.Ячейка);
		КонецЕсли; 
		Если НЕ СкладКомпании.УчетЕдиницИзмерения = Перечисления.ВидыУчетаЕдиницИзмерения.НеВедется Тогда
			СтруктураОтбора.Вставить("ЕдиницаИзмерения", ТекСтрока.ЕдиницаИзмерения);
		КонецЕсли;
		МоментВремени 	= ?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),МоментВремени());
		ТаблицаОстатков = РегистрыНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(МоментВремени,СтруктураОтбора,,"Количество");
		Если ТаблицаОстатков.Количество() > 0 Тогда
			КоличествоОстаток = ТаблицаОстатков.Итог("Количество");
			Если обЗначениеНеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
				КоличествоОстаток = КоличествоОстаток; 
			Иначе
				КоличествоОстаток = ?(ТекСтрока.ЕдиницаИзмерения.Коэффициент = 0, 0, КоличествоОстаток / ТекСтрока.ЕдиницаИзмерения.Коэффициент);
			КонецЕсли; 
			ТекСтрока.Количество = КоличествоОстаток;
		Иначе
			ТекСтрока.Количество = 0;
		КонецЕсли;
		// пересчет
		Рез = ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ТекСтрока.КоличествоУчет = 0 Тогда 
			ТекСтрока.КоличествоУчет=ТекСтрока.Количество; 
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.КоличествоУчет",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;

КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ИнвентаризацияОрдерногоСклада", "Инвентаризация ордерного склада", Истина);
	СписокХозОпераций.Добавить("ИнвентаризацияОрдерногоСкладаШин", "Инвентаризация ордерного склада шин", Ложь);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
//
// Параметры:
//  Режим - число, 0 - возвращает излишки, 1 - недостачи.
//
// Возвращаемое значение:
//  Возвращает результат запроса.
//
Функция ПолучитьРезультатЗапросаПоТоварам(Режим=0)
    ТекстЗапроса="
	|ВЫБРАТЬ
	|	ИнвентаризацияТовары.Номенклатура,
	|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры,
	|	ИнвентаризацияТовары.Ссылка КАК Партия,
	|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения";
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый Тогда
		ТекстЗапроса = ТекстЗапроса + ",
		|	ИнвентаризацияТовары.Ячейка КАК Ячейка";
	КонецЕсли; 
		
	ТекстЗапроса=ТекстЗапроса+",
	|	(ИнвентаризацияТовары.КоличествоУчет-ИнвентаризацияТовары.Количество)*ИнвентаризацияТовары.Коэффициент*"+?(Режим=1,"(-1)","1")+" КАК Количество
	|ИЗ
	|	Документ.ИнвентаризацияОрдерногоСклада.Товары КАК ИнвентаризацияТовары
    |ГДЕ
	|	ИнвентаризацияТовары.Ссылка=&Ссылка
	|	И (ИнвентаризацияТовары.КоличествоУчет-ИнвентаризацияТовары.Количество)*"+?(Режим=1,"(-1)","1")+">0
	|";

	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст=ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоТоварам()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	//ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый Тогда
		ОбязательныеТовары.Вставить("Ячейка",3);
	КонецЕсли; 

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
				
		Если НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			КонтролируемыеРеквизиты = Новый Структура();
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
		
		ЗаявкаНаХранениеШин=Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(ДокументОснование);
		Если ХозОперация = Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин Тогда
			Если ТипЗнч(ЗаявкаНаХранениеШин)<>Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" должен быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли;
		Иначе
			Если ТипЗнч(ЗаявкаНаХранениеШин)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
				дкДобавитьОшибку(Ошибки,Строка(ХозОперация)+" не может быть введен на основании документа """+Метаданные.Документы.ЗаявкаНаХранениеШин+""".");
				Результат=Ложь;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Проверяет корректность заполнения характеристики номенклатуры
//                                                                
// Параметры:
//  СтрокаТовары - строка табличной части документа - ссылка на строку.
//  Ошибки       - строка - содержит описание ошибки.
//
// Возвращаемое значение:
//  Булево 
//
Функция ПроверитьЗаполнениеХарактеристики(СтрокаТовары, Ошибки="") Экспорт
	УчетВедется = СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ  СтрокаТовары.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2;	  
	РучноеСписаниеХарактеристик = СтрокаТовары.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ
												  			  СтрокаТовары.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик.Пустая();
	Приход = СтрокаТовары.Количество<СтрокаТовары.КоличествоУчет;
	Расход = СтрокаТовары.Количество>СтрокаТовары.КоличествоУчет;	
	Если УчетВедется И (Приход Или (Расход И РучноеСписаниеХарактеристик)) И обЗначениеНеЗаполнено(СтрокаТовары.ХарактеристикаНоменклатуры)  Тогда
	 	Возврат Ложь;
	КонецЕсли;	
	Возврат Истина;
КонецФункции // ПроверитьЗаполнениеХарактеристики()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ИнвентаризацияОрдерногоСклада"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИнвентаризацияОрдерногоСклада(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ИнвентаризацияОрдерногоСклада");
	
	// Произведем преобразование макета в зависимости от типа ячеестого склада
	ЭтоЯчеистыйСклад = (СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Ячеистый);
	Если НЕ ЭтоЯчеистыйСклад Тогда
		ОбластьЯчейка				= Макет.Область("Ячейка");
		ОбластьТовар				= Макет.Область("Товар");
		ОбластьТовар.ШиринаКолонки	= ОбластьТовар.ШиринаКолонки + ОбластьЯчейка.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьЯчейка,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка										= дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка				= ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада      = спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	ОбластьМакета.Параметры.СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	НомерСтраницы		= 2;
	НомерСтраницыПред	= НомерСтраницы;
	
	СтруктураИтоговПоСтранице = Новый Структура();
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		СтруктураСтроки.Вставить("Ячейка", СтрокаТабличнойЧасти.Ячейка);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		КонецЕсли;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы	
	Если НомерСтраницы > 2 Тогда
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакетаИтогоПоСтранице, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьПодвал.Параметры.Заполнить(МОЛ);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьИнвентаризацияОрдерногоСклада()

// Формирует печатную форму "ИНВ3"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИНВ3(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ИНВ3");
	
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ДатаНачалаИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаНачалаИнвентаризации",ЭтотОбъект.Дата);
	ОбластьМакета.Параметры.ДатаОкончанияИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОкончанияИнвентаризации",ЭтотОбъект.Дата);
	
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	СтрокНаСтранице	= 24;
	СтрокШапки		= 5;
	СтрокПодвала	= 5;
	НомерСтраницы	= 2;
	Ном				= 1;
	
	ИтогКоличество		= 0;
	//ИтогСумма			= 0;
	ИтогКоличествоУчет	= 0;
	//ИтогСуммаУчет		= 0;
	
	КолвоСтрокПоСтранице	= 0;
	КолвоПоСтранице			= 0;
	//СуммаЛиста				= 0;
	
	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	ЗаголовокТаблицы.Параметры.Валюта = ВалютаДокумента;
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ВыборкаСтрокТовары = ЭтотОбъект.Товары;
	КоличествоСтрок = ВыборкаСтрокТовары.Количество();

	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// Определим, что показывать в колонке кода - код или артикул
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект).Имя;
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для Каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
	
		
		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница	= (СтрокШапки + Ном - 1) / СтрокНаСтранице;

		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда

			ОбластьИтоговПоСтранице                               = Макет.ПолучитьОбласть("ПодвалСтраницы");
			ОбластьИтоговПоСтранице.Параметры.ИтогоКоличество      = Формат(ИтогКоличество,ФорматВыводаКоличества);
			ОбластьИтоговПоСтранице.Параметры.ИтогоКоличествоУчет = Формат(ИтогКоличествоУчет,ФорматВыводаКоличества);

			ОбластьИтоговПоСтранице.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(КолвоСтрокПоСтранице, ,",,,,,,,,0");
			ОбластьИтоговПоСтранице.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(КолвоПоСтранице);
			ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
			Ном =1;

			ИтогКоличество = 0;
			ИтогКоличествоУчет  = 0;

			КолвоСтрокПоСтранице = 0;
			КолвоПоСтранице      = 0;

		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		ОбластьМакета.Параметры.КоличествоУчет				= Формат(СтрокаТовар.КоличествоУчет,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.Количество					= Формат(СтрокаТовар.Количество,ФорматВыводаКоличества);
		
		Характеристика										= Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(СтрокаТовар.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					= дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКолонкиКода,ЭтотОбъект);
		
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения.Код;
		ОбластьМакета.Параметры.Номер=СтрокаТовар.НомерСтроки;

		ТабДокумент.Вывести(ОбластьМакета);
		Ном = Ном + 1;

		ИтогКоличество     = ИтогКоличество     + СтрокаТовар.Количество;
		ИтогКоличествоУчет = ИтогКоличествоУчет + СтрокаТовар.КоличествоУчет;

		КолвоСтрокПоСтранице = КолвоСтрокПоСтранице + 1;
		КолвоПоСтранице      = КолвоПоСтранице      + СтрокаТовар.КоличествоУчет;

	КонецЦикла;

	// Выводим итоги по последней странице
	ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ПодвалСтраницы");
	ОбластьИтоговПоСтранице.Параметры.ИтогоКоличество     = Формат(ИтогКоличество,ФорматВыводаКоличества);
	ОбластьИтоговПоСтранице.Параметры.ИтогоКоличествоУчет = Формат(ИтогКоличествоУчет,ФорматВыводаКоличества);
	ОбластьИтоговПоСтранице.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(КолвоСтрокПоСтранице, ,",,,,,,,,0");
	ОбластьИтоговПоСтранице.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(КолвоПоСтранице);
	ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

	// Выводим подвал документа
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	ОбластьМакета = Макет.ПолучитьОбласть("ПодвалОписи");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.НачальныйНомерПоПорядку = 1;
	ОбластьМакета.Параметры.НомерКонца              = ВыборкаСтрокТовары.Количество();
	ОбластьМакета.Параметры.ОбщееКоличествоЕдиницФактическиНаСтраницеПрописью = ЧислоПрописью(ВыборкаСтрокТовары.Итог("КоличествоУчет"));
	ОбластьМакета.Параметры.КоличествоПорядковыхНомеровНаСтраницеПрописью     = ЧислоПрописью(ВыборкаСтрокТовары.Количество(), ,",,,,,,,,0");

	// Заполним информацию о руководителях и ответственных
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	
	ПредседательКомиссии= дкОтветственноеЛицо(ЭтотОбъект,"ПредседательКомиссии");
	ЧленКомиссии1		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии1");
	ЧленКомиссии2		= дкОтветственноеЛицо(ЭтотОбъект,"ЧленКомиссии2");
	ОбластьМакета.Параметры.Заполнить(ПредседательКомиссии);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии1);
	ОбластьМакета.Параметры.Заполнить(ЧленКомиссии2);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьИНВ3()

// Формирует печатную форму "ИНВ19"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИНВ19(ТабДокумент) Экспорт
	Макет = ПолучитьОбщийМакет("ИНВ19");
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ДатаНачалаИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаНачалаИнвентаризации",ЭтотОбъект.Дата);
	ОбластьМакета.Параметры.ДатаОкончанияИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОкончанияИнвентаризации",ЭтотОбъект.Дата);
	
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	СтрокНаСтранице = 27;
	СтрокШапки      = 5;
	СтрокПодвала    = 5;
	НомерСтраницы   = 2;
	Ном             = 1;
	Сч              = 1;
	ИтогоРезультатИзлишекКолво   = 0;
	ИтогоРезультатНедостачаКолво = 0;

	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	ВыборкаСтрокТовары = ЭтотОбъект.Товары;
	КоличествоСтрок  = ВыборкаСтрокТовары.Количество();
	
	Если КоличествоСтрок = 1 Тогда
		ПереноситьПоследнююСтроку = 0;
	Иначе
		ЦелыхСтраницСПодвалом     = Цел((СтрокШапки + КоличествоСтрок + СтрокПодвала) / СтрокНаСтранице);
		ЦелыхСтраницБезПодвала    = Цел((СтрокШапки + КоличествоСтрок - 1) / СтрокНаСтранице);
		ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
	КонецЕсли;

	// Определим, что показывать в колонке кода - код или артикул
	ИмяКодаСправочника = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,Права).Имя;
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
	// структура итогов по странице
	СтруктураИтогов = Новый Структура("ИтогоРезультатИзлишекКолво,ИтогоРезультатНедостачаКолво",0,0);
	Для Каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
	
		//Начинаем новую страницу, если предыдущая строка была последней на странице
		//или пора переносить последнюю строку на последнюю страницу с подвалом.
		ЦелаяСтраница = (СтрокШапки + Ном - 1) / СтрокНаСтранице;

		Если (ЦелаяСтраница = Цел(ЦелаяСтраница))
		 или ((ПереноситьПоследнююСтроку = 1) и (Ном = КоличествоСтрок)) Тогда

			ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоТаблицыИтогиПоСтранице");
			//ОбластьИтоговПоСтранице.Параметры.Заполнить(СтруктураИтогов);
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоговПоСтранице,СтруктураИтогов,Права);
			СтруктураИтогов = Новый Структура("ИтогоРезультатИзлишекКолво,ИтогоРезультатНедостачаКолво",0,0);

			//ТабДокумент.Вывести(ОбластьИтоговПоСтранице);

			НомерСтраницы = НомерСтраницы + 1;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
			Ном =1;

		КонецЕсли;

		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		Характеристика										= Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование			= спПолучитьНаименование(СтрокаТовар.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					= дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКодаСправочника,Права);
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения.Код;
		ОбластьМакета.Параметры.Номер = Сч;

		Разница     = 0;
		Разница     = СтрокаТовар.КоличествоУчет - СтрокаТовар.Количество;
		
		Если Разница = 0 Тогда
			Продолжить;
		КонецЕсли;

		Если Разница < 0 Тогда
			ОбластьМакета.Параметры.РезультатНедостачаКолво = Формат(-Разница,ФорматВыводаКоличества);
			ОбластьМакета.Параметры.РезультатИзлишекКолво   = 0;

			ИтогоРезультатНедостачаКолво = ИтогоРезультатНедостачаКолво + (-Разница);
			ИтогоРезультатИзлишекКолво   = ИтогоРезультатИзлишекКолво   + 0;
			
			СтруктураИтогов.ИтогоРезультатИзлишекКолво   = СтруктураИтогов.ИтогоРезультатИзлишекКолво + 0;
			СтруктураИтогов.ИтогоРезультатНедостачаКолво = СтруктураИтогов.ИтогоРезультатНедостачаКолво + (-Разница);

		Иначе
			
			ОбластьМакета.Параметры.РезультатНедостачаКолво = 0;
			ОбластьМакета.Параметры.РезультатИзлишекКолво   = Формат(Разница,ФорматВыводаКоличества);

			ИтогоРезультатНедостачаКолво = ИтогоРезультатНедостачаКолво + 0;
			ИтогоРезультатИзлишекКолво   = ИтогоРезультатИзлишекКолво   + Разница;
			
			СтруктураИтогов.ИтогоРезультатИзлишекКолво   = СтруктураИтогов.ИтогоРезультатИзлишекКолво + Разница;
			СтруктураИтогов.ИтогоРезультатНедостачаКолво = СтруктураИтогов.ИтогоРезультатНедостачаКолво + 0;
			
		КонецЕсли; 

		ТабДокумент.Вывести(ОбластьМакета);

		Ном = Ном + 1;
		Сч = Сч + 1;
	КонецЦикла;
	
	// выведем область итогов по странице
	Если НомерСтраницы > 2 Тогда
		ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоТаблицыИтогиПоСтранице");
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоговПоСтранице,СтруктураИтогов,Права);
		//ОбластьИтоговПоСтранице.Параметры.Заполнить(СтруктураИтогов);
		//ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
	КонецЕсли;
	
	ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоТаблицы");
	ОбластьИтоговПоСтранице.Параметры.ИтогоРезультатИзлишекКолво   = Формат(ИтогоРезультатИзлишекКолво,ФорматВыводаКоличества);
	ОбластьИтоговПоСтранице.Параметры.ИтогоРезультатНедостачаКолво = Формат(ИтогоРезультатНедостачаКолво,ФорматВыводаКоличества);
	ТабДокумент.Вывести(ОбластьИтоговПоСтранице);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	// Заполним информацию о руководителях и ответственных
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	
	ОбластьМакета.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	= ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Возврат ТабДокумент;
КонецФункции // ПечатьИНВ19()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если Основание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин") ИЛИ 
		 Основание.ХозОперация=Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин Тогда
		 ХозОперация=Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровОрдерныйСкладОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровОрдерныйСкладОстатки.Ячейка КАК Ячейка,
	|	ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) = 0
	|			ТОГДА ОстаткиТоваровОрдерныйСкладОстатки.КоличествоОстаток
	|		ИНАЧЕ ОстаткиТоваровОрдерныйСкладОстатки.КоличествоОстаток / ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) = 0
	|			ТОГДА ОстаткиТоваровОрдерныйСкладОстатки.КоличествоОстаток
	|		ИНАЧЕ ОстаткиТоваровОрдерныйСкладОстатки.КоличествоОстаток / ОстаткиТоваровОрдерныйСкладОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК КоличествоУчет
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(
	|			&Момент,
	|			СкладКомпании = &СкладКомпании
	|				И (НЕ Номенклатура.ВидНоменклатуры = &Услуга)
	|				И Номенклатура В (&СписокНоменклатуры)
	|				И ЗаявкаНаХранениеШин=&ЗаявкаНаХранениеШин
	|	) КАК ОстаткиТоваровОрдерныйСкладОстатки";
	Запрос.УстановитьПараметр("Момент",ТекущаяДата());
	Запрос.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
	Запрос.УстановитьПараметр("СкладКомпании",Основание.СкладКомпании);
	Если ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
		Запрос.УстановитьПараметр("СписокНоменклатуры",Основание.Шины.ВыгрузитьКолонку("Номенклатура"));
	Иначе
		Запрос.УстановитьПараметр("СписокНоменклатуры",Основание.Товары.ВыгрузитьКолонку("Номенклатура"));
	КонецЕсли;
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин Тогда
		ЗаявкаНаХранениеШин=Документы.ЗаявкаНаХранениеШин.ПолучитьЗаявкуНаХранениеШин(ДокументОснование);
		Если ТипЗнч(ЗаявкаНаХранениеШин)=Тип("ДокументСсылка.ЗаявкаНаХранениеШин") Тогда
			Запрос.УстановитьПараметр("ЗаявкаНаХранениеШин",ЗаявкаНаХранениеШин);
		Иначе
			Запрос.УстановитьПараметр("ЗаявкаНаХранениеШин",Документы.ЗаявкаНаХранениеШин.ПустаяСсылка());
		КонецЕсли;
	Иначе
		Запрос.УстановитьПараметр("ЗаявкаНаХранениеШин",Документы.ЗаявкаНаХранениеШин.ПустаяСсылка());
	КонецЕсли; 
	ТЧОснования = Запрос.Выполнить().Выгрузить();
	Товары.Загрузить(ТЧОснования);
	Если ХозОперация<>Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин Тогда
		// теперь посмотрим что унас добавилось а что нет
		Для Каждого ТекСтрока Из Основание.Товары Цикл
			МассСтрДок = Товары.НайтиСтроки(Новый Структура("Номенклатура",ТекСтрока.Номенклатура));
			Если МассСтрДок.Количество() = 0 Тогда
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = ТекСтрока.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
				НоваяСтрока.Количество = 0;
				НоваяСтрока.КоличествоУчет = ТекСтрока.КоличествоФакт;
				НоваяСтрока.ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = ТекСтрока.Коэффициент;
			Иначе
				МассСтрДок[0].КоличествоУчет = ТекСтрока.КоличествоФакт;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		БылиДвижения = Ложь;
		
		// ордерный склад
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ОстаткиТоваровОрдерныйСклад ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// на всякий случай проверим тип склада, его могли поменять в самом справочнике
	Если СкладКомпании.ВидСклада = Перечисления.ВидыСкладов.Обычный Тогда
		Отказ = Истина;
		РезультатОбработки = "Склад документа имеет вид """ + СкладКомпании.ВидСклада + """. Проведение возможно только для ""Ордерного"" или ""Ячеистого"" склада!";
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли; 
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// у нас не стандартная таблица, поэтому сформирует свой результат запроса по товарам
	РезультатЗапросаПоТоварамИзлишки   = ПолучитьРезультатЗапросаПоТоварам();
	РезультатЗапросаПоТоварамНедостачи = ПолучитьРезультатЗапросаПоТоварам(1);
	НаборЗаписейОстатки = Движения.ОстаткиТоваровОрдерныйСклад;
	// 1. Списываем недостачи
	НаборЗаписейОстатки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании  = СкладКомпании;
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин Тогда
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=ДокументОснование;	
	Иначе
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=Неопределено;	
	КонецЕсли; 
	НаборЗаписейОстатки.РежимПроведения = Режим;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамНедостачи;
	Отказ = НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Если Отказ Тогда
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // дальше смысла не имеет
	КонецЕсли;
	
	// 2. Приходуем излишки
	НаборЗаписейОстатки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании  = СкладКомпании;
	Если ХозОперация=Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин Тогда
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=ДокументОснование;	
	Иначе
		НаборЗаписейОстатки.ЗаявкаНаХранениеШин=Неопределено;	
	КонецЕсли; 
	НаборЗаписейОстатки.РежимПроведения = Режим;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварамИзлишки;
	Отказ = НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;

	ХранениеШинКомплектами=обПраво("ХранениеШинКомплектами",Права,,ЭтотОбъект);
	Если (НЕ Отказ) И (ХозОперация=Справочники.ХозОперации.ИнвентаризацияОрдерногоСкладаШин) И (ХранениеШинКомплектами)Тогда
		//Контроль остатка шин на хранении
		НаборЗаписейОстатки.Записать();
		ТаблицаОстатковТоваров=РегистрыНакопления.ОстаткиТоваровОрдерныйСклад.Остатки(
			Новый Граница(МоментВремени(),ВидГраницы.Включая),
			Новый Структура("ЗаявкаНаХранениеШин",ДокументОснование),
			"ЗаявкаНаХранениеШин,СкладКомпании,Номенклатура,Ячейка",
			"Количество"
		);
		ЗаголовокСообщения=Истина;
		СписокЗаявокНаХранениеШин=Новый СписокЗначений;
		ТаблицаОстатковПоЯчейкам=ТаблицаОстатковТоваров.Скопировать();
		ТаблицаОстатковПоЯчейкам.Свернуть("ЗаявкаНаХранениеШин,Ячейка","Количество");
		Для каждого СтрокаОстатков Из ТаблицаОстатковТоваров Цикл
			Если СписокЗаявокНаХранениеШин.НайтиПоЗначению(СтрокаОстатков.ЗаявкаНаХранениеШин)<>Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СписокЗаявокНаХранениеШин.Добавить(СтрокаОстатков.ЗаявкаНаХранениеШин);
			
			ЯчейкиЗаявкиНаХранениеШин=ТаблицаОстатковПоЯчейкам.НайтиСтроки(Новый Структура("ЗаявкаНаХранениеШин",СтрокаОстатков.ЗаявкаНаХранениеШин));
			Если ЯчейкиЗаявкиНаХранениеШин.Количество()>1 Тогда
				ЯчейкиЗаявкиНаХранениеШин=ТаблицаОстатковТоваров.НайтиСтроки(Новый Структура("ЗаявкаНаХранениеШин",СтрокаОстатков.ЗаявкаНаХранениеШин));
				Если ЗаголовокСообщения Тогда
					дкСообщитьРезультат("Хранение шин допускается только покомплектно.","Важное&Остатки товаров ордерного склада:",ЭтотОбъект,Неопределено);
					ЗаголовокСообщения=Ложь;
				КонецЕсли; 
				Для каждого СтрокаОстатковЗаявкиНаХранениеШин Из ЯчейкиЗаявкиНаХранениеШин Цикл
					СтрРезультат = ""+СтрокаОстатковЗаявкиНаХранениеШин.ЗаявкаНаХранениеШин+": склад """+СтрокаОстатковЗаявкиНаХранениеШин.СкладКомпании+""". Остаток";
					Если СтрокаОстатковЗаявкиНаХранениеШин.СкладКомпании.ВидСклада=Перечисления.ВидыСкладов.Ячеистый Тогда
						СтрРезультат=СтрРезультат+" в ячейке """+СокрЛП(СтрокаОстатковЗаявкиНаХранениеШин.Ячейка)+"""";
					КонецЕсли; 
					СтрРезультат=СтрРезультат+" в количестве "+Формат(СтрокаОстатковЗаявкиНаХранениеШин.Количество,"ЧДЦ=0; ЧН=0")+".";
					дкСообщитьРезультат(СтрРезультат,"Важное&Остатки товаров ордерного склада:",ЭтотОбъект,Неопределено);
				КонецЦикла; 
				Отказ=Истина;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	// двигаем границу последовательности ордерного склада
	СвойствоСкладКомпанииБыл = неопределено;
	ДополнительныеСвойства.Свойство("СкладКомпанииБыл",СвойствоСкладКомпанииБыл);
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(СвойствоСкладКомпанииБыл) Тогда
		НаборЗаписейГраницыОрдерныйСклад = РегистрыСведений.ГраницыОрдерныйСклад.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= СкладКомпании;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= Дата;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыОрдерныйСклад.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыОрдерныйСклад.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыОрдерныйСклад.ДокументСсылка	= Ссылка;
		НаборЗаписейГраницыОрдерныйСклад.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// Модуль документа ОтчетКомитентуЗаАвтомобили

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы автомобилей
	ОбязательныеАвтомобили=Новый Структура();
	ОбязательныеАвтомобили.Вставить("Автомобиль",3);
	ОбязательныеАвтомобили.Вставить("Количество",1);
	ОбязательныеАвтомобили.Вставить("ДокументПоступления",3);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Автомобили",ОбязательныеАвтомобили);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проверим договор документов поступления, он должен совпадать с договором шапки
	Для Каждого СтрокаАвтомобиль Из Автомобили Цикл
		Если НЕ обЗначениеНеЗаполнено(СтрокаАвтомобиль.ДокументПоступления) Тогда
			ДоговорДокументаПоступления = СтрокаАвтомобиль.ДокументПоступления.ДоговорВзаиморасчетов;
			Если ДоговорДокументаПоступления <> ДоговорВзаиморасчетов Тогда
				дкДобавитьОшибку(Ошибки,"Строка №"+СтрокаАвтомобиль.НомерСтроки+". Договор документов поступления """+СокрЛП(ДоговорДокументаПоступления.Наименование)+""" должен быть равен договору в шапке документа");
				Результат = Ложь;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению); 			
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыАвтомобили = Новый Структура();
				КонтролируемыеРеквизитыАвтомобили.Вставить("ДокументПоступления");
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("Автомобили",КонтролируемыеРеквизитыАвтомобили);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки,"Автомобили", "Автомобиль", Ложь) И Результат;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ОтчетКомитентуЗаАвтомобили","Отчет комитенту за автомобили",Истина);
	Возврат СписокХозОпераций;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	/////////// ПРИВАТ ////////////
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СуммаВознаграждения КАК СуммаВознаграждения
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Возвращает Истина - все хорошо, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	
	Отказ=Ложь;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложено", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		//Закроем взаиморасчеты с контрагентом
		Если (ШапкаДокумента.СуммаДокумента-ШапкаДокумента.СуммаВознаграждения)<>0 Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
			НаборЗаписейДоходыИРасходы.Расход = ШапкаДокумента.СуммаДокумента-ШапкаДокумента.СуммаВознаграждения;
			НаборЗаписейДоходыИРасходы.Приход();
		КонецЕсли; 
		//Закроем принятые и реализованные на комиссию автомобили
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(РеализованныеАвтомобили.СуммаУпр),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
		|ГДЕ
		|	РеализованныеАвтомобили.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.СуммаУпр<>0 Тогда
				НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
				НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
				НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
				НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
				НаборЗаписейДоходыИРасходы.ВУпрВалюте = Истина;
				НаборЗаписейДоходыИРасходы.Доход = Выборка.СуммаУпр;
				НаборЗаписейДоходыИРасходы.Приход();
			КонецЕсли; 
		КонецЕсли; 		 
		Возврат НЕ Отказ;
	КонецЕсли;
	
	////Спишем оборудование автомобиля
	////YABS: его вроде как уже списали при продаже
	//НаборЗаписейКомплектацияАвтомобилей=Движения.КомплектацияАвтомобилей;
	//Для каждого Автомобиль Из Автомобили Цикл
	//	//Спишем оборудование по документу
	//	НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
	//	НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
	//	НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
	//	НаборЗаписейКомплектацияАвтомобилей.Автомобиль=Автомобиль.Автомобиль;
	//	НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=МоментВремени();
	//	НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Номенклатура";
	//	Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
	//	//Спишем опции, установленное производителем
	//	НаборЗаписейКомплектацияАвтомобилей.РежимПроведения=Режим;
	//	НаборЗаписейКомплектацияАвтомобилей.ДокументОбъект=ЭтотОбъект;
	//	НаборЗаписейКомплектацияАвтомобилей.РезультатЗапросаПоТоварам=Неопределено;
	//	НаборЗаписейКомплектацияАвтомобилей.Автомобиль=Автомобиль.Автомобиль;
	//	НаборЗаписейКомплектацияАвтомобилей.ПериодДвижения=МоментВремени();
	//	НаборЗаписейКомплектацияАвтомобилей.ТипОборудования="Опции";
	//	Отказ=НЕ НаборЗаписейКомплектацияАвтомобилей.Расход() ИЛИ Отказ;
	//КонецЦикла;
	
	Если НЕ Отказ Тогда
		//доходы и расходы
		ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		Если обЗначениеНеЗаполнено(ШапкаДокумента.КурсВалютыУпр) Тогда
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр,ШапкаДокумента.Дата);
			КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		Иначе
			КурсУпр = ШапкаДокумента.КурсВалютыУпр;
		КонецЕсли;
		
		НаборЗаписейРеализованныеАвтомобили = Движения.РеализованныеАвтомобили;
		
		СуммаУпр = НаборЗаписейРеализованныеАвтомобили.Итог("СуммаУпр");
		СуммаДиР = Окр(обПересчет(ШапкаДокумента.СуммаДокумента, ШапкаДокумента.ВалютаДокумента, ШапкаДокумента.КурсДокумента, ВалютаУпр, КурсУпр)-СуммаУпр, 2);
		Если СуммаДиР<>0 Тогда
			НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДоходыИРасходы.Подразделение      = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьАвтомобилей;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Расход                 = СуммаДиР;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход();
		КонецЕсли;
	КонецЕсли;
	
	//Итог Вознаграждение
	Если ШапкаДокумента.СуммаВознаграждения<>0 Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Вознаграждение;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.Доход                  = ШапкаДокумента.СуммаВознаграждения;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;   
		
		//чья то доработка
		НаборЗаписейДоходыИРасходы.ДоходБезНДС=ШапкаДокумента.СуммаВознаграждения-ШапкаДокумента.Ссылка.Автомобили.Итог("СуммаНДСВознаграждение");		
		//чья то доработка
		
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Автомобили.Итог("СуммаВсего");
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаТЧ Из Автомобили Цикл
				НоваяЦена=обПересчет(СтрокаТЧ.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаТЧ.Цена Тогда
					СтрокаТЧ.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Автомобили.Цена",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;

	ИначеЕсли Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ОтчетКомитентуЗаАвтомобили Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомитентом);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);
		КонецЕсли; 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если (НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов)) И (НЕ ДоговорВзаиморасчетов.ПолучитьОбъект().СоответствуетХозОперации(ХозОперация)) Тогда
			ДоговорВзаиморасчетов	= Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
				Рез	= ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		КонецЕсли;		
		// проверим вид договора
		Если ДоговорВзаиморасчетов.Пустая() Тогда
		ИначеЕсли НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			Если ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.СКомитентом И
				 ДоговорВзаиморасчетов.ВидДоговора<>Перечисления.ВидыДоговоров.Прочее Тогда
				 #Если Клиент Тогда
				 Предупреждение("Необходимо выбирать договор комиссии !",10);
				 #КонецЕсли
				 ДоговорВзаиморасчетов=Неопределено;
				 ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
				 Возврат Ложь;
			КонецЕсли; 
		КонецЕсли;
		#Если Клиент Тогда
		Если (ЭтаФорма<>Неопределено) И
			 (НЕ ДоговорВзаиморасчетов.Пустая()) И
			 (ДоговорВзаиморасчетов.ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом) Тогда
			Если Автомобили.Количество()>0 Тогда
				Если Вопрос("Перерассчитать сумму комиссионного вознаграждения?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
					ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;		
					//Пройдемся по ТЧ и рассчитаем сумму комиссионного вознаграждения
					Для Каждого СтрокаТЧ Из Автомобили Цикл
						Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
							ОбработкаРеквизита("Автомобили.Сумма",СтрокаТЧ);	
						Иначе
							ОбработкаРеквизита("Автомобили.СуммаПродажи",СтрокаТЧ);	
						КонецЕсли;						
						ОбработкаРеквизита("РассчитатьВознаграждение",СтрокаТЧ);
					КонецЦикла;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		#КонецЕсли
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "АВТОМОБИЛИ"
	ИначеЕсли Имя="Автомобили.Автомобиль" Тогда
		
		Если дкПроверитьАвтомобильДляКомиссионныхДокументов(ЭтотОбъект.ХозОперация, ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		//Проставим количество
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
		// Заполним ставку НДС
		Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДС) Тогда
			ОсвобожденОтНДС = ЭтотОбъект.Контрагент.ОсвобожденОтНДС;
			Если ОсвобожденОтНДС Тогда
				ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				//ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;  
				ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;  //ЧалапАю БизТех 13.02.2025
			КонецЕсли;
		КонецЕсли;
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	РеализованныеАвтомобилиОстатки.ДокументПередачи КАК ДокументПередачи
		                      |ИЗ
		                      |	РегистрНакопления.РеализованныеАвтомобили.Остатки(&НаМомент,
		                      |		Автомобиль = &Автомобиль И 
		                      |		ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
							  |	) КАК РеализованныеАвтомобилиОстатки");
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Новый МоментВремени(КонецДня(Дата)),Новый Граница(Дата,ВидГраницы.Исключая)));
		Запрос.УстановитьПараметр("Автомобиль",ТекСтрока.Автомобиль);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекСтрока.ДокументПоступления=Выборка.ДокументПередачи;
		Иначе
			ТекСтрока.ДокументПоступления=Неопределено;
		КонецЕсли;
		Возврат ОбработкаРеквизита("Автомобили.ДокументПоступления",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Количество" ИЛИ Имя="Автомобили.Цена" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;		
		Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
			ТекСтрока.Сумма			= ТекСтрока.Цена*ТекСтрока.Количество;
		Иначе
			ТекСтрока.СуммаПродажи	= ТекСтрока.Цена*ТекСтрока.Количество;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС - Сумма всего равна сумме			
			Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
				ТекСтрока.СуммаНДС	= (ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка);
				ТекСтрока.СуммаВсего= ТекСтрока.Сумма;
			Иначе
				ТекСтрока.СуммаНДС	= (ТекСтрока.СуммаПродажи*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка);
				ТекСтрока.СуммаВсего= ТекСтрока.СуммаПродажи;				
			КонецЕсли;
		Иначе
			//НДС в цену не включен - Получим сумму НДС			
			Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
				ТекСтрока.СуммаНДС	= (ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/100;
				ТекСтрока.СуммаВсего= ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
			Иначе
				ТекСтрока.СуммаНДС	= (ТекСтрока.СуммаПродажи*ТекСтрока.СтавкаНДС.Ставка)/100;
				ТекСтрока.СуммаВсего= ТекСтрока.СуммаПродажи+ТекСтрока.СуммаНДС;				
			КонецЕсли;
		КонецЕсли;
		Возврат ОбработкаРеквизита("РассчитатьВознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Сумма" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;		
		Если ТекСтрока.Количество<>0 И ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
			ТекСтрока.Цена=ТекСтрока.Сумма/ТекСтрока.Количество;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаПродажи" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;		
		Если ТекСтрока.Количество<>0 И НЕ ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
			ТекСтрока.Цена=ТекСтрока.СуммаПродажи/ТекСтрока.Количество;
		КонецЕсли; 
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.СуммаВсего" Тогда
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
			ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
		Иначе
			ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
		КонецЕсли;		
		//Получим новую сумму НДС как процент от суммы всего
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
			ЦенаВключаетНДС=Истина;
		Иначе
			ЦенаВключаетНДС=ЭтотОбъект.ТипЦен.ЦенаВключаетНДС;
		КонецЕсли; 
		Если ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
				ТекСтрока.Сумма			= ТекСтрока.СуммаВсего;
				Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
			Иначе
				ТекСтрока.СуммаПродажи	= ТекСтрока.СуммаВсего;
				Возврат ОбработкаРеквизита("Автомобили.СуммаПродажи",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		Иначе
			//НДС в цену не включен
			Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
				ТекСтрока.Сумма=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
				Возврат ОбработкаРеквизита("Автомобили.Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
			Иначе
				ТекСтрока.СуммаПродажи=(100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка);
				Возврат ОбработкаРеквизита("Автомобили.СуммаПродажи",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		КонецЕсли; 		
		
	ИначеЕсли Имя="Автомобили.СтавкаНДС" Тогда
		Возврат ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Автомобили.Вознаграждение"
		ИЛИ Имя="Автомобили.СтавкаНДСВознаграждения" Тогда
		
		Ставка = ТекСтрока.СтавкаНДСВознаграждения.Ставка;	
		ТекСтрока.СуммаНДСВознаграждения = ТекСтрока.Вознаграждение * Ставка / (100 + Ставка);
		
	ИначеЕсли Имя="Автомобили.ДокументПоступления" Тогда
		Рез = Истина;		
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ДокументПоступления) Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	РеализованныеАвтомобилиОстатки.СуммаПродажиОстаток КАК СуммаПродажи,
			|	РеализованныеАвтомобилиОстатки.СуммаПродажиРеглОстаток КАК СуммаПродажиРегл,
			|	РеализованныеАвтомобилиОстатки.СуммаУпрОстаток КАК СуммаУпр,
			|	РеализованныеАвтомобилиОстатки.СуммаРеглОстаток КАК СуммаРегл
			|ИЗ
			|	РегистрНакопления.РеализованныеАвтомобили.Остатки(,
			|			ДокументПередачи = &ДокументПередачи
			|			И Автомобиль = &Автомобиль) КАК РеализованныеАвтомобилиОстатки";
			
			Запрос.УстановитьПараметр("ДокументПередачи", ТекСтрока.ДокументПоступления);
			Запрос.УстановитьПараметр("Автомобиль", ТекСтрока.Автомобиль);			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
			Иначе
				ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
			КонецЕсли;
			ВалютаУпр  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			
			Если Выборка.Следующий() Тогда
				Если Выборка.СуммаПродажи <> 0 Тогда
					// получим сумму для расчета
					Если ВалютаДокумента = ВалютаРегл Тогда
						ТекСтрока.Сумма			= Выборка.СуммаРегл;
						ТекСтрока.СуммаПродажи	= Выборка.СуммаПродажиРегл;												
					Иначе	
						ТекСтрока.Сумма			= обПересчет(Выборка.СуммаУпр,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
						ТекСтрока.СуммаПродажи	= обПересчет(Выборка.СуммаПродажи,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);												
					КонецЕсли;
					
					Если ТекущаяКомиссияОтчетНаОснованииПродаж Тогда
						СуммаРасчет	= ТекСтрока.Сумма;
					Иначе
						СуммаРасчет	= ТекСтрока.СуммаПродажи;
					КонецЕсли;					

					СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
					Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
						СуммаРасчет = Окр((СуммаРасчет*100)/(100+СтавкаНДС),2);
					КонецЕсли;
					ТекСтрока.Цена = Окр(СуммаРасчет,2);
				Иначе
					ТекСтрока.Цена = 0;
				КонецЕсли;   
				ТекСтрока.Вознаграждение = ТекСтрока.СуммаПродажи-ТекСтрока.Сумма; //ЧалапАю БизТех 13.02.2025 
				Рез=ОбработкаРеквизита("Автомобили.Цена",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли;

		КонецЕсли;	
		
	ИначеЕсли Имя="РассчитатьВознаграждение" Тогда
		//расчет вознаграждения по реализованному автомобилю
		Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда 
			Если не ДоговорВзаиморасчетов.ПроцентКомиссионногоВознаграждения = 0 тогда  //ЧалапАю БизТех 13.02.2025
				Если ДоговорВзаиморасчетов.ТипСуммыКомиссии=0 Тогда
					ТекСтрока.Вознаграждение=(ТекСтрока.СуммаВсего*ДоговорВзаиморасчетов.ПроцентКомиссионногоВознаграждения)/100;
				Иначе
					ТекСтрока.Вознаграждение=ДоговорВзаиморасчетов.СуммаКомиссионногоВознаграждения; 
					
					//KamikadZe begin add 26.05.2014 21:34:26
					Попытка
						ТекСтрока.Вознаграждение = (ТекСтрока.СуммаПродажи - ТекСтрока.Сумма) * обПолучитьЗначениеСвойства(ДоговорВзаиморасчетов,"ПроцентВознагражденияКомиссияТТ",100) / 100;
						ТекСтрока.СтавкаНДСВознаграждение = ?(Организация.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая,Справочники.СтавкиНДС.НеИспользоватьОсновнаяСтавкаНДС,Справочники.СтавкиНДС.БезНДС);
						ТекСтрока.СуммаНДСВознаграждение	= (ТекСтрока.Вознаграждение*ТекСтрока.СтавкаНДСВознаграждение.Ставка)/(100+ТекСтрока.СтавкаНДСВознаграждение.Ставка);
					Исключение
					КонецПопытки;
					//KamikadZe end add
					
				КонецЕсли; 
			иначе
				//<<ЧалапАю БизТех 13.02.2025
				ТекСтрока.Вознаграждение = (ТекСтрока.СуммаПродажи - ТекСтрока.Сумма);   
				ТекСтрока.СтавкаНДСВознаграждение = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
				ТекСтрока.СуммаНДСВознаграждение = (ТекСтрока.Вознаграждение*ТекСтрока.СтавкаНДСВознаграждение.Ставка)/(100+ТекСтрока.СтавкаНДСВознаграждение.Ставка);
				//ЧалапАю БизТех 13.02.2025>>
			КонецЕсли;
		Иначе  
			ТекСтрока.Вознаграждение=0;   
		КонецЕсли;
		
		// Заполним ставку НДС
		Если обЗначениеНеЗаполнено(ТекСтрока.СтавкаНДСВознаграждения) Тогда
			ОсвобожденОтНДС=ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
			Если ОсвобожденОтНДС Тогда
				ТекСтрока.СтавкаНДСВознаграждения = Справочники.СтавкиНДС.БезНДС;
			Иначе
				ТекСтрока.СтавкаНДСВознаграждения=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
		КонецЕсли;
		
		ОбработкаРеквизита("Автомобили.Вознаграждение",ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ОтчетКомитенту"
// Возвращает сформированный табличный документ:
Функция ПечатьОтчетКомитентуЗаАвтомобили(ТабДокумент) Экспорт
	Если НЕ ЭтотОбъект.Проведен Тогда
		Сообщить("Печать непроведенного документа невозможна.");
		Возврат Неопределено;
	КонецЕсли;

	Макет = ПолучитьМакет("ОтчетКомитентуЗаАвтомобили");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = орПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
    ОбластьМакета.Параметры.ПредставлениеКомитента		= спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.ПредставлениеКомиссионера	= спПолучитьПредставление(Организация);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ОтчетКомитентуЗаАвтомобилиАвтомобили.Автомобиль,
	|	ОтчетКомитентуЗаАвтомобилиАвтомобили.Автомобиль.VIN КАК VIN,
	|	1 КАК Количество,
	|	ЕСТЬNULL(Реализации.СуммаПоступления, 0) КАК СуммаПоступления,
	|	ЕСТЬNULL(Реализации.СуммаПродажи, 0) КАК СуммаВсего
	|ИЗ
	|	Документ.ОтчетКомитентуЗаАвтомобили.Автомобили КАК ОтчетКомитентуЗаАвтомобилиАвтомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РеализованныеАвтомобили.Автомобиль КАК Автомобиль,
	|			РеализованныеАвтомобили.СуммаУпр КАК СуммаПоступления,
	|			РеализованныеАвтомобили.СуммаПродажи КАК СуммаПродажи
	|		ИЗ
	|			РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
	|		ГДЕ
	|			РеализованныеАвтомобили.Регистратор = &Ссылка) КАК Реализации
	|		ПО ОтчетКомитентуЗаАвтомобилиАвтомобили.Автомобиль = Реализации.Автомобиль
	|ГДЕ
	|	ОтчетКомитентуЗаАвтомобилиАвтомобили.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	//перебор строк
	ВыборкаТабличнойЧасти = Запрос.Выполнить().Выгрузить();
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КурсВалУпр = ?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр),обКурсДляВалюты(ВалютаУпр,Дата).Курс,ЭтотОбъект.КурсВалютыУпр);
	Ном = 1; СуммаИтого = 0;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьМакета.Параметры.НомерСтроки = Ном;
		ОбластьМакета.Параметры.Автомобиль = СтрокаТабличнойЧасти.Автомобиль;
		ОбластьМакета.Параметры.АвтомобильНаименование = спПолучитьНаименование(СтрокаТабличнойЧасти.Автомобиль);
		ОбластьМакета.Параметры.VIN = СтрокаТабличнойЧасти.Автомобиль.VIN;
		СуммаПоступления = обПересчет(СтрокаТабличнойЧасти.СуммаПоступления,ВалютаУпр,КурсВалУпр,ВалютаДокумента,КурсДокумента);
		СуммаВсего = СтрокаТабличнойЧасти.СуммаВсего;
		ОбластьМакета.Параметры.СуммаПоступления = Формат(СуммаПоступления,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаВсего		 = Формат(СуммаВсего,ФорматВыводаСуммы);
		СуммаИтого = СуммаВсего + СуммаИтого;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		СтруктураСтрокиИтогов = Новый Структура("СуммаВсего",СуммаВсего);
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		Ном = Ном + 1;
	КонецЦикла;

	//довыводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаИтого,ФорматВыводаСуммы);
	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаИтого,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьМакета.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьМакета.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И (НЕ ТипЗнч(Основание) = Тип("СправочникСсылка.Контрагенты")) И (Основание.ХозОперация=Справочники.ХозОперации.РеализацияАвтомобилей) Тогда
		// В качестве контрагента должен выступать поставщик, а не покупатель из реализации
		Контрагент = обПраво("ОсновнойПоставщик",Права,,ЭтотОбъект);
		Если ЗначениеЗаполнено(Контрагент) Тогда
			ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент, Перечисления.ВидыДоговоров.СКомитентом, ЭтотОбъект,, Истина, Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	// при вводе на основании реализации автомобилей заполним документ передачи
	Если (НЕ обЗначениеНеЗаполнено(Основание)) И 
		 (Основание.ХозОперация=Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия ИЛИ
		  Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковАвтомобилейПринятыхНаКомиссию) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализованныеАвтомобилиОстатки.Автомобиль,
		|	РеализованныеАвтомобилиОстатки.ДокументПередачи,
		|	РеализованныеАвтомобилиОстатки.ГТД,
		|	РеализованныеАвтомобилиОстатки.КоличествоОстаток КАК Количество,
		|	РеализованныеАвтомобилиОстатки.СуммаУпрОстаток КАК СуммаУпр,
		|	РеализованныеАвтомобилиОстатки.СуммаРеглОстаток КАК СуммаРегл,		
		|	РеализованныеАвтомобилиОстатки.СуммаПродажиОстаток КАК СуммаПродажи,
		|	РеализованныеАвтомобилиОстатки.СуммаПродажиРеглОстаток КАК СуммаПродажиРегл
		|ИЗ
		|	РегистрНакопления.РеализованныеАвтомобили.Остатки(
		|			,
		|			Контрагент = &Контрагент
		|				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК РеализованныеАвтомобилиОстатки";
		
		Запрос.УстановитьПараметр("Контрагент"           ,Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ВалютаУпр	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		ВалютаРегл	= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();		
		Автомобили.Очистить();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Автомобили.Добавить();
			НоваяСтрока.Автомобиль = Выборка.Автомобиль;
			ОбработкаРеквизита("Автомобили.Автомобиль",НоваяСтрока);
			НоваяСтрока.Количество = Выборка.Количество;
			
			Если ВалютаДокумента = ВалютаУпр Тогда
				НоваяСтрока.Сумма		= Выборка.СуммаУпр;
			ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
				НоваяСтрока.Сумма		= Выборка.СуммаРегл;
			Иначе
				НоваяСтрока.Сумма		= обПересчет(Выборка.СуммаУпр,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);	
			КонецЕсли;				
			ОбработкаРеквизита("Автомобили.Сумма",НоваяСтрока);
				
			Если ВалютаДокумента = ВалютаУпр Тогда
				НоваяСтрока.СуммаПродажи = Выборка.СуммаПродажи;
			ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
				НоваяСтрока.СуммаПродажи = Выборка.СуммаПродажиРегл;
			Иначе
				НоваяСтрока.СуммаПродажи = обПересчет(Выборка.СуммаПродажи,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			ОбработкаРеквизита("Автомобили.СуммаПродажи",НоваяСтрока);
				
			Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
			Иначе
				ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
			КонецЕсли;				
			Если ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрока.Сумма>0 Тогда
				НоваяСтрока.СуммаВсего	= НоваяСтрока.Сумма;
				ОбработкаРеквизита("Автомобили.СуммаВсего",НоваяСтрока);
			ИначеЕсли НЕ ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрока.СуммаПродажи>0 Тогда
				НоваяСтрока.СуммаВсего	= НоваяСтрока.СуммаПродажи;
				ОбработкаРеквизита("Автомобили.СуммаВсего",НоваяСтрока);
			КонецЕсли;			
			
			НоваяСтрока.ДокументПоступления = Выборка.ДокументПередачи;
			//ОбработкаРеквизита("Автомобили.ДокументПоступления",НоваяСтрока);
		КонецЦикла;
	ИначеЕсли (НЕ обЗначениеНеЗаполнено(Основание)) И (Основание.ХозОперация=Справочники.ХозОперации.РеализацияАвтомобилей) Тогда
		Автомобили.Очистить();
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализованныеАвтомобили.Автомобиль,
		|	РеализованныеАвтомобили.ДокументПередачи,
		|	РеализованныеАвтомобили.Контрагент,
		|	РеализованныеАвтомобили.ДоговорВзаиморасчетов,
		|	РеализованныеАвтомобили.ГТД,
		|	СУММА(РеализованныеАвтомобили.СуммаУпр) КАК СуммаУпр,
		|	СУММА(РеализованныеАвтомобили.СуммаРегл) КАК СуммаРегл,		
		|	СУММА(РеализованныеАвтомобили.СуммаПродажи) КАК СуммаПродажи,
		|	СУММА(РеализованныеАвтомобили.СуммаПродажиРегл) КАК СуммаПродажиРегл,
		|	СУММА(РеализованныеАвтомобили.Количество) КАК Количество
		|ИЗ
		|	РегистрНакопления.РеализованныеАвтомобили КАК РеализованныеАвтомобили
		|ГДЕ
		|	РеализованныеАвтомобили.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализованныеАвтомобили.Автомобиль,
		|	РеализованныеАвтомобили.ДокументПередачи,
		|	РеализованныеАвтомобили.Контрагент,
		|	РеализованныеАвтомобили.ДоговорВзаиморасчетов,
		|	РеализованныеАвтомобили.ГТД";
		Запрос.УстановитьПараметр("Регистратор",Основание);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Контрагент = Выборка.Контрагент;
			ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
		КонецЕсли;
		ВалютаУпр	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		ВалютаРегл	= Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		Выборка.Сбросить();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Автомобили.Добавить();
			НоваяСтрока.Автомобиль = Выборка.Автомобиль;
			ОбработкаРеквизита("Автомобили.Автомобиль",НоваяСтрока);
			НоваяСтрока.Количество = Выборка.Количество;
			
			Если ВалютаДокумента = ВалютаУпр Тогда
				НоваяСтрока.Сумма		= Выборка.СуммаУпр;
			ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
				НоваяСтрока.Сумма		= Выборка.СуммаРегл;
			Иначе
				НоваяСтрока.Сумма		= обПересчет(Выборка.СуммаУпр,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);	
			КонецЕсли;				
			ОбработкаРеквизита("Автомобили.Сумма",НоваяСтрока);
			
			Если ВалютаДокумента = ВалютаУпр Тогда
				НоваяСтрока.СуммаПродажи	= Выборка.СуммаПродажи;
			ИначеЕсли ВалютаДокумента = ВалютаРегл Тогда
				НоваяСтрока.СуммаПродажи	= Выборка.СуммаПродажиРегл;
			Иначе
				НоваяСтрока.СуммаПродажи	= обПересчет(Выборка.СуммаПродажи,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
			КонецЕсли;
			ОбработкаРеквизита("Автомобили.СуммаПродажи",НоваяСтрока);			
						
			Если НЕ обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ТекущаяКомиссияОтчетНаОснованииПродаж	= ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж;
			Иначе
				ТекущаяКомиссияОтчетНаОснованииПродаж	= Истина;
			КонецЕсли;			
			
			Если ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрока.Сумма>0 Тогда
				НоваяСтрока.СуммаВсего	= НоваяСтрока.Сумма;
				ОбработкаРеквизита("Автомобили.СуммаВсего",НоваяСтрока);
			ИначеЕсли НЕ ТекущаяКомиссияОтчетНаОснованииПродаж И НоваяСтрока.СуммаПродажи>0 Тогда
				НоваяСтрока.СуммаВсего	= НоваяСтрока.СуммаПродажи;
				ОбработкаРеквизита("Автомобили.СуммаВсего",НоваяСтрока);
			КонецЕсли;			
			НоваяСтрока.ДокументПоступления = Выборка.ДокументПередачи;
			//ОбработкаРеквизита("Автомобили.ДокументПоступления",НоваяСтрока);
		КонецЦикла;
	КонецЕсли;
	//Пройдемся по ТЧ и рассчитаем сумму комиссионного вознаграждения
	Для Каждого СтрокаТЧ Из Автомобили Цикл
		ОбработкаРеквизита("РассчитатьВознаграждение",СтрокаТЧ);
	КонецЦикла;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;      
	
	//ЧалапАю БизТех 13.02.2025
	тзАвтомобили = Автомобили.Выгрузить();  
	Автомобили.Очистить();
	
	Для каждого Стр из тзАвтомобили цикл   
		ДанныеПродажи3Лицу = БТ_СлужебныеФункции.НайдемДатуПродажи(Стр.Автомобиль,Стр.ДокументПоступления.Дата);      
		СуммаПоступленияКомисии = Стр.ДокументПоступления.Автомобили.Найти(Стр.Автомобиль, "Автомобиль").СуммаВсего;
		если не ДанныеПродажи3Лицу = Неопределено тогда
			ДатаПродажи = ДанныеПродажи3Лицу.Дата;
			СуммаПродажи3Лицу = ДанныеПродажи3Лицу.СуммаПродажи3Лицу;
			НайденныйОтчетКомитента = БТ_СлужебныеФункции.НайтиОтчетКомитента(Стр.Автомобиль, Стр.ДокументПоступления.Дата);
			Если НайденныйОтчетКомитента = Неопределено тогда     //если не было еще отчетов комитенту
				НоваяСтрока = Автомобили.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,Стр);  
				НоваяСтрока.Сумма = СуммаПоступленияКомисии;
				НоваяСтрока.СуммаВсего = СуммаПродажи3Лицу;
				НоваяСтрока.Цена = СуммаПродажи3Лицу;  
				НоваяСтрока.Вознаграждение = СуммаПродажи3Лицу - СуммаПоступленияКомисии;
				НоваяСтрока.СуммаНДС = окр(НоваяСтрока.Вознаграждение*НоваяСтрока.СтавкаНДС.Ставка/(100+НоваяСтрока.СтавкаНДС.Ставка),2);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//ЧалапАю БизТех 13.02.2025>>

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И НЕ ЭтоНовый() И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		// прошлый момент времени
		Запрос = Новый Запрос("ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка");
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
	КонецЕсли;
	
	СуммаВознаграждения=Автомобили.Итог("Вознаграждение");
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// проведем взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
	НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
	НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Ложь;
	НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента-СуммаВознаграждения;
	НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
	Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
	// доходы и расходы по суммовым разницам
	СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли; 
		
		//чья то доработка
		ДоходБезНДС = СуммаВознаграждения - Автомобили.Итог("СуммаНДСВознаграждение");
		НаборЗаписейДиР.ДоходБЕЗНДС                  = ДоходБезНДС;
		//чья то доработка
		
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	//Спишем автомобили с регистра проданных комиссионных автомобилей
	НаборЗаписейРеализованныеАвтомобили=Движения.РеализованныеАвтомобили;
	НаборЗаписейРеализованныеАвтомобили.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейРеализованныеАвтомобили.Контрагент=Контрагент;
	НаборЗаписейРеализованныеАвтомобили.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
	Отказ=НЕ НаборЗаписейРеализованныеАвтомобили.Расход() ИЛИ Отказ;
	
	// партии
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

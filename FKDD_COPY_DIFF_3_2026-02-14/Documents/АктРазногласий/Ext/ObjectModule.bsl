// Модуль документа АктРазногласий

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
Перем ОбработкаРасчетСкидокАвторабот Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ФорматПредставленияГодаВыпускаАвтомобиля Экспорт; //Переменная с форматом отображения года выпуска автомобиля

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа перед изменением
Перем ТекущийКурсДокумента Экспорт; //Текущий курс документа перед изменением
Перем СуммаРазногласий; //Текущая сумма разногласий

Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ОтменитьОткрытиеФормы Экспорт; // Флаг отмены открытия формы

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	ВремСуммаНоменклатурыДокумента=Товары.Итог("СуммаВсего");
	ВремСуммаРаботДокумента=Работы.Итог("СуммаВсего");
	ВремСуммаДокумента=ВремСуммаНоменклатурыДокумента+ВремСуммаРаботДокумента;
	Если СуммаНоменклатурыДокумента<>ВремСуммаНоменклатурыДокумента Тогда
		СуммаНоменклатурыДокумента=ВремСуммаНоменклатурыДокумента;
	КонецЕсли; 
	Если СуммаРаботДокумента<>ВремСуммаРаботДокумента Тогда
		СуммаРаботДокумента=ВремСуммаРаботДокумента;
	КонецЕсли; 
	Если СуммаДокумента<>ВремСуммаДокумента Тогда
		СуммаДокумента=ВремСуммаДокумента;
	КонецЕсли;
	Возврат СуммаДокумента;
КонецФункции

// возвращает выборку по шапке
// ДокументСсылка - Ссылка на документ для которого получаем шапку
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ТипЦенРабот КАК ТипЦенРабот,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.ДокументОснование КАК ЗаказНаряд,
	|	Док.ВидРемонта КАК ВидРемонта,
	|	Док.Автомобиль КАК Автомобиль,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СуммаНоменклатурыДокумента КАК СуммаНоменклатурыДокумента,
	|	Док.СуммаРаботДокумента КАК СуммаРаботДокумента,
	|	&СуммаРазногласий КАК СуммаДокумента,
	|	Док.ДокументОснование КАК ДокументОснование
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("СуммаРазногласий",Товары.Итог("СуммаВсегоРазница")+Работы.Итог("СуммаВсегоРазница"));
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

// формирует движения документа по партионным регистрам
// Режим - режим проведения (оперативный/неоперативный)
// ДокументСсылка - ссылка на документ который надо допровести по партиям
// Возвращает Истина - все нормально, ложь - чего-то не так
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	ВедетсяБалансПоПодразделению=(Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
	НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
	Если ВедетсяБалансПоПодразделению Тогда
		НаборЗаписейДоходыИРасходы.Подразделение=ДоговорВзаиморасчетов.Подразделение;
	КонецЕсли;
	НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
	НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте = Ложь;
	НаборЗаписейДоходыИРасходы.Доход = СуммаРазногласий;
	НаборЗаписейДоходыИРасходы.Приход();
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;

	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса=обКурсДляВалюты(ВалютаРегл,МоментВремени());
	КурсРегл=СтруктураКурса.Курс/?(СтруктураКурса.Кратность=0,1,СтруктураКурса.Кратность);
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	ПартияТоваровОтрицательныхОстатков=Константы.ПартияТоваровОтрицательныхОстатков.Получить();
	ПартияТоваровОтрицательныхОстатковСтатусПартии=?(ПартияТоваровОтрицательныхОстатков.ХозОперация=Справочники.ХозОперации.ПоступлениеТоваровКомиссия,Перечисления.СтатусыПартий.ТоварПринятыйКомиссия,Перечисления.СтатусыПартий.ТоварКупленный);
	ПартияТоваровОтрицательныхОстатковКонтрагент=ПартияТоваровОтрицательныхОстатков.Контрагент;
	
	НаборЗаписейПродажи=Движения.Продажи;
	
	//Проведем приход/сторнирование деталей по регистру продаж
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	Продажи.ПодразделениеКомпании,
	             |	Продажи.Номенклатура,
	             |	Продажи.ДокументПродажи,
	             |	Продажи.Поставщик,
	             |	Продажи.Покупатель,
	             |	Продажи.СтатусПартии,
	             |	Продажи.ХозОперация,
	             |	Продажи.ДоговорВзаиморасчетов,
	             |	Продажи.ХарактеристикаНоменклатуры,
	             |	Продажи.СкладКомпании,
	             |	Продажи.Авторабота,
	             |	Продажи.СтавкаНДС,
	             |	Продажи.Партия,
	             |	Продажи.ГТД,
	             |	Продажи.Количество,
	             |	Продажи.Сумма,
	             |	Продажи.СуммаНДС,
	             |	Продажи.СуммаСкидки,
	             |	Продажи.СуммаУпр,
	             |	Продажи.СебестоимостьУпр,
	             |	Продажи.Себестоимость,
	             |	Продажи.СуммаНДСВходящий
	             |ИЗ
	             |	РегистрНакопления.Продажи КАК Продажи
	             |ГДЕ
	             |	Продажи.Регистратор = &ЗаказНаряд
	             |	И Продажи.Авторабота = &АвтоработаПустая
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	Продажи.НомерСтроки";
	Запрос.УстановитьПараметр("ЗаказНаряд",ДокументОснование);
	Запрос.УстановитьПараметр("АвтоработаПустая",Справочники.Автоработы.ПустаяСсылка());
	тзПродажи=Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЧ Из Товары Цикл
		Количество=СтрокаТЧ.КоличествоРазница;
		СуммаРегл=обПересчет(СтрокаТЧ.СуммаВсегоРазница,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
		СуммаУпр=обПересчет(СтрокаТЧ.СуммаВсегоРазница,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсВалютыУпр);
		СуммаНДС=обПересчет(СтрокаТЧ.СуммаНДСРазница,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
		СуммаСкидки=обПересчет(СтрокаТЧ.СуммаСкидкиРазница,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
		Если Количество=0 И СуммаРегл=0 И СуммаУпр=0 И СуммаНДС=0 И СуммаСкидки=0 Тогда
			Продолжить;
		КонецЕсли; 
		Если Количество>=0 Тогда
			//Если количество деталей увеличено - приход по партии отрицательных остатков
			НоваяЗаписьПродажи=НаборЗаписейПродажи.Добавить();
			НоваяЗаписьПродажи.Период=ШапкаДокумента.Дата;
			НоваяЗаписьПродажи.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗаписьПродажи.ПодразделениеКомпании=ШапкаДокумента.ПодразделениеКомпании;
			НоваяЗаписьПродажи.Номенклатура=СтрокаТЧ.Номенклатура;
			//НоваяЗаписьПродажи.ДокументПродажи=?(Количество=0,ШапкаДокумента.ЗаказНаряд,ШапкаДокумента.Ссылка);
			НоваяЗаписьПродажи.ДокументПродажи=ШапкаДокумента.ЗаказНаряд;
			НоваяЗаписьПродажи.Поставщик=ПартияТоваровОтрицательныхОстатковКонтрагент;
			НоваяЗаписьПродажи.Покупатель=ШапкаДокумента.Контрагент;
			НоваяЗаписьПродажи.СтатусПартии=ПартияТоваровОтрицательныхОстатковСтатусПартии;
			НоваяЗаписьПродажи.ХозОперация=ШапкаДокумента.ХозОперация;
			НоваяЗаписьПродажи.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
			НоваяЗаписьПродажи.ХарактеристикаНоменклатуры=СтрокаТЧ.ХарактеристикаНоменклатуры;
			НоваяЗаписьПродажи.СкладКомпании=ДокументОснование.Цех;
			НоваяЗаписьПродажи.СтавкаНДС=СтрокаТЧ.СтавкаНДС;
			НоваяЗаписьПродажи.Партия=ПартияТоваровОтрицательныхОстатков;
			НоваяЗаписьПродажи.Количество=Количество;
			НоваяЗаписьПродажи.Сумма=СуммаРегл;
			НоваяЗаписьПродажи.СуммаНДС=СуммаНДС;
			НоваяЗаписьПродажи.СуммаСкидки=СуммаСкидки;
			НоваяЗаписьПродажи.СуммаУпр=СуммаУпр;
			НоваяЗаписьПродажи.СебестоимостьУпр=0;
			НоваяЗаписьПродажи.Себестоимость=0;
			НоваяЗаписьПродажи.СуммаНДСВходящий=0;
		Иначе
			//При уменьшении количества деталей - сторнируем те продажи, 
			//которые были проведены заказ-нарядом
			Количество=-Количество;
			СуммаРегл=-СуммаРегл;
			СуммаУпр=-СуммаУпр;
			СуммаНДС=-СуммаНДС;
			СуммаСкидки=-СуммаСкидки;
			СтрокиПродаж=тзПродажи.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,СтавкаНДС",СтрокаТЧ.Номенклатура,СтрокаТЧ.ХарактеристикаНоменклатуры,СтрокаТЧ.СтавкаНДСПоЗаказНаряду));
			НоваяЗаписьПродажи=Неопределено;
			Для каждого СтрокаПродаж Из СтрокиПродаж Цикл
				Если Количество<=0 Тогда Прервать; КонецЕсли;
				НоваяЗаписьПродажи=НаборЗаписейПродажи.Добавить();
				НоваяЗаписьПродажи.Период=ШапкаДокумента.Дата;
				НоваяЗаписьПродажи.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗаписьПродажи.ПодразделениеКомпании=СтрокаПродаж.ПодразделениеКомпании;
				НоваяЗаписьПродажи.Номенклатура=СтрокаПродаж.Номенклатура;
				НоваяЗаписьПродажи.ДокументПродажи=СтрокаПродаж.ДокументПродажи;
				НоваяЗаписьПродажи.Поставщик=СтрокаПродаж.Поставщик;
				НоваяЗаписьПродажи.Покупатель=СтрокаПродаж.Покупатель;
				НоваяЗаписьПродажи.СтатусПартии=СтрокаПродаж.СтатусПартии;
				НоваяЗаписьПродажи.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗаписьПродажи.ДоговорВзаиморасчетов=СтрокаПродаж.ДоговорВзаиморасчетов;
				НоваяЗаписьПродажи.ХарактеристикаНоменклатуры=СтрокаПродаж.ХарактеристикаНоменклатуры;
				НоваяЗаписьПродажи.СкладКомпании=СтрокаПродаж.СкладКомпании;
				НоваяЗаписьПродажи.СтавкаНДС=СтрокаПродаж.СтавкаНДС;
				НоваяЗаписьПродажи.Партия=СтрокаПродаж.Партия;
				НоваяЗаписьПродажи.ГТД=СтрокаПродаж.ГТД;
				НоваяЗаписьПродажи.Количество=-Мин(СтрокаПродаж.Количество,Количество);
				НоваяЗаписьПродажи.Сумма=-Мин(СтрокаПродаж.Сумма,СуммаРегл);
				НоваяЗаписьПродажи.СуммаНДС=-Мин(СтрокаПродаж.СуммаНДС,СуммаНДС);
				НоваяЗаписьПродажи.СуммаСкидки=-Мин(СтрокаПродаж.СуммаСкидки,СуммаСкидки);
				НоваяЗаписьПродажи.СуммаУпр=-Мин(СтрокаПродаж.СуммаУпр,СуммаУпр);
				Количество=Количество-(-НоваяЗаписьПродажи.Количество);
				СуммаРегл=СуммаРегл-(-НоваяЗаписьПродажи.Сумма);
				СуммаУпр=СуммаУпр-(-НоваяЗаписьПродажи.СуммаУпр);
				СуммаНДС=СуммаНДС-(-НоваяЗаписьПродажи.СуммаНДС);
				СуммаСкидки=СуммаСкидки-(-НоваяЗаписьПродажи.СуммаСкидки);
			КонецЦикла; 
			Количество=Окр(Количество,3);
			СуммаРегл=Окр(СуммаРегл,2);
			СуммаУпр=Окр(СуммаУпр,2);
			СуммаНДС=Окр(СуммаНДС,2);
			СуммаСкидки=Окр(СуммаСкидки,2);
			Если Количество<>0 ИЛИ СуммаРегл<>0 ИЛИ СуммаУпр<>0 ИЛИ СуммаНДС<>0 ИЛИ СуммаСкидки<>0 Тогда
				//Если после сторнирования еще что-то осталось - сторно на последнюю партию или партию отрицательных остатков
				НоваяЗаписьПродажиПревышение=НаборЗаписейПродажи.Добавить();
				Если НоваяЗаписьПродажи=Неопределено Тогда
					НоваяЗаписьПродажиПревышение.Поставщик=ПартияТоваровОтрицательныхОстатковКонтрагент;
					НоваяЗаписьПродажиПревышение.Партия=ПартияТоваровОтрицательныхОстатков;
					НоваяЗаписьПродажиПревышение.СтатусПартии=ПартияТоваровОтрицательныхОстатковСтатусПартии;
					НоваяЗаписьПродажиПревышение.СкладКомпании=ДокументОснование.Цех;
				Иначе
					НоваяЗаписьПродажиПревышение.Поставщик=НоваяЗаписьПродажи.Поставщик;
					НоваяЗаписьПродажиПревышение.Партия=НоваяЗаписьПродажи.Партия;
					НоваяЗаписьПродажиПревышение.СтатусПартии=НоваяЗаписьПродажи.СтатусПартии;
					НоваяЗаписьПродажиПревышение.СкладКомпании=НоваяЗаписьПродажи.СкладКомпании;
				КонецЕсли; 
				НоваяЗаписьПродажиПревышение.Период=ШапкаДокумента.Дата;
				НоваяЗаписьПродажиПревышение.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗаписьПродажиПревышение.ПодразделениеКомпании=ШапкаДокумента.ПодразделениеКомпании;
				НоваяЗаписьПродажиПревышение.Номенклатура=СтрокаТЧ.Номенклатура;
				//НоваяЗаписьПродажиПревышение.ДокументПродажи=ШапкаДокумента.Ссылка;
				НоваяЗаписьПродажиПревышение.ДокументПродажи=ШапкаДокумента.ЗаказНаряд;
				НоваяЗаписьПродажиПревышение.Покупатель=ШапкаДокумента.Контрагент;
				НоваяЗаписьПродажиПревышение.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗаписьПродажиПревышение.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
				НоваяЗаписьПродажиПревышение.ХарактеристикаНоменклатуры=СтрокаТЧ.ХарактеристикаНоменклатуры;
				НоваяЗаписьПродажиПревышение.СтавкаНДС=СтрокаТЧ.СтавкаНДС;
				НоваяЗаписьПродажиПревышение.Количество=-Количество;
				НоваяЗаписьПродажиПревышение.Сумма=-СуммаРегл;
				НоваяЗаписьПродажиПревышение.СуммаНДС=-СуммаНДС;
				НоваяЗаписьПродажиПревышение.СуммаСкидки=-СуммаСкидки;
				НоваяЗаписьПродажиПревышение.СуммаУпр=-СуммаУпр;
				НоваяЗаписьПродажиПревышение.СебестоимостьУпр=0;
				НоваяЗаписьПродажиПревышение.Себестоимость=0;
				НоваяЗаписьПродажиПревышение.СуммаНДСВходящий=0;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	//Проведем приход/сторнирование авторабот по регистру продаж
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	Продажи.ПодразделениеКомпании,
	             |	Продажи.Номенклатура,
	             |	Продажи.ДокументПродажи,
	             |	Продажи.Поставщик,
	             |	Продажи.Покупатель,
	             |	Продажи.СтатусПартии,
	             |	Продажи.ХозОперация,
	             |	Продажи.ДоговорВзаиморасчетов,
	             |	Продажи.ХарактеристикаНоменклатуры,
	             |	Продажи.СкладКомпании,
	             |	Продажи.Авторабота,
	             |	Продажи.СтавкаНДС,
	             |	Продажи.Партия,
	             |	Продажи.ГТД,
	             |	Продажи.Количество,
	             |	Продажи.КоличествоНормочасов,
	             |	Продажи.Сумма,
	             |	Продажи.СуммаНДС,
	             |	Продажи.СуммаСкидки,
	             |	Продажи.СуммаУпр,
	             |	Продажи.СебестоимостьУпр,
	             |	Продажи.Себестоимость,
	             |	Продажи.СуммаНДСВходящий
	             |ИЗ
	             |	РегистрНакопления.Продажи КАК Продажи
	             |ГДЕ
	             |	Продажи.Регистратор = &ЗаказНаряд
	             |	И Продажи.Номенклатура = &НоменклатураАвторабота
	             |	И Продажи.Авторабота <> &АвтоработаПустая
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	Продажи.НомерСтроки";
	Запрос.УстановитьПараметр("ЗаказНаряд",ДокументОснование);
	Запрос.УстановитьПараметр("НоменклатураАвторабота",Справочники.Номенклатура.Авторабота);
	Запрос.УстановитьПараметр("АвтоработаПустая",Справочники.Автоработы.ПустаяСсылка());
	тзПродажи=Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЧ Из Работы Цикл
		Количество           = СтрокаТЧ.КоличествоРазница;
		
		Если СтрокаТЧ.Нормочас.Цена = 1 Тогда
			КоличествоНормочасов = 0;
		Иначе
			КоличествоНормочасов = СтрокаТЧ.Количество*СтрокаТЧ.Коэффициент;
		КонецЕсли;
		
		Если НЕ СтрокаТЧ.НормочасПоЗаказНаряду.Цена = 1 Тогда
			КоличествоНормочасов = КоличествоНормочасов - (СтрокаТЧ.КоличествоПоЗаказНаряду*СтрокаТЧ.КоэффициентПоЗаказНаряду);
		КонецЕсли;
		
		СуммаРегл   = обПересчет(СтрокаТЧ.СуммаВсегоРазница,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
		СуммаУпр    = обПересчет(СтрокаТЧ.СуммаВсегоРазница,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсВалютыУпр);
		СуммаНДС    = обПересчет(СтрокаТЧ.СуммаНДСРазница,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
		СуммаСкидки = обПересчет(СтрокаТЧ.СуммаСкидкиРазница,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
		Если Количество=0 И СуммаРегл=0 И СуммаУпр=0 И СуммаНДС=0 И СуммаСкидки=0 И КоличествоНормочасов = 0 Тогда
			Продолжить;
		КонецЕсли; 
		Если Количество>=0 Тогда
			//Если количество работ увеличено - приход по продажам
			НоваяЗаписьПродажи=НаборЗаписейПродажи.Добавить();
			НоваяЗаписьПродажи.Период=ШапкаДокумента.Дата;
			НоваяЗаписьПродажи.Регистратор=ШапкаДокумента.Ссылка;
			НоваяЗаписьПродажи.ПодразделениеКомпании=ШапкаДокумента.ПодразделениеКомпании;
			НоваяЗаписьПродажи.Номенклатура=Справочники.Номенклатура.Авторабота;
			//НоваяЗаписьПродажи.ДокументПродажи=?(Количество=0,ШапкаДокумента.ЗаказНаряд,ШапкаДокумента.Ссылка);
			НоваяЗаписьПродажи.ДокументПродажи=ШапкаДокумента.ЗаказНаряд;
	//		НоваяЗаписьПродажи.Поставщик=ПартияТоваровОтрицательныхОстатковКонтрагент;
			НоваяЗаписьПродажи.Покупатель=ШапкаДокумента.Контрагент;
	//		НоваяЗаписьПродажи.СтатусПартии=ПартияТоваровОтрицательныхОстатковСтатусПартии;
			НоваяЗаписьПродажи.ХозОперация=ШапкаДокумента.ХозОперация;
			НоваяЗаписьПродажи.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
	//		НоваяЗаписьПродажи.ХарактеристикаНоменклатуры=СтрокаТЧ.ХарактеристикаНоменклатуры;
			НоваяЗаписьПродажи.СкладКомпании=ДокументОснование.Цех;
			НоваяЗаписьПродажи.Авторабота=СтрокаТЧ.Работа;
			НоваяЗаписьПродажи.СтавкаНДС=СтрокаТЧ.СтавкаНДС;
	//		НоваяЗаписьПродажи.Партия=ПартияТоваровОтрицательныхОстатков;
			НоваяЗаписьПродажи.Количество=Количество;
			НоваяЗаписьПродажи.КоличествоНормочасов=КоличествоНормочасов;
			НоваяЗаписьПродажи.Сумма=СуммаРегл;
			НоваяЗаписьПродажи.СуммаНДС=СуммаНДС;
			НоваяЗаписьПродажи.СуммаСкидки=СуммаСкидки;
			НоваяЗаписьПродажи.СуммаУпр=СуммаУпр;
			НоваяЗаписьПродажи.СебестоимостьУпр=0;
			НоваяЗаписьПродажи.Себестоимость=0;
			НоваяЗаписьПродажи.СуммаНДСВходящий=0;
		Иначе
			//При уменьшении количества работ - сторнируем те продажи, 
			//которые были проведены заказ-нарядом
			Количество=-Количество;
			КоличествоНормочасов = -КоличествоНормочасов;
			СуммаРегл=-СуммаРегл;
			СуммаУпр=-СуммаУпр;
			СуммаНДС=-СуммаНДС;
			СуммаСкидки=-СуммаСкидки;
			СтрокиПродаж=тзПродажи.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Авторабота,СтавкаНДС",Справочники.Номенклатура.Авторабота,Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(),СтрокаТЧ.Работа,СтрокаТЧ.СтавкаНДСПоЗаказНаряду));
			НоваяЗаписьПродажи=Неопределено;
			Для каждого СтрокаПродаж Из СтрокиПродаж Цикл
				Если Количество<=0 Тогда Прервать; КонецЕсли;
				НоваяЗаписьПродажи=НаборЗаписейПродажи.Добавить();
				НоваяЗаписьПродажи.Период=ШапкаДокумента.Дата;
				НоваяЗаписьПродажи.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗаписьПродажи.ПодразделениеКомпании=СтрокаПродаж.ПодразделениеКомпании;
				НоваяЗаписьПродажи.Номенклатура=СтрокаПродаж.Номенклатура;
				НоваяЗаписьПродажи.ДокументПродажи=СтрокаПродаж.ДокументПродажи;
				НоваяЗаписьПродажи.Поставщик=СтрокаПродаж.Поставщик;
				НоваяЗаписьПродажи.Покупатель=СтрокаПродаж.Покупатель;
				НоваяЗаписьПродажи.СтатусПартии=СтрокаПродаж.СтатусПартии;
				НоваяЗаписьПродажи.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗаписьПродажи.ДоговорВзаиморасчетов=СтрокаПродаж.ДоговорВзаиморасчетов;
				НоваяЗаписьПродажи.ХарактеристикаНоменклатуры=СтрокаПродаж.ХарактеристикаНоменклатуры;
				НоваяЗаписьПродажи.СкладКомпании=СтрокаПродаж.СкладКомпании;
				НоваяЗаписьПродажи.Авторабота=СтрокаТЧ.Работа;
				НоваяЗаписьПродажи.СтавкаНДС=СтрокаПродаж.СтавкаНДС;
				НоваяЗаписьПродажи.Партия=СтрокаПродаж.Партия;
				НоваяЗаписьПродажи.ГТД=СтрокаПродаж.ГТД;
				НоваяЗаписьПродажи.Количество           = -Мин(СтрокаПродаж.Количество, Количество);
				НоваяЗаписьПродажи.КоличествоНормочасов = -Мин(СтрокаПродаж.КоличествоНормочасов, КоличествоНормочасов);
				НоваяЗаписьПродажи.Сумма=-Мин(СтрокаПродаж.Сумма,СуммаРегл);
				НоваяЗаписьПродажи.СуммаНДС=-Мин(СтрокаПродаж.СуммаНДС,СуммаНДС);
				НоваяЗаписьПродажи.СуммаСкидки=-Мин(СтрокаПродаж.СуммаСкидки,СуммаСкидки);
				НоваяЗаписьПродажи.СуммаУпр=-Мин(СтрокаПродаж.СуммаУпр,СуммаУпр);
				Количество           = Количество-(-НоваяЗаписьПродажи.Количество);
				КоличествоНормочасов = КоличествоНормочасов-(-НоваяЗаписьПродажи.КоличествоНормочасов);
				СуммаРегл=СуммаРегл-(-НоваяЗаписьПродажи.Сумма);
				СуммаУпр=СуммаУпр-(-НоваяЗаписьПродажи.СуммаУпр);
				СуммаНДС=СуммаНДС-(-НоваяЗаписьПродажи.СуммаНДС);
				СуммаСкидки=СуммаСкидки-(-НоваяЗаписьПродажи.СуммаСкидки);
			КонецЦикла;
			Количество=Окр(Количество,3);
			КоличествоНормочасов = Окр(КоличествоНормочасов,3);
			СуммаРегл=Окр(СуммаРегл,2);
			СуммаУпр=Окр(СуммаУпр,2);
			СуммаНДС=Окр(СуммаНДС,2);
			СуммаСкидки=Окр(СуммаСкидки,2);
			Если Количество<>0 ИЛИ СуммаРегл<>0 ИЛИ СуммаУпр<>0 ИЛИ СуммаНДС<>0 ИЛИ СуммаСкидки<>0 ИЛИ КоличествоНормочасов<>0 Тогда
				//Если после сторнирования еще что-то осталось - сторно на последнюю партию или на пустую партию
				НоваяЗаписьПродажиПревышение=НаборЗаписейПродажи.Добавить();
				Если НоваяЗаписьПродажи=Неопределено Тогда
					НоваяЗаписьПродажиПревышение.СкладКомпании=ДокументОснование.Цех;
				Иначе
					НоваяЗаписьПродажиПревышение.Поставщик=НоваяЗаписьПродажи.Поставщик;
					НоваяЗаписьПродажиПревышение.Партия=НоваяЗаписьПродажи.Партия;
					НоваяЗаписьПродажиПревышение.СтатусПартии=НоваяЗаписьПродажи.СтатусПартии;
					НоваяЗаписьПродажиПревышение.СкладКомпании=НоваяЗаписьПродажи.СкладКомпании;
				КонецЕсли; 
				НоваяЗаписьПродажиПревышение.Период=ШапкаДокумента.Дата;
				НоваяЗаписьПродажиПревышение.Регистратор=ШапкаДокумента.Ссылка;
				НоваяЗаписьПродажиПревышение.ПодразделениеКомпании=ШапкаДокумента.ПодразделениеКомпании;
				НоваяЗаписьПродажиПревышение.Номенклатура=Справочники.Номенклатура.Авторабота;
				//НоваяЗаписьПродажиПревышение.ДокументПродажи=ШапкаДокумента.Ссылка;
				НоваяЗаписьПродажиПревышение.ДокументПродажи=ШапкаДокумента.ЗаказНаряд;
				НоваяЗаписьПродажиПревышение.Покупатель=ШапкаДокумента.Контрагент;
				НоваяЗаписьПродажиПревышение.ХозОперация=ШапкаДокумента.ХозОперация;
				НоваяЗаписьПродажиПревышение.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
				НоваяЗаписьПродажиПревышение.Авторабота=СтрокаТЧ.Работа;
				НоваяЗаписьПродажиПревышение.СтавкаНДС=СтрокаТЧ.СтавкаНДС;
				НоваяЗаписьПродажиПревышение.Количество=-Количество;
				НоваяЗаписьПродажиПревышение.КоличествоНормочасов=-КоличествоНормочасов;
				НоваяЗаписьПродажиПревышение.Сумма=-СуммаРегл;
				НоваяЗаписьПродажиПревышение.СуммаНДС=-СуммаНДС;
				НоваяЗаписьПродажиПревышение.СуммаСкидки=-СуммаСкидки;
				НоваяЗаписьПродажиПревышение.СуммаУпр=-СуммаУпр;
				НоваяЗаписьПродажиПревышение.СебестоимостьУпр=0;
				НоваяЗаписьПродажиПревышение.Себестоимость=0;
				НоваяЗаписьПродажиПревышение.СуммаНДСВходящий=0;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат НЕ Отказ;
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа.
//	Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если ДопПараметры=Неопределено Тогда
		ДопПараметры=Новый Структура;
	КонецЕсли;
	Если ТипЗнч(ДопПараметры)=Тип("Структура") Тогда
		ДопПараметры.Вставить("НеРассчитыватьСкидки",Истина);
	КонецЕсли; 
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			Для каждого СтрокаРабот Из Работы Цикл
				НоваяЦена=обПересчет(СтрокаРабот.Цена,ТекущаяВалютаДокумента,ТекущийКурсДокумента,ВалютаДокумента,КурсДокумента);
				Если НоваяЦена<>СтрокаРабот.Цена Тогда
					СтрокаРабот.Цена=НоваяЦена;
					Рез=ОбработкаРеквизита("Работы.Цена",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Организация может является плательщиком НДС, а может и нет.. надо обработать таблицу работ
		ТаблицаРабот=ЭтотОбъект.Работы;
		ОсвобожденОтНДС=ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаРабот Из ТаблицаРабот Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаРабот.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаРабот.СтавкаНДС=СтрокаРабот.Работа.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			Рез=ЭтотОбъект.ОбработкаРеквизита("Работы.СтавкаНДС",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		КонецЦикла;
		
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Подразделение может является плательщиком НДС, а может и нет.. надо обработать таблицу работ
		ТаблицаРабот=ЭтотОбъект.Работы;
		ОсвобожденОтНДС=ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		Для Каждого СтрокаРабот Из ТаблицаРабот Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаРабот.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаРабот.СтавкаНДС=СтрокаРабот.Работа.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			Рез=ЭтотОбъект.ОбработкаРеквизита("Работы.СтавкаНДС",СтрокаРабот,ЭтаФорма,ДопПараметры) И Рез;
		КонецЦикла;
		Возврат Рез;
		
	ИначеЕсли Имя="ДокументОснование" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда 
			Если Вопрос("Заполнить по документу-основания?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да Тогда
				ЭтотОбъект.Заполнить(ДокументОснование);
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
				ЭтаФорма.ОбновитьИнформацию();
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда
			Возврат Рез;
		Иначе
			ТекСтрока.Подтверждение = Истина;
		КонецЕсли;
	
	ИначеЕсли Имя="Товары.Подтверждение" Тогда
		Если ТекСтрока.Подтверждение Тогда
			ТекСтрока.Количество=(ТекСтрока.КоличествоПоЗаказНаряду*ТекСтрока.КоэффициентПоЗаказНаряду)/ТекСтрока.Коэффициент;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			ТекСтрока.СуммаВсего=ТекСтрока.СуммаВсегоПоЗаказНаряду;
			Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			ТекСтрока.Количество=0;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Рез=ОбработкаРеквизита("Товары.РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
	
	ИначеЕсли Имя="Товары.РасчетРазницы" Тогда
		Рез=Истина;
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда
			Возврат Рез;
		КонецЕсли;
		ТекСтрока.КоличествоРазница=(ТекСтрока.Количество*ТекСтрока.Коэффициент)-(ТекСтрока.КоличествоПоЗаказНаряду*ТекСтрока.КоэффициентПоЗаказНаряду);
		ТекСтрока.СуммаНДСРазница=ТекСтрока.СуммаНДС-ТекСтрока.СуммаНДСПоЗаказНаряду;
		ТекСтрока.СуммаСкидкиРазница=ТекСтрока.СуммаСкидки-ТекСтрока.СуммаСкидкиПоЗаказНаряду;
		ТекСтрока.СуммаВсегоРазница=ТекСтрока.СуммаВсего-ТекСтрока.СуммаВсегоПоЗаказНаряду;
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "РАБОТЫ"
	
	ИначеЕсли Имя="Работы.Подтверждение" Тогда
		Если ТекСтрока.Подтверждение Тогда
			ТекСтрока.Количество=ТекСтрока.КоличествоПоЗаказНаряду;
			ТекСтрока.Коэффициент=ТекСтрока.КоэффициентПоЗаказНаряду;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			ТекСтрока.СуммаВсего=ТекСтрока.СуммаВсегоПоЗаказНаряду;
			Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			ТекСтрока.Количество=0;
			ТекСтрока.Коэффициент=0;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Рез=ОбработкаРеквизита("Товары.РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
	
	ИначеЕсли Имя="Работы.РасчетРазницы" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда
			Возврат Истина;
		КонецЕсли;
		
		ТекСтрока.КоличествоРазница=ТекСтрока.Количество-ТекСтрока.КоличествоПоЗаказНаряду;
		ТекСтрока.КоэффициентРазница=ТекСтрока.Коэффициент-ТекСтрока.КоэффициентПоЗаказНаряду;
		ТекСтрока.СуммаНДСРазница=ТекСтрока.СуммаНДС-ТекСтрока.СуммаНДСПоЗаказНаряду;
		ТекСтрока.СуммаСкидкиРазница=ТекСтрока.СуммаСкидки-ТекСтрока.СуммаСкидкиПоЗаказНаряду;
		ТекСтрока.СуммаВсегоРазница=ТекСтрока.СуммаВсего-ТекСтрока.СуммаВсегоПоЗаказНаряду;
		Возврат Истина;
	
	ИначеЕсли Имя="Работы.Работа" Тогда
		//Проверим на повторение строки в таблице
		//Получим из формы последнюю текущую работу
		Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1; КонецЕсли;
		
		// заполним значение скидки работ
		Если НЕ (СкидкаНаценкаРаботы = Неопределено) И (НЕ СкидкаНаценкаРаботы.Пустая()) Тогда
			Если СкидкаНаценкаРаботы.СпособВычисления=Перечисления.СкидкиСпособВычисления.Абсолютная Тогда
				ТекСтрока.ПроцентСкидки = 0;
			Иначе
				ТекСтрока.ПроцентСкидки = ЗначениеСкидкиНаценкиРабот;
			КонецЕсли;
		КонецЕсли;
		//Рассчитаем цену текущей работы
		ОбработкаРеквизита("РасчетЦеныРаботы",ТекСтрока,ЭтаФорма,ДопПараметры);
		//Заполнение ставок налогов
		Если ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС ИЛИ ЭтотОбъект.ВидРемонта.ОсвобожденОтНДСРаботы Тогда
			ТекСтрока.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
		Иначе
			ТекСтрока.СтавкаНДС=ТекСтрока.Работа.Номенклатура.СтавкаНДС;
		КонецЕсли; 
		//Расчет сумм по строке
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Количество" Тогда
		Возврат ОбработкаРеквизита("Работы.Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Нормочас" Тогда
		ТекСтрока.Цена=обПересчет(ТекСтрока.Нормочас.Цена,ТекСтрока.Нормочас.Валюта,Дата,ВалютаДокумента,КурсДокумента);
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Коэффициент" Тогда
		Возврат ОбработкаРеквизита("Работы.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Возврат ОбработкаРеквизита("Работы.ПроцентСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество*ТекСтрока.Коэффициент=0,0,ТекСтрока.Сумма/(ТекСтрока.Количество*ТекСтрока.Коэффициент));
		Возврат ОбработкаРеквизита("Работы.ПроцентСкидки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.ПроцентСкидки" Тогда
		Рез=Истина;
		// Скидка не может быть выше 100%
		Если ТекСтрока.ПроцентСкидки>100 Тогда
			ТекСтрока.ПроцентСкидки = 100;	
		КонецЕсли;
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
		ТекСтрока_СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
		СуммаСкидки=Окр(ТекСтрока.ПроцентСкидки*ТекСтрока_СуммаБезСкидки/100,2);
		Если СуммаСкидки<>ТекСтрока.СуммаСкидки Тогда // Сумма скидки изменилась
			ТекСтрока.СуммаСкидки=СуммаСкидки;
		КонецЕсли; 
		ДопПараметры.Вставить("ПересчитыватьПроцентСкидки",Ложь);
		Рез=ОбработкаРеквизита("Работы.СуммаСкидки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Работы.СуммаСкидки" Тогда
		Попытка	// Определим необходимость обратного расчета процента скидки.
			ПересчитыватьПроцентСкидки=ДопПараметры.ПересчитыватьПроцентСкидки;
		Исключение 
			ПересчитыватьПроцентСкидки=Истина;	
		КонецПопытки;
		Если ПересчитыватьПроцентСкидки Тогда
			Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("ТипЦен", ЭтотОбъект.ТипЦенРабот);
			ТекСтрока_СуммаБезСкидки=дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока,ДопПараметры);
			ТекСтрока.ПроцентСкидки=?(ТекСтрока_СуммаБезСкидки=0,0,Окр(ТекСтрока.СуммаСкидки*100/ТекСтрока_СуммаБезСкидки,2));
		КонецЕсли;
		// Выполним расчет итоговой суммы скидки на документ.
		СуммаСкидкиНаценкиРабот=Работы.Итог("СуммаСкидки");
		СуммаСкидкиНаценкиРабот=СуммаСкидкиНаценкиРабот+Работы.Итог("СуммаСкидкиСтроки");
		// Ничего делать не будем - весь расчет от НДС зависит (включено или нет)
		Возврат ОбработкаРеквизита("Работы.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Работы.СуммаВсего" Тогда
		//Получим новую сумму скидки как процент от суммы всего (уже со скидкой)
		Если ТекСтрока.ПроцентСкидки=100 Тогда
			ТекСтрока.СуммаВсего=0;
			СуммаБезСкидки=ТекСтрока.СуммаСкидки;
		Иначе
			СуммаБезСкидки=Окр((ТекСтрока.СуммаВсего*100)/(100-ТекСтрока.ПроцентСкидки),2);
			ТекСтрока.СуммаСкидки=СуммаБезСкидки-ТекСтрока.СуммаВсего;
		КонецЕсли; 
		//Получим новую сумму НДС как процент от суммы всего
		СуммаБезСкидкиБезНДС=Окр((100*СуммаБезСкидки)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		СуммаБезНДС=Окр((100*ТекСтрока.СуммаВсего)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		СуммаНДСБезСкидки=СуммаБезСкидки-СуммаБезСкидкиБезНДС;
		ТекСтрока.СуммаНДС=ТекСтрока.СуммаВсего-СуммаБезНДС;
		Если ЗначениеЗаполнено(ТипЦенРабот) И ТипЦенРабот.ЦенаВключаетНДС Тогда
			//Цена уже содержит НДС
			//Получим сумму как разницу суммы всего и суммы скидки
			ТекСтрока.Сумма=СуммаБезСкидки;
		Иначе
			//НДС в цену не включен
			//Получим сумму как разницу суммы всего и суммы скидки и суммы НДС
			ТекСтрока.Сумма=СуммаБезСкидки-СуммаНДСБезСкидки;
		КонецЕсли;
		ТекСтрока.Цена=?(ТекСтрока.Количество*ТекСтрока.Коэффициент=0,0,ТекСтрока.Сумма/(ТекСтрока.Количество*ТекСтрока.Коэффициент));
		Возврат Истина;
		
	ИначеЕсли Имя="Работы.СтавкаНДС" Тогда
		Если ДопПараметры = Неопределено Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		ДопПараметры.Вставить("ТипЦен", ТипЦенРабот);
		СуммаСоСкидкой=дкПолучитьСуммуСтрокиСоСкидкой(ЭтотОбъект,ТекСтрока, ДопПараметры);
		СтавкаНДС=ТекСтрока.СтавкаНДС.Ставка;
		ТекСтрока.СуммаНДС=Окр((СуммаСоСкидкой*СтавкаНДС)/(100+СтавкаНДС),2);
		ТекСтрока.СуммаВсего=СуммаСоСкидкой;
		Возврат Истина;
		
	ИначеЕсли Имя="Работы.СуммаНДС" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="РасчетЦеныРаботы" Тогда
		//Расчет цены работы
		Если обЗначениеНеЗаполнено(ТекСтрока.Работа) Тогда Возврат Ложь; КонецЕсли;
		//Если работа задана - получим ее цену согласно класса автомобиля (если автомобиль выбран)
		Если обЗначениеНеЗаполнено(Автомобиль) Тогда
			ВариантКомплектации = Справочники.Модели.ПустаяСсылка();
		Иначе
			Если ЗначениеЗаполнено(Автомобиль.ВариантКомплектации) Тогда
				ВариантКомплектации = Автомобиль.ВариантКомплектации;
			Иначе
				ВариантКомплектации = Автомобиль.Модель;
			КонецЕсли;
		КонецЕсли;
		ЦенаРаботы = орПолучитьЦенуАвтоработы(ТипЦенРабот, ТекСтрока.Работа, ВариантКомплектации, ДоговорВзаиморасчетов, ДокументОснование.Цех, ВидРемонта,, ВалютаДокумента, КурсДокумента);
		ТекСтрока.Нормочас=ЦенаРаботы.Нормочас;
		ТекСтрока.Коэффициент=?(обЗначениеНеЗаполнено(ЦенаРаботы.НормаВремени),ТекСтрока.Коэффициент,ЦенаРаботы.НормаВремени);
		ТекСтрока.Цена=ЦенаРаботы.Цена;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,0);
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	//ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеРаботы=Новый Структура();
	ОбязательныеРаботы.Вставить("Работа",3);
	//ОбязательныеРаботы.Вставить("Количество",1);
	ОбязательныеРаботы.Вставить("Нормочас",1);
	//ОбязательныеРаботы.Вставить("Коэффициент",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ТипЦенРабот",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ВидРемонта",1);
	//Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон") Тогда
		КомплектацияАвтомобиля=Справочники.ВидыРемонта.КомплектацияАвтомобиля;
		Если (КомплектацияАвтомобиля.Пустая()) ИЛИ (НЕ КомплектацияАвтомобиля.Предопределенный) Тогда
			КомплектацияАвтомобиля=Неопределено;
		КонецЕсли; 
	//Иначе
	//	КомплектацияАвтомобиля=Неопределено;
	//КонецЕсли; 
	Если ВидРемонта<>КомплектацияАвтомобиля Тогда
		ОбязательныеРеквизиты.Вставить("Заказчик",1);
		Если ВидРемонта.ТипРемонта=Перечисления.ТипыРемонта.Платный Тогда
			ОбязательныеРеквизиты.Вставить("Контрагент",1);
			ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
		КонецЕсли; 
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("Автомобиль",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("Работы",ОбязательныеРаботы);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности договора и склада документа. 
	// Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов",КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
		
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	Если Результат И НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд") И ДокументОснование.Состояние <> Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			дкДобавитьОшибку(Ошибки, "Проведение акта разногласий возможено только на основании закрытого заказ-наряда.");
			Результат = Ложь;
		Иначе
			Если ДокументОснование.ВидРемонта.ТипРемонта<>Перечисления.ТипыРемонта.Платный Тогда
				дкДобавитьОшибку(Ошибки, "Проведение акта разногласий возможено только на основании заказ-наряда с платным видом ремонта.");
				Результат = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("АктРазногласий","Акт разногласий",Истина);
	
	Возврат СписокХозОпераций;
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ЗаказНаряд"
// Возвращает сформированный табличный документ:
Функция ПечатьЗаказНаряд(ТабДокумент) Экспорт
	ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента=Неопределено Тогда Возврат Неопределено; КонецЕсли; 
	ТаблицаТоваров=Товары.Выгрузить();
	зфПерерасчетТаблицыТоваров(ТаблицаТоваров,ЭтотОбъект,ВалютаПечатногоДокумента);
	ТаблицаРабот=Работы.Выгрузить();
	зфПерерасчетТаблицыТоваров(ТаблицаРабот,ЭтотОбъект,ВалютаПечатногоДокумента,Истина);
	
	Макет = ПолучитьМакет("ЗаказНаряд");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	НомерДляПечатиДокументОснование=дкПолучитьНомерДляПечати(ДокументОснование);

	ЕстьСкидкаПоДеталям = Ложь;
	Если Товары.Итог("СуммаСкидки") <> 0 Тогда
		ЕстьСкидкаПоДеталям = Истина;
	КонецЕсли;
		
	ЕстьСкидкаПоРаботам = Ложь;
	Если Работы.Итог("СуммаСкидки") <> 0 Тогда
		ЕстьСкидкаПоРаботам = Истина;
	КонецЕсли;
	
	//зададим параметры макета
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
	ОбластьСкидка = Макет.Область("Скидка");
		
	ОбластьУслуги = Макет.ПолучитьОбласть("Услуги");
	ОбластьДетали = Макет.ПолучитьОбласть("Детали");
		
	ОбластьСтрокаДеталей	        = Макет.ПолучитьОбласть("СтрокаДеталей");
	ОбластьИтоговПоСтраницеДетали	= Макет.ПолучитьОбласть("ИтогиПоСтраницеДеталей");
	ОбластьПодвалДеталей	        = Макет.ПолучитьОбласть("ПодвалДетали");
		
	ОбластьСтрокаУслуг		        = Макет.ПолучитьОбласть("СтрокаРабот");
	ОбластьИтоговПоСтраницеУслуги	= Макет.ПолучитьОбласть("ИтогиПоСтраницеРабот");
	ОбластьПодвалУслуг		        = Макет.ПолучитьОбласть("ПодвалУслуги");
		
	БезЛинии = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	ОбычнаяЛиния = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	// Настроем колонки вывода деталей если не было скидок
	Если Не ЕстьСкидкаПоДеталям Тогда
		ПоложениеШапкиДеталей = ОбластьДетали.Область("ШапкаДеталей").Низ;
		ПозицияКолонки = ОбластьСкидка.Лево;
		Пока ПозицияКолонки > 6 Цикл
			ОбластьДетали.Область(ПоложениеШапкиДеталей-1,ПозицияКолонки,ПоложениеШапкиДеталей-1,ПозицияКолонки).Текст = ОбластьДетали.Область(ПоложениеШапкиДеталей-1,ПозицияКолонки-1,ПоложениеШапкиДеталей-1,ПозицияКолонки-1).Текст;
			ОбластьДетали.Область(ПоложениеШапкиДеталей,ПозицияКолонки,ПоложениеШапкиДеталей,ПозицияКолонки).Текст = ОбластьДетали.Область(ПоложениеШапкиДеталей,ПозицияКолонки-1,ПоложениеШапкиДеталей,ПозицияКолонки-1).Текст;
				
			ОбластьИсточник = ОбластьСтрокаДеталей.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
			ОбластьПриемник = ОбластьСтрокаДеталей.Область(1,ПозицияКолонки,1,ПозицияКолонки);
			ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
			ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
			ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
			ОбластьИсточник = ОбластьИтоговПоСтраницеДетали.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
			ОбластьПриемник = ОбластьИтоговПоСтраницеДетали.Область(1,ПозицияКолонки,1,ПозицияКолонки);
			ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
			ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
			ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
			ОбластьИсточник = ОбластьПодвалДеталей.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
			ОбластьПриемник = ОбластьПодвалДеталей.Область(1,ПозицияКолонки,1,ПозицияКолонки);
			ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
			ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
			ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
			ПозицияКолонки = ПозицияКолонки-1;
		КонецЦикла;
			
		ОбластьДетали.Область(ПоложениеШапкиДеталей-1, 4, ПоложениеШапкиДеталей-1, 6).Объединить();
		ОбластьДетали.Область(ПоложениеШапкиДеталей, 4, ПоложениеШапкиДеталей, 6).Объединить();
		ОбластьСтрокаДеталей.Область(1, 4, 1, 6).Объединить();
			
		ТекОбласть = ОбластьИтоговПоСтраницеДетали.Область(1, 2, 1, 6);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСнизу = БезЛинии;
			
		ТекОбласть = ОбластьПодвалДеталей.Область(1, 2, 1, 6);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСнизу = БезЛинии;
			
		ТекОбласть = ОбластьИтоговПоСтраницеДетали.Область(1, 8, 1, 9);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСлева = ОбычнаяЛиния;
		ТекОбласть.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ТекОбласть.Текст = "на сумму:";
			
		ТекОбласть = ОбластьПодвалДеталей.Область(1, 8, 1, 9);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСлева = ОбычнаяЛиния;
		ТекОбласть.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ТекОбласть.Текст = "на сумму:";
			
		ПозицияКолонки = ОбластьСкидка.Лево-1;
		Пока ПозицияКолонки <= ОбластьДетали.ШиринаТаблицы Цикл
			ОбластьДетали.Область(ПоложениеШапкиДеталей,ПозицияКолонки,ПоложениеШапкиДеталей,ПозицияКолонки).Текст = ПозицияКолонки-3;
			ПозицияКолонки = ПозицияКолонки + 1;
		КонецЦикла;
	КонецЕсли;
		
	ОбластьШапкаДеталей = ОбластьДетали.ПолучитьОбласть("ШапкаДеталей");
		
	// Настроем колонки вывода услуг если не было скидок
	Если Не ЕстьСкидкаПоРаботам Тогда
		ПоложениеШапкиУслуг = ОбластьУслуги.Область("ШапкаУслуг").Низ;
		ПозицияКолонки = ОбластьСкидка.Лево;
		Пока ПозицияКолонки > 5 Цикл
			ОбластьУслуги.Область(ПоложениеШапкиУслуг-1,ПозицияКолонки,ПоложениеШапкиУслуг-1,ПозицияКолонки).Текст = ОбластьУслуги.Область(ПоложениеШапкиУслуг-1,ПозицияКолонки-1,ПоложениеШапкиУслуг-1,ПозицияКолонки-1).Текст;
			ОбластьУслуги.Область(ПоложениеШапкиУслуг,ПозицияКолонки,ПоложениеШапкиУслуг,ПозицияКолонки).Текст = ОбластьУслуги.Область(ПоложениеШапкиУслуг,ПозицияКолонки-1,ПоложениеШапкиУслуг,ПозицияКолонки-1).Текст;
				
			ОбластьИсточник = ОбластьСтрокаУслуг.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
			ОбластьПриемник = ОбластьСтрокаУслуг.Область(1,ПозицияКолонки,1,ПозицияКолонки);
			ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
			ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
			ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
			ОбластьИсточник = ОбластьИтоговПоСтраницеУслуги.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
			ОбластьПриемник = ОбластьИтоговПоСтраницеУслуги.Область(1,ПозицияКолонки,1,ПозицияКолонки);
			ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
			ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
			ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
			ОбластьИсточник = ОбластьПодвалУслуг.Область(1,ПозицияКолонки-1,1,ПозицияКолонки-1);
			ОбластьПриемник = ОбластьПодвалУслуг.Область(1,ПозицияКолонки,1,ПозицияКолонки);
			ОбластьПриемник.Текст 		= ОбластьИсточник.Текст;
			ОбластьПриемник.Заполнение	= ОбластьИсточник.Заполнение;
			ОбластьПриемник.Параметр	= ОбластьИсточник.Параметр;
				
			ПозицияКолонки = ПозицияКолонки-1;
		КонецЦикла;
			
		ОбластьУслуги.Область(ПоложениеШапкиУслуг-1, 4, ПоложениеШапкиУслуг-1, 5).Объединить();
		ОбластьУслуги.Область(ПоложениеШапкиУслуг, 4, ПоложениеШапкиУслуг, 5).Объединить();
		ОбластьСтрокаУслуг.Область(1, 4, 1, 5).Объединить();
			
		ТекОбласть = ОбластьИтоговПоСтраницеУслуги.Область(1, 2, 1, 5);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСнизу = БезЛинии;
			
		ТекОбласть = ОбластьПодвалУслуг.Область(1, 2, 1, 5);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСнизу = БезЛинии;
			
		ТекОбласть = ОбластьИтоговПоСтраницеУслуги.Область(1, 7, 1, 9);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСлева = ОбычнаяЛиния;
		ТекОбласть.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ТекОбласть.Текст = "на сумму:";
			
		ТекОбласть = ОбластьПодвалУслуг.Область(1, 7, 1, 9);
		ТекОбласть.Объединить();
		ТекОбласть.ГраницаСлева = ОбычнаяЛиния;
		ТекОбласть.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ТекОбласть.Текст = "на сумму:";
			
		ПозицияКолонки = ОбластьСкидка.Лево-1;
		Пока ПозицияКолонки <= ОбластьУслуги.ШиринаТаблицы Цикл
			ОбластьУслуги.Область(ПоложениеШапкиУслуг,ПозицияКолонки,ПоложениеШапкиУслуг,ПозицияКолонки).Текст = ПозицияКолонки-2;
			ПозицияКолонки = ПозицияКолонки + 1;
		КонецЦикла;
	КонецЕсли;
		
	ОбластьШапкаУслуг = ОбластьУслуги.ПолучитьОбласть("ШапкаУслуг");
		
	// инициализация итогов по документу
	ИтогоСуммаРабот	              = 0;
	ИтогоСуммаНДСРабот            = 0;
	ИтогоСуммаСкидкиРабот         = 0;
	ИтогоКоличествоРабот          = 0;
	ИтогоСуммаДеталей	          = 0;
	ИтогоСуммаНДСДеталей          = 0;
	ИтогоСуммаСкидкиДеталей       = 0;
	ИтогоКоличествоДеталей        = 0;
	
	ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	ВыводитьПроизводителя  = обПраво("ВыводитьПроизводителяВПечатныхФормах",Права,,ЭтотОбъект);
	
	//Вывод шапки документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Организация = Организация;
	ОбластьМакета.Параметры.ФирмаПолноеНаименование = спПолучитьНаименование(Организация);
	ОбластьМакета.Параметры.ФирмаАдресЮридический = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	ОбластьМакета.Параметры.НомерДок = НомерДляПечатиДокументОснование;
	ОбластьМакета.Параметры.ДатаДок = Формат(ДокументОснование.Дата,"ДФ=дд.ММ.гггг");
	ОбластьМакета.Параметры.Заказчик = Заказчик;
	ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Заказчик);
	ОбластьМакета.Параметры.ЗаказчикАдресПочтовый = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
	ОбластьМакета.Параметры.ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Заказчик, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
	ОбластьМакета.Параметры.Контрагент = Контрагент;
	ОбластьМакета.Параметры.КонтрагентПолноеНаименование = спПолучитьНаименование(Контрагент);;
	ОбластьМакета.Параметры.КонтрагентИНН = Контрагент.ИНН;
	ОбластьМакета.Параметры.КонтрагентАдресЮридический = киПолучитьПредставлениеКИ(Контрагент, Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	ОбластьМакета.Параметры.КонтрагентТелефоны = киПолучитьПредставлениеКИ(Контрагент, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	
	Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
		ОбластьМакета.Параметры.АвтомобильГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер, ДокументОснование.ДатаСоздания);
		ОбластьМакета.Параметры.АвтомобильПробег   = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег, ДокументОснование.ДатаНачала));
	КонецЕсли;
	ОбластьМакета.Параметры.Автомобиль = Автомобиль;
	ОбластьМакета.Параметры.АвтомобильКод = Автомобиль.VIN;
	ОбластьМакета.Параметры.АвтомобильМодель = Автомобиль.Модель;
	ОбластьМакета.Параметры.АвтомобильГодВыпуска = Формат(Автомобиль.ГодВыпуска,ФорматПредставленияГодаВыпускаАвтомобиля);
	
	ОбластьМакета.Рисунки.Принят.Параметр = "Принят:
		|" + Формат(ДокументОснование.ДатаСоздания, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Рисунки.ВидРемонта.Параметр = "Вид ремонта:
		|" + ВидРемонта.Наименование;
	ОбластьМакета.Рисунки.ВидРемонта.Расшифровка = ВидРемонта;
	ОбластьМакета.Рисунки.Диспетчер.Параметр = "Диспетчер:
		|" + ДокументОснование.Диспетчер.Наименование;
	ОбластьМакета.Рисунки.Диспетчер.Расшифровка = ДокументОснование.Диспетчер;
	ОбластьМакета.Рисунки.Мастер.Параметр = "Мастер:
		|" + ДокументОснование.Мастер.Наименование;
	ОбластьМакета.Рисунки.Мастер.Расшифровка = ДокументОснование.Мастер;
	ОбластьМакета.Рисунки.Состояние.Параметр = ДокументОснование.Состояние;
	ОбластьМакета.Параметры.Валюта = ВалютаПечатногоДокумента;
	ТабДокумент.Вывести(ОбластьМакета);

	КолонкаКода=дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект);
	
	//Временная таблица
	Табл = ТаблицаРабот.Скопировать();
	КоличествоРабот = Табл.Количество();
	ПустыеСтроки=Табл.НайтиСтроки(Новый Структура("Количество",0));
	Для каждого ПустаяСтрока Из ПустыеСтроки Цикл
		Табл.Удалить(ПустаяСтрока);
	КонецЦикла; 
	
	// инициализация итогов по документу
	ИтогоСуммаРабот		= 0;
	ИтогоСуммаНДСРабот	= 0;
	
	Если КоличествоРабот = 0 Тогда
		//не выводим
	Иначе
		//Вывод шапки табличной части работ(услуг)
		ОбластьУслуги.Параметры.НомерДок = НомерДляПечатиДокументОснование;
		ОбластьУслуги.Параметры.ДатаДок = Формат(ДокументОснование.Дата, "ДФ=дд.ММ.гггг");
		ОбластьУслуги.Параметры.ИмяКолонкиКодов = КолонкаКода.Синоним;
		ТабДокумент.Вывести(ОбластьУслуги);
		
		НомерСтраницы = 2;
		НомерСтраницыПред = 2;
		ТекстЗаголовка = "Выполненные работы по заказ-наряду №  "+НомерДляПечатиДокументОснование+" от "+Формат(ДокументОснование.Дата, "ДФ=дд.ММ.гггг");
	
		//Вывод табличной части работ
		Ном	= 0;
		СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаНДС, СуммаСкидки, Количество", 0, 0, 0, 0);
			
		МассивОбластиПодвала = Новый Массив;
		МассивОбластиПодвала.Добавить(ОбластьПодвалУслуг);
		Для каждого СтрокаРабот Из Табл Цикл
			Ном = Ном + 1;
			ОбластьСтрокаУслуг.Параметры.НомСтр = Ном;
			ОбластьСтрокаУслуг.Параметры.Код = дкПолучитьЗначениеКолонкиКода(СтрокаРабот.Работа,ИмяРеквизитаКода,ЭтотОбъект);
			ОбластьСтрокаУслуг.Параметры.Номенклатура = СтрокаРабот.Работа;
			ОбластьСтрокаУслуг.Параметры.Наименование = спПолучитьНаименование(СтрокаРабот.Работа);
			ОбластьСтрокаУслуг.Параметры.Цена = Формат(СтрокаРабот.Цена, "ЧЦ=15; ЧДЦ=2");
			ОбластьСтрокаУслуг.Параметры.Коэффициент = Формат(СтрокаРабот.Коэффициент, "ЧЦ=10; ЧДЦ=3");
			ОбластьСтрокаУслуг.Параметры.Количество = Формат(СтрокаРабот.Количество, ФорматВыводаКоличества);
			ОбластьСтрокаУслуг.Параметры.Единица = СтрокаРабот.Нормочас;
			ОбластьСтрокаУслуг.Параметры.Сумма = Формат(СтрокаРабот.СуммаВсего, ФорматВыводаСуммы);
			Если ЕстьСкидкаПоРаботам Тогда
				ОбластьСтрокаУслуг.Параметры.Скидка = Формат(СтрокаРабот.СуммаСкидки, ФорматВыводаСуммы);
			КонецЕсли;
			ОбластьСтрокаУслуг.Параметры.НДС = Формат(СтрокаРабот.СуммаНДС, ФорматВыводаСуммы);
			
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрокаУслуг, ОбластьШапкаУслуг, ОбластьИтоговПоСтраницеУслуги, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект, ?(Ном=КоличествоРабот, МассивОбластиПодвала, Неопределено));
				
			// Обновим итоги по документу
			ИтогоКоличествоРабот  = ИтогоКоличествоРабот + СтрокаРабот.Количество;
			ИтогоСуммаРабот       = ИтогоСуммаРабот + СтрокаРабот.СуммаВсего;
			ИтогоСуммаНДСРабот    = ИтогоСуммаНДСРабот + СтрокаРабот.СуммаНДС;
			ИтогоСуммаСкидкиРабот = ИтогоСуммаСкидкиРабот + СтрокаРабот.СуммаСкидки;
				
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаНДС, СуммаСкидки, Количество", 0, 0, 0, 0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаУслуг.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
				
			//обновим итоги по странице
			дкДобавитьИтогиПоСтранице(СтрокаРабот, СтруктураИтоговПоСтранице);
		КонецЦикла; 
		
		//выводим последний итог по странице, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьИтоговПоСтраницеУслуги, СтруктураИтоговПоСтранице, ЭтотОбъект);
		КонецЕсли;
		
		//Вывод подвала табличной части услуг
		ОбластьПодвалУслуг.Параметры.Количество = Формат(ИтогоКоличествоРабот, ФорматВыводаКоличества);
		ОбластьПодвалУслуг.Параметры.Сумма = Формат(ИтогоСуммаРабот, ФорматВыводаСуммы);
		ОбластьПодвалУслуг.Параметры.НДС = Формат(ИтогоСуммаНДСРабот, ФорматВыводаСуммы);
		Если ЕстьСкидкаПоРаботам Тогда
			ОбластьПодвалУслуг.Параметры.Скидка = Формат(ИтогоСуммаСкидкиРабот, ФорматВыводаСуммы);
		КонецЕсли;
		ОбластьПодвалУслуг.Параметры.ЧислоПрописью = обЧислоПрописью(ИтогоСуммаРабот, ВалютаПечатногоДокумента);
		Если ИтогоСуммаНДСРабот <> 0 Тогда
			ОбластьПодвалУслуг.Параметры.ЧислоПрописью = ОбластьПодвалУслуг.Параметры.ЧислоПрописью + " в т.ч. НДС " + Формат(ИтогоСуммаНДСРабот, "ЧЦ=15; ЧДЦ=2") + " " + ВалютаПечатногоДокумента;
		КонецЕсли; 
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалУслуг, , , НомерСтраницы, , ЭтотОбъект);
	КонецЕсли;
	
	// Формирование врем. таблиц товаров
	Табл = ТаблицаТоваров.Скопировать();
	ПустыеСтроки=Табл.НайтиСтроки(Новый Структура("Количество",0));
	Для каждого ПустаяСтрока Из ПустыеСтроки Цикл
		Табл.Удалить(ПустаяСтрока);
	КонецЦикла; 
	КоличествоДеталей = Табл.Количество();

	ТабДокВрем = Новый ТабличныйДокумент;
	ТабДокВрем.ПолеСверху = 0; ТабДокВрем.ПолеСнизу  = 0;
	ТабДокВрем.ПолеСлева  = 0; ТабДокВрем.ПолеСправа = 0;
		
	Если КоличествоДеталей = 0 Тогда
		//не выводим
	Иначе
		//Вывод шапки табличной части деталей
		ОбластьДетали.Параметры.НомерДок = НомерДляПечатиДокументОснование;
		ОбластьДетали.Параметры.ДатаДок = Формат(ДокументОснование.Дата, "ДФ=дд.ММ.гггг");
		// Если помещается только шапка, но ни одной строчки, то шапку переносим на след. страницу
		ОбластьДетали.Параметры.ИмяКолонкиКодов = КолонкаКода.Синоним;
		ТабДокВрем.Вывести(ОбластьДетали);
		ОбластьМакетаСтрокаВрем = Макет.ПолучитьОбласть("СтрокаДеталей");
		СтрокаДеталей = Табл[0];
		ТекОбласть = ОбластьМакетаСтрокаВрем.Область(1, , 1, );
		//присутствуют заполненные строки (заполняем не все параметры - только наиболее вероятно длинные)
		ТекТовар = СтрокаДеталей.Номенклатура;
		
		ОбластьМакетаСтрокаВрем.Параметры.Наименование = ТекТовар.НаименованиеПолное;
		ТабДокВрем.Вывести(ОбластьМакетаСтрокаВрем);
		ТабДокВрем.Вывести(ОбластьИтоговПоСтраницеДетали);
		Попытка
			Если НЕ ТабДокумент.ПроверитьВывод(ТабДокВрем) Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
		Исключение
		КонецПопытки;
		ОбластьДетали.Параметры.НомерДок = НомерДляПечатиДокументОснование;
		ОбластьДетали.Параметры.ДатаДок = Формат(ДокументОснование.Дата, "ДФ = дд.ММ.гггг");
		ТабДокумент.Вывести(ОбластьДетали);
		
		НомерСтраницы = 2;
		НомерСтраницыПред = 2;
	
		ТекстЗаголовка = "Расходная накладная к заказ-наряду №  "+НомерДляПечатиДокументОснование+" от "+Формат(ДокументОснование.Дата, "ДФ = дд.ММ.гггг");
			
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаДеталей.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаДеталей.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		ОбластьШапкаДеталей.Параметры.ИмяКолонкиКодов = КолонкаКода.Синоним;
			
		//Вывод табличной части деталей
		Ном = 0;
		СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаНДС, СуммаСкидки, Количество", 0, 0, 0, 0);
			
		МассивОбластиПодвала = Новый Массив;
		МассивОбластиПодвала.Добавить(ОбластьПодвалДеталей);
		Для каждого СтрокаДеталей Из Табл Цикл
			Ном = Ном + 1;
			
			ОбластьСтрокаДеталей.Параметры.НомСтр = Ном;
			ТекТовар = СтрокаДеталей.Номенклатура;
			ОбластьСтрокаДеталей.Параметры.Код = дкПолучитьЗначениеКолонкиКода(ТекТовар,ИмяРеквизитаКода,ЭтотОбъект,ВыводитьПроизводителя);
			ОбластьСтрокаДеталей.Параметры.Номенклатура = ТекТовар;
			ОбластьСтрокаДеталей.Параметры.Наименование = ТекТовар.НаименованиеПолное;
			ОбластьСтрокаДеталей.Параметры.Цена = Формат(СтрокаДеталей.Цена, "ЧЦ=15; ЧДЦ=2");
			ОбластьСтрокаДеталей.Параметры.Количество = Формат(СтрокаДеталей.Количество*СтрокаДеталей.Коэффициент, ФорматВыводаКоличества);
			ОбластьСтрокаДеталей.Параметры.Единица = СтрокаДеталей.ЕдиницаИзмерения;
			ОбластьСтрокаДеталей.Параметры.Сумма = Формат(СтрокаДеталей.СуммаВсего, ФорматВыводаСуммы);
			Если ЕстьСкидкаПоДеталям Тогда
				ОбластьСтрокаДеталей.Параметры.Скидка = Формат(СтрокаДеталей.СуммаСкидки, ФорматВыводаСуммы);
			КонецЕсли;
			ОбластьСтрокаДеталей.Параметры.НДС = Формат(СтрокаДеталей.СуммаНДС, ФорматВыводаСуммы);
			
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрокаДеталей, ОбластьШапкаДеталей, ОбластьИтоговПоСтраницеДетали, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект, ?(Ном=КоличествоДеталей, МассивОбластиПодвала, Неопределено));
				
			// Обновим итоги по документу
			ИтогоКоличествоДеталей = ИтогоКоличествоДеталей + СтрокаДеталей.Количество*СтрокаДеталей.Коэффициент;
			ИтогоСуммаДеталей		= ИтогоСуммаДеталей		+ СтрокаДеталей.СуммаВсего;
			ИтогоСуммаНДСДеталей	= ИтогоСуммаНДСДеталей	+ СтрокаДеталей.СуммаНДС;
			ИтогоСуммаСкидкиДеталей	= ИтогоСуммаСкидкиДеталей	+ СтрокаДеталей.СуммаСкидки;
				
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаНДС, СуммаСкидки, Количество", 0, 0, 0, 0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаДеталей.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
				
			//обновим итоги по странице
			СтруктураИтоговПоСтранице.СуммаВсего = СтруктураИтоговПоСтранице.СуммаВсего + СтрокаДеталей.СуммаВсего;
			СтруктураИтоговПоСтранице.СуммаНДС   = СтруктураИтоговПоСтранице.СуммаНДС + СтрокаДеталей.СуммаНДС;
			СтруктураИтоговПоСтранице.Количество = СтруктураИтоговПоСтранице.Количество + СтрокаДеталей.Количество*СтрокаДеталей.Коэффициент;
			СтруктураИтоговПоСтранице.СуммаСкидки = СтруктураИтоговПоСтранице.СуммаСкидки + СтрокаДеталей.СуммаСкидки;
		КонецЦикла;
		
		//выводим последний итог по странице, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьИтоговПоСтраницеДетали, СтруктураИтоговПоСтранице, ЭтотОбъект);
		КонецЕсли;
		
		//Вывод подвала табличной части деталей
		ОбластьПодвалДеталей.Параметры.Количество = Формат(ИтогоКоличествоДеталей, ФорматВыводаКоличества);
		ОбластьПодвалДеталей.Параметры.Сумма = Формат(ИтогоСуммаДеталей, ФорматВыводаСуммы);
		ОбластьПодвалДеталей.Параметры.НДС = Формат(ИтогоСуммаНДСДеталей, ФорматВыводаСуммы);
		Если ЕстьСкидкаПоДеталям Тогда
			ОбластьПодвалДеталей.Параметры.Скидка = Формат(ИтогоСуммаСкидкиДеталей, ФорматВыводаСуммы);
		КонецЕсли;
		ОбластьПодвалДеталей.Параметры.ЧислоПрописью = обЧислоПрописью(ИтогоСуммаДеталей, ВалютаПечатногоДокумента);
		Если ИтогоСуммаНДСДеталей <> 0 Тогда
			ОбластьПодвалДеталей.Параметры.ЧислоПрописью = ОбластьПодвалДеталей.Параметры.ЧислоПрописью + " в т.ч. НДС " +Формат(ИтогоСуммаНДСДеталей, ФорматВыводаСуммы) + " " + ВалютаПечатногоДокумента;
		КонецЕсли; 
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвалДеталей, , , НомерСтраницы, , ЭтотОбъект);
	КонецЕсли;
	
	//Вывод подвала документа
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Сумма = Формат(ИтогоСуммаРабот + ИтогоСуммаДеталей, ФорматВыводаСуммы);
	ОбластьМакета.Параметры.НДС = Формат(ИтогоСуммаНДСРабот + ИтогоСуммаНДСДеталей, ФорматВыводаСуммы);
	ОбластьМакета.Параметры.ЧислоПрописью = обЧислоПрописью(ИтогоСуммаРабот + ИтогоСуммаДеталей, ВалютаПечатногоДокумента);
	Если (ИтогоСуммаНДСРабот + ИтогоСуммаНДСДеталей) <> 0 Тогда
		ОбластьМакета.Параметры.ЧислоПрописью = ОбластьМакета.Параметры.ЧислоПрописью + " в т.ч. НДС " + Формат(ИтогоСуммаНДСРабот+ИтогоСуммаНДСДеталей, ФорматВыводаСуммы) + " " + ВалютаПечатногоДокумента;
	КонецЕсли; 
	Если ПустаяСтрока(ДокументОснование.Гарантии) Тогда
		ТекОбласть = ОбластьМакета.Область(7, , 7, );
		ТекОбласть.АвтовысотаСтроки = Ложь;
		ТекОбласть.ВысотаСтроки = 1;
		ОбластьМакета.Область(7, 3, 7, 3).Текст = "";
	Иначе
		ОбластьМакета.Параметры.Гарантии = ДокументОснование.Гарантии;
	КонецЕсли;
	Если ПустаяСтрока(ДокументОснование.Рекомендации) Тогда
		ТекОбласть = ОбластьМакета.Область(9, , 9, );
		ТекОбласть.АвтовысотаСтроки = Ложь;
		ТекОбласть.ВысотаСтроки = 1;
		ОбластьМакета.Область(9, 3, 9, 3).Текст = "";
	Иначе
		ОбластьМакета.Параметры.Рекомендации = ДокументОснование.Рекомендации;	
	КонецЕсли;
	ОбластьМакета.Параметры.Мастер = ДокументОснование.Мастер;
	ОбластьМакета.Параметры.МастерПолноеНаименование = ДокументОснование.Мастер.Наименование;
	ОбластьМакета.Параметры.Заказчик = Заказчик;
	ОбластьМакета.Параметры.ЗаказчикПолноеНаименование = спПолучитьНаименование(Заказчик);
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата,"ДФ = дд.ММ.гггг");
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы, , ЭтотОбъект);
	
	// Вывод причины
	ОбластьМакета = Макет.ПолучитьОбласть("Причина");
	Если ПустаяСтрока(ДокументОснование.ПричинаОбращения) Тогда
		ТекОбласть = ОбластьМакета.Область(2, , 2, );
		ТекОбласть.АвтовысотаСтроки = Ложь;
		ТекОбласть.ВысотаСтроки = 22;
	КонецЕсли;
	ОбластьМакета.Параметры.ПричинаОбращения = ДокументОснование.ПричинаОбращения;
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы, , ЭтотОбъект);
	
	// Верхние колонтитулы
	ТабДокумент.ВерхнийКолонтитул.ВертикальноеПоложение=ВертикальноеПоложение.Низ;
	ТабДокумент.ВерхнийКолонтитул.Выводить=Истина;
	ТабДокумент.ВерхнийКолонтитул.НачальнаяСтраница=2;
	ТабДокумент.ВерхнийКолонтитул.ТекстСправа="Акт разногласий № "+НомерДляПечати+" от "+Формат(Дата,"ДФ = дд.ММ.гггг");
	ТабДокумент.ВерхнийКолонтитул.Шрифт=Новый Шрифт(ТабДокумент.ВерхнийКолонтитул.Шрифт,,,,Истина,Истина,);
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "ТОРГ-12"
// Возвращает сформированный табличный документ:
Функция ПечатьТОРГ12(ТабДокумент) Экспорт
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	// Получим валюту регламентированного учета для перерасчета цены и суммы
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ТОРГ12");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	НомерДляПечатиДокументОснование=дкПолучитьНомерДляПечати(ДокументОснование);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления1=Новый Структура(
		"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	СтруктураПредставления2=Новый Структура(
		"Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// получим р/с из свойств документа
	РасчетныйСчетДокумента=обПолучитьЗначениеСвойства(ЭтотОбъект, "РасчетныйСчет", Неопределено);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ДополнительныеПараметры.РасчетныйСчетДокумента = РасчетныйСчетДокумента;
	ОбластьМакета.Параметры.ОрганизацияПредставление= спПолучитьПредставление(
		ОбластьМакета.Параметры.ПодразделениеКомпании, СтруктураПредставления2, ДополнительныеПараметры);
	ОбластьМакета.Параметры.Поставщик				= обПолучитьЗначениеСвойства(ЭтотОбъект, "ПоставщикОрганизация", ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПоставщикПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Поставщик, СтруктураПредставления2);
	
	ОбластьМакета.Параметры.Грузополучатель				= обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузополучатель",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ГрузополучательПредставление= спПолучитьПредставление(ОбластьМакета.Параметры.Грузополучатель,СтруктураПредставления2);
	ОбластьМакета.Параметры.Плательщик					= обПолучитьЗначениеСвойства(ЭтотОбъект,"Плательщик",ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ПлательщикПредставление		= спПолучитьПредставление(ОбластьМакета.Параметры.Плательщик,СтруктураПредставления1);

	//дальше заполнить Шапку
	ОбластьМакета.Параметры.КодПоОКПО				= ЭтотОбъект.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП	= ЭтотОбъект.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ГрузополучательПоОКПО	= ОбластьМакета.Параметры.Грузополучатель.КодПоОКПО;
	ОбластьМакета.Параметры.ПоставщикПоОКПО			= ОбластьМакета.Параметры.Поставщик.КодПоОКПО;
	ОбластьМакета.Параметры.ПлательщикПоОКПО		= ОбластьМакета.Параметры.Плательщик.КодПоОКПО;
	
	Если обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
		ОбластьМакета.Параметры.ДокументОснованиеПредставление = дкПолучитьПредставление(ДокументОснование);
		ОбластьМакета.Параметры.ДокументОснование	= ДокументОснование;
	Иначе
		ОбластьМакета.Параметры.ДокументОснованиеПредставление = ДоговорВзаиморасчетов.Наименование;
		ОбластьМакета.Параметры.ДокументОснование	= ДоговорВзаиморасчетов;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ОснованиеДата	= "";
	ОбластьМакета.Параметры.ОснованиеНомер	= ""; 

	ОбластьМакета.Параметры.Номер = НомерДляПечати;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы	= 2;
	НомерСтраницыПред = НомерСтраницы;
	КоличествоСтрокНаТекущейСтранице=0;
	
	ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	//!!! TimR: В новой пф данное поле не выводится
	//ОбластьВсегоСтавкаНДС	= Макет.ПолучитьОбласть("ВсегоСтавкаНДС");

	СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);		
	
	ОбластьЗаголовокТаблицы.Параметры.валюта=ВалютаРегл;
	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);	

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ВыборкаСтрок = ЭтотОбъект.Товары.Выгрузить();
	Для каждого СтрокаТЧ Из ВыборкаСтрок Цикл
		Если СтрокаТЧ.Количество=0 Тогда
			ВыборкаСтрок.Удалить(СтрокаТЧ);
		КонецЕсли; 
	КонецЦикла; 
	
	КоличествоСтрок = ВыборкаСтрок.Количество();
	
	// инициализация итогов по документу
	ИтогоКоличество = 0;
	ИтогоСумма      = 0;
	ИтогоНДС        = 0;
	ИтогоСуммаСНДС  = 0;
	
	СоответствиеИтоговПоставкамНДС = Новый Соответствие();
	
	// Выводим многострочную часть документа
	Для каждого СтрокаТоваров Из ВыборкаСтрок Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТоваров,ЭтотОбъект);
		ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
		ОбластьСтрока.Параметры.Код							= дкПолучитьЗначениеКолонкиКода(СтрокаТоваров.Номенклатура,,ЭтотОбъект);
		ОбластьСтрока.Параметры.БазоваяЕдиницаНаименование	= СтрокаТоваров.ЕдиницаИзмерения;
		ОбластьСтрока.Параметры.БазоваяЕдиницаКодПоОКЕИ		= СтрокаТоваров.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
		ОбластьСтрока.Параметры.Количество					= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоМест				= Формат(СтрокаТоваров.Количество,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.КоличествоВОдномМесте		= Формат(СтрокаТоваров.Коэффициент,ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.ТоварНаименование 			= СтруктураСтроки.ТоварНаименование + ?(ЗначениеЗаполнено(СтрокаТоваров.Номенклатура.Артикул), ", "+СтрокаТоваров.Номенклатура.Артикул, "");
		
		// Пересчитаем сумму в соответствии с регламентированной валютой
		СуммаВсего = Окр(обПересчет(СтрокаТоваров.СуммаВсего, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаНДС   = Окр(обПересчет(СтрокаТоваров.СуммаНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата), 2);
		СуммаБезНДС = СуммаВсего - СуммаНДС;
		
		ОбластьСтрока.Параметры.СуммаВсего  = Формат(СуммаВсего, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС    = ?(СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС, "", Формат(СуммаНДС, ФорматВыводаСуммы));
		ОбластьСтрока.Параметры.СуммаБезНДС = Формат(СуммаБезНДС, ФорматВыводаСуммы);
		
		// Рассчитаем цену.
		ОбластьСтрока.Параметры.ЦенаБезНДС = Формат(СуммаБезНДС / (СтрокаТоваров.Количество * ?(СтрокаТоваров.Коэффициент = 0, 1, СтрокаТоваров.Коэффициент)), ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаСтрок.Индекс(СтрокаТоваров) = ВыборкаСтрок.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьВсего);
			//!!! TimR: В новой пф данное поле не выводится
			//мсвДопОбластиПодвала.Добавить(ОбластьВсегоСтавкаНДС);
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		Если КоличествоСтрокНаТекущейСтранице=0 Тогда
			ТабДокумент.Вывести(ОбластьСтрока);
		Иначе
			//выводим строку, делая проверку попадания на лист		
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьЗаголовокТаблицы, ОбластьИтоги,
				НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		КонецЕсли; 
		
		// увеличим итоги по странице
		СтруктураСтрокиИтогов = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице");
		СтруктураСтрокиИтогов.ИтогКоличествоПоСтранице = СтрокаТоваров.Количество;
		СтруктураСтрокиИтогов.ИтогСуммыПоСтранице      = СуммаБезНДС;
		СтруктураСтрокиИтогов.ИтогНДСПоСтранице		   = СуммаНДС;
		СтруктураСтрокиИтогов.ИтогСуммыСНДСПоСтранице  = СуммаВсего;
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ИтогКоличествоПоСтранице,ИтогСуммыПоСтранице,ИтогНДСПоСтранице,ИтогСуммыСНДСПоСтранице",0,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			КоличествоСтрокНаТекущейСтранице=0;
			ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		Иначе
			КоличествоСтрокНаТекущейСтранице=КоличествоСтрокНаТекущейСтранице+1;
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтрокиИтогов,СтруктураИтоговПоСтранице);
		
		// итоги по ставкам НДС
		Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.СтавкаНДС) Тогда
			Если СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = Неопределено Тогда
				СоответствиеИтоговПоставкамНДС.Вставить(СтрокаТоваров.СтавкаНДС,0);
			КонецЕсли;
			СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] = СоответствиеИтоговПоставкамНДС[СтрокаТоваров.СтавкаНДС] + СтрокаТоваров.СуммаНДС;
		КонецЕсли;
		
		//Сформируем итоги по документу
		ИтогоСумма      = ИтогоСумма + СуммаБезНДС;
		ИтогоНДС        = ИтогоНДС + СуммаНДС;
		ИтогоСуммаСНДС  = ИтогоСуммаСНДС + СуммаВсего;
		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 1 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтоги,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	// Выводим итоги по документу в целом
	ОбластьВсего.Параметры.ИтогКоличество = Формат(ВыборкаСтрок.Итог("Количество"), ФорматВыводаКоличества);
	ОбластьВсего.Параметры.ИтогСуммы      = Формат(ИтогоСумма, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогНДС        = Формат(ИтогоНДС, ФорматВыводаСуммы);
	ОбластьВсего.Параметры.ИтогСуммыСНДС  = Формат(ИтогоСуммаСНДС, ФорматВыводаСуммы);
	ТабДокумент.Вывести(ОбластьВсего);
	
	// Выводим подвал документа
	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(КоличествоСтрок, ,",,,,,,,,0");
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(ИтогоСуммаСНДС,ВалютаРегл);

	Доверенность = обПолучитьЗначениеСвойства(ЭтотОбъект,"Доверенность");
	Если НЕ обЗначениеНеЗаполнено(Доверенность) Тогда
		ОбластьПодвал.Параметры.ДоверенностьНомер = ?(ПустаяСтрока(Доверенность.Серия),"",Доверенность.Серия + "-") + Доверенность.Номер;
		ОбластьПодвал.Параметры.ДоверенностьДата  = Доверенность.ДатаВыдачи;
		ОбластьПодвал.Параметры.ДоверенностьВыдана= Доверенность.КемВыдан;
		ВладелецДоверенности = Доверенность.Владелец;
		Если ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Организации") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = спПолучитьНаименование(ВладелецДоверенности);
		ИначеЕсли ТипЗнч(ВладелецДоверенности)=Тип("СправочникСсылка.Сотрудники") Тогда
			ОбластьПодвал.Параметры.ДоверенностьЧерезКого = ?(обЗначениеНеЗаполнено(ВладелецДоверенности.Должность),"",спПолучитьНаименование(ВладелецДоверенности.Должность)+", ")+спПолучитьНаименование(ВладелецДоверенности);
		КонецЕсли;
	КонецЕсли;
	
	ДатаОтгрузки=обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОтгрузки",Дата('00010101'));
	ДатаОтгрузки=?(обЗначениеНеЗаполнено(ДатаОтгрузки),"""___""____________ 20___",Формат(ДатаОтгрузки,"ДФ=dd.MM.yyyy"));
	ОбластьПодвал.Параметры.ДатаОтгрузки = ДатаОтгрузки;
	
	// Заполним информацию о руководителях организации
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	ОбластьПодвал.Параметры.Заполнить(Руководитель);
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	
	Получил = дкОтветственноеЛицо(ЭтотОбъект,"ПолучилКонтрагент");
	Получил.Вставить("ПолучилПредставление", Получил.ПолучилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
КонецФункции

// Формирует печатную форму "Акт об оказании услуг"
// Возвращает сформированный табличный документ:
Функция ПечатьАктОбОказанииУслуг(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("АктОбОказанииУслуг");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	//зададим параметры макета
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ВалютаПечатногоДокумента=зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента=Неопределено Тогда Возврат Неопределено; КонецЕсли; 
	ТаблицаРабот=Работы.Выгрузить();
	зфПерерасчетТаблицыТоваров(ТаблицаРабот,ЭтотОбъект,ВалютаПечатногоДокумента,Истина);
	
	ЕстьСкидка = Истина;
	Если ТаблицаРабот.Итог("СуммаСкидки") = 0 Тогда
		ЕстьСкидка = Ложь;
		ОбластьСкидка = Макет.Область("Скидка");
		ОбластьУслуга = Макет.Область("Услуга");
		ОбластьУслуга.ШиринаКолонки = ОбластьУслуга.ШиринаКолонки + ОбластьСкидка.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьСкидка, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	
	ФорматВыводаСуммы      = обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.НомерДок = НомерДляПечати;
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата, "ДФ = дд.ММ.гггг");
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(ЭтотОбъект.Организация);
	ОбластьМакета.Параметры.ПредставлениеПолучателя = спПолучитьПредставление(ЭтотОбъект.Контрагент);
	ОбластьМакета.Параметры.ВалютаПечатногоДокумента = ВалютаПечатногоДокумента;
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
		ОбластьАвтомобиль = Макет.ПолучитьОбласть("Автомобиль");
		ОбластьАвтомобиль.Параметры.ПредставлениеАвтомобиля = СокрЛП(Автомобиль.Наименование);
		ТабДокумент.Вывести(ОбластьАвтомобиль);
	КонецЕсли;
	
	ОбластьДокументОснование = Макет.ПолучитьОбласть("ДокументОснование");
	ОбластьДокументОснование.Параметры.ДокументОснование = СокрЛП(ЭтотОбъект.ДокументОснование);
	ТабДокумент.Вывести(ОбластьДокументОснование);

	ОбластьШапка = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьШапка);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = 2;
	ТекстЗаголовка = "Акт об оказании услуг №  "+НомерДляПечати+" от "+Формат(Дата, "ДФ=дд.ММ.гггг");
		
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапка.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапка.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 

	//Вывод табличной части работ
	Ном	= 0;
	СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаНДС, СуммаСкидки", 0, 0, 0);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтогПоСтранице = Макет.ПолучитьОбласть("ИтогиПоСтранице");
	
	ВыборкаТабличнойЧасти = ТаблицаРабот;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		Если СтрокаТабличнойЧасти.Количество=0 Тогда Продолжить; КонецЕсли; 
		ОбластьСтрока.Параметры.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
		ОбластьСтрока.Параметры.Работа = спПолучитьНаименование(СтрокаТабличнойЧасти.Работа);
		ОбластьСтрока.Параметры.Количество = Формат(СтрокаТабличнойЧасти.Количество, ФорматВыводаКоличества);
		ОбластьСтрока.Параметры.Коэффициент = Формат(СтрокаТабличнойЧасти.Коэффициент, "ЧЦ=10; ЧДЦ=3");
		ОбластьСтрока.Параметры.Нормочас = СтрокаТабличнойЧасти.Нормочас;
		ОбластьСтрока.Параметры.Цена = Формат(СтрокаТабличнойЧасти.Цена, ФорматВыводаСуммы);
		Если ЕстьСкидка Тогда
			ОбластьСтрока.Параметры.СуммаСкидки = Формат(СтрокаТабличнойЧасти.СуммаСкидки, ФорматВыводаСуммы);
		КонецЕсли;
		ОбластьСтрока.Параметры.СуммаВсего = Формат(СтрокаТабличнойЧасти.СуммаВсего, ФорматВыводаСуммы);
		ОбластьСтрока.Параметры.СуммаНДС = Формат(СтрокаТабличнойЧасти.СуммаНДС, ФорматВыводаСуммы);
			
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапка, ОбластьИтогПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
			
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("СуммаВсего, СуммаНДС, СуммаСкидки", 0, 0, 0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапка.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
			
		//обновим итоги по странице
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти, СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	//выведем последний итог по странице, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент, ОбластьИтогПоСтранице, СтруктураИтоговПоСтранице, ЭтотОбъект);
	КонецЕсли;

	СуммаВсего=ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	СуммаНДС=ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");
	Если ЕстьСкидка Тогда
		ОбластьМакета.Параметры.СуммаСкидки = Формат(ВыборкаТабличнойЧасти.Итог("СуммаСкидки"), "ЧДЦ=2; ЧРГ=' '; ЧН=0,00");
	КонецЕсли;
	
	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,"ЧДЦ=2; ЧРГ=' '; ЧН=0,00");
	ОбластьМакета.Параметры.СуммаНДС = Формат(СуммаНДС,"ЧДЦ=2; ЧРГ=' '; ЧН=0,00");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего услуг "+ВыборкаТабличнойЧасти.Количество()+" на сумму "+Формат(СуммаВсего,"ЧДЦ=2; ЧРГ=' '; ЧН=0,00")+" "+ВалютаПечатногоДокумента+" (в т.ч. НДС "+Формат(СуммаНДС,"ЧДЦ=2; ЧРГ=' '; ЧН=0,00")+" "+ВалютаПечатногоДокумента+")";
	ОбластьМакета.Параметры.СуммаПрописью = обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	
	Отпустил = обПолучитьЗначениеСвойства(ЭтотОбъект,"Отпустил");
	ОбластьМакета.Параметры.ОтпустилПредставление=Отпустил.Наименование;
	
	Получил = обПолучитьЗначениеСвойства(ЭтотОбъект,"ПолучилКонтрагент");
	ОбластьМакета.Параметры.ПолучилПредставление=Получил.Наименование;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Основание<>Неопределено Тогда
		Если Основание.Состояние<>Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			#Если Клиент Тогда
				Сообщить("Ввод акта разногласий возможен только на основании закрытого заказ-наряда.",СтатусСообщения.Внимание);
			#КонецЕсли
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			ОтменитьОткрытиеФормы = Истина;
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") И Основание.Состояние <> Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
			#Если Клиент Тогда
				Сообщить("Ввод акта разногласий возможен только на основании закрытого заказ-наряда.",СтатусСообщения.Внимание);
			#КонецЕсли
			Основание=Неопределено;
			ДокументОснование=Неопределено;
			ОтменитьОткрытиеФормы = Истина;
		Иначе
			Если Основание.ВидРемонта.ТипРемонта<>Перечисления.ТипыРемонта.Платный Тогда
				Сообщить("Ввод акта разногласий возможен только на основании заказ-наряда с платным видом ремонта.",СтатусСообщения.Внимание);
				Основание=Неопределено;
				ДокументОснование=Неопределено;
				ОтменитьОткрытиеФормы = Истина;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
	Товары.Очистить();
	Работы.Очистить();
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Для каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.ПоЗаказНаряду=Истина;
		СтрокаТЧ.Подтверждение=Истина;
		СтрокаТЧ.КоличествоПоЗаказНаряду=СтрокаТЧ.Количество;
		СтрокаТЧ.ЕдиницаИзмеренияПоЗаказНаряду=СтрокаТЧ.ЕдиницаИзмерения;
		СтрокаТЧ.КоэффициентПоЗаказНаряду=СтрокаТЧ.Коэффициент;
		СтрокаТЧ.СтавкаНДСПоЗаказНаряду=СтрокаТЧ.СтавкаНДС;
		СтрокаТЧ.СуммаНДСПоЗаказНаряду=СтрокаТЧ.СуммаНДС;
		СтрокаТЧ.ПроцентСкидки=СтрокаТЧ.ПроцентСкидки+СтрокаТЧ.ПроцентСкидкиСтроки;
		СтрокаТЧ.ПроцентСкидкиСтроки=0;
		СтрокаТЧ.СуммаСкидки=СтрокаТЧ.СуммаСкидки+СтрокаТЧ.СуммаСкидкиСтроки;
		СтрокаТЧ.СуммаСкидкиСтроки=0;
		СтрокаТЧ.СуммаСкидкиПоЗаказНаряду=СтрокаТЧ.СуммаСкидки;
		СтрокаТЧ.СуммаВсегоПоЗаказНаряду=СтрокаТЧ.СуммаВсего;
	КонецЦикла; 
	Для каждого СтрокаТЧ Из Работы Цикл
		СтрокаТЧ.ПоЗаказНаряду=Истина;
		СтрокаТЧ.Подтверждение=Истина;
		СтрокаТЧ.КоличествоПоЗаказНаряду=СтрокаТЧ.Количество;
		СтрокаТЧ.НормочасПоЗаказНаряду = СтрокаТЧ.Нормочас;
		СтрокаТЧ.КоэффициентПоЗаказНаряду=СтрокаТЧ.Коэффициент;
		СтрокаТЧ.СтавкаНДСПоЗаказНаряду=СтрокаТЧ.СтавкаНДС;
		СтрокаТЧ.СуммаНДСПоЗаказНаряду=СтрокаТЧ.СуммаНДС;
		СтрокаТЧ.ПроцентСкидки=СтрокаТЧ.ПроцентСкидки+СтрокаТЧ.ПроцентСкидкиСтроки;
		СтрокаТЧ.ПроцентСкидкиСтроки=0;
		СтрокаТЧ.СуммаСкидки=СтрокаТЧ.СуммаСкидки+СтрокаТЧ.СуммаСкидкиСтроки;
		СтрокаТЧ.СуммаСкидкиСтроки=0;
		СтрокаТЧ.СуммаСкидкиПоЗаказНаряду=СтрокаТЧ.СуммаСкидки;
		СтрокаТЧ.СуммаВсегоПоЗаказНаряду=СтрокаТЧ.СуммаВсего;
	КонецЦикла; 
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	РассчитатьСуммуВсего();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	//Удалим накопление сумм
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейНакоплениеСумм.ОтменаПроведения();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	ВедетсяБалансПоПодразделению=(Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	СуммаРазногласий=Товары.Итог("СуммаВсегоРазница")+Работы.Итог("СуммаВсегоРазница");
	
	ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса=обКурсДляВалюты(ВалютаРегл,МоментВремени());
	КурсРегл=СтруктураКурса.Курс/?(СтруктураКурса.Кратность=0,1,СтруктураКурса.Кратность);
	ВалютаУпр=Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	Если ВалютаДокумента=ДоговорВзаиморасчетов.ВалютаВзаиморасчетов Тогда
		СуммаДокументаДог=СуммаРазногласий;
	ИначеЕсли ДоговорВзаиморасчетов.ВалютаВзаиморасчетов=ВалютаУпр Тогда
		СуммаДокументаДог=обПересчет(СуммаРазногласий,ВалютаДокумента,КурсДокумента,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,КурсВалютыУпр);
	Иначе
		СуммаДокументаДог=обПересчет(СуммаРазногласий,ВалютаДокумента,КурсДокумента,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,МоментВремени());
	КонецЕсли; 
	Если ВалютаДокумента=ВалютаУпр Тогда
		СуммаДокументаУпр=СуммаРазногласий;
	Иначе
		СуммаДокументаУпр=обПересчет(СуммаРазногласий,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсВалютыУпр);
	КонецЕсли; 
	Если ВалютаДокумента=ВалютаРегл Тогда
		СуммаДокументаБаз=СуммаРазногласий;
	Иначе
		СуммаДокументаБаз=обПересчет(СуммаРазногласий,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
	КонецЕсли; 
	
	// проводим взаиморасчеты
	НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
	Если СуммаРазногласий>0 Тогда
		
		//Для способа закрытия сделок по счетам и заказам - всю сумму на сделку ЗН
		НоваяЗапись=НаборЗаписейВзаиморасчеты.Добавить();
		НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
		НоваяЗапись.Период=Дата;
		НоваяЗапись.Регистратор=Ссылка;
		НоваяЗапись.Контрагент=Контрагент;
		НоваяЗапись.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		НоваяЗапись.Сделка=ДокументОснование;
		НоваяЗапись.ХозОперация=ХозОперация;
		НоваяЗапись.ВидОперации = Перечисления.ВидыОперацийВзаиморасчетов.НачислениеДебиторскойЗадолженности;
		НоваяЗапись.Сумма=СуммаДокументаДог;
		НоваяЗапись.СуммаУпр=СуммаДокументаУпр;
		НоваяЗапись.СуммаБаз=СуммаДокументаБаз;
		
	ИначеЕсли СуммаРазногласий<0 Тогда
		//При уменьшении суммы - отсторнируем движения по взаиморасчетам самого ЗН
		СуммаДокументаДог=-СуммаДокументаДог;
		СуммаДокументаУпр=-СуммаДокументаУпр;
		СуммаДокументаБаз=-СуммаДокументаБаз;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ВзаиморасчетыКомпании.Контрагент,
		             |	ВзаиморасчетыКомпании.ДоговорВзаиморасчетов,
		             |	ВзаиморасчетыКомпании.Сделка КАК Сделка,
		             |	СУММА(ВзаиморасчетыКомпании.Сумма) КАК Сумма,
		             |	СУММА(ВзаиморасчетыКомпании.СуммаУпр) КАК СуммаУпр,
		             |	СУММА(ВзаиморасчетыКомпании.СуммаБаз) КАК СуммаБаз
		             |ИЗ
		             |	РегистрНакопления.ВзаиморасчетыКомпании КАК ВзаиморасчетыКомпании
		             |ГДЕ
		             |	ВзаиморасчетыКомпании.Регистратор = &ЗаказНаряд
		             |
		             |СГРУППИРОВАТЬ ПО
		             |	ВзаиморасчетыКомпании.ДоговорВзаиморасчетов,
		             |	ВзаиморасчетыКомпании.Контрагент,
		             |	ВзаиморасчетыКомпании.Сделка,
		             |	ВзаиморасчетыКомпании.НомерСтроки
		             |
		             |ДЛЯ ИЗМЕНЕНИЯ
		             |	РегистрНакопления.ВзаиморасчетыКомпании
		             |
		             |УПОРЯДОЧИТЬ ПО
		             |	Сделка УБЫВ";
		Запрос.УстановитьПараметр("ЗаказНаряд",ДокументОснование);
		
		// Наложим блокировку на считываемые данные
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ВзаиморасчетыКомпании");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, Дата));
		//ЗначенияБлокировки.Вставить("Регистратор", ДокументОснование);
		ЗначенияБлокировки.Вставить("Контрагент", Контрагент);
		ЗначенияБлокировки.Вставить("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов); 
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки);
		
		Выборка=Запрос.Выполнить().Выбрать();
		СуммаДокументаОстаток=-СуммаРазногласий;
		Пока Выборка.Следующий() Цикл
			Если СуммаДокументаОстаток<=0 Тогда Прервать; КонецЕсли;
			Если ВалютаДокумента=ДоговорВзаиморасчетов.ВалютаВзаиморасчетов Тогда
				СуммаДвиженияДог=Мин(Выборка.Сумма,СуммаДокументаДог);
				СуммаДвиженияУпр=обПересчет(СуммаДвиженияДог,ВалютаДокумента,КурсДокумента,ВалютаУпр,КурсВалютыУпр);
				СуммаДвиженияБаз=обПересчет(СуммаДвиженияДог,ВалютаДокумента,КурсДокумента,ВалютаРегл,КурсРегл);
				СуммаДокументаОстаток=СуммаДокументаОстаток-СуммаДвиженияДог;
			ИначеЕсли ВалютаДокумента=ВалютаУпр Тогда
				СуммаДвиженияУпр=Мин(Выборка.СуммаУпр,СуммаДокументаУпр);
				СуммаДвиженияДог=обПересчет(СуммаДвиженияУпр,ВалютаУпр,КурсВалютыУпр,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,МоментВремени());
				СуммаДвиженияБаз=обПересчет(СуммаДвиженияУпр,ВалютаУпр,КурсВалютыУпр,ВалютаРегл,КурсРегл);
				СуммаДокументаОстаток=СуммаДокументаОстаток-обПересчет(СуммаДвиженияУпр,ВалютаУпр,КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
			ИначеЕсли ВалютаДокумента=ВалютаРегл Тогда
				СуммаДвиженияБаз=Мин(Выборка.СуммаБаз,СуммаДокументаБаз);
				СуммаДвиженияДог=обПересчет(СуммаДвиженияБаз,ВалютаРегл,КурсРегл,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,МоментВремени());
				СуммаДвиженияУпр=обПересчет(СуммаДвиженияБаз,ВалютаРегл,КурсРегл,ВалютаУпр,КурсВалютыУпр);
				СуммаДокументаОстаток=СуммаДокументаОстаток-обПересчет(СуммаДвиженияБаз,ВалютаРегл,КурсРегл,ВалютаДокумента,КурсДокумента);
			Иначе
				СуммаДвиженияДог=обПересчет(-СуммаРазногласий,ВалютаДокумента,КурсДокумента,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,МоментВремени());
				СуммаДвиженияДог=Мин(СуммаДвиженияДог,Выборка.Сумма);
				СуммаДвиженияУпр=обПересчет(СуммаДвиженияДог,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,МоментВремени(),ВалютаУпр,КурсВалютыУпр);
				СуммаДвиженияБаз=обПересчет(СуммаДвиженияДог,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,МоментВремени(),ВалютаРегл,КурсРегл);
				СуммаДокументаОстаток=СуммаДокументаОстаток-обПересчет(СуммаДвиженияДог,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов,МоментВремени(),ВалютаДокумента,КурсДокумента);
			КонецЕсли; 
			НоваяЗапись=НаборЗаписейВзаиморасчеты.Добавить();
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период=Дата;
			НоваяЗапись.Регистратор=Ссылка;
			НоваяЗапись.Контрагент=Выборка.Контрагент;
			НоваяЗапись.ДоговорВзаиморасчетов=Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.Сделка=Выборка.Сделка;
			НоваяЗапись.ХозОперация=ХозОперация;
			НоваяЗапись.ВидОперации = Перечисления.ВидыОперацийВзаиморасчетов.НачислениеДебиторскойЗадолженности;
			НоваяЗапись.Сумма=-СуммаДвиженияДог;
			НоваяЗапись.СуммаУпр=-СуммаДвиженияУпр;
			НоваяЗапись.СуммаБаз=-СуммаДвиженияБаз;
			СуммаДокументаДог=СуммаДокументаДог-СуммаДвиженияДог;
			СуммаДокументаУпр=СуммаДокументаУпр-СуммаДвиженияУпр;
			СуммаДокументаБаз=СуммаДокументаБаз-СуммаДвиженияБаз;
		КонецЦикла;
	КонецЕсли;
	СуммаДоходаРасходаСуммовыхРазниц=0;
	тзНаборЗаписейВзаиморасчеты=НаборЗаписейВзаиморасчеты.Выгрузить();
	тзНаборЗаписейВзаиморасчеты.Свернуть("Контрагент,ДоговорВзаиморасчетов,Сделка");
	Для каждого СделкаВзаиморасчетов Из тзНаборЗаписейВзаиморасчеты Цикл
		НаборЗаписейВзаиморасчеты.Контрагент=СделкаВзаиморасчетов.Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=СделкаВзаиморасчетов.ДоговорВзаиморасчетов;
		НаборЗаписейВзаиморасчеты.Сделка=СделкаВзаиморасчетов.Сделка;
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок=АвтоЗакрытиеСделок;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.ШапкаДокумента=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.МоментВремени=МоментВремени();
		НаборЗаписейВзаиморасчеты.СписаниеКурсовойРазницыБаз(СделкаВзаиморасчетов.ДоговорВзаиморасчетов,СделкаВзаиморасчетов.Контрагент,СделкаВзаиморасчетов.Сделка);
		СуммаДоходаРасходаСуммовыхРазниц=СуммаДоходаРасходаСуммовыхРазниц+НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
	КонецЦикла; 
	Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
		НаборЗаписейДиР = Движения.ДоходыИРасходы;
		НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
		Если ВедетсяБалансПоПодразделению Тогда
			НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДиР.ВУпрВалюте = Истина;
		Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
			НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
		Иначе
			НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
	КонецЕсли;
	Если обПраво("РежимСписанияСуммовыхРазниц",Права,,ЭтотОбъект)=Перечисления.РежимыРегламентныхОпераций.ПриПроведенииДокумента Тогда
		НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		НаборЗаписейВзаиморасчеты.АвтоЗакрытиеСделок=АвтоЗакрытиеСделок;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.ШапкаДокумента=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.МоментВремени=МоментВремени();
		НаборЗаписейВзаиморасчеты.СписаниеСуммовыхРазниц();
	КонецЕсли;
	НаборЗаписейВзаиморасчеты.МоментВремени=Неопределено;
	НаборЗаписейВзаиморасчеты.ДокументОбъект=Неопределено;
	НаборЗаписейВзаиморасчеты.ШапкаДокумента=Неопределено;
	
	// проведем партии товаров
	Отказ = НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	Если (НЕ Отказ) И (ВидРемонта.ТипРемонта=Перечисления.ТипыРемонта.Платный) Тогда
		//Сформируем сумму накопления
		НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
		НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейНакоплениеСумм.Контрагент=Контрагент;
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(АктРазногласийТовары.КоличествоРазница),0) КАК Количество
		|ИЗ
		|	Документ.АктРазногласий.Товары КАК АктРазногласийТовары
		|ГДЕ
		|	АктРазногласийТовары.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			НаборЗаписейНакоплениеСумм.КоличествоНоменклатуры=Выборка.Количество;
		Иначе
			НаборЗаписейНакоплениеСумм.КоличествоНоменклатуры=0;
		КонецЕсли; 
		НаборЗаписейНакоплениеСумм.Карточка=Справочники.Карточки.ПустаяСсылка();
		НаборЗаписейНакоплениеСумм.Сторно=Ложь;
		Отказ=НЕ НаборЗаписейНакоплениеСумм.НакоплениеСуммы() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ОбработкаРасчетСкидок=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.ИмяРеквизитаСкладКомпании="Цех";
ОбработкаРасчетСкидок.НеРассчитыватьСтрочныеСкидки=Истина;
ОбработкаРасчетСкидок.НеРассчитыватьАвтоматическиеСкидки=Истина;
ОбработкаРасчетСкидокАвторабот=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаСкладКомпании="Цех";
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаСкидкаНаценка="СкидкаНаценкаРаботы";
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаЗначениеСкидкиНаценки="ЗначениеСкидкиНаценкиРабот";
ОбработкаРасчетСкидокАвторабот.ИмяРеквизитаСуммаСкидкиНаценки="СуммаСкидкиНаценкиРабот";
ОбработкаРасчетСкидокАвторабот.ИмяТабличнойЧасти="Работы";
ОбработкаРасчетСкидокАвторабот.СкидкаНаРаботы=Истина;
ОбработкаРасчетСкидокАвторабот.НеРассчитыватьСтрочныеСкидки=Истина;
ОбработкаРасчетСкидокАвторабот.НеРассчитыватьАвтоматическиеСкидки=Истина;

ФорматПредставленияГодаВыпускаАвтомобиля=орПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Права);
ОтменитьОткрытиеФормы = Ложь;

// Получение сделки по документу основания
//
// Параметры
//  Сделка  - ДокументСсылка - Ссылка на документ основания.
//
// Возвращаемое значение:
//   ДокументСсылка   - Сделка в цепочке документов.
//
Функция ПолучитьСделку(Знач Сделка) Экспорт 
	Если (ЗначениеЗаполнено(Сделка)) И ТипЗнч(Сделка) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		Сделка = ПолучитьСделку(Сделка.ДокументОснование);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Сделка) Тогда
		Сделка = Неопределено;
	КонецЕсли; 
	Возврат Сделка;
КонецФункции // ПолучитьСделку()

// Определяет возможен ли ввод корректировки на основании данного документа основания.
//
// Параметры:
//	Основание - документ поступление товаров или корректировки поступления.
//
Функция КорректировкаНеДоступна(Основание) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИСТИНА КАК ЕстьВозврат
	|ИЗ
	|	Документ.ВозвратПоставщику КАК ТаблицаОбъекта
	|ГДЕ
	|	ТаблицаОбъекта.ДокументОснование = &Основание
	|	И ТаблицаОбъекта.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Документ.ВозвратПоставщику.Товары КАК ТаблицаОбъекта
	|ГДЕ
	|	ТаблицаОбъекта.Партия = &Основание
	|	И ТаблицаОбъекта.Ссылка.Проведен
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Основание", Основание);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // КорректировкаНеДоступна()

// Возвращает все документы, в которых присутствуют коды маркировки.
//
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - выборка результата запроса, содержащая требуемые документы.
//
Функция ДокументыСКодамиМаркировки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КорректировкаПоступленияКодыМаркировки.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаПоступления.КодыМаркировки КАК КорректировкаПоступленияКодыМаркировки
	|ГДЕ
	|	КорректировкаПоступленияКодыМаркировки.КодМаркировки <> """"";
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ПоступлениеАвтомобили - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами(Объект) Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	ДанныеКонтрагента = Объект.Контрагент.СтранаРегистрации.УчастникЕАЭС;
	
	// Для контрагентов из ЕАЭС другая форма отчетности
	Если ДанныеКонтрагента Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ОсвобождентОтНДС = Объект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Объект.Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Объект.Товары.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	Если Объект.ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияИсправлениеВПервичныхДокументах Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УПД) КАК ВидДокумента,
		|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПоступлениеТоваров) КАК КодОперации,
		|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
		|	СУММА(КорректировкаПоступленияТовары.Количество * КорректировкаПоступленияТовары.Коэффициент) КАК КоличествоПрослеживаемости,
		|	КорректировкаПоступленияТовары.ГТД КАК РНПТ,
		|	КорректировкаПоступленияТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(КорректировкаПоступленияТовары.СуммаВсего - КорректировкаПоступленияТовары.СуммаНДС) КАК СуммаБезНДС
		|ИЗ
		|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
		|ГДЕ
		|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
		|	И КорректировкаПоступленияТовары.ГТД.РНПТ
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаПоступленияТовары.Номенклатура,
		|	КорректировкаПоступленияТовары.ГТД,
		|	КорректировкаПоступленияТовары.СтавкаНДС";
		
		Документ = Объект.Сделка;
		Если ЗначениеЗаполнено(Объект.Сделка) Тогда
			Если ТипЗнч(Объект.Сделка) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
				ДанныеПоступления = Новый Структура("ВхДокНомер,ВхДокДата,Дата,Номер,Контрагент",
                	Объект.Сделка.ВхДокНомер,
					Объект.Сделка.ВхДокДата,
					Объект.Сделка.Дата,
					Объект.Сделка.Номер,
					Объект.Сделка.Контрагент
				);
				ПериодОтчета = НачалоКвартала(ДанныеПоступления.Дата);
				НомерДокумента = ДанныеПоступления.ВхДокНомер;
				ДатаДокумента = ДанныеПоступления.ВхДокДата;
				КонтрагентОперации = ДанныеПоступления.Контрагент;
			Иначе
				ДанныеПоступления = Новый Структура("Дата,Номер,Контрагент",
					Объект.Сделка.Дата,
					Объект.Сделка.Номер,
					Объект.Сделка.Контрагент
				);
				ПериодОтчета = НачалоКвартала(ДанныеПоступления.Дата);
				НомерДокумента = Объект.ВхДокНомер;
				ДатаДокумента = Объект.ВхДокДата;
				КонтрагентОперации = ДанныеПоступления.Контрагент;
			КонецЕсли;
		Иначе
			ПериодОтчета = НачалоКвартала(Объект.Дата);
			КонтрагентОперации = Объект.Контрагент;
		КонецЕсли;
	Иначе
		ТекстЗапроса = "ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УКД) КАК ВидДокумента,
		|	ВЫБОР
		|		КОГДА КорректировкаПоступленияТовары.СуммаВсегоРазница > 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочнаяСчетФактураУвеличение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочнаяСчетФактураУменьшение)
		|	КОНЕЦ КАК КодОперации,
		|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
		|	СУММА(КорректировкаПоступленияТовары.КоличествоРазница * ВЫБОР
		|			КОГДА КорректировкаПоступленияТовары.КоличествоРазница < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ * КорректировкаПоступленияТовары.Коэффициент) КАК КоличествоПрослеживаемости,
		|	КорректировкаПоступленияТовары.ГТД КАК РНПТ,
		|	КорректировкаПоступленияТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(КорректировкаПоступленияТовары.СуммаВсегоРазница * ВЫБОР
		|			КОГДА КорректировкаПоступленияТовары.СуммаВсегоРазница < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ - КорректировкаПоступленияТовары.СуммаНДСРазница * ВЫБОР
		|			КОГДА КорректировкаПоступленияТовары.СуммаНДСРазница < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК СуммаБезНДС
		|ИЗ
		|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
		|ГДЕ
		|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
		|	И КорректировкаПоступленияТовары.ГТД.РНПТ
		|	И КорректировкаПоступленияТовары.СуммаВсегоРазница <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаПоступленияТовары.Номенклатура,
		|	КорректировкаПоступленияТовары.ГТД,
		|	КорректировкаПоступленияТовары.СтавкаНДС,
		|	ВЫБОР
		|		КОГДА КорректировкаПоступленияТовары.СуммаВсегоРазница > 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочнаяСчетФактураУвеличение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолученнаяКорректировочнаяСчетФактураУменьшение)
		|	КОНЕЦ";
		Документ = Объект.Ссылка;
		ПериодОтчета = НачалоКвартала(Объект.Дата);
		НомерДокумента = Объект.ВхДокНомер;
		ДатаДокумента = Объект.ВхДокДата;
		КонтрагентОперации = Объект.Контрагент;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Проверим, что в документе есть РНПТ
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ТаблицаРНПТ = Запрос.Выполнить().Выгрузить();
	
	// Зафиксируем данные для заполнения
	Организация = Объект.Организация;
	
	// Для пересчета валюты в рубли
	ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаРегл, Объект.Дата);
	КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ВалютаДокумента = Объект.ВалютаДокумента;
	КурсДокумента = Объект.КурсДокумента;
	ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
	
	// Сформируем таблицу
	Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.ПериодОтчета = ПериодОтчета;
		НоваяСтрока.Документ = Документ;
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.ДатаДокумента = ДатаДокумента;
		НоваяСтрока.Контрагент = КонтрагентОперации;
		
		Если ВалютаНеРегл Тогда
			НоваяСтрока.СуммаБезНДС = Окр(
				обПересчет(НоваяСтрока.СуммаБезНДС, ВалютаДокумента,
					КурсДокумента, ВалютаРегл, КурсРегл), 2);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

// получить удаляемые кнопки
Функция УдаляемыеКнопкиЗаполнения() Экспорт
	
	УдаляемыеКнопки = Новый Массив();
	УдаляемыеКнопки.Добавить("ЗаполнитьИзКорзины");
	УдаляемыеКнопки.Добавить("ВыгрузитьВКорзину");
	УдаляемыеКнопки.Добавить("ЗаполнитьИзТСД");
	УдаляемыеКнопки.Добавить("ЗаполнитьИзФайла");
	
	Возврат УдаляемыеКнопки;
	
КонецФункции

#Область ИнформацияОДокументе

//************************//
// ИНФОРМАЦИЯ О ДОКУМЕНТЕ //
//************************//


// Возвращает доступные варианты печати документа
// Параметры:
//	ДокументСсылка - Ссылка на документ печати
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати. Первый элемент структуры - форма по умолчанию
Функция ПолучитьСписокПечатныхФорм(ДокументСсылка) Экспорт
	
	СтруктураМакетов = Новый Структура();
	
	СтруктураМакетов.Вставить("ПоступлениеТоваров", "Приходная накладная");
	
	СтруктураМакетов.Вставить("УКД", "Универсальный корректировочный документ");
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСписокПечатныхФорм()


#КонецОбласти
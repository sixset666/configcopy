// Модуль документа Корректировка поступления

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;                            // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;               // Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

Перем ТекущаяВалютаДокумента Экспорт;           // Текущая валюта документа перед изменением
Перем ТекущийКурсДокумента Экспорт;             //Текущий курс документа перед изменением
Перем СуммаРазногласий;                         //Текущая сумма разногласий

Перем ИмяРеквизитаКода Экспорт;                 // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	
	Запрос=Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	/////////// ПРИВАТ ////////////
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.СкладКомпании КАК СкладКомпании,
	|	&СуммаРазногласий КАК СуммаДокумента,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.Сделка КАК Сделка
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("СуммаРазногласий",Товары.Итог("СуммаВсегоРазница"));
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
	
КонецФункции // ПолучитьШапкуДокумента()

// возвращает таблицу списания номенклатуры
Функция ПолучитьТаблицуНоменклатуры()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	СУММА(КорректировкаПоступленияТовары.КоличествоПоДокументуПоступления * КорректировкаПоступленияТовары.КоэффициентПоДокументуПоступления) КАК КоличествоПоДокументуПоступления,
	|	СУММА(КорректировкаПоступленияТовары.Количество * КорректировкаПоступленияТовары.Коэффициент) КАК Количество,
	|	СУММА(КорректировкаПоступленияТовары.КоличествоРазница * КорректировкаПоступленияТовары.Коэффициент) КАК КоличествоРазница,
	|	КорректировкаПоступленияТовары.СтавкаНДС,
	|	СУММА(КорректировкаПоступленияТовары.СуммаНДСПоДокументуПоступления) КАК СуммаНДСПоДокументуПоступления,
	|	СУММА(КорректировкаПоступленияТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(КорректировкаПоступленияТовары.СуммаВсего) КАК СуммаВсего,
	|	СУММА(КорректировкаПоступленияТовары.СуммаВсегоПоДокументуПоступления) КАК СуммаВсегоПоДокументуПоступления,
	|	СУММА(КорректировкаПоступленияТовары.СуммаРозничнаяПоДокументуПоступления) КАК СуммаРозничнаяПоДокументуПоступления,
	|	СУММА(КорректировкаПоступленияТовары.СуммаРозничная) КАК СуммаРозничная,
	|	КорректировкаПоступленияТовары.ЦенаРозничная,
	|	СУММА(КорректировкаПоступленияТовары.СуммаВсегоРазница) КАК СуммаВсегоРазница,
	|	СУММА(КорректировкаПоступленияТовары.СуммаРозничнаяРазница) КАК СуммаРозничнаяРазница,
	|	СУММА(КорректировкаПоступленияТовары.СуммаНДСРазница) КАК СуммаНДСРазница
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	(КорректировкаПоступленияТовары.КоличествоРазница <> 0
	|			ИЛИ КорректировкаПоступленияТовары.СуммаРозничнаяРазница <> 0
	|			ИЛИ КорректировкаПоступленияТовары.СуммаВсегоРазница <> 0
	|			ИЛИ КорректировкаПоступленияТовары.СуммаНДСРазница <> 0)
	|	И КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.СтавкаНДС,
	|	КорректировкаПоступленияТовары.ЦенаРозничная";
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьТаблицуНоменклатуры()

Процедура ВыполнитьДвиженияПоСебестоимости(ТаблицаСебестоимости)
	
	
	Если ТаблицаСебестоимости.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	// Спишем себестоимость ранее списанного товара
	Для Каждого ТекущаяСтрока Из ТаблицаСебестоимости Цикл
		
		НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСтрока);
		НоваяЗапись.Период = Дата;
		НоваяЗапись.Регистратор   = Ссылка;
		НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
		НоваяЗапись.СкладКомпании = СкладКомпании;
		НоваяЗапись.ХозОперация = ХозОперация;
		НоваяЗапись.Проект = Проект;
		НоваяЗапись.Количество = 0;
		НоваяЗапись.СуммаБезНДС    = НоваяЗапись.Сумма - НоваяЗапись.СуммаНДС;
		НоваяЗапись.СуммаБезНДСУпр = НоваяЗапись.СуммаУпр - НоваяЗапись.СуммаНДСУпр;
		
	КонецЦикла;
	
	// Получим движения по регистрам продаж для актуализации себестоимости
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТаблицаСебестоимости ИЗ &ТаблицаСебестоимости КАК ТаблицаСебестоимости";
	Запрос.УстановитьПараметр("ТаблицаСебестоимости", ТаблицаСебестоимости);
	Запрос.Выполнить();

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Продажи.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	Продажи.Автомобиль КАК Автомобиль,
		|	Продажи.Номенклатура КАК Номенклатура,
		|	Продажи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Продажи.Поставщик КАК Поставщик,
		|	Продажи.Покупатель КАК Покупатель,
		|	Продажи.СтатусПартии КАК СтатусПартии,
		|	Продажи.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	Продажи.СкладКомпании КАК СкладКомпании,
		|	Продажи.СтавкаНДС КАК СтавкаНДС,
		|	Продажи.Партия КАК Партия,
		|	Продажи.Проект КАК Проект,
		|	Продажи.ГТД КАК ГТД,
		|	Продажи.ДокументПродажи КАК ДокументПродажи,
		|	Продажи.Количество КАК КоличествоРеализации,
		|	ТаблицаСебестоимости.Количество КАК Количество,
		|	ТаблицаСебестоимости.Сумма КАК Сумма,
		|	ТаблицаСебестоимости.СуммаУпр КАК СуммаУпр,
		|	ТаблицаСебестоимости.СуммаНДС КАК СуммаНДС,
		|	ТаблицаСебестоимости.СуммаНДСУпр КАК СуммаНДСУпр
		|ИЗ
		|	ТаблицаСебестоимости КАК ТаблицаСебестоимости
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи КАК Продажи
		|		ПО ТаблицаСебестоимости.Номенклатура = Продажи.Номенклатура
		|			И ТаблицаСебестоимости.ХарактеристикаНоменклатуры = Продажи.ХарактеристикаНоменклатуры
		|			И ТаблицаСебестоимости.Партия = Продажи.Партия
		|			И ТаблицаСебестоимости.СтатусПартии = Продажи.СтатусПартии
		|			И (Продажи.Период <= &ДатаДокумента)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Продажи.Период УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	МАКСИМУМ(СуммаУпр),
		|	МАКСИМУМ(СуммаНДС),
		|	МАКСИМУМ(СуммаНДСУпр)
		|ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры";
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ВыборкаХарактеристикиНоменклатуры = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаХарактеристикиНоменклатуры.Следующий() Цикл
			
			КоличествоРаспределить = ВыборкаХарактеристикиНоменклатуры.Количество;
			СуммаРаспределить = ВыборкаХарактеристикиНоменклатуры.Сумма;
			СуммаУпрРаспределить = ВыборкаХарактеристикиНоменклатуры.СуммаУпр;
			СуммаНДСРаспределить = ВыборкаХарактеристикиНоменклатуры.СуммаНДС;
			СуммаНДСУпрРаспределить = ВыборкаХарактеристикиНоменклатуры.СуммаНДСУпр;
			
			Выборка = ВыборкаХарактеристикиНоменклатуры.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				НоваяЗапись = Движения.Продажи.Добавить();
				НоваяЗапись.Период = Дата;
				НоваяЗапись.Регистратор = Ссылка;
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка,, "Количество,Сумма,СуммаУпр,СуммаНДС,СуммаНДСУпр");
				
				Если КоличествоРаспределить <= Выборка.КоличествоРеализации Тогда
					
					НоваяЗапись.Себестоимость = СуммаРаспределить;
					НоваяЗапись.СуммаНДСВходящий = СуммаНДСРаспределить;
					НоваяЗапись.СебестоимостьБезНДС = НоваяЗапись.Себестоимость - НоваяЗапись.СуммаНДСВходящий;
					НоваяЗапись.СебестоимостьУпр = СуммаУпрРаспределить;
					НоваяЗапись.СуммаНДСВходящийУпр = СуммаНДСУпрРаспределить;
					НоваяЗапись.СебестоимостьБезНДСУпр = НоваяЗапись.СебестоимостьУпр - НоваяЗапись.СуммаНДСВходящийУпр;
					КоличествоРаспределить = 0;
					
				Иначе
					
					Себестоимость = Окр(СуммаРаспределить / КоличествоРаспределить * Выборка.КоличествоРеализации, 2);
					СуммаНДСВходящий = Окр(СуммаНДСРаспределить / КоличествоРаспределить * Выборка.КоличествоРеализации, 2);
					СебестоимостьУпр = Окр(СуммаУпрРаспределить / КоличествоРаспределить * Выборка.КоличествоРеализации, 2);
					СуммаНДСВходящийУпр = Окр(СуммаНДСУпрРаспределить / КоличествоРаспределить * Выборка.КоличествоРеализации, 2);
					НоваяЗапись.Себестоимость = Себестоимость;
					НоваяЗапись.СуммаНДСВходящий = СуммаНДСВходящий;
					НоваяЗапись.СебестоимостьБезНДС = НоваяЗапись.Себестоимость - НоваяЗапись.СуммаНДСВходящий;
					НоваяЗапись.СебестоимостьУпр = СебестоимостьУпр;
					НоваяЗапись.СуммаНДСВходящийУпр = СуммаНДСВходящийУпр;
					НоваяЗапись.СебестоимостьБезНДСУпр = НоваяЗапись.СебестоимостьУпр - НоваяЗапись.СуммаНДСВходящийУпр;
					КоличествоРаспределить = КоличествоРаспределить - Выборка.КоличествоРеализации;
					СуммаРаспределить = СуммаРаспределить - Себестоимость;
					СуммаНДСРаспределить = СуммаНДСРаспределить - СуммаНДСВходящий;
					СуммаУпрРаспределить = СуммаУпрРаспределить - СебестоимостьУпр;
					СуммаНДСУпрРаспределить = СуммаНДСУпрРаспределить - СуммаНДСВходящийУпр;
					
				КонецЕсли;
				
				// Зафиксируем себестоимость в реализованных товарах
				Если Выборка.СтатусПартии <> Перечисления.СтатусыПартий.ТоварКупленный
					И Сделка.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия
					И ДоговорВзаиморасчетов.КомиссияОтчетНаОснованииПродаж Тогда
					
					НоваяЗаписьРеализованныеТовары = Движения.РеализованныеТовары.Добавить();
					НоваяЗаписьРеализованныеТовары.Период = Дата;
					НоваяЗаписьРеализованныеТовары.Регистратор = Ссылка;
					НоваяЗаписьРеализованныеТовары.Контрагент = Контрагент;
					НоваяЗаписьРеализованныеТовары.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
					НоваяЗаписьРеализованныеТовары.Номенклатура = Выборка.Номенклатура;
					НоваяЗаписьРеализованныеТовары.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
					НоваяЗаписьРеализованныеТовары.ДокументПередачи = Сделка;
					НоваяЗаписьРеализованныеТовары.ГТД = Выборка.ГТД;
					НоваяЗаписьРеализованныеТовары.СуммаУпр = НоваяЗапись.СебестоимостьУпр;
					НоваяЗаписьРеализованныеТовары.СуммаНДСУпр = НоваяЗапись.СуммаНДСВходящийУпр;
					НоваяЗаписьРеализованныеТовары.СуммаБезНДСУпр = НоваяЗапись.СебестоимостьБезНДСУпр;
					НоваяЗаписьРеализованныеТовары.СуммаРегл = НоваяЗапись.Себестоимость;
					НоваяЗаписьРеализованныеТовары.СуммаНДС = НоваяЗапись.СуммаНДСВходящий;
					НоваяЗаписьРеализованныеТовары.СуммаБезНДС = НоваяЗапись.СебестоимостьБезНДС;
					
				КонецЕсли;
					
				Если КоличествоРаспределить = 0 Тогда
					
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Движения.Продажи.Записать();
	Движения.РеализованныеТовары.Записать();
	
КонецПроцедуры

Процедура СформироватьРазницуСебестоимостиТоваров(ТаблицаСебестоимости, стрНоменклатура, НоваяЗапись)
	
	// Проверим не изменилась ли себестоимость товаров
	Если стрНоменклатура.КоличествоПоДокументуПоступления = 0 Тогда
		Возврат
	КонецЕсли;
	
	СуммаЗаЕдиницуТовараПоступления =
		Окр(стрНоменклатура.СуммаВсегоПоДокументуПоступления / стрНоменклатура.КоличествоПоДокументуПоступления, 2);
	СуммаЗаЕдиницуТовара = Окр(стрНоменклатура.СуммаВсего / стрНоменклатура.Количество, 2);
	КорректировкаСуммы = (СуммаЗаЕдиницуТовара <> СуммаЗаЕдиницуТовараПоступления);
	
	СуммаНДСЗаЕдиницуТовараПоступления =
		Окр(стрНоменклатура.СуммаНДСПоДокументуПоступления / стрНоменклатура.КоличествоПоДокументуПоступления, 2);
	СуммаНДСЗаЕдиницуТовара = Окр(стрНоменклатура.СуммаНДС / стрНоменклатура.Количество, 2);
	КорректировкаСуммыНДС = (СуммаНДСЗаЕдиницуТовара <> СуммаНДСЗаЕдиницуТовараПоступления);
	
	Если НЕ КорректировкаСуммы И НЕ КорректировкаСуммыНДС Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СуммаУпрДок = обПересчет(
		стрНоменклатура.СуммаВсего,
		ВалютаДокумента,
		Дата,
		Константы.ВалютаУправленческогоУчетаКомпании.Получить(),
		Дата,
		РежимОкругления.Окр15как20);
	СуммаРеглДок = обПересчет(
		стрНоменклатура.СуммаВсего,
		ВалютаДокумента,
		Дата,
		Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),
		Дата,
		РежимОкругления.Окр15как20);
	СуммаУпрДокНДС = обПересчет(
		стрНоменклатура.СуммаНДС,
		ВалютаДокумента,
		Дата,
		Константы.ВалютаУправленческогоУчетаКомпании.Получить(),
		Дата,
		РежимОкругления.Окр15как20);
	СуммаРеглДокНДС = обПересчет(
		стрНоменклатура.СуммаНДС,
		ВалютаДокумента,
		Дата,
		Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),
		Дата,
		РежимОкругления.Окр15как20);
		
	СуммаУпрПоступленияНДС = обПересчет(
		стрНоменклатура.СуммаНДСПоДокументуПоступления,
		ВалютаДокумента,
		Дата,
		Константы.ВалютаУправленческогоУчетаКомпании.Получить(),
		Дата,
		РежимОкругления.Окр15как20);
	СуммаРеглПоступленияНДС = обПересчет(
		стрНоменклатура.СуммаНДСПоДокументуПоступления,
		ВалютаДокумента,
		Дата,
		Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),
		Дата,
		РежимОкругления.Окр15как20);
		
		
	НоваяСтрока = ТаблицаСебестоимости.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, стрНоменклатура);
	НоваяСтрока.СтатусПартии = НоваяЗапись.СтатусПартии;
	НоваяСтрока.Количество = стрНоменклатура.КоличествоПоДокументуПоступления;
	НоваяСтрока.Партия = НоваяЗапись.Партия;
	НоваяСтрока.Сумма = ?(НЕ КорректировкаСуммы, 0,
		НоваяЗапись.Сумма - Окр(СуммаРеглДок / стрНоменклатура.Количество
		* (стрНоменклатура.Количество - стрНоменклатура.КоличествоПоДокументуПоступления), 2));
	НоваяСтрока.СуммаУпр = ?(НЕ КорректировкаСуммы, 0,
		НоваяЗапись.СуммаУпр - Окр(СуммаУпрДок / стрНоменклатура.Количество
		* стрНоменклатура.КоличествоРазница, 2));
	НоваяСтрока.СуммаНДС = ?(НЕ КорректировкаСуммыНДС, 0,
		Окр(СуммаРеглДокНДС / стрНоменклатура.Количество
		* стрНоменклатура.КоличествоПоДокументуПоступления, 2) - СуммаРеглПоступленияНДС);
	НоваяСтрока.СуммаНДСУпр = ?(НЕ КорректировкаСуммыНДС, 0,
		Окр(СуммаУпрДокНДС / стрНоменклатура.Количество
		* стрНоменклатура.КоличествоПоДокументуПоступления, 2) - СуммаУпрПоступленияНДС);
	
КонецПроцедуры

// Обработчик события объекта возникает в момент, когда выполняется установка нового номера.
//
// Параметры:
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения системной обработки события.
//  Префикс              - Строка - Префикс, который будет использоваться для генерации номера.
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения объекта копированием.
//
// Параметры:
//  ОбъектКопирования - ДокументОбъект - Исходный объект, который является источником копирования.
//
Процедура ПриКопировании(ОбъектКопирования)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
	
	КодыМаркировки.Очистить();
	ВхДокНомер = "";
	ВхДокДата = Дата(1,1,1);
	
КонецПроцедуры // ПриКопировании()

// Обработчик события возникающего после начала транзакции записи, но до начала записи объекта.
//
// Параметры:
//  Отказ           - Булево                   - Признак отказа от совершения операции.
//  РежимЗаписи     - РежимЗаписиДокумента     - Текущий режим записи документа.
//  РежимПроведения - РежимПроведенияДокумента - Текущий режим проведения.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Проверим хоз. операции
	// составим список документов движений.
	Если ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияКорректировкаПоСогласованиюСторон Тогда
		
		// заполним значение до корректировки
		Для Каждого стрТовары Из Товары Цикл
			стрТовары.КоличествоДоКорректировки = стрТовары.КоличествоПоДокументуПоступления;
			стрТовары.СуммаВсегоДоКорректировки = стрТовары.СуммаВсегоПоДокументуПоступления;
			стрТовары.СуммаНДСДоКорректировки   = стрТовары.СуммаНДСПоДокументуПоступления;
		КонецЦикла;
		
	КонецЕсли;
	
	// Вызываем общий обработчик события
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияИсправлениеВПервичныхДокументах Тогда
		
		// проверим введенные на основании корректировки
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодчиненныеДокументы.Ссылка КАК Документ
		|ИЗ
		|	КритерийОтбора.ПодчиненныеДокументы(&Основание) КАК ПодчиненныеДокументы
		|ГДЕ
		|	ПодчиненныеДокументы.Ссылка ССЫЛКА Документ.КорректировкаПоступления
		|	И НЕ ПодчиненныеДокументы.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.КорректировкаПоступленияКорректировкаПоСогласованиюСторон)
		|	И НЕ ПодчиненныеДокументы.Ссылка = &Ссылка";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Основание", ДокументОснование);
		Запрос.УстановитьПараметр("Ссылка",    Ссылка);
		ВыполнениеЗапроса = Запрос.Выполнить();
		
		Если НЕ ВыполнениеЗапроса.Пустой() Тогда
			ТекстСообщения = НСтр("ru = 'К документу " + Строка(ДокументОснование) + " введено больше одного корректировочного документа с хоз. операцей ""Исправление первичных документов"". 
								|Каждую последующую корректировку следует вводить на основании предыдущей.'");
			Сообщить(ТекстСообщения, СтатусСообщения.Внимание);
		КонецЕсли;
		
	КонецЕсли;
	
	// Произведем очистку не использующихся в текущем контексте реквизитов
	Если НЕ СкладКомпании.Розничный Тогда
		Для Каждого СтрокаТЧ Из Товары Цикл 
			СтрокаТЧ.ЦенаРозничная = 0;
			СтрокаТЧ.СуммаРозничная = 0;
			СтрокаТЧ.СуммаРозничнаяПоДокументуПоступления = 0;
			СтрокаТЧ.СуммаРозничнаяДоКорректировки = 0;
			СтрокаТЧ.СуммаРозничнаяРазница = 0;
		КонецЦикла;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда

		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	РассчитатьСуммуВсего();
	
КонецПроцедуры

// Обработчик события возникающего после записи объекта в базу данных, но до окончания транзакции записи.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ПриЗаписи(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

// Обработчик события возникающего в транзакции удаления перед непосредственным удалением объекта из базы данных.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ПередУдалением(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередУдалением()

// Обработчик события возникающего при отмене проведения документа. Выполняется в транзакции записи.
//
// Параметры:
//  Отказ - Булево - Признак отказа от совершения операции.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Вызываем общий обработчик события
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();

КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события возникающего в транзакции записи для формирования движений документу по подчиненным регистрам.
//
// Параметры:
//  Отказ           - Булево                   - Признак отказа от совершения операции.
//  РежимПроведения - РежимПроведенияДокумента - Текущий режим проведения.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Вызываем общий обработчик события
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения) Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ВозможенВВодНаОсновании(Сделка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Ошибки = "";
	ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	СтрОснований = ПолучитьПартииТоваров();
	
	// Спишем остатки
	// выполним списание по регистру остатков и выполним переоценку если надо.
	ТаблицаНоменклатуры = ПолучитьТаблицуНоменклатуры();
	Если СтрОснований.ПартияДок.ХозОперация <> Справочники.ХозОперации.УслугиСтороннихОрганизаций Тогда
		ВыполнитьДвиженияПоОстаткам(ТаблицаНоменклатуры, Отказ, Ошибки);
		Если НЕ Отказ Тогда
			ВыполнитьДвиженияПоПартиям(ТаблицаНоменклатуры, Отказ, Ошибки);
		КонецЕсли;
	КонецЕсли;
	
	// оприходуем услуги
	Если НЕ Отказ Тогда
		ВыполнитьДвиженияПоУслугам(ТаблицаНоменклатуры, Отказ, Ошибки);
	КонецЕсли;
	
	// Проверим налиие прослеживаемых товаров, которые были возвращены из розницы
	ТаблицаПрослеживаемыхТоваров = Документы.КорректировкаПоступления.ОперацииСПрослеживаемымиТоварами(
		ЭтотОбъект);
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ТаблицаПрослеживаемыхТоваров;
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		// при безвозмездном поступлении сумму на доходы и расходы
		Если СтрОснований.ПартияДок.ХозОперация=Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда
			СуммаСебестоимости = 0;
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			// В случае, если ведется баланс по подразделению, передадим подразделение, соответствующее корреспонденции.
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение          = ПодразделениеКомпании;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДиР.ШапкаДокумента         = ШапкаДокумента;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			НаборЗаписейДиР.Доход                  = Движения.ПартииТоваровКомпании.Итог("СуммаУпр");;
			СуммаСебестоимости                     = СуммаСебестоимости+НаборЗаписейДиР.Доход;
			Если НаборЗаписейДиР.Доход <> 0 Тогда
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли;
		
		// взаиморасчеты проведем
		Если СтрОснований.ПартияДок.ХозОперация<>Справочники.ХозОперации.ПоступлениеТоваровКомиссия И СтрОснований.ПартияДок.ХозОперация<>Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда // И Контрагент.ВидКонтрагента<>Перечисления.ВидыКонтрагентов.ПодотчетноеЛицо
			СуммаДоходаРасходаСуммовыхРазниц = ВыполнитьДвиженияПоВзаиморасчетам(Отказ, Ошибки);
			// доходы и расходы по суммовым разницам
			Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
				НаборЗаписейДиР = Движения.ДоходыИРасходы;
				НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
				// В случае, если ведется баланс по подразделению, передадим подразделение, соответствующее корреспонденции.
				Если ВедетсяБалансПоПодразделению Тогда
					НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
				КонецЕсли;
				НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
				НаборЗаписейДиР.ВУпрВалюте = Истина;
				Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
					НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
				Иначе
					НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
				КонецЕсли;
				Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			КонецЕсли;
		КонецЕсли;
		
		// Двигаем границы
		// Двигаем границу последовательности взаиморасчетов
		ДвигаемГраницу = (РежимПроведения<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
		Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
			НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
			Если ДвигаемГраницу Тогда
				ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
				ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
				НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
				НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
				НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
				НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
			Иначе
				НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
				НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
				НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
				НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
			КонецЕсли;
			НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
			НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
		КонецЕсли;
		
		// Двигаем границу последовательности партий
		Если РежимПроведения<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
			НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
			Если РежимПроведения<>РежимПроведенияДокумента.Оперативный Тогда
				НаборЗаписейГраницыПартий.СкладКомпании		= СкладКомпании;
				НаборЗаписейГраницыПартий.МоментВремени		= Дата;
				НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
				НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
			Иначе
				НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
				НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
				НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
				НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
			КонецЕсли;
			НаборЗаписейГраницыПартий.ДокументСсылка	= Ссылка;
			НаборЗаписейГраницыПартий.ИзменитьГраницу();
		КонецЕсли;

	КонецЕсли;

	// Запишим коды маркировки
	Если НЕ Отказ Тогда
		ПровестиПоКодамМаркировки(РежимПроведения);
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// Зафиксируем новые штрихкоды товара из кода маркировки
	ГрупповаяОбработкаДокументов = Ложь;
	Если НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ГрупповаяОбработкаДокументов",ГрупповаяОбработкаДокументов) Тогда
		ГрупповаяОбработкаДокументов = Ложь;
	КонецЕсли;
	Если НЕ ГрупповаяОбработкаДокументов Тогда
		дкПроверитьИДобавитьГТИННоменклатуры(ЭтотОбъект);
	КонецЕсли;

	Если Отказ Тогда
		Сообщить("При проведении обнаружены ошибки:", СтатусСообщения.Внимание);
		Сообщить(Ошибки);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения

Функция ВозможенВВодНаОсновании(ДанныеЗаполнения)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения)
		И (ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваров")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КорректировкаПоступления")) Тогда
		
		Если ДанныеЗаполнения = Ссылка Тогда
			Сообщить("Ввод корректировки поступления на основании самой себя запрещен.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Если НЕ ДанныеЗаполнения.Проведен Тогда
			Сообщить("Ввод корректировки поступления возможен только на основании проведенного документа.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		ХозОперацияОснования = ДанныеЗаполнения.ХозОперация;
		Если ХозОперацияОснования = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
			Сообщить("Ввод корректировки поступления не возможен на основании поступления товаров комиссия.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Если Документы.КорректировкаПоступления.КорректировкаНеДоступна(ДанныеЗаполнения) Тогда
			Сообщить("На основании <" + ДанныеЗаполнения + "> введен возврат товаров поставщику. Ввод корректировки поступления невозможен.", СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьПартииТоваров()
	
	СтрПартий = Новый Структура;
	
	// Получим партию средних остатков
	СтрПартий.Вставить("ПартияОтрицательныхОстатков",Константы.ПартияТоваровОтрицательныхОстатков.Получить());
	
	Если обЗначениеНеЗаполнено(СкладКомпании.СтратегияСписанияПартийТоваровПоДатам) Тогда
		// Берем стратегию списания из константы
		СтратегияСписанияПартийТоваровПоДатам = Константы.СтратегияСписанияПартийТоваровПоДатам.Получить();
	Иначе
		// Берем стратегию списания со склада
		СтратегияСписанияПартийТоваровПоДатам = СкладКомпании.СтратегияСписанияПартийТоваровПоДатам;
	КонецЕсли;	
	
	Если СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.Средняя Тогда
		СтрПартий.Вставить("ПартияДок", СтрПартий.ПартияОтрицательныхОстатков);
	Иначе
		ДокОснование = ДокументОснование;
		Пока ТипЗнч(ДокОснование) = Тип("ДокументСсылка.КорректировкаПоступления") Цикл
			ДокОснование = ДокОснование.ДокументОснование;
		КонецЦикла;
		
		СтрПартий.Вставить("ПартияДок", ДокОснование);
	КонецЕсли;
	
	Возврат СтрПартий;
	
КонецФункции // ПолучитьПартииТоваров()

Функция ВыполнитьДвиженияПоОстаткам(ТаблицаНоменклатуры, Отказ, Ошибки)
	
	// проверим разрешены ли отр. остатки
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки",Права,,ЭтотОбъект)<>Перечисления.ВидыРазрешенныхОтрицательныхОстатков.Запрещены);
	// запишем движения
	Для Каждого стрНоменклатуры Из ТаблицаНоменклатуры Цикл
		Если стрНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга ИЛИ стрНоменклатуры.КоличествоРазница = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись               = Движения.ОстаткиТоваровКомпании.Добавить();
		НоваяЗапись.Период        = Дата;
		НоваяЗапись.Регистратор   = Ссылка;
		НоваяЗапись.ВидДвижения   = ВидДвиженияНакопления.Приход;
		НоваяЗапись.Номенклатура  = стрНоменклатуры.Номенклатура;
		НоваяЗапись.ХарактеристикаНоменклатуры = стрНоменклатуры.ХарактеристикаНоменклатуры;
		НоваяЗапись.СкладКомпании = СкладКомпании;
		НоваяЗапись.Количество    = стрНоменклатуры.КоличествоРазница;
		НоваяЗапись.СуммаРозн     = стрНоменклатуры.СуммаРозничнаяРазница;
		НоваяЗапись.ХозОперация   = ХозОперация;
		НоваяЗапись.Контрагент    = Контрагент;
	КонецЦикла;
	Движения.ОстаткиТоваровКомпании.Записать();
	
	// получим остатки по складу
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток,
	|	ОстаткиТоваровКомпанииОстатки.РезервОстаток,
	|	ОстаткиТоваровКомпанииОстатки.СуммаРознОстаток
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|			&Момент,
	|			СкладКомпании = &СкладКомпании
	|				И Номенклатура В (&СписокНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки";
	
	Запрос.УстановитьПараметр("Момент", Новый Граница(МоментВремени(),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("СписокНоменклатуры", ТаблицаНоменклатуры.ВыгрузитьКолонку("Номенклатура"));
	
	ВыборкаОстатков = Запрос.Выполнить().Выбрать(); Ошибки = "";
	
	Пока ВыборкаОстатков.Следующий() Цикл
		
		// проверим остаток
		Если ВыборкаОстатков.КоличествоОстаток < ВыборкаОстатков.РезервОстаток И ВыборкаОстатков.РезервОстаток > 0 Тогда
			Отказ = Истина;
			дкДобавитьОшибку(Ошибки, "Нельзя откорректировать зарезервированный товар <"+ВыборкаОстатков.Номенклатура+"> - превышение "+ Строка(ВыборкаОстатков.РезервОстаток-ВыборкаОстатков.КоличествоОстаток)+".");
		КонецЕсли;
		
		Если НЕ ОтрицательныеОстаткиРазрешены И ВыборкаОстатков.КоличествоОстаток < 0 Тогда
			Отказ = Истина;
			дкДобавитьОшибку(Ошибки, "Не хватает товара <"+ВыборкаОстатков.Номенклатура+"> для корректировки в количестве "+Строка(-ВыборкаОстатков.КоличествоОстаток)+".");
		КонецЕсли;
		
		Если СкладКомпании.Розничный И ПодразделениеКомпании.ПереоценкаРозницаПоПриходу Тогда
			// выполним переоценку
			// получим цену из ТЧ
			стрПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",ВыборкаОстатков.Номенклатура,ВыборкаОстатков.ХарактеристикаНоменклатуры);
			МассивСтрок = ТаблицаНоменклатуры.НайтиСтроки(стрПоиска);
			Если МассивСтрок.Количество() > 0 Тогда
				ЦенаПоДок = МассивСтрок[0].ЦенаРозничная;
			Иначе
				ЦенаПоДок = 0;
			КонецЕсли;
			
			Если (ЦенаПоДок <> 0) И (ЦенаПоДок*ВыборкаОстатков.КоличествоОстаток) <> ВыборкаОстатков.СуммаРознОстаток Тогда
				НоваяЗапись               = Движения.ОстаткиТоваровКомпании.Добавить();
				НоваяЗапись.Период        = Дата;
				НоваяЗапись.Регистратор   = Ссылка;
				НоваяЗапись.ВидДвижения   = ВидДвиженияНакопления.Приход;
				НоваяЗапись.Номенклатура  = ВыборкаОстатков.Номенклатура;
				НоваяЗапись.ХарактеристикаНоменклатуры = ВыборкаОстатков.ХарактеристикаНоменклатуры;
				НоваяЗапись.СкладКомпании = СкладКомпании;
				НоваяЗапись.Количество    = 0;
				НоваяЗапись.СуммаРозн     = (ЦенаПоДок*ВыборкаОстатков.КоличествоОстаток)-ВыборкаОстатков.СуммаРознОстаток;
				НоваяЗапись.ХозОперация   = ХозОперация;
				НоваяЗапись.Контрагент    = Контрагент;
				НоваяЗапись.Цена          = ЦенаПоДок;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции // ВыполнитьДвиженияПоОстаткам()

Функция ВыполнитьДвиженияПоПартиям(ТаблицаНоменклатуры, Отказ, Ошибки)
	
	// Распределим себестоимость товаров
	ТаблицаСебестоимости = Новый ТаблицаЗначений();
	ТаблицаСебестоимости.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаСебестоимости.Колонки.Добавить("ХарактеристикаНоменклатуры",
		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаСебестоимости.Колонки.Добавить("Партия", Новый ОписаниеТипов("ДокументСсылка.ПоступлениеТоваров"));
	ТаблицаСебестоимости.Колонки.Добавить("СтатусПартии", Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыПартий"));
	ТаблицаСебестоимости.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	ТаблицаСебестоимости.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	ТаблицаСебестоимости.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ТаблицаСебестоимости.Колонки.Добавить("СуммаУпр", Новый ОписаниеТипов("Число"));
	ТаблицаСебестоимости.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число"));
	
	ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
	
	// получим партии
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки",Права,,ЭтотОбъект)=Перечисления.ВидыРазрешенныхОтрицательныхОстатков.ПоПартиям);
	СтрПартий = ПолучитьПартииТоваров();
	
	// получим остатки по партии
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииТоваровКомпанииОстатки.Номенклатура,
	|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпанииОстатки.КоличествоОстаток,
	|	ПартииТоваровКомпанииОстатки.СтатусПартии,
	|	ПартииТоваровКомпанииОстатки.СуммаОстаток,
	|	ПартииТоваровКомпанииОстатки.СуммаУпрОстаток,
	|	ПартииТоваровКомпанииОстатки.СуммаНДСОстаток
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
	|			&Период,
	|			СкладКомпании = &СкладКомпании
	|				И Партия = &Партия
	|				И Номенклатура В (&СписокНоменклатуры)) КАК ПартииТоваровКомпанииОстатки";
	
	Запрос.УстановитьПараметр("СкладКомпании"      , СкладКомпании);
	Запрос.УстановитьПараметр("Партия"             , СтрПартий.ПартияДок);
	Запрос.УстановитьПараметр("СписокНоменклатуры" , ТаблицаНоменклатуры.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Период"             , МоментВремени());
	
	ТаблицаПартий = Запрос.Выполнить().Выгрузить();
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();  
	КурсУпр = Ссылка.КурсВалютыУпр; 
	Если НЕ ЗначениеЗаполнено(КурсВалютыУпр) Тогда
		СтруктураКурса = обКурсДляВалюты(ВалютаУпр, Дата);
		КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	КонецЕсли;	
	
	Для Каждого стрНоменклатура Из ТаблицаНоменклатуры Цикл
		Если стрНоменклатура.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			Продолжить;
		КонецЕсли;
		
		Если
			стрНоменклатура.КоличествоРазница = 0
			И стрНоменклатура.СуммаРозничнаяРазница = 0
			И стрНоменклатура.СуммаВсегоРазница = 0
			И стрНоменклатура.СуммаНДСРазница = 0
		Тогда
			Продолжить;
		КонецЕсли;
		
		
		// получим сумму в упр. и регл. валюте
		СуммаУпрНом     = обПересчет(стрНоменклатура.СуммаВсегоРазница,ВалютаДокумента,Дата,ВалютаУпр,Дата,РежимОкругления.Окр15как20);
		СуммаРеглНом    = обПересчет(стрНоменклатура.СуммаВсегоРазница,ВалютаДокумента,Дата,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),Дата,РежимОкругления.Окр15как20);
		СуммаУпрНомНДС  = обПересчет(стрНоменклатура.СуммаНДСРазница,ВалютаДокумента,Дата,ВалютаУпр,Дата,РежимОкругления.Окр15как20);
		СуммаРеглНомНДС = обПересчет(стрНоменклатура.СуммаНДСРазница,ВалютаДокумента,Дата,Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),Дата,РежимОкругления.Окр15как20);
		
		// получим строку остатков
		СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",
			стрНоменклатура.Номенклатура,
			стрНоменклатура.ХарактеристикаНоменклатуры
		);
		
		МассивСтрок = ТаблицаПартий.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСтрок.Количество() = 0 Тогда
			
			// от партии ничего не осталось
			Если стрНоменклатура.КоличествоРазница = 0 Тогда
				Отказ = Истина;
				дкДобавитьОшибку(Ошибки, "Не удалось откорректировать сумму по товару <"+стрНоменклатура.Номенклатура+"> - партия была расформирована");
				Продолжить;
			ИначеЕсли НЕ ОтрицательныеОстаткиРазрешены И стрНоменклатура.КоличествоРазница < 0 Тогда
				Отказ = Истина;
				дкДобавитьОшибку(Ошибки, "Нехватка товара <"+стрНоменклатура.Номенклатура+"> по партии в количестве " + Строка(-стрНоменклатура.КоличествоРазница)+".");
				Продолжить;
			КонецЕсли;
			
			НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
			НоваяЗапись.Период = Дата;
			НоваяЗапись.Регистратор   = Ссылка;
			НоваяЗапись.Номенклатура  = стрНоменклатура.Номенклатура;
			НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
			НоваяЗапись.СкладКомпании = СкладКомпании;
			НоваяЗапись.ХарактеристикаНоменклатуры = стрНоменклатура.ХарактеристикаНоменклатуры;
			НоваяЗапись.Партия = ?(стрНоменклатура.КоличествоРазница > 0, СтрПартий.ПартияДок, СтрПартий.ПартияОтрицательныхОстатков);
			Если СтрПартий.ПартияДок.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
				НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
			Иначе
				НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
			КонецЕсли;
			НоваяЗапись.Количество = стрНоменклатура.КоличествоРазница;
			НоваяЗапись.Сумма = СуммаРеглНом;
			НоваяЗапись.СуммаУпр = СуммаУпрНом;
			НоваяЗапись.СуммаНДС = СуммаРеглНомНДС;
			НоваяЗапись.ХозОперация = ХозОперация;
			НоваяЗапись.СтавкаНДС = стрНоменклатура.СтавкаНДС;
			НоваяЗапись.Проект = Проект;
			
			// Сформируем записи для корректировки себестоимости в регистрах продажи
			СформироватьРазницуСебестоимостиТоваров(ТаблицаСебестоимости, стрНоменклатура, НоваяЗапись);
			
		Иначе
			// списываем или оприходуем кол-во
			стрПартии = МассивСтрок[0];
			
			Если (стрПартии.КоличествоОстаток + стрНоменклатура.КоличествоРазница) = 0 Тогда
				
				// Установим суммы списания по регистру
				СуммаРеглНом = - стрПартии.СуммаОстаток;
				СуммаУпрНом = - стрПартии.СуммаУпрОстаток;
				СуммаРеглНомНДС = - стрПартии.СуммаНДСОстаток;
				
			ИначеЕсли НЕ ОтрицательныеОстаткиРазрешены
				И (стрПартии.КоличествоОстаток + стрНоменклатура.КоличествоРазница) < 0 Тогда
				Отказ = Истина;
				дкДобавитьОшибку(Ошибки, "Нехватка товара <"+стрНоменклатура.Номенклатура+"> по партии в количестве " + Строка(-стрНоменклатура.КоличествоРазница)+".");
				Продолжить;
			КонецЕсли;
			
			// оприходуем партию
			КолвоОприх = стрНоменклатура.КоличествоРазница;
			Если КолвоОприх > 0 Тогда
				НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
				НоваяЗапись.Период = Дата;
				НоваяЗапись.Регистратор   = Ссылка;
				НоваяЗапись.Номенклатура  = стрНоменклатура.Номенклатура;
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.СкладКомпании = СкладКомпании;
				НоваяЗапись.ХарактеристикаНоменклатуры = стрНоменклатура.ХарактеристикаНоменклатуры;
				Если СтрПартий.ПартияДок.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
					НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварПринятыйКомиссия;
				Иначе
					НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
				КонецЕсли;
				НоваяЗапись.Партия = СтрПартий.ПартияДок;
				НоваяЗапись.Количество = стрНоменклатура.КоличествоРазница;
				НоваяЗапись.Сумма = СуммаРеглНом;
				НоваяЗапись.СуммаУпр = СуммаУпрНом;
				НоваяЗапись.СуммаНДС = СуммаРеглНомНДС;
				НоваяЗапись.ХозОперация = ХозОперация;
				НоваяЗапись.СтавкаНДС = стрНоменклатура.СтавкаНДС;
				НоваяЗапись.Проект = Проект;
			Иначе
				КолвоСпис = Мин(-КолвоОприх,стрПартии.КоличествоОстаток);
				Если КолвоСпис = -КолвоОприх Тогда
					СуммаСписУпр  = СуммаУпрНом;
					СуммаСписРегл = СуммаРеглНом;
					СуммаСписРеглНДС = СуммаРеглНомНДС;
				Иначе
					СуммаСписУпр     = стрПартии.СуммаУпрОстаток/КолвоОприх*КолвоСпис;
					СуммаСписРегл    = стрПартии.СуммаОстаток/КолвоОприх*КолвоСпис;
					СуммаСписРеглНДС = стрПартии.СуммаНДСОстаток/КолвоОприх*КолвоСпис;
				КонецЕсли;
				
				НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
				НоваяЗапись.Период = Дата;
				НоваяЗапись.Регистратор   = Ссылка;
				НоваяЗапись.Номенклатура  = стрНоменклатура.Номенклатура;
				НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
				НоваяЗапись.СкладКомпании = СкладКомпании;
				НоваяЗапись.ХарактеристикаНоменклатуры = стрНоменклатура.ХарактеристикаНоменклатуры;
				НоваяЗапись.СтатусПартии = стрПартии.СтатусПартии;
				НоваяЗапись.Партия = СтрПартий.ПартияДок;
				НоваяЗапись.Количество = -КолвоСпис;
				НоваяЗапись.Сумма = СуммаСписРегл;
				НоваяЗапись.СуммаУпр = СуммаСписУпр;
				НоваяЗапись.СуммаНДС = СуммаСписРеглНДС;
				НоваяЗапись.ХозОперация = ХозОперация;
				НоваяЗапись.СтавкаНДС = стрНоменклатура.СтавкаНДС;
				НоваяЗапись.Проект = Проект;
				
				Если -КолвоОприх <> КолвоСпис Тогда
					НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
					НоваяЗапись.Период = Дата;
					НоваяЗапись.Регистратор   = Ссылка;
					НоваяЗапись.Номенклатура  = стрНоменклатура.Номенклатура;
					НоваяЗапись.ВидДвижения=ВидДвиженияНакопления.Приход;
					НоваяЗапись.СкладКомпании = СкладКомпании;
					НоваяЗапись.ХарактеристикаНоменклатуры = стрНоменклатура.ХарактеристикаНоменклатуры;
					НоваяЗапись.СтатусПартии = Перечисления.СтатусыПартий.ТоварКупленный;
					НоваяЗапись.Партия = СтрПартий.ПартияОтрицательныхОстатков;
					НоваяЗапись.Количество = КолвоОприх + КолвоСпис;
					НоваяЗапись.Сумма = СуммаСписРегл + СуммаРеглНом;
					НоваяЗапись.СуммаУпр = СуммаУпрНом + СуммаСписУпр;
					НоваяЗапись.СуммаНДС = СуммаРеглНомНДС + СуммаСписРеглНДС;
					НоваяЗапись.ХозОперация = ХозОперация;
					НоваяЗапись.СтавкаНДС = стрНоменклатура.СтавкаНДС;
					НоваяЗапись.Проект = Проект;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Учем новую себестоимость при записи в регистр
	ВыполнитьДвиженияПоСебестоимости(ТаблицаСебестоимости);
	
	Движения.ПартииТоваровКомпании.Записать();
	
	Если НЕ Отказ Тогда
		ВыполнитьДвиженияПоГТД(Отказ, Ошибки);
	КонецЕсли;
	
КонецФункции // ВыполнитьДвиженияПоПартиям()

Функция ВыполнитьДвиженияПоУслугам(ТаблицаНоменклатуры, Отказ, Ошибки)
	
	ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
	
	СуммаПоКоличествуБаз     = 0; СуммаПоКоличествуНДСБаз     = 0;
	СуммаПоСуммеБаз          = 0; СуммаПоСуммеНДСБаз          = 0;
	СуммаНаДоходыИРасходыБаз = 0; СуммаНаДоходыИРасходыНДСБаз = 0;
	МассивНаДоходыИРасходы = Новый Массив;
	СтрПартий = ПолучитьПартииТоваров();
	// получим набор услуг для распределения суммы на товары
	Для Каждого стрНом Из ТаблицаНоменклатуры Цикл
		Если стрНом.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
			Продолжить;
		КонецЕсли;
		
		Если стрНом.Номенклатура.СпособРаспределенияДопРасходов = Перечисления.СпособыРаспределенияДопРасходов.ПоКоличеству Тогда
			СуммаПоКоличествуБаз = СуммаПоКоличествуБаз + стрНом.СуммаВсегоРазница;
			СуммаПоКоличествуНДСБаз = СуммаПоКоличествуНДСБаз + стрНом.СуммаНДСРазница;
		ИначеЕсли стрНом.Номенклатура.СпособРаспределенияДопРасходов = Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы Тогда
			СуммаНаДоходыИРасходыБаз = СуммаНаДоходыИРасходыБаз + стрНом.СуммаВсегоРазница;
			СуммаНаДоходыИРасходыНДСБаз = СуммаНаДоходыИРасходыНДСБаз + стрНом.СуммаНДСРазница;
			МассивНаДоходыИРасходы.Добавить(стрНом);
		Иначе
			СуммаПоСуммеБаз = СуммаПоСуммеБаз + стрНом.СуммаВсегоРазница;
			СуммаПоСуммеНДСБаз = СуммаПоСуммеНДСБаз + стрНом.СуммаНДСРазница;
		КонецЕсли;
	КонецЦикла;
	
	// сразу переведем суммы
	ВалютаУпр                = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВалютаРегл               = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	
	// упр
	СуммаПоКоличеству        = обПересчет(СуммаПоКоличествуБаз, ВалютаДокумента, КурсДокумента, ВалютаУпр, КурсВалютыУпр); 
	СуммаПоСумме             = обПересчет(СуммаПоСуммеБаз, ВалютаДокумента, КурсДокумента, ВалютаУпр, КурсВалютыУпр); 
	СуммаНаДоходыИРасходы    = обПересчет(СуммаНаДоходыИРасходыБаз, ВалютаДокумента, КурсДокумента, ВалютаУпр, КурсВалютыУпр);
	СуммаНаДоходыИРасходыНДС = обПересчет(СуммаНаДоходыИРасходыНДСБаз, ВалютаДокумента, КурсДокумента, ВалютаУпр, КурсВалютыУпр);
	
	// регл
	СуммаПоКоличествуРегл        = обПересчет(СуммаПоКоличествуБаз, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата); 
	СуммаПоКоличествуНДСРегл     = обПересчет(СуммаПоКоличествуНДСБаз, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата);
	СуммаПоСуммеРегл             = обПересчет(СуммаПоСуммеБаз, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата); 
	СуммаПоСуммеНДСРегл          = обПересчет(СуммаПоСуммеНДСБаз, ВалютаДокумента, КурсДокумента, ВалютаРегл, Дата);
	// получим товары после проведения основных изысканий
	Если СуммаПоКоличеству <> 0 ИЛИ СуммаПоСумме <> 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПартииТоваровКомпанииОстатки.Номенклатура,
		|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпанииОстатки.КоличествоОстаток > 0
		|			ТОГДА ПартииТоваровКомпанииОстатки.КоличествоОстаток
		|		ИНАЧЕ -ПартииТоваровКомпанииОстатки.КоличествоОстаток
		|	КОНЕЦ КАК КоличествоОстаток,
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпанииОстатки.СуммаОстаток > 0
		|			ТОГДА ПартииТоваровКомпанииОстатки.СуммаОстаток
		|		ИНАЧЕ -ПартииТоваровКомпанииОстатки.СуммаОстаток
		|	КОНЕЦ КАК СуммаОстаток,
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпанииОстатки.СуммаУпрОстаток > 0
		|			ТОГДА ПартииТоваровКомпанииОстатки.СуммаУпрОстаток
		|		ИНАЧЕ -ПартииТоваровКомпанииОстатки.СуммаУпрОстаток
		|	КОНЕЦ КАК СуммаУпрОстаток,
		|	ПартииТоваровКомпанииОстатки.СтатусПартии
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		|			&Граница,
		|			Партия = &Партия
		|				И СкладКомпании = &СкладКомпании) КАК ПартииТоваровКомпанииОстатки
		|ИТОГИ
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток),
		|	СУММА(СуммаУпрОстаток)
		|ПО
		|	ОБЩИЕ";
		Запрос.УстановитьПараметр("Партия"        , СтрПартий.ПартияДок);
		Запрос.УстановитьПараметр("СкладКомпании" , СкладКомпании);
		Запрос.УстановитьПараметр("Граница"       , Новый Граница(МоментВремени(),ВидГраницы.Включая));
		
		ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаИтоги.Следующий() Тогда
			Если СуммаПоКоличеству <> 0 И ВыборкаИтоги.КоличествоОстаток = 0 Тогда
				Отказ = Истина;
				дкДобавитьОшибку(Ошибки, "Не удалось распределить услуги по количеству так как количество товаров в партии 0.");
			КонецЕсли;
			
			Если СуммаПоСумме <> 0 И ВыборкаИтоги.СуммаОстаток = 0 Тогда
				Отказ = Истина;
				дкДобавитьОшибку(Ошибки, "Не удалось распределить услуги по сумме так как сумма товаров в партии 0.");
			КонецЕсли;
			
			Если Отказ Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			// распишем услуги по количеству
			ВыборкаДетали     = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			СуммаНаЕдиницу    = Окр(СуммаПоКоличеству/ВыборкаИтоги.КоличествоОстаток,2,РежимОкругления.Окр15как20);
			СуммаОсталось     = СуммаПоКоличеству;
			
			СуммаНаЕдиницуРегл    = Окр(СуммаПоКоличествуРегл/ВыборкаИтоги.КоличествоОстаток,2,РежимОкругления.Окр15как20);
			СуммаНаЕдиницуНДСРегл = Окр(СуммаПоКоличествуНДСРегл/ВыборкаИтоги.КоличествоОстаток,2,РежимОкругления.Окр15как20);
			СуммаОсталосьРегл     = СуммаПоКоличествуРегл;
			СуммаОсталосьНДСРегл  = СуммаПоКоличествуНДСРегл;
			
			Пока ВыборкаДетали.Следующий() Цикл
				Если СуммаОсталось = 0 Тогда Прервать; КонецЕсли;
				
				СуммаОприх = СуммаНаЕдиницу*ВыборкаДетали.КоличествоОстаток;
				СуммаОприхРегл = СуммаНаЕдиницуРегл*ВыборкаДетали.СуммаОстаток;
				СуммаОприхНДСРегл = СуммаНаЕдиницуНДСРегл*ВыборкаДетали.СуммаОстаток;
				Если СуммаОприх <> 0 Тогда
					СуммаОсталось = СуммаОсталось - СуммаОприх;
					СуммаОсталосьРегл = СуммаОсталосьРегл - СуммаОприхРегл;
					СуммаОсталосьНДСРегл = СуммаОсталосьНДСРегл - СуммаОприхНДСРегл;
					
					НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
					
					НоваяЗапись.СкладКомпании              = СкладКомпании;
					НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
					НоваяЗапись.Период                     = Дата;
					НоваяЗапись.Регистратор                = Ссылка;
					НоваяЗапись.Номенклатура               = ВыборкаДетали.Номенклатура;
					НоваяЗапись.ХарактеристикаНоменклатуры = ВыборкаДетали.ХарактеристикаНоменклатуры;
					НоваяЗапись.СтатусПартии               = ВыборкаДетали.СтатусПартии;
					НоваяЗапись.Партия                     = СтрПартий.ПартияДок;
					
					НоваяЗапись.Количество                 = 0;
					НоваяЗапись.Сумма                      = СуммаОприхРегл;
					НоваяЗапись.СуммаУпр                   = СуммаОприх;
					НоваяЗапись.СуммаНДС                   = СуммаОприхНДСРегл;
					
					НоваяЗапись.ХозОперация = ХозОперация;
				КонецЕсли;
			КонецЦикла;
			
			Если СуммаОсталось <> 0 ИЛИ СуммаОсталосьРегл <> 0 ИЛИ СуммаОсталосьНДСРегл <> 0 И НоваяЗапись <> Неопределено Тогда
				НоваяЗапись.Сумма    = НоваяЗапись.Сумма + СуммаОсталосьРегл;
				НоваяЗапись.СуммаНДС = НоваяЗапись.СуммаНДС + СуммаОсталосьНДСРегл;
				НоваяЗапись.СуммаУпр = НоваяЗапись.СуммаУпр + СуммаОсталось;
			КонецЕсли;
			
			
			// распишем услуги по сумме
			ВыборкаДетали.Сбросить();
			ВыборкаДетали = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			СуммаНаЕдиницу = Окр(СуммаПоСумме/ВыборкаИтоги.СуммаОстаток,2,РежимОкругления.Окр15как20);
			СуммаОсталось = СуммаПоСумме;
			
			СуммаНаЕдиницуРегл    = Окр(СуммаПоСуммеРегл/ВыборкаИтоги.СуммаОстаток,2,РежимОкругления.Окр15как20);
			СуммаНаЕдиницуНДСРегл = Окр(СуммаПоСуммеНДСРегл/ВыборкаИтоги.СуммаОстаток,2,РежимОкругления.Окр15как20);
			СуммаОсталосьРегл     = СуммаПоСуммеРегл;
			СуммаОсталосьНДСРегл  = СуммаПоСуммеНДСРегл;

			Пока ВыборкаДетали.Следующий() Цикл
				Если СуммаОсталось = 0 Тогда Прервать; КонецЕсли;
				
				СуммаОприх = СуммаНаЕдиницу*ВыборкаДетали.СуммаОстаток;
				СуммаОприхРегл = СуммаНаЕдиницуРегл*ВыборкаДетали.СуммаОстаток;
				СуммаОприхНДСРегл = СуммаНаЕдиницуНДСРегл*ВыборкаДетали.СуммаОстаток;
				Если СуммаОприх <> 0 Тогда
					СуммаОсталось = СуммаОсталось - СуммаОприх;
					СуммаОсталосьРегл = СуммаОсталосьРегл - СуммаОприхРегл;
					СуммаОсталосьНДСРегл = СуммаОсталосьНДСРегл - СуммаОприхНДСРегл;
					
					НоваяЗапись = Движения.ПартииТоваровКомпании.Добавить();
					
					НоваяЗапись.СкладКомпании              = СкладКомпании;
					НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
					НоваяЗапись.Период                     = Дата;
					НоваяЗапись.Регистратор                = Ссылка;
					НоваяЗапись.Номенклатура               = ВыборкаДетали.Номенклатура;
					НоваяЗапись.ХарактеристикаНоменклатуры = ВыборкаДетали.ХарактеристикаНоменклатуры;
					НоваяЗапись.СтатусПартии               = ВыборкаДетали.СтатусПартии;
					НоваяЗапись.Партия                     = СтрПартий.ПартияДок;
					
					НоваяЗапись.Количество                 = 0;
					НоваяЗапись.Сумма                      = СуммаОприхРегл;
					НоваяЗапись.СуммаУпр                   = СуммаОприх;
					НоваяЗапись.СуммаНДС                   = СуммаОприхНДСРегл;
					
					НоваяЗапись.ХозОперация = ХозОперация;
				КонецЕсли;
			КонецЦикла;
			
			Если СуммаОсталось <> 0 ИЛИ СуммаОсталосьРегл <> 0 ИЛИ СуммаОсталосьНДСРегл <> 0 И НоваяЗапись <> Неопределено Тогда
				НоваяЗапись.Сумма    = НоваяЗапись.Сумма + СуммаОсталосьРегл;
				НоваяЗапись.СуммаНДС = НоваяЗапись.СуммаНДС + СуммаОсталосьНДСРегл;
				НоваяЗапись.СуммаУпр = НоваяЗапись.СуммаУпр + СуммаОсталось;
			КонецЕсли;
		Иначе
			Отказ = Истина;
			дкДобавитьОшибку(Ошибки, "Не удалось распределить услуги так как товаров в партии не осталось.");
		КонецЕсли;
	КонецЕсли;
	
	// Запишем сумму на доходы и расходы
	
	// определим необходимость формирования корректирующих проводок
	ВедетсяБалансПоПодразделению = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	ПодразделениеСклад = обПолучитьБалансовоеПодразделение(СкладКомпании.Подразделение);
	ПодразделениеДоговорВзаиморасчетов = обПолучитьБалансовоеПодразделение(ДоговорВзаиморасчетов.Подразделение);
	ПодразделениеПодразделениеКомпании = обПолучитьБалансовоеПодразделение(ПодразделениеКомпании);
	Если ХозОперация = Справочники.ХозОперации.УслугиСтороннихОрганизаций Тогда
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеПодразделениеКомпании);
	Иначе	
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговорВзаиморасчетов<>ПодразделениеСклад);
	КонецЕсли;
	
	Если СуммаНаДоходыИРасходы <> 0 И СтрПартий.ПартияДок.ХозОперация<>Справочники.ХозОперации.ПоступлениеБезвозмездное Тогда
		СуммаУслуг = 0;
		Для Каждого СтрокаУслуги Из МассивНаДоходыИРасходы Цикл
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			// В случае, если ведется баланс по подразделению, передадим подразделение, соответствующее корреспонденции.
			Если ВедетсяБалансПоПодразделению Тогда
				НаборЗаписейДиР.Подразделение=ПодразделениеКомпании;
			КонецЕсли;
			НаборЗаписейДиР.ШапкаДокумента = ШапкаДокумента;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = СтрокаУслуги.Номенклатура.СтатьяДопРасходов;
			НаборЗаписейДиР.ВУпрВалюте = (ШапкаДокумента.ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
			НаборЗаписейДиР.Расход = СтрокаУслуги.СуммаВсегоРазница;
			
			//<<Павличенко 12.01.18
			НаборЗаписейДиР.РасходБезНДС = СтрокаУслуги.СуммаВсегоРазница-СтрокаУслуги.СуммаНДСРазница;
			//>>Павличенко 12.01.18
			
			СуммаУслуг = СуммаУслуг+СтрокаУслуги.СуммаВсегоРазница;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЦикла;
		
		// способ ведения не по подразделениям то ничего не будем корректировать
		Если ВедетсяБалансПоПодразделению И БалансовыеПодразделенияНеРавны  И СуммаУслуг<>0 Тогда	
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.Подразделение = ПодразделениеКомпании;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Ложь;
			НаборЗаписейДиР.Доход = СуммаУслуг;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;	
		КонецЕсли;
	КонецЕсли;
	
КонецФункции // ВыполнитьДвиженияПоУслугам()

Функция ВыполнитьДвиженияПоВзаиморасчетам(Отказ, Ошибки)
	
	// получим сумму документа
	СуммаДок = Товары.Итог("СуммаВсегоРазница");
	Если СуммаДок = 0 Тогда
		Возврат 0;
	КонецЕсли;
	СтрОснований = ПолучитьПартииТоваров();
	
	НаборВзаиморасчетов = Движения.ВзаиморасчетыКомпании;
	НоваяЗапись = НаборВзаиморасчетов.Добавить();
	НоваяЗапись.Регистратор = Ссылка;
	НоваяЗапись.Период = Дата;
	НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
	НоваяЗапись.Контрагент = Контрагент;
	НоваяЗапись.ДоговорВзаиморасчетов = ДоговорВзаиморасчетов;
	Если СтрОснований.ПартияДок = СтрОснований.ПартияОтрицательныхОстатков Тогда
		// сделка - не партия, а документ-основание
		НоваяЗапись.Сделка = ДокументОснование;
	Иначе
		НоваяЗапись.Сделка = СтрОснований.ПартияДок;
	КонецЕсли;
	НоваяЗапись.Сумма    = обПересчет(СуммаДок, ВалютаДокумента, КурсДокумента,ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата);
	НоваяЗапись.СуммаУпр = обПересчет(СуммаДок, ВалютаДокумента, КурсДокумента, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), КурсВалютыУпр);
	НоваяЗапись.СуммаБаз = обПересчет(СуммаДок, ВалютаДокумента, КурсДокумента, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Дата);
	НоваяЗапись.ХозОперация = ХозОперация;
	НоваяЗапись.ВидОперации = Перечисления.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности;
	НаборВзаиморасчетов.Записать();
	
	Возврат 0;
	
КонецФункции // ВыполнитьДвиженияПоВзаиморасчетам()

Функция ВыполнитьДвиженияПоГТД(Отказ, Ошибки)
	
	ШапкаДокумента = ПолучитьШапкуДокумента(Ссылка);
	
	// получим партии
	ОтрицательныеОстаткиРазрешены = (обПраво("РазрешитьОтрицательныеСкладскиеОстатки",Права,,ЭтотОбъект)=Перечисления.ВидыРазрешенныхОтрицательныхОстатков.ПоПартиям);
	СтрПартий = ПолучитьПартииТоваров();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.ГТД КАК ГТД,
	|	СУММА(КорректировкаПоступленияТовары.КоличествоРазница) КАК Количество
	|ПОМЕСТИТЬ ДвиженияГТД
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.КоличествоРазница <> 0
	|	И КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ГТД <> ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)
	|	И КорректировкаПоступленияТовары.ГТД = КорректировкаПоступленияТовары.ГТДДоКорректировки
	|	И НЕ КорректировкаПоступленияТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.ГТД,
	|	КорректировкаПоступленияТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.ГТД,
	|	СУММА(КорректировкаПоступленияТовары.Количество)
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ГТД <> КорректировкаПоступленияТовары.ГТДДоКорректировки
	|	И КорректировкаПоступленияТовары.Количество <> 0
	|	И КорректировкаПоступленияТовары.ГТД <> ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)
	|	И НЕ КорректировкаПоступленияТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.ГТД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.ГТДДоКорректировки,
	|	СУММА(-КорректировкаПоступленияТовары.КоличествоПоДокументуПоступления)
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ГТД <> КорректировкаПоступленияТовары.ГТДДоКорректировки
	|	И КорректировкаПоступленияТовары.Количество <> 0
	|	И КорректировкаПоступленияТовары.ПоДокументуПоступления = ИСТИНА
	|	И НЕ КорректировкаПоступленияТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.ГТДДоКорректировки,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.ГТДДоКорректировки,
	|	КорректировкаПоступленияТовары.КоличествоРазница
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ГТД <> КорректировкаПоступленияТовары.ГТДДоКорректировки
	|	И КорректировкаПоступленияТовары.Количество = 0
	|	И НЕ КорректировкаПоступленияТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|	И КорректировкаПоступленияТовары.КоличествоРазница <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.ГТДДоКорректировки,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.КоличествоРазница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияГТД.Номенклатура КАК Номенклатура,
	|	ДвиженияГТД.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДвиженияГТД.ГТД КАК ГТД,
	|	ДвиженияГТД.Количество КАК Количество
	|ИЗ
	|	ДвиженияГТД КАК ДвиженияГТД
	|ГДЕ
	|	ДвиженияГТД.ГТД <> ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// получим список ГТД
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
	|	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	|	ГТДПартийТоваровКомпанииОстатки.ГТД,
	|	СУММА(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	|			&Момент,
	|			Партия = &Партия
	|				И СкладКомпании = &СкладКомпании) КАК ГТДПартийТоваровКомпанииОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпанииОстатки.Номенклатура,
	|	ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	|	ГТДПартийТоваровКомпанииОстатки.ГТД";
	Запрос.УстановитьПараметр("Момент"       , МоментВремени());
	Запрос.УстановитьПараметр("Партия"       , СтрПартий.ПартияДок);
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	ОстаткиГТД = Запрос.Выполнить().Выгрузить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество < 0 И НЕ ОтрицательныеОстаткиРазрешены Тогда
			
			// если расход проверим можно ли его отсторнировать
			НайденныеГТД = ОстаткиГТД.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД",Выборка.Номенклатура,Выборка.ХарактеристикаНоменклатуры,Выборка.ГТД));
			Если НайденныеГТД.Количество() = 0 Тогда
				Отказ = Истина;
				дкДобавитьОшибку(Ошибки, "Нехватка товара <"+Выборка.Номенклатура+"> с ГТД <" + Выборка.ГТД + "> в количестве " + Строка(-Выборка.Количество)+".");
				Продолжить;
			КонецЕсли;
			
			СтрокаГТД = НайденныеГТД[0];
			Если СтрокаГТД.КоличествоОстаток < -Выборка.Количество Тогда
				Отказ = Истина;
				дкДобавитьОшибку(Ошибки, "Нехватка товара """+Выборка.Номенклатура+""" с ГТД """+Выборка.ГТД+""" в количестве "+Строка(-Выборка.Количество-СтрокаГТД.КоличествоОстаток)+".");
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// если товара хватает спишем его
		НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
		
		НоваяЗапись.Регистратор                = Ссылка;
		НоваяЗапись.Период                     = Дата;
		НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
		НоваяЗапись.ХозОперация                = ХозОперация;
		НоваяЗапись.Номенклатура               = Выборка.Номенклатура;
		НоваяЗапись.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		НоваяЗапись.ГТД                        = Выборка.ГТД;
		НоваяЗапись.СкладКомпании              = СкладКомпании;
		НоваяЗапись.Партия                     = СтрПартий.ПартияДок;
		НоваяЗапись.Количество                 = Выборка.Количество;
	КонецЦикла;
	
КонецФункции // ВыполнитьДвиженияПоГТД()

Процедура ПровестиПоКодамМаркировки(РежимПроведения)
	
	// Отменим записи состояний документа
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
	ТаблицаМаркировки = НаборЗаписейСостоянияКодовМаркировки.ТаблицаКодовМаркировки();
	
	ТаблицаМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));
	ТаблицаМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50)));

	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	// Разберем маркировку на состовляющие для поиска
	Для Каждого ТекущийКодМаркировки Из ТаблицаМаркировки Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущийКодМаркировки.КодМаркировки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущийКодМаркировки.КодМаркировки);
		
		// Это не маркировка товара
		Если НЕ СтруктураМаркировки.Разобран Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийКодМаркировки.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		ТекущийКодМаркировки.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		
	КонецЦикла;
	
	ТекущиеСтатусыМаркировки = РегистрыСведений.СостоянияКодовМаркировки.ТекущиеСтатусыКодовМаркировки(
		ТаблицаМаркировки,
		МоментВремени()
	);
	
	// Проверим коды на выбытие
	СостоянияВОбороте = Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки();
	
	// Коды Маркировок, которые надо вывести из оборота
	ТаблицаВывода = ТекущиеСтатусыМаркировки.СкопироватьКолонки();
	
	// Таблица маркирвки, которые необходимо вернуть
	ТаблицаВвода = ТекущиеСтатусыМаркировки.СкопироватьКолонки();
	ТаблицаНеПодтвержденных = ТекущиеСтатусыМаркировки.СкопироватьКолонки();
	
	Для Каждого ТекущаяСтрока Из КодыМаркировки Цикл
		СтрокаМаркировки = ТекущиеСтатусыМаркировки.Найти(ТекущаяСтрока.КодМаркировки, "КодМаркировки");
		СтрокаТоваров = Товары.Найти(ТекущаяСтрока.ИдентификаторТовара, "ИдентификаторТовара");
		ЭтоВозврат = ТекущаяСтрока.Возврат;
		Если СтрокаМаркировки = Неопределено И СтрокаТоваров <> Неопределено Тогда
			НоваяСтрока = ТаблицаВывода.Добавить();
			НоваяСтрока.КодМаркировки = ТекущаяСтрока.КодМаркировки;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров);
		ИначеЕсли СостоянияВОбороте.Найти(СтрокаМаркировки.Состояние) <> Неопределено И ЭтоВозврат Тогда
			НоваяСтрока = ТаблицаВвода.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМаркировки);
		ИначеЕсли СтрокаТоваров <> Неопределено И НЕ СтрокаТоваров.Подтверждение Тогда
			НоваяСтрока = ТаблицаНеПодтвержденных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМаркировки);
		КонецЕсли;
	КонецЦикла;
	
	// Для перемаркируемых поставим состояние выбытия из оборота
	ОчищатьЗаписи = Истина;
	УдалятьДвижения = Истина;
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
	НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = РежимПроведения;
	
	// Выведем указанную маркировку из оборота
	Если ТаблицаВывода.Количество() > 0 Тогда
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВведенВОборот;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаВывода;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
		ОчищатьЗаписи = Ложь;
		УдалятьДвижения = Ложь;
	КонецЕсли;
	
	// Введем в оборот возврат
	Если ТаблицаВвода.Количество() > 0 Тогда
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаВвода;
		НаборЗаписейСостоянияКодовМаркировки.ОчищатьЗаписи = ОчищатьЗаписи;
		НаборЗаписейСостоянияКодовМаркировки.УдалятьДвижения = УдалятьДвижения;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Для неподтверженных товаров с кодами сделаем в обороте
	Если ТаблицаНеПодтвержденных.Количество() > 0 Тогда
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТаблицаНеПодтвержденных;
		НаборЗаписейСостоянияКодовМаркировки.ОчищатьЗаписи = ОчищатьЗаписи;
		НаборЗаписейСостоянияКодовМаркировки.УдалятьДвижения = УдалятьДвижения;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Расчет суммы документа
Функция РассчитатьСуммуВсего()Экспорт
	ВремСуммаДокумента=Товары.Итог("СуммаВсего");
	Если СуммаДокумента<>ВремСуммаДокумента Тогда
		СуммаДокумента=ВремСуммаДокумента;
	КонецЕсли;
	Возврат СуммаДокумента;
КонецФункции

/// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа.
//	Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если ДопПараметры=Неопределено Тогда
		ДопПараметры=Новый Структура;
	КонецЕсли;
	Если ТипЗнч(ДопПараметры)=Тип("Структура") Тогда
		ДопПараметры.Вставить("НеРассчитыватьСкидки",Истина);
	КонецЕсли; 
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет=ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет=Ложь;
		КонецПопытки; 
		Рез=Истина;
		ТекущаяВалютаДокумента=ВалютаДокумента;
		ТекущийКурсДокумента=КурсДокумента;
		Возврат Рез;
		
	ИначеЕсли Имя="Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="ДокументОснование" Тогда
		Если ДокументОснование <> Неопределено Тогда
			Если ДокументОснование = Ссылка Тогда
				ДокументОснование = Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Сообщить("Ввод корректировки поступления на основании самой себя запрещен.",СтатусСообщения.Внимание);
				Возврат Истина;
			КонецЕсли;
			
			Если НЕ ДокументОснование.Проведен Тогда
				Сообщить("Ввод корректировки поступления возможен только на основании проведенного документа.",СтатусСообщения.Внимание);
				ДокументОснование=Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	КорректировкаПоступления.Ссылка
			|ИЗ
			|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
			|ГДЕ
			|	КорректировкаПоступления.ДокументОснование = &ДокументОснование
			|	И КорректировкаПоступления.Ссылка <> &Ссылка";
			Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			РезЗапроса = Запрос.Выполнить();
			Если НЕ РезЗапроса.Пустой() Тогда
				Выборка = РезЗапроса.Выбрать();
				Выборка.Следующий();
				#Если Клиент Тогда
					Предупреждение("На основании документа " + ДокументОснование + " уже введен" + Выборка.Ссылка);
				#КонецЕсли
				ДокументОснование = Неопределено;
				ОбработкаРеквизита("ДокументОснование");
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда 
				Если Вопрос("Заполнить по документу-основания?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да Тогда
					ЭтотОбъект.Заполнить(ДокументОснование);
					ЭтаФорма.УправлениеДиалогом();
					дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
				КонецЕсли;
			КонецЕсли; 
		#КонецЕсли
		Сделка = Документы.КорректировкаПоступления.ПолучитьСделку(ДокументОснование);
		Возврат Рез;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда
			Возврат Рез;
		Иначе
			ТекСтрока.Подтверждение = Истина;
		КонецЕсли;
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
	ИначеЕсли Имя = "Товары.Количество" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры)
	ИначеЕсли Имя="Товары.Подтверждение" Тогда
		Если ТекСтрока.Подтверждение Тогда
			ТекСтрока.Количество=(ТекСтрока.КоличествоПоДокументуПоступления*ТекСтрока.КоэффициентПоДокументуПоступления)/ТекСтрока.Коэффициент;
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			ТекСтрока.СуммаВсего=ТекСтрока.СуммаВсегоПоДокументуПоступления;
			Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры);
			ТекСтрока.СуммаРозничная = ТекСтрока.СуммаРозничнаяПоДокументуПоступления;
			Рез=ОбработкаРеквизита("Товары.СуммаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			ТекСтрока.Количество=0;
			ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			Рез=Истина;
			ТекСтрока.СуммаРозничная = ТекСтрока.Количество*ТекСтрока.Коэффициент*ТекСтрока.ЦенаРозничная;
		КонецЕсли; 
		Рез=ОбработкаРеквизита("Товары.РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
	ИначеЕсли Имя = "Товары.ЦенаРозничная" Тогда
		
		ТекСтрока.СуммаРозничная = ТекСтрока.Количество*ТекСтрока.Коэффициент*ТекСтрока.ЦенаРозничная;
		Возврат ОбработкаРеквизита("Товары.РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "Товары.СуммаРозничная" Тогда
		
		Если (ТекСтрока.Количество=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=?(ТекСтрока.Количество*ТекСтрока.Коэффициент=0,0,ТекСтрока.СуммаРозничная/ТекСтрока.Количество*ТекСтрока.Коэффициент);
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("Товары.РасчетРазницы",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.РасчетРазницы" Тогда
		Рез=Истина;
		Если обЗначениеНеЗаполнено(ТекСтрока) Тогда
			Возврат Рез;
		КонецЕсли;
		Если ТекСтрока.Коэффициент = 0 Тогда
			ТекСтрока.КоличествоРазница=0;
		Иначе
			ТекСтрока.КоличествоРазница=(ТекСтрока.Количество)-((ТекСтрока.КоличествоПоДокументуПоступления*ТекСтрока.КоэффициентПоДокументуПоступления)/ТекСтрока.Коэффициент);
		КонецЕсли;
		ТекСтрока.СуммаНДСРазница=ТекСтрока.СуммаНДС-ТекСтрока.СуммаНДСПоДокументуПоступления;
		ТекСтрока.СуммаВсегоРазница=ТекСтрока.СуммаВсего-ТекСтрока.СуммаВсегоПоДокументуПоступления;
		ТекСтрока.СуммаРозничнаяРазница = ТекСтрока.СуммаРозничная - ТекСтрока.СуммаРозничнаяПоДокументуПоступления;
		Возврат Рез;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//Зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Неопределено;
	Результат=Новый Соответствие();
	//Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.ПрочиеАктивы,0);
	Возврат Результат;
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ДокументОснование",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ГТД",2);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары); 
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации, 
	// или если установлен способ контроля корректности договора и склада документа. 
	// Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов",КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
		
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат Тогда
		ХООснования = ПолучитьПартииТоваров().ПартияДок.ХозОперация;
		Если ХООснования = Справочники.ХозОперации.УслугиСтороннихОрганизаций ИЛИ ХООснования = Справочники.ХозОперации.УслугиПоСубподряду Тогда
			Для Каждого стрТовар Из Товары Цикл
				Если стрТовар.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
					Если стрТовар.Номенклатура.СпособРаспределенияДопРасходов <> Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы Тогда
						Результат = Ложь;
						дкДобавитьОшибку(Ошибки, "Услуга в строке №"+стрТовар.НомерСтроки+" распределяется не на доходы и расходы.");
					КонецЕсли;
				Иначе
					Результат = Ложь;
					дкДобавитьОшибку(Ошибки, "Номенклатура <"+стрТовар.Номенклатура+"> в строке №"+стрТовар.НомерСтроки+" не является услугой.");
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ПолучитьПартииТоваров().ПартияДок.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровКомиссия Тогда
			СтрокаОшибок = "";
			Для Каждого стрТовар Из Товары Цикл
				Если стрТовар.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
					Результат = Ложь;
					дкДобавитьОшибку(СтрокаОшибок, "Номенклатура <"+стрТовар.Номенклатура+"> в строке №"+стрТовар.НомерСтроки+" услуга.");
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ Результат И НЕ ПустаяСтрока(СтрокаОшибок) Тогда
				дкДобавитьОшибку(Ошибки, "При комиссионном поступлении запрещено вводить услуги:" + Символы.ПС + СтрокаОшибок);
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	Возврат Результат;
КонецФункции

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("КорректировкаПоступленияИсправлениеВПервичныхДокументах","Исправление в первичных документах",Истина);
	СписокХозОпераций.Добавить("КорректировкаПоступленияКорректировкаПоСогласованиюСторон","Корректировка по согласованию сторон",Ложь);
	Возврат СписокХозОпераций;
КонецФункции

Процедура ВыполнитьДвиженияПриИзмененииГТД(Отказ, Ошибки)
	
	ДокументПоступления = ДокументОснование;
	Пока НЕ ТипЗнч(ДокументПоступления) = Тип("ДокументСсылка.ПоступлениеТоваров") Цикл
		ДокументПоступления = ДокументПоступления.ДокументОснование;
	КонецЦикла;
	
	ПредДвиженияПоТекПроведениюГТД = Движения.ГТДПартийТоваровКомпании.Выгрузить().Скопировать();
	Движения.ГТДПартийТоваровКомпании.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ ВТ_ТЧ
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ГТД <> КорректировкаПоступленияТовары.ГТДДоКорректировки
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТЧ.Номенклатура КАК Номенклатура,
	|	ВТ_ТЧ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЕСТЬNULL(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокГТД,
	|	ГТДПартийТоваровКомпанииОстатки.ГТД КАК ГТД
	|ИЗ
	|	ВТ_ТЧ КАК ВТ_ТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	|				&МоментВремени,
	|				СкладКомпании = &СкладКомпании
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_ТЧ.Номенклатура КАК Номенклатура
	|						ИЗ
	|							ВТ_ТЧ КАК ВТ_ТЧ)) КАК ГТДПартийТоваровКомпанииОстатки
	|		ПО ВТ_ТЧ.Номенклатура = ГТДПартийТоваровКомпанииОстатки.Номенклатура
	|			И ВТ_ТЧ.ХарактеристикаНоменклатуры = ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДПартийТоваровКомпанииОстатки.ГТД,
	|	ВТ_ТЧ.Номенклатура,
	|	ВТ_ТЧ.ХарактеристикаНоменклатуры,
	|	ЕСТЬNULL(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток, 0)";
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Партия", ДокументПоступления);
	ТабСписатьГТД = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из ТабСписатьГТД Цикл
		
		НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
		
		НоваяЗапись.Регистратор                = Ссылка;
		НоваяЗапись.Период                     = Дата;
		НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
		НоваяЗапись.ХозОперация                = ХозОперация;
		НоваяЗапись.Номенклатура               = Строка.Номенклатура;
		НоваяЗапись.ХарактеристикаНоменклатуры = Строка.ХарактеристикаНоменклатуры;
		НоваяЗапись.ГТД                        = Строка.ГТД;
		НоваяЗапись.СкладКомпании              = СкладКомпании;
		НоваяЗапись.Партия                     = ДокументПоступления;
		НоваяЗапись.Количество                 = -Строка.КоличествоОстатокГТД;
		
	КонецЦикла;
	
	// Приход по ГТД
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПоступленияТовары.ГТД КАК ГТД,
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.ГТДДоКорректировки КАК ГТДДоКорректировки,
	|	КорректировкаПоступленияТовары.Количество * КорректировкаПоступленияТовары.Коэффициент КАК Количество,
	|	КорректировкаПоступленияТовары.КоличествоРазница КАК КоличествоРазница
	|ПОМЕСТИТЬ ВТ_ТЧ
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.ГТД <> ЗНАЧЕНИЕ(Справочник.ГТД.ПустаяСсылка)
	|	И КорректировкаПоступленияТовары.ГТД <> КорректировкаПоступленияТовары.ГТДДоКорректировки
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияТовары.ХарактеристикаНоменклатуры,
	|	КорректировкаПоступленияТовары.Номенклатура,
	|	КорректировкаПоступленияТовары.ГТД,
	|	КорректировкаПоступленияТовары.ГТДДоКорректировки,
	|	КорректировкаПоступленияТовары.Количество * КорректировкаПоступленияТовары.Коэффициент,
	|	КорректировкаПоступленияТовары.КоличествоРазница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТЧ.Номенклатура КАК Номенклатура,
	|	ВТ_ТЧ.ГТД КАК ГТД,
	|	ВТ_ТЧ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТЧ.ГТДДоКорректировки КАК ГТДДоКорректировки,
	|	ВТ_ТЧ.Количество КАК Количество,
	|	ЕСТЬNULL(ГТДПартийТоваровКомпанииОстатки.КоличествоОстаток, 0) - ВТ_ТЧ.КоличествоРазница КАК КоличествоОстатокГТД,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) - ВТ_ТЧ.КоличествоРазница КАК КоличествоОстатокПартия
	|ИЗ
	|	ВТ_ТЧ КАК ВТ_ТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ГТДПартийТоваровКомпании.Остатки(
	|				&МоментВремени,
	|				СкладКомпании = &СкладКомпании
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_ТЧ.Номенклатура КАК Номенклатура
	|						ИЗ
	|							ВТ_ТЧ КАК ВТ_ТЧ)) КАК ГТДПартийТоваровКомпанииОстатки
	|		ПО ВТ_ТЧ.Номенклатура = ГТДПартийТоваровКомпанииОстатки.Номенклатура
	|			И ВТ_ТЧ.ГТДДоКорректировки = ГТДПартийТоваровКомпанииОстатки.ГТД
	|			И ВТ_ТЧ.ХарактеристикаНоменклатуры = ГТДПартийТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки(
	|				&МоментВремени,
	|				СкладКомпании = &СкладКомпании
	|					И Партия = &Партия
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_ТЧ.Номенклатура КАК Номенклатура
	|						ИЗ
	|							ВТ_ТЧ КАК ВТ_ТЧ)) КАК ПартииТоваровКомпанииОстатки
	|		ПО ВТ_ТЧ.Номенклатура = ПартииТоваровКомпанииОстатки.Номенклатура
	|			И ВТ_ТЧ.ХарактеристикаНоменклатуры = ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры";
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Партия", ДокументПоступления);
	ТабТовары = Запрос.Выполнить().Выгрузить();
	Для Каждого Строка Из ТабТовары Цикл
		КоличествоГТДПартийТоваровКомпании = Мин(Строка.КоличествоОстатокПартия, Строка.Количество);
		Если КоличествоГТДПартийТоваровКомпании = 0 Тогда
			Продолжить;
		//Если Строка.КоличествоОстатокПартия < Строка.Количество Тогда
		//	Отказ = Истина;
		//	дкДобавитьОшибку(Ошибки, "Не удалось откорректировать ГТД / РНПТ по товару <"+Строка.Номенклатура+"> - партия была расформирована");
		//	Прервать;
		Иначе			
			НоваяЗапись = Движения.ГТДПартийТоваровКомпании.Добавить();
			
			НоваяЗапись.Регистратор                = Ссылка;
			НоваяЗапись.Период                     = Дата;
			НоваяЗапись.ВидДвижения                = ВидДвиженияНакопления.Приход;
			НоваяЗапись.ХозОперация                = ХозОперация;
			НоваяЗапись.Номенклатура               = Строка.Номенклатура;
			НоваяЗапись.ХарактеристикаНоменклатуры = Строка.ХарактеристикаНоменклатуры;
			НоваяЗапись.ГТД                        = Строка.ГТД;
			НоваяЗапись.СкладКомпании              = СкладКомпании;
			НоваяЗапись.Партия                     = ДокументПоступления;
		//	НоваяЗапись.Количество                 = Строка.Количество;
			НоваяЗапись.Количество                 = КоличествоГТДПартийТоваровКомпании;
			
		КонецЕсли;
				
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Движения.ГТДПартийТоваровКомпании.Записать();
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьДвиженияПриИзмененииГТД()

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

// Формирует табличный документ 
	// 
	// Параметры:
	//  ТабДокумент - табличный документ.
	//
	// Возвращаемое значение:
	//  ТабДокумент - возвращает сформированный табличный документ.
	//
Функция ПечатьПоступлениеТоваров(ТабДокумент) Экспорт
		Макет = ПолучитьМакет("ПоступлениеТоваров");
		
		//для начала настроим макет
		ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
		ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
		
		//вывод заголовка документа
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		
		ТекстЗаголовка = "Корректировка поступления № " + дкПолучитьНомерДляПечати(ЭтотОбъект)
			+ " от " + Формат(Дата, "ДФ = dd.MM.yyyy");
		
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(Контрагент);
		ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
		ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
		ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
		ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
		ОбластьМакета.Параметры.ПредставлениеСклада		= спПолучитьНаименование(СкладКомпании);
		
		//вывод свойств
		СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
		ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
		ТабДокумент.Вывести(ОбластьМакета);
		
		//сразу два, т.к. выводим на второй странице только
		НомерСтраницы = 2;
		НомерСтраницыПред = НомерСтраницы;
		
		//теперь выводим шапку
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
		//готовим области строки
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
		
		//заполним параметры шапки таблицы для следующего листа
		ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		//перебор строк
		ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
		Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
			//заполняем данные строки
			СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
			ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
			
			//доп. области
			мсвДопОбластиПодвала = Неопределено;
			Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
				мсвДопОбластиПодвала = Новый Массив;
				мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			//выводим строку, делая проверку попадания на лист
			НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
			
			//инициализация итогов по странице
			Если НомерСтраницы <> НомерСтраницыПред Тогда
				СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
				НомерСтраницыПред = НомерСтраницы;
				ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
			КонецЕсли;
			//добавляем итоги
			дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		КонецЦикла;
		
		//выводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
		НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
		ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
		
		// Выводим представления и расшифровки подписей
		Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
		Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
		ОбластьПодвал.Параметры.Заполнить(Отпустил);
		Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
		Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
		ОбластьПодвал.Параметры.Заполнить(Получил);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
		
		Возврат ТабДокумент;
	КонецФункции // ПечатьПоступлениеТоваров()

// Формирует печатную форму "УКД"
// Возвращает сформированный табличный документ:
Функция ПечатьУКД(ТабДокумент) Экспорт
	
	ТабДокумент = дкПечатьУКД(ЭтотОбъект, ТабДокумент);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьУКД

// Функция получения данных для УКД.
//
Функция ПолучитьДанныеДляПечатиУКД() Экспорт
	
	ДокументФактура = Документы.СчетФактураПолученный.НайтиПоРеквизиту("ДокументОснование", ЭтотОбъект.Ссылка);
	ДокументОбъект = ДокументФактура;
	ДанныеОбъекта = Новый Структура();
	
	// Сформируем статус документа
	Если ДокументФактура <> Документы.СчетФактураПолученный.ПустаяСсылка() И НЕ ДокументФактура.ПометкаУдаления Тогда
		Статус = 1;
		ДокументОбъект = ДокументФактура;
	Иначе
		Статус = 2;
		ДокументОбъект = ЭтотОбъект;
	КонецЕсли;
	Если Дата > Дата('20210701') Тогда
		ТаблицаТоваров = Документы.СчетФактураПолученный.ТоварыДляПечатиУКД(ЭтотОбъект.Ссылка);
	Иначе
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПоступленияТовары.Количество КАК Количество,
	|	КорректировкаПоступленияТовары.КоличествоРазница КАК КоличествоРазница,
	|	КорректировкаПоступленияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	КорректировкаПоступленияТовары.Коэффициент КАК Коэффициент,
	|	КорректировкаПоступленияТовары.СтавкаНДС КАК СтавкаНДС,
	|	КорректировкаПоступленияТовары.СуммаНДС КАК СуммаНДС,
	|	КорректировкаПоступленияТовары.СуммаНДСРазница КАК СуммаНДСРазница,
	|	КорректировкаПоступленияТовары.СуммаРозничная КАК СуммаРозничная,
	|	КорректировкаПоступленияТовары.СуммаРозничнаяРазница КАК СуммаРозничнаяРазница,
	|	КорректировкаПоступленияТовары.СуммаВсего КАК СуммаВсего,
	|	КорректировкаПоступленияТовары.СуммаВсегоРазница КАК СуммаВсегоРазница,
	|	КорректировкаПоступленияТовары.ГТД.Страна.Наименование КАК ПредставлениеСтраны,
	|	КорректировкаПоступленияТовары.ГТД.Наименование КАК ПредставлениеГТД,
	|	КорректировкаПоступленияТовары.ГТД.Страна.Код КАК СтранаПроисхождения,
	|	КорректировкаПоступленияТовары.ГТД.РНПТ КАК РНПТ
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И (КорректировкаПоступленияТовары.Количество <> КорректировкаПоступленияТовары.КоличествоПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияТовары.ЕдиницаИзмерения <> КорректировкаПоступленияТовары.ЕдиницаИзмеренияПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияТовары.Коэффициент <> КорректировкаПоступленияТовары.КоэффициентПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияТовары.СтавкаНДС <> КорректировкаПоступленияТовары.СтавкаНДСПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияТовары.СуммаНДС <> КорректировкаПоступленияТовары.СуммаНДСПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияТовары.СуммаРозничная <> КорректировкаПоступленияТовары.СуммаРозничнаяПоДокументуПоступления
	|			ИЛИ КорректировкаПоступленияТовары.СуммаВсего <> КорректировкаПоступленияТовары.СуммаВсегоПоДокументуПоступления)";
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
КонецЕсли;

	// данные документа
	ДанныеОбъекта.Вставить("Дата"                   	, ДокументОбъект.Дата);
	ДанныеОбъекта.Вставить("Номер"                  	, дкПолучитьНомерДляПечати(ДокументОбъект));
	ДанныеОбъекта.Вставить("ХозОперация"            	, ДокументОбъект.ХозОперация);
	ДанныеОбъекта.Вставить("ДокументОснование"      	, ДокументОбъект.ДокументОснование);
	ДанныеОбъекта.Вставить("ВалютаДокумента"      		, ЭтотОбъект.ВалютаДокумента);
	ДанныеОбъекта.Вставить("Поставщик"              	, ДокументОбъект.Контрагент);
	ДанныеОбъекта.Вставить("ПодразделениеКомпании"  	, ДокументОбъект.ПодразделениеКомпании);
	ДанныеОбъекта.Вставить("Покупатель"              	, ДокументОбъект.Организация);
	ДанныеОбъекта.Вставить("Организация"              	, ЭтотОбъект.Организация);
	ДанныеОбъекта.Вставить("ДоговорВзаиморасчетов"     	, ?(ЗначениеЗаполнено(ДокументОбъект.ДоговорВзаиморасчетов), ЭтотОбъект.ДоговорВзаиморасчетов, "--"));
	ДанныеОбъекта.Вставить("Товары"              		, ТаблицаТоваров);
	ДанныеОбъекта.Вставить("Статус"              		, Статус);
	ДанныеОбъекта.Вставить("Ссылка"              		, ДокументОбъект.Ссылка);
	ДанныеОбъекта.Вставить("ИдентификаторГосударственногоКонтракта", Неопределено);
	ДанныеОбъекта.Вставить("ДатаОтгрузки", Неопределено);
	
	Если Статус = 1 Тогда
		ДанныеОбъекта.Вставить("НомерИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.НомерИсходногоДокумента),
				дкПолучитьНомерДляПечати(ДокументОбъект,,"НомерИсходногоДокумента"),
				"--"));
		ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",
			?(ЗначениеЗаполнено(ДокументОбъект.ДатаИсходногоДокумента), ДокументОбъект.ДатаИсходногоДокумента, "--"));
		ДанныеОбъекта.Вставить("ПлатежноРасчетныеДокументы" , Неопределено);
	КонецЕсли;
	
	// Свойства
	ДанныеОбъекта.Вставить("Руководитель"     		, дкОтветственноеЛицо(ЭтотОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.Руководитель));
	ДанныеОбъекта.Вставить("ГлавныйБухгалтер" 		, дкОтветственноеЛицо(ЭтотОбъект.ДокументОснование, Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер));
	ДанныеОбъекта.Вставить("Менеджер" 				, дкОтветственноеЛицо(ДокументОбъект, "Менеджер"));
	
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецЕсли

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание,,Ложь) Тогда
		Возврат;
	КонецЕсли;

	Если НЕ ВозможенВводНаОсновании(Основание) Тогда
		ДополнительныеСвойства.Вставить("ВводНаОснованииЗапрещен", Истина);
		Возврат;
	КонецЕсли;
	
	Если Основание <> Неопределено Тогда
		Контрагент = Основание.Контрагент;
		ДоговорВзаиморасчетов = Основание.ДоговорВзаиморасчетов;
		Если НЕ Основание.Проведен Тогда
			Сообщить("Ввод корректировки поступления возможен только на основании проведенного документа.",
			СтатусСообщения.Внимание);
			дкОтменаВводаДокумента(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("ИдентификаторТовара");
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ПоступлениеТоваровТовары.Номенклатура,
			|	ПоступлениеТоваровТовары.Количество КАК Количество,
			|	ПоступлениеТоваровТовары.ЕдиницаИзмерения,
			|	ПоступлениеТоваровТовары.Коэффициент,
			|	ПоступлениеТоваровТовары.Цена,
			|	ПоступлениеТоваровТовары.Сумма КАК Сумма,
			|	ПоступлениеТоваровТовары.СуммаВсего КАК СуммаВсего,
			|	ПоступлениеТоваровТовары.СтавкаНДС,
			|	ПоступлениеТоваровТовары.СуммаНДС КАК СуммаНДС,
			|	ПоступлениеТоваровТовары.ЦенаРозничная,
			|	ПоступлениеТоваровТовары.СуммаРозничная КАК СуммаРозничная,
			|	ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры,
			|	ПоступлениеТоваровТовары.ГТД,
			|	ПоступлениеТоваровТовары.ИдентификаторТовара
			|ИЗ
			|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
			|ГДЕ
			|	ПоступлениеТоваровТовары.Ссылка = &Основание
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПоступлениеТоваровТовары.НомерСтроки";
			Запрос.УстановитьПараметр("Основание", Основание);
			
			КодыМаркировкиОснования = Основание.КодыМаркировки.Выгрузить();
			
			ВхДокНомер = "";
			
			ВхДокДата = Дата(1,1,1);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрокаТоваров                                       = Товары.Добавить();
				НоваяСтрокаТоваров.ПоДокументуПоступления                 = Истина;
				НоваяСтрокаТоваров.Подтверждение                         = Истина;
				НоваяСтрокаТоваров.Номенклатура                          = Выборка.Номенклатура;
				НоваяСтрокаТоваров.ХарактеристикаНоменклатуры            = Выборка.ХарактеристикаНоменклатуры;
				НоваяСтрокаТоваров.ЕдиницаИзмерения                      = Выборка.ЕдиницаИзмерения;
				НоваяСтрокаТоваров.ЕдиницаИзмеренияПоДокументуПоступления = Выборка.ЕдиницаИзмерения;
				НоваяСтрокаТоваров.Коэффициент                           = Выборка.Коэффициент;
				НоваяСтрокаТоваров.КоэффициентПоДокументуПоступления      = Выборка.Коэффициент;
				НоваяСтрокаТоваров.Количество                            = Выборка.Количество;
				НоваяСтрокаТоваров.КоличествоПоДокументуПоступления       = Выборка.Количество;
				НоваяСтрокаТоваров.Цена                                  = Выборка.Цена;
				НоваяСтрокаТоваров.ЦенаРозничная                         = Выборка.ЦенаРозничная;
				НоваяСтрокаТоваров.Сумма                                 = Выборка.Сумма;
				НоваяСтрокаТоваров.СуммаРозничная                        = Выборка.СуммаРозничная;
				НоваяСтрокаТоваров.СуммаРозничнаяПоДокументуПоступления   = Выборка.СуммаРозничная;
				НоваяСтрокаТоваров.СуммаВсего                            = Выборка.СуммаВсего;
				НоваяСтрокаТоваров.СуммаВсегоПоДокументуПоступления       = Выборка.СуммаВсего;
				НоваяСтрокаТоваров.СтавкаНДС                             = Выборка.СтавкаНДС;
				НоваяСтрокаТоваров.СуммаНДС                              = Выборка.СуммаНДС;
				НоваяСтрокаТоваров.СтавкаНДСПоДокументуПоступления        = Выборка.СтавкаНДС;
				НоваяСтрокаТоваров.СуммаНДСПоДокументуПоступления         = Выборка.СуммаНДС;
				НоваяСтрокаТоваров.ГТДДоКорректировки					 = Выборка.ГТД;
				НоваяСтрокаТоваров.ГТД                                   = Выборка.ГТД;
				НоваяСтрокаТоваров.ИдентификаторТовара                   = Выборка.ИдентификаторТовара;
				
				// Заполним коды маркировки товаров
				СтруктураПоиска.ИдентификаторТовара = НоваяСтрокаТоваров.ИдентификаторТовара;
				НайденныеКодыМаркировки = КодыМаркировкиОснования.НайтиСтроки(СтруктураПоиска);
				Для Каждого ТекущаяСтрока Из НайденныеКодыМаркировки Цикл
					ЗаполнитьЗначенияСвойств(КодыМаркировки.Добавить(), ТекущаяСтрока);
				КонецЦикла;
				
			КонецЦикла;
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
			Запрос = Новый Запрос;
			ОснованиеКорректировка = (Основание.ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияКорректировкаПоСогласованиюСторон);
			
			ТоварыОснования = Основание.Товары.Выгрузить();
			КодыМаркировкиОснования = Основание.КодыМаркировки.Выгрузить();
			
			ВхДокНомер = "";
			
			ВхДокДата = Дата(1,1,1);
			
			Для Каждого стрТоваров Из ТоварыОснования Цикл
				Если (НЕ стрТоваров.Подтверждение) ИЛИ (стрТоваров.Количество = 0) Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрокаТоваров = Товары.Добавить();
				НоваяСтрокаТоваров.ПоДокументуПоступления                 = Истина;
				НоваяСтрокаТоваров.Подтверждение                         = Истина;
				НоваяСтрокаТоваров.ИдентификаторТовара = стрТоваров.ИдентификаторТовара;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТоваров,стрТоваров,,
				"ПоДокументуПоступления,Подтверждение,КоличествоРазница,СуммаНДСРазница,СуммаРозничнаяРазница,СуммаВсегоРазница");
				НоваяСтрокаТоваров.КоличествоПоДокументуПоступления     = НоваяСтрокаТоваров.Количество;
				НоваяСтрокаТоваров.СуммаНДСПоДокументуПоступления       = НоваяСтрокаТоваров.СуммаНДС;
				НоваяСтрокаТоваров.СтавкаНДСПоДокументуПоступления      = НоваяСтрокаТоваров.СтавкаНДС;
				НоваяСтрокаТоваров.СуммаВсегоПоДокументуПоступления     = НоваяСтрокаТоваров.СуммаВсего;
				НоваяСтрокаТоваров.СуммаРозничнаяПоДокументуПоступления = НоваяСтрокаТоваров.СуммаРозничная;
				Если ОснованиеКорректировка Тогда
					НоваяСтрокаТоваров.КоличествоДоКорректировки     = НоваяСтрокаТоваров.КоличествоПоДокументуПоступления;
					НоваяСтрокаТоваров.СуммаНДСДоКорректировки       = НоваяСтрокаТоваров.СтавкаНДСПоДокументуПоступления;
					НоваяСтрокаТоваров.СуммаВсегоДоКорректировки     = НоваяСтрокаТоваров.СуммаВсегоПоДокументуПоступления;
					НоваяСтрокаТоваров.СуммаРозничнаяДоКорректировки = НоваяСтрокаТоваров.СуммаРозничнаяПоДокументуПоступления;
					НоваяСтрокаТоваров.ГТДДоКорректировки			 = НоваяСтрокаТоваров.ГТД;
				КонецЕсли;
				
				// Заполним коды маркировки товаров
				СтруктураПоиска.ИдентификаторТовара = НоваяСтрокаТоваров.ИдентификаторТовара;
				НайденныеКодыМаркировки = КодыМаркировкиОснования.НайтиСтроки(СтруктураПоиска);
				Для Каждого ТекущаяСтрока Из НайденныеКодыМаркировки Цикл
					Если ТекущаяСтрока.Возврат Тогда
						Продолжить;
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(КодыМаркировки.Добавить(), ТекущаяСтрока);
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
	Иначе
		
		Сообщить("Ввод корректировки поступления возможен только на основании.",СтатусСообщения.Внимание);
		Возврат;
		
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

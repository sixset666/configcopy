// Модуль документа ПересортицаТоваров

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования


// возвращает результат запроса по таблице товаров, для проведения по регистру "ОстаткиТоваровКомпании"
//
// Параметры:
//  Направление - строка - задает направление "Приход" или "Расход".
//
Функция ПолучитьРезультатЗапросаОстатки(Направление)
    Направление = ?(Направление="Приход", "",Направление);
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ПересортицаТоваровТовары.Номенклатура"+Направление+" КАК Номенклатура,
	|	ПересортицаТоваровТовары.ХарактеристикаНоменклатуры"+Направление+" КАК ХарактеристикаНоменклатуры,
	|	МАКСИМУМ(ПересортицаТоваровТовары.ЦенаРозничная"+Направление+") КАК ЦенаРозничная,
	|	СУММА(ПересортицаТоваровТовары.Количество"+Направление+"*ПересортицаТоваровТовары.Коэффициент"+Направление+") КАК Количество,
	|	СУММА(ПересортицаТоваровТовары.СуммаРозничная"+Направление+") КАК СуммаРозничная,
	|	СУММА(0) КАК Резерв
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
	|ГДЕ
	|	ПересортицаТоваровТовары.Ссылка=&Ссылка
	|СГРУППИРОВАТЬ
	|	ПО ПересортицаТоваровТовары.Номенклатура"+Направление+",ПересортицаТоваровТовары.ХарактеристикаНоменклатуры"+Направление);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаОстатки()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Набор,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("НоменклатураРасход",3);
	Если обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект) Тогда
		ОбязательныеТовары.Вставить("Партия",2);
		ОбязательныеТовары.Вставить("ГТДРасход",2);
	КонецЕсли;
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("КоличествоРасход",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмеренияРасход",1);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("КоэффициентРасход",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатурыРасход",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ИмяКолонкиНоменклатуры","НоменклатураРасход");
	ДопПараметры.Вставить("ИмяКолонкиХарактеристики","ХарактеристикаНоменклатурыРасход");
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, , ДопПараметры);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект,,"НоменклатураРасход", "ГТДРасход");
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПересортицаТоваров","Пересортица товаров",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Ссылка на документ для которого получаем шапку.
//
// Возвращаемое значение:
//  Выборка - выборка по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		Возврат НЕ Отказ;
	КонецЕсли;
	
	// проверяем, присутствуют ли партии в табличной части
	ЕстьПартии = Ложь;
	Для Каждого СтрокаТовар Из Товары Цикл
		Если ЗначениеЗаполнено(СтрокаТовар.Партия) Тогда
			ЕстьПартии = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
	НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
	НаборЗаписейПартии.Сторно=Ложь;
	НаборЗаписейПартии.ИмяРеквизитаДокумент=?(ЕстьПартии,"Партия","");
	НаборЗаписейПартии.СтатусПартии=Перечисления.СтатусыПартий.ТоварКупленный;
	НаборЗаписейПартии.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейПартии.Пересортица() ИЛИ Отказ;
	
	// считаем суммы, для проведения по доходам и расходам
	СписалиУпр = 0;
	ОприходовалиУпр = 0;
	Для Каждого Запись Из НаборЗаписейПартии Цикл
		Если Запись.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			ОприходовалиУпр = ОприходовалиУпр + Запись.СуммаУпр;
		Иначе
			СписалиУпр = СписалиУпр + Запись.СуммаУпр;
		КонецЕсли;
	КонецЦикла;
	Разница = ОприходовалиУпр - СписалиУпр;
	
	// проводим по ДиР
	Если Разница <> 0 Тогда
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Пересортица;
		Если Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению Тогда
			НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.СкладКомпании.Подразделение;
		КонецЕсли;	
		НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
		Если Разница > 0 Тогда
			НаборЗаписейДоходыИРасходы.Доход = Разница;
		Иначе
			НаборЗаписейДоходыИРасходы.Расход = - Разница;
		КонецЕсли;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Расчет суммы документа
//
// Возвращаемое значение:
//  Возвращает итог по клонке "СуммаРозничнаяПриход".
//
Функция РассчитатьСуммуВсего()Экспорт
	Возврат Товары.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	Если Имя="СкладКомпании" Тогда
		дкОбработкаРеквизита(ЭтотОбъект,"СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		Для Каждого стрТовары Из Товары Цикл
			Если обЗначениеНеЗаполнено(СкладКомпании) ИЛИ обЗначениеНеЗаполнено(стрТовары.Номенклатура) Тогда
				стрТовары.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Иначе
				стрТовары.Ячейка=Справочники.Номенклатура.ПолучитьЯчейкуХранения(стрТовары.Номенклатура,СкладКомпании);
			КонецЕсли;
			Если обЗначениеНеЗаполнено(СкладКомпании) ИЛИ обЗначениеНеЗаполнено(стрТовары.НоменклатураРасход) Тогда
				стрТовары.ЯчейкаРасход=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Иначе
				стрТовары.ЯчейкаРасход=Справочники.Номенклатура.ПолучитьЯчейкуХранения(стрТовары.НоменклатураРасход,СкладКомпании);
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Имя="Товары.НоменклатураРасход" Тогда
		// проверки
		Если обЗначениеНеЗаполнено(ТекСтрока.НоменклатураРасход) Тогда 
			ТекСтрока.ЯчейкаРасход=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Возврат Ложь;
		КонецЕсли;
		Если ТекСтрока.НоменклатураРасход.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга ИЛИ ТекСтрока.НоменклатураРасход.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Набор Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.НоменклатураРасход.ВидНоменклатуры)+"""",5);
			#КонецЕсли
			ТекСтрока.НоменклатураРасход=Неопределено; Возврат Ложь;
			ОбработкаРеквизита("Товары.НоменклатураРасход",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ХарактеристикаНоменклатурыРасход) Тогда
			ТекСтрока.ХарактеристикаНоменклатурыРасход=НЕОПРЕДЕЛЕНО;
		КонецЕсли;
		Попытка 
			ТекСтрока.ЕдиницаИзмеренияРасход=ДопПараметры.ЕдиницаИзмеренияРасход;
		Исключение
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.НоменклатураРасход) Тогда
				ТекСтрока.ЕдиницаИзмеренияРасход=ТекСтрока.НоменклатураРасход.ОсновнаяЕдиницаИзмерения;
			КонецЕсли;
		КонецПопытки;
		
		ТекСтрока.КоэффициентРасход=1;
		ТекСтрока.КоличествоРасход=?(ТекСтрока.КоличествоРасход=0,1,ТекСтрока.КоличествоРасход);
		// получим розничную цену
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ТекСтрока.ЦенаРозничнаяРасход = обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли,ТекСтрока.НоменклатураРасход,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатурыРасход, ТекСтрока.ЕдиницаИзмеренияРасход, СкладКомпании.Подразделение);
			Возврат ОбработкаРеквизита("Товары.ЦенаРозничнаяРасход",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		// заполнение ячейки
		Если НЕ ТекСтрока=Неопределено Тогда
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.НоменклатураРасход) И НЕ обЗначениеНеЗаполнено(СкладКомпании) ТОгда
				ТекСтрока.ЯчейкаРасход=Справочники.Номенклатура.ПолучитьЯчейкуХранения(ТекСтрока.НоменклатураРасход,СкладКомпании);
			Иначе
				ТекСтрока.ЯчейкаРасход=Справочники.ЯчейкиХранения.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		// проверки
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			ТекСтрока.Ячейка=Справочники.ЯчейкиХранения.ПустаяСсылка();
			Возврат Ложь;
		КонецЕсли;
		Если ТекСтрока.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга ИЛИ ТекСтрока.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Набор Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""",5);
			#КонецЕсли
			ТекСтрока.Номенклатура=Неопределено; Возврат Ложь;
			ОбработкаРеквизита("Товары.Номенклатура",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			ТекСтрока.ЕдиницаИзмерения=ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
		КонецЕсли;
		ТекСтрока.Коэффициент=1;
		ТекСтрока.Количество=?(ТекСтрока.Количество=0,1,ТекСтрока.Количество);
		
		// получим цену
		ТекСтрока.Цена=обПолучитьЦену(ТипЦен,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
		ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
		// получим розничную цену
		Если НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ТекСтрока.ЦенаРозничная=обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли, ТекСтрока.Номенклатура, ?(Ссылка.Пустая(), Дата, МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
			Возврат ОбработкаРеквизита("Товары.ЦенаРозничная", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ТекСтрока.ПонижениеСортности Тогда
			ТекСтрока.Количество=ТекСтрока.КоличествоРасход;
		КонецЕсли;
		Если Не ОбработкаРеквизита("Товары.ЦенаРозничная",ТекСтрока,ЭтаФорма,ДопПараметры) Тогда
			Возврат Ложь;
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.КоличествоРасход" Тогда
		Если ТекСтрока.ПонижениеСортности Тогда
			ТекСтрока.Количество=ТекСтрока.КоличествоРасход;
		КонецЕсли;
		Возврат ОбработкаРеквизита("Товары.ЦенаРозничнаяРасход",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		ТекСтрока.ЦенаРозничная=?(ТекСтрока.Количество=0,0,ТекСтрока.СуммаРозничная/(ТекСтрока.Количество*ТекСтрока.Коэффициент));
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ЦенаРозничнаяРасход" Тогда
		ТекСтрока.СуммаРозничнаяРасход=ТекСтрока.ЦенаРозничнаяРасход*ТекСтрока.КоличествоРасход*ТекСтрока.КоэффициентРасход;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.СуммаРозничнаяРасход" Тогда
		ТекСтрока.ЦенаРозничнаяРасход=?(ТекСтрока.КоличествоРасход=0,0,ТекСтрока.СуммаРозничнаяРасход/(ТекСтрока.КоличествоРасход*ТекСтрока.КоэффициентРасход));
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество*ТекСтрока.Коэффициент;
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/(ТекСтрока.Количество*ТекСтрока.Коэффициент));
		Возврат Истина;
		
	ИначеЕсли Имя="ТипЦен" Тогда
		ТребуетсяПересчет=Истина;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ТребуетсяПересчет",ТребуетсяПересчет) Тогда
				ТребуетсяПересчет=Истина;
			КонецЕсли; 
		КонецЕсли;
		Если ТребуетсяПересчет Тогда
			Для Каждого ТекСтрока Из Товары Цикл
				Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура, Ссылка, , ВалютаДокумента, КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
				Если Цена<>0 Тогда
					ТекСтрока.Цена = Цена;
					ЭтотОбъект.ОбработкаРеквизита("Товары.Цена", ТекСтрока, ЭтаФорма);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="Товары.ПонижениеСортности" Тогда
		ТекСтрока.Количество=ТекСтрока.КоличествоРасход;
		Возврат ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА


// Формирует печатную форму "Накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПереоценка(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("Переоценка");
	
	//для начала настроим макет
	ОбластьТовар = Макет.Область("Товар");
	ЕстьКод = обПраво("ВыводитьКодВПечатныхФормах", Права,,ЭтотОбъект);
	
	//если код не выводим
	Если НЕ ЕстьКод Тогда
		ОбластьКод = Макет.Область("Код");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);		
	КонецЕсли;
	
	ЭтоРозничный = Ложь;
	ТипОперации = "Опт";
	Если (НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный) Тогда
		ЭтоРозничный = Истина;
		ТипОперации = "Розница";
	КонецЕсли;
	//теперь запишем параметры шапки
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы"+ТипОперации);
	
	ИмяКода = "";
	Если ЕстьКод Тогда
		КолонкаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект);
		ИмяКода = КолонкаКода.Имя;
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиКод = КолонкаКода.Синоним;
	КонецЕсли;
	
	//форматы вывода
	ФорматВыводаКоличества	= обПраво("ФорматВыводаКоличества", Права,,ЭтотОбъект);
	ФорматВыводаСуммы		= обПраво("ФорматВыводаСуммы", Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал"+ТипОперации);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка"+ТипОперации);
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице"+ТипОперации);
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаПереоценки",ВалютаДокумента,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	СуммаВсего = 0;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = Новый Структура;
		ТоварНаименование = спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура);
		//характеристику выводим в строке с наименованием
		ТоварНаименование = ТоварНаименование + ", " + спПолучитьНаименование(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
		//заполняем структуру строки
		СтруктураСтроки.Вставить("НомерСтроки",СтрокаТабличнойЧасти.НомерСтроки);
		СтруктураСтроки.Вставить("ТоварНаименование",ТоварНаименование);
		//для расшифровки
		СтруктураСтроки.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
		
		//получаем код номенклатуры в зависимости от настройки прав
		Если ЕстьКод Тогда
			//КодНоменклатуры=СтрокаТабличнойЧасти.Номенклатура[ИмяКода];
			КодНоменклатуры=дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.Номенклатура,ИмяРеквизитаКода,ЭтотОбъект);
			СтруктураСтроки.Вставить("Код", КодНоменклатуры);
		КонецЕсли;
		
		//если количество не равно базовому, корректируем вывод
		СтруктураСтроки.Вставить("Количество",Формат(СтрокаТабличнойЧасти.Количество,ФорматВыводаКоличества));
		СтруктураСтроки.Вставить("ЕдиницаИзмерения",СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		Если ЭтоРозничный Тогда
			СуммаПереоценки = СтрокаТабличнойЧасти.СуммаРозничная-СтрокаТабличнойЧасти.СуммаРозничнаяРасход;
			СуммаПереоценки = ?(СуммаПереоценки<0,-СуммаПереоценки,СуммаПереоценки);
			СтруктураСтроки.Вставить("СуммаПереоценки",Формат(СуммаПереоценки,ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("ЦенаСтарая",Формат(СтрокаТабличнойЧасти.ЦенаРозничнаяРасход,ФорматВыводаСуммы));			
			СтруктураСтроки.Вставить("Цена",Формат(СтрокаТабличнойЧасти.ЦенаРозничная,ФорматВыводаСуммы));					
			ПроцентНаценки = ?(СтрокаТабличнойЧасти.ЦенаРозничнаяРасход = 0,0,100*(СтрокаТабличнойЧасти.ЦенаРозничная-СтрокаТабличнойЧасти.ЦенаРозничнаяРасход)/СтрокаТабличнойЧасти.ЦенаРозничнаяРасход);
			СтруктураСтроки.Вставить("ПроцентНаценки",Формат(ПроцентНаценки,"ЧЦ=6; ЧДЦ=2"));
		Иначе
			СуммаПереоценки = СтрокаТабличнойЧасти.Сумма;
			СтруктураСтроки.Вставить("СуммаПереоценки",Формат(СуммаПереоценки, ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("Цена", Формат(СтрокаТабличнойЧасти.Цена, ФорматВыводаСуммы));
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
		НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаПереоценки",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		СтруктураИтоговПоСтранице.Вставить("СуммаПереоценки",СтруктураИтоговПоСтранице.СуммаПереоценки + СуммаПереоценки);
		
		СуммаВсего = СуммаВсего + СуммаПереоценки;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований " + ВыборкаТабличнойЧасти.Количество() +
	" на сумму " + обЧислоПрописью(СуммаВсего, ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьПодвал.Параметры.Заполнить(МОЛ);
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПереоценка()

// Формирует печатную форму "Накладная"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПересортицаТоваров(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПересортицаТоваров");
	
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	
	ЕстьКод = обПраво("ВыводитьКодВПечатныхФормах", Права,,ЭтотОбъект);
	//если код не выводим
	Если НЕ ЕстьКод Тогда
		ОбластьТовар = Макет.Область("ТоварРасход");
		ОбластьКод = Макет.Область("КодРасход");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);
		ОбластьТовар = Макет.Область("ТоварПриход");
		ОбластьКод = Макет.Область("КодПриход");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	
	ЭтоРозничный = (НЕ обЗначениеНеЗаполнено(СкладКомпании) И СкладКомпании.Розничный);
	ПоказыватьСебестоимость = Истина;
	Если Не ЭтоРозничный И Не обПраво("ПоказыватьСебестоимостьПроведения", Права,,ЭтотОбъект) Тогда
		ОбластьТовар = Макет.Область("ТоварРасход");
		ОбластьСуммаСписания = Макет.Область("СуммаСписания");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьСуммаСписания.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьСуммаСписания,ТипСмещенияТабличногоДокумента.ПоВертикали);
		ПоказыватьСебестоимость = Ложь;
	КонецЕсли;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ИмяКода = "";
	Если ЕстьКод Тогда
		КолонкаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект);
		ИмяКода = КолонкаКода.Имя;
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиКод = КолонкаКода.Синоним;
	КонецЕсли;
	
	ОбластьШапкаТаблицы.Параметры.ИмяКолонкиЦена = ?(ЭтоРозничный, "Цена розн.", "Цена");
	
	//форматы вывода
	ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",Права,,ЭтотОбъект);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаРозничнаяРасход,СуммаРозничнаяПриход",ВалютаДокумента,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	СуммаВсегоРасход = 0; СуммаВсегоПриход = 0;
	СуммаСписания = 0;
	СуммаОприходования = 0;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = Новый Структура;
		ТоварНаименованиеРасход = спПолучитьНаименование(СтрокаТабличнойЧасти.НоменклатураРасход);
		ТоварНаименованиеПриход = спПолучитьНаименование(СтрокаТабличнойЧасти.Номенклатура);
		//характеристику выводим в строке с наименованием
		ТоварНаименованиеРасход = ТоварНаименованиеРасход + ", " + спПолучитьНаименование(СтрокаТабличнойЧасти.ХарактеристикаНоменклатурыРасход);
		ТоварНаименованиеПриход = ТоварНаименованиеПриход + ", " + спПолучитьНаименование(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);		
		//заполняем структуру строки
		СтруктураСтроки.Вставить("НомерСтроки",СтрокаТабличнойЧасти.НомерСтроки);
		СтруктураСтроки.Вставить("ТоварНаименованиеРасход",ТоварНаименованиеРасход);
		СтруктураСтроки.Вставить("ТоварНаименованиеПриход",ТоварНаименованиеПриход);		
		//для расшифровки
		СтруктураСтроки.Вставить("НоменклатураРасход",СтрокаТабличнойЧасти.НоменклатураРасход);
		СтруктураСтроки.Вставить("НоменклатураПриход",СтрокаТабличнойЧасти.Номенклатура);		
		
		//получаем код номенклатуры в зависимости от настройки прав
		Если ЕстьКод Тогда
			//КодНоменклатуры=СтрокаТабличнойЧасти.НоменклатураРасход[ИмяКода];
			КодНоменклатуры=дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.НоменклатураРасход,ИмяРеквизитаКода,ЭтотОбъект);
			СтруктураСтроки.Вставить("КодРасход", КодНоменклатуры);
			//КодНоменклатуры=СтрокаТабличнойЧасти.Номенклатура[ИмяКода];
			КодНоменклатуры=дкПолучитьЗначениеКолонкиКода(СтрокаТабличнойЧасти.Номенклатура,ИмяРеквизитаКода,ЭтотОбъект);
			СтруктураСтроки.Вставить("КодПриход", КодНоменклатуры);
		КонецЕсли;
		
		//если количество не равно базовому, корректируем вывод
		СтруктураСтроки.Вставить("КоличествоРасход",Формат(СтрокаТабличнойЧасти.КоличествоРасход,ФорматВыводаКоличества));
		СтруктураСтроки.Вставить("КоличествоПриход",Формат(СтрокаТабличнойЧасти.Количество,ФорматВыводаКоличества));
		СтруктураСтроки.Вставить("ЕдиницаИзмеренияРасход",СтрокаТабличнойЧасти.ЕдиницаИзмеренияРасход);
		СтруктураСтроки.Вставить("ЕдиницаИзмеренияПриход",СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		Если ЭтоРозничный Тогда
			СуммаСписания = СтрокаТабличнойЧасти.СуммаРозничнаяРасход;
			СуммаОприходования = СтрокаТабличнойЧасти.СуммаРозничная;
			СтруктураСтроки.Вставить("ЦенаРозничнаяРасход",Формат(СтрокаТабличнойЧасти.ЦенаРозничнаяРасход,ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("ЦенаРозничнаяПриход",Формат(СтрокаТабличнойЧасти.ЦенаРозничная,ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("СуммаРозничнаяРасход",Формат(СуммаСписания, ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("СуммаРозничнаяПриход",Формат(СуммаОприходования, ФорматВыводаСуммы));
		Иначе
			Если ПоказыватьСебестоимость Тогда
				СуммаСписания = дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании", тКэшСуммСписания, 
				СтрокаТабличнойЧасти.НоменклатураРасход,
				СтрокаТабличнойЧасти.ХарактеристикаНоменклатурыРасход,
				СтрокаТабличнойЧасти.НомерСтроки);
				ЦенаСписания = ?(СтрокаТабличнойЧасти.КоличествоРасход=0, 0 , СуммаСписания/СтрокаТабличнойЧасти.КоличествоРасход);
			Иначе
				СуммаСписания = 0;
				ЦенаСписания = 0;
			КонецЕсли;
			СуммаОприходования = СтрокаТабличнойЧасти.Сумма;
			СтруктураСтроки.Вставить("ЦенаРозничнаяРасход", Формат(ЦенаСписания, ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("ЦенаРозничнаяПриход",Формат(СтрокаТабличнойЧасти.Цена, ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("СуммаРозничнаяРасход",Формат(СуммаСписания, ФорматВыводаСуммы));
			СтруктураСтроки.Вставить("СуммаРозничнаяПриход",Формат(СуммаОприходования, ФорматВыводаСуммы));
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаРозничнаяРасход,СуммаРозничнаяПриход",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтруктураСтроки, СтруктураИтоговПоСтранице);
		
		СуммаВсегоРасход = СуммаВсегоРасход + СуммаСписания;		
		СуммаВсегоПриход = СуммаВсегоПриход + СуммаОприходования;
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	Если Не ЭтоРозничный И Не ПоказыватьСебестоимость Тогда
		ОбластьПодвал.Параметры.ВсегоНаименованийРасход = "Всего наименований (расход): "+ВыборкаТабличнойЧасти.Количество();
	Иначе
		ОбластьПодвал.Параметры.СуммаВсегоРасход = Формат(СуммаВсегоРасход,ФорматВыводаСуммы);
		ОбластьПодвал.Параметры.ВсегоНаименованийРасход = "Всего наименований (расход): "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсегоРасход,ЭтотОбъект.ВалютаДокумента);
	КонецЕсли;
	ОбластьПодвал.Параметры.СуммаВсегоПриход = Формат(СуммаВсегоПриход,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ВсегоНаименованийПриход = "Всего наименований (приход): "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсегоПриход,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	МОЛ = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.МОЛ);
	МОЛ.МОЛПредставление = ?(обЗначениеНеЗаполнено(МОЛ.МОЛ),"","/ "+ МОЛ.МОЛПредставление);
	ОбластьПодвал.Параметры.Заполнить(МОЛ);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	ТабДокумент.АвтоМасштаб			= ИСТИНА;
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПересортицаТоваров()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Вывод итогов по странице печатной формы документа
//
// Параметры:
// ТабДокумент - табличный документ, печатная форма документа
// ОбластьПодвал - область табличного документа, область куда выводятся итоги
// СтруктураИтоговПоСтранице - структура, сравнит данные для вывода
// Права   - структура, права текущего пользователя или объект документа
//
Процедура ВывестиИтогиПоСтраницеИНВ19(ТабДокумент,ОбластьПодвал,СтруктураИтоговПоСтранице,Права) 
	Если ОбластьПодвал <> Неопределено Тогда
		Если СтруктураИтоговПоСтранице <> Неопределено Тогда
			Попытка
				ПраваПользователя=Права.Права;
				ОбъектПрава=Права;
			Исключение
				ПраваПользователя=Права;
				ОбъектПрава=Неопределено;
			КонецПопытки; 
			ФорматВыводаКоличества = обПраво("ФорматВыводаКоличества",ПраваПользователя,,ОбъектПрава);
			ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",ПраваПользователя,,ОбъектПрава);
			Для Каждого ЭлементИтога Из СтруктураИтоговПоСтранице Цикл
				Если Найти(ЭлементИтога.Ключ,"Сум") <> 0 Тогда
					ЗначениеПараметра = Формат(ЭлементИтога.Значение,ФорматВыводаСуммы);
				Иначе
					ЗначениеПараметра = Формат(ЭлементИтога.Значение,ФорматВыводаКоличества);
				КонецЕсли; 
				Попытка
					ОбластьПодвал.Параметры[ЭлементИтога.Ключ] = ЗначениеПараметра;
				Исключение
				КонецПопытки; 
			КонецЦикла;
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьПодвал);
	КонецЕсли;
КонецПроцедуры // дкДействияФормыСоздатьНапоминание()

// Функция проверяет попадание текущей области и области подвала на страницу
//
// Параметры:
// ТабДокумент 			 - табличный документ, в который выводится информация, подготовленная к печати
// Область 				 - текущая или массив текущих областей, предназначенных для вывода в данный момент
// ОбластьШапка 			 - область или массив областей, выступающих в качестве шапки и
// которые будут выводиться на каждой странице
// ОбластьПодвал 			 - область, которая будет выводиться в качестве подвала на каждой странице
// НомерСтраницы 			 - номер печатаемой страницы
// ТекстЗаголовка 			 - текст, выводимый как подпись к шапке очередного листа
// СтруктураИтоговПоСтранице - структура содержащая, накопленные итоги по текущей странице
//					 	 	 значение ключа структуры должно совпадать с идентификатором реквизита табличной части документа
// мсвДопОбластиПодвала  - подвальная часть макета. Проверка попадания последней строки таблицы на страницу
//							общего подвала Если = Неопределено, то проверка не осуществляется
// ВсегоСтраниц 			 - количество страниц выводимой
//
//Возвращаемое значение:
// Число - текущий номер страницы
//
Функция ВывестиГоризонтальнуюОбластьИНВ19(ТабДокумент, Область, ОбластьШапка = Неопределено, ОбластьПодвал = Неопределено, НомерСтраницы = 1,
	СтруктураИтоговПоСтранице = Неопределено, Права = Неопределено, мсвДопОбластиПодвала = Неопределено) 
	
	Попытка
		ПраваПользователя=Права.Права;
		ОбъектПрава=Права;
	Исключение
		ПраваПользователя=Права;
		ОбъектПрава=Неопределено;
	КонецПопытки; 
	
	ИспользоватьРазбиениеНаСтраницы = обПраво("ИспользоватьРазбиениеНаСтраницы",ПраваПользователя,,ОбъектПрава);
	
	//если не разбиваем, выводим и все
	Если Не ИспользоватьРазбиениеНаСтраницы Тогда
		ТабДокумент.Вывести(Область);
		Возврат НомерСтраницы;
	КонецЕсли;
	
	//запишем их в один массив
	МассивОбластейДляПроверки = Новый Массив;
	МассивОбластейДляПроверки.Добавить(Область);

	Если ОбластьПодвал <> Неопределено Тогда
		МассивОбластейДляПроверки.Добавить(ОбластьПодвал);
	КонецЕсли;
	Если мсвДопОбластиПодвала <> Неопределено Тогда
		Для Каждого ТекОбласть Из мсвДопОбластиПодвала Цикл
			МассивОбластейДляПроверки.Добавить(ТекОбласть);
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		//проверим, помещаются ли на странице
		РезультатПроверки = ТабДокумент.ПроверитьВывод(МассивОбластейДляПроверки);
		
		//если не помещаются, то ...
		Если НЕ РезультатПроверки Тогда
			ВывестиИтогиПоСтраницеИНВ19(ТабДокумент,ОбластьПодвал,СтруктураИтоговПоСтранице,Права); //выводим итог по странице
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц(); //переходим на следующую страницу
			НомерСтраницы = НомерСтраницы + 1;
			Если ОбластьШапка <> Неопределено Тогда
				ОбластьШапка.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;				
				ТабДокумент.Вывести(ОбластьШапка); //выводим шапку таблицы
			КонецЕсли;
		КонецЕсли;
	Исключение 		
	КонецПопытки;
	
	//выводим текущую область
	ТабДокумент.Вывести(Область);
	
	Возврат НомерСтраницы;
КонецФункции // дкВывестиГоризонтальнуюОбласть()

// Формирует печатную форму "ИНВ19"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьИНВ19(ТабДокумент) Экспорт
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьОбщийМакет("ИНВ19");
	
	ФорматВыводаСуммы = "ЧДЦ=2";
	ФорматВыводаКоличества = "";
	
	// Выводим шапку накладной
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	СтруктураПредставления=Новый Структура(
		"Наименование,ИНН,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
		"","ИНН ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	
	// Правим, что автоматом не заполнилось, или заполнилось неправильно
	ОбластьМакета.Параметры.ОрганизацияПредставление	= спПолучитьПредставление(ОбластьМакета.Параметры.Организация,СтруктураПредставления);
	ОбластьМакета.Параметры.ОрганизацияПоОКПО			= ОбластьМакета.Параметры.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.ВидДеятельностиПоОКДП		= ОбластьМакета.Параметры.Организация.КодПоОКДП;
	ОбластьМакета.Параметры.ДатаНачалаИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаНачалаИнвентаризации",ЭтотОбъект.Дата);
	ОбластьМакета.Параметры.ДатаОкончанияИнвентаризации	= обПолучитьЗначениеСвойства(ЭтотОбъект,"ДатаОкончанияИнвентаризации",ЭтотОбъект.Дата);
	
	ОбластьМакета.Параметры.МОЛ				 = обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьМакета.Параметры.МОЛДолжность	 = ОбластьМакета.Параметры.МОЛ.Должность;
	ОбластьМакета.Параметры.МОЛПредставление = спПолучитьНаименование(ОбластьМакета.Параметры.МОЛ);
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	СтрокНаСтранице = 27;
	СтрокШапки      = 5;
	СтрокПодвала    = 5;
	НомерСтраницы   = 2;
	Ном             = 1;

	ИтогоРезультатИзлишекКолво  	   = 0;
	ИтогоРезультатИзлишекСумма  	   = 0;
	ИтогоРезультатНедостачаКолво	   = 0;
	ИтогоРезультатНедостачаСумма	   = 0;
	ИтогоПересортНедостачиКолво  	   = 0;
	ИтогоПересортНедостачиСумма 	   = 0;
	ИтогоПересортИзлишкиКолво  		   = 0;
	ИтогоПересортИзлишкиСумма  		   = 0;
	ИтогоПриходИзлишковКолво           = 0;
	ИтогоПриходИзлишковСумма           = 0;
	ИтогоСписаниеНедостачКолонка1Колво = 0;
	ИтогоСписаниеНедостачКолонка1Сумма = 0;
	
	// Выводим заголовок таблицы
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
	ЗаголовокТаблицы.Параметры.Валюта = ЭтотОбъект.ВалютаДокумента; 
	ТабДокумент.Вывести(ЗаголовокТаблицы);

 	ВыборкаСтрокТовары = ЭтотОбъект.Товары;
	
	// Определим, что показывать в колонке кода - код или артикул
	ИмяКодаСправочника = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,Права).Имя;
	
	// Выводим многострочную часть документа
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	// структура итогов по странице
	СтруктураИтогов = Новый Структура("ИтогоРезультатИзлишекКолво,ИтогоРезультатИзлишекСумма,ИтогоРезультатНедостачаКолво,ИтогоРезультатНедостачаСумма,"+
	"ИтогоПересортНедостачиКолво,ИтогоПересортНедостачиСумма,ИтогоПересортИзлишкиКолво,ИтогоПересортИзлишкиСумма,"+
	"ИтогоПриходИзлишковКолво,ИтогоПриходИзлишковСумма,ИтогоСписаниеНедостачКолонка1Колво,ИтогоСписаниеНедостачКолонка1Сумма",0,0,0,0,0,0,0,0,0,0,0,0);
	
 	ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоТаблицыИтогиПоСтранице");
	ОбластьИтоговТаблицы	= Макет.ПолучитьОбласть("ИтогоТаблицы");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	мсвДопОбластиПодвала = Новый Массив;
	мсвДопОбластиПодвала.Добавить(ОбластьИтоговТаблицы);
	мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
	
	Для каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
	
		НедостачаКолво  = 0;
		НедостачаСумма  = 0;
		ИзлишекКолво 	= 0;
		ИзлишекСумма	= 0;
		ПересортКолво   = 0;
		ПересортСумма   = 0;
		
		ТребуетсяПересчетИзУпр = (ЭтотОбъект.ВалютаДокумента = Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		ТребуетсяПересчетИзРегл = (ЭтотОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
		
		НедостачаКолво  = СтрокаТовар.КоличествоРасход;
		ИзлишекКолво 	= СтрокаТовар.Количество;
		
		Если СкладКомпании.Розничный Тогда
			НедостачаСумма  = СтрокаТовар.СуммаРозничнаяРасход;
			ИзлишекСумма	= СтрокаТовар.СуммаРозничная;
		Иначе
			НедостачаСумма  = дкПолучитьСуммуСписания(ЭтотОбъект, "ПартииТоваровКомпании", Неопределено, СтрокаТовар.НоменклатураРасход,
				СтрокаТовар.ХарактеристикаНоменклатурыРасход, СтрокаТовар.НомерСтроки);
			ИзлишекСумма	= СтрокаТовар.Сумма;
			Если ТребуетсяПересчетИзРегл Тогда
				НедостачаСумма = обПересчет(НедостачаСумма, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
				ИзлишекСумма = обПересчет(ИзлишекСумма, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
			ИначеЕсли ТребуетсяПересчетИзУпр Тогда
				НедостачаСумма = обПересчет(НедостачаСумма, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
				ИзлишекСумма = обПересчет(ИзлишекСумма, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), ЭтотОбъект.Дата, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента);
			КонецЕсли;
		КонецЕсли;
		
		Если НедостачаКолво = 0 И ИзлишекКолво = 0 Тогда
			Продолжить;
		КонецЕсли;

		ПересортКолво = Мин(ИзлишекКолво,НедостачаКолво);
		ПересортСумма = Мин(ИзлишекСумма,НедостачаСумма); 
		
		//формируем строку недостачи
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		Характеристика										 = Строка(СтрокаТовар.ХарактеристикаНоменклатурыРасход);
		ОбластьМакета.Параметры.ТоварНаименование			 = спПолучитьНаименование(СтрокаТовар.НоменклатураРасход) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					 = дкПолучитьЗначениеКолонкиКода(СтрокаТовар.НоменклатураРасход,ИмяКодаСправочника,Права);
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТовар.НоменклатураРасход.БазоваяЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ    = СтрокаТовар.НоменклатураРасход.БазоваяЕдиницаИзмерения.Код;
		
		ОбластьМакета.Параметры.РезультатИзлишекКолво         = 0;
		ОбластьМакета.Параметры.РезультатИзлишекСумма  		  = 0;
		ОбластьМакета.Параметры.РезультатНедостачаКолво		  = Формат(НедостачаКолво,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.РезультатНедостачаСумма		  = Формат(НедостачаСумма,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ПересортИзлишкиКолво   		  = 0;
		ОбластьМакета.Параметры.ПересортИзлишкиСумма   		  = 0;
		ОбластьМакета.Параметры.ПересортНедостачиКолво 		  = Формат(ПересортКолво,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.ПересортНедостачиСумма 		  = Формат(ПересортСумма,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ПриходИзлишковКолво           = 0;
		ОбластьМакета.Параметры.ПриходИзлишковСумма   		  = 0;
		ОбластьМакета.Параметры.СписаниеНедостачКолонка1Колво = Формат((НедостачаКолво - ПересортКолво),ФорматВыводаКоличества);
		ОбластьМакета.Параметры.СписаниеНедостачКолонка1Сумма = Формат((НедостачаСумма - ПересортСумма),ФорматВыводаСуммы);
		
		ИтогоРезультатИзлишекКолво   	   = ИтогоРезультатИзлишекКолво   + 0;
		ИтогоРезультатИзлишекСумма   	   = ИтогоРезультатИзлишекСумма   + 0;
		ИтогоРезультатНедостачаКолво 	   = ИтогоРезультатНедостачаКолво + НедостачаКолво;
		ИтогоРезультатНедостачаСумма 	   = ИтогоРезультатНедостачаСумма + НедостачаСумма;
		ИтогоПересортИзлишкиКолво		   = ИтогоПересортИзлишкиКолво   + 0;
		ИтогоПересортИзлишкиСумма  		   = ИтогоПересортИзлишкиСумма   + 0;
		ИтогоПересортНедостачиКолво 	   = ИтогоПересортНедостачиКолво + ПересортКолво;
		ИтогоПересортНедостачиСумма 	   = ИтогоПересортНедостачиСумма + ПересортСумма;
		ИтогоПриходИзлишковКолво           = ИтогоПриходИзлишковКолво + 0;
		ИтогоПриходИзлишковСумма           = ИтогоПриходИзлишковСумма + 0;
		ИтогоСписаниеНедостачКолонка1Колво = ИтогоСписаниеНедостачКолонка1Колво + (НедостачаКолво - ПересортКолво);
		ИтогоСписаниеНедостачКолонка1Сумма = ИтогоСписаниеНедостачКолонка1Сумма + (НедостачаСумма - ПересортСумма);
		
		ОбластьМакета.Параметры.ПересортИзлишкиНомер 	= 0; 
		ОбластьМакета.Параметры.ПересортНедостачиНомер  = Ном+1; 
		ОбластьМакета.Параметры.Номер 					= Ном;
		Ном = Ном + 1;			
		
		СтраницаДоВывода = НомерСтраницы;
		НомерСтраницы = ВывестиГоризонтальнуюОбластьИНВ19(ТабДокумент, ОбластьМакета, ЗаголовокТаблицы , ОбластьИтоговПоСтранице , НомерСтраницы, СтруктураИтогов, ЭтотОбъект, мсвДопОбластиПодвала);
		Если СтраницаДоВывода <> НомерСтраницы Тогда 
			СтруктураИтогов = Новый Структура("ИтогоРезультатИзлишекКолво,ИтогоРезультатИзлишекСумма,ИтогоРезультатНедостачаКолво,ИтогоРезультатНедостачаСумма,"+
			"ИтогоПересортНедостачиКолво,ИтогоПересортНедостачиСумма,ИтогоПересортИзлишкиКолво,ИтогоПересортИзлишкиСумма,"+
			"ИтогоПриходИзлишковКолво,ИтогоПриходИзлишковСумма,ИтогоСписаниеНедостачКолонка1Колво,ИтогоСписаниеНедостачКолонка1Сумма",0,0,0,0,0,0,0,0,0,0,0,0);
		КонецЕсли;
		
		СтруктураИтогов.ИтогоРезультатИзлишекКолво   		  = СтруктураИтогов.ИтогоРезультатИзлишекКолво + 0;
		СтруктураИтогов.ИтогоРезультатИзлишекСумма  		  = СтруктураИтогов.ИтогоРезультатИзлишекСумма + 0;
		СтруктураИтогов.ИтогоРезультатНедостачаКолво 		  = СтруктураИтогов.ИтогоРезультатНедостачаКолво + НедостачаКолво;
		СтруктураИтогов.ИтогоРезультатНедостачаСумма 		  = СтруктураИтогов.ИтогоРезультатНедостачаСумма + НедостачаСумма;
		СтруктураИтогов.ИтогоПересортИзлишкиКолво  			  = СтруктураИтогов.ИтогоПересортИзлишкиКолво + 0;
		СтруктураИтогов.ИтогоПересортИзлишкиСумма 			  = СтруктураИтогов.ИтогоПересортИзлишкиСумма + 0;
		СтруктураИтогов.ИтогоПересортНедостачиКолво   		  = СтруктураИтогов.ИтогоПересортНедостачиКолво + ПересортКолво;
		СтруктураИтогов.ИтогоПересортНедостачиСумма   	   	  = СтруктураИтогов.ИтогоПересортНедостачиСумма + ПересортСумма;
		СтруктураИтогов.ИтогоПриходИзлишковКолво           	  = СтруктураИтогов.ИтогоПриходИзлишковКолво + 0;
		СтруктураИтогов.ИтогоПриходИзлишковСумма           	  = СтруктураИтогов.ИтогоПриходИзлишковСумма + 0;
		СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво 	  = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво + (НедостачаКолво - ПересортКолво);
		СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма    = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма + (НедостачаСумма - ПересортСумма);
		
		//формируем строку излишка
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТовар);
		ОбластьМакета.Параметры.ТоварНаименование			 = СтрокаТовар.Номенклатура;
		Характеристика										 = Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
		ОбластьМакета.Параметры.ТоварНаименование			 = спПолучитьНаименование(СтрокаТовар.Номенклатура) + ?(Характеристика = "", "",", " + Характеристика);
		ОбластьМакета.Параметры.ТоварКод					 = дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура,ИмяКодаСправочника,Права);
		ОбластьМакета.Параметры.ЕдиницаИзмеренияНаименование = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения;
		ОбластьМакета.Параметры.ЕдиницаИзмеренияКодПоОКЕИ    = СтрокаТовар.Номенклатура.БазоваяЕдиницаИзмерения.Код;
		
		ОбластьМакета.Параметры.РезультатИзлишекКолво   	  = Формат(ИзлишекКолво,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.РезультатИзлишекСумма   	  = Формат(ИзлишекСумма,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.РезультатНедостачаКолво 	  = 0;
		ОбластьМакета.Параметры.РезультатНедостачаСумма 	  = 0;
		ОбластьМакета.Параметры.ПересортИзлишкиКолво   		  = Формат(ПересортКолво,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.ПересортИзлишкиСумма   		  = Формат(ПересортСумма,ФорматВыводаСуммы);		
		ОбластьМакета.Параметры.ПересортНедостачиКолво 		  = 0;
		ОбластьМакета.Параметры.ПересортНедостачиСумма 		  = 0;
		ОбластьМакета.Параметры.ПриходИзлишковКолво           = Формат(ИзлишекКолво - ПересортКолво,ФорматВыводаКоличества);
		ОбластьМакета.Параметры.ПриходИзлишковСумма   		  = Формат(ИзлишекСумма - ПересортСумма,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СписаниеНедостачКолонка1Колво = 0;
		ОбластьМакета.Параметры.СписаниеНедостачКолонка1Сумма = 0;
		
		ИтогоРезультатИзлишекКолво   	   = ИтогоРезультатИзлишекКолво  + ИзлишекКолво;
		ИтогоРезультатИзлишекСумма  	   = ИтогоРезультатИзлишекСумма  + ИзлишекСумма;
		ИтогоРезультатНедостачаКолво 	   = ИтогоРезультатНедостачаКолво + 0;
		ИтогоРезультатНедостачаСумма	   = ИтогоРезультатНедостачаСумма + 0;
		ИтогоПересортИзлишкиКолво  		   = ИтогоПересортИзлишкиКолво   + ПересортКолво;
		ИтогоПересортИзлишкиСумма  		   = ИтогоПересортИзлишкиСумма   + ПересортСумма;
		ИтогоПересортНедостачиКолво 	   = ИтогоПересортНедостачиКолво + 0;
		ИтогоПересортНедостачиСумма 	   = ИтогоПересортНедостачиСумма + 0;
		ИтогоПриходИзлишковКолво           = ИтогоПриходИзлишковКолво + (ИзлишекКолво - ПересортКолво);
		ИтогоПриходИзлишковСумма           = ИтогоПриходИзлишковСумма + (ИзлишекСумма - ПересортСумма);
		ИтогоСписаниеНедостачКолонка1Колво = ИтогоСписаниеНедостачКолонка1Колво + 0;
		ИтогоСписаниеНедостачКолонка1Сумма = ИтогоСписаниеНедостачКолонка1Сумма + 0;
		
		ОбластьМакета.Параметры.ПересортИзлишкиНомер 	= Ном-1; 
		ОбластьМакета.Параметры.ПересортНедостачиНомер  = 0; 
		ОбластьМакета.Параметры.Номер 					= Ном;
		Ном = Ном + 1;			
		
		СтраницаДоВывода = НомерСтраницы;
		НомерСтраницы = ВывестиГоризонтальнуюОбластьИНВ19(ТабДокумент, ОбластьМакета, ЗаголовокТаблицы , ОбластьИтоговПоСтранице , НомерСтраницы, СтруктураИтогов, ЭтотОбъект, мсвДопОбластиПодвала);
		Если СтраницаДоВывода <> НомерСтраницы Тогда 
			СтруктураИтогов = Новый Структура("ИтогоРезультатИзлишекКолво,ИтогоРезультатИзлишекСумма,ИтогоРезультатНедостачаКолво,ИтогоРезультатНедостачаСумма,"+
			"ИтогоПересортНедостачиКолво,ИтогоПересортНедостачиСумма,ИтогоПересортИзлишкиКолво,ИтогоПересортИзлишкиСумма,"+
			"ИтогоПриходИзлишковКолво,ИтогоПриходИзлишковСумма,ИтогоСписаниеНедостачКолонка1Колво,ИтогоСписаниеНедостачКолонка1Сумма",0,0,0,0,0,0,0,0,0,0,0,0);
		КонецЕсли;

		СтруктураИтогов.ИтогоРезультатИзлишекКолво   		  = СтруктураИтогов.ИтогоРезультатИзлишекКолво + ИзлишекКолво;
		СтруктураИтогов.ИтогоРезультатИзлишекСумма  		  = СтруктураИтогов.ИтогоРезультатИзлишекСумма + ИзлишекСумма;
		СтруктураИтогов.ИтогоРезультатНедостачаКолво 		  = СтруктураИтогов.ИтогоРезультатНедостачаКолво + 0;
		СтруктураИтогов.ИтогоРезультатНедостачаСумма 	 	  = СтруктураИтогов.ИтогоРезультатНедостачаСумма + 0;
		СтруктураИтогов.ИтогоПересортИзлишкиКолво  			  = СтруктураИтогов.ИтогоПересортИзлишкиКолво + ПересортКолво;
		СтруктураИтогов.ИтогоПересортИзлишкиСумма  		      = СтруктураИтогов.ИтогоПересортИзлишкиСумма + ПересортСумма;
		СтруктураИтогов.ИтогоПересортНедостачиКолво   	      = СтруктураИтогов.ИтогоПересортНедостачиКолво + 0;
		СтруктураИтогов.ИтогоПересортНедостачиСумма   	      = СтруктураИтогов.ИтогоПересортНедостачиСумма + 0;
		СтруктураИтогов.ИтогоПриходИзлишковКолво           	  = СтруктураИтогов.ИтогоПриходИзлишковКолво + (ИзлишекКолво - ПересортКолво);
		СтруктураИтогов.ИтогоПриходИзлишковСумма           	  = СтруктураИтогов.ИтогоПриходИзлишковСумма + (ИзлишекСумма - ПересортСумма);
		СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво 	  = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Колво + 0;
		СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма    = СтруктураИтогов.ИтогоСписаниеНедостачКолонка1Сумма + 0;
		
	КонецЦикла;

	// выведем область итогов по странице
	Если НомерСтраницы > 2 Тогда
		ВывестиИтогиПоСтраницеИНВ19(ТабДокумент,ОбластьИтоговПоСтранице,СтруктураИтогов,Права);
		//заменим область итогов таблицы, в ней отличается оформление
		ОбластьИтоговТаблицы	= Макет.ПолучитьОбласть("ИтогоТаблицы2");
	КонецЕсли;
	
	ФорматВыводаКоличества = "ЧЦ=15; ЧДЦ=3; ЧН=";
	ФорматВыводаСуммы = "ЧЦ=15; ЧДЦ=2; ЧН=";
	
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатИзлишекКолво   	 = Формат(ИтогоРезультатИзлишекКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатИзлишекСумма   	 = Формат(ИтогоРезультатИзлишекСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатНедостачаКолво 	 = Формат(ИтогоРезультатНедостачаКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоРезультатНедостачаСумма 	 = Формат(ИтогоРезультатНедостачаСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоПересортИзлишкиКолво 		 = Формат(ИтогоПересортИзлишкиКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоПересортИзлишкиСумма 		 = Формат(ИтогоПересортИзлишкиСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоПересортНедостачиКолво   	 = Формат(ИтогоПересортНедостачиКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоПересортНедостачиСумма   	 = Формат(ИтогоПересортНедостачиСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоПриходИзлишковКолво 		 = Формат(ИтогоПриходИзлишковКолво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоПриходИзлишковСумма 		 = Формат(ИтогоПриходИзлишковСумма,ФорматВыводаСуммы);
	ОбластьИтоговТаблицы.Параметры.ИтогоСписаниеНедостачКолонка1Колво= Формат(ИтогоСписаниеНедостачКолонка1Колво,ФорматВыводаКоличества);
	ОбластьИтоговТаблицы.Параметры.ИтогоСписаниеНедостачКолонка1Сумма= Формат(ИтогоСписаниеНедостачКолонка1Сумма,ФорматВыводаСуммы);
	
	ТабДокумент.Вывести(ОбластьИтоговТаблицы);

	ОбластьПодвал.Параметры.Заполнить(ЭтотОбъект);
	
	// Заполним информацию о руководителях и ответственных
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер);
	ОбластьПодвал.Параметры.Заполнить(ГлавныйБухгалтер);
	
	ОбластьПодвал.Параметры.МОЛ				= обПолучитьЗначениеСвойства(ЭтотОбъект,"МОЛ",СкладКомпании.МОЛ);
	ОбластьПодвал.Параметры.МОЛДолжность	= ОбластьПодвал.Параметры.МОЛ.Должность;
	ОбластьПодвал.Параметры.МОЛПредставление= спПолучитьНаименование(ОбластьПодвал.Параметры.МОЛ);
	
	ТабДокумент.Вывести(ОбластьПодвал);

	Возврат ТабДокумент;
КонецФункции // ПечатьИНВ19()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Заказ ИЗ РегистрНакопления.ЗаказыПокупателей ГДЕ Регистратор = &Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	// сначала спишем то что надо списать
	НаборЗаписейОстатки = Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения           = Режим;
	НаборЗаписейОстатки.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам = ПолучитьРезультатЗапросаОстатки("Расход");
	НаборЗаписейОстатки.СкладКомпании             = СкладКомпании;
	НаборЗаписейОстатки.Пересортица               = Истина;
	НаборЗаписейОстатки.ЕстьРозничнаяСумма        = Ложь;
	НаборЗаписейОстатки.Резервировать             = Истина;
	НаборЗаписейОстатки.ДвиженияПоРознице         = СкладКомпании.Розничный;
	
	Отказ=НЕ НаборЗаписейОстатки.Расход() ИЛИ Отказ;
	Если Отказ Тогда
		// выведем сообщения об ошибках и предупреждениях
		дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
		Возврат; // дальше смысла не имеет
	КонецЕсли;
	
	// Снимаем резервы по заказам (если таковые были)
	НаборЗаписейЗаказыПокупателей = Движения.ЗаказыПокупателей;
	НаборЗаписейЗаказыПокупателей.РежимПроведения = Режим;
	НаборЗаписейЗаказыПокупателей.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейЗаказыПокупателей.РезультатЗапросаПоТоварам = НаборЗаписейОстатки.Выгрузить();
	НаборЗаписейЗаказыПокупателей.СкладКомпании = СкладКомпании;
	Отказ=НЕ НаборЗаписейЗаказыПокупателей.СнятиеРезервовЗаказовПокупателей() ИЛИ Отказ;
	
	// оприходуем что надо оприходовать
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.РежимПроведения=Режим;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=ПолучитьРезультатЗапросаОстатки("Приход");
	НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
	НаборЗаписейОстатки.Приходовать=Истина;
	НаборЗаписейОстатки.Резервировать=Ложь;
	НаборЗаписейОстатки.ЕстьРозничнаяСумма=Ложь;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
	Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	
	// партии
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	// если приходуем товар на розничный склад, то установим розничные цены на этот товар
	Если НЕ Отказ И СкладКомпании.Розничный И ПодразделениеКомпании.УстановкаЦенДокументамиПоступления И НЕ СкладКомпании.ТипЦенРозничнойТорговли.Рассчитывается Тогда
		// устанавливаем цены.
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаНоменклатура="Номенклатура";
		НаборЗаписейЦены.ТипЦен=СкладКомпании.ТипЦенРозничнойТорговли;
		НаборЗаписейЦены.ИмяРеквизитаЦена="ЦенаРозничная";
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// установим закупочные цены компании
	Если НЕ Отказ И ПодразделениеКомпании.ФормироватьЗакупочнуюЦену И НЕ Справочники.ТипыЦен.ОсновнойТипЦенЗакупки.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаНоменклатура="Номенклатура";
		НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
		НаборЗаписейЦены.ТипЦен=Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	//установим нормативные цены компании
	Если НЕ Отказ И ПодразделениеКомпании.ФормироватьНормативнуюЦену И НЕ Справочники.ТипыЦен.НормативнаяЦена.Рассчитывается Тогда
		НаборЗаписейЦены=Движения.Цены;
		НаборЗаписейЦены.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейЦены.Контрагент=Неопределено;
		НаборЗаписейЦены.ИмяРеквизитаНоменклатура="Номенклатура";
		НаборЗаписейЦены.ИмяРеквизитаЦена="Цена";
		НаборЗаписейЦены.ТипЦен=Справочники.ТипыЦен.НормативнаяЦена;
		Отказ = НЕ НаборЗаписейЦены.УстановитьЦены() ИЛИ Отказ;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности заказов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ЗаказыПокупателей.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаЗаказов = Движения.ЗаказыПокупателей.Выгрузить();
			ТаблицаЗаказов.Свернуть("Заказ","");
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("Заказ");
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
#Область ИнформацияОДокументе

//************************//
// ИНФОРМАЦИЯ О ДОКУМЕНТЕ //
//************************//


// Возвращает доступные варианты печати документа
// Параметры:
//	ДокументСсылка - Ссылка на документ печати
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати. Первый элемент структуры - форма по умолчанию
Функция ПолучитьСписокПечатныхФорм(ДокументСсылка) Экспорт
	
	СтруктураМакетов = Новый Структура();
	
	СтруктураМакетов.Вставить("СчетФактура" , "Счет-фактура");
	СтруктураМакетов.Вставить("УПД"         , "Универсальный передаточный документ");
	
	Если ДокументСсылка.ХозОперация = Справочники.ХозОперации.СчетФактураВыданныйКорректировка Тогда
		
		СтруктураМакетов.Вставить("КорректировочныйСчетФактура", "Корректировочный счет-фактура");
		СтруктураМакетов.Вставить("УКД", "Универсальный корректировочный документ");
	КонецЕсли;
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСписокПечатныхФорм()

// получение строк с указаным ГТД
Функция ПолучитьСтрокиСГТД(ТаблицаТоваров) Экспорт
	МассивСтрок = Новый Массив;
	Для Каждого стрТовар Из ТаблицаТоваров Цикл
		Если НЕ обЗначениеНеЗаполнено(стрТовар.ГТД) Тогда
			МассивСтрок.Добавить(стрТовар);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивСтрок;
КонецФункции

// получение строк с указаной партией
Функция ПолучитьСтрокиСПартиями(ТаблицаТоваров) Экспорт
	МассивСтрок = Новый Массив;
	Для Каждого стрТовар Из ТаблицаТоваров Цикл
		Если НЕ обЗначениеНеЗаполнено(стрТовар.Партия) Тогда
			МассивСтрок.Добавить(стрТовар);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивСтрок;
КонецФункции

Функция ПолучитьТоварыСчетФактуры(Основание)
	
	ДанныеДокумента = Новый Структура("Товары,ТоварыСГТД");
	
	Запрос = Новый Запрос();
	Запрос.Текст =  "ВЫБРАТЬ
	                |	СчетФактураВыданный.Ссылка КАК Ссылка
	                |ИЗ
	                |	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	                |ГДЕ
	                |	СчетФактураВыданный.ДокументОснование = &ДокументОснование";
	 
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДанныеДокумента.Вставить("Товары", Выборка.Ссылка.Товары.Выгрузить());
		ДанныеДокумента.Вставить("ТоварыСГТД", Выборка.Ссылка.ПолучитьОбъект().ТаблицаДвиженийПоГТД(Основание));
		
		Возврат ДанныеДокумента;
		
	КонецЕсли;
	
	СФОснования = Документы.СчетФактураВыданный.СоздатьДокумент();
	СФОснования.ДополнительныеСвойства.Вставить("НеИскатьСчетФактуру");
	СФОснования.Заполнить(Основание);
	ДанныеДокумента.Вставить("Товары", СФОснования.Товары.Выгрузить());
	ДанныеДокумента.Вставить("ТоварыСГТД", СФОснования.ТаблицаДвиженийПоГТД(Основание));
	
	Возврат ДанныеДокумента;
	
КонецФункции

Функция ЗаполнитьНомераСтрокВТаблице(ТаблицаИзменений, Ссылка)
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.КорректировкаРеализацииАвтомобилей") Тогда
		СтруктураПоиска = Новый Структура("Номенклатура,ГТД");
	Иначе
		СтруктураПоиска = Новый Структура("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры,ГТД");
	КонецЕсли;
	 	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыданныйТовары.Номенклатура КАК Номенклатура,
		|	СчетФактураВыданныйТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СчетФактураВыданныйТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СчетФактураВыданныйТовары.ГТД КАК ГТД,
		|	СчетФактураВыданныйТовары.НомерИсходнойСтроки КАК НомерИсходнойСтроки
		|ИЗ
		|	Документ.СчетФактураВыданный.Товары КАК СчетФактураВыданныйТовары
		|ГДЕ
		|	СчетФактураВыданныйТовары.Ссылка.ДокументОснование = &ССылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ДанныеСФ = Запрос.Выполнить().Выгрузить();
	
	Для каждого ТекущаяСтрока Из ТаблицаИзменений Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		
		НайденныеСтроки = ДанныеСФ.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ТекущаяСтрока.НомерИсходнойСтроки = НайденныеСтроки[0].НомерИсходнойСтроки; 
		
	КонецЦикла;
	
	Возврат ТаблицаИзменений;

	
КонецФункции

// Получение ТЧ в разрезе ГТД для печати
Функция ПолучитьДанныеДляЗаполненияПФКорректировки(ОбъектДокумента) Экспорт
	
	//Получим данные по документу основанию
	Если ТипЗнч(ОбъектДокумента) = Тип("ДокументОбъект.КорректировкаРеализации")
		ИЛИ ТипЗнч(ОбъектДокумента) = Тип("ДокументОбъект.КорректировкаРеализацииАвтомобилей") Тогда
		
		Основание = ОбъектДокумента.ДокументОснование;
		ДокументКорректировки = ОбъектДокумента.Ссылка;
		
		// Проверим наличие СФ
		ТоварыДокумента = ПолучитьТоварыСчетФактуры(ДокументКорректировки);
		ДанныеПослеИзменения = ТоварыДокумента.Товары;
		ДанныеПослеИзмененияСГТД = ТоварыДокумента.ТоварыСГТД;
		
	Иначе
		
		Основание = ОбъектДокумента.ДокументОснование.ДокументОснование;
		ДокументКорректировки = ОбъектДокумента.ДокументОснование;
		ДанныеПослеИзменения = ОбъектДокумента.Товары.Выгрузить();
		ДанныеПослеИзмененияСГТД = ОбъектДокумента.ТаблицаДвиженийПоГТД(ДокументКорректировки);
		
	КонецЕсли;
	
	ТоварыДокументаОснования = ПолучитьТоварыСчетФактуры(Основание);
	ДанныеДоИзменения = ТоварыДокументаОснования.Товары;
	ДанныеДоИзмененияСГТД = ТоварыДокументаОснования.ТоварыСГТД;
		
	ДанныеДоИзменения.Колонки.Добавить("ГТДКоличествоДо");
	
	// Тут проветка на право
	ДанныеДоИзменения.Колонки.Добавить("ГТДДоКорректировки");
	//

	СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД");
	
	Для каждого ТекущаяСтрока Из ДанныеДоИзменения Цикл
		
		Если ТекущаяСтрока.Партия <>  Неопределено Тогда 
			СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД,Партия");
		Иначе 
			СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		
		НайденныеСтроки = ДанныеДоИзмененияСГТД.НайтиСтроки(СтруктураПоиска);
		
		Если НЕ ТипЗнч(ТекущаяСтрока.Номенклатура) = Тип("СправочникСсылка.Автомобили") Тогда
			ТекущаяСтрока.ГТДКоличествоДо = 0;
		КонецЕсли;
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
						
		Количество = 0;
		Для Каждого Элемент Из НайденныеСтроки Цикл 
			Количество = Количество + Элемент.Количество;
			ДанныеДоИзмененияСГТД.Удалить(Элемент);

		КонецЦикла;
		ТекущаяСтрока.ГТДКоличествоДо = Количество;
	КонецЦикла;
	
	ДанныеПослеИзменения.Колонки.Добавить("ГТДКоличествоПосле");
	ДанныеПослеИзменения.Колонки.Добавить("ГТДДоКорректировки");
	
	Если ДанныеПослеИзмененияСГТД.Колонки.Найти("ГТДДоКорректировки") <> Неопределено Тогда
		ЕстьГТДДоКорректировки = Истина;
	Иначе
		ЕстьГТДДоКорректировки = Ложь;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД,Партия");
	Для Каждого ТекущаяСтрока Из ДанныеПослеИзменения Цикл
		
		Если ТекущаяСтрока.Партия <>  Неопределено Тогда 
			СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД,Партия");
		Иначе 
			СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ГТД");
		КонецЕсли;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		
		НайденныеСтроки = ДанныеПослеИзмененияСГТД.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Количество = 0;
		Для Каждого Элемент Из НайденныеСтроки Цикл 
			Количество = Количество + Элемент.Количество;
			ДанныеПослеИзмененияСГТД.Удалить(Элемент);
		КонецЦикла;
		ТекущаяСтрока.ГТДКоличествоПосле = Количество;
		
	КонецЦикла;
	
	МассивТиповНоменклатуры = Новый Массив;
	МассивТиповНоменклатуры.Добавить(Тип("СправочникСсылка.Номенклатура"));
	МассивТиповНоменклатуры.Добавить(Тип("СправочникСсылка.Автомобили"));
	МассивТиповНоменклатуры.Добавить(Тип("СправочникСсылка.Автоработы"));
	
	ТаблицаИзменений = Новый ТаблицаЗначений();
	ТаблицаИзменений.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("Номенклатура",
		Новый ОписаниеТипов(МассивТиповНоменклатуры));
	ТаблицаИзменений.Колонки.Добавить("ХарактеристикаНоменклатуры",
		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаИзменений.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("КоличествоПоДокументуРеализации", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("КоличествоРазница", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("ЕдиницаИзмерения",
		Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	ТаблицаИзменений.Колонки.Добавить("ЕдиницаИзмеренияПоДокументуРеализации", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	ТаблицаИзменений.Колонки.Добавить("Коэффициент", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("КоэффициентПоДокументуРеализации", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	ТаблицаИзменений.Колонки.Добавить("СтавкаНДСПоДокументуРеализации", Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	ТаблицаИзменений.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("СуммаНДСПоДокументуРеализации", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("СуммаНДСРазница", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("СуммаВсего", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("СуммаВсегоПоДокументуРеализации", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("СуммаВсегоРазница", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("ГТД", Новый ОписаниеТипов("СправочникСсылка.ГТД"));
	ТаблицаИзменений.Колонки.Добавить("ГТДДоКорректировки", Новый ОписаниеТипов("СправочникСсылка.ГТД"));
	ТаблицаИзменений.Колонки.Добавить("ГТДКоличествоДо", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("ГТДКоличествоПосле", Новый ОписаниеТипов("Число"));
	ТаблицаИзменений.Колонки.Добавить("НомерИсходнойСтроки", Новый ОписаниеТипов("Число"));

	ТаблицаИзменений.Колонки.Добавить("Товар", Новый ОписаниеТипов("Булево"));
	
	// Сформируем структуру поиска
	СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,ГТД");
	МассивУдалитьДо = Новый Массив;
	МассивУдалитьПосле = Новый Массив();
	Для Каждого ТекущаяСтрока Из ДанныеПослеИзменения Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		// тут плюсануть структуроу на проверку данных до  , 
		//проверить нужно ли добавлять в данные до изменения колонки
		// проверить нужна ли партия до начиная с получения тч по гтд 
		
		// Проверим были ли изменения
		НайденныеСтроки = ДанныеДоИзменения.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
		КонецЕсли;
		Если НайденныеСтроки[0].Количество <> ТекущаяСтрока.Количество ИЛИ 
			НайденныеСтроки[0].СуммаВсего <> ТекущаяСтрока.СуммаВсего  ИЛИ 
			НайденныеСтроки[0].ЕдиницаИзмерения <> ТекущаяСтрока.ЕдиницаИзмерения ИЛИ
			НайденныеСтроки[0].Коэффициент <> ТекущаяСтрока.Коэффициент ИЛИ
			НайденныеСтроки[0].СтавкаНДС <> ТекущаяСтрока.СтавкаНДС ИЛИ
			НайденныеСтроки[0].ГТДДоКорректировки <> ТекущаяСтрока.ГТД ИЛИ
			НайденныеСтроки[0].СуммаНДС <> ТекущаяСтрока.СуммаНДС Тогда 
			НоваяСтрока = ТаблицаИзменений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			НоваяСтрока.КоличествоПоДокументуРеализации = НайденныеСтроки[0].Количество;
			НоваяСтрока.ЕдиницаИзмеренияПоДокументуРеализации = НайденныеСтроки[0].ЕдиницаИзмерения;
			НоваяСтрока.КоэффициентПоДокументуРеализации = НайденныеСтроки[0].Коэффициент;
			НоваяСтрока.СуммаНДСПоДокументуРеализации = НайденныеСтроки[0].СуммаНДС;
			НоваяСтрока.СуммаВсегоПоДокументуРеализации = НайденныеСтроки[0].СуммаВсего;
			НоваяСтрока.КоличествоРазница = ТекущаяСтрока.Количество - НайденныеСтроки[0].Количество;
			НоваяСтрока.СуммаНДСРазница = ТекущаяСтрока.СуммаНДС- НайденныеСтроки[0].СуммаНДС;
			НоваяСтрока.СуммаВсегоРазница = ТекущаяСтрока.СуммаВсего - НайденныеСтроки[0].СуммаВсего;
			НоваяСтрока.ГТДКоличествоДо= НайденныеСтроки[0].ГТДКоличествоДо;
			НоваяСтрока.СтавкаНДСПоДокументуРеализации = НайденныеСтроки[0].СтавкаНДС; 
			НоваяСтрока.ГТДДоКорректировки = НайденныеСтроки[0].ГТД;
			
			НоваяСтрока.НомерИсходнойСтроки =  НайденныеСтроки[0].НомерИсходнойСтроки;
			
			Если ТипЗнч(ТекущаяСтрока.Номенклатура) = Тип("СправочникСсылка.Автоработы") Тогда
				
				НоваяСтрока.Товар = Ложь;
				
			Иначе
				
				НоваяСтрока.Товар = Истина;
				
			КонецЕсли;
		КонецЕсли;
		
		ДанныеДоИзменения.Удалить(НайденныеСтроки[0]);
		МассивУдалитьПосле.Добавить(ТекущаяСтрока);
		
	КонецЦикла;
	
	// Удалим ранее заполненные после изменения
	Для Каждого ТекущаяСтрока Из МассивУдалитьПосле Цикл
		
		ДанныеПослеИзменения.Удалить(ТекущаяСтрока);
		
	КонецЦикла;
		
	Для Каждого ТекущаяСтрока Из ДанныеПослеИзменения Цикл
		
		НоваяСтрока = ТаблицаИзменений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.КоличествоПоДокументуРеализации = 0;
		НоваяСтрока.КоличествоРазница = НоваяСтрока.Количество;
		НоваяСтрока.ЕдиницаИзмеренияПоДокументуРеализации = НоваяСтрока.ЕдиницаИзмерения;
		НоваяСтрока.КоэффициентПоДокументуРеализации = НоваяСтрока.Коэффициент;
		НоваяСтрока.СуммаНДСПоДокументуРеализации = 0;
		НоваяСтрока.СуммаНДСРазница = НоваяСтрока.СуммаНДС;
		НоваяСтрока.СуммаВсегоПоДокументуРеализации = 0;
		НоваяСтрока.СуммаВсегоРазница = НоваяСтрока.СуммаВсего;
		НоваяСтрока.ГТДКоличествоДо = 0;
		НоваяСтрока.НомерИсходнойСтроки = 0;
		НоваяСтрока.СтавкаНДСПоДокументуРеализации = НоваяСтрока.СтавкаНДС;
		
		Если ТипЗнч(ТекущаяСтрока.Номенклатура) = Тип("СправочникСсылка.Автоработы") Тогда
				НоваяСтрока.Товар = Ложь;
			Иначе
				НоваяСтрока.Товар = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из ДанныеДоИзменения Цикл
		
		НоваяСтрока = ТаблицаИзменений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока,, "Количество,Цена,Сумма,СуммаВсего,СуммаНДС,ГТД");
		НоваяСтрока.КоличествоПоДокументуРеализации = ТекущаяСтрока.Количество;
		НоваяСтрока.КоличествоРазница = - НоваяСтрока.КоличествоПоДокументуРеализации; 
		НоваяСтрока.ЕдиницаИзмерения = ТекущаяСтрока.ЕдиницаИзмерения;
		НоваяСтрока.ЕдиницаИзмеренияПоДокументуРеализации = ТекущаяСтрока.ЕдиницаИзмерения;
		НоваяСтрока.Коэффициент = ТекущаяСтрока.Коэффициент;
		НоваяСтрока.КоэффициентПоДокументуРеализации = ТекущаяСтрока.Коэффициент;
		НоваяСтрока.СуммаНДСПоДокументуРеализации = ТекущаяСтрока.СуммаНДС;
		НоваяСтрока.СуммаНДСРазница = - ТекущаяСтрока.СуммаНДС;
		НоваяСтрока.СуммаВсегоПоДокументуРеализации = ТекущаяСтрока.СуммаВсего;
		НоваяСтрока.СуммаВсегоРазница = - ТекущаяСтрока.СуммаВсего;
		НоваяСтрока.ГТДКоличествоПосле = 0;
		НоваяСтрока.НомерИсходнойСтроки = ТекущаяСтрока.НомерИсходнойСтроки;
		НоваяСтрока.СтавкаНДСПоДокументуРеализации = НоваяСтрока.СтавкаНДС;
		НоваяСтрока.ГТДДоКорректировки = ТекущаяСтрока.ГТД;
		НоваяСтрока.ГТД = ТекущаяСтрока.ГТД;
		
		Если ТипЗнч(ТекущаяСтрока.Номенклатура) = Тип("СправочникСсылка.Автоработы") Тогда
				НоваяСтрока.Товар = Ложь;
			Иначе
				НоваяСтрока.Товар = Истина;
		КонецЕсли;

		НоваяСтрока.НомерСтроки = 10000000;
		
	КонецЦикла;
	
	ТаблицаИзменений.Сортировать("НомерИсходнойСтроки");
	
	Возврат ТаблицаИзменений;

КонецФункции

Функция ПолучитьНаименованиеДокументаОтгрузки(ЭтоУПД, ВыборкаТабличнойЧасти) Экспорт 
	
	Если ЭтоУПД Тогда
		
		НаименованиеДокумента = НСтр("ru = 'Универсальный передаточный документ'");
		
	Иначе 

		НаименованиеДокумента = НСтр("ru = 'Товарная накладная'");
		
		Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда 

			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
			                      |	ВыборкаТабличнойЧасти.Номенклатура КАК Номенклатура
			                      |ПОМЕСТИТЬ ВТВыборкаТабличнойЧасти
			                      |ИЗ
			                      |	&ВыборкаТабличнойЧасти КАК ВыборкаТабличнойЧасти
			                      |;
			                      |
			                      |////////////////////////////////////////////////////////////////////////////////
			                      |ВЫБРАТЬ ПЕРВЫЕ 1
			                      |	ВТВыборкаТабличнойЧасти.Номенклатура КАК Номенклатура
			                      |ИЗ
			                      |	ВТВыборкаТабличнойЧасти КАК ВТВыборкаТабличнойЧасти
			                      |ГДЕ
								  |	ВЫБОР
								  |		КОГДА ТИПЗНАЧЕНИЯ(ВТВыборкаТабличнойЧасти.Номенклатура) = ТИП(Справочник.Номенклатура)
								  |			ТОГДА ВЫБОР
								  |					КОГДА ВТВыборкаТабличнойЧасти.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
								  |						ТОГДА ЛОЖЬ
								  |					ИНАЧЕ ИСТИНА
								  |				КОНЕЦ
								  |		КОГДА ТИПЗНАЧЕНИЯ(ВТВыборкаТабличнойЧасти.Номенклатура) = ТИП(Справочник.Автоработы)
								  |			ТОГДА ЛОЖЬ
								  |		КОГДА ТИПЗНАЧЕНИЯ(ВТВыборкаТабличнойЧасти.Номенклатура) = ТИП(Справочник.Автомобили)
								  |			ТОГДА ИСТИНА
								  |		ИНАЧЕ ИСТИНА
								  |	КОНЕЦ
								  |");

			Запрос.УстановитьПараметр("ВыборкаТабличнойЧасти", ВыборкаТабличнойЧасти);
			ТоварнаяНоменклатура = Запрос.Выполнить().Выбрать();

			Если НЕ ТоварнаяНоменклатура.Следующий() Тогда 

				НаименованиеДокумента = НСтр("ru = 'Акт'");

			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
		
	Возврат НаименованиеДокумента;
КонецФункции

#КонецОбласти
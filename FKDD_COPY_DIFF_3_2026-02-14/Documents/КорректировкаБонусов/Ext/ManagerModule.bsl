#Область ИнформацияОДокументе

//************************//
// ИНФОРМАЦИЯ О ДОКУМЕНТЕ //
//************************//


// Возвращает доступные варианты печати документа
// Параметры:
//	ДокументСсылка - Ссылка на документ печати
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати. Первый элемент структуры - форма по умолчанию
Функция ПолучитьСписокПечатныхФорм(ДокументСсылка) Экспорт
	
	СтруктураМакетов = Новый Структура();
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСписокПечатныхФорм()


#КонецОбласти

#Область ЗаполнениеТабличныхЧастей

//***************************************//
// ЗАПОЛНЕНИЕ ТАБЛИЧНЫХ ЧАСТЕЙ ДОКУМЕНТА //
//***************************************//

// Очистка переданной табличной части
Процедура ОчиститьТабличнуюЧасть(Таблица) Экспорт
	
	Таблица.Очистить();
	
КонецПроцедуры

// Заполнение таблиц начисления и списания готовыми к активации бонусами
//
Процедура ЗаполнитьГотовымиКАктивацииБонусами(Объект, Дата, ОчищатьПередЗаполненим = Истина) Экспорт
	
	Если ОчищатьПередЗаполненим Тогда
		Объект.БонусыКНачислению.Очистить();
		Объект.БонусыКСписанию.Очистить();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БонусныеБаллыОстатки.БонуснаяКарта,
	|	БонусныеБаллыОстатки.ДатаСписанияБонусов,
	|	БонусныеБаллыОстатки.БонуснаяКарта.БонуснаяПрограмма КАК БонуснаяПрограмма,
	|	БонусныеБаллыОстатки.КоличествоКНачислениюОстаток
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы.Остатки(
	|			,
	|			ДатаСписанияБонусов < &ТекДата" +?(НЕ ЗначениеЗаполнено(Объект.БонуснаяПрограмма), ")"," И БонуснаяКарта.БонуснаяПрограмма = &БонуснаяПрограмма)") + " КАК БонусныеБаллыОстатки";
	Запрос.УстановитьПараметр("ТекДата", Дата);
	Запрос.УстановитьПараметр("БонуснаяПрограмма", Объект.БонуснаяПрограмма);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Объект.БонусыКНачислению.Добавить();
			НоваяСтрока.Карточка              = Выборка.БонуснаяКарта;
			НоваяСтрока.ДатаСписания          = ?(Выборка.БонуснаяПрограмма.СрокСгоранияБонусов <> 0, НачалоДня(Дата + 24*60*60*Выборка.БонуснаяПрограмма.СрокСгоранияБонусов), Дата("39990101"));
			НоваяСтрока.Количество            = Выборка.КоличествоКНачислениюОстаток;
			НоваяСтрока.КоличествоКНачислению = 0;
			
			НоваяСтрокаСписания = Объект.БонусыКСписанию.Добавить();
			НоваяСтрокаСписания.Карточка              = Выборка.БонуснаяКарта;
			НоваяСтрокаСписания.ДатаСписания          = Выборка.ДатаСписанияБонусов;
			НоваяСтрокаСписания.Количество            = 0;
			НоваяСтрокаСписания.КоличествоКНачислению = Выборка.КоличествоКНачислениюОстаток;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Заполнение просроченными бнусами
Процедура ЗаполнитьПросроченнымиБонусами(Объект, Дата, ОчищатьПередЗаполненим = Истина) Экспорт
	
	Если ОчищатьПередЗаполненим Тогда
		Объект.БонусыКСписанию.Очистить();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БонусныеБаллыОстатки.БонуснаяКарта       КАК Карточка,
	|	БонусныеБаллыОстатки.ДатаСписанияБонусов КАК ДатаСписания,
	|	БонусныеБаллыОстатки.КоличествоОстаток   КАК Количество
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы.Остатки(, ДатаСписанияБонусов < &ТекДата" +?(НЕ ЗначениеЗаполнено(Объект.БонуснаяПрограмма), ")"," И БонуснаяКарта.БонуснаяПрограмма = &БонуснаяПрограмма)") + " КАК БонусныеБаллыОстатки";
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	Запрос.УстановитьПараметр("БонуснаяПрограмма", Объект.БонуснаяПрограмма);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		// создадим документ
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Объект.БонусыКСписанию.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
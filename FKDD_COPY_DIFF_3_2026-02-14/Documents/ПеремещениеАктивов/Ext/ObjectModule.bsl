// Модуль документа ПеремещениеАктивов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ВалютаКурсДок Экспорт;        // Кэш валюты и курса документа
Перем МОЛОтбор Экспорт;             // Кэш МОЛа-получателя для отбора заполнения строк ТЧ
Перем тКэшСуммСписания Экспорт;		// Переменная для кэширования результатов вычисления суммы списания
Перем ФлагПерерасчет;               // Переменная для обозначения того, что ведется перерасчет суммы документа
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Если Клиент Тогда
// Перезаполняет ТЧ "Активы" при изменении подразделения/МОЛа
//
// Параметры:
//  ЭтаФорма - форма - ссылка на форму, 
//  ТЧАктивы - табличная часть документа - ссылка на табличную часть.
//
Процедура ПерезаполнитьТабличнуюЧасть(ЭтаФорма, ТЧАктивы)

	флПерезаполнить = Истина;
		
	Если Вопрос("Перезаполнить активами текущего подразделения <"+СокрЛП(ПодразделениеКомпании)+"> по текущему МОЛу?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		флПерезаполнить = Ложь;
	КонецЕсли;
	
	Если флПерезаполнить Тогда
		ТЧАктивы.Очистить();
		Результат = дкКоманднаяПанельАктивыЗаполнение(ЭтаФорма, ТЧАктивы, Новый Структура("Имя", "ЗаполнитьАктивамиПодразделения"));
		// смотрим что нам вернули
		Если ТипЗнч(Результат) = Тип("ТаблицаЗначений") Тогда
			ВсегоСтрок = Результат.Количество();
			Для Каждого СтрокаАктив Из Результат Цикл
				НоваяСтрока = ТЧАктивы.Добавить();
				НоваяСтрока.ПрочийАктив = СтрокаАктив.Актив;
				Состояние("Загружено " + СокрЛП(НоваяСтрока.НомерСтроки) + " из " + СокрЛП(ВсегоСтрок));
			КонецЦикла;
		КонецЕсли;
		дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры // ПерезаполнитьТабличнуюЧасть()
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеАктивы = Новый Структура();
	ОбязательныеАктивы.Вставить("ПрочийАктив", 1);
			
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("МОЛ", 1);
	ОбязательныеРеквизиты.Вставить("МОЛПолучатель", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеПолучатель", 1);
	ОбязательныеРеквизиты.Вставить("Активы", ОбязательныеАктивы);
		
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// проверять на корректность реквизитов по организации будем только в случае если все нормально
	// с остальными реквизитами
	//Чтение значения для определения способа ведения баланса
	СпособВеденияБаланса = Константы.СпособВеденияБаланса.Получить();
	
	// способ ведения не по организации или не прошла предыдущая проверка то и проверять ничего не будем
	Если  Результат И СпособВеденияБаланса = Перечисления.СпособВеденияБаланса.ПоОрганизации Тогда
		КонтролируемыеРеквизиты = Новый Структура();
		
		КонтролируемыеРеквизитыШапки = Новый Структура();
		КонтролируемыеРеквизитыШапки.Вставить("ПодразделениеПолучатель");
		КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
		
		Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
	КонецЕсли;	
	
	Для Каждого ТекСтрока Из Активы Цикл
		Если НЕ (ТекСтрока.ПрочийАктив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецодежда ИЛИ
			     ТекСтрока.ПрочийАктив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Спецоснастка ИЛИ
				 ТекСтрока.ПрочийАктив.ВидПрочегоАктива = Перечисления.ВидыПрочихАктивов.Инструменты) Тогда
			Если НЕ (ТекСтрока.Количество = 1) Тогда
				ТекСтрока.Количество = 1;
				#Если Клиент Тогда
					Сообщить("Табличная часть ""Активы"" строка " + ТекСтрока.НомерСтроки + ": количество актива """ + СокрЛП(ТекСтрока.ПрочийАктив) + """ заменено на 1,000.", СтатусСообщения.Важное);
				#КонецЕсли
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;		
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПеремещениеАктивов", "Перемещение активов", Истина);
		
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// у нас не стандартная таблица, поэтому сформирует свой результат запроса по активам
// 
Функция ПолучитьРезультатЗапросаПоАктивам()
	ТекстЗапроса="ВЫБРАТЬ
	             |	ПеремещениеАктивовАктивы.ПрочийАктив КАК Актив,
	             |	СУММА(ПеремещениеАктивовАктивы.Количество) КАК Количество
	             |ИЗ
	             |	Документ.ПеремещениеАктивов.Активы КАК ПеремещениеАктивовАктивы
	             |ГДЕ
	             |	ПеремещениеАктивовАктивы.Ссылка = &Ссылка
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ПеремещениеАктивовАктивы.ПрочийАктив";

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаПоАктивам()

// Расчет общей балансовой стоимости активов
//
Функция РассчитатьСуммуВсего() Экспорт
	СуммаВсего = дкПолучитьСуммуСписания(ЭтотОбъект, "ПрочиеАктивыВЭксплуатации", тКэшСуммСписания);
	Возврат СуммаВсего;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	//подразделение выдающее
	Если Имя = "ПодразделениеКомпании" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		//здесь будет перезаполнена ТЧ "Активы"
		#Если Клиент Тогда
		ПерезаполнитьТабличнуюЧасть(ЭтаФорма, Активы);
		#КонецЕсли
		Возврат Истина;
		
	//МОЛ выдающий
	ИначеЕсли Имя = "МОЛ" Тогда
		#Если Клиент Тогда
		ПерезаполнитьТабличнуюЧасть(ЭтаФорма, Активы);
		#КонецЕсли
		Возврат Истина;
		
	//валюта и курс	
	ИначеЕсли Имя = "ВалютаДокумента" И (ВалютаКурсДок.Валюта <> ВалютаДокумента ИЛИ ВалютаКурсДок.Курс <> КурсДокумента) Тогда
		ВалютаКурсДок.Валюта = ВалютаДокумента;
		ВалютаКурсДок.Курс = КурсДокумента;
		Возврат Истина;
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "ПеремещениеАктивов"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПеремещениеАктивов(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ПеремещениеАктивов");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьМакета.Параметры.ПредставлениеПодразделения	= спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеМОЛОтправитель	= спПолучитьНаименование(МОЛ);
	ОбластьМакета.Параметры.ПредставлениеПодразделенияПолучателя = спПолучитьНаименование(ПодразделениеПолучатель);
	ОбластьМакета.Параметры.ПредставлениеМОЛПолучатель	= спПолучитьНаименование(МОЛПолучатель);
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ВРегламентированнойВалюте = (ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
	ВалютаПечатнойФормы = ?(ВРегламентированнойВалюте,ВалютаДокумента,Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатнойФормы;
	
	ТабДокумент.Вывести(ОбластьМакета);
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	Запрос = Новый Запрос;
	Если Проведен Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПеремещениеАктивовАктивы.НомерСтроки,
		|	ПрочиеАктивыВЭксплуатации.ПрочийАктив КАК ПрочийАктив,
		|";
		Если ВРегламентированнойВалюте Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимость,0) КАК БалансоваяСтоимость,
			|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимость,0) - ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаАмортизации,0) КАК ОстаточнаяСтоимость
			|";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпр,0) КАК БалансоваяСтоимость,
			|	(ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпр,0) - ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпр,0)) КАК ОстаточнаяСтоимость
			|";
		КонецЕсли; 
		ТекстЗапроса=ТекстЗапроса+"
		|ИЗ
		|	Документ.ПеремещениеАктивов.Активы КАК ПеремещениеАктивовАктивы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
		|		ПО ПеремещениеАктивовАктивы.ПрочийАктив = ПрочиеАктивыВЭксплуатации.ПрочийАктив
		|ГДЕ
		|	ПрочиеАктивыВЭксплуатации.ВидДвижения = &ВидДвиженияНакопленияРасход
		|	И ПрочиеАктивыВЭксплуатации.Регистратор = &Ссылка
		|	И ПеремещениеАктивовАктивы.Ссылка = &Ссылка";
	Иначе
		ТЧДокумента = Активы.Выгрузить();
		ТЧДокумента.Свернуть("ПрочийАктив","Количество");
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПрочийАктив КАК ПрочийАктив,
		|	Количество  КАК Количество
		|ПОМЕСТИТЬ
		|	ТабДокумента
		|ИЗ
		|	&ТЧДокумента КАК ТЧДокумента
		|;
		|ВЫБРАТЬ
		|	ТабДокумента.ПрочийАктив КАК ПрочийАктив,
		|	ТабДокумента.Количество  КАК Количество,
		|	0 КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ПрочиеАктивыВЭксплуатации.КоличествоОстаток ЕСТЬ NULL ТОГДА
		|			0
		|		ИНАЧЕ
		|			(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпрОстаток * &МножительКурса)/ПрочиеАктивыВЭксплуатации.КоличествоОстаток*ТабДокумента.Количество
		|	КОНЕЦ КАК БалансоваяСтоимость,
		|	ВЫБОР
		|		КОГДА ПрочиеАктивыВЭксплуатации.КоличествоОстаток ЕСТЬ NULL ТОГДА
		|			0
		|		ИНАЧЕ
		|			(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпрОстаток * &МножительКурса)/ПрочиеАктивыВЭксплуатации.КоличествоОстаток*ТабДокумента.Количество
		|	КОНЕЦ - ВЫБОР
		|			КОГДА ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпрОстаток ЕСТЬ NULL 
		|				ТОГДА 0
		|			ИНАЧЕ ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпрОстаток * &МножительКурса
		|		КОНЕЦ КАК ОстаточнаяСтоимость
		|ИЗ
		|	ТабДокумента КАК ТабДокумента
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(
		|		&Момент,
		|		ПрочийАктив В (&СписокАктивов)
		|		И ПодразделениеКомпании = &ПодразделениеКомпании)КАК ПрочиеАктивыВЭксплуатации
		|ПО
		|	ТабДокумента.ПрочийАктив = ПрочиеАктивыВЭксплуатации.ПрочийАктив";
		Запрос.УстановитьПараметр("Момент"               , ?(ЭтоНовый(), Новый Граница(ТекущаяДата(),ВидГраницы.Исключая), МоментВремени()));
		Запрос.УстановитьПараметр("СписокАктивов"        , ТЧДокумента.ВыгрузитьКолонку("ПрочийАктив"));
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
		Запрос.УстановитьПараметр("ТЧДокумента"          , ТЧДокумента);
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		МножительКурса = обПересчет(1, ВалютаУправленческогоУчета,?(ЭтоНовый(), ТекущаяДата(), МоментВремени()), ВалютаДокумента, КурсДокумента);
		Запрос.УстановитьПараметр("МножительКурса",МножительКурса);
	КонецЕсли;
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,ОстаточнаяСтоимость",ВалютаПечатнойФормы,0);
		
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход",ВидДвиженияНакопления.Расход);
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = Запрос.Выполнить().Выгрузить();
	Если Не Проведен Тогда
		Сч = 1;
		Для каждого текСтрока Из ВыборкаТабличнойЧасти Цикл
			текСтрока.НомерСтроки = Сч;
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.ПрочийАктив			= СтрокаТабличнойЧасти.ПрочийАктив;
		ОбластьМакета.Параметры.АктивНаименование	= спПолучитьНаименование(СтрокаТабличнойЧасти.ПрочийАктив);
		ОбластьМакета.Параметры.Код					= СтрокаТабличнойЧасти.ПрочийАктив.ИнвентарныйНомер;
		ОбластьМакета.Параметры.БалансоваяСтоимость	= Формат(СтрокаТабличнойЧасти.БалансоваяСтоимость,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.ОстаточнаяСтоимость	= Формат(СтрокаТабличнойЧасти.ОстаточнаяСтоимость,ФорматВыводаСуммы);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;

		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,ОстаточнаяСтоимость",ВалютаПечатнойФормы,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаПечатнойФормы;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("ОстаточнаяСтоимость");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(ВыборкаТабличнойЧасти.Итог("ОстаточнаяСтоимость"),ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(ВыборкаТабличнойЧасти.Итог("ОстаточнаяСтоимость"),ВалютаПечатнойФормы);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"Отпустил");
	Отпустил.ОтпустилПредставление = ?(обЗначениеНеЗаполнено(Отпустил.Отпустил),"","/ "+ Отпустил.ОтпустилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьПеремещениеАктивов()

// Формирует печатную форму ОС2
//
Функция ПечатьОС2(ТабДокумент) Экспорт
	
	Макет = ПолучитьМакет("ОС2");
	ОбластьЗаголовок		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка1			= Макет.ПолучитьОбласть("Шапка1");
	ОбластьШапка2			= Макет.ПолучитьОбласть("Шапка2");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтогоПоСтранице	= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьПустаяСтрока 	= Макет.ПолучитьОбласть("ПустаяСтрока");
	ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//сохраняем параметры настроек
	ТабДокумент.ИмяПараметровПечати = "ПечатнаяФормаДокументов_ПеремещениеАктивов_Накладная_ОС-2";
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//Заполняем заголовок
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьЗаголовок.Параметры.ПредставлениеОрганизации	= спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	ОбластьЗаголовок.Параметры.Организация								= Организация;
	ОбластьЗаголовок.Параметры.ПредставлениеПодразделения				= спПолучитьНаименование(ПодразделениеКомпании);
	ОбластьЗаголовок.Параметры.ПодразделениеКомпании					= ПодразделениеКомпании;
	ОбластьЗаголовок.Параметры.ПредставлениеПодразделенияПолучателя	= спПолучитьНаименование(ПодразделениеПолучатель);
	ОбластьЗаголовок.Параметры.ПодразделениеПолучатель				 	= ПодразделениеПолучатель;
	ОбластьЗаголовок.Параметры.ДатаДок		  	= Формат(Дата,"ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.НомерДок = дкПолучитьНомерДляПечати(ЭтотОбъект);
	ТабДокумент.Вывести(ОбластьЗаголовок);
	ВалютаПечатнойФормы = ВалютаДокумента;
	
	Если Проведен Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПеремещениеАктивовАктивы.НомерСтроки КАК НомерСтроки,
		|	ПеремещениеАктивовАктивы.ПрочийАктив КАК ПрочийАктив,
		|	ПеремещениеАктивовАктивы.ПрочийАктив.ДатаВводаВЭксплуатацию КАК ДатаВвода,
		|	ПеремещениеАктивовАктивы.ПрочийАктив.ИнвентарныйНомер КАК ИнвНомер,";
		Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимость,0) - ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаАмортизации,0) КАК СуммаПеремещения
			|";
		Иначе
			ТекстЗапроса=ТекстЗапроса+"
			|	(ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпр,0) - ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпр,0)) КАК СуммаПеремещения
			|";
			ВалютаПечатнойФормы = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
		КонецЕсли; 
		ТекстЗапроса=ТекстЗапроса+"ИЗ
		|	Документ.ПеремещениеАктивов.Активы КАК ПеремещениеАктивовАктивы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
		|		ПО ПеремещениеАктивовАктивы.ПрочийАктив = ПрочиеАктивыВЭксплуатации.ПрочийАктив
		|		И ПеремещениеАктивовАктивы.Ссылка = ПрочиеАктивыВЭксплуатации.Регистратор
		|ГДЕ
		|	ПрочиеАктивыВЭксплуатации.ВидДвижения = &ВидДвиженияНакопленияРасход
		|	И ПеремещениеАктивовАктивы.Ссылка = &Ссылка";
		
		ОбластьШапка1.Параметры.ВалютаДокумента = ВалютаПечатнойФормы;
		ТабДокумент.Вывести(ОбластьШапка1);
		ОбластьШапка1.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
	Иначе
		ТЧДокумента = Активы.Выгрузить();
		ТЧДокумента.Свернуть("ПрочийАктив","Количество");
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПрочийАктив КАК ПрочийАктив,
		|	Количество  КАК Количество
		|ПОМЕСТИТЬ
		|	ТабДокумента
		|ИЗ
		|	&ТЧДокумента КАК ТЧДокумента
		|;
		|ВЫБРАТЬ
		|	ТабДокумента.ПрочийАктив КАК ПрочийАктив,
		|	ТабДокумента.Количество  КАК Количество,
		|	ТабДокумента.ПрочийАктив.ДатаВводаВЭксплуатацию КАК ДатаВвода,
		|	ТабДокумента.ПрочийАктив.ИнвентарныйНомер КАК ИнвНомер,
		|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.ТипЭксплуатации, ЗНАЧЕНИЕ(Справочник.ТипыЭксплуатации.ПустаяСсылка)) КАК ТипЭксплуатации,
		|	ВЫБОР
		|		КОГДА ПрочиеАктивыВЭксплуатации.КоличествоОстаток ЕСТЬ NULL ТОГДА
		|			0
		|		ИНАЧЕ
		|			ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпрОстаток/ПрочиеАктивыВЭксплуатации.КоличествоОстаток*ТабДокумента.Количество
		|	КОНЕЦ - ВЫБОР
		|				КОГДА ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпрОстаток ЕСТЬ NULL 
		|					ТОГДА 0
		|				ИНАЧЕ ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпрОстаток
		|			КОНЕЦ КАК СуммаПеремещения
		|ИЗ
		|	ТабДокумента КАК ТабДокумента
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(
		|		&Момент,
		|		ПрочийАктив В (&СписокАктивов)
		|		И ПодразделениеКомпании = &ПодразделениеКомпании)КАК ПрочиеАктивыВЭксплуатации
		|ПО
		|	ТабДокумента.ПрочийАктив = ПрочиеАктивыВЭксплуатации.ПрочийАктив";
		Запрос.УстановитьПараметр("Момент"               , ?(ЭтоНовый(), Новый Граница(ТекущаяДата(),ВидГраницы.Исключая), МоментВремени()));
		Запрос.УстановитьПараметр("СписокАктивов"        , ТЧДокумента.ВыгрузитьКолонку("ПрочийАктив"));
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
		Запрос.УстановитьПараметр("ТЧДокумента"          , ТЧДокумента);
	КонецЕсли;
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, СуммаПеремещения",ВалютаПечатнойФормы,0);
	
	СуммаВсего = 0;
	ВыборкаТабличнойЧасти = Запрос.Выполнить().Выгрузить();
	Если Не Проведен Тогда
		ВыборкаТабличнойЧасти.Колонки.Добавить("НомерСтроки",Новый ОписаниеТипов("Число",,Новый КвалификаторыЧисла(5,0)));
		Сч = 1;
		Для каждого текСтрока Из ВыборкаТабличнойЧасти Цикл
			текСтрока.НомерСтроки = Сч;
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьСтрока.Параметры.СуммаПеремещения = Формат(СтрокаТабличнойЧасти.СуммаПеремещения,ФорматВыводаСуммы);
		СуммаВсего = СуммаВсего + СтрокаТабличнойЧасти.СуммаПеремещения;
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьСтрока, ОбластьШапка1, ОбластьИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, СуммаПеремещения",ВалютаПечатнойФормы,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапка1.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
		КонецЕсли;
		СтруктураИтоговПоСтранице.Вставить("СуммаПеремещения",СтруктураИтоговПоСтранице.СуммаПеремещения+СтрокаТабличнойЧасти.СуммаПеремещения);
	КонецЦикла;
	
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	Позиция=Активы.Количество()+1;
	Если (Позиция < 17) ИЛИ (Не обПраво("ИспользоватьРазбиениеНаСтраницы",Права,,ЭтотОбъект)) Тогда
		МассивОбластейДляПроверки = Новый Массив;
		МассивОбластейДляПроверки.Добавить(ОбластьПодвал);
		Попытка
			Если (Позиция < 17) ИЛИ (Не ТабДокумент.ПроверитьВывод(МассивОбластейДляПроверки)) Тогда 
				Пока Истина Цикл
					МассивОбластейДляПроверки = Новый Массив;
					МассивОбластейДляПроверки.Добавить(ОбластьПустаяСтрока);
					Если Не ТабДокумент.ПроверитьВывод(МассивОбластейДляПроверки) Тогда Прервать; КонецЕсли;
					ТабДокумент.Вывести(ОбластьПустаяСтрока);
				КонецЦикла;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ОбластьШапка2.Параметры.ВалютаДокумента = ВалютаПечатнойФормы;
				ТабДокумент.Вывести(ОбластьШапка2);
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект, Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер, Организация);
	
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ПредставлениеМОЛОтправитель		= спПолучитьНаименование(МОЛ);
	ОбластьПодвал.Параметры.МОЛ								= МОЛ;
	ОбластьПодвал.Параметры.ДолжностьМОЛОтправитель         = МОЛ.Должность;
	ОбластьПодвал.Параметры.ТабНомерМОЛОтправитель          = МОЛ.ТабельныйНомер;
	ОбластьПодвал.Параметры.ПредставлениеМОЛПолучатель		= спПолучитьНаименование(МОЛПолучатель);
	ОбластьПодвал.Параметры.МОЛПолучатель					= МОЛПолучатель;
	ОбластьПодвал.Параметры.ДолжностьМОЛПолучатель          = МОЛПолучатель.Должность;
	ОбластьПодвал.Параметры.ТабНомерМОЛПолучатель           = МОЛПолучатель.ТабельныйНомер;
	ОбластьПодвал.Параметры.ПредставлениеГлавныйБухгалтер	= ГлавныйБухгалтер.ГлавныйБухгалтерПредставление;
	ОбластьПодвал.Параметры.ГлавныйБухгалтер				= ГлавныйБухгалтер.ГлавныйБухгалтер;
	ТабДокумент.Вывести(ОбластьПодвал);
	ДополнительноКПредставлению = ?(обЗначениеНеЗаполнено(ДополнительноКПредставлению),"",ДополнительноКПредставлению);
	
	Возврат ТабДокумент;

КонецФункции // ПечатьОС2()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер = Ложь, Документ = Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработка заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Результат = Истина;
	Если Не ФлагПерерасчет Тогда
		Результат = дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	КонецЕсли;
	ФлагПерерасчет = Ложь;
	Если НЕ Результат Тогда
		дкУстановитьЗначениеДополнительногоСвойства(ЭтотОбъект,Отказ);
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = 0;
	КонецЕсли;	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	РезультатЗапросаПоАктивам = ПолучитьРезультатЗапросаПоАктивам();
	
	// переместим "Активы в эксплуатации"
	НаборЗаписейЭксплуатация = Движения.ПрочиеАктивыВЭксплуатации;
	НаборЗаписейЭксплуатация.ДокументОбъект            = ЭтотОбъект;
	НаборЗаписейЭксплуатация.ПодразделениеКомпании     = ПодразделениеКомпании;
	НаборЗаписейЭксплуатация.МОЛ                       = МОЛ;
	НаборЗаписейЭксплуатация.ПодразделениеКомпанииПолучатель = ПодразделениеПолучатель;
	НаборЗаписейЭксплуатация.МОЛПолучатель			   = МОЛПолучатель;
	НаборЗаписейЭксплуатация.РезультатЗапросаПоАктивам = РезультатЗапросаПоАктивам;
	Отказ = НЕ НаборЗаписейЭксплуатация.Перемещение() ИЛИ Отказ;
	
	// переместим авто на тест драйве если необходимо
	ТаблицаВрем = НаборЗаписейЭксплуатация.Выгрузить();
	ТаблицаКопия = ТаблицаВрем.Скопировать();
	ТаблицаКопия.Очистить();
	
	Для Каждого стрВрем из ТаблицаВрем Цикл
		Если стрВрем.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
			НоваяСтрока = ТаблицаКопия.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,стрВрем);
		КонецЕсли;
	КонецЦикла;
	
	// 2) Проведем по ТестДрайву
	НаборЗаписейТестДрайв = Движения.АвтомобилиДляТестДрайва;
	НаборЗаписейТестДрайв.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейТестДрайв.ПодразделениеКомпании = ПодразделениеПолучатель;
	НаборЗаписейТестДрайв.РезультатЗапросаПоАвтомобилям = ТаблицаКопия;
	НаборЗаписейТестДрайв.Перемещение();
	
	БалансВедетсяПоПодразделениям      = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	ПодразделениеОтправительБалансовое = обПолучитьБалансовоеПодразделение(ПодразделениеКомпании);
	ПодразделениеПолучательБалансовое  = обПолучитьБалансовоеПодразделение(ПодразделениеПолучатель);
	БалансовыеПодразделенияНеРавны     = (ПодразделениеОтправительБалансовое<>ПодразделениеПолучательБалансовое);
	
	Если БалансВедетсяПоПодразделениям И БалансовыеПодразделенияНеРавны Тогда
		// БАЛАНС: Уменьшим доход по подразделению отправителя и увеличим по подразделению получателя
		ТаблицаПрочихАктивов = Движения.ПрочиеАктивыВЭксплуатации.Выгрузить();
		ТаблицаПрочихАктивов.Свернуть("ВидДвижения","БалансоваяСтоимостьУпр,СуммаАмортизацииУпр");
		
		// Коррекция по себестоимости
		СтрокаСписания = ТаблицаПрочихАктивов.Найти(ВидДвиженияНакопления.Расход,"ВидДвижения");
		Если СтрокаСписания <> Неопределено И СтрокаСписания.БалансоваяСтоимостьУпр <> 0 Тогда
			// Уменьшение дохода
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ПодразделениеОтправительБалансовое;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
			НаборЗаписейДоходыИРасходы.Доход=СтрокаСписания.БалансоваяСтоимостьУпр;
			НаборЗаписейДоходыИРасходы.Расход=0;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			
			// Увеличение дохода
			СтрокаПрихода = ТаблицаПрочихАктивов.Найти(ВидДвиженияНакопления.Приход,"ВидДвижения");
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ПодразделениеПолучательБалансовое;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
			НаборЗаписейДоходыИРасходы.Доход=СтрокаСписания.БалансоваяСтоимостьУпр;
			НаборЗаписейДоходыИРасходы.Расход=0;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		
		// Коррекция по амортизации
		Если СтрокаСписания <> Неопределено И СтрокаСписания.СуммаАмортизацииУпр <> 0 Тогда
			// Уменьшение дохода
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ПодразделениеОтправительБалансовое;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
			НаборЗаписейДоходыИРасходы.Доход=0;
			НаборЗаписейДоходыИРасходы.Расход=СтрокаСписания.СуммаАмортизацииУпр;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
			
			// Увеличение дохода
			СтрокаПрихода = ТаблицаПрочихАктивов.Найти(ВидДвиженияНакопления.Приход,"ВидДвижения");
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ПодразделениеПолучательБалансовое;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Истина;
			НаборЗаписейДоходыИРасходы.Доход=0;
			НаборЗаписейДоходыИРасходы.Расход=СтрокаСписания.СуммаАмортизацииУпр;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	Если Не Отказ Тогда
		тКэшСуммСписания = Неопределено;
		СуммаДокумента = РассчитатьСуммуВсего();
		ФлагПерерасчет = Истина;
		Записать();
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// инициализация кэша
ВалютаКурсДок = Новый Структура("Валюта, Курс", ВалютаДокумента, КурсДокумента);
МОЛОтбор = МОЛ;
ФлагПерерасчет = Ложь;

// Получение сделки по документу основания
//
// Параметры
//  Сделка  - ДокументСсылка - Ссылка на документ основания.
//
// Возвращаемое значение:
//   ДокументСсылка   - Сделка в цепочке документов.
//
Функция ПолучитьСделку(Знач Сделка) Экспорт 
	Если (ЗначениеЗаполнено(Сделка)) И ТипЗнч(Сделка) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		Сделка = ПолучитьСделку(Сделка.ДокументОснование);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Сделка) Тогда
		Сделка = Неопределено;
	КонецЕсли; 
	Возврат Сделка;
КонецФункции // ПолучитьСделку()

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.КорректировкаРеализации - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами(Объект) Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	ОсвобождентОтНДС = Объект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Объект.Организация.ОсвобожденОтНДС;
	
	// Проверим есть ли ставка НДС в документе
	ТаблицаНДС = Объект.Товары.Выгрузить();
	ТаблицаНДС.Свернуть("СтавкаНДС");
	ЕстьНДС = НЕ (ТаблицаНДС.Количество() = 1 И ТаблицаНДС[0].СтавкаНДС = Справочники.СтавкиНДС.БезНДС);
	
	// Операцию об отчете не выводим если есть ставка НДС в документе и организация плательщик НДС
	Если ЕстьНДС И НЕ ОсвобождентОтНДС Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	
	Если Объект.ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииИсправлениеВПервичныхДокументах Тогда
		// Составим список документов движений
		ДокОснование = Объект.Ссылка;
		СписокОснований = Новый СписокЗначений;
		
		Пока Истина Цикл
			СписокОснований.Добавить(ДокОснование);
			Если ТипЗнч(ДокОснование) <> Тип("ДокументСсылка.КорректировкаРеализации") Тогда
				Прервать;
			КонецЕсли;
			ДокОснование = ДокОснование.ДокументОснование;
		КонецЦикла;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	КорректировкаРеализацииТовары.Номенклатура КАК Номенклатура,
		|	СУММА(КорректировкаРеализацииТовары.Количество * КорректировкаРеализацииТовары.Коэффициент) КАК КоличествоОсталось,
		|	СУММА(КорректировкаРеализацииТовары.СуммаВсего - КорректировкаРеализацииТовары.СуммаНДС) КАК СуммаОсталось
		|ИЗ
		|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
		|ГДЕ
		|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаРеализацииТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УПД) КАК ВидДокумента,
		|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияТоваров) КАК КодОперации,
		|	ГТДПартийТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ГТДПартийТоваровКомпании.ГТД КАК РНПТ,
		|	СУММА(ГТДПартийТоваровКомпании.Количество) КАК КоличествоПрослеживаемости
		|ИЗ
		|	РегистрНакопления.ГТДПартийТоваровКомпании КАК ГТДПартийТоваровКомпании
		|ГДЕ
		|	ГТДПартийТоваровКомпании.Регистратор В(&СписокОснований)
		|	И ГТДПартийТоваровКомпании.ГТД.РНПТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ГТДПартийТоваровКомпании.Номенклатура,
		|	ГТДПартийТоваровКомпании.ГТД";
		Запрос.УстановитьПараметр("СписокОснований", СписокОснований);
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		ПакетЗапросов = Запрос.ВыполнитьПакет();
		
		// Проверим есть РНПТ у документа
		Если ПакетЗапросов[1].Пустой() Тогда
			Возврат ТаблицаОперацииПрослеживаемости;
		КонецЕсли;
		
		ТаблицаТоваров = ПакетЗапросов[0].Выгрузить();
		ТаблицаРНПТ = ПакетЗапросов[1].Выгрузить();
		
		Документ = Объект.Сделка;
		Если ЗначениеЗаполнено(Объект.Сделка) Тогда
			Если ТипЗнч(Объект.Сделка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
				ДанныеРеализации = Новый Структура("ДатаЗакрытия,Номер", Объект.Сделка.ДатаЗакрытия, Объект.Сделка.Номер);
				ПериодОтчета = НачалоКвартала(ДанныеРеализации.ДатаЗакрытия);
				ДатаДокумента = ДанныеРеализации.ДатаЗакрытия;
			Иначе
				ДанныеРеализации = Новый Структура("Дата,Номер", Объект.Сделка.Дата, Объект.Сделка.Номер);
				ПериодОтчета = НачалоКвартала(ДанныеРеализации.Дата);
				ДатаДокумента = ДанныеРеализации.Дата;
			КонецЕсли;
			НомерДокумента = дкПолучитьНомерДляПечати(Документ);
		Иначе
			ПериодОтчета = НачалоКвартала(Объект.Дата);
		КонецЕсли;
		
		// Зафиксируем данные для заполнения
		Организация = Объект.Организация;
		КонтрагентОперации = Объект.Контрагент;
		
		// Для пересчета валюты в рубли
		ВалютаРегл = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		ВалютаДокумента = Объект.ВалютаДокумента;
		КурсДокумента = Объект.КурсДокумента;
		ВалютаНеРегл = (ВалютаДокумента <> ВалютаРегл);
		
		Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
			
			НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			НоваяСтрока.Организация = Организация;
			НоваяСтрока.ПериодОтчета = ПериодОтчета;
			НоваяСтрока.Документ = Документ;
			НоваяСтрока.НомерДокумента = НомерДокумента;
			НоваяСтрока.ДатаДокумента = ДатаДокумента;
			НоваяСтрока.Контрагент = КонтрагентОперации;
			
			// Получим сумму товара
			КоличествоРНПТ = ТекущаяСтрока.КоличествоПрослеживаемости;
			НайденныеСтроки = ТаблицаТоваров.НайтиСтроки(Новый Структура("Номенклатура", ТекущаяСтрока.Номенклатура));
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаНоменклатуры = НайденныеСтроки[0];
			
			Если КоличествоРНПТ >= СтрокаНоменклатуры.КоличествоОсталось Тогда
				СуммаБезНДС = СтрокаНоменклатуры.СуммаОсталось;
			Иначе
				СуммаБезНДС = Окр(СтрокаНоменклатуры.СуммаОсталось /
					?(СтрокаНоменклатуры.КоличествоОсталось = 0, 1, СтрокаНоменклатуры.КоличествоОсталось)
						* КоличествоРНПТ, 2);
			КонецЕсли;
			
			// Пересчет суммы
			Если ВалютаНеРегл Тогда
				СуммаБезНДС = Окр(обПересчет(СуммаБезНДС, ВалютаДокумента, КурсДокумента, ВалютаРегл, Объект.Дата), 2);
			КонецЕсли;
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			
			СтрокаНоменклатуры.КоличествоОсталось = СтрокаНоменклатуры.КоличествоОсталось - КоличествоРНПТ;
			СтрокаНоменклатуры.СуммаОсталось = СтрокаНоменклатуры.СуммаОсталось - СуммаБезНДС;
			Если СтрокаНоменклатуры.КоличествоОсталось <= 0 Тогда
				ТаблицаТоваров.Удалить(СтрокаНоменклатуры);
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.УКД) КАК ВидДокумента,
		|	ВЫБОР
		|		КОГДА Продажи.Сумма - Продажи.СуммаНДС > 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУвеличение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУменьшение)
		|	КОНЕЦ КАК КодОперации,
		|	Продажи.Номенклатура КАК Номенклатура,
		|	Продажи.ГТД КАК РНПТ,
		|	СУММА(Продажи.Количество * ВЫБОР
		|			КОГДА Продажи.Количество < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК КоличествоПрослеживаемости,
		|	СУММА((Продажи.Сумма - Продажи.СуммаНДС) * ВЫБОР
		|			КОГДА Продажи.Сумма - Продажи.СуммаНДС < 0
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК СуммаБезНДС
		|ИЗ
		|	РегистрНакопления.Продажи КАК Продажи
		|ГДЕ
		|	Продажи.Регистратор = &Ссылка
		|	И Продажи.ГТД.РНПТ
		|	И Продажи.Сумма - Продажи.СуммаНДС <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА Продажи.Сумма - Продажи.СуммаНДС > 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУвеличение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУменьшение)
		|	КОНЕЦ,
		|	Продажи.Номенклатура,
		|	Продажи.ГТД";
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		
		// Проверим есть РНПТ у документа
		Если РезультатЗапроса.Пустой() Тогда
			Возврат ТаблицаОперацииПрослеживаемости;
		КонецЕсли;
		
		ТаблицаРНПТ = РезультатЗапроса.Выгрузить();
		Документ = Объект.Ссылка;
		ПериодОтчета = НачалоКвартала(Объект.Дата);
		НомерДокумента = дкПолучитьНомерДляПечати(Объект.Ссылка);
		ДатаДокумента = Объект.Дата;
		
		// Зафиксируем данные для заполнения
		Организация = Объект.Организация;
		КонтрагентОперации = Объект.Контрагент;
		
		Для Каждого ТекущаяСтрока Из ТаблицаРНПТ Цикл
			НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			НоваяСтрока.Организация = Организация;
			НоваяСтрока.ПериодОтчета = ПериодОтчета;
			НоваяСтрока.Документ = Документ;
			НоваяСтрока.НомерДокумента = НомерДокумента;
			НоваяСтрока.ДатаДокумента = ДатаДокумента;
			НоваяСтрока.Контрагент = КонтрагентОперации;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

// Получение всех оснований корректировки реализации в цепочке документов
//
// Параметры:
//  ДокументСсылка	 - ДокументСсылка - Ссылка на документ, для которого необходимо получить
//                     все основания в цепочке.
// 
// Возвращаемое значение:
//   Основания  - СписокЗначений - Основания всех документов цепочки. 
//
Функция ОснованияЦепочкиКорректировкиРеализации(ДокументСсылка) Экспорт

	Основания = Новый СписокЗначений;
	
	Если НЕ ЗначениеЗаполнено(ДокументСсылка) Тогда
	
		Возврат Основания;
	                                               
	КонецЕсли;
	
	ТекущийДокументОснование = ДокументСсылка;

	Пока ТипЗнч(ТекущийДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации") Цикл
		
		ТекущийДокументОснование = ТекущийДокументОснование.ДокументОснование;
		Основания.Добавить(ТекущийДокументОснование);  
		
	КонецЦикла;    
	
	Возврат Основания;
	
КонецФункции 

// Добавление документов "Перемещение товаров в производство", если среди переданных
// оснований присутствуют документы "Заказ-наряд"
//
// Параметры:
//  Основания - СписокЗначений - Список документов-оснований корректировки реализаци.
//	ОставитьЗаказНаряды - Булево - Если Ложь, то в возвращаемом списке будут отсутствовать Заказ-наряды,
//	                               Если Истина, то в возвращаемом списке Заказ-наряды останутся.
// 
// Возвращаемое значение:
//   Основания_Документы  - СписокЗначений - Список переданных параметром документов-оснований корректировки реализаци, 
//                          в который добавлены документы "Перемещение товаров в производство", являющиеся основаниями
//                          для Заказ-нарядов (если таковые были переданы)
//
Функция ДобавитьПеремещенияТоваровЗаказНарядаИзОснований(Основания, ОставитьЗаказНаряды) Экспорт

	Основания_Документы = Новый СписокЗначений;
	Основания_ЗН = Новый Массив;
	
	// Отделим заказ-наряд от остальных оснований.
	Для Каждого ДокОснование Из Основания Цикл
		
		Если НЕ ТипЗнч(ДокОснование.Значение) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			
			Основания_Документы.Добавить(ДокОснование.Значение);
			
		Иначе
			
			Основания_ЗН.Добавить(ДокОснование.Значение);
			
		КонецЕсли;
	
	КонецЦикла;
	
	ЕстьЗН = Основания_ЗН.Количество() > 0;	
	
	Если ЕстьЗН Тогда
		
    	ПеремещенияВПроизводство = Новый Массив;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПеремещениеТоваровВПроизводство.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.ПеремещениеТоваровВПроизводство КАК ПеремещениеТоваровВПроизводство
			|ГДЕ
			|	ПеремещениеТоваровВПроизводство.ПометкаУдаления = ЛОЖЬ
			|	И ПеремещениеТоваровВПроизводство.ДокументОснование В(&ДокументыОснований_ЗН)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ИзвлечениеТоваровИзПроизводства.Ссылка
			|ИЗ
			|	Документ.ИзвлечениеТоваровИзПроизводства КАК ИзвлечениеТоваровИзПроизводства
			|ГДЕ
			|	ИзвлечениеТоваровИзПроизводства.ПометкаУдаления = ЛОЖЬ
			|	И ИзвлечениеТоваровИзПроизводства.ДокументОснование В(&ДокументыОснований_ЗН)";
		
		Запрос.УстановитьПараметр("ДокументыОснований_ЗН", Основания_ЗН);
		
		ПеремещенияВПроизводство = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		// Добавляем в список оснований документы "Перемещение товаров в производство".
		Для Каждого ДокПеремещениеВПроизводство Из ПеремещенияВПроизводство Цикл
			
			Основания_Документы.Добавить(ДокПеремещениеВПроизводство);
		
		КонецЦикла;
		
		Если ОставитьЗаказНаряды Тогда
			// Добавляем в список оснований документы "Заказ-наряд".
			Для Каждого ДокЗаказНаряд Из Основания_ЗН Цикл
				
				Основания_Документы.Добавить(ДокЗаказНаряд);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Основания_Документы;

КонецФункции

// Получение документа продажи по документу корректировки реализации
//
// Параметры:
//  ДокументСсылка	 - ДокументСсылка - Ссылка на документ корректировки реализации, для которого 
//	                   необходимо получить документ продажи.
// 
// Возвращаемое значение:
//   ДокументПродажи  - ДокументСсылка - Ссылка на документ продажи
//
Функция ПолучитьДокументПродажиКорректировкиРеализации(ДокументСсылка) Экспорт
	
	ДокументПродажи = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(ДокументСсылка) Тогда
		
		Возврат ДокументПродажи;
		
	КонецЕсли;  
	
	ТекущийДокументОснование = ДокументСсылка;
	
	Пока ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации") Цикл
		
		ТекущийДокументОснование = ТекущийДокументОснование.ДокументОснование;
		
		Если НЕ ТипЗнч(ТекущийДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			
			ДокументПродажи = ТекущийДокументОснование;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;    
	
	Возврат ДокументПродажи;
	
КонецФункции

// Определяет необходимость заполнения склада переданого объекта.
// Если ХозОперация сделки переданного объекта равна "Акт об оказании услуг",
// то в использовании склада нет необходимости
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - Объект, для которого выполняется операция.
//  
// Возвращаемое значение:
//  Булево - Ложь, в случае если ХозОперация сделки равна "Акт об оказании услуг"
//           Истина, в ином случае.
//
Функция НеобходимоИспользоватьСклад(Объект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
	
		Возврат Истина;
	
	КонецЕсли;
	
	Сделка = ПолучитьСделку(Объект.ДокументОснование);
	
	Если НЕ ЗначениеЗаполнено(Сделка) Тогда
	
		Возврат Истина;
	
	КонецЕсли;
	
	ХозОперацияСделки = Сделка.ХозОперация;

	Если ХозОперацияСделки = Справочники.ХозОперации.АктОбОказанииУслуг
		ИЛИ ХозОперацияСделки = Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции

// получить удаляемые кнопки
Функция УдаляемыеКнопкиЗаполнения() Экспорт
	
	УдаляемыеКнопки = Новый Массив();
	УдаляемыеКнопки.Добавить("ЗаполнитьИзКорзины");
	УдаляемыеКнопки.Добавить("ВыгрузитьВКорзину");
	УдаляемыеКнопки.Добавить("ЗаполнитьИзТСД");
	УдаляемыеКнопки.Добавить("ЗаполнитьИзФайла");
	
	Возврат УдаляемыеКнопки;
	
КонецФункции

// Возвращает текст запроса для получения регистраторов для первоначального заполнения
//  регистра "Операции прослеживаемых товаров"
//
// Возвращаемое значение:
//  Строка - текст запроса
Функция ЗапросРегистраторовОперацийПрослеживаемогоТовара() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КорректировкаРеализации.Ссылка КАК Регистратор,
	|	КорректировкаРеализации.Дата КАК Период
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И КорректировкаРеализации.Проведен";
	Возврат ТекстЗапроса;
	
КонецФункции

#Область ИнформацияОДокументе

//************************//
// ИНФОРМАЦИЯ О ДОКУМЕНТЕ //
//************************//

// Возвращает доступные варианты печати документа
// Параметры:
//	ДокументСсылка - Ссылка на документ печати
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати. Первый элемент структуры - форма по умолчанию
Функция ПолучитьСписокПечатныхФорм(ДокументСсылка) Экспорт
	
	СтруктураМакетов = Новый Структура();
	
	СтруктураМакетов.Вставить("РасходнаяНаклодная" , "Расходная накладная");
	СтруктураМакетов.Вставить("ТОРГ12"             , "ТОРГ-12 (Товарная накладная)");
	СтруктураМакетов.Вставить("УПД"                , "Универсальный передаточный документ");
	СтруктураМакетов.Вставить("УКД"                , "Универсальный корректировочный документ");
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСписокПечатныхФорм()

#КонецОбласти
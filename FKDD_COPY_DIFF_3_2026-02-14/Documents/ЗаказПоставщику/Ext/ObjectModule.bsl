// Модуль документа ЗаказПоставщику

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

//Перерасчет таблицы товаров в соответствии с таблицей распределений
//
//Параметры:
//  ЭтаФорма - форма - форма обрабатываемого документа
//  УчитыватьПрайсЛистыВРаспределенииЗаказов - Булево - Использовать информацию о прайс-листах из табличной части Распределение заказов
//
Процедура ПерерасчетРаспределения(ЭтаФорма=Неопределено, УчитыватьПрайсЛистыИзРаспределенияЗаказов=Ложь) Экспорт
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		СтрокаТоваров.Количество   = СтрокаТоваров.Количество - Мин(СтрокаТоваров.Распределено, СтрокаТоваров.Количество);
		СтрокаТоваров.Распределено = 0;
	КонецЦикла;
	
	//СтруктураНоменклатуры = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	УстаревшиеПрайсЛисты = Новый Массив;
	Если УчитыватьПрайсЛистыИзРаспределенияЗаказов Тогда
		СтруктураНоменклатуры = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, ПрайсЛист, КлючСтрокиПоставщика");
	Иначе
		СтруктураНоменклатуры = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	КонецЕсли;
	
	Для Каждого СтрокаРаспределения Из РаспределениеЗаказа Цикл
		Если НЕ СтрокаРаспределения.Номенклатура.Пустая() Тогда
			
			ЗаполнитьЗначенияСвойств(СтруктураНоменклатуры, СтрокаРаспределения);
			
			МассивСтрокТоваров = Товары.НайтиСтроки(СтруктураНоменклатуры);
			Если МассивСтрокТоваров.Количество() <= 0 Тогда
				СтрокаТоваров = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТоваров, СтрокаРаспределения,, "Количество, Цена, ПрайсЛист, КлючСтрокиПоставщика");
				ЗаполнитьЗначенияСвойств(СтрокаТоваров, СтруктураНоменклатуры);
				ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров,ЭтаФорма);
				СтрокаТоваров.Количество   = 0;
				СтрокаТоваров.Распределено = 0;
			Иначе
				СтрокаТоваров = МассивСтрокТоваров[0];
			КонецЕсли;
			Коэффициент = ?(СтрокаРаспределения.Коэффициент=0,1,СтрокаРаспределения.Коэффициент);
			Количество = ((СтрокаРаспределения.Количество*Коэффициент)/Коэффициент);
			СтрокаТоваров.Распределено = СтрокаТоваров.Распределено + Количество;
			СтрокаТоваров.Количество   = СтрокаТоваров.Количество   + Количество;
			ОбработкаРеквизита("Товары.Распределено", СтрокаТоваров, ЭтаФорма);
			
			Если УчитыватьПрайсЛистыИзРаспределенияЗаказов И ЗначениеЗаполнено(СтрокаТоваров.КлючСтрокиПоставщика) Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ПозицииПрайсЛиста.Валюта,
				|	ПозицииПрайсЛиста.Цена * (100 - ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0))))) / 100 КАК Цена,
				|	ПозицииПрайсЛиста.СрокПоставки
				|ИЗ
				|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК ПозицииПрайсЛиста
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(, ПрайсЛист = &ПрайсЛист) КАК Скидки
				|		ПО (НЕ Скидки.Отменена)
				|			И (Скидки.ТегПозиции = ПозицииПрайсЛиста.ТегПозиции)
				|			И (Скидки.Производитель = ПозицииПрайсЛиста.Производитель)
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
				|				,
				|				ПрайсЛист = &ПрайсЛист
				|					И ТегПозиции <> """"
				|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиТегПозиции
				|		ПО (НЕ СкидкиТегПозиции.Отменена)
				|			И (СкидкиТегПозиции.ТегПозиции = ПозицииПрайсЛиста.ТегПозиции)
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
				|				,
				|				ПрайсЛист = &ПрайсЛист
				|					И ТегПозиции = """"
				|					И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиПроизводитель
				|		ПО (НЕ СкидкиТегПозиции.Отменена)
				|			И (СкидкиТегПозиции.Производитель = ПозицииПрайсЛиста.Производитель)
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
				|				,
				|				ПрайсЛист = &ПрайсЛист
				|					И ТегПозиции = """"
				|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиБазовые
				|		ПО (НЕ СкидкиБазовые.Отменена)
				|ГДЕ
				|	ПозицииПрайсЛиста.ПрайсЛист = &ПрайсЛист
				|	И ПозицииПрайсЛиста.КлючСтрокиПоставщика = &КлючСтрокиПоставщика";
				
				Запрос.УстановитьПараметр("ПрайсЛист", СтрокаТоваров.ПрайсЛист);
				Запрос.УстановитьПараметр("КлючСтрокиПоставщика", СтрокаТоваров.КлючСтрокиПоставщика);
				Выборка = Запрос.Выполнить().Выбрать();
				
				Если Выборка.Следующий() Тогда
					СтрокаТоваров.Цена = обПересчет(Выборка.Цена, Выборка.Валюта, Дата, ВалютаДокумента, КурсДокумента);
					//СтрокаТоваров.ПримечаниеНоменклатура = Выборка.СрокПоставки;
				Иначе
					// Нет такой строки в прайс-листе!
					СтрокаТоваров.Цена = 0;
					//СтрокаТоваров.ПримечаниеНоменклатура = "";
					УстаревшиеПрайсЛисты.Добавить(Новый Структура("ПрайсЛист, КлючСтрокиПоставщика", СтрокаТоваров.ПрайсЛист, СтрокаТоваров.КлючСтрокиПоставщика));
				КонецЕсли;
				
				ОбработкаРеквизита("Товары.Цена", СтрокаТоваров, ЭтаФорма);
			КонецЕсли;
			
		КонецЕсли; 
	КонецЦикла;
	
	Если УстаревшиеПрайсЛисты.Количество() > 0 Тогда
		Для Каждого УстаревшийПрайсЛист ИЗ УстаревшиеПрайсЛисты Цикл
			МассивСтрокТоваров=Товары.НайтиСтроки(УстаревшийПрайсЛист);
			Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
				СтрокаТоваров.КлючСтрокиПоставщика = "";
			КонецЦикла;
			МассивСтрокТоваров=РаспределениеЗаказа.НайтиСтроки(УстаревшийПрайсЛист);
			Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
				СтрокаТоваров.КлючСтрокиПоставщика = "";
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	МассивСтрокТоваров=Товары.НайтиСтроки(Новый Структура("Количество, Распределено", 0, 0));
	Для Каждого СтрокаТоваров Из МассивСтрокТоваров Цикл
		Товары.Удалить(СтрокаТоваров);
	КонецЦикла;
	
	#Если Клиент Тогда
	Если ЭтаФорма <> Неопределено Тогда
		дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
	КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры // ПерерасчетРаспределения()

//Получает количество нераспределенного товара. Если полностью распределено или товар в заказе не найден,
//то возвращает Неопределено
//Параметры:
//	Заказ - {ЗаказПокупателя, ЗаказВнутренний} Ссылка - ссылка на заказ,
//	Номенклатура - номенклатурная позиция,
//	Характеристика - характеристика номенклатуры.
//
Функция ПолучитьТекущееСостояниеЗаказа(Заказ, Номенклатура, Характеристика)
	ТекстЗапроса = "ВЫБРАТЬ
	|	(ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) - ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Количество
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(
	|		&НаДату,
	|		Заказ = &Заказ И 
	|		Номенклатура = &Номенклатура И 
	|		ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыПокупателейОстатки
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыРаспределение.Остатки(
	|		&НаДату,
	|		ЗаказПокупателя = &Заказ И 
	|		Номенклатура = &Номенклатура И 
	|		ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ЗаказыРаспределениеОстатки
	|ПО
	|	ЗаказыПокупателейОстатки.Номенклатура               = ЗаказыРаспределениеОстатки.Номенклатура И 
	|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗаказыПокупателейОстатки.РезервОстаток, 0) > ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", ?(ЭтоНовый(), ТекущаяДата(), Новый Граница(Дата, ?(Проведен, ВидГраницы.Исключая, ВидГраницы.Включая))));
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", Характеристика);
	Рез = Запрос.Выполнить().Выбрать();
	Если Рез.Следующий() Тогда
		Возврат Рез.Количество;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции // ПолучитьТекущееСостояниеЗаказа()

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ПрайсЛист",2);
	ОбязательныеТовары.Вставить("КлючСтрокиПоставщика",2);
	
	// Обязательные поля таблицы распределения заказов
	ОбязательныеРаспределение=Новый Структура();
	ОбязательныеРаспределение.Вставить("Номенклатура",3);
	ОбязательныеРаспределение.Вставить("Количество",1);
	ОбязательныеРаспределение.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеРаспределение.Вставить("Коэффициент",1);
	ОбязательныеРаспределение.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеРаспределение.Вставить("ЗаказПокупателя",3);
	ОбязательныеРаспределение.Вставить("ПрайсЛист",2);
	ОбязательныеРаспределение.Вставить("КлючСтрокиПоставщика",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("СрокПоставки",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("РаспределениеЗаказа",ОбязательныеРаспределение);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);

	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Если БалансВедетсяПоОрганизации Тогда
				КонтролируемыеРеквизитыРаспределениеЗаказа = Новый Структура();
				КонтролируемыеРеквизитыРаспределениеЗаказа.Вставить("ЗаказПокупателя", Ложь);
				ТабличныеЧасти = Новый Структура();
				ТабличныеЧасти.Вставить("РаспределениеЗаказа",КонтролируемыеРеквизитыРаспределениеЗаказа);
				КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);		
			КонецЕсли;
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) и Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	// ++ Alexku 29.08.2015
	// Проверим на принадлежность прайс-листов в документе контрагенту.
	// Владелец прайс-листа со временем может меняться и в документе будет старый контрагент, но это не ошибка.
	// Просто сообщим.
	Если Результат Тогда
		Для Каждого СтрокаРаспределения Из РаспределениеЗаказа Цикл
			Если ЗначениеЗаполнено(СтрокаРаспределения.ПрайсЛист) И СтрокаРаспределения.ПрайсЛист.Владелец <> Контрагент Тогда
				Сообщить("Прайс-лист < " + СтрокаРаспределения.ПрайсЛист + " > в строке < " + СтрокаРаспределения.НомерСтроки + " > не принадлежит контрагенту документа < " + Контрагент + " >", СтатусСообщения.Внимание);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	// -- Alexku 
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//	 			   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ЗаказПоставщику","Заказ поставщику",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ЗаказПоставщику Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("Контрагент",Контрагент);
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.Покупка);
			ДопПараметры.Вставить("ТипЦенПокупки",ТипЦен);
			ДопПараметры.Вставить("ТипЦенПродажи",Справочники.ТипыЦен.ПустаяСсылка());
		КонецЕсли; 
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			//Чтение значения для срока поставки
			СтруктураОтбора=Новый Структура("Организация,Объект",Контрагент,Перечисления.ВидыОбъектовСведений.СрокПоставки);
			СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(?(обЗначениеНеЗаполнено(Дата),ТекущаяДата(),Дата),СтруктураОтбора);
			СрокПоставкиДней=?(обЗначениеНеЗаполнено(СтруктураСведений.Значение),обПраво("СрокПоставкиПокупателюПоУмолчанию",Права,,ЭтотОбъект),СтруктураСведений.Значение);
			СрокПоставки=НачалоДня(Дата)+СрокПоставкиДней*60*60*24;
			#Если Клиент Тогда
				Если ЭтаФорма<>Неопределено Тогда ЭтаФорма.Обновить(); КонецЕсли; 
			#КонецЕсли
			ОбработкаРеквизита("ОтображениеСрокаПоставки",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		Возврат Рез;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Если ДопПараметры = Неопределено Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("Контрагент", Контрагент);
		КонецЕсли;
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если ТекСтрока<>Неопределено Тогда
			Попытка
				Количество=ТекСтрока.Количество;
				Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры) ИЛИ Рез;
			Исключение
			КонецПопытки; 
		КонецЕсли; 
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОбновитьПодборЗамен();
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		Если ТекСтрока.Количество < ТекСтрока.Распределено Тогда
			ТекСтрока.Количество = ТекСтрока.Распределено;
			Сообщить("Количество номенклатуры не может быть меньше распределенной");
		КонецЕсли;
		// проверим кратное количество
		Если УчитыватьКратность И НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда
			Кратность = ТекСтрока.Номенклатура.КратностьПоставок;
			Если Кратность > 0 Тогда
				ЦелЧасть   = Цел(ТекСтрока.Количество/Кратность);
				ОстатокДел = ТекСтрока.Количество%Кратность;
				Если ОстатокДел > 0 Тогда
					ТекСтрока.Количество = (ЦелЧасть+1)*Кратность;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.Распределено" Тогда
		Возврат ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Если ТекСтрока.Коэффициент = ТекСтрока.ЕдиницаИзмерения.Коэффициент Тогда
			Возврат Ложь;
		Иначе
			КоэффВрем = ?(ТекСтрока.Коэффициент = 0, 1, ТекСтрока.Коэффициент); //запоминаем старый коэффициент
			ТекСтрока.Коэффициент  = ТекСтрока.ЕдиницаИзмерения.Коэффициент; //установим новый
			ТекСтрока.Распределено = ТекСтрока.Распределено * КоэффВрем / ТекСтрока.Коэффициент; //рассчитаем колич.
			ТекСтрока.Количество   = ТекСтрока.Количество * КоэффВрем / ТекСтрока.Коэффициент; //рассчитаем колич.
			Возврат ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "РАСПРЕДЕЛЕНИЕ ЗАКАЗА"
	ИначеЕсли Имя="РаспределениеЗаказа.Номенклатура" Тогда
		Если ТекСтрока.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Набор Тогда
			// получим виды номенклатуры которые нельзя выбирать
			БлокироватьВидыНоменклатуры=ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
			Если БлокироватьВидыНоменклатуры<>Неопределено И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
				Попытка ЕстьОшибка=(БлокироватьВидыНоменклатуры[ТекСтрока.Номенклатура.ВидНоменклатуры]<>Неопределено); Исключение ЕстьОшибка=Ложь; КонецПопытки;
				Если ЕстьОшибка Тогда
					#Если Клиент Тогда
					Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""",5);
					#КонецЕсли
					ТекСтрока.Номенклатура=Неопределено;
					ОбработкаРеквизита("РаспределениеЗаказа.Номенклатура",ТекСтрока,ЭтаФорма,ДопПараметры);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			
			РезПроверки = дкПроверитьВозможностьОперацийСНоменклатурой(ТекСтрока,ЭтаФорма, ЭтотОбъект);
			
			Если РезПроверки.ЕстьБлок Тогда
				ТекСтрока.Номенклатура = Неопределено;
			КонецЕсли;
			
			Если (РезПроверки.ЕстьПредупреждение ИЛИ РезПроверки.ЕстьБлок) И НЕ ПустаяСтрока(РезПроверки.Сообщение) Тогда
				Сообщить(РезПроверки.Сообщение,СтатусСообщения.Внимание);
			КонецЕсли;
			
			
			// установим количество
			Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
			// сбросим характеристику, если она не подходит
			Если (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура) И (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
				ТекСтрока.ХарактеристикаНоменклатуры=Неопределено;
			КонецЕсли;
			// заполним единицу измерения
			Если ДопПараметры=Неопределено Тогда
				НоваяЕдиницаИзмерения=ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
			Иначе
				Если НЕ ДопПараметры.Свойство("ЕдиницаИзмерения",НоваяЕдиницаИзмерения) Тогда
					НоваяЕдиницаИзмерения=ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
				КонецЕсли; 
			КонецЕсли;
			// обработаем изменение единицы измерения
			ТекСтрока.ЕдиницаИзмерения=НоваяЕдиницаИзмерения;
			Рез=ОбработкаРеквизита("РаспределениеЗаказа.ЕдиницаИзмерения",ТекСтрока,ЭтаФорма,ДопПараметры);
			#Если Клиент Тогда
			// покажем если надо колонку "характеристика номенклатуры"
			Если ЭтаФорма<>Неопределено Тогда
				//ТекСтрока=ЭлементыФормы.РаспределениеЗаказа.ТекущиеДанные;
				Если обПраво("АвтоматическиСкрыватьКолонкуХарактеристик",Права,,ЭтотОбъект) И НЕ
					 ЭтаФорма.ЭлементыФормы.РаспределениеЗаказа.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
					 ЭтаФорма.ЭлементыФормы.РаспределениеЗаказа.Колонки.ХарактеристикаНоменклатуры.Видимость = (ТекСтрока.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=1 ИЛИ ТекСтрока.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=2);
				КонецЕсли;
			КонецЕсли;
			#КонецЕсли
		Иначе
			Рез=Истина;
			// имеем дело с набором, разложим его...
			Набор=ТекСтрока.Номенклатура; КоличествоНабора=ТекСтрока.Количество;
			Если КоличествоНабора=0 Тогда КоличествоНабора=1; КонецЕсли; 
			// удалим строку из табличной части
			ЭтотОбъект.РаспределениеЗаказа.Удалить(ТекСтрока);
			// теперь будем рекурсивно добавлять состав набора
			Для Каждого ТоварИзНабора Из Набор.СоставНабора Цикл
				СтрокаТЧ=ЭтотОбъект.РаспределениеЗаказа.Добавить();
				СтрокаТЧ.Номенклатура=ТоварИзНабора.Номенклатура;
				Рез=ОбработкаРеквизита("РаспределениеЗаказа.Номенклатура",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Рез;
				СтрокаТЧ.Количество=ТоварИзНабора.Количество*КоличествоНабора/?(СтрокаТЧ.Коэффициент=0,1,СтрокаТЧ.Коэффициент);
			КонецЦикла;
			ПерерасчетРаспределения(ЭтаФорма);
		КонецЕсли;
		Возврат Рез И ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="РаспределениеЗаказа.ХарактеристикаНоменклатуры" Тогда
		Возврат ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="РаспределениеЗаказа.ЕдиницаИзмерения" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ЕдиницаИзмерения) Тогда
			ТекСтрока.Коэффициент = ТекСтрока.ЕдиницаИзмерения.Коэффициент;
			Возврат Истина;
		КонецЕсли;
		Возврат Ложь;
		
	ИначеЕсли Имя = "РаспределениеЗаказа.ЗаказПокупателя" Тогда
		Если НЕ обЗначениеНеЗаполнено(ТекСтрока.ЗаказПокупателя) Тогда
			ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя = "РаспределениеЗаказа.Количество" Тогда
		Возврат ОбработкаРеквизита("РаспределениеЗаказа.ПроверкаЗаполнения", ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя = "РаспределениеЗаказа.ПроверкаЗаполнения" Тогда
		
		// Потом эти поля возьмем из заказа покупателя
		ТекСтрока.ПрайсЛист = Неопределено;
		ТекСтрока.КлючСтрокиПоставщика = "";
		ТекСтрока.СрокПоставкиВСтроке = "";
		Если обЗначениеНеЗаполнено(ТекСтрока.ЗаказПокупателя) ИЛИ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда Возврат Ложь; КонецЕсли;
		
		КоличествоНераспределенного = ПолучитьТекущееСостояниеЗаказа(ТекСтрока.ЗаказПокупателя, ТекСтрока.Номенклатура, ТекСтрока.ХарактеристикаНоменклатуры);
		Если КоличествоНераспределенного = Неопределено Тогда
			Сообщить("Номенклатура <" + ТекСтрока.Номенклатура.Наименование
				+ ?(ТекСтрока.ХарактеристикаНоменклатуры.Пустая(), "", " / " + ТекСтрока.ХарактеристикаНоменклатуры.Наименование)
				+ "> отсутствует в заказе <" + ТекСтрока.ЗаказПокупателя + "> или полностью распределена!");
				
			Если ТипЗнч(ТекСтрока.ЗаказПокупателя) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				ТекСтрока.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
			Иначе
				ТекСтрока.ЗаказПокупателя = Документы.ЗаказВнутренний.ПустаяСсылка();
			КонецЕсли;
		Иначе
			Если КоличествоНераспределенного < ТекСтрока.Количество * ТекСтрока.Коэффициент Тогда
				ТекСтрока.Количество = КоличествоНераспределенного / ТекСтрока.Коэффициент;
			КонецЕсли;
			мсвСтрок = ТекСтрока.ЗаказПокупателя.Товары.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", ТекСтрока.Номенклатура, ТекСтрока.ХарактеристикаНоменклатуры));
			Если мсвСтрок.Количество() > 0 Тогда
				СтрокаЗаказа = мсвСтрок[0];
				ТекСтрока.Цена = СтрокаЗаказа.Цена;
				Если ТипЗнч(СтрокаЗаказа.Поставщик) = Тип("СправочникСсылка.ПрайсЛистыКонтрагентов") Тогда
					ТекСтрока.ПрайсЛист = СтрокаЗаказа.Поставщик;
					ТекСтрока.КлючСтрокиПоставщика = СтрокаЗаказа.КлючСтрокиПоставщика;
					ТекСтрока.СрокПоставкиВСтроке = СтрокаЗаказа.СрокПоставкиВСтроке;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="СрокПоставки" Тогда
		Возврат ОбработкаРеквизита("ОтображениеСрокаПоставки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ОтображениеСрокаПоставки" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если обЗначениеНеЗаполнено(СрокПоставки) Тогда
				ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="срок не определен";
				ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(0,0,128);
			Иначе
				
				Если Ссылка.Пустая() ИЛИ НЕ Ссылка.Проведен Тогда
					ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
				Иначе
					Запрос = Новый Запрос("ВЫБРАТЬ
					|	МАКСИМУМ(ЗаказыПоставщикам.Период) КАК Период,
					|	СУММА(ВЫБОР
					|		КОГДА ЗаказыПоставщикам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			ЗаказыПоставщикам.Заказано
					|		ИНАЧЕ
					|			-ЗаказыПоставщикам.Заказано
					|	КОНЕЦ) КАК Заказано
					|ИЗ
					|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
					|ГДЕ
					|	ЗаказыПоставщикам.ЗаказПоставщику = &Заказ");
					Запрос.УстановитьПараметр("Заказ", Ссылка);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Если Выборка.Период = NULL ИЛИ Выборка.Заказано = 0 Тогда
							ПросроченоДней = 0;
						Иначе
							ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
						КонецЕсли;
					Иначе
						ПросроченоДней = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если ПросроченоДней>0 Тогда
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="срок поставки истек " + СокрЛП(ПросроченоДней)+ " " + обПолучитьПредставлениеДня(ПросроченоДней) + " назад";
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(128,0,0);
				Иначе
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="";
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(0,128,0);
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	дкПечатьЭтикетокИЦенников(ЭтотОбъект);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ЗаказПоставщику"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЗаказПоставщику(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ЗаказПоставщику");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Контрагент);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"ИсполнительКонтрагент");
	Исполнитель.ИсполнительКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.ИсполнительКонтрагент),"","/ "+ Исполнитель.ИсполнительКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Исполнитель);
	Заказчик  = дкОтветственноеЛицо(ЭтотОбъект,"ЗаказчикОрганизация");
	Заказчик.ЗаказчикОрганизацияПредставление = ?(обЗначениеНеЗаполнено(Заказчик.ЗаказчикОрганизация),"","/ "+ Заказчик.ЗаказчикОрганизацияПредставление);
	ОбластьПодвал.Параметры.Заполнить(Заказчик);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЗаказПоставщику()

// ++ Alexku 28.08.2015

Функция ПолучитьДанныеСтрокиПрайсЛиста(СтрокаТЧ, ПрайсЛистПоУмолчанию=Неопределено)
	
	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("ПрайсЛист", СтрокаТЧ.ПрайсЛист);
	СтруктураСтроки.Вставить("КодВПрайсЛисте", "--");
	
	СтруктураСтроки.Вставить("ЕдиницаИзмерения", СтрокаТЧ.ЕдиницаИзмерения);
	СтруктураСтроки.Вставить("Код", СтрокаТЧ.Номенклатура.Артикул);
	СтруктураСтроки.Вставить("Производитель", СтрокаТЧ.Номенклатура.Производитель);
	СтруктураСтроки.Вставить("Наименование", СтрокаТЧ.Номенклатура.Наименование);
	
	СтруктураСтроки.Вставить("АртикулВПрайсЛисте", СтрокаТЧ.Номенклатура.Артикул);
	СтруктураСтроки.Вставить("ПроизводительВПрайсЛисте", СтрокаТЧ.Номенклатура.Производитель);
	СтруктураСтроки.Вставить("НаименованиеВПрайсЛисте", СтрокаТЧ.Номенклатура.Наименование);
	СтруктураСтроки.Вставить("СрокПоставкиВПрайсЛисте", "--");
	СтруктураСтроки.Вставить("ЦенаВПрайсЛисте", "--");
	СтруктураСтроки.Вставить("ВалютаВПрайсЛисте", "--");
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ПрайсЛист", СтрокаТЧ.ПрайсЛист);
	Запрос.УстановитьПараметр("ПрайсЛистПоУмолчанию", ПрайсЛистПоУмолчанию);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Если обЗначениеНеЗаполнено(СтрокаТЧ.КлючСтрокиПоставщика) Тогда
		
		Запрос.УстановитьПараметр("АртикулДляПоиска", СтрокаТЧ.Номенклатура.АртикулДляПоиска);
		Запрос.УстановитьПараметр("Производитель", СтрокаТЧ.Номенклатура.Производитель);
		Запрос.УстановитьПараметр("Номенклатура", СтрокаТЧ.Номенклатура);
		ЗапросТекст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Временный.ПрайсЛист,
		|	Временный.КлючСтрокиПоставщика
		|ПОМЕСТИТЬ втПрайсЛисты
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК Временный
		|ГДЕ
		|	"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), "Временный.ПрайсЛист.Владелец = &Контрагент", "Временный.ПрайсЛист = &ПрайсЛистПоУмолчанию"), "Временный.ПрайсЛист = &ПрайсЛист") + "
		|	И Временный.АртикулДляПоиска <> """"
		|	И Временный.АртикулДляПоиска = &АртикулДляПоиска
		|	И Временный.Производитель = &Производитель
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Временный.ПрайсЛист,
		|	Временный.КлючСтрокиПоставщика
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК Временный
		|		ПО ПравилаЗагрузки.ПрайсЛист = Временный.ПрайсЛист
		|			И ((ВЫРАЗИТЬ(ПравилаЗагрузки.ОбъектПравила КАК СТРОКА(32))) = Временный.КлючСтрокиПоставщика)
		|ГДЕ
		|	"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), "ПравилаЗагрузки.ПрайсЛист.Владелец = &Контрагент", "ПравилаЗагрузки.ПрайсЛист = &ПрайсЛистПоУмолчанию"), "ПравилаЗагрузки.ПрайсЛист = &ПрайсЛист") + "
		|	И ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки)
		|	И ПравилаЗагрузки.ИмяРеквизитаПрайсЛиста = ""Номенклатура""
		|	И (ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура)) = &Номенклатура
		|;
		|ВЫБРАТЬ
		|	Временный.АртикулВПрайсЛисте,
		|	Временный.ПроизводительВПрайсЛисте,
		|	Временный.Наименование КАК НаименованиеВПрайсЛисте,
		|	Временный.СрокПоставкиВПрайсЛисте,
		|	Временный.ОстальныеПоляВПрайсЛисте,
		|	Временный.Цена * (100 - ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0))))) / 100 КАК ЦенаВПрайсЛисте,
		|	Временный.Валюта КАК ВалютаВПрайсЛисте,
		|	Временный.ПрайсЛист
		|ИЗ
		|	втПрайсЛисты КАК ПрайсЛисты
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК Временный
		|		ПО ПрайсЛисты.ПрайсЛист = Временный.ПрайсЛист
		|			И ПрайсЛисты.КлючСтрокиПоставщика = Временный.КлючСтрокиПоставщика
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|			) КАК Скидки
		|		ПО (Не Скидки.Отменена)
		|			И Временный.ПрайсЛист = Скидки.ПрайсЛист
		|			И Временный.ТегПозиции = Скидки.ТегПозиции
		|			И Временный.Производитель = Скидки.Производитель
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|				И ТегПозиции <> """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиТегПозиции
		|		ПО (Не СкидкиТегПозиции.Отменена)
		|			И Временный.ПрайсЛист = СкидкиТегПозиции.ПрайсЛист
		|			И Временный.ТегПозиции = СкидкиТегПозиции.ТегПозиции
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|				И ТегПозиции = """"
		|				И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиПроизводитель
		|		ПО (Не СкидкиПроизводитель.Отменена)
		|			И Временный.ПрайсЛист = СкидкиПроизводитель.ПрайсЛист
		|			И Временный.Производитель = СкидкиПроизводитель.Производитель
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиБазовые
		|		ПО (Не СкидкиБазовые.Отменена)
		|			И Временный.ПрайсЛист = СкидкиБазовые.ПрайсЛист
		|";
		
	Иначе
		
		Запрос.УстановитьПараметр("КлючСтрокиПоставщика", СтрокаТЧ.КлючСтрокиПоставщика);
		ЗапросТекст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Временный.АртикулВПрайсЛисте,
		|	Временный.ПроизводительВПрайсЛисте,
		|	Временный.Наименование КАК НаименованиеВПрайсЛисте,
		|	Временный.СрокПоставкиВПрайсЛисте,
		|	Временный.ОстальныеПоляВПрайсЛисте,
		|	Временный.Цена * (100 - ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0))))) / 100 КАК ЦенаВПрайсЛисте,
		|	Временный.Валюта КАК ВалютаВПрайсЛисте,
		|	Временный.ПрайсЛист
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК Временный
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|			) КАК Скидки
		|		ПО (Не Скидки.Отменена)
		|			И Временный.ПрайсЛист = Скидки.ПрайсЛист
		|			И Временный.ТегПозиции = Скидки.ТегПозиции
		|			И Временный.Производитель = Скидки.Производитель
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|				И ТегПозиции <> """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиТегПозиции
		|		ПО (Не СкидкиТегПозиции.Отменена)
		|			И Временный.ПрайсЛист = СкидкиТегПозиции.ПрайсЛист
		|			И Временный.ТегПозиции = СкидкиТегПозиции.ТегПозиции
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|				И ТегПозиции = """"
		|				И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиПроизводитель
		|		ПО (Не СкидкиПроизводитель.Отменена)
		|			И Временный.ПрайсЛист = СкидкиПроизводитель.ПрайсЛист
		|			И Временный.Производитель = СкидкиПроизводитель.Производитель
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
		|			&Дата,
		|			ПрайсЛист"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), ?(обЗначениеНеЗаполнено(ПрайсЛистПоУмолчанию), ".Владелец = &Контрагент", " = &ПрайсЛистПоУмолчанию"), " = &ПрайсЛист") + "
		|				И ТегПозиции = """"
		|				И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиБазовые
		|		ПО (Не СкидкиБазовые.Отменена)
		|			И Временный.ПрайсЛист = СкидкиБазовые.ПрайсЛист
		|ГДЕ
		|	"+?(обЗначениеНеЗаполнено(СтрокаТЧ.ПрайсЛист), "Временный.ПрайсЛист.Владелец = &Контрагент", "Временный.ПрайсЛист = &ПрайсЛист") + "
		|	И Временный.КлючСтрокиПоставщика = &КлючСтрокиПоставщика";
		
	КонецЕсли;
	
	Запрос.Текст = ЗапросТекст;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 1 Тогда
		СтруктураСтроки.Вставить("КодВПрайсЛисте", "??");
		СтруктураСтроки.Вставить("СрокПоставкиВПрайсЛисте", "??");
		СтруктураСтроки.Вставить("ЦенаВПрайсЛисте", "??");
		СтруктураСтроки.Вставить("ВалютаВПрайсЛисте", "??");
		Возврат СтруктураСтроки;
	КонецЕсли;
	
	Если Выборка.Количество() = 0 Тогда
		Возврат СтруктураСтроки;
	КонецЕсли;
	
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(СтруктураСтроки, Выборка);
	СтрокаКлюч = Выборка.ПрайсЛист.СтруктураФайлаПрайсЛиста.Найти("КлючСтрокиПоставщика", "ИмяРеквизитаПрайсЛиста");
	Если СтрокаКлюч <> Неопределено Тогда
		ОстальныеПоля = Выборка.ОстальныеПоляВПрайсЛисте.Получить();
		ПолеКодВПрайсЛисте = ОстальныеПоля.НайтиПоЗначению(СтрокаКлюч.ИмяПоляФайла);
		Если ПолеКодВПрайсЛисте <> Неопределено Тогда
			СтруктураСтроки.КодВПрайсЛисте = ПолеКодВПрайсЛисте.Представление;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураСтроки;
	
КонецФункции

// Формирует печатную форму "ЗаказПоДаннымПоставщика"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьЗаказПоДаннымПоставщика(ТабДокумент) Экспорт
	
	ПрайсЛистПоУмолчанию = Неопределено;
	
	Если Товары.Найти(Справочники.ПрайсЛистыКонтрагентов.ПустаяСсылка(), "ПрайсЛист") <> Неопределено Тогда
		
		Ответ = Вопрос("Есть строки, в которых не указан прайс-лист поставщика.
			|Выберите вариант подстановки данных:
			|Да     - Автоматически найти данные в любом прайс-листе поставщика,
			|Нет    - Выбрать прайс-лист поставщика из списка,
			|Отмена - Отмена печати.", РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			ФормаВыбора = Справочники.ПрайсЛистыКонтрагентов.ПолучитьФормуВыбора();
			ФормаВыбора.ПараметрОтборПоВладельцу = Контрагент;
			ПрайсЛистПоУмолчанию = ФормаВыбора.ОткрытьМодально();
		КонецЕсли;
		
	КонецЕсли;
	
	Макет = ПолучитьМакет("ЗаказПоДаннымПоставщика");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеПоставщика = спПолучитьПредставление(Контрагент);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПокупателя = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		СтруктураСтрокиПрайсЛиста = ПолучитьДанныеСтрокиПрайсЛиста(СтрокаТабличнойЧасти, ПрайсЛистПоУмолчанию);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтрокиПрайсЛиста);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаВсего",ВалютаДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"ИсполнительКонтрагент");
	Исполнитель.ИсполнительКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.ИсполнительКонтрагент),"","/ "+ Исполнитель.ИсполнительКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Исполнитель);
	Заказчик  = дкОтветственноеЛицо(ЭтотОбъект,"ЗаказчикОрганизация");
	Заказчик.ЗаказчикОрганизацияПредставление = ?(обЗначениеНеЗаполнено(Заказчик.ЗаказчикОрганизация),"","/ "+ Заказчик.ЗаказчикОрганизацияПредставление);
	ОбластьПодвал.Параметры.Заполнить(Заказчик);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьЗаказПоставщику()

// -- Alexku 

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

// Процедура вызова отчета о состоянии заказов поставщикам с тбором по номенклатуре
Процедура СформироватьОтчетПоЗаказаннойНоменклатуре() Экспорт
	
	// Установм начальные параметры формирования
	Отчет = Отчеты.СостояниеЗаказовПоставщикам.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.СостояниеЗаказываемыхТоваров;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",             НачалоМесяца(Дата));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания",          КонецДня(Дата));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ВыводитьЗакрытыеЗаказы", Ложь);
	
	МассивНоменклатуры = Товары.ВыгрузитьКолонку("Номенклатура");
	СписокДляОтбора    = Новый СписокЗначений;
	СписокДляОтбора.ЗагрузитьЗначения(МассивНоменклатуры);
	
	ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Номенклатура", СписокДляОтбора, ВидСравненияКомпоновкиДанных.ВСписке);
	
	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
	
КонецПроцедуры

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") ИЛИ 
		 ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя")Тогда
		 
		 //SHMI нужно подставить контрагента по умолчанию
		 //для блокировки сообщения о несоответствии хоз.операции
		 Контрагент = обПраво("ОсновнойПоставщик",Права,,ЭтотОбъект);
		 //Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		 //	ЭтотОбъект.ОбработкаРеквизита("Контрагент",);
		 //КонецЕсли;
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Попытка
		Если ЭтотОбъект.Метаданные().ВводитсяНаОсновании.Содержит(Основание.Метаданные())
			// KRAA 18.06.10 Запретим очистку, если ввод на основании счета от поставщика
			И (ТипЗнч(Основание)<>Тип("ДокументСсылка.СчетОтПоставщика"))
			// KRAA --
			И (ТипЗнч(Основание)<>Тип("ДокументСсылка.ПоступлениеТоваров")) Тогда
			Контрагент				= Неопределено;
			ДоговорВзаиморасчетов	= Неопределено;
		КонецЕсли;
	Исключение
	КонецПопытки;

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказВнутренний") ИЛИ 
		ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя")Тогда
		
		 //SHMI заново установим контрагента, т.к. он сбросился в дкОбработкаЗаполнения
		 Контрагент = обПраво("ОсновнойПоставщик",Права,,ЭтотОбъект);
		 Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		 	ЭтотОбъект.ОбработкаРеквизита("Контрагент",);
		 КонецЕсли;
		
		//Для документов покупки
		ТипЦен = обПраво("ОсновнойТипЦенЗакупки",Права,,ЭтотОбъект);
		
		Товары.Очистить();
		РаспределениеЗаказа.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Заказ КАК Заказ,
		|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПокупателейОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	(ЗаказыПокупателейОстатки.ЗаказаноОстаток - 
		|	ЗаказыПокупателейОстатки.РезервОстаток - 
		|	ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0)) КАК Количество
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&КонецПериода, Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ЗаказыРаспределение.Остатки(&КонецПериода, ЗаказПокупателя = &Заказ) КАК ЗаказыРаспределениеОстатки
		|ПО
		|	ЗаказыПокупателейОстатки.Номенклатура               = ЗаказыРаспределениеОстатки.Номенклатура И 
		|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры = ЗаказыРаспределениеОстатки.ХарактеристикаНоменклатуры
		|ГДЕ
		|	(ЗаказыПокупателейОстатки.ЗаказаноОстаток - 
		|	ЗаказыПокупателейОстатки.РезервОстаток - 
		|	ЕСТЬNULL(ЗаказыРаспределениеОстатки.КоличествоОстаток, 0))>0
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры";
		
		Запрос.УстановитьПараметр("Заказ",        Основание);
		Запрос.УстановитьПараметр("КонецПериода", ?(Ссылка.Пустая(), ТекущаяДата(), Ссылка));
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрокаРаспределения = РаспределениеЗаказа.Добавить();
			НоваяСтрокаРаспределения.Номенклатура               = Выборка.Номенклатура;
			НоваяСтрокаРаспределения.Количество                 = Выборка.Количество;
			НоваяСтрокаРаспределения.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
			НоваяСтрокаРаспределения.ЗаказПокупателя            = Выборка.Заказ;
			НоваяСтрокаРаспределения.ЕдиницаИзмерения           = Выборка.ЕдиницаИзмерения;
			
			ОбработкаРеквизита("РаспределениеЗаказа.ЕдиницаИзмерения", НоваяСтрокаРаспределения);
			
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("ЕдиницаИзмерения", НоваяСтрокаРаспределения.ЕдиницаИзмерения);
			
			Если НЕ НоваяСтрокаРаспределения.Коэффициент = 0 Тогда
				НоваяСтрокаРаспределения.Количество = НоваяСтрокаРаспределения.Количество/НоваяСтрокаРаспределения.Коэффициент
			КонецЕсли;
			
			ОбработкаРеквизита("РаспределениеЗаказа.Номенклатура", НоваяСтрокаРаспределения,, ДопПараметры);
		КонецЦикла;
		ПерерасчетРаспределения();
		Для Каждого СтрокаТоваров Из Товары Цикл
			ОбработкаРеквизита("Товары.Номенклатура", СтрокаТоваров,,);
		КонецЦикла; 
		СуммаДокумента = Товары.Итог("СуммаВсего");
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.БюджетЗакупок") Тогда
		Товары.Очистить();
		Если Основание.ХозОперация=Справочники.ХозОперации.БюджетЗакупокПоНоменклатуре Тогда
			ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			Для Каждого ТекСтрока Из Основание.Товары Цикл
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = ТекСтрока.Номенклатура;
				ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
				Если НоваяСтрока.Коэффициент = 0 Тогда
					НоваяСтрока.Количество = ТекСтрока.Количество;
				Иначе
					НоваяСтрока.Количество = ТекСтрока.Количество/НоваяСтрока.Коэффициент
				КонецЕсли;
				НоваяСтрока.Цена = обПересчет(ТекСтрока.Цена, ВалютаУпр, КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
				ОбработкаРеквизита("Товары.Цена", НоваяСтрока);
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	 
	Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
		//Чтение значения для срока поставки
		СтруктураОтбора=Новый Структура("Организация,Объект",Контрагент,Перечисления.ВидыОбъектовСведений.СрокПоставки);
		СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(?(обЗначениеНеЗаполнено(Дата),ТекущаяДата(),Дата),СтруктураОтбора);
		СрокПоставкиДней=?(обЗначениеНеЗаполнено(СтруктураСведений.Значение),обПраво("СрокПоставкиПокупателюПоУмолчанию",Права,,ЭтотОбъект),СтруктураСведений.Значение);
		СрокПоставки=НачалоДня(?(обЗначениеНеЗаполнено(Дата),ТекущаяДата(),Дата))+СрокПоставкиДней*60*60*24;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	Если Товары.Количество() = 0 Тогда
		// заполним по копируемому документу
		Товары.Загрузить(ОбъектКопирования.Товары.Выгрузить());
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ЗаказБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ТаблицаЗаказов = РаспределениеЗаказа.Выгрузить(, "ЗаказПокупателя");
		ТаблицаЗаказов.Свернуть("ЗаказПокупателя");
		ТаблицаДолгов = орПолучитьТаблицуДолговПоПредоплатам(ЭтотОбъект, ТаблицаЗаказов.ВыгрузитьКолонку("ЗаказПокупателя"));
		
		Если ТаблицаДолгов.Количество()>0 Тогда
			Для Каждого ТекСтрока Из РаспределениеЗаказа Цикл
				СтрокаПоиска = ТаблицаДолгов.Найти(ТекСтрока.ЗаказПокупателя, "Заказ");
				Если НЕ СтрокаПоиска = Неопределено Тогда
					Сообщить("Строка: " + ТекСтрока.НомерСтроки + ", Заказ: """+СокрЛП(ТекСтрока.ЗаказПокупателя)+""" нельзя распределить. Долг по предоплате составляет: "+Формат(СтрокаПоиска.Долг,"ЧЦ=15; ЧДЦ=2")+" "+СокрЛП(СтрокаПоиска.Валюта)+".", СтатусСообщения.Важное);
					Отказ = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// Заказы
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Заказы.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПоставщикам.ЗаказПоставщику КАК Заказ
		|	ИЗ
		|		РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
		|	ГДЕ
		|		ЗаказыПоставщикам.Регистратор = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказыРаспределение.ЗаказПокупателя
		|	ИЗ
		|		РегистрНакопления.ЗаказыРаспределение КАК ЗаказыРаспределение
		|	ГДЕ
		|		ЗаказыРаспределение.Регистратор = &Ссылка) КАК Заказы";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ЗаказБыл", Результат.Выгрузить().ВыгрузитьКолонку("Заказ"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	// Формирование заказа поставщику
	НаборЗаписейЗаказыПоставщикам=Движения.ЗаказыПоставщикам;
	НаборЗаписейЗаказыПоставщикам.РежимПроведения=Режим;
	НаборЗаписейЗаказыПоставщикам.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейЗаказыПоставщикам.РезультатЗапросаПоТоварам=Неопределено;
	НаборЗаписейЗаказыПоставщикам.ЗаказПоставщику=Ссылка;
	НаборЗаписейЗаказыПоставщикам.Контрагент=Контрагент;
	Отказ=НЕ НаборЗаписейЗаказыПоставщикам.Приход() ИЛИ Отказ;
	Если НЕ Отказ Тогда
		НаборЗаписейЗаказыПоставщикам.Записать();
	КонецЕсли; 
	
	// зафиксируем распределение заказов покупателей на заказе поставщику
	НаборЗаписейЗаказыРаспределение=Движения.ЗаказыРаспределение;
	НаборЗаписейЗаказыРаспределение.РежимПроведения=Режим;
	НаборЗаписейЗаказыРаспределение.ДокументОбъект=ЭтотОбъект;
	Запрос=Новый Запрос("
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокументТовары.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ДокументТовары.Количество*ДокументТовары.Коэффициент КАК Количество
		|ИЗ
		|	Документ.ЗаказПоставщику.РаспределениеЗаказа КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	НаборЗаписейЗаказыРаспределение.РезультатЗапросаПоТоварам=Запрос.Выполнить();
	НаборЗаписейЗаказыРаспределение.ЗаказПокупателя=Неопределено; //Заказ находится в табличной части
	НаборЗаписейЗаказыРаспределение.ЗаказПоставщика=Ссылка;
	Отказ=НЕ НаборЗаписейЗаказыРаспределение.Приход() ИЛИ Отказ;
	
	// двигаем границу последовательности заказов
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ЗаказБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			ТаблицаЗаказов = Движения.ЗаказыРаспределение.Выгрузить();
			ТаблицаЗаказов.Свернуть("ЗаказПокупателя","");
			НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказы.СоздатьНаборЗаписей();
			НаборЗаписейГраницыЗаказы.Заказ = ТаблицаЗаказов.ВыгрузитьКолонку("ЗаказПокупателя");
			НаборЗаписейГраницыЗаказы.Заказ.Добавить(Ссылка);
			НаборЗаписейГраницыЗаказы.МоментВремени = Дата;
		Иначе
			НаборЗаписейГраницыЗаказы.Заказ = ДополнительныеСвойства.ЗаказБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.ЗаказБыл = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
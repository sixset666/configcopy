// Модуль документа Вывод из оборота кодов маркировки


////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Функция СостояниеКодаМаркировки()
	
	Результат = Неопределено;
	
	Если Статус = Перечисления.СтатусыОтгрузки.АннулированаОтгрузка Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВведенВОборот;
	ИначеЕсли ПричинаВыводаИзОборота = Перечисления.ВидыОтгрузкиТоваров.Продажа
		И ЗначениеЗаполнено(ДатаВыводаИзОборота) Тогда
		Результат = Перечисления.СостоянияКодовМаркировки.ВыведенИзОборота;
	Иначе
		Результат = Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверкаОбязательности()
	
	Возврат Статус = Перечисления.СтатусыОтгрузки.Выполнен
		ИЛИ Статус = Перечисления.СтатусыОтгрузки.Отправлен;
	
КонецФункции

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ВидТоварооборота", 1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Статус",1);
	ОбязательныеРеквизиты.Вставить("ДатаПервичногоДокумента",1);
	ОбязательныеРеквизиты.Вставить("НомерПервичногоДокумента",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ДатаОтгрузкиТоваров",1);
	
	Если ВыводИзОборота Тогда
		ОбязательныеРеквизиты.Вставить("ДатаВыводаИзОборота",1);
		ОбязательныеРеквизиты.Вставить("ПричинаВыводаИзОборота",1);
	КонецЕсли;
	
	Если ПричинаВыводаИзОборота = Перечисления.ПричиныОтгрузкиТоваров.ПриобретениеГосПредприятием Тогда
		ОбязательныеРеквизиты.Вставить("ИдентификаторГосКонтракта",1);
	КонецЕсли;
	
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	ОбязательныеРеквизиты.Вставить("ТоварнаяГруппа",1);
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проверим количество маркировок и количество товара в строке
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки);
	
	Если ЗначениеЗаполнено(ДатаВыводаИзОборота)
		И ДатаВыводаИзОборота < ДатаПервичногоДокумента Тогда
		Результат = Ложь;
		дкДобавитьОшибку(
			Ошибки,
			"Дата вывода из оборота не может быть меньше даты первичного документа");
	КонецЕсли;
	
	Результат = Результат И дкПроверитьСоответствиеНоменклатурыТоварнойГруппе(ЭтотОбъект,,, Ошибки);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Вовращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ОтгрузкаТоваровКодовМаркировки", "Отгрузка товаров", Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	Если Имя = "Товары.Номенклатура" Тогда
		
		// Проверим номенклатуру
		Если ЗначениеЗаполнено(ТекСтрока.Номенклатура)
			И НЕ ТекСтрока.Номенклатура.ТипНоменклатуры.ВедетсяМаркировка Тогда
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру по которой не ведется учет по маркировке",5);
			#КонецЕсли
			ТекСтрока.Номенклатура = Неопределено;
		КонецЕсли;
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "Товары.Количество"
		ИЛИ Имя = "Товары.Цена" Тогда
		
		ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество;
		ТекСтрока.СуммаНДС = Окр(ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка / (100 + ТекСтрока.СтавкаНДС.Ставка), 2);
		
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.Сумма" Тогда
		
		ТекСтрока.Цена = Окр(ТекСтрока.Сумма / ?(ТекСтрока.Количество = 0, 1, ТекСтрока.Количество), 2);
		ТекСтрока.СуммаНДС = Окр(ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка / (100 + ТекСтрока.СтавкаНДС.Ставка), 2);
		
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.СтавкаНДС" Тогда
		
		ТекСтрока.СуммаНДС = Окр(ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка / (100 + ТекСтрока.СтавкаНДС.Ставка), 2);
		
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.СуммаНДС" Тогда
		
		Возврат Истина;
		
	ИначеЕсли Имя="ДокументОснование" Тогда
		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда 
			Если Вопрос("Заполнить по документу-основания?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да Тогда
				ЭтотОбъект.Заполнить(ДокументОснование);
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
		Возврат Рез;
		
	КонецЕсли;
	
	Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходмое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события установки нового номера
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Обработчик события заполнения
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Статус) Тогда
		Статус = Перечисления.СтатусыОтгрузки.Новый;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаОтгрузкиТоваров) Тогда
		ДатаОтгрузкиТоваров = Дата;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) Тогда
		
		КодыМаркировки.Очистить();
		Товары.Очистить();
		
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
			
			Если Основание.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда
				ДатаДокумента = Основание.ДатаЗакрытия;
			ИначеЕсли ЗначениеЗаполнено(Основание.ДатаОкончания) Тогда
				ДатаДокумента = Основание.ДатаОкончания;
			Иначе
				ДатаДокумента = Основание.Дата;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
				Контрагент = Основание.Заказчик;
			КонецЕсли;
			
			ДатаОтгрузкиТоваров = ДатаДокумента;
			ДатаПервичногоДокумента = ДатаДокумента;
			НомерПервичногоДокумента = Основание.Номер;
			
			// Запросом получим коды маркировок
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	СостоянияКодовМаркировки.Номенклатура КАК Номенклатура,
			|	СостоянияКодовМаркировки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	СостоянияКодовМаркировки.КодМаркировки КАК КодМаркировки,
			|	1 КАК Количество
			|ИЗ
			|	РегистрСведений.СостоянияКодовМаркировки КАК СостоянияКодовМаркировки
			|ГДЕ
			|	СостоянияКодовМаркировки.ДокументОснование = &Основание
			|	И СостоянияКодовМаркировки.Состояние В(&Состояние)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ЗаказНарядТовары.Номенклатура КАК Номенклатура,
			|	ЗаказНарядТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	СУММА(ВЫРАЗИТЬ(ЗаказНарядТовары.Количество / ЗаказНарядТовары.Коэффициент КАК ЧИСЛО(15,3))) КАК Количество,
			|	СУММА(ЗаказНарядТовары.СуммаВсего) КАК СуммаВсего,
			|	СУММА(ЗаказНарядТовары.СуммаНДС) КАК СуммаНДС,
			|	ЗаказНарядТовары.СтавкаНДС КАК СтавкаНДС
			|ИЗ
			|	Документ.ЗаказНаряд.Товары КАК ЗаказНарядТовары
			|ГДЕ
			|	ЗаказНарядТовары.Ссылка = &Основание
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗаказНарядТовары.Номенклатура,
			|	ЗаказНарядТовары.ХарактеристикаНоменклатуры,
			|	ЗаказНарядТовары.СтавкаНДС";
			Запрос.УстановитьПараметр("Основание", Основание);
			
			СписокСостояний = Новый Массив;
			СписокСостояний.Добавить(Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаРозничнаяПродажа);
			СписокСостояний.Добавить(
			Перечисления.СостоянияКодовМаркировки.ВыведенИзОборотаИспользованДляСобственныхНуждПредприятия
			);
			СписокСостояний.Добавить(Перечисления.СостоянияКодовМаркировки.ПередачаДругомуСобственнику);
			
			Запрос.УстановитьПараметр("Состояние", СписокСостояний);
			
			ПакетЗапроса = Запрос.ВыполнитьПакет();
			
			ТаблицаМаркировки = ПакетЗапроса[0].Выгрузить();
			ТаблицаНоменклатуры = ПакетЗапроса[1].Выгрузить();
			
			КодыМаркировки.Очистить();
			Товары.Очистить();
			
			// Заполним ТЧ
			СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры");
			Для Каждого ТекущаяСтрока Из ТаблицаНоменклатуры Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
				СтрокиКМ = ТаблицаМаркировки.НайтиСтроки(СтруктураПоиска);
				
				// По данному товару нет КМ
				Если СтрокиКМ.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
				
				НоваяСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
				ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
				ЗаполненныеКМ = Новый Массив;
				КоличествоКМ = 1;
				КоличествоДоступныхКМ = СтрокиКМ.Количество();
				
				Пока КоличествоКМ <= НоваяСтрока.Количество Цикл
					
					НоваяСтрокаКМ = КодыМаркировки.Добавить();
					НоваяСтрокаКМ.ИдентификаторТовара = НоваяСтрока.ИдентификаторТовара;
					НоваяСтрокаКМ.КодМаркировки = СтрокиКМ[КоличествоКМ - 1].КодМаркировки;
					ЗаполненныеКМ.Добавить(СтрокиКМ[КоличествоКМ - 1]);
					
					Если КоличествоДоступныхКМ = КоличествоКМ Тогда
						Прервать;
					КонецЕсли;
					
					КоличествоКМ = КоличествоКМ + 1;
					
				КонецЦикла;
				
				// Удалим добавленные строки
				Для Каждого ТекущаяСтрокаКМ Из ЗаполненныеКМ Цикл
					ТаблицаМаркировки.Удалить(ТекущаяСтрокаКМ);
				КонецЦикла;
				
				НоваяСтрока.СтавкаНДС = ТекущаяСтрока.СтавкаНДС;
				Если КоличествоКМ = НоваяСтрока.Количество Тогда
					НоваяСтрока.Сумма = ТекущаяСтрока.СуммаВсего;
					НоваяСтрока.СуммаНДС = ТекущаяСтрока.СуммаНДС;
				Иначе
					НоваяСтрока.Количество = КоличествоКМ;
					НоваяСтрока.Сумма = Окр(ТекущаяСтрока.СуммаВсего / ТекущаяСтрока.Количество * НоваяСтрока.Количество, 2);
					НоваяСтрока.СуммаНДС = Окр(ТекущаяСтрока.СуммаНДС  / ТекущаяСтрока.Количество * НоваяСтрока.Количество, 2);
				КонецЕсли;
				
				НоваяСтрока.Цена = Окр(НоваяСтрока.Сумма / ?(НоваяСтрока.Количество = 0, 1, НоваяСтрока.Количество), 2);
				
			КонецЦикла;
			
		Иначе
			
			ДатаОтгрузкиТоваров = Основание.Дата;
			ДатаПервичногоДокумента = Основание.Дата;
			НомерПервичногоДокумента = Основание.Номер;
			
			// Пройдемся по ТЧ
			СтруктураПоиска = Новый Структура("ИдентификаторТовара");
			
			Для Каждого ТекущаяСтрока Из Основание.Товары Цикл
				
				СтруктураПоиска.ИдентификаторТовара = ТекущаяСтрока.ИдентификаторТовара;
				НайденныеСтроки = Основание.КодыМаркировки.НайтиСтроки(СтруктураПоиска);
				
				КоличествоКодов = НайденныеСтроки.Количество();
				
				// У данной номенклатуры нет кодов маркировки
				// не будем ее выгружать
				Если КоличествоКодов = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
				ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
				НоваяСтрока.Сумма = ТекущаяСтрока.СуммаВсего;
				Коэффициент = ?(НоваяСтрока.Коэффициент = 0, 1, НоваяСтрока.Коэффициент);
				Если КоличествоКодов <> НоваяСтрока.Количество * Коэффициент Тогда
					НоваяСтрока.Количество = Окр(КоличествоКодов / Коэффициент, 3);
				КонецЕсли;
				НоваяСтрока.Цена = Окр(НоваяСтрока.Сумма / ?(НоваяСтрока.Количество = 0, 1, НоваяСтрока.Количество), 2);
				НоваяСтрока.СуммаНДС = ТекущаяСтрока.СуммаНДС;
				НоваяСтрока.СтавкаНДС = ТекущаяСтрока.СтавкаНДС;
				
			КонецЦикла;
			
			// Загрузим все коды маркировоки
			КодыМаркировки.Загрузить(Основание.КодыМаркировки.Выгрузить());
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТипЦен = ОбПраво("ОсновнойТипЦенПродажи");
	Если НЕ Основание = Неопределено Тогда
		ТипЦен = Основание.ТипЦен;
	КонецЕсли;
	
	Для Каждого СтрокаТовар Из Товары Цикл
		
		// Заполним доп. поля для товарной строки
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
	КонецЦикла;
	
	Если обЗначениеНеЗаполнено(ТоварнаяГруппа) Тогда
		ТоварнаяГруппа = дкТоварнаяГруппа(Товары.ВыгрузитьКолонку("Номенклатура"));
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ВыводИзОборота = Истина;
		ВидТоварооборота = Перечисления.ВидыОтгрузкиТоваров.Продажа;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Обработчик события при копировании
//
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Статус = Перечисления.СтатусыОтгрузки.Новый;
	КодыМаркировки.Очистить();
	ВыводИзОборота = Истина;
	ВидТоварооборота = Перечисления.ВидыОтгрузкиТоваров.Продажа;
	
КонецПроцедуры // ПриКопировании()

// Обработчик события перед записью
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Обработчик события при записи
//
Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		орЖурналСостояний(Ссылка, Статус);
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

// Обработчик события перед удалением
//
Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Обработчик события удаления проведения
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик события проведения
//
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	АннулированаОтгрузка = (Статус = Перечисления.СтатусыОтгрузки.АннулированаОтгрузка);
	Если НЕ Отказ И (Статус = Перечисления.СтатусыОтгрузки.Выполнен
		ИЛИ АннулированаОтгрузка) Тогда
		
		// Отменим записи состояний документа
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
		
		ТаблицаМаркировки = НаборЗаписейСостоянияКодовМаркировки.ТаблицаКодовМаркировки();
		
		ТаблицаМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
		ТаблицаМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
		
		ОбъектШК = Обработки.ШтрихКоды.Создать();
		
		// Разберем маркировку на состовляющие для поиска
		Для Каждого ТекущийКодМаркировки Из ТаблицаМаркировки Цикл
			
			Если НЕ ЗначениеЗаполнено(ТекущийКодМаркировки.КодМаркировки) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущийКодМаркировки.КодМаркировки);
			
			Если НЕ СтруктураМаркировки.Разобран Тогда
				Продолжить;
			КонецЕсли;
			
			ТекущийКодМаркировки.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
			ТекущийКодМаркировки.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
			
		КонецЦикла;
		
		ТекущиеСтатусыМаркировки =
			РегистрыСведений.СостоянияКодовМаркировки.ТекущиеСтатусыКодовМаркировки(ТаблицаМаркировки, МоментВремени());
		
		ВыведенныеТовары = Новый Массив;
		СостоянияВОбороте = Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки();
		
		Для Каждого ТекущаяСтрока Из ТекущиеСтатусыМаркировки Цикл
			Если (НЕ АннулированаОтгрузка И СостоянияВОбороте.Найти(ТекущаяСтрока.Состояние) = Неопределено)
				ИЛИ (АннулированаОтгрузка И СостоянияВОбороте.Найти(ТекущаяСтрока.Состояние) <> Неопределено) Тогда
				ВыведенныеТовары.Добавить(ТекущаяСтрока);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекущаяСтрока Из ВыведенныеТовары Цикл
			ТекущиеСтатусыМаркировки.Удалить(ТекущаяСтрока);
		КонецЦикла;
		
		// Изменим состояние маркировки, которые ранее не были выведены
		НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
		НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
		НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
		НаборЗаписейСостоянияКодовМаркировки.РезультатЗапросаПоКодамМаркировки = ТекущиеСтатусыМаркировки;
		НаборЗаписейСостоянияКодовМаркировки.Состояние = СостояниеКодаМаркировки();
		НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
		Если НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

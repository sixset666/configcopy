// Модуль документа ТестДрайв

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей

Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ФорматПредставленияГодаВыпускаАвтомобиля Экспорт; //Переменная с форматом отображения года выпуска автомобиля

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="" Тогда
		Возврат Истина;
		
	ИначеЕсли Имя="Дата" Тогда		
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ЭтотОбъект.ОбработкаРеквизита("РассчитатьСкидкиРабот", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
		Возврат Рез;
	
	ИначеЕсли Имя="ХозОперация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		
		Если ДопПараметры = Неопределено ИЛИ ТипЗнч(ДопПараметры) <> Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли; 
		
		ДопПараметры.Вставить("СписокЗапрещенныхВидовОтношений",Новый СписокЗначений);
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
		#Если Клиент Тогда
		НеОбновлятьТелефон = ДопПараметры.Свойство("НеОбновлятьТелефон");
		Если НЕ НеОбновлятьТелефон ИЛИ ПредставлениеТелефона = "" Тогда	
			КодРегиона = "";
			КодГорода = "";
			НомерТелефона = "";
			ВнутреннийНомер = "";
			ПредставлениеТелефона = "";
			ВидТелефона = Неопределено;
			Если ЗначениеЗаполнено(Контрагент) Тогда
				Телефоны = киПолучитьСписокТелефоновОбъекта(Контрагент);
				Если Телефоны<>Неопределено И Телефоны.Количество()>0 Тогда
					ЗаполнитьЗначенияСвойств(ЭтотОбъект,Телефоны[0].Значение);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
		#КонецЕсли
	
		Возврат Рез;
		
	ИначеЕсли Имя="Автомобиль" Тогда
		Рез=Истина;
		
		Если ЗначениеЗаполнено(Автомобиль) И ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
			СписокАвтоДляТестДрайва = Документы.ТестДрайв.ПолучитьАвтомобилиДляТестДрайва();	
			Если СписокАвтоДляТестДрайва.НайтиПоЗначению(Автомобиль) = Неопределено Тогда
				Автомобиль = Справочники.Автомобили.ПустаяСсылка();
				Сообщить("Выбранный автомобиль не введен в эксплуатацию для тест-драйва!");
			КонецЕсли;
		КонецЕсли;
		
		//подставляется актуальный пробег автомобиля
		Если ЗначениеЗаполнено(Автомобиль) И ТипЗнч(Автомобиль)=Тип("СправочникСсылка.Автомобили") Тогда
			Пробег = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег, Дата);
			ПробегНаНачало=Пробег;
			ПробегНаКонец=0;
		КонецЕсли;
		
		Возврат Рез;

	ИначеЕсли Имя = "СоздатьНапоминание" Тогда
		ДатаНапоминания=Неопределено;
		Если НЕ обЗначениеНеЗаполнено(ДатаОкончания) Тогда
			ДатаНапоминания=ДатаОкончания;
		КонецЕсли;
		Если ТипЗнч(ДопПараметры)<>Тип("Структура") Тогда
			ДопПараметры=Новый Структура;
		КонецЕсли; 
		Если ДатаНапоминания<>Неопределено Тогда
			ДопПараметры.Вставить("ДатаНачала",ДатаНапоминания);
			ДопПараметры.Вставить("РеальнаяДатаОповещения",ДатаНапоминания);
		КонецЕсли;
		Получатели=Новый СписокЗначений;
		Получатели.Добавить(Автор);
		ДопПараметры.Вставить("СписокПользователей", Получатели); 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

//Формирует строку с информацией об автомобиле для отображения на форме
Функция СформироватьИнформациюОбАвтомобиле(Автомобиль,ДатаДок=Неопределено) Экспорт
	
	Текст = "";
	
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		
		Если ЗначениеЗаполнено(Автомобиль.Модель) Тогда
			Текст = СокрЛП(Автомобиль.Модель.Наименование) + ";";
		КонецЕсли;
		
		ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,ДатаДок);
 		Если ЗначениеЗаполнено(ГосНомер) Тогда
			Текст = Текст + " " + ГосНомер + ";";
		КонецЕсли;
		
		Пробег = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег,ДатаДок);
 		Если ЗначениеЗаполнено(Пробег) Тогда
			Текст = Текст + " пробег " + Пробег + ";";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Автомобиль.ВариантКомплектации) Тогда
			Текст = Текст + " комплектация: " + СокрЛП(Автомобиль.ВариантКомплектации) + ";";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Автомобиль.Цвет) Тогда
			Текст = Текст + " цвет: " + СокрЛП(Автомобиль.Цвет) + ";";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Возвращает таблицу документов, пересекающихся по времени с текущим документом
Функция ПолучитьПересечениеДокументов()  Экспорт
	
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ГрафикРаботыРесурсов.Объект,
		               |	ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.НачалоРабочегоВремени)) КАК НачалоРабочегоВремени,
		               |	ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.КонецРабочегоВремени)) КАК КонецРабочегоВремени
		               |ПОМЕСТИТЬ ГрафикиРаботРесурсов
		               |ИЗ
		               |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
		               |ГДЕ
		               |	ГрафикРаботыРесурсов.Ресурс1 = &Ресурс1
		               |	И ГрафикРаботыРесурсов.Дата >= &ДеньНачала
		               |	И ГрафикРаботыРесурсов.Дата <= &ДеньОкончания
		               |	И ГрафикРаботыРесурсов.Объект <> &ЭтаСсылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ГрафикРаботыРесурсов.Объект КАК Объект,
		               |	ГрафикРаботыРесурсов.НачалоРабочегоВремени,
		               |	ГрафикРаботыРесурсов.КонецРабочегоВремени
		               |ИЗ
		               |	ГрафикиРаботРесурсов КАК ГрафикРаботыРесурсов
		               |ГДЕ
		               |	(&ДатаНачала >= ГрафикРаботыРесурсов.НачалоРабочегоВремени
		               |				И &ДатаНачала < ГрафикРаботыРесурсов.КонецРабочегоВремени
		               |			ИЛИ &ДатаОкончания > ГрафикРаботыРесурсов.НачалоРабочегоВремени
		               |				И &ДатаОкончания <= ГрафикРаботыРесурсов.КонецРабочегоВремени
		               |			ИЛИ &ДатаНачала <= ГрафикРаботыРесурсов.НачалоРабочегоВремени
		               |				И &ДатаОкончания >= ГрафикРаботыРесурсов.КонецРабочегоВремени)";					   
				   
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ДеньНачала", НачалоДня(ДатаНачала));
		Запрос.УстановитьПараметр("ДеньОкончания", НачалоДня(ДатаОкончания));
		Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
		Запрос.УстановитьПараметр("Ресурс1", Автомобиль);
		//по сотрудникам пересечение не контроллируется
		//Запрос.УстановитьПараметр("Ресурс2", Менеджер); 
		Запрос.УстановитьПараметр("ЭтаСсылка", Ссылка);
		Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции //ПолучитьПересечениеДокументов()

Функция ПроверитьСостояние(Отказ=Ложь) Экспорт
	Ошибки="";
	ПредАнкета = Неопределено;
	ИтогАнкета = Неопределено;
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		Анкеты = Документы.ТестДрайв.ПолучитьАнкетыТестДрайва(Дата,Автомобиль.Модель);	
		Если ТипЗнч(Анкеты)=Тип("Структура") Тогда
			ПредАнкета = Анкеты.ПредАнкета;
			ИтогАнкета = Анкеты.ИтогАнкета;
		КонецЕсли;
	КонецЕсли;
	
	ПредОпрос = Неопределено;
	ИтогОпрос = Неопределено;
	Если НЕ ЭтоНовый() Тогда
		Опросы = Документы.ТестДрайв.ПолучитьОпросыТестДрайва(ЭтотОбъект.Ссылка,ПредАнкета,ИтогАнкета);
		Если ТипЗнч(Опросы)=Тип("Структура") Тогда
			ПредОпрос = Опросы.ПредОпрос;
			ИтогОпрос = Опросы.ИтогОпрос;
		КонецЕсли;
	КонецЕсли;
	
	Если Состояние=Перечисления.СостоянияСобытий.Выполняется Тогда
		Если ЗначениеЗаполнено(ПредАнкета) И НЕ ЗначениеЗаполнено(ПредОпрос) Тогда
			дкДобавитьОшибку(Ошибки,"Для перевода документа в состояние ""Выполнение"" необходимо провести предварительное анкетирование!");
			Отказ=Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Состояние=Перечисления.СостоянияСобытий.Завершено Тогда
		
		Если ЗначениеЗаполнено(ПредАнкета) И НЕ ЗначениеЗаполнено(ПредОпрос) Тогда
			дкДобавитьОшибку(Ошибки,"Для перевода документа в состояние ""Завершено"" необходимо провести предварительное анкетирование!");
			Отказ=Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(ИтогАнкета) И НЕ ЗначениеЗаполнено(ИтогОпрос) Тогда
			дкДобавитьОшибку(Ошибки,"Для перевода документа в состояние ""Завершено"" необходимо провести итоговое анкетирование!");
			Отказ=Истина;
		КонецЕсли;
	КонецЕсли;
	СтруктураОшибок = Новый Структура;
	СтруктураОшибок.Вставить("Ошибки", Ошибки);
	СтруктураОшибок.Вставить("Отказ", Отказ);
	Возврат СтруктураОшибок;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("Автомобиль",1);
	ОбязательныеРеквизиты.Вставить("Маршрут",1);
	ОбязательныеРеквизиты.Вставить("Менеджер",1);
	ОбязательныеРеквизиты.Вставить("Состояние",1);
	ОбязательныеРеквизиты.Вставить("ДатаНачала",1);
	ОбязательныеРеквизиты.Вставить("ДатаОкончания",1);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
//	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
//	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
//	// или если установлен способ контроля корректности
//	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
//	// дополнительно осуществляется проверка соответствия подразделений.
//	Если Результат Тогда
//		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
//		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
//		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
//		
//		Если БалансВедетсяПоОрганизации Или Не КонтрольОтсутствует Тогда
//			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
//		
//			КонтролируемыеРеквизиты = Новый Структура();
//			
//			КонтролируемыеРеквизитыШапки = Новый Структура();
//			КонтролируемыеРеквизитыШапки.Вставить("Цех",КонтрольПоПодразделению);
//			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов",КонтрольПоПодразделению);
//			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
//			
//			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
//		КонецЕсли;	
//	КонецЕсли;
//	
	Возврат Результат;
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ТестДрайв","Тест-драйв",Истина);
	Возврат СписокХозОпераций;
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Договор проведения тест-драйва"
// Возвращает сформированный табличный документ:
Функция ПечатьДоговор(ТабДокумент) Экспорт 
	
	Макет = ПолучитьМакет("Договор");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Номер = дкПолучитьНомерДляПечати(ЭтотОбъект,Истина);
	ОбластьМакета.Параметры.Город = киПолучитьГород(ЭтотОбъект.ПодразделениеКомпании,Справочники.ВидыКонтактнойИнформации.АдресФактический);
	ОбластьМакета.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	
	//шапка - данные организации
	ОбластьМакета.Параметры.Организация = Организация.НаименованиеПолное;
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	Если ЗначениеЗаполнено(Руководитель) И ТипЗнч(Руководитель)=Тип("Структура") Тогда
		Должность = ", в лице";
		Если ЗначениеЗаполнено(Руководитель.РуководительДолжность) Тогда
			Должность = Должность + " " + СокрЛП(НРег(Руководитель.РуководительДолжность));
		КонецЕсли;
		ОбластьМакета.Параметры.ОрганизацияДолжность = Должность;
		
		ОрганизацияЛицо = " " + Руководитель.РуководительПредставление;
		ДоверенностьРуководителя = спПолучитьПодтверждающийДокументОбъектаПоВиду(Руководитель.Руководитель,Перечисления.ВидыДокументов.Доверенность);
		Если ЗначениеЗаполнено(ДоверенностьРуководителя) Тогда
			ОрганизацияЛицо = ОрганизацияЛицо + ", действующего  на основании " + ДоверенностьРуководителя;
		КонецЕсли;
		ОбластьМакета.Параметры.ОрганизацияЛицо = ОрганизацияЛицо;
	КонецЕсли;
	
    //шапка - данные заказчика
	ОбластьМакета.Параметры.Покупатель = Контрагент.НаименованиеПолное;
	Паспорт = спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Паспорт);
	Если ЗначениеЗаполнено(Паспорт) Тогда
		ПаспортныеДанные = "паспорт серии " + Паспорт.Серия + " № "+ Паспорт.Номер + " выдан " + Паспорт.КемВыдан + " " + Формат(Паспорт.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г.";
		ОбластьМакета.Параметры.ПаспортныеДанные = " (" + ПаспортныеДанные + ")";
	КонецЕсли;
	
	//данные автомобиля
	ФорматПредставленияГодаВыпускаАвтомобиля=орПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Права);
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		ОбластьМакета.Параметры.Модель = Автомобиль.Модель;  
		ОбластьМакета.Параметры.ГодВыпуска = Формат(Автомобиль.ГодВыпуска,ФорматПредставленияГодаВыпускаАвтомобиля);
		ОбластьМакета.Параметры.ГосНомер = 	Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,Дата);
		ОбластьМакета.Параметры.Цвет = Автомобиль.Цвет;
		ОбластьМакета.Параметры.НомерДвигателя = Автомобиль.НомерДвигателя;
		ОбластьМакета.Параметры.VIN = Автомобиль.VIN;
	КонецЕсли;
	
	//подвал - данные организации
	СтруктураПредставления1=Новый Структура(
	"Наименование,ИНН,КПП,АдресЮридический,ТелефонРабочий,БанковскийСчет,Банк,БИК,КоррСчет",
	"","ИНН ","КПП ","","тел.: ","р/с ","в банке ","БИК ","к/с "
	);
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДляПечати = Истина;
	ОбластьМакета.Параметры.ОписаниеОрганизации = спПолучитьПредставление(ПодразделениеКомпании,СтруктураПредставления1,ДополнительныеПараметры);

	//подвал - данные заказчика
	ОписаниеЗаказчика = спПолучитьНаименование(Контрагент);
	ЗаказчикПочтовыйАдрес = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	Если ЗначениеЗаполнено(ПаспортныеДанные) Тогда
		ОписаниеЗаказчика = ОписаниеЗаказчика + "," + Символы.ПС + ПаспортныеДанные;
	КонецЕсли;
	Если ЗначениеЗаполнено(ЗаказчикПочтовыйАдрес) Тогда
		ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Адрес: " + ЗаказчикПочтовыйАдрес;
	КонецЕсли;
	ЗаказчикТелефоны = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	Если ЗначениеЗаполнено(ЗаказчикТелефоны) Тогда
		ОписаниеЗаказчика = ОписаниеЗаказчика + Символы.ПС + "Телефон: " +  ЗаказчикТелефоны;
	КонецЕсли;
	ОбластьМакета.Параметры.ОписаниеЗаказчика = ОписаниеЗаказчика;
	
	ТабДокумент.Вывести(ОбластьМакета);
	Возврат ТабДокумент;	
КонецФункции

// Формирует печатную форму "Довереноость на право управления ТС"
// Возвращает сформированный табличный документ:
Функция ПечатьДоверенность(ТабДокумент) Экспорт 
	
	Макет = ПолучитьМакет("Доверенность");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Город = киПолучитьГород(ЭтотОбъект.ПодразделениеКомпании,Справочники.ВидыКонтактнойИнформации.АдресФактический);
	ОбластьМакета.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	
	//данные организации
	СтруктураПредставления=Новый Структура("Наименование,ИНН,КПП,АдресФактический,ТелефонРабочий","","ИНН ","КПП ","","тел.: ");
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.ДЛяпечати = Истина;
	ОбластьМакета.Параметры.Организация = спПолучитьПредставление(ПодразделениеКомпании,СтруктураПредставления,ДополнительныеПараметры);
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель);
	Если ЗначениеЗаполнено(Руководитель) И ТипЗнч(Руководитель)=Тип("Структура") Тогда
		Должность = ", в лице";
		Если ЗначениеЗаполнено(Руководитель.РуководительДолжность) Тогда
			Должность = Должность + " " + СокрЛП(НРег(Руководитель.РуководительДолжность));
		КонецЕсли;
		ОбластьМакета.Параметры.ОрганизацияДолжность = Должность;
		
		ОрганизацияЛицо = " " + Руководитель.РуководительПредставление;
		ДоверенностьРуководителя = спПолучитьПодтверждающийДокументОбъектаПоВиду(Руководитель.Руководитель,Перечисления.ВидыДокументов.Доверенность);
		Если ЗначениеЗаполнено(ДоверенностьРуководителя) Тогда
			ОрганизацияЛицо = ОрганизацияЛицо + ", действующего  на основании " + ДоверенностьРуководителя;
		КонецЕсли;
		ОбластьМакета.Параметры.ОрганизацияЛицо = ОрганизацияЛицо;
	КонецЕсли;
	
    //данные заказчика
	ОбластьМакета.Параметры.Покупатель = Контрагент.НаименованиеПолное;
	Паспорт = спПолучитьПодтверждающийДокументОбъектаПоВиду(Контрагент,Перечисления.ВидыДокументов.Паспорт);
	Если ЗначениеЗаполнено(Паспорт) Тогда
		ПаспортныеДанные = "паспорт серии " + Паспорт.Серия + " № "+ Паспорт.Номер + " выдан " + Паспорт.КемВыдан + " " + Формат(Паспорт.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г.";
		ОбластьМакета.Параметры.ПаспортныеДанные = " (" + ПаспортныеДанные + ")";
	КонецЕсли;
	
	//форма собственности заказчика 
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ОбластьМакета.Параметры.ТипПокупателя = "юридическому лицу ";	
	ИначеЕсли Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
		ОбластьМакета.Параметры.ТипПокупателя = "частному предпринимателю ";	
	Иначе
		ОбластьМакета.Параметры.ТипПокупателя = "физическому лицу ";	
	КонецЕсли;
	
	//данные автомобиля
	ПТС = спПолучитьПодтверждающийДокументОбъектаПоВиду(Автомобиль,Перечисления.ВидыДокументов.ПТС);
	ФорматПредставленияГодаВыпускаАвтомобиля=орПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Права);
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		ОбластьМакета.Параметры.Модель = Автомобиль.Модель;  
		ОбластьМакета.Параметры.ГодВыпуска = Формат(Автомобиль.ГодВыпуска,ФорматПредставленияГодаВыпускаАвтомобиля);
		ОбластьМакета.Параметры.ГосНомер = 	Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,Дата);
		ОбластьМакета.Параметры.Цвет = Автомобиль.Цвет;
		ОбластьМакета.Параметры.НомерДвигателя = Автомобиль.НомерДвигателя;
		ОбластьМакета.Параметры.VIN = Автомобиль.VIN;
		Если ЗначениеЗаполнено(ПТС) Тогда
			ОбластьМакета.Параметры.ПТС = "ПТС " + ПТС.Серия + " № "+ ПТС.Номер + " выдан " + ПТС.КемВыдан + " " + Формат(ПТС.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + "г. ";
		КонецЕсли;
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	Возврат ТабДокумент;	
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено, ДопПараметры=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ, ДопПараметры);
КонецФункции

#КонецЕсли
// завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Состояние = Перечисления.СостоянияСобытий.Запланировано;	
	КонецЕсли;
	
	//Если автомобиль не подставился из шапки документа, попробуем отыскать в табличной части
	СписокАвтоДляТестДрайва = Документы.ТестДрайв.ПолучитьАвтомобилиДляТестДрайва();	
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		Если СписокАвтоДляТестДрайва.НайтиПоЗначению(Автомобиль) = Неопределено Тогда
			Автомобиль = Справочники.Автомобили.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Основание)=Тип("СправочникСсылка.Контрагенты") Тогда
		Контрагент=Основание; 
	ИначеЕсли ТипЗнч(Основание)=Тип("ДокументСсылка.Событие") Тогда
		
		//Если автомобиль не подставился из шапки документа, попробуем отыскать в табличной части
		Если НЕ ЗначениеЗаполнено(Автомобиль) И Основание.Автомобили.Количество()>0 И СписокАвтоДляТестДрайва.Количество()>0 Тогда
			Для Каждого ТекСтрока ИЗ Основание.Автомобили Цикл
				Если СписокАвтоДляТестДрайва.НайтиПоЗначению(ТекСтрока.Автомобиль) <> Неопределено Тогда
					Автомобиль = ТекСтрока.Автомобиль;
					Прервать;	
				КонецЕсли;				
			КонецЦикла;
		КонецЕсли;
		
		//возьмем телефон из документа-основания
		Если ЗначениеЗаполнено(Основание.КонтактнаяИнформация) Тогда
			ПредставлениеТелефона = Основание.КонтактнаяИнформация;
		КонецЕсли;
		
		ДатаНачала = Основание.ДатаНачала;
		ДатаОкончания = Основание.ДатаОкончания;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("НеОбновлятьТелефон", Истина); 
	ОбработкаРеквизита("Контрагент",,,ДопПараметры);
	ОбработкаРеквизита("Автомобиль");

	#Если Клиент Тогда
	МаршрутПоУмолчанию = ВосстановитьЗначение("АРМТД_МаршрутПоУмолчанию");	
	Если ЗначениеЗаполнено(МаршрутПоУмолчанию) Тогда
		Маршрут = МаршрутПоУмолчанию;
	КонецЕсли;
	#КонецЕсли

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Ошибки="";
	
	//Проверка корректности дат
	Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		Если (НЕ обЗначениеНеЗаполнено(ДатаНачала)) И (НЕ обЗначениеНеЗаполнено(ДатаОкончания)) Тогда
			Если ДатаНачала>ДатаОкончания Тогда
				дкДобавитьОшибку(Ошибки,"Дата начала тест-драйва больше даты окончания!");
				Отказ=Истина;
			КонецЕсли;
		КонецЕсли; 
		
		//проверка по состоянию документа
		Если НЕ Отказ И (Состояние=Перечисления.СостоянияСобытий.Выполняется
			ИЛИ Состояние=Перечисления.СостоянияСобытий.Завершено) Тогда
			
			Структура = ПроверитьСостояние(Отказ);
			Ошибки = Структура.Ошибки;
			Отказ = Структура.Отказ;
			
		КонецЕсли;
		
		Если Отказ Тогда
			// Выведем все накопившиеся ошибки 
			Сообщить("При записи <"+СокрЛП(ЭтотОбъект)+"> обнаружены ошибки :",СтатусСообщения.Внимание);
			Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
			Сообщить("Из-за возникших ошибок операция записи <"+СокрЛП(ЭтотОбъект)+"> была отменена.",СтатусСообщения.Важное);
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда Возврат; КонецЕсли;
	
	Если Состояние = Перечисления.СостоянияСобытий.Завершено Тогда
		Если НЕ ЗначениеЗаполнено(ДатаФактическогоЗавершения) Тогда
			#Если Клиент Тогда
				ДатаФактическогоЗавершения=РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
			#Иначе
				ДатаФактическогоЗавершения=ТекущаяДата();
			#КонецЕсли
		КонецЕсли;
	Иначе
		ДатаФактическогоЗавершения = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если Отказ Тогда Возврат; КонецЕсли;
	#Если Клиент Тогда
		Оповестить("ОбновитьДеревоСобытий", ЭтотОбъект,);
	#КонецЕсли
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	Если НЕ Отказ Тогда
		Отказ=НЕ Документы.ТестДрайв.ЗаполнитьГрафикРаботыАвтомобилей(Ссылка);
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ФорматПредставленияГодаВыпускаАвтомобиля=орПолучитьФорматПредставленияГодаВыпускаАвтомобиля(Права);


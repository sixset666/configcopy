// Модуль документа

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ

Перем ТекущаяВалютаДокумента Экспорт; // Текущая валюта документа
Перем ТекущийКурсДокумента Экспорт;   // Текущий курс валюты документа


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	
	Если ХозОперация=Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильОтмена ИЛИ
		ХозОперация=Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильИзменение Тогда
		ОбязательныеРеквизиты.Вставить("ДокументОснование", 1);
	КонецЕсли;
	
	Если НЕ ХозОперация=Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильОтмена Тогда
		ОбязательныеРеквизиты.Вставить("Контрагент",   1);
		ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
		ОбязательныеРеквизиты.Вставить("СрокПоставки", 1);
		ОбязательныеРеквизиты.Вставить("Модель",   1);
		//ОбязательныеРеквизиты.Вставить("Автомобиль",   1);
		
		ОбязательныеОпции=Новый Структура();
		ОбязательныеОпции.Вставить("Опция",      3);
		ОбязательныеОпции.Вставить("Количество", 1);
		ОбязательныеРеквизиты.Вставить("Опции", ОбязательныеОпции);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	//Если ДопРеквизиты=Неопределено Тогда
	//	ДопРеквизиты = Новый Структура;
	//КонецЕсли; 
	//ДопРеквизиты.Вставить("Автомобиль",1);
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);
		КонтрольОтсутствует        = (СпособКонтроляКорректности = Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Проверяет корректность заполнения табличных частей объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
Функция ПроверитьЗаполненностьТЧ(Ошибки="", ДопРеквизиты=Неопределено) Экспорт
	//Документ считаем корректно заполненным, независимо от заполненности табличных частей
	Возврат Истина;
КонецФункции

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: СписокЗначений. 
// Строка списка: Значение - имя предопределенного элемента; 
//				  Представление - красивое его представление
// 				  "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	
	СписокХозОпераций = Новый СписокЗначений;
	Если ЗначениеЗаполнено(ДокументОснование) И ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
		СписокХозОпераций.Добавить("ЗаказПоставщикуНаАвтомобильИзменение", "Изменение автомобиля", Истина);
		СписокХозОпераций.Добавить("ЗаказПоставщикуНаАвтомобильОтмена",    "Отмена заказа поставщику на автомобиль", Ложь);
	Иначе
		СписокХозОпераций.Добавить("ЗаказПоставщикуНаАвтомобиль",             "Заказ поставщику на автомобиль", Истина);
		СписокХозОпераций.Добавить("ЗаказПоставщикуНаАвтомобильПодТестДрайв", "Заказ поставщику на автомобиль под тест-драйв", Ложь);
	КонецЕсли;
	
	Возврат СписокХозОпераций;
	
КонецФункции

// пересчет цены на авто при изменении флага цена включает НДС
Процедура ПересчитатьЦеныДляНовогоТипаЦен() Экспорт
	ОбработкаРеквизита("ЦенаАвтомобиля");
КонецПроцедуры

// Расчет суммы документа
Функция РассчитатьСуммуВсего() Экспорт
	
	Возврат СуммаВсегоНаАвтомобиль + Опции.Итог("СуммаВсего");
	
КонецФункции

// Расчет суммы НДС
Функция РассчитатьСуммуНДСВсего() Экспорт
	
	Возврат СуммаНДСНаАвтомобиль + Опции.Итог("СуммаНДС");
	
КонецФункции

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат=Истина;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ТипЦен" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		ТребуетсяПересчет = Истина;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("ТребуетсяПересчет", ТребуетсяПересчет) Тогда
				ТребуетсяПересчет = Истина;
			КонецЕсли;
		КонецЕсли;
		Если ТребуетсяПересчет Тогда
			
			Если ЗначениеЗаполнено(Автомобиль) Тогда
				ЦенаАвтомобиля = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()), ?(Ссылка.Пустая(), Дата, МоментВремени()),, ВалютаДокумента, КурсДокумента);
			Иначе
				ЦенаАвтомобиля = обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль, ВариантКомплектации",Модель, ВариантКомплектации), ?(Ссылка.Пустая(), Дата, МоментВремени()),,ВалютаДокумента, КурсДокумента);
			КонецЕсли;
			Результат = ОбработкаРеквизита("ЦенаАвтомобиля", ТекСтрока,ЭтаФорма, ДопПараметры) И Результат;
			Для каждого СтрокаТЧ Из Опции Цикл
				Результат = ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
			КонецЦикла;
		КонецЕсли;
		ТекущаяВалютаДокумента = ВалютаДокумента;
		ТекущийКурсДокумента   = КурсДокумента;
		Возврат Результат;
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		Попытка
			ТребуетсяПересчет = ДопПараметры.ТребуетсяПересчет;
		Исключение
			ТребуетсяПересчет = Ложь;
		КонецПопытки;
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Если ТребуетсяПересчет И (НЕ обЗначениеНеЗаполнено(ТекущаяВалютаДокумента)) Тогда
			ЦенаАвтомобиля = обПересчет(ЦенаАвтомобиля, ТекущаяВалютаДокумента, ТекущийКурсДокумента, ВалютаДокумента, КурсДокумента);
			ОбработкаРеквизита("ЦенаАвтомобиля", ТекСтрока, ЭтаФорма, ДопПараметры);
			Для Каждого СтрокаТЧ Из Опции Цикл
				СтрокаТЧ.Цена = обПересчет(СтрокаТЧ.Цена, ТекущаяВалютаДокумента, ТекущийКурсДокумента, ВалютаДокумента, КурсДокумента);
				Результат = ОбработкаРеквизита("Опции.Цена", СтрокаТЧ, ЭтаФорма, ДопПараметры) И Результат;
			КонецЦикла; 
		КонецЕсли; 
		ТекущаяВалютаДокумента = ВалютаДокумента;
		ТекущийКурсДокумента   = КурсДокумента;
		
	ИначеЕсли Имя="Организация" Тогда
		
		Результат = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// Организация может являются плательщиком НДС, а может и нет.. надо обработать ТЧ
		Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
			ОсвобожденОтНДС	= ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
				ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
		Иначе
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
				ЭтотОбъект.Организация.ОсвобожденОтНДС;
		КонецЕсли;
		Если ОсвобожденОтНДС Тогда
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Результат=ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",,ЭтаФорма,ДопПараметры) И Результат;
		Для каждого СтрокаТЧ Из Опции Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат = ОбработкаРеквизита("Опции.СтавкаНДС",СтрокаТЧ,ЭтаФорма,ДопПараметры) И Результат;
		КонецЦикла;
		
	ИначеЕсли Имя = "ПодразделениеКомпании" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		// Подразделение может являются плательщиком НДС, а может и нет.. надо обработать ТЧ
		Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
			ОсвобожденОтНДС	= ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
				ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
		Иначе
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
				ЭтотОбъект.Организация.ОсвобожденОтНДС;
		КонецЕсли;

		Если ОсвобожденОтНДС Тогда
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Результат = ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",,ЭтаФорма,ДопПараметры) И Результат;
		Для каждого СтрокаТЧ Из Опции Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат = ОбработкаРеквизита("Опции.СтавкаНДС", СтрокаТЧ, ЭтаФорма, ДопПараметры) И Результат;
		КонецЦикла;
		
	ИначеЕсли Имя="СрокПоставки" Тогда
		Результат = ОбработкаРеквизита("ОтображениеСрокаПоставки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ОтображениеСрокаПоставки" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если обЗначениеНеЗаполнено(СрокПоставки) Тогда
				ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок="срок не определен";
			Иначе
				Если Ссылка.Пустая() ИЛИ НЕ Ссылка.Проведен Тогда
					ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
				Иначе
					Запрос = Новый Запрос("ВЫБРАТЬ
					|	МАКСИМУМ(ЗаказыПоставщикамНаАвтомобили.Период) КАК Период,
					|	СУММА(ВЫБОР
					|		КОГДА ЗаказыПоставщикамНаАвтомобили.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
					|			ЗаказыПоставщикамНаАвтомобили.Количество
					|		ИНАЧЕ
					|			-ЗаказыПоставщикамНаАвтомобили.Количество
					|	КОНЕЦ) КАК Количество
					|ИЗ
					|	РегистрНакопления.ЗаказыПоставщикамНаАвтомобили КАК ЗаказыПоставщикамНаАвтомобили
					|ГДЕ
					|	ЗаказыПоставщикамНаАвтомобили.ЗаказПоставщику = &Заказ");
					Запрос.УстановитьПараметр("Заказ", Ссылка);
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Если Выборка.Период = NULL Тогда
							ПросроченоДней = 0;
						ИначеЕсли Выборка.Количество > 0 Тогда
							ПросроченоДней = Цел((ТекущаяДата()-СрокПоставки)/86400);
						Иначе
							ПросроченоДней = 0;
						КонецЕсли;
					Иначе
						ПросроченоДней = 0;
					КонецЕсли;
				КонецЕсли;
					
				КолДней = Макс(ПросроченоДней, -ПросроченоДней); 
				ОстатокОтДеления = КолДней%10; 
				Если ОстатокОтДеления = 0 ИЛИ ОстатокОтДеления >=5 И ОстатокОтДеления <=9 
					ИЛИ КолДней%100 >= 10 И КолДней%100 <= 19 Тогда
					НадписьДень =  "дней";	
				ИначеЕсли ОстатокОтДеления = 1 Тогда
					НадписьДень = "день";
				ИначеЕсли ОстатокОтДеления >=2 И ОстатокОтДеления <=4 Тогда
					НадписьДень = "дня";
				КонецЕсли;		
				
				Если ПросроченоДней > 0 Тогда
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок = "просрочено на " + СокрЛП(ПросроченоДней) + " " + НадписьДень;
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(128,0,0);
				Иначе
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.Заголовок = "";
					ЭтаФорма.ЭлементыФормы.тЗадержкаПоставки.ЦветТекста=Новый Цвет(0,0,128);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		#КонецЕсли
	ИначеЕсли Имя="Контрагент" Тогда
		Результат = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		// Подразделение может являются плательщиком НДС, а может и нет.. надо обработать ТЧ
		Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
			ОсвобожденОтНДС	= ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
				ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
		Иначе
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
				ЭтотОбъект.Организация.ОсвобожденОтНДС;
		КонецЕсли;

		Если ОсвобожденОтНДС Тогда
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДСНаАвтомобиль=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли;
		Результат = ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",,ЭтаФорма,ДопПараметры) И Результат;
		Для каждого СтрокаТЧ Из Опции Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Результат = ОбработкаРеквизита("Опции.СтавкаНДС", СтрокаТЧ, ЭтаФорма, ДопПараметры) И Результат;
		КонецЦикла;
		
	// ОБРАБОТКА РЕКВИЗИТОВ АВТОМОБИЛЯ
	ИначеЕсли Имя="Автомобиль" Тогда
		Результат = Истина;
		Если ЗначениеЗаполнено(Автомобиль) Тогда
			Если ДопПараметры = Неопределено Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("НеИзменятьЦенуАвтомобиля", Истина);
			Модель              = Автомобиль.Модель;
			ВариантКомплектации = Автомобиль.ВариантКомплектации;
			Результат = ОбработкаРеквизита("Модель", ТекСтрока, ЭтаФорма, ДопПараметры);
			Результат = ОбработкаРеквизита("ВариантКомплектации", ТекСтрока, ЭтаФорма, ДопПараметры);
			ДопПараметры.Удалить("НеИзменятьЦенуАвтомобиля");
			Цвет      = Автомобиль.Цвет;
			ЦветКод   = Автомобиль.ЦветКод;
			ТипСалона = Автомобиль.ТипСалона;
			НоваяЦенаАвтомобиля = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации", Автомобиль, Справочники.ВариантыКомплектации.ПустаяСсылка()),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			#Если Клиент Тогда
			Если НоваяЦенаАвтомобиля<>ЦенаАвтомобиля Тогда
				Если ЭтаФорма<>Неопределено Тогда
					ТекстВопроса = "Цена выбранного автомобиля составляет "+Формат(НоваяЦенаАвтомобиля,"ЧДЦ=2; ЧН=0,00")+" "+СокрЛП(ВалютаДокумента)+".
						|Цена автомобиля по заказу "+Формат(ЦенаАвтомобиля,"ЧДЦ=2; ЧН=0,00")+" "+СокрЛП(ВалютаДокумента)+".
						|
						|Изменить цену автомобиля в заказе?";
					Ответ=Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да,"Изменение цены");
					Если Ответ<>КодВозвратаДиалога.Да Тогда
						НоваяЦенаАвтомобиля=ЦенаАвтомобиля;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
			Если ЭтаФорма<>Неопределено Тогда
				ЭтаФорма.ПолучитьОборудованиеАвтомобиля();
				дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			КонецЕсли;
			#КонецЕсли
			Если НоваяЦенаАвтомобиля<>ЦенаАвтомобиля Тогда
				ЦенаАвтомобиля=НоваяЦенаАвтомобиля;
				Результат=ОбработкаРеквизита("ЦенаАвтомобиля",ТекСтрока,ЭтаФорма,ДопПараметры);
			КонецЕсли;
		КонецЕсли;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.УправлениеДиалогом();
		КонецЕсли; 
		#КонецЕсли
		
	ИначеЕсли Имя="Модель" Тогда
		ВариантКомплектацииИзменен=Ложь;
		Если ВариантКомплектации.Владелец<>Модель Тогда
			ВариантКомплектации=Неопределено;
			ВариантКомплектацииИзменен=Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(Модель) И (ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобиль ИЛИ ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильПодТестДрайв) Тогда
			НовыйПоставщик = Неопределено;
			НовыйДоговорВзаиморасчетов = Неопределено;
			Если ЗначениеЗаполнено(Модель.Контрагент) Тогда
				//И обЗначениеНеЗаполнено(Контрагент) Тогда
				НовыйПоставщик = Модель.Контрагент;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Модель.ДоговорВзаиморасчетов) Тогда
					// И обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				НовыйДоговорВзаиморасчетов = Модель.ДоговорВзаиморасчетов;
			КонецЕсли;
			
			Если НовыйПоставщик<>Неопределено И НовыйПоставщик<>Контрагент Тогда
				Контрагент            = НовыйПоставщик;
				ДоговорВзаиморасчетов = НовыйДоговорВзаиморасчетов;
				Результат = ОбработкаРеквизита("Контрагент",            ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
				Результат = ОбработкаРеквизита("ДоговорВзаиморасчетов", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
			ИначеЕсли НовыйДоговорВзаиморасчетов<>Неопределено И НовыйДоговорВзаиморасчетов<>ДоговорВзаиморасчетов Тогда
				ДоговорВзаиморасчетов = НовыйДоговорВзаиморасчетов;
				Результат = ОбработкаРеквизита("ДоговорВзаиморасчетов", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
			КонецЕсли;
		КонецЕсли;
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ОтключитьОбработчикИзмененияДанных("АвтомобильЗаказа");
			АвтомобильИзменен=Ложь;
			Если Модель<>ЭтаФорма.АвтомобильЗаказа.Модель Тогда
				ЭтаФорма.АвтомобильЗаказа.Модель=Модель;
				АвтомобильИзменен=Истина;
			КонецЕсли; 
			Если ВариантКомплектации<>ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации Тогда
				ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации=ВариантКомплектации;
				АвтомобильИзменен=Истина;
			КонецЕсли;
			Если АвтомобильИзменен Тогда
				ЭтаФорма.АвтомобильЗаказа.ФормированиеНаименованияАвтомобиля("");
			КонецЕсли; 
			ЭтаФорма.ПодключитьОбработчикИзмененияДанных("АвтомобильЗаказа","АвтомобильЗаказаПриИзменении",Истина);
		КонецЕсли; 
		#КонецЕсли
	    Если ВариантКомплектацииИзменен Тогда
			Результат=ОбработкаРеквизита("ВариантКомплектации",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="ВариантКомплектации" Тогда
		Если ЗначениеЗаполнено(ВариантКомплектации) Тогда
			Если ВариантКомплектации.Владелец<>Модель Тогда
				Модель    = ВариантКомплектации.Владелец;
				Результат = ОбработкаРеквизита("Модель",ТекСтрока,ЭтаФорма,ДопПараметры) И Результат;
			КонецЕсли;
		КонецЕсли;
		НеИзменятьЦенуАвтомобиля = Ложь;
		Если ДопПараметры<>Неопределено Тогда
			Если НЕ ДопПараметры.Свойство("НеИзменятьЦенуАвтомобиля",НеИзменятьЦенуАвтомобиля) Тогда
				НеИзменятьЦенуАвтомобиля=Ложь;
			КонецЕсли; 
		КонецЕсли;
		Если НЕ НеИзменятьЦенуАвтомобиля Тогда
			ЦенаАвтомобиля = обПолучитьЦену(ТипЦен, Новый Структура("Автомобиль, ВариантКомплектации",Модель, ВариантКомплектации),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			Результат = ОбработкаРеквизита("ЦенаАвтомобиля", ТекСтрока, ЭтаФорма, ДопПараметры) И Результат;
		КонецЕсли;
		
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			ЭтаФорма.ПолучитьОборудованиеАвтомобиля();
		КонецЕсли;
		#КонецЕсли
		
		Для Каждого СтрокаТЧ Из Опции Цикл
			Результат = ОбработкаРеквизита("Опции.Опция", СтрокаТЧ, ЭтаФорма, ДопПараметры) И Результат;
		КонецЦикла;
		
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
			Если ВариантКомплектации<>ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации Тогда
				ЭтаФорма.АвтомобильЗаказа.ВариантКомплектации=ВариантКомплектации;
			КонецЕсли;
		КонецЕсли;
		#КонецЕсли
		
	ИначеЕсли Имя="Цвет" ИЛИ Имя="ЦветКод" ИЛИ Имя="ТипСалона" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если ЭтотОбъект[Имя]<>ЭтаФорма.АвтомобильЗаказа[Имя] Тогда
				ЭтаФорма.АвтомобильЗаказа[Имя]=ЭтотОбъект[Имя];
			КонецЕсли;
			Если Имя="Цвет" Тогда
				ЭтаФорма.ОтключитьОбработчикИзмененияДанных("АвтомобильЗаказа");
				ЭтаФорма.АвтомобильЗаказа.ФормированиеНаименованияАвтомобиля("");
				ЭтаФорма.ПодключитьОбработчикИзмененияДанных("АвтомобильЗаказа","АвтомобильЗаказаПриИзменении",Истина);
			КонецЕсли;
		КонецЕсли; 
		#КонецЕсли
	    Результат = Истина;
		
	ИначеЕсли Имя="ЦенаАвтомобиля" Тогда
		//Результат=ОбработкаРеквизита("РассчитатьСкидкуНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		Результат = ОбработкаРеквизита("СтавкаНДСНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СтавкаНДСНаАвтомобиль" Тогда
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			СуммаНДСНаАвтомобиль=Окр((ЦенаАвтомобиля*СтавкаНДСНаАвтомобиль.Ставка)/(100+СтавкаНДСНаАвтомобиль.Ставка),2);
		Иначе
			СуммаНДСНаАвтомобиль=Окр(ЦенаАвтомобиля*СтавкаНДСНаАвтомобиль.Ставка/100,2);
		КонецЕсли;
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			СуммаВсегоНаАвтомобиль = ЦенаАвтомобиля;
		Иначе
			СуммаВсегоНаАвтомобиль = ЦенаАвтомобиля + СуммаНДСНаАвтомобиль;
		КонецЕсли;
		
		Результат = ЭтотОбъект.ОбработкаРеквизита("СуммаНДСНаАвтомобиль",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СуммаНДСНаАвтомобиль" Тогда
		// Определим каким образом у нас вычисляется НДС
		//ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		//Если ЦенаВключаетНДС Тогда
		//	СуммаВсегоНаАвтомобиль = ЦенаАвтомобиля;
		//Иначе
		//	СуммаВсегоНаАвтомобиль = ЦенаАвтомобиля + СуммаНДСНаАвтомобиль;
		//КонецЕсли;
		Возврат Результат;
		
	ИначеЕсли Имя="СуммаВсегоНаАвтомобиль" Тогда
		
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			ЦенаАвтомобиля = СуммаВсегоНаАвтомобиль;
		Иначе
			ЦенаАвтомобиля = Окр(СуммаВсегоНаАвтомобиль*100/(100+СтавкаНДСНаАвтомобиль.Ставка), 2);
		КонецЕсли;
		
		// Рассчитываем новую сумму НДС.
		Если ЦенаВключаетНДС Тогда
			СуммаНДСНаАвтомобиль=Окр((ЦенаАвтомобиля*СтавкаНДСНаАвтомобиль.Ставка)/(100+СтавкаНДСНаАвтомобиль.Ставка),2);
		Иначе
			СуммаНДСНаАвтомобиль=Окр(ЦенаАвтомобиля*СтавкаНДСНаАвтомобиль.Ставка/100,2);
		КонецЕсли;
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ДОПОЛНИТЕЛЬНОЕ ОБОРУДОВАНИЕ"
	ИначеЕсли Имя="Опции.Опция" Тогда
		Если ЭтаФорма<>Неопределено Тогда
			Если НЕ обЗначениеНеЗаполнено(ТекСтрока.Опция) Тогда
				Если ЭтаФорма.ОборудованиеПроизводителя.Найти(ТекСтрока.Опция, "Опция")=Неопределено Тогда
					#Если Клиент Тогда
					Если ЭтаФорма<>Неопределено Тогда
						Сообщить("Опция <"+ТекСтрока.Опция+"> недопустима для данного варианта комплектации.");
					КонецЕсли; 
					#КонецЕсли
					ТекСтрока.Опция=Неопределено;
					Возврат ОбработкаРеквизита("Опции.Опция",ТекСтрока,ЭтаФорма,ДопПараметры);
				 КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		
		// проверим а не набор ли у нас...
		Если НЕ ТекСтрока.Опция.ПризнакНабора Тогда
			// установим количество
			Если ТекСтрока.Количество=0 Тогда ТекСтрока.Количество=1 КонецЕсли;
			// Заполним ставку НДС
			Если дкПроверитьПринадленостьХозОперации(ЭтотОбъект, "Поступление") Тогда
				ОсвобожденОтНДС	= ЭтотОбъект.Контрагент.ОсвобожденОтНДС ИЛИ
					ЭтотОбъект.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
			Иначе
				ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ
					ЭтотОбъект.Организация.ОсвобожденОтНДС;
			КонецЕсли;

			Если ОсвобожденОтНДС Тогда
				ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				ТекСтрока.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			КонецЕсли;
			// попытаемся получить цену
			ТекСтрока.Цена=обПолучитьЦену(ТипЦен,Новый Структура("Автомобиль,ВариантКомплектации,Опция",Модель,ВариантКомплектации,ТекСтрока.Опция),?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента);
			Результат  =ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		Иначе
			// имеем дело с набором, разложим его...
			Набор=ТекСтрока.Опция.СоставНабора; КоличествоНабора=ТекСтрока.Количество;
			Если КоличествоНабора=0 Тогда КоличествоНабора=1; КонецЕсли; 
			// удалим строку из табличной части
			Опции.Удалить(ТекСтрока);
			// теперь будем рекурсивно добавлять состав набора
			Для Каждого ОборудованиеИзНабора Из Набор Цикл
				СтрокаТЧ=Опции.Найти(ОборудованиеИзНабора.Опция,"Опция");
				Если СтрокаТЧ=Неопределено Тогда
					СтрокаТЧ=Опции.Добавить();
					СтрокаТЧ.Опция=ОборудованиеИзНабора.Опция;
					СтрокаТЧ.Количество=ОборудованиеИзНабора.Количество*КоличествоНабора;
					ОбработкаРеквизита("Опции.Опция",СтрокаТЧ,ЭтаФорма,ДопПараметры);
					Если обЗначениеНеЗаполнено(СтрокаТЧ.Опция) Тогда
						Опции.Удалить(СтрокаТЧ);
					КонецЕсли; 
				Иначе
					СтрокаТЧ.Количество=СтрокаТЧ.Количество+(ОборудованиеИзНабора.Количество*КоличествоНабора);
					ОбработкаРеквизита("Опции.Количество",СтрокаТЧ,ЭтаФорма,ДопПараметры);
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Имя="Опции.Количество" Тогда
		Результат=ОбработкаРеквизита("Опции.Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.Цена" Тогда
		ТекСтрока.Сумма=ТекСтрока.Цена*ТекСтрока.Количество;
		Результат = ОбработкаРеквизита("Опции.Сумма", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя="Опции.Сумма" Тогда
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		Результат=ОбработкаРеквизита("Опции.СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.СтавкаНДС" Тогда
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Расчет НДС
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаНДС=Окр((ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС=Окр(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка/100,2);
		КонецЕсли;
		
		// Определим каким образом у нас вычисляется НДС
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма;
		Иначе
			ТекСтрока.СуммаВсего=ТекСтрока.Сумма+ТекСтрока.СуммаНДС;
		КонецЕсли;
		
		// В любом случаее идем в секцию "СуммаНДС", т.к. в ней будем произведен расчет суммы всего.
		Результат=ЭтотОбъект.ОбработкаРеквизита("Опции.СуммаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Опции.СуммаНДС" Тогда
		
	ИначеЕсли Имя="Опции.СуммаВсего" Тогда
		// Определим включают ли в себя НДС цены документа
		ЦенаВключаетНДС=(обЗначениеНеЗаполнено(ТипЦен) ИЛИ ТипЦен.ЦенаВключаетНДС);
		// Рассчитаем значение реквизита "Сумма". Если расчет НДС не ведется, то ЦенаВключаетНДС будет всегда Истина. 
		Если НЕ ЦенаВключаетНДС Тогда
			ТекСтрока.Сумма=Окр(ТекСтрока.СуммаВсего*100/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		КонецЕсли;
		// Пересчитываем цену.
		ТекСтрока.Цена=?(ТекСтрока.Количество=0,0,ТекСтрока.Сумма/ТекСтрока.Количество);
		
		// Рассчитываем новую сумму НДС.
		Если ЦенаВключаетНДС Тогда
			ТекСтрока.СуммаНДС=Окр((ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка)/(100+ТекСтрока.СтавкаНДС.Ставка),2);
		Иначе
			ТекСтрока.СуммаНДС=Окр(ТекСтрока.Сумма*ТекСтрока.СтавкаНДС.Ставка/100,2);
		КонецЕсли;
		
	Иначе
		Результат=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ОбработкаРеквизита()

//Открывает отчет по оборудованию
Процедура ВывестиОтчетОборудования() Экспорт
	
	Если ЗначениеЗаполнено(Автомобиль) Тогда
		Автомобиль.ПолучитьОбъект().ОтчетОборудованиеАвтомобиля();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу комплектации по автомобилю
Процедура ЗаполнитьКомплектациюАвтомобиля() Экспорт
	// таблица комплектации
	Перем табКомплектация;
	
	// проверим указание на авто
	Если обЗначениеНеЗаполнено(Автомобиль) Тогда
		Возврат;
	КонецЕсли;
	
	// получим таблицу комплектации
	НайденыйАвтомобильОбъект = Автомобиль.ПолучитьОбъект();
	Рез = НайденыйАвтомобильОбъект.ПолучитьОпцииИОборудованиеАвтомобиля(табКомплектация);
	
	#Если Клиент Тогда
		Если (Опции.Количество() <> 0) Тогда
			ТекстВопроса = "Очистить табличную часть ""Опции""?";	
			Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
				Опции.Очистить();
			КонецЕсли;
		КонецЕсли;
	#КонецЕсли
	
	//если нет данных выйдем
	Если НЕ Рез Тогда
		Возврат;
	КонецЕсли;
	
	// заполнияем ТЧ
	Для Каждого стрКомплектации Из табКомплектация Цикл
		Если ТипЗнч(стрКомплектации.Номенклатура) = Тип("СправочникСсылка.Опции") Тогда
			// добавление в опции
			НоваяСтрока = Опции.Добавить();
			НоваяСтрока.Опция = стрКомплектации.Номенклатура;
			ОбработкаРеквизита("Опции.Опция",НоваяСтрока);
			НоваяСтрока.Количество = стрКомплектации.КоличествоОстаток;
			НоваяСтрока.СуммаВсего = обПересчет(стрКомплектации.СуммаУпрОстаток,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),?(обЗначениеНеЗаполнено(ЭтотОбъект.КурсВалютыУпр), Дата, ЭтотОбъект.КурсВалютыУпр),ВалютаДокумента,Дата,РежимОкругления.Окр15как20);;
			ОбработкаРеквизита("Опции.СуммаВсего",НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

#Если Клиент Тогда

// Формирует печатную форму "ЗаказНаАвтомобиль"
// Возвращает сформированный табличный документ:
Функция ПечатьЗаказПоставщикуНаАвтомобиль(ТабДокумент) Экспорт
	// Предварительные обработки данных
	
	//валюта печати
	ВалютаПечатногоДокумента = зфВыборВалютыПечатногоДокумента(ЭтотОбъект);
	Если ВалютаПечатногоДокумента = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	КоэффициентПересчета = обПересчет(1, ЭтотОбъект.ВалютаДокумента, ЭтотОбъект.КурсДокумента, ВалютаПечатногоДокумента, ЭтотОбъект.Дата);
	
	// приступим к печати
	Макет = ПолучитьМакет("ЗаказПоставщикуНаАвтомобиль1");
	
	//для начала настроим макет
	ОбластьТовар = Макет.Область("Товар");
	//определяем есть ли скидки
	
	ЕстьКод = обПраво("ВыводитьКодВПечатныхФормах",Права,,ЭтотОбъект);
	//если код не выводим
	Если НЕ ЕстьКод Тогда
		ОбластьКод = Макет.Область("Код");
		ОбластьТовар.ШиринаКолонки = ОбластьТовар.ШиринаКолонки + ОбластьКод.ШиринаКолонки;
		Макет.УдалитьОбласть(ОбластьКод,ТипСмещенияТабличногоДокумента.ПоВертикали);		
	КонецЕсли;
		
	//теперь запишем параметры шапки
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	Если ЕстьКод Тогда
		КолонкаКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ЭтотОбъект);
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиКод = КолонкаКода.Синоним;
	КонецЕсли;
	
	//заполняем заголовок колонки НДС по типу цен
	ОбластьШапкаТаблицы.Параметры.НДС = "НДС";
	Если ТипЦен.ЦенаВключаетНДС Тогда	// Если НДС включен
		Если НЕ (ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС) Тогда	// Однако не все организации - плательщики НДС
			ОбластьШапкаТаблицы.Параметры.НДС = "в т.ч. НДС";
		КонецЕсли;
	КонецЕсли;
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	
	ОбластьМакета.Параметры.ПредставлениеПокупателя	= спПолучитьПредставление(Организация);
	//контрагент
	ОбластьМакета.Параметры.ПредставлениеПоставщика	= спПолучитьПредставление(Контрагент);
	//автомобиль
	ОбластьМакета.Параметры.Заполнить(ВариантКомплектации);	
	ОбластьМакета.Параметры.АвтомобильМодель		= спПолучитьНаименование(Модель);
	ОбластьМакета.Параметры.АвтомобильВариантКомплектации = спПолучитьНаименование(ВариантКомплектации);
	ОбластьМакета.Параметры.АвтомобильЦвет			= спПолучитьНаименование(Цвет);
	ОбластьМакета.Параметры.АвтомобильЦветКод		= ЦветКод;
	ОбластьМакета.Параметры.АвтомобильТипКузова		= спПолучитьНаименование(ВариантКомплектации.ТипКузова);
	ОбластьМакета.Параметры.АвтомобильТипДвигателя	= спПолучитьНаименование(ВариантКомплектации.ТипДвигателя);
	ОбластьМакета.Параметры.АвтомобильТипКПП		= спПолучитьНаименование(ВариантКомплектации.ТипКПП);
	ОбластьМакета.Параметры.АвтомобильТипСалона		= спПолучитьНаименование(ТипСалона);
	ОбластьМакета.Параметры.АвтомобильЦена			= Формат(ЦенаАвтомобиля * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОбластьМакета.Параметры.ПредставлениеНДС		= ?(ТипЦен.ЦенаВключаетНДС,"В т.ч. ","")+"НДС" + " (" + СтавкаНДСНаАвтомобиль.Ставка + "%)" + ": "+Формат(СуммаНДСНаАвтомобиль * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОбластьМакета.Параметры.СуммаВсегоНаАвтомобиль	= Формат(СуммаВсегоНаАвтомобиль * КоэффициентПересчета, ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	
	//базовое оборудование автомобиля
	ТекстЗапроса = "ВЫБРАТЬ
	|	ОпцииВариантовКомплектации.Опция КАК Опция,
	|	ОпцииВариантовКомплектации.Количество КАК Количество
	|ИЗ
	|	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
	|ГДЕ
	|	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации
	|	И ОпцииВариантовКомплектации.ВидВключения = &ВидВключения";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВариантКомплектации", ВариантКомплектации);
	Запрос.УстановитьПараметр("ВидВключения", 2);
	
	Выборка = Запрос.Выполнить().Выбрать();
	СтрокаДопОборудования = "";
	Пока Выборка.Следующий() Цикл
		СтрокаДопОборудования = СтрокаДопОборудования + спПолучитьНаименование(Выборка.Опция) + ?(Выборка.Количество = 0,"","("+Выборка.Количество+")") + " ,";
	КонецЦикла;
	ОбластьМакета.Параметры.СписокБазовогоОборудования = Лев(СтрокаДопОборудования, СтрДлина(СтрокаДопОборудования) - 2);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0);
	
	//пересчитаем ТЧ
	ЭтотОбъектОпции = Опции.Выгрузить();
	зфПерерасчетТаблицыТоваров(ЭтотОбъектОпции, ЭтотОбъект, ВалютаПечатногоДокумента);
	
	//дополнительные колонки для того чтобы замаскировать ТЧ Опции под ТЧ Товары
	ЭтотОбъектОпции.Колонки.Добавить("Номенклатура");
	ЭтотОбъектОпции.Колонки.Добавить("ЕдиницаИзмерения");
	ЭтотОбъектОпции.Колонки.Добавить("Коэффициент");
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъектОпции;
	
	Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда 
		// ПЕЧАТЬ ТАБЛИЦЫ ОПЦИЙ
		//теперь выводим шапку
		ОбластьЗаголовокТаблицы.Параметры.ЗаголовокТаблицы = "Опции";
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиТовара = "Опции";
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	КонецЕсли;
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	
	Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//предварительная обработка
		СтрокаТабличнойЧасти.Номенклатура		= СтрокаТабличнойЧасти.Опция;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения	= "";
		СтрокаТабличнойЧасти.Коэффициент		= 1;
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	Если ВыборкаТабличнойЧасти.Количество() > 0 Тогда
		//довыводим последний подвал, если страниц больше единицы
		Если НомерСтраницы > 2 Тогда
			дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
		КонецЕсли;
		
		//итоги
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
		СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
		ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
		НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
		ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
		
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	КонецЕсли;
	// ПЕЧАТЬ ТАБЛИЦЫ ТОВАРОВ	
	//готовим области строки
	//ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	//ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	//СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
	
	////пересчитаем ТЧ
	//ЭтотОбъектТовары = Товары.Выгрузить();
	//зфПерерасчетТаблицыТоваров(ЭтотОбъектТовары, ЭтотОбъект, ВалютаПечатногоДокумента);

	////перебор строк
	//ВыборкаТабличнойЧасти = ЭтотОбъектТовары;
	//
	////заполним параметры шапки таблицы для следующего листа
	//ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка= "";
	//ОбластьШапкаТаблицы.Параметры.НомерСтраницы	= "";
	//
	//Если ВыборкаТабличнойЧасти.Количество()>0 Тогда
	//	ОбластьЗаголовокТаблицы.Параметры.ЗаголовокТаблицы = "Оборудование";
	//	ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
	//	//выводим шапку
	//	ОбластьШапкаТаблицы.Параметры.ИмяКолонкиТовара = "Оборудование";
	//	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	//КонецЕсли;
	//
	////заполним параметры шапки таблицы для следующего листа
	//ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	//ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы;
	//
	//Для каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
	//	//заполняем данные строки
	//	СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
	//	ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
	//	
	//	//выводим строку, делая проверку попадания на лист
	//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице, НомерСтраницы, СтруктураИтоговПоСтранице,ЭтотОбъект);
	//	
	//	//инициализация итогов по странице
	//	Если НомерСтраницы <> НомерСтраницыПред Тогда
	//		СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаПечатногоДокумента,0,0,0);
	//		НомерСтраницыПред = НомерСтраницы;
	//		ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	//	КонецЕсли;
	//	//добавляем итоги
	//	дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
	//КонецЦикла;
	//
	//Если ВыборкаТабличнойЧасти.Количество()>0 Тогда
	//	//довыводим последний подвал, если страниц больше единицы
	//	Если НомерСтраницы > 2 Тогда
	//		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	//	КонецЕсли;
	//	
	//	//итоги
	//	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	//	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаПечатногоДокумента;
	//	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	//	ОбластьМакета.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	//	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	//	ОбластьМакета.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	//	Если ВыборкаТабличнойЧасти.Итог("СуммаСкидки") > 0 Тогда
	//		СуммаСкидки = ВыборкаТабличнойЧасти.Итог("СуммаСкидки");
	//		ОбластьМакета.Параметры.СкидкаВсего = Формат(СуммаСкидки,ФорматВыводаСуммы);
	//	КонецЕсли;
	//	ОбластьМакета.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ВалютаПечатногоДокумента);
	//	
	//	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, , , НомерСтраницы,,ЭтотОбъект);
	//КонецЕсли;
	
	// Выводим представления и расшифровки подписей
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	
	ОбластьМакета.Параметры.ВалютаДокумента	= ВалютаПечатногоДокумента;
	ОбластьМакета.Параметры.СуммаДокумента	= Формат(СуммаДокумента * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	ОбластьМакета.Параметры.СуммаДокументаПрописью = обЧислоПрописью(СуммаДокумента*КоэффициентПересчета,ВалютаПечатногоДокумента);
	СуммаНДС = СуммаНДСНаАвтомобиль + Опции.Итог("СуммаНДС");
	ОбластьМакета.Параметры.СуммаНДС		= Формат(СуммаНДС * КоэффициентПересчета,  ?(НЕ ПустаяСтрока(ФорматВыводаСуммы),ФорматВыводаСуммы,ФорматВыводаСуммы + "ЧДЦ=2; ЧН=0,00"));
	
	//Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"Исполнитель");
	//Исполнитель.ИсполнительПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.Исполнитель),"","/ "+ Исполнитель.ИсполнительПредставление);
	//ОбластьМакета.Параметры.Заполнить(Исполнитель);
	
	// Выводим представления и расшифровки подписей
	Исполнитель = дкОтветственноеЛицо(ЭтотОбъект,"ИсполнительКонтрагент");
	Исполнитель.ИсполнительКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Исполнитель.ИсполнительКонтрагент),"","/ "+ Исполнитель.ИсполнительКонтрагентПредставление);
	ОбластьМакета.Параметры.Заполнить(Исполнитель);
	Заказчик  = дкОтветственноеЛицо(ЭтотОбъект,"ЗаказчикОрганизация");
	Заказчик.ЗаказчикОрганизацияПредставление = ?(обЗначениеНеЗаполнено(Заказчик.ЗаказчикОрганизация),"","/ "+ Заказчик.ЗаказчикОрганизацияПредставление);
	ОбластьМакета.Параметры.Заполнить(Заказчик);
	
	
	//ОЛЗаказчик	= дкОтветственноеЛицо(ЭтотОбъект,"Заказчик");
	//ОЛЗаказчик.ЗаказчикПредставление = ?(обЗначениеНеЗаполнено(ОЛЗаказчик.Заказчик),"","/ "+ ОЛЗаказчик.ЗаказчикПредставление);
	//ОбластьМакета.Параметры.Заполнить(ОЛЗаказчик);
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
// Параметры: НазваниеМакета - строка, название макета, если пусто - то по умолчанию,
//   Документ - сюда можно передать уже созданный табличный документ для заполнения
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=1, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	// Вызов конкретно печатной формы и печать результата прописано в общем модуле
	// здесь могут быть вставлены дополнительные механизмы
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если ТипЗнч(ДокументОснование)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") И ЗначениеЗаполнено(ДокументОснование) Тогда
		Если ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобиль ИЛИ ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильПодТестДрайв Тогда
			ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильИзменение;
		КонецЕсли; 
	КонецЕсли; 
	
	Если (НЕ ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильОтмена) И обЗначениеНеЗаполнено(СрокПоставки) Тогда
		СрокПоставкиДней = обПраво("СрокПоставкиПокупателюПоУмолчанию",Права,,ЭтотОбъект);
		СрокПоставки     = НачалоДня(ТекущаяДата())+СрокПоставкиДней*60*60*24;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Основание) Тогда
		Если обЗначениеНеЗаполнено(СтавкаНДСНаАвтомобиль) Тогда
			СтавкаНДСНаАвтомобиль = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			ОбработкаРеквизита("СтавкаНДСНаАвтомобиль");
		КонецЕсли; 
		НоваяСуммаДокумента = РассчитатьСуммуВсего();
		Если СуммаДокумента<>НоваяСуммаДокумента Тогда
			СуммаДокумента  = НоваяСуммаДокумента;
		КонецЕсли; 
	КонецЕсли;
	
	ТекущаяВалютаДокумента=ВалютаДокумента;
	ТекущийКурсДокумента=КурсДокумента;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(Автомобиль) Тогда
		Автомобиль=Справочники.Автомобили.ПустаяСсылка();
		ОбработкаРеквизита("Автомобиль");
	КонецЕсли; 
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("АвтомобильБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	//Проверим сумму предоплаты если есть заказ покупателя
	Если НЕ Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ДокументОбъектСтруктура=Новый Структура();
		ДокументОбъектСтруктура.Вставить("Представление",Строка(ЭтотОбъект));
		ДокументОбъектСтруктура.Вставить("Дата",Дата);
		ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
		ДокументОбъектСтруктура.Вставить("Проведен",Проведен);
		ДокументОбъектСтруктура.Вставить("Автомобиль",Автомобиль);
		Отказ = НЕ зфЗащищенныеФункцииСервер.ЗаказПоставщикуНаАвтомобильПроверитьПредоплатуПоЗаказуНаАвтомобиль(ДокументОбъектСтруктура);
	КонецЕсли;
	
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		// заказы на автомобили
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыПоставщикамНаАвтомобили.Автомобиль КАК Автомобиль
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикамНаАвтомобили КАК ЗаказыПоставщикамНаАвтомобили
		|ГДЕ
		|	ЗаказыПоставщикамНаАвтомобили.Регистратор = &Ссылка
		|";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("АвтомобильБыл", Результат.Выгрузить().ВыгрузитьКолонку("Автомобиль"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Проверка наличия других заказов на автомобиль с данным VINом
//
// Параметры
//  VINАвтомобиля  – <Строка> – VIN автомобиля, заказы на который проверяются
//  ОбъектПроверки  – <ДокументОбъект> – Документ, который проверяет наличие заказов
//
// Возвращаемое значение:
//   <Булево>   – Заказов на данный автомобиль нет
//
Функция ПроверитьЗаказыНаАвтомобиль(АвтомобильПроверки,ОбъектПроверки)
	ДокументОбъектСтруктура=Новый Структура();
	ДокументОбъектСтруктура.Вставить("Ссылка",Ссылка);
	ДокументОбъектСтруктура.Вставить("МоментВремени",МоментВремени());
	Если ТипЗнч(РезультатОбработки) = Тип("ОбработкаОбъект.ИнформационноеПоле") Тогда
		СтрокаСообщений="";
		Рез=зфЗащищенныеФункцииСервер.ЗаказПоставщикуНаАвтомобильПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,АвтомобильПроверки,СтрокаСообщений);
		Если НЕ ПустаяСтрока(СтрокаСообщений) Тогда
			Для НомерСтроки=1 По СтрЧислоСтрок(СтрокаСообщений) Цикл
				СообщениеПользователю=СтрПолучитьСтроку(СтрокаСообщений,НомерСтроки);
				РезультатОбработки.ДобавитьСтрокуСообщения(СообщениеПользователю,"Важное",Неопределено,Неопределено);
			КонецЦикла; 
		КонецЕсли; 
	Иначе
		Рез=зфЗащищенныеФункцииСервер.ЗаказПоставщикуНаАвтомобильПроверитьЗаказыНаАвтомобиль(ДокументОбъектСтруктура,АвтомобильПроверки,РезультатОбработки);
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьЗаказыНаАвтомобиль()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОснование) И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
		//Проверим, а нет ли заказа на этот автомобиль по заказу документа основания
		//Сформируем таблицы документов оснований
		МассивДокументовОснований=Новый Массив;
		ТаблицаДокументовОснований=Новый ТаблицаЗначений;
		ТаблицаДокументовОснований.Колонки.Добавить("ЗаказПоставщику",Новый ОписаниеТипов("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль"));
		ДокОснования=ДокументОснование;
		Пока ЗначениеЗаполнено(ДокОснования) Цикл
			Если ТипЗнч(ДокОснования)=Тип("ДокументСсылка.ЗаказПоставщикуНаАвтомобиль") Тогда
				МассивДокументовОснований.Добавить(ДокОснования);
				НоваяСтрока=ТаблицаДокументовОснований.Добавить();
				НоваяСтрока.ЗаказПоставщику=ДокОснования;
			Иначе
				Прервать;
			КонецЕсли;
			ДокОснования=ДокОснования.ДокументОснование;
		КонецЦикла; 
		//Получим остатки заказов-оснований
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗаказыПоставщикамНаАвтомобилиОстатки.Автомобиль КАК Автомобиль,
		               |	ЗаказыПоставщикамНаАвтомобилиОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
		               |	ЕСТЬNULL(ЗаказыПоставщикамНаАвтомобилиОстатки.СуммаОстаток, 0) КАК Сумма,
		               |	ЕСТЬNULL(ЗаказыПоставщикамНаАвтомобилиОстатки.СуммаУпрОстаток, 0) КАК СуммаУпр,
		               |	ЕСТЬNULL(ЗаказыПоставщикамНаАвтомобилиОстатки.КоличествоОстаток, 0) КАК Количество
		               |ИЗ
		               |	РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Остатки(&НаДату, ЗаказПоставщику В (&Заказ)) КАК ЗаказыПоставщикамНаАвтомобилиОстатки";
		Запрос.УстановитьПараметр("Заказ",  МассивДокументовОснований);
		Запрос.УстановитьПараметр("НаДату", МоментВремени());
		
		// Наложим блокировку на считываемые данные
		СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ЗаказыПоставщикамНаАвтомобили");
		ЗначенияБлокировки = Новый Соответствие;
		ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, Дата));
		//ЗначенияБлокировки.Вставить("ЗаказПоставщику", ДокументОснование);
		СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаДокументовОснований);
		ОписаниеИсточника = Новый Соответствие;
		ОписаниеИсточника.Вставить("ЗаказПоставщику","ЗаказПоставщику");
		обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.Количество<>0 Тогда
			//Снимаем заказ документа основания
			НаборЗаписейЗаказы = Движения.ЗаказыПоставщикамНаАвтомобили;
			НоваяЗапись = НаборЗаписейЗаказы.Добавить();
			НоваяЗапись.ВидДвижения     = ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период          = Дата;
			НоваяЗапись.Регистратор     = Ссылка;
			НоваяЗапись.Автомобиль      = Выборка.Автомобиль;
			НоваяЗапись.ЗаказПоставщику = Выборка.ЗаказПоставщику;
			НоваяЗапись.Количество      = -Выборка.Количество;
			НоваяЗапись.Сумма           = -Выборка.Сумма;
			НоваяЗапись.СуммаУпр        = -Выборка.СуммаУпр;
			НоваяЗапись.ХозОперация     = ХозОперация;
			Движения.ЗаказыПоставщикамНаАвтомобили.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуНаАвтомобильОтмена Тогда
		
		// Проверим нет ли другого заказа на автомобиль
		Отказ = ПроверитьЗаказыНаАвтомобиль(Автомобиль, ЭтотОбъект) ИЛИ Отказ;
		Если НЕ Отказ Тогда
			//Теперь зарегистрируем наш заказ
			НаборЗаписейЗаказы = Движения.ЗаказыПоставщикамНаАвтомобили;
			НоваяЗапись = НаборЗаписейЗаказы.Добавить();
			НоваяЗапись.ВидДвижения     = ВидДвиженияНакопления.Приход;
			НоваяЗапись.Период          = Дата;
			НоваяЗапись.Регистратор     = Ссылка;
			НоваяЗапись.Автомобиль      = Автомобиль;
			НоваяЗапись.ЗаказПоставщику = Ссылка;
			НоваяЗапись.Количество      = 1;
			НоваяЗапись.Сумма           = Окр(обПересчет(СуммаДокумента, ВалютаДокумента, КурсДокумента, ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Дата), 2);
			НоваяЗапись.СуммаУпр        = Окр(обПересчет(СуммаДокумента, ВалютаДокумента, КурсДокумента, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), Дата), 2);
			НоваяЗапись.ХозОперация     = ХозОперация;
		КонецЕсли;
		
	КонецЕсли;
	
	// двигаем границу последовательности заказы на автомобили
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ЗаказыПоставщикамНаАвтомобили.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.АвтомобильБыл) Тогда
		НаборЗаписейГраницыЗаказы = РегистрыСведений.ГраницыЗаказыНаАвтомобили.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаАвтомобилей = Движения.ЗаказыПоставщикамНаАвтомобили.Выгрузить();
			ТаблицаАвтомобилей.Свернуть("Автомобиль","");
			НаборЗаписейГраницыЗаказы.Автомобиль       = ТаблицаАвтомобилей.ВыгрузитьКолонку("Автомобиль");
			НаборЗаписейГраницыЗаказы.МоментВремени    = Дата;
			НаборЗаписейГраницыЗаказы.АвтомобильБыл    = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыЗаказы.Автомобиль       = ДополнительныеСвойства.АвтомобильБыл;
			НаборЗаписейГраницыЗаказы.МоментВремени    = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыЗаказы.АвтомобильБыл    = Неопределено;
			НаборЗаписейГраницыЗаказы.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыЗаказы.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыЗаказы.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


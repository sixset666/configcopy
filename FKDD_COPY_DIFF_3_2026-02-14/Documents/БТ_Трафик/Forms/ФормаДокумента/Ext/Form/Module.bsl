
&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.ПоказателиТрафика.Количество() = 0 ТОгда
		
		ЗаполнитьНаСервере();
		
	Иначе
		
		Оп = новый ОписаниеОповещения("ПослеВопроса",ЭтаФорма);
		ПоказатьВопрос(ОП,"В таблице есть данные, очистить?",РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура ПослеВопроса(Результат,ДопПараметры) Экспорт
	
	
	Если Результат = КодВозвратаДиалога.Да ТОгда
		
		Объект.ПоказателиТрафика.Очистить();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыТрафика.Ссылка КАК Ссылка
		|ИЗ
		|	Перечисление.БТ_ВидыТрафика КАК ВидыТрафика";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если Объект.ПоказателиТрафика.НайтиСтроки(Новый Структура("ВидТрафика",ВыборкаДетальныеЗаписи.Ссылка)).Количество() = 0 Тогда
		
			Объект.ПоказателиТрафика.Добавить().ВидТрафика = ВыборкаДетальныеЗаписи.Ссылка;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокВыбора()
	
	Возврат Справочники.ПодразделенияКомпании.ПолучитьСписокПодразделенийССП();
	
КонецФункции


&НаКлиенте
Процедура ПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ПолучитьСписокВыбора();
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

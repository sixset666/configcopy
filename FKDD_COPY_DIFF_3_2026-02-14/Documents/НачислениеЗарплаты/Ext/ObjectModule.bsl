// Модуль документа НачислениеЗарплаты

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы сотрудников
	ОбязательныеСотрудники=Новый Структура();
	ОбязательныеСотрудники.Вставить("Контрагент",1);
	ОбязательныеСотрудники.Вставить("Должность",1);	
	ОбязательныеСотрудники.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеСотрудники.Вставить("НачислениеУдержание",1);	
	//ОбязательныеСотрудники.Вставить("БазоваяСумма",1);
	//ОбязательныеСотрудники.Вставить("Сумма",1);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ДатаНачала",1);
	ОбязательныеРеквизиты.Вставить("ДатаКонца",1);
	ОбязательныеРеквизиты.Вставить("КоличествоРабочихДней",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);	
	ОбязательныеРеквизиты.Вставить("Сотрудники",ОбязательныеСотрудники);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = Истина;
	Если (НачалоДня(Дата) < НачалоДня(ДатаНачала)) ИЛИ (КонецДня(Дата) > КонецДня(ДатаКонца)) Тогда
		Ошибки = "Дата начала, окончания и дата документа должны находится в одном периоде!"; 
		Результат = Ложь;
	КонецЕсли;
	
	Если ДатаНачала > ДатаКонца Тогда
		Ошибки = "Неправильно задан период!"; 
		Результат = Ложь;
	КонецЕсли;
	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Сотрудники;
	ОшибкиРабочийПериод = "";
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		Если СтрокаТабличнойЧасти.Отработано > КоличествоРабочихДней Тогда 
			ОшибкиРабочийПериод = ОшибкиРабочийПериод + Символы.ПС
				+ "Строка №" + СтрокаТабличнойЧасти.НомерСтроки + ": количество отработанных дней больше количества рабочих дней в периоде";
			//Результат = Ложь;
		КонецЕсли; 
	КонецЦикла;
	Если ОшибкиРабочийПериод <> "" Тогда
		Сообщить(СокрЛП(ОшибкиРабочийПериод), СтатусСообщения.Внимание);
	КонецЕсли;
	
	Результат = Результат И дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
			
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыСотрудники = Новый Структура();
			КонтролируемыеРеквизитыСотрудники.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Сотрудники",КонтролируемыеРеквизитыСотрудники);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти);		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("НачислениеЗарплаты","Начисление заработной платы",Истина);
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	
	Если (Имя = "ДатаНачала") ИЛИ (Имя = "ДатаКонца") Тогда
		Если ДатаНачала > ДатаКонца Тогда
			#Если Клиент Тогда
				Сообщить("Дата начала периода не может быть больше даты его окончания!"); 
			#КонецЕсли
		КонецЕсли; 
		Возврат ОбработкаРеквизита("КоличествоРабочихДнейПересчет");
		
	ИначеЕсли Имя = "Дата" Тогда
		дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		ДатаНачала = НачалоМесяца(Дата);
		ДатаКонца = КонецМесяца(Дата);
		Возврат ОбработкаРеквизита("КоличествоРабочихДнейПересчет");
		
	ИначеЕсли Имя="Сотрудники.Контрагент" Тогда
		Если ТекСтрока.Контрагент.Пустая() ИЛИ ТекСтрока.Контрагент.ЭтоГруппа Тогда
			ТекСтрока.Контрагент			= Справочники.Контрагенты.ПустаяСсылка();
			ТекСтрока.ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			ТекСтрока.Должность             = Справочники.Должности.ПустаяСсылка();
		Иначе
			// Установим должность сотрудника.
			Если НЕ ТекСтрока.Контрагент.Сотрудник.Пустая() Тогда  				
				ТекСтрока.Должность = ТекСтрока.Контрагент.Сотрудник.Должность; 				
			КонецЕсли;
			
			// Получим основной договор для контрагента.
			ДопПараметры = Новый Структура;
			ВидДоговора = Перечисления.ВидыДоговоров.Зарплата;
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.Зарплата);
			ДопПараметры.Вставить("ВалютаВзаиморасчетов", ВалютаДокумента);
			ТекСтрока.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ТекСтрока.Контрагент,ВидДоговора,ЭтотОбъект);
			
			Возврат ОбработкаРеквизита("Сотрудники.Должность", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя="Сотрудники.НачислениеУдержание" ИЛИ Имя="Сотрудники.Должность" Тогда
		Если НЕ ТекСтрока.Контрагент.Пустая() Тогда
			// Получим оклад для сотрудника.
			СтруктураОтбора=Новый Структура("Сотрудник",ТекСтрока.Контрагент.Сотрудник);
			СтруктураОтбора.Вставить("Подразделение",       ПодразделениеКомпании);
			СтруктураОтбора.Вставить("Должность" ,          ТекСтрока.Должность);
			СтруктураОтбора.Вставить("НачислениеУдержание", ТекСтрока.НачислениеУдержание);
			СтруктураСведений = РегистрыСведений.СведенияОСотрудниках.ПолучитьПоследнее(Дата,СтруктураОтбора);
			
			Если ТекСтрока.НачислениеУдержание.СпособРасчета=Перечисления.СпособыРасчета.Процентом Тогда
				ТекСтрока.БазоваяСумма = СтруктураСведений.БазоваяСумма;
			Иначе
				ТекСтрока.БазоваяСумма = обПересчет(СтруктураСведений.БазоваяСумма, Константы.ВалютаУправленческогоУчетаКомпании.Получить(), КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
			КонецЕсли;
			
			Если ТекСтрока.НачислениеУдержание.СпособРасчета=Перечисления.СпособыРасчета.ПоДням Тогда
				ТекСтрока.Отработано = КоличествоРабочихДней;	
			Иначе
				ТекСтрока.Отработано = 0;
			КонецЕсли;
			
			Возврат ОбработкаРеквизита("Сотрудники.Отработано", ТекСтрока, ЭтаФорма);	
		КонецЕсли;
		
	ИначеЕсли Имя = "Сотрудники.Отработано" Тогда 
		Возврат ОбработкаРеквизита("РассчитатьСуммуВыплаты", ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя = "Сотрудники.БазоваяСумма" Тогда 
		Возврат ОбработкаРеквизита("РассчитатьСуммуВыплаты", ТекСтрока, ЭтаФорма);
		
	ИначеЕсли Имя = "РассчитатьСуммуВыплаты" Тогда 
		Если ТекСтрока.НачислениеУдержание.СпособРасчета = Перечисления.СпособыРасчета.Процентом Тогда
			БазоваяСуммаВнеш = обПересчет(ПолучитьБазовуюСумму(ТекСтрока), Константы.ВалютаУправленческогоУчетаКомпании.Получить(), КурсВалютыУпр, ВалютаДокумента, КурсДокумента);
			ТекСтрока.Сумма = (ТекСтрока.БазоваяСумма/100)*БазоваяСуммаВнеш;
		Иначе
			Если КоличествоРабочихДней > 0 Тогда
				Если ТекСтрока.НачислениеУдержание.СпособРасчета = Перечисления.СпособыРасчета.ПоДням Тогда
					ТекСтрока.Сумма = (ТекСтрока.Отработано/КоличествоРабочихДней)*ТекСтрока.БазоваяСумма;
				ИначеЕсли 
					ТекСтрока.НачислениеУдержание.СпособРасчета = Перечисления.СпособыРасчета.Суммой Тогда 
					ТекСтрока.Сумма = ТекСтрока.БазоваяСумма;
				КонецЕсли; 
			Иначе
				ТекСтрока.Сумма = 0;	
			КонецЕсли; 
		КонецЕсли; 			
		ТекСтрока.Сумма = Окр(ТекСтрока.Сумма, ОкруглятьНачислениеДо);
		
		#Если Клиент Тогда
			дкВывестиЗаголовокСуммаДокумента(ЭтаФорма);
		#КонецЕсли
		Возврат Истина;
		
	ИначеЕсли Имя = "КоличествоРабочихДнейПересчет" Тогда				
		ОдинДень = 60*60*24;
		СтароеКоличествоРабДней = КоличествоРабочихДней; 
		КоличествоРабочихДней = 0;
		ТекДень = ДатаНачала;
		Пока ТекДень <= ДатаКонца Цикл
			Если ДеньНедели(ТекДень)<6 Тогда
				КоличествоРабочихДней = КоличествоРабочихДней + 1;
			КонецЕсли; 
			ТекДень = ТекДень + ОдинДень;
		КонецЦикла;
		КоличествоРабДнейИзменилось = СтароеКоличествоРабДней <> КоличествоРабочихДней;
		//проверим нужно ли пересчитать ТЧ
		ЕстьПараметрПересчет = ТипЗнч(ДопПараметры) = Тип("Структура") И ДопПараметры.Свойство("НуженПересчет");
		
		Если КоличествоРабДнейИзменилось И (НЕ ЕстьПараметрПересчет ИЛИ (ЕстьПараметрПересчет И ДопПараметры.НуженПересчет)) Тогда
			Возврат ОбработкаРеквизита("КоличествоРабочихДней");
		Иначе
			Возврат Истина;
		КонецЕсли;
		
	ИначеЕсли Имя = "КоличествоРабочихДней" Тогда
		//спрашиваем когда строк больше 0
		ОтветПересчет = Сотрудники.Количество() > 0;
		#Если Клиент Тогда
			ОтветПересчет = ОтветПересчет И (Вопрос("Пересчитать табличную часть?",РежимДиалогаВопрос.ДаНет,,,"Пересчет таблицы")=КодВозвратаДиалога.Да);
		#КонецЕсли
		Если ОтветПересчет Тогда
			Рез=Истина;
			Для Каждого ТекСтрока Из Сотрудники Цикл
				Рез=ОбработкаРеквизита("РассчитатьСуммуВыплаты", ТекСтрока) И Рез;	
			КонецЦикла; 
		КонецЕсли;
		Возврат Рез;
		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);	
	КонецЕсли; 
КонецФункции // ОбработкаРеквизита()

// Расчет суммы документа
//
// Возвращаемое значение:
//  текСумма - число - сумма всего.
//
Функция РассчитатьСуммуВсего()Экспорт
	текСумма = 0;
	Для Каждого Стр Из Сотрудники Цикл
		коэф = ?(Стр.НачислениеУдержание.НачислениеИлиУдержание = Перечисления.НачислениеИлиУдержание.Начисление, 1, -1);
		текСумма = текСумма + коэф*Стр.Сумма;
	КонецЦикла; 
	Возврат текСумма;
КонецФункции // РассчитатьСуммуВсего()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
Функция ПолучитьБазовуюСуммуИзМакета(ТекСтрока, НаименованиеОбработки)

	//период начисления
	ДатаНачала_ = ДатаНачала;
	ДатаКонца_  = ДатаКонца;

	ТекМакет = ПолучитьМакет(НаименованиеОбработки);
	// получим пока все содержимое макета, возможно необходимо продумать использование областей
	ТекстЗапроса = ТекМакет.ПолучитьТекст();
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала_);
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца_);
	Запрос.УстановитьПараметр("Менеджер", ТекСтрока.Контрагент.Сотрудник);
	Запрос.УстановитьПараметр("Подразделение", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Должность", ТекСтрока.Должность);
    Запрос.УстановитьПараметр("НачислениеУдержание", ТекСтрока.НачислениеУдержание);

	//выполняем запрос
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
		
    Возврат ?(Выборка.БазоваяСумма = Неопределено,0,Выборка.БазоваяСумма);
	
КонецФункции // ПолучитьБазовуюСуммуИзМакета()

// получает базовую сумму из файла 
//
// Параметры:
//  ТекСтрока             - Строка для которой надо получить сумму,
//  НаименованиеОбработки - Строка - путь к обработке. 
//
Функция ПолучитьБазовуюСуммуИзФайла(ТекСтрока, НаименованиеОбработки)

	//период начисления
	ДатаНачала_ = ДатаНачала;
	ДатаКонца_  = ДатаКонца;
		
	//читаем файл с текстом запроса
	ТекстЗапроса = Новый ТекстовыйДокумент;
	ТекстЗапроса.Прочитать(НаименованиеОбработки,КодировкаТекста.ANSI);
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса.ПолучитьТекст();
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала_);
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца_);
	Запрос.УстановитьПараметр("Менеджер", ТекСтрока.Контрагент.Сотрудник);

	//выполняем запрос
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
		
    Возврат ?(Выборка.БазоваяСумма = Неопределено,0,Выборка.БазоваяСумма);

КонецФункции // ПолучитьБазовуюСуммуИзМакета()
#КонецЕсли

//Получает Базовую сумму для начисления процентом
//
// Параметры:
//  ТекСтрока - Строка для которой надо получить сумму
//
// Возвращаемое значение:
//  Возвращает "Базовую сумму" если запрос выполнился, иначе возвращает 0
//
Функция ПолучитьБазовуюСумму(ТекСтрока) Экспорт
	БазоваяСумма_ =0;
	ВидОбъекта = ТекСтрока.НачислениеУдержание.ВидОбъекта;
	НаименованиеОбработки = ТекСтрока.НачислениеУдержание.ПутьКОбъекту;
	Если ВидОбъекта=1 Тогда
		//Встроенный запрос из текстового макета
		Попытка
			#Если Клиент Тогда
			БазоваяСумма_ = ПолучитьБазовуюСуммуИзМакета(ТекСтрока, НаименованиеОбработки);
			#КонецЕсли
	    Исключение
			Возврат 0;
		КонецПопытки;
	ИначеЕсли ВидОбъекта=2 Тогда
		//Внешний запрос из текстового файла
		Попытка
			#Если Клиент Тогда
			БазоваяСумма_ = ПолучитьБазовуюСуммуИзФайла(ТекСтрока, НаименованиеОбработки);
			#КонецЕсли
	    Исключение
			Возврат 0;
		КонецПопытки;
	ИначеЕсли ВидОбъекта=3 Тогда
	//Встроенная обработка
		Попытка
			#Если Клиент Тогда
			ОбработкаПолученияБазовойСуммы = Обработки[НаименованиеОбработки].Создать();
			ОбработкаПолученияБазовойСуммы.Сотрудник = ТекСтрока.Контрагент.Сотрудник;
			ОбработкаПолученияБазовойСуммы.ДатаНачала = ДатаНачала;
			ОбработкаПолученияБазовойСуммы.ДатаКонца = ДатаКонца;
			БазоваяСумма_ = ОбработкаПолученияБазовойСуммы.БазоваяСумма();
			#КонецЕсли
		Исключение
			Возврат 0;
		КонецПопытки;
	ИначеЕсли ВидОбъекта=4 Тогда
	//Внешняя обработка
		Попытка
			#Если Клиент Тогда
			ОбработкаПолученияБазовойСуммы = ВнешниеОбработки.Создать(НаименованиеОбработки);
			ОбработкаПолученияБазовойСуммы.Сотрудник = ТекСтрока.Контрагент.Сотрудник;
			ОбработкаПолученияБазовойСуммы.ДатаНачала = ДатаНачала;
			ОбработкаПолученияБазовойСуммы.ДатаКонца = ДатаКонца;
			БазоваяСумма_ = ОбработкаПолученияБазовойСуммы.БазоваяСумма();			
			#КонецЕсли
		Исключение
			Возврат 0;
		КонецПопытки;
	КонецЕсли;
	Возврат БазоваяСумма_;
КонецФункции // ПолучитьБазовуюСумму()


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Начисление"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьНачисление(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("Начисление");

	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы", Права, , ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.Организация = Организация;
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ТекстДатаНачала = Формат(ДатаНачала, "ДФ=dd.MM.yyyy"); 
	ТекстДатаКонца = Формат(ДатаКонца, "ДФ=dd.MM.yyyy"); 
	ОбластьМакета.Параметры.Период = "Период с " + ТекстДатаНачала + " по " + ТекстДатаКонца + ". Количество рабочих дней: " + КоличествоРабочихДней;
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьМакета);
	
	Запрос=Новый Запрос("ВЫБРАТЬ
	                    |	НачислениеЗарплатыСотрудники.Контрагент КАК Контрагент,
	                    |	НачислениеЗарплатыСотрудники.НачислениеУдержание КАК НачислениеУдержание,
	                    |	НачислениеЗарплатыСотрудники.НачислениеУдержание.СтатьяДоходовИРасходов КАК СтатьяДоходовИРасходов,
	                    |	НачислениеЗарплатыСотрудники.НачислениеУдержание.СпособРасчета КАК СпособРасчета,
	                    |	ВЫБОР
	                    |		КОГДА НачислениеЗарплатыСотрудники.НачислениеУдержание.НачислениеИлиУдержание = &ЭтоНачисление
	                    |			ТОГДА ""+""
	                    |		ИНАЧЕ ""-""
	                    |	КОНЕЦ КАК ЗнакСуммы,
	                    |	СУММА(ВЫБОР
	                    |			КОГДА НачислениеЗарплатыСотрудники.НачислениеУдержание.НачислениеИлиУдержание = &ЭтоНачисление
	                    |				ТОГДА НачислениеЗарплатыСотрудники.Сумма
	                    |			ИНАЧЕ НачислениеЗарплатыСотрудники.Сумма * -1
	                    |		КОНЕЦ) КАК Сумма
	                    |ИЗ
	                    |	Документ.НачислениеЗарплаты.Сотрудники КАК НачислениеЗарплатыСотрудники
	                    |ГДЕ
	                    |	НачислениеЗарплатыСотрудники.Ссылка = &ДокументСсылка
	                    |
	                    |СГРУППИРОВАТЬ ПО
	                    |	НачислениеЗарплатыСотрудники.Контрагент,
	                    |	НачислениеЗарплатыСотрудники.НачислениеУдержание,
	                    |	НачислениеЗарплатыСотрудники.НачислениеУдержание.СтатьяДоходовИРасходов,
	                    |	НачислениеЗарплатыСотрудники.НачислениеУдержание.СпособРасчета,
	                    |	ВЫБОР
	                    |		КОГДА НачислениеЗарплатыСотрудники.НачислениеУдержание.НачислениеИлиУдержание = &ЭтоНачисление
	                    |			ТОГДА ""+""
	                    |		ИНАЧЕ ""-""
	                    |	КОНЕЦ,
	                    |	НачислениеЗарплатыСотрудники.НомерСтроки
	                    |
	                    |УПОРЯДОЧИТЬ ПО
	                    |	НачислениеЗарплатыСотрудники.НомерСтроки Возр
	                    |ИТОГИ
	                    |	СУММА(Сумма)
	                    |ПО
	                    |	Контрагент");
	Запрос.УстановитьПараметр("ДокументСсылка",Ссылка);
	Запрос.УстановитьПараметр("ЭтоНачисление",Перечисления.НачислениеИлиУдержание.Начисление);
	
	Выборка1 = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//Получаем область строки документа
	ОбластьСтрокаКонтрагент = Макет.ПолучитьОбласть("СтрокаКонтрагент");
	ОбластьСтрокаНачисление = Макет.ПолучитьОбласть("СтрокаНачисление");
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма", ВалютаДокумента, 0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	НомерСтроки = 0;
	СуммаВсего = 0;
	
	Пока Выборка1.Следующий() Цикл
		НомерСтроки = НомерСтроки + 1;
		
		ТабДокВрем = Новый ТабличныйДокумент;
		
		ОбластьСтрокаКонтрагент.Параметры.НомерСтроки = НомерСтроки;
		ОбластьСтрокаКонтрагент.Параметры.Контрагент = Выборка1.Контрагент;
		ОбластьСтрокаКонтрагент.Параметры.ЗнакСуммы = ?(Выборка1.Сумма > 0, "+", "-");
		ОбластьСтрокаКонтрагент.Параметры.Сумма = Формат(?(Выборка1.Сумма > 0, Выборка1.Сумма, -Выборка1.Сумма),ФорматВыводаСуммы);
		
		СуммаВсего = СуммаВсего + Выборка1.Сумма; 
		
		ТабДокВрем.Вывести(ОбластьСтрокаКонтрагент);
		
		Выборка2 = Выборка1.Выбрать();
		Пока Выборка2.Следующий() Цикл
			ОбластьСтрокаНачисление.Параметры.НачислениеУдержание = Выборка2.НачислениеУдержание;
			ОбластьСтрокаНачисление.Параметры.ЗнакСуммы = ?(Выборка2.Сумма > 0, "+", "-");
			ОбластьСтрокаНачисление.Параметры.Сумма = Формат(?(Выборка2.Сумма > 0, Выборка2.Сумма, -Выборка2.Сумма),ФорматВыводаСуммы);
			ТабДокВрем.Вывести(ОбластьСтрокаНачисление);
		КонецЦикла;
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если НомерСтроки = Выборка1.Количество() Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ТабДокВрем, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента, Сумма", ВалютаДокумента, 0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(Выборка1, СтруктураИтоговПоСтранице);
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;

	ОбластьПодвал.Параметры.Всего = Формат(СуммаВсего, ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	ОбластьПодвал.Параметры.СуммаПрописью = обЧислоПрописью(СуммаВсего, ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьПодвал);

	Возврат ТабДокумент;
КонецФункции // ПечатьНачисление()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Получить способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// Суммовые разницы необходимо списывать в разрезе подразделений договоров. Поэтому используем таблицу значений.
	ТаблицаСуммовыхРазниц = Новый ТаблицаЗначений;
	ТаблицаСуммовыхРазниц.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаСуммовыхРазниц.Колонки.Добавить("СуммаУпр",      Новый ОписаниеТипов("Число"));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НачислениеЗарплатыСотрудники.Контрагент КАК Контрагент,
	|	НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов.Подразделение КАК Подразделение,
	|	СУММА(ВЫБОР
	|			КОГДА НачислениеЗарплатыСотрудники.НачислениеУдержание.НачислениеИлиУдержание = &ЭтоНачисление
	|				ТОГДА НачислениеЗарплатыСотрудники.Сумма
	|			ИНАЧЕ -НачислениеЗарплатыСотрудники.Сумма
	|		КОНЕЦ) КАК Сумма
	|ИЗ
	|	Документ.НачислениеЗарплаты.Сотрудники КАК НачислениеЗарплатыСотрудники
	|ГДЕ
	|	НачислениеЗарплатыСотрудники.Ссылка = &ДокументСсылка
	|	И НачислениеЗарплатыСотрудники.Сумма <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеЗарплатыСотрудники.Контрагент,
	|	НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов,
	|	НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов.Подразделение";
	Запрос = Новый Запрос(ТекстЗапроса);					
	Запрос.УстановитьПараметр("ДокументСсылка",Ссылка);
	Запрос.УстановитьПараметр("ЭтоНачисление",Перечисления.НачислениеИлиУдержание.Начисление);
	РезультатЗапросаПоСотрудникам = Запрос.Выполнить();
	Если НЕ РезультатЗапросаПоСотрудникам.Пустой() Тогда
		НаборЗаписейВзаиморасчеты = Движения.ВзаиморасчетыКомпании;
		Выборка = РезультатЗапросаПоСотрудникам.Выбрать();
		Пока Выборка.Следующий() И НЕ Отказ Цикл
			НаборЗаписейВзаиморасчеты.ДокументОбъект 		= ЭтотОбъект;
			НаборЗаписейВзаиморасчеты.РежимПроведения		= Режим;
			НаборЗаписейВзаиморасчеты.Контрагент 			= Выборка.Контрагент;
			НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			НаборЗаписейВзаиморасчеты.Сделка 				= Неопределено;
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Неопределено;
			НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
			Если Выборка.Сумма > 0 Тогда
				НаборЗаписейВзаиморасчеты.Сумма 			= Выборка.Сумма;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
			ИначеЕсли Выборка.Сумма < 0 Тогда				
				НаборЗаписейВзаиморасчеты.Сумма 			= -Выборка.Сумма;
				Отказ = НЕ НаборЗаписейВзаиморасчеты.Приход() ИЛИ Отказ;
			КонецЕсли; 
			
			// доходы и расходы по суммовым разницам
			СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
			Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
				НоваяСтрока = ТаблицаСуммовыхРазниц.Добавить();
				Если БалансВедетсяПоПодразделениям Тогда
					НоваяСтрока.Подразделение = Выборка.Подразделение;
				Иначе
					НоваяСтрока.Подразделение = ПодразделениеКомпании;
				КонецЕсли;
				НоваяСтрока.СуммаУпр      = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Суммовые разницы.
	ТаблицаСуммовыхРазниц.Свернуть("Подразделение", "СуммаУпр");
	Для Каждого ТекСтрока Из ТаблицаСуммовыхРазниц Цикл
		Если ТекСтрока.СуммаУпр<>0 Тогда
			НаборЗаписейДиР 						= Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект	 		= ЭтотОбъект.Ссылка;
			НаборЗаписейДиР.Подразделение	 		= ТекСтрока.Подразделение; 
			НаборЗаписейДиР.СтатьяДоходовИРасходов 	= Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте 				= Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц < 0 Тогда
				НаборЗаписейДиР.Расход = -ТекСтрока.СуммаУпр;
			Иначе
				НаборЗаписейДиР.Доход  = ТекСтрока.СуммаУпр;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;	
	КонецЦикла;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов.Подразделение КАК Подразделение,
	|	НачислениеЗарплатыСотрудники.НачислениеУдержание.СтатьяДоходовИРасходов КАК СтатьяДоходовИРасходов,
	|	СУММА(ВЫБОР
	|			КОГДА НачислениеЗарплатыСотрудники.НачислениеУдержание.НачислениеИлиУдержание = &ЭтоНачисление
	|				ТОГДА НачислениеЗарплатыСотрудники.Сумма
	|			ИНАЧЕ -НачислениеЗарплатыСотрудники.Сумма
	|		КОНЕЦ) КАК Сумма
	|ИЗ                                                                                           
	|	Документ.НачислениеЗарплаты.Сотрудники КАК НачислениеЗарплатыСотрудники
	|ГДЕ
	|	НачислениеЗарплатыСотрудники.Ссылка = &ДокументСсылка
	|	И НачислениеЗарплатыСотрудники.Сумма <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|   НачислениеЗарплатыСотрудники.ДоговорВзаиморасчетов.Подразделение,
	|	НачислениеЗарплатыСотрудники.НачислениеУдержание.СтатьяДоходовИРасходов";
		
	Запрос = Новый Запрос(ТекстЗапроса);				
	Запрос.УстановитьПараметр("ДокументСсылка",Ссылка);
	Запрос.УстановитьПараметр("ЭтоНачисление",Перечисления.НачислениеИлиУдержание.Начисление);
	РезультатЗапросаПоСтатьям = Запрос.Выполнить();
	Если НЕ РезультатЗапросаПоСтатьям.Пустой() Тогда
		ФлагУпрВалюты   = (ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
		НаборЗаписейДиР	= Движения.ДоходыИРасходы;
		Выборка = РезультатЗапросаПоСтатьям.Выбрать();
		Пока Выборка.Следующий() И НЕ Отказ Цикл
			НаборЗаписейДиР.ДокументОбъект 			= ЭтотОбъект.Ссылка;
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = Выборка.Подразделение;
			КонецЕсли;
			НаборЗаписейДиР.СтатьяДоходовИРасходов	= Выборка.СтатьяДоходовИРасходов;
			НаборЗаписейДиР.ВУпрВалюте				= ФлагУпрВалюты;
			НаборЗаписейДиР.Расход					= Выборка.Сумма;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЦикла;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

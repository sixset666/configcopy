// Модуль документа ВводОстатковВзаиморасчетов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеСостав=Новый Структура();
	ОбязательныеСостав.Вставить("Контрагент",3);
	ОбязательныеСостав.Вставить("ДоговорВзаиморасчетов",3);
	ОбязательныеСостав.Вставить("Сделка",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("Состав",ОбязательныеСостав);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат = дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
			
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);

		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
						
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыСостава = Новый Структура();
			КонтролируемыеРеквизитыСостава.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			ТабличныеЧасти = Новый Структура();
			ТабличныеЧасти.Вставить("Состав",КонтролируемыеРеквизитыСостава);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыТабличныхЧастей",ТабличныеЧасти); 			
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВводОстатковВзаиморасчетов","Ввод остатков взаиморасчетов",Истина);

	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// Расчет суммы документа
//
// Возвращаемое значение;
//  возвращает сумму - число - расчитывает суммы колонок "СуммаДебет" и "СуммаКредит" и возвращает их сумму.
//
Функция РассчитатьСуммуВсего() Экспорт
	ВремСуммаДебет=Состав.Итог("СуммаДебет");
	Если СуммаДебет<>ВремСуммаДебет Тогда СуммаДебет=ВремСуммаДебет; КонецЕсли; 
	ВремСуммаКредит=Состав.Итог("СуммаКредит");
	Если СуммаКредит<>ВремСуммаКредит Тогда СуммаКредит=ВремСуммаКредит; КонецЕсли; 
	Возврат СуммаДебет+СуммаКредит;
КонецФункции // РассчитатьСуммуВсего()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			- Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	- Форма	 - Ссылка на форму документа. Если значение неопределено,
//							производится программная обработка реквизитов.
//  ТекСтрока	- СтрокаТабличнойЧасти - Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры- Структура			- Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   - Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Если Имя="Состав.Контрагент" Тогда
		
		Если обЗначениеНеЗаполнено(ТекСтрока.ДоговорВзаиморасчетов) ИЛИ ТекСтрока.ДоговорВзаиморасчетов.Владелец<>ТекСтрока.Контрагент Тогда
			ТекСтрока.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ТекСтрока.Контрагент, , ЭтотОбъект, ДопПараметры);
			Возврат ОбработкаРеквизита("Состав.ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли Имя="Состав.Сделка" И (НЕ ТекСтрока = Неопределено) Тогда
		Если ТипЗнч(ТекСтрока.Сделка) = Тип("ДокументСсылка.СчетНаОплату") Тогда
			ТекСтрока.ДоговорВзаиморасчетов = ТекСтрока.Сделка.ДоговорВзаиморасчетов;
			
			ТекСтрока.СуммаКредит = обПересчет(ТекСтрока.Сделка.СуммаДокумента, ТекСтрока.Сделка.ВалютаДокумента, Дата, ВалютаДокумента, КурсДокумента);
			РассчитатьСуммуВсего();
			
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		// пересчитаем курсы взаиморасчетов в табличной части "Состав"
		Для Каждого ТекСтрока Из Состав Цикл
			Если ТекСтрока.ДоговорВзаиморасчетов.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			спрСтрокаСоставВалютаВзаиморасчетов = ТекСтрока.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
			// если валюта взаиморасчета в строке и валюта документа совпадают,
			// установим курс взаиморасчета равным курсу документа
			Если спрСтрокаСоставВалютаВзаиморасчетов=ВалютаДокумента Тогда
				ТекСтрока.КурсВалютыВзаиморасчетов = КурсДокумента;
				ОбработкаРеквизита("Состав.КурсВалютыВзаиморасчетов", ТекСтрока, ЭтаФорма);
			КонецЕсли;
		КонецЦикла;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры


#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

// Формирует печатную форму "Остатки взаиморасчетов"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьОстаткиВзаиморасчетов(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ОстаткиВзаимозачетов");
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);	
	
	//определяем шапку ТЧ, которая будет повторяться на всех страницах документа
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	//реквизиты шапки документа
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеОрганизация = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	ОбластьМакета.Параметры.Организация = Организация;
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ОбластьМакета.Параметры.ВалютаДокумента = ВалютаДокумента;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//шапка
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//Итоги по странице
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("СуммаДебет, СуммаКредит",0,0);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;	
	
	//Подвал
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 		
	
	ВыборкаТабличнойЧасти = ЭтотОбъект.Состав;	
	//строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		ОбластьМакета.Параметры.НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки;
		ОбластьМакета.Параметры.Контрагент = СтрокаТабличнойЧасти.Контрагент;
		ОбластьМакета.Параметры.ДоговорВзаиморасчетов = СтрокаТабличнойЧасти.ДоговорВзаиморасчетов;
		ОбластьМакета.Параметры.СуммаДебет = Формат(СтрокаТабличнойЧасти.СуммаДебет,ФорматВыводаСуммы);
		ОбластьМакета.Параметры.СуммаКредит = Формат(СтрокаТабличнойЧасти.СуммаКредит,ФорматВыводаСуммы);
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			НомерСтраницыПред = НомерСтраницы;
			СтруктураИтоговПоСтранице = Новый Структура("СуммаДебет, СуммаКредит",0,0);
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);		
	КонецЦикла;
	
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;		
	
	//итоги	
	ИтогоСуммаДебет = Состав.Итог("СуммаДебет");
	ИтогоСуммаКредит = Состав.Итог("СуммаКредит");
	ОбластьПодвал.Параметры.ИтогоСуммаДебет = Формат(ИтогоСуммаДебет,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.ИтогоСуммаКредит = Формат(ИтогоСуммаКредит,ФорматВыводаСуммы);
	
	//сумма прописью
	ОбластьПодвал.Параметры.ИтогоСуммаДебетПрописью = обЧислоПрописью(ИтогоСуммаДебет,ЭтотОбъект.ВалютаДокумента);
	ОбластьПодвал.Параметры.ИтогоСуммаКредитПрописью = обЧислоПрописью(ИтогоСуммаКредит,ЭтотОбъект.ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьПодвал);
															   
	//зададим параметры макета
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева  = 0;
	ТабДокумент.ПолеСнизу  = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
	Возврат ТабДокумент;
КонецФункции // ПечатьОстаткиВзаиморасчетов()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	// получим старые значения, влияющие на границы последовательности
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// проводим
	НаборЗаписейВзаиморасчетыКомпании=Движения.ВзаиморасчетыКомпании;
	НаборЗаписейВзаиморасчетыКомпании.РежимПроведения=Режим;
	
	НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
	НаборЗаписейДоходыИРасходы.ВУпрВалюте=(ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	
	Дебет=0; Кредит=0; 
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделению = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// Созадим временную таблицу, для накопления движений по регистру "Доходы и расходы".
	// Ее назначение - уменьшить количество записей по регистру. 
	ТаблицаВзаиморасчетов = Новый ТаблицаЗначений;
	ТаблицаВзаиморасчетов.Колонки.Добавить("Подразделение",  Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпрДебет",  Новый ОписаниеТипов("Число"));	
	ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпрКредит", Новый ОписаниеТипов("Число"));
	ТаблицаВзаиморасчетов.Колонки.Добавить("СуммаУпрСуммоваяРазница", Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТЧ Из Состав Цикл
		СуммаДоходаРасходаСуммовыхРазниц = 0;
		Сделка = СтрокаТЧ.Сделка;
		Если СтрокаТЧ.СуммаДебет<>0 Тогда
			//Проведем задолженность контрагента по взаиморасчетам
			НаборЗаписейВзаиморасчетыКомпании.ДокументОбъект        = ЭтотОбъект;
			НаборЗаписейВзаиморасчетыКомпании.Контрагент            = СтрокаТЧ.Контрагент;
			НаборЗаписейВзаиморасчетыКомпании.КурсВзаиморасчетов    = СтрокаТЧ.КурсВалютыВзаиморасчетов;
			НаборЗаписейВзаиморасчетыКомпании.ДоговорВзаиморасчетов = СтрокаТЧ.ДоговорВзаиморасчетов;
			
			Если ЗначениеЗаполнено(Сделка)Тогда
				НаборЗаписейВзаиморасчетыКомпании.Сделка = Сделка;
				//ТипСделка = ТипЗнч(Сделка);
				//Если орПолучитьТипыСделок(Истина).СодержитТип(ТипСделка) Тогда
				//	НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем = Истина;
				//ИначеЕсли орПолучитьТипыСделок(Ложь).СодержитТип(ТипСделка) Тогда
				//	НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем = Ложь;
				//Иначе
				//	НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем = Неопределено;
				//КонецЕсли; 
				НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Ложь;
			Иначе
				НаборЗаписейВзаиморасчетыКомпании.Сделка = Неопределено;
			КонецЕсли;
			
			НаборЗаписейВзаиморасчетыКомпании.Сумма                 = СтрокаТЧ.СуммаДебет;
			НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц = 0;
			Отказ = НЕ НаборЗаписейВзаиморасчетыКомпании.Приход() ИЛИ Отказ;
			Дебет = Дебет + СтрокаТЧ.СуммаДебет;
			СуммаДоходаРасходаСуммовыхРазниц = НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли;
		
		Если СтрокаТЧ.СуммаКредит<>0 Тогда
			//Проведем задолженность перед контрагентом по взаиморасчетам
			НаборЗаписейВзаиморасчетыКомпании.ДокументОбъект            = ЭтотОбъект;
			НаборЗаписейВзаиморасчетыКомпании.Контрагент                = СтрокаТЧ.Контрагент;
			НаборЗаписейВзаиморасчетыКомпании.КурсВзаиморасчетов        = СтрокаТЧ.КурсВалютыВзаиморасчетов; 
			НаборЗаписейВзаиморасчетыКомпании.ДоговорВзаиморасчетов     = СтрокаТЧ.ДоговорВзаиморасчетов;
			
			Если ЗначениеЗаполнено(Сделка)Тогда
				НаборЗаписейВзаиморасчетыКомпании.Сделка = Сделка;
				//ТипСделка=ТипЗнч(Сделка);
				//Если орПолучитьТипыСделок(Ложь).СодержитТип(ТипСделка) Тогда
				//	НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Ложь;
				//ИначеЕсли орПолучитьТипыСделок(Истина).СодержитТип(ТипСделка) Тогда
				//	НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Истина;
				//Иначе
				//	НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Неопределено;
				//КонецЕсли;
				НаборЗаписейВзаиморасчетыКомпании.ВзаиморасчетыСПокупателем=Истина;
			Иначе
				НаборЗаписейВзаиморасчетыКомпании.Сделка=Неопределено;
			КонецЕсли;
			
			НаборЗаписейВзаиморасчетыКомпании.Сумма                     = СтрокаТЧ.СуммаКредит;
			НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц = 0;
			Отказ  = НЕ НаборЗаписейВзаиморасчетыКомпании.Расход() ИЛИ Отказ;
			Кредит = Кредит + СтрокаТЧ.СуммаКредит;
			СуммаДоходаРасходаСуммовыхРазниц = СуммаДоходаРасходаСуммовыхРазниц + НаборЗаписейВзаиморасчетыКомпании.СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли; 
		Если СтрокаТЧ.СуммаДебет<>0 ИЛИ СтрокаТЧ.СуммаКредит<>0 Тогда
			НоваяСтрока = ТаблицаВзаиморасчетов.Добавить();	
			Если БалансВедетсяПоПодразделению Тогда
				НоваяСтрока.Подразделение = СтрокаТЧ.ДоговорВзаиморасчетов.Подразделение;
			Иначе
				// Если баланс ведется не по подразделениям, то в качестве подразделения должно быть
				// подразделение документа (шапки).
				НоваяСтрока.Подразделение = ПодразделениеКомпании;
			КонецЕсли;
			НоваяСтрока.СуммаУпрДебет           = СтрокаТЧ.СуммаДебет;
			НоваяСтрока.СуммаУпрКредит          = СтрокаТЧ.СуммаКредит;
			НоваяСтрока.СуммаУпрСуммоваяРазница = СуммаДоходаРасходаСуммовыхРазниц;
		КонецЕсли; 
	КонецЦикла;
	
	// Создаем движения по регистру "Доходы и расходы"
	
	ТаблицаВзаиморасчетов.Свернуть("Подразделение", "СуммаУпрДебет, СуммаУпрКредит, СуммаУпрСуммоваяРазница");
	
	Для Каждого ТекСтрока Из ТаблицаВзаиморасчетов Цикл  
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = ТекСтрока.Подразделение;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.ВводОстатковВзаиморасчетов;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.Доход                  = ТекСтрока.СуммаУпрДебет;
		НаборЗаписейДоходыИРасходы.Расход                 = ТекСтрока.СуммаУпрКредит;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ; 
		
		// Теперь суммовые разницы
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		НаборЗаписейДоходыИРасходы.Подразделение          = ТекСтрока.Подразделение;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте = Истина;
		Если ТекСтрока.СуммаУпрСуммоваяРазница<0 Тогда
			НаборЗаписейДоходыИРасходы.Расход = -ТекСтрока.СуммаУпрСуммоваяРазница;
			НаборЗаписейДоходыИРасходы.Доход  = 0;
		Иначе
			НаборЗаписейДоходыИРасходы.Доход  = ТекСтрока.СуммаУпрСуммоваяРазница;
			НаборЗаписейДоходыИРасходы.Расход = 0;
		КонецЕсли;
		Отказ = НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ; 		
	КонецЦикла;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль документа ВозвратОтПокупателя

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Кэш права "РежимВыводаКодаВДокументах". Служит для настройки вывода кода/артикула в ТЧ
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ОбработкаРасчетСкидок Экспорт; // Переменная для хранения ссылки на обработку "Расчет скидок". Данная обработка содержит необходимые
                                     // свойства и методы для расчета скидок документа.
Перем СообщениеОбОшибке Экспорт;	// Переменная для накопления результатов записи. Если = неопределено, то вывод будет на экран, иначе тут сообщения об ошибках.

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение:
//  Результат - соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Результат.Вставить(Перечисления.ВидыНоменклатуры.Услуга,0);
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("КоличествоБазовое",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);
	ОбязательныеТовары.Вставить("ДокументПродажи",2);
	ОбязательныеТовары.Вставить("Партия",2);
	ОбязательныеТовары.Вставить("ГТД",2);
	
	// Обязательные поля таблицы "Коды маркировки"
	ОбязательныеКодыМаркировки = Новый Структура();
	ОбязательныеКодыМаркировки.Вставить("КодМаркировки",3);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	ОбязательныеРеквизиты.Вставить("КодыМаркировки", ОбязательныеКодыМаркировки);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	// Здесь могут быть определены дополнительные к стандартным обязательным реквизитам условия проверки
	Результат=дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина);
	
	Если Результат Тогда
		ЗапрещеннаяНоменклатура=ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
		Если ЗапрещеннаяНоменклатура.Количество()>0 Тогда
			Для Каждого СтрокаТоваров Из Товары Цикл
				Попытка ЕстьОшибка=(ЗапрещеннаяНоменклатура[СтрокаТоваров.Номенклатура.ВидНоменклатуры]<>Неопределено); Исключение ЕстьОшибка=Ложь; КонецПопытки;
				Если ЕстьОшибка Тогда
					Результат=Ложь;
					дкДобавитьОшибку(Ошибки,"Таблица <Товары> значение колонки <Номенклатура> не может содержать услугу ! Строка номер "+СокрЛП(СтрокаТоваров.НомерСтроки));
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	
	Если (НЕ обЗначениеНеЗаполнено(ДокументОснование)) И (ТипЗнч(ДокументОснование)<>Тип("ДокументСсылка.ЗакрытиеСмены")) Тогда
		Если ДокументОснование.Контрагент<>Контрагент Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Контрагент документа основания отличается от контрагента текущего документа ");
		КонецЕсли;
		
		Если ДокументОснование.ДоговорВзаиморасчетов<>ДоговорВзаиморасчетов Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки,"Договор взаиморасчетов документа основания отличается от договора взаиморасчетов текущего документа ");
		КонецЕсли;
	КонецЕсли;
	
	// Проведем проверку соответствия организации документа организациям некоторых реквизитов документа.
	// Такую проверку необходимо осуществлять, если баланс в системе ведется по организации,
	// или если установлен способ контроля корректности
	// договора и склада документа. Причем, если способ контроля выбран "Контроль по организации и подразделению", то
	// дополнительно осуществляется проверка соответствия подразделений.
	Если Результат Тогда
		СпособКонтроляКорректности = обПраво("КонтрольКорректностиДоговораИСкладаДокумента", Права,,ЭтотОбъект);	
		КонтрольОтсутствует        = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольОтсутствует);
		БалансВедетсяПоОрганизации = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоОрганизации);
		
		Если БалансВедетсяПоОрганизации ИЛИ НЕ КонтрольОтсутствует Тогда
			КонтрольПоПодразделению = (СпособКонтроляКорректности=Перечисления.СпособКонтроляКорректностиДоговораИСкладаДокумента.КонтрольПоОрганизацииИПодразделению);
			
			КонтролируемыеРеквизиты = Новый Структура();
			
			КонтролируемыеРеквизитыШапки = Новый Структура();
			КонтролируемыеРеквизитыШапки.Вставить("СкладКомпании",         КонтрольПоПодразделению);
			КонтролируемыеРеквизитыШапки.Вставить("ДоговорВзаиморасчетов", КонтрольПоПодразделению);
			КонтролируемыеРеквизиты.Вставить("КонтролируемыеРеквизитыШапки",КонтролируемыеРеквизитыШапки);   		
			
			Результат = дкПроверитьКорректностьПоОрганизации(ЭтотОбъект,КонтролируемыеРеквизиты,Ошибки) И Результат;
		КонецЕсли;  		
	КонецЕсли;
	
	// Проверим, а не указал ли пользователь отрицательные количества.
	Для Каждого ТекСтрокаТЧ Из Товары Цикл  	
		Если ТекСтрокаТЧ.Количество<0 Тогда
			Результат = Ложь;
			дкДобавитьОшибку(Ошибки, "Ошибка! Табличная часть <Товары>, отрицательное значение количества. Строка: " + ТекСтрокаТЧ.НомерСтроки);	
		КонецЕсли;
	КонецЦикла;    
	
	Результат = дкПроверитьКорректностьТоваровКомиссионныхДокументов(ЭтотОбъект,Ошибки) И Результат;
	
	Результат = Результат И дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, Ошибки, Истина);
	
	дкПроверитьКорректностьРНПТ(ЭтотОбъект);
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ВозвратТоваровОтПокупателя","Возврат товаров от покупателя",Истина);
	СписокХозОпераций.Добавить("ВозвратТоваровОтПокупателяКомиссия","Возврат товаров от покупателя (комиссия)");
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// возвращает выборку по шапке
//
// Параметры:
//  ДокументСсылка - Документ.Ссылка - Ссылка на документ для которого получаем шапку
//
// Возвращаемое значение:
//  Выборка - возвращает выборку по шапке
//
Функция ПолучитьШапкуДокумента(ДокументСсылка)
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Дата КАК Дата,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсДокумента КАК КурсДокумента,
	|	Док.ТипЦен КАК ТипЦен,
	|	Док.ХозОперация КАК ХозОперация,
	|	Док.КурсВалютыУпр КАК КурсВалютыУпр,
	|	Док.КурсВалютыВзаиморасчетов КАК КурсВалютыВзаиморасчетов,
	|	Док.МоментВремени КАК МоментВремени,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Док.Проект КАК Проект,
	/////////// ПРИВАТ ////////////
	|	Док.СкладКомпании КАК СкладКомпании,
	|	Док.СкладКомпании.Розничный КАК СкладКомпанииРозничный,
	|	Док.СкладКомпании.ТипЦенРозничнойТорговли КАК СкладКомпанииТипЦенРозничнойТорговли,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	Док.ДокументОснование КАК ДокументОснование,
	|	Док.ТипЦен.ЦенаВключаетНДС КАК ТипЦенЦенаВключаетНДС,
	|	ЕСТЬNULL(ДокТовары.СуммаВсего,0) КАК СуммаВсего
	|
	|ИЗ
	|	Документ."+Метаданные().Имя+" КАК Док
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ СУММА(СуммаВсего) КАК СуммаВсего ИЗ Документ."+Метаданные().Имя+".Товары ГДЕ Ссылка=&Ссылка) КАК ДокТовары
	|	ПО Истина
	|
	|ГДЕ
	|	Док.Ссылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Выборка=Запрос.Выполнить().Выбрать(); Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции // ПолучитьШапкуДокумента()

// получаем таблицу товаров которые надо оприходовать на остатки
//
// Возвращаемое значение:
//  ТаблицаТоваров - таблица значений.
//
Функция ПолучитьТаблицуТоваров()
	// нам фактически надо сформировать таблицу товаров которые были проданы, что бы их заново оприходовать
	// все из-за автоматического списания характеристик
	ЗапросСписание=Новый Запрос("
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпании.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпании.Регистратор КАК ДокументПродажи,
	|	ОстаткиТоваровКомпании.Количество КАК Количество
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
	|ГДЕ
	|	ОстаткиТоваровКомпании.Регистратор В (&Регистраторы)
	|	И ОстаткиТоваровКомпании.Номенклатура В (&Номенклатура)
	|	И ОстаткиТоваровКомпании.ВидДвижения = &ВидДвижения
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ОстаткиТоваровКомпании
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОстаткиТоваровКомпании.НомерСтроки Убыв");
	ЗапросСписание.УстановитьПараметр("Регистраторы",Товары.ВыгрузитьКолонку("ДокументПродажи"));
	ЗапросСписание.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
	ЗапросСписание.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Расход);
	
	// наложим блокировку на считываемые данные
	СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ОстаткиТоваровКомпании");
	ЗначенияБлокировки = Новый Соответствие;
	ЗначенияБлокировки.Вставить("Период",Новый Диапазон(,Дата)); 
	ЗначенияБлокировки.Вставить("СкладКомпании",СкладКомпании); 
	СтруктураПараметровБлокировки.Вставить("ИсточникДанных",Товары);
	ОписаниеИсточника = Новый Соответствие;
	ОписаниеИсточника.Вставить("Номенклатура","Номенклатура");
	обУстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
	
	ТаблицаСписания=ЗапросСписание.Выполнить().Выгрузить();
	// получим таблицу документа
	ТаблицаДокумента=Товары.Выгрузить();
	ТаблицаДокумента.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,ДокументПродажи","КоличествоБазовое, ЦенаРозничная");
	ТаблицаДокумента.Сортировать("Номенклатура Возр,ХарактеристикаНоменклатуры Убыв");
	
	// создадим и заполним таблицу товаров
	ТаблицаТоваров=Новый ТаблицаЗначений();
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТаблицаТоваров.Колонки.Добавить("СкладКомпании");
	ТаблицаТоваров.Колонки.Добавить("Количество");
	ТаблицаТоваров.Колонки.Добавить("Резерв");
	ТаблицаТоваров.Колонки.Добавить("ЦенаРозничная");
	
	ТаблицаРучныхХарактеристик = дкПолучитьНоменклатуруСРучнымСписаниемХарактеристик(Ссылка);
	Розничный = СкладКомпании.Розничный;
	//ТипРозничныхЦен = СкладКомпании.ТипЦенРозничнойТорговли;
	//ВалютаРегл=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	//СтруктураКурса = обКурсДляВалюты(ВалютаРегл,Дата);
	//КурсРегл = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Для Каждого СтрокаТовар Из ТаблицаДокумента Цикл
		Если СтрокаТовар.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга Тогда
			Продолжить;
		КонецЕсли; 
		СтуктураОтбора=Новый Структура("Номенклатура,ДокументПродажи",СтрокаТовар.Номенклатура,СтрокаТовар.ДокументПродажи);
		Если НЕ обЗначениеНеЗаполнено(СтрокаТовар.ХарактеристикаНоменклатуры) ИЛИ (ТаблицаРучныхХарактеристик<>Неопределено И ТаблицаРучныхХарактеристик.Найти(СтрокаТовар.Номенклатура,"Номенклатура")<>Неопределено) Тогда
			СтуктураОтбора.Вставить("ХарактеристикаНоменклатуры",СтрокаТовар.ХарактеристикаНоменклатуры);
		КонецЕсли;
		МассивНайденныхСтрок = ТаблицаСписания.НайтиСтроки(СтуктураОтбора);
		НадоВернуть    = СтрокаТовар.КоличествоБазовое;
		Для Сч=0 по МассивНайденныхСтрок.ВГраница() Цикл
			
			ТекСтрока = МассивНайденныхСтрок[Сч];
			КоличествоКПоступлению = Мин(НадоВернуть, ТекСтрока.Количество);
			
			НоваяСтрока=ТаблицаТоваров.Добавить();
			НоваяСтрока.Номенклатура               = ТекСтрока.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
			НоваяСтрока.СкладКомпании              = СкладКомпании;
			НоваяСтрока.Количество                 = КоличествоКПоступлению;
			НоваяСтрока.Резерв                     = 0;
			Если Розничный Тогда
				//НоваяСтрока.ЦенаРозничная = обПолучитьЦену(ТипРозничныхЦен,НоваяСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаРегл,КурсРегл, ТекСтрока.ХарактеристикаНоменклатуры, СтрокаТовар.ЕдиницаИзмерения, СкладКомпании.Подразделение);
				НоваяСтрока.ЦенаРозничная = СтрокаТовар.ЦенаРозничная;
			Иначе
				НоваяСтрока.ЦенаРозничная = 0;
			Конецесли;
			
			Если ТекСтрока.Количество > НадоВернуть Тогда
				ТекСтрока.Количество = ТекСтрока.Количество - НадоВернуть;
			Иначе
				// Удалим ненужную строку
				ТаблицаСписания.Удалить(ТекСтрока);
			КонецЕсли;
			
			НадоВернуть = НадоВернуть - КоличествоКПоступлению;
			Если НадоВернуть<=0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		// Может еще что-то осталось
		Если НадоВернуть>0 Тогда
			НоваяСтрока=ТаблицаТоваров.Добавить();
			НоваяСтрока.Номенклатура               = СтрокаТовар.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТовар.ХарактеристикаНоменклатуры;
			НоваяСтрока.СкладКомпании              = СкладКомпании;
			НоваяСтрока.Количество                 = НадоВернуть;
			НоваяСтрока.Резерв                     = 0;
			Если Розничный Тогда
				//НоваяСтрока.ЦенаРозничная = обПолучитьЦену(ТипРозничныхЦен,НоваяСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаРегл,КурсРегл, СтрокаТовар.ХарактеристикаНоменклатуры, СтрокаТовар.ЕдиницаИзмерения, СкладКомпании.Подразделение);
				НоваяСтрока.ЦенаРозничная  = СтрокаТовар.ЦенаРозничная;
			Иначе
				НоваяСтрока.ЦенаРозничная  = 0;
			Конецесли;
		КонецЕсли;
	КонецЦикла;
	ТаблицаТоваров.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,СкладКомпании,ЦенаРозничная","Количество, Резерв");
	
	Возврат ТаблицаТоваров;
КонецФункции // ПолучитьТаблицуТоваров()

// формирует движения документа по партионным регистрам
//
// Параметры:
//  Режим           - режим проведения (оперативный/неоперативный)
//  ДокументСсылка  - ссылка на документ который надо допровести по партиям
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, ложь - чего-то не так
//
Функция ПровестиПоПартиям(Режим,ДокументСсылка) Экспорт
	Отказ=Ложь;
	
	// получим шапку документа
	ШапкаДокумента=ПолучитьШапкуДокумента(ДокументСсылка);
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проверим, если подразделение проводиться по партиям "отложенно", то дальше не идем
	НаборЗаписейДопроведениеПоПартиям=Движения.ДопроведениеПоПартиям;
	НаборЗаписейДопроведениеПоПартиям.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейДопроведениеПоПартиям.ШапкаДокумента=ШапкаДокумента;
	Отказ=НЕ НаборЗаписейДопроведениеПоПартиям.Зафиксировать() ИЛИ Отказ;
	Если НаборЗаписейДопроведениеПоПартиям.НеПроводитьПартии Тогда
		// Доходы и расходы на себестоимость не оприходованных партий
		Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия Тогда
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение=ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте=Ложь;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов=Справочники.СтатьиДоходовИРасходов.СебестоимостьНеоприходованныхПартий;
			НаборЗаписейДоходыИРасходы.Расход=ШапкаДокумента.СуммаВсего;
			НаборЗаписейДоходыИРасходы.ШапкаДокумента=ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Приход() ИЛИ Отказ;
		КонецЕсли;
		Возврат НЕ Отказ;
	КонецЕсли;
	
	Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия Тогда
		// сформируем таблицу товарного состава
		Запрос=Новый Запрос("ВЫБРАТЬ
		                    |	ВозвратОтПокупателяТовары.Номенклатура КАК Номенклатура,
		                    |	ВозвратОтПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		                    |	ВозвратОтПокупателяТовары.ДокументПродажи КАК ДокументПродажи,
		                    |	ВозвратОтПокупателяТовары.Партия КАК Партия,
		                    |	ВозвратОтПокупателяТовары.ГТД КАК ГТД,
		                    |	ВозвратОтПокупателяТовары.СтавкаНДС,
		                    |	СУММА(ВозвратОтПокупателяТовары.КоличествоБазовое) КАК Количество,
		                    |	ВозвратОтПокупателяТовары.Себестоимость
		                    |ИЗ
		                    |	Документ.ВозвратОтПокупателя.Товары КАК ВозвратОтПокупателяТовары
		                    |ГДЕ
		                    |	ВозвратОтПокупателяТовары.Ссылка = &Ссылка
		                    |	И ВозвратОтПокупателяТовары.Номенклатура.ВидНоменклатуры <> &Услуга
		                    |
		                    |СГРУППИРОВАТЬ ПО
		                    |	ВозвратОтПокупателяТовары.Номенклатура,
		                    |	ВозвратОтПокупателяТовары.ХарактеристикаНоменклатуры,
		                    |	ВозвратОтПокупателяТовары.ДокументПродажи,
		                    |	ВозвратОтПокупателяТовары.Партия,
		                    |	ВозвратОтПокупателяТовары.ГТД,
		                    |	ВозвратОтПокупателяТовары.СтавкаНДС,
		                    |	ВозвратОтПокупателяТовары.Себестоимость
		                    |УПОРЯДОЧИТЬ ПО
		                    |	ВозвратОтПокупателяТовары.Партия Убыв,
		                    |	ВозвратОтПокупателяТовары.ГТД Убыв
							|");
		Запрос.УстановитьПараметр("Ссылка",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
		
		// вернем партии
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
		НаборЗаписейПартии.РезультатЗапросаПоТоварам=Запрос.Выполнить().Выгрузить();
		НаборЗаписейПартии.ПоБазовомуКоличеству=Истина;
		НаборЗаписейПартии.ИмяРеквизитаДокумент="ДокументПродажи";
		НаборЗаписейПартии.Сторно=Истина;
		НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
		
		// если среди возвращенного товара есть комиссионный, то отсторнируем реализованные товары
		НаборЗаписейРеализованныеТовары=Движения.РеализованныеТовары;
		НаборЗаписейРеализованныеТовары.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейРеализованныеТовары.Сторно=Истина;
		НаборЗаписейРеализованныеТовары.ПоБазовомуКоличеству=Истина;
		НаборЗаписейРеализованныеТовары.ШапкаДокумента=ШапкаДокумента;
		Отказ=НЕ НаборЗаписейРеализованныеТовары.Приход() ИЛИ Отказ;
		
		// вернем бонусные баллы
		НаборЗаписейБонусныхБаллов=Движения.БонусныеБаллы;
		НаборЗаписейБонусныхБаллов.ДокДата     = ШапкаДокумента.Дата;
		НаборЗаписейБонусныхБаллов.Регистратор = ШапкаДокумента.Ссылка;
		НаборЗаписейБонусныхБаллов.Сторно      = Истина;
		НаборЗаписейБонусныхБаллов.ХозОперация = ШапкаДокумента.ХозОперация;
		Отказ = НЕ НаборЗаписейБонусныхБаллов.Расход() ИЛИ Отказ;
		
		// продажи
		Если НЕ Отказ Тогда
			НаборЗаписейПродажи=Движения.Продажи;
			НаборЗаписейПродажи.ДокументОбъект=ЭтотОбъект;
			НаборЗаписейПродажи.СкладКомпании=ШапкаДокумента.СкладКомпании;
			НаборЗаписейПродажи.Сторно=Истина;
			НаборЗаписейПродажи.ПоБазовомуКоличеству=Истина;
			НаборЗаписейПродажи.Покупатель=ШапкаДокумента.Контрагент;
			НаборЗаписейПродажи.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
			НаборЗаписейПродажи.ПодразделениеКомпании=ШапкаДокумента.ПодразделениеКомпании;
			НаборЗаписейПродажи.Комиссия=(ШапкаДокумента.ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия);
			НаборЗаписейПродажи.ШапкаДокумента=ШапкаДокумента;
			НаборЗаписейПродажи.ИмяРеквизитаДокумент="Партия";
			Отказ=НЕ НаборЗаписейПродажи.Приход() ИЛИ Отказ;
		КонецЕсли;
	Иначе
		// товар возвращает комиссионер. отсторнируем передачу на комиссию
		НаборЗаписейПартииОтданные=Движения.ПартииТоваровОтданные;
		НаборЗаписейПартииОтданные.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПартииОтданные.Сторно=Истина;
		НаборЗаписейПартииОтданные.Контрагент=ШапкаДокумента.Контрагент;
		НаборЗаписейПартииОтданные.ДоговорВзаиморасчетов=ШапкаДокумента.ДоговорВзаиморасчетов;
		НаборЗаписейПартииОтданные.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейПартииОтданные.ПоБазовомуКоличеству=Истина;
		Отказ=НЕ НаборЗаписейПартииОтданные.Приход() ИЛИ Отказ;
		
		Если НЕ Отказ Тогда
			НаборЗаписейПартииОтданные.Записать();
		КонецЕсли; 
		// Вернем партии товаров компании
		Запрос=Новый Запрос("ВЫБРАТЬ
		                    |	ПартииТоваровОтданные.Номенклатура КАК Номенклатура,
		                    |	ПартииТоваровОтданные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		                    |	ПартииТоваровОтданные.ДокументПередачи КАК ДокументПродажи,
		                    |	ПартииТоваровОтданные.Партия КАК Партия,
		                    |	ПартииТоваровОтданные.ГТД КАК ГТД,
		                    |	-СУММА(ПартииТоваровОтданные.Количество) КАК Количество,
		                    |	-СУММА(ПартииТоваровОтданные.СуммаСебестоимостиУпр) КАК Себестоимость
		                    |ИЗ
		                    |	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
		                    |ГДЕ
		                    |	ПартииТоваровОтданные.Регистратор = &Регистратор
		                    |
		                    |СГРУППИРОВАТЬ ПО
		                    |	ПартииТоваровОтданные.Номенклатура,
		                    |	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
		                    |	ПартииТоваровОтданные.ДокументПередачи,
		                    |	ПартииТоваровОтданные.Партия,
		                    |	ПартииТоваровОтданные.ГТД,
		                    |	ПартииТоваровОтданные.СуммаСебестоимостиУпр");
		Запрос.УстановитьПараметр("Регистратор",ШапкаДокумента.Ссылка);
		НаборЗаписейПартии=Движения.ПартииТоваровКомпании;
		НаборЗаписейПартии.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейПартии.СкладКомпании=ШапкаДокумента.СкладКомпании;
		НаборЗаписейПартии.РезультатЗапросаПоТоварам=Запрос.Выполнить().Выгрузить();
		НаборЗаписейПартии.ПоБазовомуКоличеству=Ложь;
		НаборЗаписейПартии.ШапкаДокумента=ШапкаДокумента;
		НаборЗаписейПартии.ИмяРеквизитаДокумент="ДокументПродажи";
		НаборЗаписейПартии.Сторно=Истина;
		Отказ=НЕ НаборЗаписейПартии.Расход() ИЛИ Отказ;
		
		//возможно, что возврат производится на склад, на котором подразделение не совпадает с подразделением договора
		ПодразделениеСклад = обПолучитьБалансовоеПодразделение(ШапкаДокумента.СкладКомпании.Подразделение);
		ПодразделениеДоговор = обПолучитьБалансовоеПодразделение(ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение);
		БалансовыеПодразделенияНеРавны = (ПодразделениеДоговор<>ПодразделениеСклад);
		
		//получим себестоимость
		ПартииТоваровКомпании=Движения.ПартииТоваровКомпании;
		СебестоимостьПартий = -ПартииТоваровКомпании.Итог("СуммаУпр");
		
		ПартииТоваровОтданные=Движения.ПартииТоваровОтданные;
		СебестоимостьПартийОтданные = -ПартииТоваровОтданные.Итог("СуммаСебестоимостиУпр");
		Если БалансВедетсяПоПодразделениям и БалансовыеПодразделенияНеРавны Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьПартий;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
			
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;;
			НаборЗаписейДиР.ДокументОбъект = ЭтотОбъект;
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.КорректировкаБалансаПоПодразделениям;
			НаборЗаписейДиР.ВУпрВалюте = Истина;
			НаборЗаписейДиР.Доход = СебестоимостьПартийОтданные;
			Отказ = НЕ НаборЗаписейДиР.Расход() ИЛИ Отказ;
		КонецЕсли;	
	КонецЕсли;  	
	
	Если ШапкаДокумента.ХозОперация<>Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия Тогда
		//доходы и расходы
		
		// посчитаем себестоимость возвращенных партий кроме комиссионных
		ПартииТоваровКомпании=Движения.ПартииТоваровКомпании;
		
		ТаблицаСписанийПартий = ПартииТоваровКомпании.Выгрузить();
		ТаблицаСписанийПартий.Свернуть("СтатусПартии","СуммаУпр");
		СтруктураОтбора=Новый Структура("СтатусПартии",Перечисления.СтатусыПартий.ТоварПринятыйКомиссия);
		МассивНайденныхСтрок=ТаблицаСписанийПартий.НайтиСтроки(СтруктураОтбора);
		Для Сч=0 По МассивНайденныхСтрок.ВГраница() Цикл
			ТаблицаСписанийПартий.Удалить(МассивНайденныхСтрок[Сч]);
		КонецЦикла;
		Себестоимость=-ТаблицаСписанийПартий.Итог("СуммаУпр"); СуммаУслуг=0;
		 		        				
		//Итоговая себестоимость товара
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.СкладКомпании.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
		НаборЗаписейДоходыИРасходы.Расход                 = Себестоимость;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		
		// подготовим таблицу движений в разрезе подразделений комиссионных товаров
		РеализованныеТовары = Движения.РеализованныеТовары;
		ТаблицаСписанийПартийРеализованных = РеализованныеТовары.Выгрузить();
		ТаблицаСписанийПартийРеализованных.Свернуть("ДоговорВзаиморасчетов","СуммаУпр");  		
		ТаблицаСписанийПартийРеализованных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
		
		Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
			Если БалансВедетсяПоПодразделениям Тогда
				СтрокаСписания.Подразделение = СтрокаСписания.ДоговорВзаиморасчетов.Подразделение;
			Иначе
				СтрокаСписания.Подразделение = ПодразделениеКомпании;
			КонецЕсли;
		КонецЦикла;	
		
		ТаблицаСписанийПартийРеализованных.Свернуть("Подразделение","СуммаУпр");
		
		Для Каждого СтрокаСписания Из ТаблицаСписанийПартийРеализованных Цикл
			НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
			НаборЗаписейДоходыИРасходы.ДокументОбъект         = ЭтотОбъект;
			НаборЗаписейДоходыИРасходы.Подразделение          = СтрокаСписания.Подразделение;
			НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.Себестоимость;
			НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Истина;
			НаборЗаписейДоходыИРасходы.Расход                 = -СтрокаСписания.СуммаУпр;	
			НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
			Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		КонецЦикла;    		
		
		//посчитаем сумму услуг
		Запрос=Новый Запрос("
		|	ВЫБРАТЬ
		|		СУММА(РеализацияТоваровТовары.СуммаВсего) КАК СуммаУслуг
		|	ИЗ
		|		Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|	ГДЕ
		|		РеализацияТоваровТовары.Ссылка = &ТекДок И РеализацияТоваровТовары.Номенклатура.ВидНоменклатуры=&Услуга
		|");
		Запрос.УстановитьПараметр("ТекДок",ШапкаДокумента.Ссылка);
		Запрос.УстановитьПараметр("Услуга",Перечисления.ВидыНоменклатуры.Услуга);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СуммаУслуг = ?(Выборка.СуммаУслуг=NULL,0,Выборка.СуммаУслуг);
		КонецЕсли;

		//Итог СуммаВсего кроме услуг
		СуммаТоваров = ШапкаДокумента.СуммаВсего - СуммаУслуг;
		НаборЗаписейДоходыИРасходы = Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли;
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоТовару;
		НаборЗаписейДоходыИРасходы.Доход                  = СуммаТоваров;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
		
		//СуммаВсего по услугам
		НаборЗаписейДоходыИРасходы=Движения.ДоходыИРасходы;
		НаборЗаписейДоходыИРасходы.ДокументОбъект = ЭтотОбъект;
		Если БалансВедетсяПоПодразделениям Тогда
			НаборЗаписейДоходыИРасходы.Подразделение = ШапкаДокумента.ДоговорВзаиморасчетов.Подразделение;
		КонецЕсли; 
		НаборЗаписейДоходыИРасходы.ВУпрВалюте             = Ложь;
		НаборЗаписейДоходыИРасходы.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.НачислениеДебиторскойЗадолженностиПоУслугам;
		НаборЗаписейДоходыИРасходы.Доход                  = СуммаУслуг;
		НаборЗаписейДоходыИРасходы.ШапкаДокумента         = ШапкаДокумента;
		Отказ=НЕ НаборЗаписейДоходыИРасходы.Расход() ИЛИ Отказ;
	КонецЕсли;
	
	// двигаем границу последовательности партий
	Если Режим<>РежимПроведенияДокумента.Оперативный ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.СкладКомпанииБыл) Тогда
		// если идет допроведение, то надо получить те значения которые были раньше
		Если Ссылка<>ДокументСсылка Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка");
			Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
			Результат = Запрос.Выполнить(); СкладКомпанииБыл=Неопределено;
			Если НЕ Результат.Пустой() Тогда
				СкладКомпанииБыл = Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании");
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл",СкладКомпанииБыл);
			ДополнительныеСвойства.МоментВремениБыл = ДокументСсылка.МоментВремени();
		КонецЕсли;
		
		НаборЗаписейГраницыПартий = РегистрыСведений.ГраницыПартий.СоздатьНаборЗаписей();
		Если Режим<>РежимПроведенияДокумента.Оперативный Тогда
			НаборЗаписейГраницыПартий.СкладКомпании		= ШапкаДокумента.СкладКомпании;
			НаборЗаписейГраницыПартий.МоментВремени		= ШапкаДокумента.Дата;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыПартий.СкладКомпании		= ДополнительныеСвойства.СкладКомпанииБыл;
			НаборЗаписейГраницыПартий.МоментВремени		= ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыПартий.СкладКомпанииБыл	= Неопределено;
			НаборЗаписейГраницыПартий.МоментВремениБыл	= Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыПартий.ДокументСсылка	= ШапкаДокумента.Ссылка;
		НаборЗаписейГраницыПартий.ИзменитьГраницу();
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции // ПровестиПоПартиям()

// Рассчитывает количество и сумму указанной номенклатуры
//
// Параметры:
//  Номенклатура               - СправочникСсылка - "Номенклатура",
//  ХарактеристикаНоменклатуры - СправочникСсылка - "ХарактеристикиНоменклатуры",
//  ДокументПродажи            - ДокументСсылка - "Инвентаризация", "ЗакрытиеСмены", "РеализацияТоваров",
//                                                "ВводОстатковТоваров","ЗаказНаряд",
//  Партия                     - Характеристика - "ТипыПартий".
//
// Возвращаемое значение:
//  СтруктураКоличествоСумма - структура.
//
Функция РассчитатьКоличествоСумму(Номенклатура,ХарактеристикаНоменклатуры,ДокументПродажи,Партия,ГТД)
	Если ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия Тогда
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ПартииТоваровОтданныеОстатки.КоличествоОстаток),0) КАК Количество,
		|	ЕСТЬNULL(СУММА(ПартииТоваровОтданныеОстатки.СуммаОстаток),0) КАК Сумма,
		|	ЕСТЬNULL(СУММА(ПартииТоваровОтданныеОстатки.СуммаУпрОстаток),0) КАК СуммаУпр
		|ИЗ
		|	РегистрНакопления.ПартииТоваровОтданные.Остатки(
		|		&НаМомент,
		|		ДокументПередачи = &ДокументПередачи
		|			И Номенклатура = &Номенклатура
		|			"+?(обЗначениеНеЗаполнено(Партия),"","И Партия = &Партия")+"
		|			"+?(обЗначениеНеЗаполнено(ГТД),"","И ГТД = &ГТД")+"
		|			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ПартииТоваровОтданныеОстатки";
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,Новый Граница(МоментВремени(),ВидГраницы.Включая)));
		Запрос.УстановитьПараметр("ДокументПередачи",ДокументПродажи);
		Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
		Запрос.УстановитьПараметр("Партия",Партия);
		Запрос.УстановитьПараметр("ГТД",ГТД);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры);
	Иначе
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
					 |	ЕСТЬNULL(СУММА(ПродажиОбороты.КоличествоОборот),0) КАК Количество,
					 |	ЕСТЬNULL(СУММА(ПродажиОбороты.СуммаОборот),0) КАК Сумма,
					 |	ЕСТЬNULL(СУММА(ПродажиОбороты.СуммаУпрОборот),0) КАК СуммаУпр
					 |ИЗ
					 |	РегистрНакопления.Продажи.Обороты(
					 |		,
					 |		&НаМомент,
					 |		,
					 |			ДокументПродажи = &ДокументОснование
					 |			И Номенклатура = &Номенклатура
					 |			"+?(обЗначениеНеЗаполнено(Партия),"","И Партия = &Партия")+"
					 |			"+?(обЗначениеНеЗаполнено(ГТД),"","И ГТД = &ГТД")+"
					 |			И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры) КАК ПродажиОбороты";
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,Новый Граница(МоментВремени(),ВидГраницы.Исключая)));
		Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
		Запрос.УстановитьПараметр("Партия",Партия);
		Запрос.УстановитьПараметр("ГТД",ГТД);
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры);
		Запрос.УстановитьПараметр("ДокументОснование",ДокументПродажи);
	КонецЕсли;
	СтруктураКоличествоСумма=Новый Структура("Количество,Сумма",0,0);
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураКоличествоСумма.Количество=Выборка.Количество;
		Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
			СтруктураКоличествоСумма.Сумма=Выборка.Сумма;
		ИначеЕсли ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
			СтруктураКоличествоСумма.Сумма=Выборка.СуммаУпр;
		Иначе
			СтруктураКоличествоСумма.Сумма=обПересчет(Выборка.СуммаУпр,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),КурсВалютыУпр,ВалютаДокумента,КурсДокумента);
		КонецЕсли; 
	КонецЕсли;
	Возврат СтруктураКоличествоСумма;
КонецФункции // РассчитатьКоличествоСумму()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. Если значение неопределено,
//                                    производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Контрагент" Тогда
		Если ХозОперация = Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия Тогда
			//Параметры для подстановки или создания договора контрагента
			Если ДопПараметры=Неопределено Тогда ДопПараметры=Новый Структура; КонецЕсли; 
			ДопПараметры.Вставить("ВидДоговора",Перечисления.ВидыДоговоров.СКомиссионером);
			ДопПараметры.Вставить("ТипЦенПокупки",Справочники.ТипыЦен.ПустаяСсылка());
			ДопПараметры.Вставить("ТипЦенПродажи",ТипЦен);
		КонецЕсли; 
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Организация" Тогда
		ВидимостьПатент = ?(Организация.ВидНалога = Перечисления.ВидыНалогов.ПСН, Истина, Ложь);
		ЭтаФорма.ЭлементыФормы.Патент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Видимость = ВидимостьПатент;
		ЭтаФорма.ЭлементыФормы.ИспользоватьПатент.Доступность = ВидимостьПатент;
		ЭтотОбъект.Патент = Неопределено;
		Если ВидимостьПатент Тогда
			ЭтаФорма.ИспользоватьПатент = Ложь;
			ЭтаФорма.ЭлементыФормы.Патент.Доступность = Ложь;
		КонецЕсли;
		Возврат Истина;
		
	ИначеЕсли Имя="СкладКомпании" Тогда
		Рез = дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		// отработаем специфику розничного склада
		Если ЗначениеЗаполнено(СкладКомпании) Тогда
			Если (СкладКомпании.Розничный) И (ЗначениеЗаполнено(СкладКомпании.ТипЦенРозничнойТорговли)) Тогда
				Для Каждого СтрокаТоваров Из Товары Цикл
					Рез = ОбработкаРеквизита("УстановитьРозничнуюЦену",  СтрокаТоваров,ЭтаФорма) И Рез;
					Рез = ОбработкаРеквизита("УстановитьПроцентНаценки", СтрокаТоваров,ЭтаФорма) И Рез;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("ПроверкаСкладаКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ПроверкаСкладаКомпании" Тогда
		Если СкладКомпании.Пустая() Тогда
		ИначеЕсли (ХозОперация = Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия) И (СкладКомпании.Розничный) Тогда
			#Если Клиент Тогда
				Предупреждение("Товар отданный на комиссию, не может быть возвращен на розничный склад!",10);
			#КонецЕсли
			СкладКомпании = Неопределено;
			ОбработкаРеквизита("СкладКомпании",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
		
		// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		// отработаем глобально
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		ТекСтрока.ДокументПродажи=?(обЗначениеНеЗаполнено(ДокументОснование),ТекСтрока.ДокументПродажи,ДокументОснование);
		Рез=ОбработкаРеквизита("РассчитатьКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Рез=ОбработкаРеквизита("РассчитатьСебестоимость",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		
		Если ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			Рез = ОбработкаРеквизита("УстановитьРозничнуюЦену",  ТекСтрока, ЭтаФорма, ДопПараметры);
			Рез = ОбработкаРеквизита("УстановитьПроцентНаценки", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("РассчитатьКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ДокументПродажи" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("РассчитатьКоличество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Рез=ОбработкаРеквизита("РассчитатьСебестоимость",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Количество" ИЛИ Имя="Товары.КоличествоБазовое" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("ПроверитьПревышениеОтгрузки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Цена" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.Сумма" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.СуммаВсего" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ПроцентНаценки" Тогда
		
		Рез = Истина;
		Если ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			ВалютаРозницы  = обВалютаТипаЦены(ТекСтрока.Номенклатура, СкладКомпании.ТипЦенРозничнойТорговли, Истина);
			СтруктураКурса = обКурсДляВалюты(ВалютаРозницы, Дата);
			КурсРозницы    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			ТекЦенаЗакупки = ?(КурсРозницы = КурсДокумента, ТекСтрока.Цена, обПересчет(ТекСтрока.Цена, ВалютаДокумента, КурсДокумента, ВалютаРозницы, КурсРозницы)); 
			
			ТекСтрока.ЦенаРозничная = ТекЦенаЗакупки + (ТекЦенаЗакупки*ТекСтрока.ПроцентНаценки/100);
			Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
				ТекСтрока.ЦенаРозничная = ТекСтрока.ЦенаРозничная*(1 + (ТекСтрока.СтавкаНДС.Ставка/100));
			КонецЕсли;
			
			Рез = ОбработкаРеквизита("УстановитьРозничнуюСумму", ТекСтрока, ЭтаФорма, ДопПараметры);
			
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.ЦенаРозничная" Тогда
		Рез=ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		Рез=ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		Возврат Рез;
		
	ИначеЕсли Имя="Товары.СуммаРозничная" Тогда
		
		Если (ТекСтрока.Количество=0) Тогда
			ТекСтрока.ЦенаРозничная=0;
		Иначе
			ТекСтрока.ЦенаРозничная=?(ТекСтрока.КоличествоБазовое=0,0,ТекСтрока.СуммаРозничная/ТекСтрока.КоличествоБазовое);
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("УстановитьПроцентНаценки",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьПроцентНаценки" Тогда
		//Установка процента наценки
		Если ТекСтрока.Цена = 0 Тогда
			
			ТекСтрока.ПроцентНаценки = 0;
			
		ИначеЕсли ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
			
			ВалютаРозницы  = обВалютаТипаЦены(ТекСтрока.Номенклатура, СкладКомпании.ТипЦенРозничнойТорговли, Истина);
			СтруктураКурса = обКурсДляВалюты(ВалютаРозницы, Дата);
			КурсРозницы    = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			
			ТекЦенаЗакупки = ?(КурсРозницы = КурсДокумента, ТекСтрока.Цена, обПересчет(ТекСтрока.Цена, ВалютаДокумента, КурсДокумента, ВалютаРозницы, КурсРозницы)); 
			ЦенаРозничная  = ТекСтрока.ЦенаРозничная;
			
			Если НЕ ТипЦен.ЦенаВключаетНДС Тогда
				ЦенаРозничная = ЦенаРозничная * 100 / (100 + ТекСтрока.СтавкаНДС.Ставка);
			КонецЕсли;
			
			ТекСтрока.ПроцентНаценки=((ЦенаРозничная-ТекЦенаЗакупки)/ТекЦенаЗакупки)*100;
			
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя="УстановитьРозничнуюЦену" Тогда
		//Заполним розничную цену (для розничного склада)
		Если (ЗначениеЗаполнено(СкладКомпании)) И (СкладКомпании.Розничный) Тогда
			ТекСтрока.ЦенаРозничная = обПолучитьЦену(СкладКомпании.ТипЦенРозничнойТорговли, ТекСтрока.Номенклатура, ?(Ссылка.Пустая(), Дата, МоментВремени()),,,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, СкладКомпании.Подразделение);
		КонецЕсли;
		
		Возврат ОбработкаРеквизита("УстановитьРозничнуюСумму",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="УстановитьРозничнуюСумму" Тогда
		//Заполним розничную цену (для розничного склада)
		ТекСтрока.СуммаРозничная=ТекСтрока.ЦенаРозничная*ТекСтрока.КоличествоБазовое;
		Возврат Истина;
		
	ИначеЕсли Имя="РассчитатьКоличество" Тогда
		Рез=Истина;
		Если обЗначениеНеЗаполнено(ТекСтрока.ДокументПродажи) Тогда
		Иначе
			КоличествоСумма=РассчитатьКоличествоСумму(ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры,ТекСтрока.ДокументПродажи,ТекСтрока.Партия,ТекСтрока.ГТД);
			ТекСтрока.Количество=КоличествоСумма.Количество/?(ТекСтрока.Коэффициент=0,1,ТекСтрока.Коэффициент);
			Рез=ОбработкаРеквизита("Товары.Количество",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			//Если ТипЦен.ЦенаВключаетНДС Тогда
				ТекСтрока.СуммаВсего = КоличествоСумма.Сумма;
			//Иначе
			//	ТекСтрока.СуммаВсего = КоличествоСумма.Сумма - КоличествоСумма.Сумма*ТекСтрока.СтавкаНДС.Ставка/(100+ТекСтрока.СтавкаНДС.Ставка);
			//КонецЕсли;
			Рез=ОбработкаРеквизита("Товары.СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		Возврат Рез;
		
	ИначеЕсли Имя="РассчитатьСебестоимость" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.ДокументПродажи) Тогда
			Себестоимость=обПолучитьЦену(Справочники.ТипыЦен.НормативнаяЦена,ТекСтрока.Номенклатура,?(Ссылка.Пустая(),Дата,МоментВремени()),,ВалютаДокумента,КурсДокумента, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании);
			ТекСтрока.Себестоимость=Себестоимость;
		Иначе
			ТекСтрока.Себестоимость=0;
		КонецЕсли; 
		Возврат Истина;
		
	ИначеЕсли Имя="ПроверитьПревышениеОтгрузки" Тогда
		Если обЗначениеНеЗаполнено(ТекСтрока.Номенклатура) Тогда Возврат Истина; КонецЕсли;
		Если обЗначениеНеЗаполнено(ТекСтрока.ДокументПродажи) И обЗначениеНеЗаполнено(ДокументОснование) Тогда Возврат Истина; КонецЕсли;
		
		Если обЗначениеНеЗаполнено(ТекСтрока.ХарактеристикаНоменклатуры) Тогда
			ИспользованиеХарактеристик = ТекСтрока.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик;
			Если (ИспользованиеХарактеристик = 1)или(ИспользованиеХарактеристик = 2) Тогда
				Возврат Истина;
			КонецЕсли;	
		КонецЕсли;	
		Док=?(обЗначениеНеЗаполнено(ТекСтрока.ДокументПродажи),ДокументОснование,ТекСтрока.ДокументПродажи);
		КоличествоСумма=РассчитатьКоличествоСумму(ТекСтрока.Номенклатура,ТекСтрока.ХарактеристикаНоменклатуры,Док,ТекСтрока.Партия,ТекСтрока.ГТД);
		Количество=КоличествоСумма.Количество/?(ТекСтрока.Коэффициент=0,1,ТекСтрока.Коэффициент);
		Если ТекСтрока.Количество>Количество Тогда
			Код = дкПолучитьЗначениеКолонкиКода(ТекСтрока.Номенклатура,ИмяРеквизитаКода,ЭтотОбъект,Истина);
			Код = ?(ПустаяСтрока(Код),"","["+Код+"] ");
			Сообщить(Код+"<"+СокрЛП(ТекСтрока.Номенклатура)+">"+?(обЗначениеНеЗаполнено(ТекСтрока.ХарактеристикаНоменклатуры),""," с характеристикой <"+СокрЛП(ТекСтрока.ХарактеристикаНоменклатуры)+">")+": по документу отгрузки количество "+Формат(Количество,"ЧДЦ=3; ЧН=0,000")+" "+ТекСтрока.Номенклатура.БазоваяЕдиницаИзмерения,СтатусСообщения.Внимание);
		КонецЕсли;
		Возврат Истина;
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
 //зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Получение данных о прослеживаемых товаров
//
// Параметры:
//  Объект	 - ДокументОбъект.ВозвратОтПокупателя - Документ, на основании которого произошла операция с товаром
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список прослеживаемых товаров
//
Функция ОперацииСПрослеживаемымиТоварами() Экспорт
	
	// TODO: На данный момент сделано только для формирования отчета об операциях
	
	// Получим таблицу для формирования данных по операции
	ТаблицаОперацииПрослеживаемости = ПрослеживаемостьТоваровСервер.ИнициализацияТаблицыОпераций();
	
	// Сформируем запрос из документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.ГТД КАК РНПТ,
	|	СУММА(-Продажи.Количество) КАК КоличествоПрослеживаемости,
	|	СУММА(-(Продажи.Сумма - Продажи.СуммаНДС)) КАК СуммаБезНДС
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Регистратор = &Ссылка
	|	И Продажи.ГТД.РНПТ
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Номенклатура,
	|	Продажи.ГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратОтПокупателя.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ВозвратОтПокупателя.Дата, КВАРТАЛ) КАК ПериодОтчета,
	|	ЗНАЧЕНИЕ(Перечисление.ДокументыОтображенияОпераций.ОтчетОбОперациях) КАК ОтчетностьОперации,
	|	ВЫБОР
	|		КОГДА ВозвратОтПокупателя.ДокументОснование ССЫЛКА Документ.ЗакрытиеСмены
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратРозница)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.УКДУменьшение)
	|	КОНЕЦ КАК КодОперации,
	|	ВозвратОтПокупателя.Ссылка КАК Документ,
	|	ВозвратОтПокупателя.Дата КАК ДатаДокумента,
	|	ВозвратОтПокупателя.Номер КАК НомерДокумента,
	|	ВозвратОтПокупателя.Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДокументов.Другое) КАК ВидДокумента,
	|	ВозвратОтПокупателя.Контрагент.СтранаРегистрации КАК СтранаРегистрации
	|ИЗ
	|	Документ.ВозвратОтПокупателя КАК ВозвратОтПокупателя
	|ГДЕ
	|	ВозвратОтПокупателя.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	// Проверим есть РНПТ у документа
	Если ПакетЗапросов[0].Пустой() Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	ШапкаДокумента = ПакетЗапросов[1].Выбрать();
	ШапкаДокумента.Следующий();
	
	// Указываем для отчета только на основании Закрытия смены
	// и для контрагентов не из ЕАЭС
	Если ПрослеживаемостьТоваровСервер.СтранаУчастникЕАЭС(ШапкаДокумента.СтранаРегистрации) Тогда
		Возврат ТаблицаОперацииПрослеживаемости;
	КонецЕсли;
	
	РНПТДокумента = ПакетЗапросов[0].Выгрузить();
	НомерДокумента = дкПолучитьНомерДляПечати(ШапкаДокумента.Документ);
	
	Для Каждого ТекущаяСтрока Из РНПТДокумента Цикл
		
		НоваяСтрока = ТаблицаОперацииПрослеживаемости.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ШапкаДокумента);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.НомерДокумента = НомерДокумента;
		
	КонецЦикла;
	
	Возврат ТаблицаОперацииПрослеживаемости;
	
КонецФункции

#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА

//Функция печати этикеток и ценников
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Функция ПечатьЭтикетокИЦенников(ТабДокумент) Экспорт
	
	СтруктураПараметров=Неопределено;
	Если ЗначениеЗаполнено(СкладКомпании) И СкладКомпании.Розничный Тогда
		СтруктураПараметров = Новый Структура("ИмяРеквизитаЦена, ТипЦен", "ЦенаРозничная", СкладКомпании.ТипЦенРозничнойТорговли);
	КонецЕсли;
	
	дкПечатьЭтикетокИЦенников(ЭтотОбъект, СтруктураПараметров);
КонецФункции // ПечатьЭтикетокИЦенников()

// Формирует печатную форму "ВозвратОтПокупателя"
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьВозвратОтПокупателя(ТабДокумент) Экспорт
	Макет = ПолучитьМакет("ВозвратОтПокупателя");
	
	//для начала настроим макет
	ОбластьШапкаТаблицы = дкПривестиМакетПечатнойФормы(ЭтотОбъект,Макет);
			
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПредставлениеОтправителя	= спПолучитьПредставление(Контрагент);
	ОбластьМакета.Параметры.ПредставлениеСклада			= спПолучитьНаименование(СкладКомпании);
	
	ДополнительныеПараметры = спСоздатьПараметрыПолученияПредставления();
	ДополнительныеПараметры.Вставить("ИспользоватьКПППодразделения");
	ДополнительныеПараметры.Вставить("ПодразделениеКомпании", ПодразделениеКомпании);
	ОбластьМакета.Параметры.ПредставлениеПолучателя = спПолучитьПредставление(Организация, , ДополнительныеПараметры);
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	// Выводим документ основания, если он не равен ВозвратОтПокупателя или Справочники.Контрагенты. 
	Если ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров") ИЛИ 
		ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ЗакрытиеСмены") ИЛИ 
		ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.ВводОстатковТоваров") ИЛИ 
		ТипЗнч(ЭтотОбъект.ДокументОснование) = Тип("ДокументСсылка.Инвентаризация") Тогда 
			ОбластьМакета = Макет.ПолучитьОбласть("Основание"); 
			ОбластьМакета.Параметры.ДокументОснованиеПредставление = спПолучитьНаименование(ДокументОснование); 
			ОбластьМакета.Параметры.ДокументОснование = ДокументОснование; 
			ТабДокумент.Вывести(ОбластьМакета); 
	КонецЕсли;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Товары;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		СтруктураСтроки = дкПолучитьПредставлениеДанныхТоварнойСтроки(СтрокаТабличнойЧасти,ЭтотОбъект);
		ОбластьМакета.Параметры.Заполнить(СтруктураСтроки);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,СуммаСкидки,СуммаНДС,СуммаВсего",ВалютаДокумента,0,0,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;
	
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("СуммаВсего");
	ОбластьПодвал.Параметры.СуммаВсего = Формат(СуммаВсего,ФорматВыводаСуммы);
	НДСВсего = ВыборкаТабличнойЧасти.Итог("СуммаНДС");
	ОбластьПодвал.Параметры.НДСВсего = Формат(НДСВсего,ФорматВыводаСуммы);
	СкидкаВсего = ВыборкаТабличнойЧасти.Итог("СуммаСкидки") + ВыборкаТабличнойЧасти.Итог("СуммаСкидкиСтроки");
	Если СкидкаВсего > 0 Тогда
		ОбластьПодвал.Параметры.СкидкаВсего = Формат(СкидкаВсего,ФорматВыводаСуммы);
	КонецЕсли;
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего наименований "+ВыборкаТабличнойЧасти.Количество()+" на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	
	// Выводим представления и расшифровки подписей
	Отпустил = дкОтветственноеЛицо(ЭтотОбъект,"ОтпустилКонтрагент");
	Отпустил.ОтпустилКонтрагентПредставление = ?(обЗначениеНеЗаполнено(Отпустил.ОтпустилКонтрагент),"","/ "+ Отпустил.ОтпустилКонтрагентПредставление);
	ОбластьПодвал.Параметры.Заполнить(Отпустил);
	Получил  = дкОтветственноеЛицо(ЭтотОбъект,"Получил");
	Получил.ПолучилПредставление = ?(обЗначениеНеЗаполнено(Получил.Получил),"","/ "+ Получил.ПолучилПредставление);
	ОбластьПодвал.Параметры.Заполнить(Получил);
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
КонецФункции // ПечатьВозвратОтПокупателя()

// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
	Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
КонецФункции // Печать()

#КонецЕсли
// Если Клиент Тогда - завершена клиентская часть

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если обЗначениеНеЗаполнено(ХозОперация) Тогда
		ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.РеализацияТоваровКомиссия Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВводОстатковТоваров") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Инвентаризация") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия;
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Дата) Тогда
		#Если Клиент Тогда
		НаДату=РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
		#Иначе
	    НаДату=ТекущаяДата();
		#КонецЕсли
		Дата = НаДату;
	КонецЕсли; 
	
	// Если вид договора не соответствует хоз. операции, то его следует
	// подставлять автоматически
	Если (НЕ обЗначениеНеЗаполнено(Основание)) 
		И Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Основание)) 
		И Основание.Метаданные().Реквизиты.Найти("Контрагент")<>Неопределено Тогда
			
		// Попробуем запонить контрагента и его договор
		Контрагент	= Основание.Контрагент;		
		ДоговорВзаиморасчетов	= Основание.ДоговорВзаиморасчетов;
		ВалютаДокумента			= Основание.ВалютаДокумента;
		ТипЦен					= Основание.ТипЦен;
		
		// Проверим соответствие договора хозю операции
		Если НЕ обЗначениеНеЗаполнено(Контрагент) И
			ТипЗнч(ДоговорВзаиморасчетов)=Тип("СправочникСсылка.ДоговорыВзаиморасчетов") И
			(НЕ ДоговорВзаиморасчетов.ПолучитьОбъект().СоответствуетХозОперации(ХозОперация)) Тогда
			
			// Произвдем поиск договора с правильным видом
			Если ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия Тогда
				ТекущийВидДоговора = Перечисления.ВидыДоговоров.СКомиссионером;
			Иначе
				ТекущийВидДоговора = Перечисления.ВидыДоговоров.Продажа;
			КонецЕсли;
			
			//Параметры для подстановки или создания договора контрагента
			ДопПараметры = Новый Структура();
			ДопПараметры.Вставить("ВалютаВзаиморасчетов", ВалютаДокумента);
			
			ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент, ТекущийВидДоговора, ЭтотОбъект, ДопПараметры, Истина, Истина);
			
			// Попробуем найти договор с видом "Прочее"
			Если обЗначениеНеЗаполнено(ДоговорВзаиморасчетов) Тогда
				ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент, Перечисления.ВидыДоговоров.Прочее, ЭтотОбъект, ДопПараметры, Истина, Истина);
			КонецЕсли;
			
			// Данные реквизиты необходимо заполнять стандартно из общего модуля документов
			ВалютаДокумента = Неопределено;
			ТипЦен          = Неопределено;
			Дата            = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если Основание=Неопределено Тогда
		МассивУслуг=Новый Массив;
		Для Каждого СтрокаТоваров Из Товары Цикл
			Если СтрокаТоваров.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга Тогда
				МассивУслуг.Добавить(СтрокаТоваров);
			КонецЕсли; 
		КонецЦикла;
		Для Каждого СтрокаТоваров Из МассивУслуг Цикл
			Товары.Удалить(СтрокаТоваров);
		КонецЦикла;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Инвентаризация") Тогда
		Если Основание.ХозОперация=Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию Тогда
			ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия;
			Товары.Очистить();
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ПартииТоваровОтданные.Номенклатура,
			|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
			|	ПартииТоваровОтданные.ДокументПередачи КАК ДокументПродажи,
			|	ПартииТоваровОтданные.Партия,
			|	СУММА(ПартииТоваровОтданные.Количество) КАК Количество,
			|	СУММА(ПартииТоваровОтданные.Сумма) КАК Сумма,
			|	СУММА(ПартииТоваровОтданные.СуммаУпр) КАК СуммаУпр
			|ИЗ
			|	РегистрНакопления.ПартииТоваровОтданные КАК ПартииТоваровОтданные
			|ГДЕ
			|	ПартииТоваровОтданные.Регистратор = &Регистратор
			|	И ПартииТоваровОтданные.ВидДвижения = &ВидДвижения
			|
			|СГРУППИРОВАТЬ ПО
			|	ПартииТоваровОтданные.ХарактеристикаНоменклатуры,
			|	ПартииТоваровОтданные.Номенклатура,
			|	ПартииТоваровОтданные.ДокументПередачи,
			|	ПартииТоваровОтданные.Партия";
			Запрос.УстановитьПараметр("Регистратор" , Основание);
			Запрос.УстановитьПараметр("ВидДвижения" , ВидДвиженияНакопления.Приход);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
				ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				НоваяСтрока.СтавкаНДС = Выборка.Номенклатура.СтавкаНДС;
				Если ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
					НоваяСтрока.СуммаВсего = Выборка.Сумма;
				ИначеЕсли ВалютаДокумента=Константы.ВалютаУправленческогоУчетаКомпании.Получить() Тогда
					НоваяСтрока.СуммаВсего = Выборка.СуммаУпр;
				Иначе
					НоваяСтрока.СуммаВсего = обПересчет(Выборка.СуммаУпр,Константы.ВалютаУправленческогоУчетаКомпании.Получить(),ТекущаяДата(),ВалютаДокумента,КурсДокумента);
				КонецЕсли;
				ОбработкаРеквизита("Товары.СуммаВсего",НоваяСтрока);
			КонецЦикла;
		Иначе
			#Если Клиент Тогда
				Предупреждение("Ввод на основании инвентаризации разрешен только для хоз. операции ""Инвентаризация товаров отданных на комиссию""!", 15);
			#КонецЕсли
			
			дкОтменаВводаДокумента(ЭтотОбъект);
		КонецЕсли;
	Иначе
		Товары.Очистить();
		Запрос=Новый Запрос;
		Если Основание.ХозОперация=Справочники.ХозОперации.РеализацияТоваровКомиссия ИЛИ
			 Основание.ХозОперация=Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию Тогда
			 ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия;
			 Запрос.Текст="ВЫБРАТЬ
			              |	ПартииТоваровОтданныеОстатки.Номенклатура,
			              |	ПартииТоваровОтданныеОстатки.ХарактеристикаНоменклатуры,
						  //|	ЕСТЬNULL(СУММА(ПартииТоваровОтданныеОстатки.КоличествоОстаток),0) КАК Количество,
						  //|	ЕСТЬNULL(СУММА(ПартииТоваровОтданныеОстатки.СуммаОстаток),0) КАК Сумма,
						  //|	ЕСТЬNULL(СУММА(ПартииТоваровОтданныеОстатки.СуммаУпрОстаток),0) КАК СуммаУпр
			              |	ПартииТоваровОтданныеОстатки.Партия,
			              |	ПартииТоваровОтданныеОстатки.ГТД
			              |ИЗ
			              |	РегистрНакопления.ПартииТоваровОтданные.Остатки(
						  |		&НаМомент,
						  |		ДокументПередачи = &ДокументПередачи
			              |		И Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
			              |	) КАК ПартииТоваровОтданныеОстатки";
		ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗакрытиеСмены") Тогда
			Запрос.Текст = "ВЫБРАТЬ
			               |	ПродажиОбороты.Номенклатура,
			               |	ПродажиОбороты.ХарактеристикаНоменклатуры,
			               |	ПродажиОбороты.Партия,
			               |	ПродажиОбороты.ГТД,
			               |	СУММА(ПродажиОбороты.КоличествоОборот) КАК КоличествоОборот
			               |ПОМЕСТИТЬ ТаблицаОборотов
			               |ИЗ
			               |	РегистрНакопления.Продажи.Обороты(, &НаМомент, Регистратор, Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)) КАК ПродажиОбороты
			               |ГДЕ
			               |	ПродажиОбороты.Регистратор = &ДокументПередачи
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	ПродажиОбороты.Номенклатура,
			               |	ПродажиОбороты.ХарактеристикаНоменклатуры,
			               |	ПродажиОбороты.Партия,
			               |	ПродажиОбороты.ГТД
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	ТаблицаОборотов.Номенклатура,
			               |	ТаблицаОборотов.ХарактеристикаНоменклатуры,
			               |	ТаблицаОборотов.Партия,
			               |	ТаблицаОборотов.ГТД
			               |ИЗ
			               |	ТаблицаОборотов КАК ТаблицаОборотов
			               |ГДЕ
			               |	ТаблицаОборотов.КоличествоОборот <> 0";
		Иначе
			
			ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя;
			
			//Актуальные остатки получаются в обработчике реквизита "Номенклатуры"
			Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
				              |	ПродажиОбороты.Номенклатура,
				              |	ПродажиОбороты.ХарактеристикаНоменклатуры,
							  //|	ЕСТЬNULL(СУММА(ПродажиОбороты.КоличествоОборот),0) КАК Количество,
							  //|	ЕСТЬNULL(СУММА(ПродажиОбороты.СуммаОборот),0) КАК Сумма,
							  //|	ЕСТЬNULL(СУММА(ПродажиОбороты.СуммаУпрОборот),0) КАК СуммаУпр
							  |	ПродажиОбороты.Партия,
							  |	ПродажиОбороты.ГТД
							  |ИЗ
				              |	РегистрНакопления.Продажи.Обороты(
				              |		,
				              |		&НаМомент,
				              |		,
							  |		ДокументПродажи = &ДокументПередачи
				              |		И Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
							  |	) КАК ПродажиОбороты";
							  
		КонецЕсли;
		Запрос.УстановитьПараметр("НаМомент",?(Ссылка.Пустая(),Дата,Новый Граница(МоментВремени(),ВидГраницы.Исключая)));
		Запрос.УстановитьПараметр("ДокументПередачи",ДокументОснование);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Товары.Добавить();
			//ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,,?(НЕ обПраво("ВыборочноеСписаниеПартий",Права,,ЭтотОбъект),"Партия",""));
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ДокументПродажи = ДокументОснование;
			ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
			
		КонецЦикла;
	КонецЕсли;
	
	// Заполним доп. поля для товарной строки
	Для Каждого СтрокаТовар Из Товары Цикл
		Если ПустаяСтрока(СтрокаТовар.ИдентификаторТовара) Тогда
			СтрокаТовар.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
	// Очистим ТЧ Коды маркировки
	КодыМаркировки.Очистить();
	
	// изменим итоговую сумму документа
	СуммаДокумента = Товары.Итог("СуммаВсего");

КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	КодыМаркировки.Очистить();
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл");
	ДополнительныеСвойства.Вставить("СкладКомпанииБыл");
	ДополнительныеСвойства.Вставить("МоментВремениБыл");
	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения) Тогда Возврат; КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(ДокументОснование) Тогда
		Для каждого СтрокаТоваров Из Товары Цикл
			Если СтрокаТоваров.ДокументПродажи<>ДокументОснование Тогда
				СтрокаТоваров.ДокументПродажи=ДокументОснование;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	Если НЕ Отказ И Проведен И РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		
		БылиДвижения = Ложь;
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		// взаиморасчеты
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ДоговорВзаиморасчетов ИЗ РегистрНакопления.ВзаиморасчетыКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовБыл", Результат.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
		КонецЕсли;
		// партии
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ СкладКомпании ИЗ РегистрНакопления.ПартииТоваровКомпании ГДЕ Регистратор=&Ссылка";
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			БылиДвижения = Истина;
			ДополнительныеСвойства.Вставить("СкладКомпанииБыл", Результат.Выгрузить().ВыгрузитьКолонку("СкладКомпании"));
		КонецЕсли;
		// прошлый момент времени
		Если БылиДвижения Тогда
			Запрос.Текст = "ВЫБРАТЬ МоментВремени ИЗ Документ."+Метаданные().Имя+" ГДЕ Ссылка=&Ссылка";
			ДополнительныеСвойства.Вставить("МоментВремениБыл",Запрос.Выполнить().Выгрузить().Получить(0).МоментВремени);
		КонецЕсли;
		
	КонецЕсли;
	
	//БИЗТЕХ МАМОНОВ 05.12.2025 ++
	//Заполняем товары идентификаторами
	Для Каждого СтрокаТовара Из Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.ИдентификаторТовара) Тогда
			СтрокаТовара.ИдентификаторТовара = Новый УникальныйИдентификатор();
		КонецЕсли;
	КонецЦикла;
	//БИЗТЕХ МАМОНОВ 05.12.2025 --
	
	//++СнимченкоАА_бизтех++
	
	флОтказ = Ложь;
	Если НЕ Отказ и РежимЗаписи=РежимЗаписиДокумента.Проведение Тогда
		Для каждого тс Из Товары Цикл
			ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
			Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки и ТипНоменклатуры.МаркировкаОбязательная Тогда
				
				Кол = 0;
				Отбор = Новый Структура();
				Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
				НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
				Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл
					Кол = Кол + СтрокаТаблицы.Количество;	
				КонецЦикла;   
				
				Если Кол <> тс.Количество Тогда   
					Сообщить("По номенклатуре "+тс.Номенклатура+" в строке "+тс.НомерСтроки+" количество по кодам маркировки не равно количеству по документу!");
					флОтказ = Истина; 
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;   
		Отказ = (Отказ или флОтказ)
	КонецЕсли;
	
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	//Удалим накопление сумм
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейНакоплениеСумм.ОтменаПроведения();
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли;
	
	// Получим способ ведения баланса.
	БалансВедетсяПоПодразделениям = (Константы.СпособВеденияБаланса.Получить()=Перечисления.СпособВеденияБаланса.ПоПодразделению);
	
	// проведем взаиморасчеты
	Если ХозОперация<>Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия Тогда
		НаборЗаписейВзаиморасчеты=Движения.ВзаиморасчетыКомпании;
		НаборЗаписейВзаиморасчеты.ДокументОбъект=ЭтотОбъект;
		НаборЗаписейВзаиморасчеты.РежимПроведения=Режим;
		НаборЗаписейВзаиморасчеты.Контрагент=Контрагент;
		НаборЗаписейВзаиморасчеты.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов;
		Если НЕ обЗначениеНеЗаполнено(ДокументОснование)
			И НЕ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗакрытиеСмены") Тогда
			НаборЗаписейВзаиморасчеты.Сделка=ДокументОснование;
			НаборЗаписейВзаиморасчеты.ВзаиморасчетыСПокупателем=Истина;
		Иначе
			НаборЗаписейВзаиморасчеты.Сделка=Неопределено;
		КонецЕсли;
		НаборЗаписейВзаиморасчеты.Сумма=СуммаДокумента;
		НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц=0;
		Отказ=НЕ НаборЗаписейВзаиморасчеты.Расход() ИЛИ Отказ;
		
		// доходы и расходы по суммовым разницам
		СуммаДоходаРасходаСуммовыхРазниц=НаборЗаписейВзаиморасчеты.СуммаДоходаРасходаСуммовыхРазниц;
		Если СуммаДоходаРасходаСуммовыхРазниц<>0 Тогда
			НаборЗаписейДиР = Движения.ДоходыИРасходы;
			НаборЗаписейДиР.ДокументОбъект         = ЭтотОбъект;
			
			Если БалансВедетсяПоПодразделениям Тогда
				НаборЗаписейДиР.Подразделение = ДоговорВзаиморасчетов.Подразделение;
			КонецЕсли;
			
			НаборЗаписейДиР.СтатьяДоходовИРасходов = Справочники.СтатьиДоходовИРасходов.СуммовыеРазницы;
			НаборЗаписейДиР.ВУпрВалюте             = Истина;
			Если СуммаДоходаРасходаСуммовыхРазниц<0 Тогда
				НаборЗаписейДиР.Расход = -СуммаДоходаРасходаСуммовыхРазниц;
			Иначе
				НаборЗаписейДиР.Доход = СуммаДоходаРасходаСуммовыхРазниц;
			КонецЕсли;
			Отказ = НЕ НаборЗаписейДиР.Приход() ИЛИ Отказ;
		КонецЕсли;
	КонецЕсли;
	
	// проведем остатки товаров
	НаборЗаписейОстатки=Движения.ОстаткиТоваровКомпании;
	НаборЗаписейОстатки.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейОстатки.СкладКомпании=СкладКомпании;
	НаборЗаписейОстатки.РезультатЗапросаПоТоварам=ПолучитьТаблицуТоваров();
	НаборЗаписейОстатки.ПоБазовомуКоличеству=Истина;
	НаборЗаписейОстатки.Контрагент=Контрагент;
	НаборЗаписейОстатки.ДвиженияПоРознице=СкладКомпании.Розничный;
	НаборЗаписейОстатки.РазрешитьПереоценку = НЕ СкладКомпании.Розничный;
	Отказ=НЕ НаборЗаписейОстатки.Приход() ИЛИ Отказ;
	
	// проведем партии товаров
	Отказ=НЕ ПровестиПоПартиям(Режим,Ссылка) ИЛИ Отказ;
	
	//Уменьшим сумму накопления
	НаборЗаписейНакоплениеСумм=РегистрыСведений.НакоплениеСумм.СоздатьНаборЗаписей();
	НаборЗаписейНакоплениеСумм.ДокументОбъект=ЭтотОбъект;
	НаборЗаписейНакоплениеСумм.Контрагент=Контрагент;
	НаборЗаписейНакоплениеСумм.КоличествоНоменклатуры=Товары.Итог("КоличествоБазовое");
	НаборЗаписейНакоплениеСумм.Карточка=Карточка;
	НаборЗаписейНакоплениеСумм.Сторно=Истина;
	Отказ=НЕ НаборЗаписейНакоплениеСумм.НакоплениеСуммы() ИЛИ Отказ;
	
	// Изменим состояние маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейСостоянияКодовМаркировки.Организация = Организация;
	НаборЗаписейСостоянияКодовМаркировки.ПроверятьВыводИзОборота = Ложь;
	НаборЗаписейСостоянияКодовМаркировки.Состояние = Перечисления.СостоянияКодовМаркировки.ВведенВОборотПриВозврате;
	НаборЗаписейСостоянияКодовМаркировки.РежимПроведения = Режим;
	Отказ = НЕ НаборЗаписейСостоянияКодовМаркировки.СостояниеКодовМаркировки() ИЛИ Отказ;
	
	// Проверим наличие прослеживаемых товаров, которые были возвращены из розницы
	Если НЕ Отказ Тогда
		Движения.Продажи.Записать();
	КонецЕсли;
	НаборЗаписейОперацииПрослеживаемости = Движения.ОперацииПрослеживаемыхТоваров;
	НаборЗаписейОперацииПрослеживаемости.ДокументОбъект = ЭтотОбъект;
	НаборЗаписейОперацииПрослеживаемости.ТаблицаПрослеживаемыхТоваров = ОперацииСПрослеживаемымиТоварами();
	Если НЕ НаборЗаписейОперацииПрослеживаемости.ДобавитьОперациюПрослеживаемости() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// Возможно расхождение баланса на "копейки" из-за округления. Возникшую разницу
	// необходимо списать на доходы и расходы.
	Отказ = НЕ дкСписаниеКопейкиПриОкругленииВалютныхСчетов(ЭтотОбъект) ИЛИ Отказ;
	
	// двигаем границу последовательности взаиморасчетов
	ДвигаемГраницу = (Режим<>РежимПроведенияДокумента.Оперативный И Движения.ВзаиморасчетыКомпании.Количество()>0);
	Если ДвигаемГраницу ИЛИ ЗначениеЗаполнено(ДополнительныеСвойства.ДоговорВзаиморасчетовБыл) Тогда
		НаборЗаписейГраницыВзаиморасчетов = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьНаборЗаписей();
		Если ДвигаемГраницу Тогда
			ТаблицаДоговоров = Движения.ВзаиморасчетыКомпании.Выгрузить();
			ТаблицаДоговоров.Свернуть("ДоговорВзаиморасчетов","");
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ТаблицаДоговоров.ВыгрузитьКолонку("ДоговорВзаиморасчетов");
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = Дата;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = ДополнительныеСвойства.МоментВремениБыл;
		Иначе
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетов = ДополнительныеСвойства.ДоговорВзаиморасчетовБыл;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремени = ДополнительныеСвойства.МоментВремениБыл;
			НаборЗаписейГраницыВзаиморасчетов.ДоговорВзаиморасчетовБыл = Неопределено;
			НаборЗаписейГраницыВзаиморасчетов.МоментВремениБыл = Неопределено;
		КонецЕсли;
		НаборЗаписейГраницыВзаиморасчетов.ДокументСсылка = Ссылка;
		НаборЗаписейГраницыВзаиморасчетов.ИзменитьГраницу();
	КонецЕсли;
	
	// выведем сообщения об ошибках и предупреждениях
	дкВыводРезультатовОбработки(ЭтотОбъект,Отказ);
	
	// Зафиксируем новые штрихкоды товара из кода маркировки
	дкПроверитьИДобавитьГТИННоменклатуры(ЭтотОбъект);
	
	//++СнимченкоАА_бизтех++
	Для каждого тс Из Товары Цикл
		ТипНоменклатуры = тс.Номенклатура.ТипНоменклатуры;	
		Если ТипНоменклатуры.ДатаВведенияМаркировки<=Дата и ТипНоменклатуры.ВключенКонтрольОстатковКодовМаркировки Тогда
			
			ОбъектШК = Обработки.ШтрихКоды.Создать();
			
			Отбор = Новый Структура();
			Отбор.Вставить("ИдентификаторТовара", тс.ИдентификаторТовара);
			НайденноеЗначение = КодыМаркировки.НайтиСтроки(Отбор); 	
			Для каждого СтрокаТаблицы Из НайденноеЗначение Цикл  
				
				Движения.КодыМаркировки.Записывать = Истина;
				Движение = Движения.КодыМаркировки.Добавить(); 
				Движение.Период = Дата;
				Движение.Организация = Организация;  
				Движение.Склад = СкладКомпании;
				Движение.Номенклатура = тс.Номенклатура; 
				Движение.КодМаркировки = СтрокаТаблицы.КодМаркировки; 
				
				СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(СтрокаТаблицы.КодМаркировки);
				
				Движение.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				Движение.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Количество = тс.Количество;  
				
			КонецЦикла;   
		КонецЕсли;
	КонецЦикла; 
	//--СнимченкоАА_бизтех--
	
КонецПроцедуры // ОбработкаПроведения()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права                 = Неопределено;
ОбработкаРасчетСкидок = Неопределено;
#Если Клиент Тогда
Права = глПрава;
ОбработкаРасчетСкидок = Обработки.РасчетСкидок.Создать();
#КонецЕсли


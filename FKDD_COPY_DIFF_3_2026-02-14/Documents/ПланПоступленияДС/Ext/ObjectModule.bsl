// Модуль документа ПланПоступленияДС

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем РезультатОбработки Экспорт;	// Переменная для накопления результатов проведения по модулям наборов записей
Перем ИмяРеквизитаКода Экспорт;     // Хранит имя реквизита кода
Перем ОбработкаЗначенияСвойствОбъектов Экспорт; // Переменная для хранения значения свойств основания/объекта копирования
Перем ВалютаУпр; // Валюта управленческого учета

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	
	// Обязательные поля таблицы платежи
	ОбязательныеПлатежи=Новый Структура();
	ОбязательныеПлатежи.Вставить("ДатаПлатежа",3);
	ОбязательныеПлатежи.Вставить("Сумма",1);
	
	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	ОбязательныеРеквизиты.Вставить("СтруктурнаяЕдиница",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("Платежи",ОбязательныеПлатежи);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки       - строка, в случае некорректного заполнения содержит описанием возникших ошибок, 
//  ДопРеквизиты - структура, на вход может быть передана структура с дополнительными реквизитами для проверки,
//  Заполнение   - булево, флаг проверки, 
//  Уникальность - булево, флаг проверки.
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Результат = Истина;	
	
	// Запретим запись с невыбранным сценарием планирования
	Если обЗначениеНеЗаполнено(ВалютаДокумента) Тогда
		дкДобавитьОшибку(Ошибки, "Не выбрана валюта планируемого поступления денежных средств.");		
		Результат = Ложь;		
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(СтруктурнаяЕдиница) Тогда
		Если ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
			дкДобавитьОшибку(Ошибки, "Не выбран банковский счет планируемого поступления денежных средств.");
		Иначе
			дкДобавитьОшибку(Ошибки, "Не выбрана касса планируемого поступления денежных средств.");
		КонецЕсли;	
		Результат = Ложь;		
	КонецЕсли; 
	
	Результат = (дкПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность, Истина) И Результат);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
//
// Параметры:
//  БлокироватьРеквизиты - структура, структура блокируемых на изменение реквизитов.
//
// Возвращаемое значение:
// Булево - Возвращает Истина, если изменения возможны, ложь иначе
//          Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок, блокирующих изменение объекта
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка – Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="Организация" Тогда
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обЗначениеНеЗаполнено(Организация) И ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
			Если СтруктурнаяЕдиница.Владелец<>Организация Тогда
				СтруктурнаяЕдиница=Организация.ОсновнойБанковскийСчет;
				Рез=ОбработкаРеквизита("СчетОрганизации",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
			КонецЕсли; 
		КонецЕсли; 
		Возврат Рез;	
	ИначеЕсли Имя="СчетОрганизации" Тогда
		Рез=Истина;
		НоваяВалюта = СтруктурнаяЕдиница.ВалютаДенежныхСредств;
		СтруктураКурса = обКурсДляВалюты(НоваяВалюта,Дата);
		НовыйКурс = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		ИзменилиВалюту = (НоваяВалюта<>ВалютаДокумента ИЛИ НовыйКурс<>КурсДокумента);
		СпрашиватьПроПересчет = (обПраво("РедактированиеЦенИСуммВНоменклатурныхТаблицах",Права,,ЭтотОбъект) И (ЭтаФорма <> Неопределено));
		#Если Клиент Тогда
		ТребуетсяПересчет=?(ЭтаФорма = Неопределено, Истина, Ложь);
		Если ИзменилиВалюту И СпрашиватьПроПересчет Тогда
			Если Вопрос("Были изменены данные влияющие на суммовые показатели документа !
						|Выполнить пересчет ?",РежимДиалогаВопрос.ДаНет,обПраво("ТаймаутДиалогов",Права,,ЭтотОбъект),КодВозвратаДиалога.Да)=КодВозвратаДиалога.Да Тогда
				ТребуетсяПересчет=Истина;
			КонецЕсли;
		КонецЕсли;
		#Иначе
			ТребуетсяПересчет=Истина;
		#КонецЕсли
		Если ТребуетсяПересчет Тогда
			НоваяСуммаДокумента=СуммаДокумента;
			НоваяСуммаДокумента = обПересчет(НоваяСуммаДокумента, ВалютаДокумента, КурсДокумента, НоваяВалюта, НовыйКурс);
			Если НоваяСуммаДокумента<>СуммаДокумента Тогда
				СуммаДокумента = НоваяСуммаДокумента;
				Рез=ОбработкаРеквизита("СуммаДокумента") И Рез;
			КонецЕсли;
			//Если изменилась валюта, то надо подобрать новый договор контрагента
			Если ВалютаДокумента <> НоваяВалюта Тогда
				ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				ОбработкаРеквизита("Контрагент",,ЭтаФорма);
			КонецЕсли;
		КонецЕсли;
		ВалютаДокумента = НоваяВалюта;
		КурсДокумента = НовыйКурс;
		Возврат Рез;
		
	ИначеЕсли Имя="Контрагент" Тогда
		Если НЕ обЗначениеНеЗаполнено(СтруктурнаяЕдиница) Тогда
			Если ДопПараметры = Неопределено Тогда
				ДопПараметры = Новый Структура;
			КонецЕсли;
			ДопПараметры.Вставить("ВалютаВзаиморасчетов",СтруктурнаяЕдиница.ВалютаДенежныхСредств);
		КонецЕсли;
			
		Рез=дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		Возврат Рез;		
		
	Иначе 
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита()

// Возвращает список возможных хозяйственных операций документа
// Возвращаемое значение: 
//  СписокЗначений. 
//  Строка списка: Значение - имя предопределенного элемента; 
//				   Представление - красивое его представление
// 				   "Помеченная" строка - ХО по умолчанию.
Функция ПолучитьСписокХозОпераций() Экспорт
	СписокХозОпераций = Новый СписокЗначений;
	СписокХозОпераций.Добавить("ПланПоступленияНаРС"   ,"На расчетный счет",Истина);
	СписокХозОпераций.Добавить("ПланПоступленияВКассу","В кассу",Ложь);	
	Возврат СписокХозОпераций;
КонецФункции // ПолучитьСписокХозОпераций()

// заполнение свойств из документа основания
Процедура ЗаполнитьСвойстваНаОсновании(Основание) Экспорт
	//зарезервировано для описания заполнения свойств из свойств основания
КонецПроцедуры

// Расчет суммы документа
//
// Возвращаемое значение:
//  Число - Рассчитанный итог по колонке "Сумма".
//
Функция РассчитатьСуммуВсего() Экспорт
	Возврат Платежи.Итог("Сумма");
КонецФункции // РассчитатьСуммуВсего()

#Если Клиент Тогда  	
	////////////////////////////////////////////////////////////////////////////////
	// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ДОКУМЕНТА
	
// Формирует табличный документ 
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
// Возвращаемое значение:
//  ТабДокумент - возвращает сформированный табличный документ.
//
Функция ПечатьПланПоступленияДС(ТабДокумент) Экспорт
		
	Макет = ПолучитьМакет("ПланПоступленияДС");
	
	//для начала настроим макет
	ФорматВыводаСуммы = обПраво("ФорматВыводаСуммы",Права,,ЭтотОбъект);
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	//вывод заголовка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ТекстЗаголовка = дкПолучитьПредставление(ЭтотОбъект);
	ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьМакета.Параметры.ПодразделениеКомпании = ЭтотОбъект.ПодразделениеКомпании;
	ОбластьМакета.Параметры.СтатьяДДС = ЭтотОбъект.СтатьяДДС;
	ОбластьМакета.Параметры.СтруктурнаяЕдиница = ЭтотОбъект.СтруктурнаяЕдиница;
	Если ЭтотОбъект.ХозОперация = Справочники.ХозОперации.ПланПоступленияВКассу Тогда
		ОбластьМакета.Параметры.СтруктурнаяЕдиницаНад = "Касса компании:";
	Иначе
		ОбластьМакета.Параметры.СтруктурнаяЕдиницаНад = "Расчетный счет:";
	КонецЕсли;	
	
	//вывод свойств
	СтрокаСвойств = дкПолучитьСтрокуСвойствДокумента(ЭтотОбъект);
	ОбластьМакета.Параметры.СтрокаСвойств = СтрокаСвойств;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	//сразу два, т.к. выводим на второй странице только
	НомерСтраницы = 2;
	НомерСтраницыПред = НомерСтраницы;
	
	//теперь выводим шапку
	ТабДокумент.Вывести(ОбластьШапкаТаблицы);
	
	//готовим области строки
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	
	ОбластьМакетаИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма,",ВалютаДокумента,0);
	
	//заполним параметры шапки таблицы для следующего листа
	ОбластьШапкаТаблицы.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
		
	//перебор строк
	ВыборкаТабличнойЧасти = ЭтотОбъект.Платежи;
	Для Каждого СтрокаТабличнойЧасти Из ВыборкаТабличнойЧасти Цикл
		//заполняем данные строки
		
		ОбластьМакета.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьМакета.Параметры.ДатаПлатежа = Формат(СтрокаТабличнойЧасти.ДатаПлатежа, "ДФ=dd.MM.yyyy");
		ОбластьМакета.Параметры.ОписаниеПлатежа = СокрЛП(СтрокаТабличнойЧасти.Описание);
		
		//доп. области
		мсвДопОбластиПодвала = Неопределено;
		Если ВыборкаТабличнойЧасти.Индекс(СтрокаТабличнойЧасти) = ВыборкаТабличнойЧасти.Количество()-1 Тогда
			мсвДопОбластиПодвала = Новый Массив;
			мсвДопОбластиПодвала.Добавить(ОбластьПодвал);
		КонецЕсли;				
		
		//выводим строку, делая проверку попадания на лист
		НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьМакета, ОбластьШапкаТаблицы, ОбластьМакетаИтогоПоСтранице,
			НомерСтраницы, СтруктураИтоговПоСтранице, ЭтотОбъект, мсвДопОбластиПодвала);
		
		//инициализация итогов по странице
		Если НомерСтраницы <> НомерСтраницыПред Тогда
			СтруктураИтоговПоСтранице = Новый Структура("ВалютаДокумента,Сумма",ВалютаДокумента,0);
			НомерСтраницыПред = НомерСтраницы;
			ОбластьШапкаТаблицы.Параметры.НомерСтраницы = "Страница: " + НомерСтраницы; 
		КонецЕсли;
		//добавляем итоги
		дкДобавитьИтогиПоСтранице(СтрокаТабличнойЧасти,СтруктураИтоговПоСтранице);
		
	КонецЦикла;

	//выводим последний подвал, если страниц больше единицы
	Если НомерСтраницы > 2 Тогда
		дкВывестиИтогиПоСтранице(ТабДокумент,ОбластьМакетаИтогоПоСтранице,СтруктураИтоговПоСтранице,ЭтотОбъект);
	КонецЕсли;
	
	//итоги
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.ВалютаДокумента = ВалютаДокумента;
	СуммаВсего = ВыборкаТабличнойЧасти.Итог("Сумма");
	ОбластьПодвал.Параметры.Сумма = Формат(СуммаВсего,ФорматВыводаСуммы);
	ОбластьПодвал.Параметры.СуммаПрописью = "Всего планируется поступлений на сумму " + обЧислоПрописью(СуммаВсего,ЭтотОбъект.ВалютаДокумента);
	ОбластьПодвал.Параметры.Автор = ЭтотОбъект.Автор;
	
	НомерСтраницы = дкВывестиГоризонтальнуюОбласть(ТабДокумент, ОбластьПодвал, , , НомерСтраницы,,ЭтотОбъект);
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьПланПоступленияДС()
		
// Функция осуществляет печать произвольного документа.
// Можно направить печать на экран или принтер, а также распечатать необходимое количество копий.
//
// Параметры:
//	ЭтотОбъект							- ДокументОбъект	- Документ для печати
//	НазваниеПечатнойФормы 				- Строка			- Название печатной формы, если пусто, то по умолчанию
//	КоличествоЭкземпляров				- Число				- Количество экземпляров документа
//	НаПринтер							- Булево			- Признак отправки документа на принтер без отображения его на экране
//  Документ 	  						- ТабличныйДокумент	- Макет печатной формы
//
// Возвращаемое значение:
//   ТабличныйДокумент	- Сформированная печатная форма
//
	Функция Печать(НазваниеПечатнойФормы="", КоличествоЭкземпляров=0, НаПринтер=Ложь, Документ=Неопределено) Экспорт
		Возврат дкПечать(ЭтотОбъект, НазваниеПечатнойФормы, КоличествоЭкземпляров, НаПринтер, Документ);
	КонецФункции // Печать()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если НЕ дкПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоНомера()

// Процедура - обработчик ввода на основании
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ дкОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если Основание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//Производится заполнение реквизитов документа в зависимости от типа документа основания
	//создадим один платеж в табличной части, подставим в него сумму и дату документа основания
	НовыйПлатеж = Платежи.Добавить();
	НовыйПлатеж.ДатаПлатежа = Дата;
	НовыйПлатеж.Сумма       = Основание.СуммаДокумента;

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплату") ИЛИ
		 ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплатуЗаАвтомобили") Тогда
		СтруктурнаяЕдиница = Основание.РасчетныйСчетОрганизации;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплату") ИЛИ 
		 ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплатуЗаАвтомобили") ИЛИ 
		 ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПокупателя") ИЛИ 
		 ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказНаАвтомобиль") Тогда
		СтатьяДДС = Справочники.СтатьиДДС.ОплатаОтПокупателя;
	КонецЕсли;
	
	Контрагент            = Основание.Контрагент;
	ДоговорВзаиморасчетов = Основание.ДоговорВзаиморасчетов;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ дкПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)	
	Если НЕ дкПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения, , , ,"10111") Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	Если НЕ дкПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

Процедура ПередУдалением(Отказ)
	Если НЕ дкПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если НЕ дкОбработкаУдаленияПроведения(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ дкОбработкаПроведения(ЭтотОбъект, Отказ, Режим) Тогда Возврат; КонецЕсли; 
	
	// Собственно само проведение
	НаборЗаписей = Движения.ПлатежныйКалендарь;
	НаборЗаписей.ДокументОбъект = Ссылка;
	НаборЗаписей.Поступление	= Истина;
	
	Отказ = Не НаборЗаписей.План();
	
	// если модифицировали документ, то запишем его
	Если НЕ Отказ И Модифицированность() Тогда
		Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	дкВыводРезультатовОбработки(ЭтотОбъект, Отказ); 	
	
КонецПроцедуры // ОбработкаПроведения()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

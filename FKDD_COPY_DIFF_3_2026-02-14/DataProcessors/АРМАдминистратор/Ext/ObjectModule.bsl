#Если Клиент Тогда
	
Перем Права Экспорт;				// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// СПЕЦИАЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Рекурсивная процедура для обхода дерева прав и записи значений прав и настроек в XML файл.
//
// Параметры:
//  ТекСтрокаДерева – "ДеревоЗначений" или "СтрокаДереваЗначений" - Текущая строка дерева прав.
//
//  ЗаписьXML       – "ЗаписьXML" - Объект для последовательной записи в XML файл.
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура РекурсивныйПроходИЗаписьПоДеревуПрав(ТекСтрокаДерева, ЗаписьXML)	
	Для Каждого ТекСтрока Из ТекСтрокаДерева.Строки Цикл  	
		Если ТекСтрока.Группа Тогда
			РекурсивныйПроходИЗаписьПоДеревуПрав(ТекСтрока, ЗаписьXML);	
		ИначеЕсли ТипОбъектаНастройки = ТекСтрока.ПравоНастройка.Назначение Тогда
			// Сохраняем значение.
			ЗаписьXML.ЗаписатьНачалоЭлемента("ЭлементДерева");
			ЗаписьXML.ЗаписатьАтрибут("ПравоНастройка"   , ЗначениеВСтрокуВнутр(ТекСтрока.ПравоНастройка));
			ЗаписьXML.ЗаписатьАтрибут("ИмяПраваНастройки", ТекСтрока.ПравоНастройка.Наименование);
			ЗаписьXML.ЗаписатьАтрибут("Значение"         , ЗначениеВСтрокуВнутр(ТекСтрока.Значение));
			ЗаписьXML.ЗаписатьКонецЭлемента(); 			
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры // РекурсивныйПроходИЗаписьПоДеревуПрав() 

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//выгружает текущие настройки во внешний файл
//
Процедура СохранитьНастройкиВФайл() Экспорт
	
	// Сначала проверим, выбран ли тип объекта и объект.
	Если (ТипОбъектаНастройки<>Перечисления.НазначениеПравИНастроек.Компания) И обЗначениеНеЗаполнено(ОбъектНастройкиПрав) Тогда
		Предупреждение("Не выбран объект для настройки прав!");
		Возврат;
	КонецЕсли; 
	
	Если ТипОбъектаНастройки=Перечисления.НазначениеПравИНастроек.Пользователь Тогда
		Если НЕ ОбъектНастройкиПрав.ШаблонПрав.Пустая() Тогда // Значит права пользователя определяются шаблоном.
			Предупреждение("Сохранение невозможно, т.к. права и настройки пользователя наследуются
			|из прав и настроек пользователя <" + ОбъектНастройкиПрав.ШаблонПрав + ">");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.ПолноеИмяФайла = "ПраваИНастройки" + ТипОбъектаНастройки;
	Диалог.Расширение = "xml";
	Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	Если НЕ Диалог.Выбрать() Тогда		
		Возврат; 					
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(Диалог.ПолноеИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("ПраваИНастройки");
	ЗаписьXML.ЗаписатьАтрибут("ТипОбъектаНастройки", ЗначениеВСтрокуВнутр(ТипОбъектаНастройки));
	ЗаписьXML.ЗаписатьАтрибут("Объект",              ЗначениеВСтрокуВнутр(ОбъектНастройкиПрав));
	
	
	// Сохранение прав и настроек для текущего объекта.
	ЗаписьXML.ЗаписатьНачалоЭлемента("ДеревоПрав");
	// Рекурсивно проходимся по дереву и записываем настройки и права в XML.
	// Причем запись в XML будем производить линейно (нет смысла сохранять иерархию).
	РекурсивныйПроходИЗаписьПоДеревуПрав(ДеревоПрав, ЗаписьXML);		
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Если объект настройки пользователь, то так же выгрузим права доступа к документам и справочникам
	Если ТипОбъектаНастройки = Перечисления.НазначениеПравИНастроек.Пользователь Тогда
		
		//выборка и выгрузка прав доступа к документам
		ЗаписьXML.ЗаписатьНачалоЭлемента("ДоступКДокументам");
		Запрос = Новый Запрос();
		Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДоступКДокументам.Пользователь КАК Пользователь,
			|	ДоступКДокументам.Право КАК Право,
			|	ДоступКДокументам.Объект КАК Объект,
			|	ДоступКДокументам.ДоступЕсть КАК ДоступЕсть
			|ИЗ
			|	РегистрСведений.ДоступКДокументам КАК ДоступКДокументам
			|ГДЕ
			|	ДоступКДокументам.Пользователь = &Пользователь
			|";
		Запрос.УстановитьПараметр("Пользователь", ОбъектНастройкиПрав);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаТЗ Из РезультатЗапроса Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента("ЗаписьРегистраДоступКДокументам"); 
			ЗаписьXML.ЗаписатьАтрибут("Право",      ЗначениеВСтрокуВнутр(СтрокаТЗ.Право));
			ЗаписьXML.ЗаписатьАтрибут("Объект",     ЗначениеВСтрокуВнутр(СтрокаТЗ.Объект));				
			ЗаписьXML.ЗаписатьАтрибут("ДоступЕсть", ЗначениеВСтрокуВнутр(СтрокаТЗ.ДоступЕсть));
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла; 
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		//выборка и выгрузка утверждения документов
		ЗаписьXML.ЗаписатьНачалоЭлемента("УтверждениеДокументов");
		Запрос = Новый Запрос();
		Запрос.Текст = "
			|ВЫБРАТЬ
			|	УтверждениеДокументов.Право КАК Право,
			|	УтверждениеДокументов.Объект КАК Объект,
			|	УтверждениеДокументов.СтатусДокумента КАК СтатусДокумента,
			|	УтверждениеДокументов.Использование КАК Использование
			|ИЗ
			|	РегистрСведений.УтверждениеДокументов КАК УтверждениеДокументов
			|ГДЕ
			|	УтверждениеДокументов.Объект = &Объект
			|";
		Запрос.УстановитьПараметр("Объект", ОбъектНастройкиПрав);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаТЗ Из РезультатЗапроса Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента("ЗаписьРегистраУтверждениеДокументов"); 
			ЗаписьXML.ЗаписатьАтрибут("Право",      ЗначениеВСтрокуВнутр(СтрокаТЗ.Право));
			ЗаписьXML.ЗаписатьАтрибут("Объект",     ЗначениеВСтрокуВнутр(СтрокаТЗ.Объект));	
			ЗаписьXML.ЗаписатьАтрибут("СтатусДокумента", ЗначениеВСтрокуВнутр(СтрокаТЗ.СтатусДокумента));
			ЗаписьXML.ЗаписатьАтрибут("Использование", ЗначениеВСтрокуВнутр(СтрокаТЗ.Использование));
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла; 
		ЗаписьXML.ЗаписатьКонецЭлемента();

		
		//выборка и выгрузка прав доступа к справочникам
		ЗаписьXML.ЗаписатьНачалоЭлемента("ДоступКСправочникам");
		Запрос = Новый Запрос();
		Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДоступКСправочникам.Пользователь КАК Пользователь,
			|	ДоступКСправочникам.Право КАК Право,
			|	ДоступКСправочникам.Объект КАК Объект,
			|	ДоступКСправочникам.ДоступЕсть КАК ДоступЕсть
			|ИЗ
			|	РегистрСведений.ДоступКСправочникам КАК ДоступКСправочникам
			|ГДЕ
			|	ДоступКСправочникам.Пользователь = &Пользователь
			|";
		Запрос.УстановитьПараметр("Пользователь", ОбъектНастройкиПрав);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаТЗ Из РезультатЗапроса Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента("ЗаписьРегистраДоступКСправочникам"); 
			ЗаписьXML.ЗаписатьАтрибут("Право",      ЗначениеВСтрокуВнутр(СтрокаТЗ.Право));
			ЗаписьXML.ЗаписатьАтрибут("Объект",     ЗначениеВСтрокуВнутр(СтрокаТЗ.Объект));				
			ЗаписьXML.ЗаписатьАтрибут("ДоступЕсть", ЗначениеВСтрокуВнутр(СтрокаТЗ.ДоступЕсть));
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла; 
		ЗаписьXML.ЗаписатьКонецЭлемента();  	
		
		// выборка и выгрузка расширений прав
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасширениеПравДоступа.Пользователь,
		|	РасширениеПравДоступа.Право,
		|	РасширениеПравДоступа.Объект,
		|	РасширениеПравДоступа.Значение
		|ИЗ
		|	РегистрСведений.РасширениеПравДоступа КАК РасширениеПравДоступа
		|ГДЕ
		|	РасширениеПравДоступа.Пользователь = &Пользователь";
		Запрос.УстановитьПараметр("Пользователь", ОбъектНастройкиПрав);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			ЗаписьXML.ЗаписатьНачалоЭлемента("РасширениеПравДоступа");
			Пока Выборка.Следующий() Цикл
				ЗаписьXML.ЗаписатьНачалоЭлемента("ЗаписьРегистраРасширениеПравДоступа");
				ЗаписьXML.ЗаписатьАтрибут("Право",      ЗначениеВСтрокуВнутр(Выборка.Право));
				ЗаписьXML.ЗаписатьАтрибут("Объект",     ЗначениеВСтрокуВнутр(Выборка.Объект));
				ЗаписьXML.ЗаписатьАтрибут("Значение",   ЗначениеВСтрокуВнутр(Выборка.Значение));
				ЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЦикла;
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли; 	
	
	ЗаписьXML.ЗаписатьКонецЭлемента();		
	ЗаписьXML.Закрыть();   	
	
	Сообщить("Сохранение прав и настроек успешно завершена!");
КонецПроцедуры // СохранитьНастройкиВФайл()

// Загружает настройки из внешнего файла
//
Процедура ЗагрузитьНастройкиИзФайла(ЭтаФорма) Экспорт
		
	// Сначала проверим, выбран ли тип объекта и объект.
	Если (ТипОбъектаНастройки<>Перечисления.НазначениеПравИНастроек.Компания) И обЗначениеНеЗаполнено(ОбъектНастройкиПрав) Тогда
		Предупреждение("Не выбран объект для настройки прав!");
		Возврат;
	КонецЕсли;
	
	Если ТипОбъектаНастройки=Перечисления.НазначениеПравИНастроек.Пользователь Тогда
		Если НЕ ОбъектНастройкиПрав.ШаблонПрав.Пустая() Тогда // Значит права пользователя определяются шаблоном.
			Предупреждение("Загрузка невозможна, т.к. права и настройки пользователя наследуются
			|из прав и настроек пользователя <" + ОбъектНастройкиПрав.ШаблонПрав + ">");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	
	Если НЕ Диалог.Выбрать() Тогда 
		//Сообщить("Не указано имя файла загрузки!", СтатусСообщения.ОченьВажное);
		Возврат;
	КонецЕсли;
	
	НастройкиДляПользователя = (ТипОбъектаНастройки=Перечисления.НазначениеПравИНастроек.Пользователь);
	
	// Временная таблица для хранения восстановленных значений дерева прав.
	ТаблицаПравИНастроек = Новый ТаблицаЗначений;
	ТаблицаПравИНастроек.Колонки.Добавить("ПравоНастройка");
	ТаблицаПравИНастроек.Колонки.Добавить("Значение");
	
	//если объект настройки пользователь, то так же загрузим права доступа к документам и справочникам
	Если НастройкиДляПользователя Тогда 
		// Временная таблица для хранения восстановленных значений регистра "ДоступКДокументам"
		ТаблицаДоступаКДокументам = Новый ТаблицаЗначений;
		ТаблицаДоступаКДокументам.Колонки.Добавить("Право");
		ТаблицаДоступаКДокументам.Колонки.Добавить("Объект");
		ТаблицаДоступаКДокументам.Колонки.Добавить("ДоступЕсть");
		
		// Временная таблица для хранения восстановленных значений регистра "УтверждениеДокументов"
		ТаблицаУтверждениеДокументов = Новый ТаблицаЗначений;
		ТаблицаУтверждениеДокументов.Колонки.Добавить("Право");
		ТаблицаУтверждениеДокументов.Колонки.Добавить("Объект");
		ТаблицаУтверждениеДокументов.Колонки.Добавить("СтатусДокумента");
		ТаблицаУтверждениеДокументов.Колонки.Добавить("Использование");
		
		// Временная таблица для хранения восстановленных значений регистра "ДоступКСправочникам"
		ТаблицаДоступаКСправочникам = Новый ТаблицаЗначений;
		ТаблицаДоступаКСправочникам.Колонки.Добавить("Право");
		ТаблицаДоступаКСправочникам.Колонки.Добавить("Объект");
		ТаблицаДоступаКСправочникам.Колонки.Добавить("ДоступЕсть");
		
		// Временная таблица для хранения восстановленных значений регистра "РасширениеПравДоступа"
		ТаблицаРасширенияПравДоступа = Новый ТаблицаЗначений;
		ТаблицаРасширенияПравДоступа.Колонки.Добавить("Право");
		ТаблицаРасширенияПравДоступа.Колонки.Добавить("Объект");
		ТаблицаРасширенияПравДоступа.Колонки.Добавить("Значение");
	КонецЕсли;
	
	// Чтение XML
	Состояние("Чтение XML файла...");
	
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.ОткрытьФайл(Диалог.ПолноеИмяФайла);
	Попытка
		ЧтениеXML.Прочитать(); 	
		
		ТипОбъектаНастройкиИзФайла = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("ТипОбъектаНастройки"));
		Если ТипОбъектаНастройкиИзФайла<>ТипОбъектаНастройки Тогда 
			Предупреждение("Тип объекта загружаемых прав и настроек отличается от текущего!
			|Текущий тип объекта: " + ТипОбъектаНастройки + "
			|Тип объкта из файла: " + ТипОбъектаНастройкиИзФайла); 
			Возврат;
		КонецЕсли;
		
		Пока ЧтениеXML.Прочитать() Цикл
			
			Если ЧтениеXML.ТипУзла<>ТипУзлаXML.НачалоЭлемента Тогда
				Продолжить;
			КонецЕсли;  	
			
			Если ЧтениеXML.Имя="ЭлементДерева" Тогда
				НоваяСтрока = ТаблицаПравИНастроек.Добавить();
				
				ПравоНастройка = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("ПравоНастройка"));
				Если ПустаяСтрока(ПравоНастройка.Наименование) Тогда
					Попытка
						НаименованиеПрава = ЧтениеXML.ПолучитьАтрибут("ИмяПраваНастройки");
						ПравоНастройка    = ПланыВидовХарактеристик.ПраваИНастройки.НайтиПоНаименованию(НаименованиеПрава,Истина);
					Исключение
						ПравоНастройка = Неопределено;
					КонецПопытки;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ПравоНастройка) Тогда
					НоваяСтрока.ПравоНастройка = ПравоНастройка;
					НоваяСтрока.Значение       = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Значение"));
				КонецЕсли;
				
			ИначеЕсли НастройкиДляПользователя Тогда
				Если ЧтениеXML.Имя="ЗаписьРегистраДоступКДокументам" Тогда
					НоваяСтрока = ТаблицаДоступаКДокументам.Добавить();
					НоваяСтрока.Право      = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Право"));
					НоваяСтрока.Объект     = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Объект"));
					НоваяСтрока.ДоступЕсть = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("ДоступЕсть"));
					
				ИначеЕсли ЧтениеXML.Имя="ЗаписьРегистраУтверждениеДокументов" Тогда
					НоваяСтрока = ТаблицаУтверждениеДокументов.Добавить();
					НоваяСтрока.Право      = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Право"));
					НоваяСтрока.Объект     = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Объект"));
					НоваяСтрока.СтатусДокумента = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("СтатусДокумента"));
					НоваяСтрока.Использование = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Использование"));
					
				ИначеЕсли ЧтениеXML.Имя="ЗаписьРегистраДоступКСправочникам" Тогда
					НоваяСтрока = ТаблицаДоступаКСправочникам.Добавить();
					НоваяСтрока.Право      = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Право"));
					НоваяСтрока.Объект     = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Объект"));
					НоваяСтрока.ДоступЕсть = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("ДоступЕсть"));
					
				ИначеЕсли ЧтениеXML.Имя="ЗаписьРегистраРасширениеПравДоступа" Тогда
					НоваяСтрока = ТаблицаРасширенияПравДоступа.Добавить();
					НоваяСтрока.Право      = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Право"));
					НоваяСтрока.Объект     = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Объект"));
					НоваяСтрока.Значение   = ЗначениеИзСтрокиВнутр(ЧтениеXML.ПолучитьАтрибут("Значение"));
				КонецЕсли;
			КонецЕсли; 	
		КонецЦикла; 
		
		ЧтениеXML.Закрыть();
	Исключение
		Предупреждение("Неправильный формат файла!");
		Возврат;
	КонецПопытки;
	
	// Изменение прав и настроек дерева прав.
	Состояние("Обновление прав и настроек...");
	Для Каждого ТекСтрока Из ТаблицаПравИНастроек Цикл  	
		// Ищем соответствующее право в дереве.
		НайденнаяСтрока = ДеревоПрав.Строки.Найти(ТекСтрока.ПравоНастройка, "ПравоНастройка", Истина);
		Если НайденнаяСтрока=Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если НайденнаяСтрока.Значение<>ТекСтрока.Значение Тогда
			НайденнаяСтрока.Значение = ТекСтрока.Значение;
			НайденнаяСтрока.Изменено = Истина;
			Если ТипЗнч(НайденнаяСтрока.Значение)=Тип("Булево") Тогда
				НайденнаяСтрока.ЗначениеБулево = НайденнаяСтрока.Значение;		
			КонецЕсли;
			ЭтаФорма.Модифицированность = Истина;
			
			// Разворачиваем дерево, чтобы показать изменные настройки и права.
			РодительСтроки = НайденнаяСтрока.Родитель;
			Пока РодительСтроки<>Неопределено Цикл
				Если НЕ ЭтаФорма.ЭлементыФормы.ДеревоПрав.Развернут(РодительСтроки) Тогда
					ЭтаФорма.ЭлементыФормы.ДеревоПрав.Развернуть(РодительСтроки);
				КонецЕсли;
				РодительСтроки = РодительСтроки.Родитель;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
		           	
	Если НастройкиДляПользователя Тогда	
		Состояние("Запись прав на доступ к документам...");
		//запишем все данные в регистр "ДоступКДокументам"
		НаборЗаписей = РегистрыСведений.ДоступКДокументам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ОбъектНастройкиПрав);
		Для Каждого СтрокаТЗ Из ТаблицаДоступаКДокументам Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = ОбъектНастройкиПрав;
			НоваяЗапись.Право = СтрокаТЗ.Право;		
			НоваяЗапись.Объект = СтрокаТЗ.Объект;		
			НоваяЗапись.ДоступЕсть = СтрокаТЗ.ДоступЕсть;
		КонецЦикла;  	
		НаборЗаписей.Записать();
		
		Состояние("Запись прав на утверждение документов...");
		//запишем все данные в регистр "УтверждениеДокументов"
		НаборЗаписей = РегистрыСведений.УтверждениеДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(ОбъектНастройкиПрав);
		Для Каждого СтрокаТЗ Из ТаблицаУтверждениеДокументов Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Право = СтрокаТЗ.Право;		
			НоваяЗапись.Объект = ОбъектНастройкиПрав;	
			НоваяЗапись.СтатусДокумента = СтрокаТЗ.СтатусДокумента;
			НоваяЗапись.Использование = СтрокаТЗ.Использование;
		КонецЦикла;  	
		НаборЗаписей.Записать();
		
		Состояние("Запись прав на доступ к справочникам...");  
		//запишем все данные в регистр "ДоступКСправочникам"
		НаборЗаписей = РегистрыСведений.ДоступКСправочникам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ОбъектНастройкиПрав);
		Для Каждого СтрокаТЗ Из ТаблицаДоступаКСправочникам Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = ОбъектНастройкиПрав;
			НоваяЗапись.Право = СтрокаТЗ.Право;		
			НоваяЗапись.Объект = СтрокаТЗ.Объект;		
			НоваяЗапись.ДоступЕсть = СтрокаТЗ.ДоступЕсть;
		КонецЦикла;  	
		НаборЗаписей.Записать();
		
		Состояние("Запись расширения прав доступа...");  
		//запишем все данные в регистр "РасширениеПравДоступа"
		НаборЗаписей = РегистрыСведений.РасширениеПравДоступа.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ОбъектНастройкиПрав);
		Для Каждого СтрокаТЗ Из ТаблицаРасширенияПравДоступа Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = ОбъектНастройкиПрав;
			НоваяЗапись.Право = СтрокаТЗ.Право;		
			НоваяЗапись.Объект = СтрокаТЗ.Объект;		
			НоваяЗапись.Значение = СтрокаТЗ.Значение;
		КонецЦикла;  	
		НаборЗаписей.Записать();
	КонецЕсли;
	
	Сообщить("Загрузка прав и настроек успешно завершена!");		
КонецПроцедуры // ЗагрузитьНастройкиИзФайла()

// копирование набора прав для другого пользователя/организации/подразделения
//
// Параметры:
//  ОбъектИсточник - СправочникСсылка - Пользователи, Организации, ПодразделенияКомпании,
//  ОбъектПриемник - СправочникСсылка - Пользователи, Организации, ПодразделенияКомпании.
//
Процедура СкопироватьНаборПрав(ОбъектИсточник, ОбъектПриемник) Экспорт
	
	// права
	НаборИсточник = РегистрыСведений.ПраваИНастройки.СоздатьНаборЗаписей();
	НаборИсточник.Отбор.Объект.Установить(ОбъектИсточник);
	НаборИсточник.Прочитать();
	
	НаборПриемник = РегистрыСведений.ПраваИНастройки.СоздатьНаборЗаписей();
	НаборПриемник.Отбор.Объект.Установить(ОбъектПриемник);
	НаборПриемник.Очистить();
	
	Для Каждого Запись Из НаборИсточник Цикл
		НоваяЗапись = НаборПриемник.Добавить();
		НоваяЗапись.Объект = ОбъектПриемник;
		НоваяЗапись.ПравоНастройка = Запись.ПравоНастройка;
		НоваяЗапись.Значение = Запись.Значение;
	КонецЦикла;
	НаборПриемник.Записать();
	
	// Расширения
	НаборИсточник = РегистрыСведений.РасширениеПравДоступа.СоздатьНаборЗаписей();
	НаборИсточник.Отбор.Пользователь.Установить(ОбъектИсточник);
	НаборИсточник.Прочитать();
	
	НаборПриемник = РегистрыСведений.РасширениеПравДоступа.СоздатьНаборЗаписей();
	НаборПриемник.Отбор.Пользователь.Установить(ОбъектПриемник);
	НаборПриемник.Очистить();
	
	Для Каждого Запись Из НаборИсточник Цикл
		НоваяЗапись = НаборПриемник.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Запись, "Право,Объект,Значение");
		НоваяЗапись.Пользователь = ОбъектПриемник;
	КонецЦикла;
	НаборПриемник.Записать();

КонецПроцедуры // СкопироватьНаборПрав()
	
// Заполняет реквизит "СтруктураОписаний" из макета "НастройкиПоУмолчанию"
// В дальнейшем эта структура может использоваться для вывода описаний в форме
//
Процедура ПолучитьСтруктуруОписаний() Экспорт
	Состояние("Получение структуры описания прав и настроек...");
	Макет=ПланыВидовХарактеристик.ПраваИНастройки.ПолучитьМакет("НастройкиПоУмолчанию");	
	Для Номер = 1 По 10000 Цикл	// Заведомо больше, чем есть в макете
		Код=СокрЛП(Макет.ПолучитьОбласть("R"+Строка(Номер)+"C3").ТекущаяОбласть.Текст);
		Описание=СокрЛП(Макет.ПолучитьОбласть("R"+Строка(Номер)+"C8").ТекущаяОбласть.Текст);
		Если Код="" Тогда Прервать;
		ИначеЕсли Описание="" Тогда Продолжить;
		КонецЕсли;
		СтруктураОписаний.Вставить("_"+Код,Описание);
	КонецЦикла;
КонецПроцедуры // ПолучитьСтруктуруОписаний()
	
//Сохранение прав объекта в регистр сведений.
// Функция осуществляет рекурсивный обход дерева прав с поиском и сохранением изменений.
// Если ОбъектНастройкиПрав соответствует текущему пользователю,
// его подразделению или организации, а так же компании в целом, производится
// обновление кэша прав (только для клиента).
//
// Параметры:
//  ВеткаДерева - СтрокаДереваЗначений.
//	ВключитьУтверждение - если Истина, тогда записываем статус подготовлен для всех объектов
//
// Возвращаемое значение:
//  Булево - Если все корректно записалось - возвращает Истина.
//
Функция ЗаписатьПраваИНастройки(ВеткаДерева = Неопределено,ВключитьУтверждение = Ложь,ЗначениеУтверждение=неопределено) Экспорт
		// При неопределенном входном параметре берем корень дерева
	Если ВеткаДерева = Неопределено Тогда
		ВеткаДерева = ДеревоПрав;
	КонецЕсли; 
	
	
	// каждую ветку будем обходить в попытке, по причине возможного отказа записи
	Попытка
		Для Каждого Строка Из ВеткаДерева.Строки Цикл
			Если Строка.Группа Тогда
				Если НЕ ЗаписатьПраваИНастройки(Строка) Тогда
					Возврат Ложь;
				КонецЕсли;
			ИначеЕсли Строка.Изменено Тогда
				Рег=РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
				Если ТипЗнч (ОбъектНастройкиПрав) = Тип("СправочникСсылка.Пользователи") Тогда
					
					//Рег.Объект=?(обЗначениеНеЗаполнено(ОбъектНастройкиПрав.ШаблонПрав), ОбъектНастройкиПрав, ОбъектНастройкиПрав.ШаблонПрав);
					
					//misha
					Если Строка.ПравоНастройка.ПравоПользователя Тогда
						Рег.Объект = ОбъектНастройкиПрав;
					Иначе
						Рег.Объект=?(обЗначениеНеЗаполнено(ОбъектНастройкиПрав.ШаблонПрав), ОбъектНастройкиПрав, ОбъектНастройкиПрав.ШаблонПрав);
					КонецЕсли;					
					
				Иначе
					Рег.Объект=ОбъектНастройкиПрав;
				КонецЕсли;
				Рег.ПравоНастройка=Строка.ПравоНастройка;
				Рег.Значение=Строка.Значение;
				Рег.Записать();
				Строка.Изменено = Ложь;
			КонецЕсли;
		КонецЦикла; 
		
		// В случае исключения вернем Ложь - запись не удалась
	Исключение   
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	
	Если ВключитьУтверждение Тогда
		// запишем ПраваИНастройки
		ТабПраваИНастройки = ТабУтверждениеДокументов.Скопировать();
		ТабПраваИНастройки.Свернуть("Объект,Право");

		ПраваИНастройкиНаборЗаписей = РегистрыСведений.ПраваИНастройки.СоздатьНаборЗаписей();
		ПраваИНастройкиНаборЗаписей.Прочитать();
		ПраваИНастройкиСтрокиНабораЗаписей = ПраваИНастройкиНаборЗаписей.Выгрузить();
		ПраваИНастройкиНаборЗаписей.Очистить();
		СтруктураПоиска = Новый Структура();
		Для Каждого ТекСтрока Из ТабПраваИНастройки Цикл
			СтруктураПоиска.Вставить("Объект",ТекСтрока.Объект);
			СтруктураПоиска.Вставить("ПравоНастройка",ТекСтрока.Право);
			ПраваИНастройкиСтрокаЗаписи = ПраваИНастройкиСтрокиНабораЗаписей.НайтиСтроки(СтруктураПоиска);
			Если ПраваИНастройкиСтрокаЗаписи.Количество() = 0 Тогда
				ПраваИНастройкиСтрокаЗаписи = ПраваИНастройкиСтрокиНабораЗаписей.Добавить();
			Иначе
				ПраваИНастройкиСтрокаЗаписи = ПраваИНастройкиСтрокаЗаписи[0];
			КонецЕсли;
			ПраваИНастройкиСтрокаЗаписи.Объект = ТекСтрока.Объект;
			ПраваИНастройкиСтрокаЗаписи.ПравоНастройка = ТекСтрока.Право;
			ПраваИНастройкиСтрокаЗаписи.Значение = ЗначениеУтверждение;
		КонецЦикла;
		ПраваИНастройкиНаборЗаписей.Загрузить(ПраваИНастройкиСтрокиНабораЗаписей);
		ПраваИНастройкиНаборЗаписей.Записать(Истина);
		
		// Запишем сам признак включенности подсистемы утверждения
		Рег					=	РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
		Рег.Объект			=	"";
		Рег.ПравоНастройка	=	ПланыВидовХарактеристик.ПраваИНастройки.ИспользоватьУтверждениеДокументов;
		Рег.Значение		=	ЗначениеУтверждение;
		Рег.Записать();
		
		СтрокаДокументы = ДеревоПрав.Строки.Найти(ПланыВидовХарактеристик.ПраваИНастройки.Документы,"ПравоНастройка");
		Если СтрокаДокументы <> неопределено Тогда
			СтрокаОбщиеПараметры = СтрокаДокументы.Строки.Найти(ПланыВидовХарактеристик.ПраваИНастройки.ОбщиеПараметрыДокументов,"ПравоНастройка");
			Если СтрокаОбщиеПараметры <> неопределено Тогда
				СтрокаЗаписьИПроведение = СтрокаОбщиеПараметры.Строки.Найти(ПланыВидовХарактеристик.ПраваИНастройки.ЗаписьИПроведениеДокументов,"ПравоНастройка");
				Если СтрокаЗаписьИПроведение <> неопределено Тогда
					СтрокаИспользоватьУтверждение = СтрокаЗаписьИПроведение.Строки.Найти(ПланыВидовХарактеристик.ПраваИНастройки.ИспользоватьУтверждениеДокументов,"ПравоНастройка");
					Если СтрокаИспользоватьУтверждение <> неопределено Тогда
						СтрокаИспользоватьУтверждение.Изменено = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если ВеткаДерева = ДеревоПрав Тогда  // необходимо ввиду рекурсивности вызова
		//Сохранение настройки доступности документов и справочников в регистр сведений.
		Попытка
			Отбор=Новый Структура;
			Отбор.Вставить("Пользователь",ОбъектНастройкиПрав);
				
			ТекСтроки=ТабДоступностиСправочников.НайтиСтроки(Отбор);

			ДоступКСправочникамНаборЗаписей = РегистрыСведений.ДоступКСправочникам.СоздатьНаборЗаписей();
			ДоступКСправочникамНаборЗаписей.Прочитать();
			ДоступКСправочникамСтрокиНабораЗаписей = ДоступКСправочникамНаборЗаписей.Выгрузить();
			ДоступКСправочникамНаборЗаписей.Очистить();
			СтруктураПоиска = Новый Структура();
			Для Каждого ТекСтрока из ТекСтроки Цикл    
				Если ТекСтрока.Изменено Тогда
					СтруктураПоиска.Вставить("Пользователь",ОбъектНастройкиПрав);
					СтруктураПоиска.Вставить("Право",ТекСтрока.Право);
					СтруктураПоиска.Вставить("Объект",ТекСтрока.Объект);
					ДоступКСправочникамСтрокаЗаписи = ДоступКСправочникамСтрокиНабораЗаписей.НайтиСтроки(СтруктураПоиска);
					Если ДоступКСправочникамСтрокаЗаписи.Количество() = 0 Тогда
						ДоступКСправочникамСтрокаЗаписи = ДоступКСправочникамСтрокиНабораЗаписей.Добавить();
					Иначе
						ДоступКСправочникамСтрокаЗаписи = ДоступКСправочникамСтрокаЗаписи[0];
					КонецЕсли;
					ДоступКСправочникамСтрокаЗаписи.Пользователь = ОбъектНастройкиПрав;
					ДоступКСправочникамСтрокаЗаписи.Право = ТекСтрока.Право;
					ДоступКСправочникамСтрокаЗаписи.Объект = ТекСтрока.Объект;
					ДоступКСправочникамСтрокаЗаписи.ДоступЕсть = ТекСтрока.Доступность;
					ТекСтрока.Изменено = Ложь;	
				КонецЕсли;
			КонецЦикла;
			ДоступКСправочникамНаборЗаписей.Загрузить(ДоступКСправочникамСтрокиНабораЗаписей);
			ДоступКСправочникамНаборЗаписей.Записать(Истина);
				
			ТекСтроки=ТабДоступностиДокументов.НайтиСтроки(Отбор);

			ДоступКДокументамНаборЗаписей = РегистрыСведений.ДоступКДокументам.СоздатьНаборЗаписей();
			ДоступКДокументамНаборЗаписей.Прочитать();
			ДоступКДокументамСтрокиНабораЗаписей = ДоступКДокументамНаборЗаписей.Выгрузить();
			ДоступКДокументамНаборЗаписей.Очистить();
			СтруктураПоиска = Новый Структура();
			Для Каждого ТекСтрока из ТекСтроки Цикл    
				Если ТекСтрока.Изменено Тогда
					СтруктураПоиска.Вставить("Пользователь",ОбъектНастройкиПрав);
					СтруктураПоиска.Вставить("Право",ТекСтрока.Право);
					СтруктураПоиска.Вставить("Объект",ТекСтрока.Объект);
					ДоступКДокументамСтрокаЗаписи = ДоступКДокументамСтрокиНабораЗаписей.НайтиСтроки(СтруктураПоиска);
					Если ДоступКДокументамСтрокаЗаписи.Количество() = 0 Тогда
						ДоступКДокументамСтрокаЗаписи = ДоступКДокументамСтрокиНабораЗаписей.Добавить();
					Иначе
						ДоступКДокументамСтрокаЗаписи = ДоступКДокументамСтрокаЗаписи[0];
					КонецЕсли;
					ДоступКДокументамСтрокаЗаписи.Пользователь = ОбъектНастройкиПрав;
					ДоступКДокументамСтрокаЗаписи.Право = ТекСтрока.Право;
					ДоступКДокументамСтрокаЗаписи.Объект = ТекСтрока.Объект;
					ДоступКДокументамСтрокаЗаписи.ДоступЕсть = ТекСтрока.Доступность;
					ТекСтрока.Изменено = Ложь;	
				КонецЕсли;
			КонецЦикла;
			ДоступКДокументамНаборЗаписей.Загрузить(ДоступКДокументамСтрокиНабораЗаписей);
			ДоступКДокументамНаборЗаписей.Записать(Истина);
			
			Отбор=Новый Структура;
			Отбор.Вставить("Объект",ОбъектНастройкиПрав);
			
			Если НЕ ВключитьУтверждение Тогда
			    ТекСтроки	=	ТабУтверждениеДокументов.НайтиСтроки(Отбор);
			Иначе	
			    ТекСтроки 	= 	ТабУтверждениеДокументов;
				Если ТекСтроки.Количество() = 0 Тогда
				    Набор=РегистрыСведений.УтверждениеДокументов.СоздатьНаборЗаписей();
					Набор.Записать();
				КонецЕсли; 
			КонецЕсли; 
			
            УтверждениеДокументовНаборЗаписей = РегистрыСведений.УтверждениеДокументов.СоздатьНаборЗаписей();
			УтверждениеДокументовНаборЗаписей.Прочитать();
			УтверждениеДокументовСтрокиНабораЗаписей = УтверждениеДокументовНаборЗаписей.Выгрузить();
			УтверждениеДокументовНаборЗаписей.Очистить();
			СтруктураПоиска = Новый Структура();
			Для Каждого ТекСтрока из ТекСтроки Цикл
				Если ТекСтрока.Изменено Тогда
					СтруктураПоиска.Вставить("Объект",ТекСтрока.Объект);
					СтруктураПоиска.Вставить("Право",ТекСтрока.Право);
					СтруктураПоиска.Вставить("СтатусДокумента",ТекСтрока.СтатусДокумента);
					УтверждениеДокументовСтрокаЗаписи = УтверждениеДокументовСтрокиНабораЗаписей.НайтиСтроки(СтруктураПоиска);
					Если УтверждениеДокументовСтрокаЗаписи.Количество() = 0 Тогда
						УтверждениеДокументовСтрокаЗаписи = УтверждениеДокументовСтрокиНабораЗаписей.Добавить();
					Иначе
						УтверждениеДокументовСтрокаЗаписи = УтверждениеДокументовСтрокаЗаписи[0];
					КонецЕсли;
					УтверждениеДокументовСтрокаЗаписи.Право = ТекСтрока.Право;
					УтверждениеДокументовСтрокаЗаписи.Объект = ТекСтрока.Объект;
					УтверждениеДокументовСтрокаЗаписи.СтатусДокумента = ТекСтрока.СтатусДокумента;
					УтверждениеДокументовСтрокаЗаписи.Использование = ТекСтрока.Использование;
					ТекСтрока.Изменено = Ложь;	
				КонецЕсли;
			КонецЦикла;
			УтверждениеДокументовНаборЗаписей.Загрузить(УтверждениеДокументовСтрокиНабораЗаписей);
			УтверждениеДокументовНаборЗаписей.Записать(Истина);
		Исключение
			Возврат Ложь;
		КонецПопытки;
		
		//Запишем расширение прав доступа
		Попытка
			НаборЗаписейРасширенияПравДоступа=РегистрыСведений.РасширениеПравДоступа.СоздатьНаборЗаписей();
			Для каждого СтрокаРасширенияПравДоступа Из ТабРасширенияПравДоступа Цикл
				Отбор=Новый Структура;
				Отбор.Вставить("Пользователь",СтрокаРасширенияПравДоступа.Пользователь);
				Отбор.Вставить("Право",СтрокаРасширенияПравДоступа.Право);
				Отбор.Вставить("Объект",СтрокаРасширенияПравДоступа.Объект);
				СтрокиПоиска=ТабРасширенияПравДоступа.НайтиСтроки(Отбор);
				Для Сч=1 По СтрокиПоиска.Количество()-1 Цикл
					ТабРасширенияПравДоступа.Удалить(СтрокиПоиска[Сч]);
				КонецЦикла; 
			КонецЦикла; 
			НаборЗаписейРасширенияПравДоступа.Загрузить(ТабРасширенияПравДоступа);
			НаборЗаписейРасширенияПравДоступа.Записать(Истина);
			Для Каждого СтрокаТЗ Из ТабРасширенияПравДоступа Цикл
				СтрокаТЗ.Изменено=Ложь;
			КонецЦикла; 
			
		Исключение
			Сообщить("Ошибка записи расширений прав доступа: "+ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки; 
		
		// Если все корректно записалось, то для корневого вызова проверим
		// необходимость обновления собственных прав (только на клиенте)
		Если (ОбъектНастройкиПрав = ПараметрыСеанса.Пользователь) ИЛИ
			(ОбъектНастройкиПрав = ПараметрыСеанса.Пользователь.ШаблонПрав) ИЛИ
			(ОбъектНастройкиПрав = ПараметрыСеанса.ПодразделениеКомпании) ИЛИ
			(ОбъектНастройкиПрав = ПараметрыСеанса.Организация) ИЛИ
			(ТипОбъектаНастройки = Перечисления.НазначениеПравИНастроек.Компания) Тогда
			// Обновим текущий набор прав
			Состояние ("Обновляю права текущего пользователя");
			глПрава=обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
			
			// Помощник продаж +
			атСервер.ИнициализироватьКодСостояния();
			// Помощник продаж - 
			
			Сообщить("Права текущего пользователя обновлены !",СтатусСообщения.Информация);
			// Поскольку на экране могут быть открыты формы объектов, скажем им, что надо бы обновить кэш прав..
			Оповестить("ОбновитьПрава",ПараметрыСеанса.Пользователь);
			// запишем в режимы работы необходимость обновить права для других активных пользователей
			сфЗаписатьДанныеСеансаОбновитьПрава();
		КонецЕсли; 
	КонецЕсли;
	
	// +СофтФон
	Если сфпПереподключитьСофтФон  Тогда
		СтрокаСофтфон = ДеревоПрав.Строки.Найти(ПланыВидовХарактеристик.ПраваИНастройки.СофтФон,"ПравоНастройка");
		Если СтрокаСофтфон <> Неопределено Тогда
			СтрокаНастройка = СтрокаСофтфон.Строки.Найти(ПланыВидовХарактеристик.ПраваИНастройки.сфпИспользоватьСофтФон,"ПравоНастройка");
			Если СтрокаНастройка <> Неопределено Тогда
				НовоеЗначениеПользователя = СтрокаНастройка.Значение;
			Иначе
				НовоеЗначениеПользователя = Неопределено;
			КонецЕсли;
		Иначе
			НовоеЗначениеПользователя = Неопределено;
		КонецЕсли;
		Если НовоеЗначениеПользователя <> Неопределено Тогда
			СтрокаДереваНастроек =  ДеревоПрав.Строки.НайтиСтроки(Новый Структура("ПравоНастройка", Неопределено), Истина);
			Если НЕ сфпСофтФонПроСервер.сфпИспользоватьСофтФон() Тогда
				сфпСофтФонПроКлиент.сфпОтключитьСофтФон();
			ИначеЕсли НЕ НовоеЗначениеПользователя Тогда
				сфпСофтФонПроКлиент.сфпОтключитьСофтФон();
			Иначе	
				Оповестить("СофтФон_ЗакрытьВнутреннююПанель");
				сфпСофтФонПроКлиент.сфпПодключитьСофтФон();
			КонецЕсли;
		КонецЕсли;	
		сфпПереподключитьСофтФон = Ложь;
	КонецЕсли;
	// -СофтФон	
	
	Возврат Истина;
	
КонецФункции // ЗаписатьПраваИНастройки()
	
//Получение иерархической структуры ПВХ Права и настройки в результат запроса
//
Процедура ПолучениеИерархииПрав() Экспорт
	Состояние("Получение дерева прав и настроек...");
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланВидовХарактеристикПраваИНастройки.Ссылка,
	|	ПланВидовХарактеристикПраваИНастройки.Код,
	|	ПланВидовХарактеристикПраваИНастройки.Наименование,
	|	ПланВидовХарактеристикПраваИНастройки.ЭтоГруппа,
	|	ПланВидовХарактеристикПраваИНастройки.ТипЗначения,
	|	ПланВидовХарактеристикПраваИНастройки.Назначение,
	|	ПланВидовХарактеристикПраваИНастройки.ЭтоНастройка,
	|	ПланВидовХарактеристикПраваИНастройки.ПравоПользователя,
	|	ПланВидовХарактеристикПраваИНастройки.Родитель,
	|	ПланВидовХарактеристикПраваИНастройки.ЗначениеПоУмолчанию
	|
	|ИЗ
	|	ПланВидовХарактеристик.ПраваИНастройки КАК ПланВидовХарактеристикПраваИНастройки
	|ГДЕ
	|	ПланВидовХарактеристикПраваИНастройки.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код ИЕРАРХИЯ";
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Родитель",ПланыВидовХарактеристик.ПраваИНастройки.ПрефиксацияСправочников);
	ЗапросПравИНастроек = Запрос.Выполнить();
КонецПроцедуры // ПолучениеИерархииПрав()
	
//Получение настройки доступности справочников из результат запроса
//
Процедура ПолучениеДоступностиСправочников() Экспорт
	Текст="ВЫБРАТЬ
	|	ДоступКСправочникам.Пользователь,
	|	ДоступКСправочникам.Объект,
	|	ДоступКСправочникам.Право,
	|	ДоступКСправочникам.ДоступЕсть КАК Доступность,
	|	ЛОЖЬ КАК Изменено
	|ИЗ
	|	РегистрСведений.ДоступКСправочникам КАК ДоступКСправочникам";
	Запрос=Новый Запрос;
	Запрос.Текст=Текст;
	ТабДоступностиСправочников=Запрос.Выполнить().Выгрузить();
КонецПроцедуры // ПолучениеДоступностиСправочников()
	
//Получение настройки доступности документов из результат запроса
//
Процедура ПолучениеДоступностиДокументов() Экспорт
	Текст="ВЫБРАТЬ
	|	ДоступКДокументам.Пользователь,
	|	ДоступКДокументам.Объект,
	|	ДоступКДокументам.Право,
	|	ДоступКДокументам.ДоступЕсть КАК Доступность,
	|	ВЫБОР
	|		КОГДА ДоступКДокументам.Объект ССЫЛКА Справочник.Пользователи
	|			ТОГДА ""Пользователи""
	|		КОГДА ДоступКДокументам.Объект ССЫЛКА Справочник.ПодразделенияКомпании
	|			ТОГДА ""Подразделения""
	|	КОНЕЦ КАК ВидОбъекта,
	|	ЛОЖЬ КАК Изменено
	|ИЗ
	|	РегистрСведений.ДоступКДокументам КАК ДоступКДокументам";
	Запрос=Новый Запрос;
	Запрос.Текст=Текст;
	ТабДоступностиДокументов=Запрос.Выполнить().Выгрузить();
КонецПроцедуры // ПолучениеДоступностиДокументов()

// Получение настройки доступности утверждения документов из результат запроса
//
Процедура ПолучениеУтвержденияДокументов() Экспорт
	Текст="ВЫБРАТЬ
	|	УтверждениеДокументов.Объект,
	|	УтверждениеДокументов.Право,
	|	УтверждениеДокументов.СтатусДокумента КАК СтатусДокумента,
	|	УтверждениеДокументов.Использование КАК Использование,
	|	ВЫБОР
	|		КОГДА УтверждениеДокументов.Объект ССЫЛКА Справочник.Пользователи
	|			ТОГДА ""Пользователи""
	|		КОГДА УтверждениеДокументов.Объект ССЫЛКА Справочник.ПодразделенияКомпании
	|			ТОГДА ""Подразделения""
	|	КОНЕЦ КАК ВидОбъекта,
	|	ЛОЖЬ КАК Изменено
	|ИЗ
	|	РегистрСведений.УтверждениеДокументов КАК УтверждениеДокументов";
	Запрос=Новый Запрос;
	Запрос.Текст=Текст;
	ТабУтверждениеДокументов=Запрос.Выполнить().Выгрузить();
КонецПроцедуры // ПолучениеДоступностиДокументов()

// Получение расширений прав доступа из результат запроса
//
Процедура ПолучениеРасширенийПравДоступа() Экспорт
	Текст="ВЫБРАТЬ
	      |	РасширениеПравДоступа.Пользователь КАК Пользователь,
	      |	РасширениеПравДоступа.Право КАК Право,
	      |	РасширениеПравДоступа.Объект КАК Объект,
	      |	РасширениеПравДоступа.Значение КАК Значение,
	      |	ЛОЖЬ КАК Изменено
	      |ИЗ
	      |	РегистрСведений.РасширениеПравДоступа КАК РасширениеПравДоступа";
	Запрос=Новый Запрос;
	Запрос.Текст=Текст;
	ТабРасширенияПравДоступа=Запрос.Выполнить().Выгрузить();
КонецПроцедуры // ПолучениеРасширенийПравДоступа()

//Формирование иерархии
//
// Параметры:
//  ВыборкаРодитель - выборка - результат запроса прав и настроек,
//  СтрокиРодитель  - СтрокаДереваЗначений,
//  Уровень         - число - проверка надо ли выводить строчку выборки.
//
Процедура ПостроениеВеткиДереваПрав(ВыборкаРодитель, СтрокиРодитель, Уровень=0) Экспорт
	Состояние("Формирование дерева прав и настроек...");
	Выборка = ВыборкаРодитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		// Проверим, надо ли выводить эту строчку выборки
		Если Выборка.Уровень() <> Уровень Тогда Продолжить;
		ИначеЕсли Выборка.ЭтоГруппа Тогда;	// Ничего больше не проверять, группы показываем все
		ИначеЕсли Выборка.ЭтоНастройка И НЕ ОтображатьНастройки Тогда Продолжить;
		ИначеЕсли НЕ Выборка.ЭтоНастройка И НЕ ОтображатьПрава Тогда Продолжить;
		ИначеЕсли (Выборка.Назначение <> ТипОбъектаНастройки) И ОтображатьТолькоПоОбъекту Тогда Продолжить;
		КонецЕсли;
		
		//misha
		Если Выборка.Назначение = Перечисления.НазначениеПравИНастроек.Пользователь Тогда
			Если НЕ ОбъектНастройкиПрав.ЯвляетсяШаблоном Тогда
				Если Выборка.ПравоПользователя=NULL Тогда Продолжить; КонецЕсли;
				Если НЕ Выборка.ПравоПользователя Тогда Продолжить КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
		
		// +СофтФон
		Если НЕ сфпСофтФонПроСервер.сфпИспользоватьСофтФон() Тогда
			Если Выборка.Ссылка = ПланыВидовХарактеристик.ПраваИНастройки.сфпДействиеПриВходящемЗвонке Тогда
				Продолжить;
			ИначеЕсли Выборка.Ссылка = ПланыВидовХарактеристик.ПраваИНастройки.сфпДействиеПриИсходящемЗвонке Тогда
				Продолжить;
			ИначеЕсли Выборка.Ссылка = ПланыВидовХарактеристик.ПраваИНастройки.сфпЗакрыватьПанельПриЗавершенииРаботы Тогда
				Продолжить;
			ИначеЕсли Выборка.Ссылка = ПланыВидовХарактеристик.ПраваИНастройки.сфпИспользоватьВнутреннююПанель Тогда
				Продолжить;
			ИначеЕсли Выборка.Ссылка = ПланыВидовХарактеристик.ПраваИНастройки.сфпИспользоватьСофтФон Тогда
				Продолжить;
			ИначеЕсли Выборка.Ссылка = ПланыВидовХарактеристик.ПраваИНастройки.сфпТекущийВнутреннийНомер Тогда
				Продолжить;
			ИначеЕсли Выборка.Ссылка = ПланыВидовХарактеристик.ПраваИНастройки.СофтФон3 Тогда
				Продолжить;				
			КонецЕсли;
		КонецЕсли;
		// -СофтФон		
			
		// Добавим строку и заполним её параметры
		Строка = СтрокиРодитель.Добавить();
		Строка.ПравоНастройка = Выборка.Ссылка;
		Строка.Код = Выборка.Код;
		Строка.Назначение = Выборка.Назначение;
		Строка.ЭтоНастройка = Выборка.ЭтоНастройка;
		Строка.Группа = Выборка.ЭтоГруппа;
		Строка.Изменено = Ложь;
		Строка.ПоУмолчанию = Выборка.ЗначениеПоУмолчанию;
			
		// Проверим, если это группа, пойдем на следующий уровень рекурсией
		Если Строка.Группа Тогда
			Строка.КартинкаЭлемента = 6;	// Картинка открытой папки из ккМаркеры
			ПостроениеВеткиДереваПрав(Выборка, Строка.Строки, 1+Выборка.Уровень());
			// Для группы ни значения ни типа не бывает
		Иначе
			// Сначала проверим число типов, так как поддерживается строго только один
			МассивТипов = Выборка.ТипЗначения.Типы();
			Если МассивТипов.Количество() = 1 Тогда
				// +СофтФон
				Если Строка.ПравоНастройка = ПланыВидовХарактеристик.ПраваИНастройки.сфпДействиеПриВходящемЗвонке Тогда
					Строка.ЗначениеБулево = 2;
					ТекЗначение = обПраво(Строка.ПравоНастройка,СтруктураТекущихПрав);					
					сфпМассивАвтоматическихДействий = сфпСофтФонПроСерверПереопределяемый.сфпПолучитьМассивДоступныхДействий();
					сфпСписокАвтоматическихДействий = Новый СписокЗначений;
					Для Каждого ЭлементМассива Из сфпМассивАвтоматическихДействий Цикл
						сфпСписокАвтоматическихДействий.Добавить(ЭлементМассива.Наименование);				
					КонецЦикла;	
					ЭлементСписка = сфпСписокАвтоматическихДействий.НайтиПоЗначению(ТекЗначение);
					Если ЭлементСписка = Неопределено Тогда
						ЭлементСписка = сфпСписокАвтоматическихДействий[0];
					КонецЕсли;	
					Строка.Значение  = ЭлементСписка.Значение;
				ИначеЕсли Строка.ПравоНастройка = ПланыВидовХарактеристик.ПраваИНастройки.сфпДействиеПриИсходящемЗвонке Тогда
					Строка.ЗначениеБулево = 2;
					ТекЗначение = обПраво(Строка.ПравоНастройка,СтруктураТекущихПрав);										
					сфпМассивАвтоматическихДействий = сфпСофтФонПроСерверПереопределяемый.сфпПолучитьМассивДоступныхДействий();
					сфпСписокАвтоматическихДействий = Новый СписокЗначений;
					Для Каждого ЭлементМассива Из сфпМассивАвтоматическихДействий Цикл
						сфпСписокАвтоматическихДействий.Добавить(ЭлементМассива.Наименование);				
					КонецЦикла;	
					ЭлементСписка = сфпСписокАвтоматическихДействий.НайтиПоЗначению(ТекЗначение);
					Если ЭлементСписка = Неопределено Тогда
						ЭлементСписка = сфпСписокАвтоматическихДействий[0];
					КонецЕсли;	
					Строка.Значение  = ЭлементСписка.Значение;
				ИначеЕсли Строка.ПравоНастройка = ПланыВидовХарактеристик.ПраваИНастройки.сфпТекущийВнутреннийНомер Тогда
					Строка.Тип = МассивТипов[0];
					Строка.ЗначениеБулево = 2;
					Строка.Значение = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя(Строка.ПравоНастройка, ПараметрыСеанса.Пользователь);
					Строка.КартинкаЭлемента = 20;
					Продолжить;
				КонецЕсли;	
				// -СофтФон				
				// Определимся с типом значения
				Строка.Тип = МассивТипов[0]; // Тут только один, составные не поддерживаем
				Если Строка.Тип = Тип("Булево") Тогда
					Строка.ЗначениеБулево = обПраво(Строка.ПравоНастройка,СтруктураТекущихПрав);
					Строка.Значение = Строка.ЗначениеБулево;
				Иначе
					Строка.ЗначениеБулево = 2;
					Строка.Значение = обПраво(Строка.ПравоНастройка,СтруктураТекущихПрав);
				КонецЕсли; 
			Иначе // Умудрились создать элемент с несколькими типами или вовсе без них
				Строка.ЗначениеБулево = 2;
				Строка.Значение = "Ошибка в заданных типах - должен быть один тип!";
			КонецЕсли;
			// Теперь определим доступность и картинку
			Если Строка.Назначение <> ТипОбъектаНастройки Тогда
				Строка.КартинкаЭлемента = 21;	// Картинка серого маркера из ккМаркеры
			ИначеЕсли Строка.ЭтоНастройка Тогда
				Строка.КартинкаЭлемента = 20;	// Картинка зеленого маркера из ккМаркеры
			Иначе
				Строка.КартинкаЭлемента = 18;	// Картинка красного маркера из ккМаркеры
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры // ПостроениеВеткиДереваПрав()

// формируем дерево прав
//
Процедура ФормированиеДереваПравОбъекта() Экспорт
	// Получим соответствие прав и настроек выбранного объекта в реквизит СтруктураТекущихПрав
	СтруктураТекущихПрав = обПолучитьПраваИНастройкиПользователя(ОбъектНастройкиПрав);
	// Все запросы к БД сделаны, далее просто отобразим данные в соответствии с фильтрами
	ДеревоПрав.Строки.Очистить();
	Если НЕ (ОтображатьПрава ИЛИ ОтображатьНастройки) Тогда
		// Хотя бы один флаг должен быть включен.
		ОтображатьНастройки = Истина;
	КонецЕсли;
	ПостроениеВеткиДереваПрав(ЗапросПравИНастроек, ДеревоПрав.Строки);
КонецПроцедуры // ФормированиеДереваПравОбъекта()
	
// Определяет, пустая ли группа в дереве
//
// Параметры:
//  Строка - СтрокаДереваЗначений,
//  Пустая - булево.
//
// Возвращаемое значение:
//  Пустая - булево.
//
Функция ГруппаПустая(Строка, Пустая = Истина)
	Для Каждого Стр Из Строка.Строки Цикл
		Пустая = Пустая И ГруппаПустая(Стр, Пустая);
		Пустая = Пустая И Стр.Группа;
	КонецЦикла;
	Возврат Пустая;
КонецФункции // ГруппаПустая()
	
// Настройка вида дерева настроек (пустые группы отображаются свернутыми)
//
// Параметры:
//  ЭтаФорма - форма - ссылка на форму.
//  Дерево   - деревоЗначений - ссылка на дерево.
//
Процедура НастроитьВидДерева(ЭтаФорма, Дерево) Экспорт
	Для Каждого Стр Из Дерево.Строки Цикл
		Если Стр.Группа Тогда
			НастроитьВидДерева(ЭтаФорма, Стр);
			Если ГруппаПустая(Стр) Тогда
				ЭтаФорма.ЭлементыФормы.ДеревоПрав.Свернуть(Стр);
			Иначе
				Если Стр.Уровень()=0 Тогда
					ЭтаФорма.ЭлементыФормы.ДеревоПрав.Развернуть(Стр, Ложь);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	УдалитьПустыеГруппы(ЭтаФорма, Дерево);
КонецПроцедуры // НастроитьВидДерева()

//Определяет, использует ли текущий пользователь шаблон прав Объект
//
// Параметры:
//  Объект - произвольный.
//
// Возвращаемое значение:
//  Булево - возвращает Истина, если использует, иначе Ложь.
//
Функция ТекущийПользовательИспользуетШаблон(Объект) Экспорт
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Пользователи") Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Ссылка, ШаблонПрав
		|ИЗ
		|	Справочник.Пользователи
		|ГДЕ
		|	Ссылка = &ПараметрСсылка И ШаблонПрав = &ПараметрШаблонПрав
		|");
		Запрос.УстановитьПараметр("ПараметрСсылка", ПараметрыСеанса.Пользователь);
		Запрос.УстановитьПараметр("ПараметрШаблонПрав", ОбъектНастройкиПрав);
		Если Запрос.Выполнить().Выбрать().Количество() > 0 Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции // ТекущийПользовательИспользуетШаблон()

//Определяет, использует ли пользователь (объект) шаблон прав
//
// Параметры:
//  Объект - произвольный.
//
// Возвращаемое значение:
//  Булево - возвращает Истина, если использует, иначе Ложь.
//
Функция ОбъектИспользуетШаблонПрав(Объект) Экспорт
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Пользователи") Тогда
		Если НЕ обЗначениеНеЗаполнено(Объект) Тогда
			Если НЕ обЗначениеНеЗаполнено(Объект.ШаблонПрав) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Ложь;
КонецФункции // ОбъектИспользуетШаблонПрав()

// удаляет пустые группы из дерева
//
// Параметры:
//  ЭтаФорма - форма - ссылка на форму.
//  Дерево   - деревоЗначений - ссылка на дерево.
//
Процедура УдалитьПустыеГруппы(ЭтаФорма, Дерево)
	Пока Истина Цикл
		Все = Истина;
		Для Каждого Стр Из Дерево.Строки Цикл
			УдалитьПустыеГруппы(ЭтаФорма, Стр);
			Если Стр.Группа Тогда
				Если ГруппаПустая(Стр) Тогда
					Дерево.Строки.Удалить(Стр);
					Все = Ложь;
					Прервать;
				Иначе
					Если Стр.Уровень()=0 Тогда
						ЭтаФорма.ЭлементыФормы.ДеревоПрав.Развернуть(Стр);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если Все Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // УдалитьПустыеГруппы()

// Функция получает список пользователей, по которым можно установить отбор в журнале регистрации.
//
// Параметры:
//  НЕТ
//
// Возвращаемое значение:
//   СписокЗначений - список пользователей.
// 
Функция ПолучитьСписокПользователейПодходящихДляОтбора() Экспорт
	
	Список =  Новый СписокЗначений;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка,
	|	Пользователи.Код
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	НЕ Пользователи.ЯвляетсяШаблоном";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ПользователиИнформационнойБазы.НайтиПоИмени(СокрЛП(Выборка.Код))<>Неопределено Тогда
			Список.Добавить(Выборка.Ссылка, СокрЛП(Выборка.Код));			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Список;
КонецФункции // ПолучитьСписокПользователейПодходящихДляОтбора()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Права = глПрава;

// Создадим колонки дерева
ДеревоПрав.Колонки.Очистить();
ДеревоПрав.Колонки.Добавить("ПравоНастройка");	// Элемент ПВХ
ДеревоПрав.Колонки.Добавить("Код");				// Код элемента
ДеревоПрав.Колонки.Добавить("Значение");		// Значение права / настройки
ДеревоПрав.Колонки.Добавить("ЗначениеБулево");	// Значение права / настройки для булевого типа
ДеревоПрав.Колонки.Добавить("ПоУмолчанию");		// Значение права / настройки из ПВХ по умолчанию
ДеревоПрав.Колонки.Добавить("Изменено");		// Флаг изменения
ДеревоПрав.Колонки.Добавить("Назначение");		// Уровень назначения права / настройки
ДеревоПрав.Колонки.Добавить("ЭтоНастройка");	// Флаг настройки
ДеревоПрав.Колонки.Добавить("Группа");			// Флаг элемента группы
ДеревоПрав.Колонки.Добавить("КартинкаЭлемента");// Идентификатор картинки элемента для визуализации
ДеревоПрав.Колонки.Добавить("Тип");				// Тип значения характеристики

//Создадим колонки для дерева отбора подсистем
ДеревоОтбораПодсистем.Колонки.Добавить("Подсистема",Новый ОписаниеТипов("ОбъектМетаданных,ОбъектМетаданныхКонфигурация"));
ДеревоОтбораПодсистем.Колонки.Добавить("Отбор",Новый ОписаниеТипов("Число"));
ДеревоОтбораПодсистем.Колонки.Добавить("ИмяПодсистемы",Новый ОписаниеТипов("Строка"));

// Создадим структуру описаний (такого типа реквизитов нет, используем произвольный)
СтруктураОписаний=Новый Структура();

// Заполним список возможных счетчиков диаграммы
СписокВидовСчетчика = Новый СписокЗначений;
СписокВидовСчетчика.Добавить("callsAll","Количество обращений");
СписокВидовСчетчика.Добавить("bytesAll","Передано (байт)");
СписокВидовСчетчика.Добавить("durationAll","Время обработки (мс)");
СписокВидовСчетчика.Добавить("durationAllDBMS","Время SQL сервера (мс)");

#КонецЕсли

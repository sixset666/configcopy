
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивНесопоставленных") Тогда
		
		Для Каждого СтрокаОбъекта ИЗ Параметры.МассивНесопоставленных Цикл
			
			НоваяСтрока = ТаблицаОбъектов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОбъекта);
			Если СтрокаОбъекта.Тип = "Номенклатура" И СтрокаОбъекта.Свойство("Артикул") Тогда
				НоваяСтрока.Представление = "["+СтрокаОбъекта.Артикул+" "+СтрокаОбъекта.Производитель.Наименование+"] "+СтрокаОбъекта.Наименование;
			Иначе
				НоваяСтрока.Представление = СтрокаОбъекта.Наименование;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ЗначениеВДанныеФормы(атПовторноеИспользование.ПолучитьТаблицуСловарей(), ТаблицаСловарей);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСопоставленнуюСсылкуНаСервере(Знач Объект, Знач ВозвращатьНаименование=Неопределено, Знач ЗаписыватьОбъект=Ложь)
	Возврат атСервер.ПолучитьСопоставленнуюСсылку(Объект, ВозвращатьНаименование, ЗаписыватьОбъект);
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьОбъектИБНаСервере(Знач ИмяСправочника, СтруктураСловаря, Знач ОбменДаннымиЗагрузка=Истина)
	Возврат атСервер.СоздатьОбъектИБ(ИмяСправочника, СтруктураСловаря, ОбменДаннымиЗагрузка);
КонецФункции

&НаКлиенте
Процедура ТаблицаОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекСтрока = Элементы.ТаблицаОбъектов.ТекущиеДанные;
	
	Если ТекСтрока <> Неопределено Тогда
		
		НастройкаСловаря = ПолучитьСтрокуНастройки(ТекСтрока.Тип);
		СтруктураСловаря = Новый Структура("ЭтоСсылка, Тип, Код, ВладелецТип, ВладелецКод, Наименование", Истина, ТекСтрока.Тип, ТекСтрока.Код, ТекСтрока.ВладелецТип, ТекСтрока.ВладелецКод, ТекСтрока.Наименование);
		
		Если ЗначениеЗаполнено(ТекСтрока.Артикул) Тогда
			// Это номенклатура, а там своя технология создания. Нужно передать на всякий случай артикул и производителя
			СтруктураСловаря.Вставить("Артикул", ТекСтрока.Артикул);
			СтруктураСловаря.Вставить("АртикулДляПоиска", ТекСтрока.АртикулДляПоиска);
			СтруктураСловаря.Вставить("Производитель", ТекСтрока.Производитель);
		КонецЕсли;
		
		Если Поле = Элементы.ТаблицаОбъектовЗначение Тогда
			
			Если ЗначениеЗаполнено(ТекСтрока.Значение) Тогда
				ОткрытьЗначение(ТекСтрока.Значение);
			Иначе
				
				ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
				
				Если НастройкаСловаря.ИмяСправочника = "ЗначенияСвойств" Тогда
					
					Владелец = ПолучитьСопоставленнуюСсылкуНаСервере(Новый Структура("Тип, Код", ТекСтрока.ВладелецТип, ТекСтрока.ВладелецКод),, Ложь);
					
					Если ТипЗнч(Владелец) = Тип("ПланВидовХарактеристикСсылка.СвойстваОбъектов") И ЗначениеЗаполнено(Владелец) Тогда
						ФормаВыбора = Справочники.ЗначенияСвойств.ПолучитьФормуВыбора();
						ФормаВыбора.РежимВыбора = Истина;
						ФормаВыбора.ЗакрыватьПриВыборе = Истина;
						ФормаВыбора.ПараметрОтборПоВладельцу = Владелец;
						
						ВыбранноеЗначение = ФормаВыбора.ОткрытьМодально();
						
					Иначе
						Сообщить("Владелец значений свойств не сопоставлен!");
					КонецЕсли;
					
				ИначеЕсли НастройкаСловаря.ИмяСправочника = "СвойстваОбъектов" Тогда
					ВыбранноеЗначение = ОткрытьФормуМодально("ПланВидовХарактеристик." + НастройкаСловаря.ИмяСправочника + ".ФормаВыбора", ПараметрыФормы, ЭтаФорма);
				Иначе
					ВыбранноеЗначение = ОткрытьФормуМодально("Справочник." + НастройкаСловаря.ИмяСправочника + ".ФормаВыбора", ПараметрыФормы, ЭтаФорма);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
					
					ТекСтрока.Значение = ВыбранноеЗначение;
					атКлиент.УстановитьСоответствие(ТекСтрока, ВыбранноеЗначение, Истина);
					
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли Поле = Элементы.ТаблицаОбъектовСоздать Тогда
			
			Если ЗначениеЗаполнено(ТекСтрока.Значение) Тогда
				// Очистить значение
			Иначе
				Если НастройкаСловаря.ИмяСправочника = "ЗначенияСвойств" Тогда
					
					Владелец = ПолучитьСопоставленнуюСсылкуНаСервере(Новый Структура("Тип, Код", ТекСтрока.ВладелецТип, ТекСтрока.ВладелецКод),, Ложь);
					
					Если НЕ (ТипЗнч(Владелец) = Тип("ПланВидовХарактеристикСсылка.СвойстваОбъектов") И ЗначениеЗаполнено(Владелец)) Тогда
						Предупреждение("Владелец значений свойств не сопоставлен!");
						Возврат;
					КонецЕсли;
					
					СтруктураСловаря.Вставить("Владелец", Владелец);
					
				КонецЕсли;
				
				ТекСтрока.Значение = СоздатьОбъектИБНаСервере(НастройкаСловаря.ИмяСправочника, СтруктураСловаря, Ложь);
				атКлиент.УстановитьСоответствие(СтруктураСловаря, ТекСтрока.Значение, Ложь);
				ПараметрыОповещения = Новый Структура("СсылкаСловаря, СсылкаИБ", СтруктураСловаря, ТекСтрока.Значение);
				Оповестить("атПомощникПродаж_УстановленоСоответствие", ПараметрыОповещения);
			КонецЕсли;
			
		Иначе
			
			// Надо исключить открытие Номенклатуры, так как пока непонятно как потом обрабатывать оповещение "УстановленоСоответствие"
			Если НастройкаСловаря.ФормаОбъекта = "ОбъектНоменклатура" Тогда
				ФормаОбъекта = "ОбъектСервиса";
			Иначе
				ФормаОбъекта = НастройкаСловаря.ФормаОбъекта;
			КонецЕсли;
			ОткрытьФорму("Обработка.атПомощникПродаж.Форма."+ФормаОбъекта,,, ФормаОбъекта);
			Оповестить("атПомощникПродаж_"+ФормаОбъекта+"_ПриОткрытии", СтруктураСловаря);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Функция ПолучитьСтрокуНастройки(Тип)
	
	Результат = Неопределено;
	
	Массив = ТаблицаСловарей.НайтиСтроки(Новый Структура("Имя", Тип));
	
	Если Массив.Количество() > 0 Тогда
		
		Результат = Массив[0];
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "атПомощникПродаж_УстановленоСоответствие" Тогда
		
		СтруктураОтбора = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, Параметр.СсылкаСловаря);
		
		НайденныеСтроки = ТаблицаОбъектов.НайтиСтроки(СтруктураОтбора);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НайденнаяСтрока.Значение = Параметр.СсылкаИБ;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры


#Область КомандыФормы

&НаКлиенте
Процедура Администрирование(Команда)
	
	ОткрытьФорму("Обработка.атПомощникПродаж.Форма.АдминистрированиеСервиса",,, "АдминистрированиеСервиса");
	
КонецПроцедуры

&НаКлиенте
Процедура Авторизация(Команда)
	
	ОткрытьФормуМодально("Обработка.атПомощникПродаж.Форма.АвторизацияПользователя",,, "АвторизацияПользователя");
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеСловарями(Команда)
	
	ОткрытьФорму("Обработка.атПомощникПродаж.Форма.Словари",,, "Словари");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСтатусыСервиса()
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	
	СтатусСервера = Цел(КодСостояния/10);
	//(0, "Сервис не используется");
	//(1, "Не указаны параметры партнера"); // Партнер еще не подключен к сервису?
	//(2, "Параметры партнера заполнены"); // Статус сервера не проверялся
	//(3, "Сервер не доступен"); // Пинг не прошел
	//(4, "Сервис доступен"); // Ок, Пинг партнера прошел
	//(5, "Ошибка данных партнера"); // Err, Пинг партнера не прошел
	//(6, "Доступ запрещен"); // Err, Пинг партнера не прошел
	
	СтатусПользователя = КодСостояния%10;
	//(0, "Запрещено использование");
	//(1, "Не указан логин");
	//(2, "Сохранен логин");
	//(3, "Авторизован"); // Ок
	//(4, "Отказ от авторизации");
	//(5, "Ошибка авторизации"); // Err
	
	ЭтоАдминистаторСервиса = (СтатусПользователя = 3 И ПараметрыСеанса.атДоступныеСервисы.Администратор);
	
	Элементы.Авторизация.Доступность = (СтатусСервера = 4);
	Элементы.ГруппаФормыОбработки.Доступность = (СтатусПользователя = 2 ИЛИ СтатусПользователя = 3);
	Элементы.ГруппаАдминистрирование.Доступность = ЭтоАдминистатор ИЛИ ЭтоАдминистаторСервиса;
	
	СтруктураСтатусаСервера = атПовторноеИспользование.ОписаниеСтатусаСервера(СтатусСервера);
	Элементы.СтатусСервиса.Заголовок = СтруктураСтатусаСервера.Заголовок;
	Если СтруктураСтатусаСервера.Ошибка Тогда
		Элементы.СтатусСервиса.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
	Иначе
		Элементы.СтатусСервиса.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	КонецЕсли;
	
	СтруктураСтатусаПользователя = атПовторноеИспользование.ОписаниеСтатусаПользователя(СтатусПользователя);
	Элементы.СтатусПользователя.Заголовок = СтруктураСтатусаПользователя.Заголовок;
	Если СтруктураСтатусаПользователя.Ошибка Тогда
		Элементы.СтатусПользователя.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
	Иначе
		Элементы.СтатусПользователя.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриОткрытииНаСервере(Отказ, Ошибка)
	
	Если ПараметрыСеанса.атКодСостояния%10 = 0 Тогда
		Ошибка = "Пользователю запрещено использовать сервис ""Помощник продаж"".";
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ПравоДоступа("Администрирование", Метаданные) Тогда
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(СокрЛП(ПараметрыСеанса.Пользователь.Код));
		ЭтоАдминистатор = ПользовательИБ <> Неопределено И ПользовательИБ.Роли.Содержит(Метаданные.Роли.ПолныеПрава);
	КонецЕсли;
	
	атСервер.СервисДоступен(Ошибка, Ложь); // Принудительно не нужно делать Пинг, только если его еще не было и сохранены логин и пароль партнера
	
	ЗаполнитьСтатусыСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Ошибка = "";
	ПриОткрытииНаСервере(Отказ, Ошибка);
	
	Если Отказ Тогда
		Если ЗначениеЗаполнено(Ошибка) Тогда
			Предупреждение(Ошибка);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// Если пароль пользователя сохранен, то мы автоматически авторизуемся
	атКлиент.АвторизацияПройдена(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "атПомощникПродаж_Авторизация"
		ИЛИ (ИмяСобытия = "ОбновитьПрава" И Параметр = ПараметрыСеанса.Пользователь) Тогда
		
		ЗаполнитьСтатусыСервиса();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти




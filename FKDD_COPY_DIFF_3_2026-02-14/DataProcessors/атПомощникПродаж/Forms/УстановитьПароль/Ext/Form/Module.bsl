
&НаСервере
Функция СменитьПарольНаСервере(Ошибка="")
	
	ТекстПредупреждения = "";
	
	Если ПустаяСтрока(НовыйПароль) Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка),"",Символы.ПС) + "Не указан новый пароль!";
	КонецЕсли;
	
	Если ПустаяСтрока(НовыйПарольПодтверждение) Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка),"",Символы.ПС) + "Не указано подтверждение пароля!";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(НовыйПароль) И НЕ ПустаяСтрока(НовыйПарольПодтверждение)
		И НовыйПароль <> НовыйПарольПодтверждение Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка),"",Символы.ПС) + "Новый пароль не совпадает с подтверждением!";
		//НовыйПарольПодтверждение = "";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Ошибка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Отбор, Данные",
						  Новый Структура("Код", Код),
						  Новый Структура("Пароль", НовыйПароль));
	РезультатЗапроса = атСервер.ВыполнитьЗапрос("dictionaries", "Пользователи", атСервер.ПолучитьТекстСообщения(СтруктураПараметров), "Изменить",, Ошибка);
	
	Если РезультатЗапроса = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Ошибка = "Пароль пользователя """+Код+": "+СокрЛП(Наименование)+""" успешно установлен.";
	
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура СменитьПароль(Команда)
	Ошибка = "";
	Результат = СменитьПарольНаСервере(Ошибка);
	Предупреждение(Ошибка);
	Если Результат Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПараметрыСеанса.атКодСостояния%10 <> 3 Тогда // Не авторизован
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыСеанса.атДоступныеСервисы.Администратор Тогда
		Сообщить("Недостаточно прав для изменения пароля пользователя!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "Код, Наименование");
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПроверитьДоступностьСменыПароля(Знач Логин, Ошибка="")
	
	Если ПустаяСтрока(Логин) Тогда
		Ошибка = "Не указан логин пользователя!";
		Возврат Ложь;
		
	ИначеЕсли атСервер.ПолучитьНастройку(ПараметрыСеанса.Пользователь, "Логин") <> Логин Тогда
		Ошибка = "Разрешено менять пароль только текущего пользователя!";
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция СменитьПарольНаСервере(Знач Логин, Знач Пароль, Знач НовыйПароль, Знач НовыйПарольПодтверждение, Знач СохранятьПароль, Ошибка="")
	
	Ошибка = "";
	
	Если ПустаяСтрока(Пароль) Тогда
		Ошибка = "Не указан старый пароль!";
	КонецЕсли;
	
	Если ПустаяСтрока(НовыйПароль) Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка), "", Символы.ПС) + "Не указан новый пароль!";
	КонецЕсли;
	
	Если ПустаяСтрока(НовыйПарольПодтверждение) Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка), "", Символы.ПС) + "Не указано подтверждение пароля!";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(НовыйПароль) 
		И НЕ ПустаяСтрока(НовыйПарольПодтверждение)
		И НовыйПароль <> НовыйПарольПодтверждение Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка), "", Символы.ПС) + "Новый пароль не совпадает с подтверждением!";
		//НовыйПарольПодтверждение = "";
	КонецЕсли;
	
	// Авторизуемся со старым паролем
	Ошибки = "";
	Если Не атСервер.ВыполнитьЗапросАвторизации(Логин, Пароль, Ошибки) Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка), "", Символы.ПС) + Ошибки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ошибка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Пользователь = ПараметрыСеанса.Пользователь;
	
	Если НЕ атСервер.ИзменитьПарольПользователя(Логин, НовыйПароль) Тогда
		Ошибка = "Не удалось изменить пароль!";
		Возврат Ложь;
	КонецЕсли;
	
	Если СохранятьПароль Тогда
		атСервер.ЗаписатьНастройку(Пользователь, "Пароль", НовыйПароль);
	Иначе
		атСервер.ЗаписатьНастройку(Пользователь, "Пароль", "");
	КонецЕсли;
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	ПараметрыСеанса.атКодСостояния = Цел(КодСостояния/10) + 2; // Установим статус пользователя - Сохранен логин
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СменитьПароль(Команда)
	
	Ошибка = "";
	
	Если Режим = "Авторизация" Тогда
		
		Если НЕ ПроверитьДоступностьСменыПароля(Логин, Ошибка) Тогда
			Предупреждение(Ошибка);
			Возврат;
		КонецЕсли;
		
		Режим = "СменаПароля";
		
		Пароль = "";
		
		Элементы.Логин.Доступность = Ложь;
		Элементы.НовыйПароль.Видимость = Истина;
		Элементы.НовыйПарольПодтверждение.Видимость = Истина;
		Элементы.Войти.Доступность = Ложь;
		
	ИначеЕсли Режим = "СменаПароля" Тогда
		
		Если НЕ СменитьПарольНаСервере(Логин, Пароль, НовыйПароль, НовыйПарольПодтверждение, СохранятьПароль, Ошибка) Тогда
			Предупреждение(Ошибка);
			Возврат;
		КонецЕсли;
		
		Режим = "Авторизация";
		
		Если СохранятьПароль Тогда
			Пароль = НовыйПароль;
		Иначе
			Пароль = "";
		КонецЕсли;
		
		ПараметрыСеанса.атИдентификаторПодключения = "";
		
		Элементы.Логин.Доступность = Истина;
		Элементы.НовыйПароль.Видимость = Ложь;
		Элементы.НовыйПарольПодтверждение.Видимость = Ложь;
		Элементы.Войти.Доступность = Истина;
		
	КонецЕсли;
	
	НовыйПароль = "";
	НовыйПарольПодтверждение = "";
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВойтиНаСервере(Знач Логин, Знач Пароль, Знач СохранятьПароль, Ошибка="")
	
	Если атСервер.ВыполнитьЗапросАвторизации(Логин, Пароль, Ошибка) Тогда
		
		Пользователь = ПараметрыСеанса.Пользователь;
		
		атСервер.ЗаписатьНастройку(Пользователь, "Логин", Логин);
		Если СохранятьПароль Тогда
			атСервер.ЗаписатьНастройку(Пользователь, "Пароль", Пароль);
		Иначе
			атСервер.ЗаписатьНастройку(Пользователь, "Пароль", "");
		КонецЕсли;
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Войти(Команда)
	
	Если ПустаяСтрока(Логин) Тогда
		Предупреждение("Не указан логин пользователя!");
	Иначе
		Ошибка = "";
		Если ВойтиНаСервере(Логин, Пароль, СохранятьПароль, Ошибка) Тогда
			Оповестить("атПомощникПродаж_Авторизация"); // оповестим формы о результате авторизации
			ЭтаФорма.Закрыть(Истина);
		Иначе
			Предупреждение(Ошибка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтменаНаСервере()
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	
	СтатусСервера = Цел(КодСостояния/10);
	// Статусы сервиса
	//(0, "Сервис не используется");
	//(1, "Не указаны параметры партнера"); // Партнер еще не подключен к сервису?
	//(2, "Параметры партнера заполнены"); // Статус сервера не проверялся
	//(3, "Сервер не доступен"); // Пинг не прошел
	//(4, "Сервис доступен"); // Ок, Пинг партнера прошел
	//(5, "Ошибка данных партнера"); // Err, Пинг партнера не прошел
	//(6, "Доступ запрещен"); // Err, Пинг партнера прошел, Доступ запрещен
	
	СтатусПользователя = КодСостояния%10;
	// Статусы пользователя
	//(0, "Запрещено использование");
	//(1, "Не указан логин");
	//(2, "Сохранен логин");
	//(3, "Авторизован"); // Ок
	//(4, "Отказ от авторизации");
	//(5, "Ошибка авторизации"); // Err
	
	
	Пользователь = ПараметрыСеанса.Пользователь;
	
	Если ЗначениеЗаполнено(атСервер.ПолучитьНастройку(Пользователь, "Логин")) Тогда
		НовыйСтатусПользователя = 4; // Авторизация отменена
	Иначе
		НовыйСтатусПользователя = 1; // Не указан логин
	КонецЕсли;
	
	ПараметрыСеанса.атКодСостояния = СтатусСервера*10 + НовыйСтатусПользователя;
	
	Возврат СтатусПользователя <> НовыйСтатусПользователя;
	
КонецФункции

&НаКлиенте
Процедура Отмена(Команда)
	
	Если ОтменаНаСервере() Тогда
		Оповестить("атПомощникПродаж_Авторизация"); // оповестим формы о результате авторизации
	КонецЕсли;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры


&НаСервере
Процедура ПриОткрытииНаСервере(Отказ, Ошибка)
	
	Если Не атСервер.СервисДоступен(Ошибка, Ложь) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Логин = атСервер.ПолучитьНастройку(ПараметрыСеанса.Пользователь, "Логин");
	Пароль = атСервер.ПолучитьНастройку(ПараметрыСеанса.Пользователь, "Пароль");
	
	СохранятьПароль = ЗначениеЗаполнено(Пароль);
	
	Режим = "Авторизация";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Ошибка = "";
	ПриОткрытииНаСервере(Отказ, Ошибка);
	
	Если Отказ Тогда
		Предупреждение(Ошибка);
	КонецЕсли;
	
КонецПроцедуры

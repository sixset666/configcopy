
&НаСервере
Функция ИзменитьНаСервере(Ошибка = "")
	
	Если Не ЗначениеЗаполнено(Код) Тогда
		Ошибка = "Не указан логин пользователя!";
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка),"",Символы.ПС) + "Не указано имя пользователя!";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Ошибка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПараметрыСеанса.атДоступныеСервисы.Администратор Тогда
		СтруктураПараметров = Новый Структура("Отбор, Данные",
		                      Новый Структура("Код", Код),
		                      Новый Структура("Наименование, Администратор", Наименование, Администратор));
	Иначе //не администратор сервера не может как-то влиять на признак администратора
		СтруктураПараметров = Новый Структура("Отбор, Данные",
		                      Новый Структура("Код", Код),
		                      Новый Структура("Наименование", Наименование));
	КонецЕсли;
	
	РезультатЗапроса = атСервер.ВыполнитьЗапрос("dictionaries", "Пользователи", атСервер.ПолучитьТекстСообщения(СтруктураПараметров), "Изменить",, Ошибка);
	Если РезультатЗапроса = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	//обновим пользователя в словарях, правда не понятно зачем!
	атСервер.ЗаписатьЭлемент(РезультатЗапроса);
	
	Ошибка = "Изменения успешно сохранены.";
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция СоздатьНаСервере(Ошибка="")
	
	Если ПустаяСтрока(Наименование) Тогда
		Ошибка = "Не указано имя пользователя!";
	КонецЕсли;
	
	Если ИмяМетода = "Создать" Тогда
		Если ПустаяСтрока(Пароль) Тогда
			Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка), "", Символы.ПС) + "Не указан пароль!";
		КонецЕсли;
		Если ПустаяСтрока(ПарольПодтверждение) Тогда
			Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка), "", Символы.ПС) + "Не указано подтверждение пароля!";
		КонецЕсли;
		Если НЕ ПустаяСтрока(Пароль) И НЕ ПустаяСтрока(ПарольПодтверждение) И Пароль <> ПарольПодтверждение Тогда
			Ошибка = Ошибка + ?(ПустаяСтрока(Ошибка), "", Символы.ПС) + "Пароль не совпадает с подтверждением!";
			ПарольПодтверждение = "";
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Ошибка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Данные", Новый Структура("Наименование, Пароль, Администратор", Наименование, Пароль,Администратор));
	
	РезультатЗапроса = атСервер.ВыполнитьЗапрос("dictionaries", "Пользователи", атСервер.ПолучитьТекстСообщения(СтруктураПараметров), "Создать",, Ошибка);
	
	Если РезультатЗапроса = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Создадим новую запись в атСловари
	атСервер.ЗаписатьЭлемент(РезультатЗапроса);
	
	//Если ЗначениеЗаполнено(Пользователь) Тогда
	//	атСервер.ЗаписатьНастройку(Пользователь, "Логин", РезультатЗапроса.Код);
	//КонецЕсли;
	
	Ошибка = "Пользователь """+СокрЛП(Наименование)+""" успешно создан.";
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СоздатьИзменить(Команда)
	
	Ошибка = "";
	Если ИмяМетода = "Создать" Тогда
		Результат = СоздатьНаСервере(Ошибка);
	ИначеЕсли ИмяМетода = "Изменить" Тогда
		Результат = ИзменитьНаСервере(Ошибка);
	КонецЕсли;
	
	Если Результат Тогда
		Оповестить("атПомощникПродаж_ПользовательИзменен");
		Закрыть();
	Иначе
		Предупреждение(Ошибка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПараметрыСеанса.атКодСостояния%10 <> 3 Тогда // Не авторизован
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "Код, Наименование, Администратор, ИмяМетода");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ИмяМетода = "Создать" И Не ПараметрыСеанса.атДоступныеСервисы.Администратор Тогда
		// Создавать может только администратор сервиса
		Предупреждение("Не достаточно прав для создания пользователя");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ПараметрыСеанса.атДоступныеСервисы.Администратор Тогда
		Элементы.Администратор.Видимость = Ложь;
	КонецЕсли;
	
	Если ИмяМетода = "Изменить" Тогда
		
		ПользовательОбъект = атСервер.ПолучитьОбъект("Пользователи", Код);
		
		Если Не ЗначениеЗаполнено(ПользовательОбъект) Тогда
			Предупреждение("Пользователь не найден");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПользовательОбъект);
		
		Элементы.Пароль.Видимость = Ложь;
		Элементы.ПарольПодтверждение.Видимость = Ложь;
		Элементы.Код.Видимость = Истина;
		Элементы.СоздатьИзменить.Заголовок = "Изменить пользователя";
		Элементы.ГруппаПользователь.Заголовок = "Пользователь";
		
		Заголовок = "Пользователь ""Помощник продаж""";
		
	Иначе //Если ИмяМетода = "Создать" Тогда
		
		Элементы.Пароль.Видимость = Истина;
		Элементы.ПарольПодтверждение.Видимость = Истина;
		Элементы.Код.Видимость = Ложь;
		Элементы.СоздатьИзменить.Заголовок = "Создать пользователя";
		Элементы.ГруппаПользователь.Заголовок = "Новый пользователь";
		Заголовок = "Новый пользователь ""Помощник продаж""";
		
	КонецЕсли;
	
КонецПроцедуры

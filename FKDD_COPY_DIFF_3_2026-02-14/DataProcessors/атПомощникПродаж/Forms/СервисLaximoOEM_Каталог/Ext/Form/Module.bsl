
&НаКлиенте
Процедура ВидимостьПанельЛевая(Команда)
	Элементы.ВидимостьПанельЛевая.Пометка = Не Элементы.ВидимостьПанельЛевая.Пометка;
	Элементы.ГруппаЛевая.Видимость = Элементы.ВидимостьПанельЛевая.Пометка;
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьПанельПравая(Команда)
	Элементы.ВидимостьПанельПравая.Пометка = Не Элементы.ВидимостьПанельПравая.Пометка;
	Элементы.ГруппаДетали.Видимость = Элементы.ВидимостьПанельПравая.Пометка;
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиАРМКорзина(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	Если ВыбранныйАвтомобиль.Источник = "Автомобиль" ИЛИ ВыбранныйАвтомобиль.Источник = "Модель" Тогда
		ПараметрыОткрытия.Вставить("Клиент", Неопределено);
		ПараметрыОткрытия.Вставить("Автомобиль", ВыбранныйАвтомобиль.Автомобиль);
	Иначе
		ПараметрыОткрытия.Вставить("Клиент", ВыбранныйАвтомобиль.Автомобиль);
		ПараметрыОткрытия.Вставить("Автомобиль", Неопределено);
	КонецЕсли;
	
	ФормаФункциональнойПанели = Обработки.ФункциональнаяПанель.ПолучитьФорму();
	Если ФормаФункциональнойПанели.Открыта() Тогда
		ФормаАРМ = Обработки.АРМКорзина.ПолучитьФорму(, ФормаФункциональнойПанели);
		Если ФормаАРМ.Открыта() Тогда
			Оповестить("АктивизироватьАРМКорзина", ПараметрыОткрытия);
		Иначе
			ФормаФункциональнойПанели.ОткрытьАРМ("АРМКорзина", ФормаФункциональнойПанели, ПараметрыОткрытия, ЭтаФорма);
		КонецЕсли;
	Иначе
		ФормаФункциональнойПанели.ЗапускатьАРМПоУмолчанию = Ложь;
		ФормаФункциональнойПанели.Открыть();
		ФормаФункциональнойПанели.ОткрытьАРМ("АРМКорзина", ФормаФункциональнойПанели, ПараметрыОткрытия, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНомераДеталей(Команда)
	ЗаполнитьРабочийЛистНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "атПомощникПродаж_СервисLaximoOEM_Каталог_ПараметрыОткрытия" Тогда
		Если Источник <> Неопределено И ЭтаФорма.ВладелецФормы = Источник Тогда
			ИнициализироватьАвтомобиль(Параметр);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "атПомощникПродаж_РабочийЛист_Записан" Тогда
		Если Параметр.Источник = ВыбранныйАвтомобиль.Источник И Параметр.Объект = ВыбранныйАвтомобиль.Автомобиль Тогда
			ЗаполнитьРабочийЛистНаСервере();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьВсеКаталогиНаСервере(Ошибка="")
	ТелоЗапроса = "{""Данные"":{""Locale"":""ru_RU""}}";
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "ListCatalogs", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СоздатьСтруктуруВыбранныйАвтомобильНаСервере()
	
	ВыбранныйАвтомобиль = Новый Структура;
	ВыбранныйАвтомобиль.Вставить("Источник", ""); //
	ВыбранныйАвтомобиль.Вставить("Автомобиль", Неопределено); // Автомобиль из ИБ или строка
	ВыбранныйАвтомобиль.Вставить("Каталог", ""); // Код каталога Laximo
	ВыбранныйАвтомобиль.Вставить("Модель", ""); // Код модели (каталог внутри Laximo)
	ВыбранныйАвтомобиль.Вставить("МодельНаименование", "");
	ВыбранныйАвтомобиль.Вставить("Модификация", ""); // Код модификации
	ВыбранныйАвтомобиль.Вставить("МодификацияНаименование", "");
	ВыбранныйАвтомобиль.Вставить("Производитель", ""); // Код производителя
	ВыбранныйАвтомобиль.Вставить("ИдентификаторЗапроса", "");
	
	ВыбранныйАвтомобиль.Вставить("Catalog", "");
	ВыбранныйАвтомобиль.Вставить("VehicleId", 0);
	ВыбранныйАвтомобиль.Вставить("ssd", "");
	ВыбранныйАвтомобиль.Вставить("CategoryId", -1);
	ВыбранныйАвтомобиль.Вставить("CategorySsd", "");
	ВыбранныйАвтомобиль.Вставить("QuickGroupId", 0);
	ВыбранныйАвтомобиль.Вставить("UnitId", 0);
	ВыбранныйАвтомобиль.Вставить("UnitSsd", "");
	
	Возврат ВыбранныйАвтомобиль;
	
КонецФункции

&НаСервере
Процедура ОчиститьТаблицыКаталога()
	
	ТекстHTML = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.атПомощникПродаж")).ПолучитьМакет("Laximo").ПолучитьТекст();
	ПолеHtml = СтрЗаменить(ТекстHTML, "%Сообщение%", "Не выбран автомобиль");
	
	// ДатаКарта
	ДатаКарта.ПолучитьЭлементы().Очистить();
	
	// ВыбранныйАвтомобильФильтр
	ВыбранныйАвтомобильФильтр.Очистить();
	Элементы.ВыбранныйАвтомобильФильтр.Видимость = Ложь;
	
	// Категории
	КатегорииДерево.ПолучитьЭлементы().Очистить();
	
	// БыстрыеГруппы
	НоменклатурныеГруппыДерево.ПолучитьЭлементы().Очистить();
	
	// Детали
	ДеталиУзла.Очистить();
	
	// ПримечаниеКУзлу
	
	//Элементы.ГруппаОбщая.Доступность = Ложь;
	//Элементы.ГруппаОбщая.Заголовок = "Каталог деталей";
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьФорму(Сообщение)
	
	ТекстHTML = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.атПомощникПродаж")).ПолучитьМакет("Laximo").ПолучитьТекст();
	ПолеHtml = СтрЗаменить(ТекстHTML, "%Сообщение%", Сообщение);
	
	Элементы.ГруппаОбщая.Доступность = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РабочийЛистПользователяНаСервере(Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "РабочийЛистПользователя", ТелоЗапроса,, Ложь, Ошибка);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЛокальныйРабочийЛистНаСервере(Знач Источник, Знач Объект, Знач Автор=Неопределено)
	Возврат атСервер.ПолучитьЛокальныйРабочийЛист(Источник, Объект, Автор);
КонецФункции

&НаСервере
Процедура ЗаполнитьРабочийЛистНаСервере()
	
	НомераДеталей.Очистить();
	СписокВыбораНаборов.Очистить();
	
	Если Не ЗначениеЗаполнено(ВыбранныйАвтомобиль.Автомобиль) Тогда
		Возврат;
	КонецЕсли;
	
	РабочийЛист = ПолучитьЛокальныйРабочийЛистНаСервере(ВыбранныйАвтомобиль.Источник, ВыбранныйАвтомобиль.Автомобиль);
	
	Если РабочийЛист = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторовЗапроса = Новый Массив;
	
	ПараметрыЗапроса = Неопределено;
	Если РабочийЛист.Свойство("Запросы") Тогда
		Для Каждого Запрос Из РабочийЛист.Запросы Цикл
			Если Не (ЗначениеЗаполнено(Запрос.МодельКод) И ЗначениеЗаполнено(Запрос.МодификацияКод)) Тогда
				Продолжить;
			КонецЕсли;
			Если Запрос.МодельКод = ВыбранныйАвтомобиль.Модель.Код И МассивИдентификаторовЗапроса.Найти(Запрос.Идентификатор) = Неопределено Тогда
				МассивИдентификаторовЗапроса.Добавить(Запрос.Идентификатор);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Заполним номера деталей
	Для Каждого НомерДетали Из РабочийЛист.НомераДеталей Цикл
		Если МассивИдентификаторовЗапроса.Найти(НомерДетали.ИдентификаторЗапроса) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДетали = НомераДеталей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДетали, НомерДетали);
		Если ЗначениеЗаполнено(НомерДетали.Параметры) Тогда
			Попытка
				СтрокаДетали.Параметры = ЗначениеИзСтрокиВнутр(НомерДетали.Параметры);
			Исключение КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	НаборыДеталей = НомераДеталей.Выгрузить(,"ИмяНабора");
	НаборыДеталей.Свернуть("ИмяНабора");
	НаборыДеталей.Сортировать("ИмяНабора Возр");
	СписокВыбораНаборов.ЗагрузитьЗначения(НаборыДеталей.ВыгрузитьКолонку("ИмяНабора"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьАвтомобиль(ПараметрыОткрытия)
	
	НовыйАвтомобиль = Ложь;
	НоваяМодификация = Ложь;
	НовыйУзел = Ложь;
	
	Источник = "";
	Автомобиль = Неопределено;
	Каталог = Новый Структура("Тип, Код, Наименование");
	Модель = Новый Структура("Тип, Код, Наименование");
	Модификация = Новый Структура("Тип, Код, Наименование");
	
	Если ПараметрыОткрытия.Свойство("Объект") И ЗначениеЗаполнено(ПараметрыОткрытия.Объект) Тогда
		Источник = ПараметрыОткрытия.Источник;
		Автомобиль = ПараметрыОткрытия.Объект;
	КонецЕсли;
	
	Если Автомобиль <> ВыбранныйАвтомобиль.Автомобиль ИЛИ Источник <> ВыбранныйАвтомобиль.Источник Тогда
		НовыйАвтомобиль = Истина;
		// Очистим все реквизиты выбранного автомобиля
		ВыбранныйАвтомобиль = СоздатьСтруктуруВыбранныйАвтомобильНаСервере();
		// Инициализируем все страницы
		ОчиститьТаблицыКаталога();
	КонецЕсли;
	
	ПараметрыОткрытия.Свойство("Каталог", Каталог);
	ПараметрыОткрытия.Свойство("Модель", Модель);
	ПараметрыОткрытия.Свойство("Модификация", Модификация);
	
	Если ПараметрыОткрытия.Свойство("ПараметрыЗапроса") И ЗначениеЗаполнено(ПараметрыОткрытия.ПараметрыЗапроса) Тогда
		ПараметрыЗапроса = ПараметрыОткрытия.ПараметрыЗапроса;
	КонецЕсли;
	
	// Запишем дату обращения к каталогу и получим при необходимости ПараметрыЗапроса, если их не передали извне!
	Результат = ОбновитьЗапросРабочегоЛистаНаСервере(Источник, Автомобиль, Каталог, Модель, Модификация, ПараметрыЗапроса, ВыбранныйАвтомобиль.ИдентификаторЗапроса);
	
	Если ПараметрыЗапроса = Неопределено Тогда
		ЗаблокироватьФорму("Не найдены параметры для открытия каталога!");
		Возврат;
	КонецЕсли;
	
	Если Результат = Истина Тогда
		// Реально добавлен новый запрос
		// Оповестим форму владельца о добавлении запроса
		СтруктураОповещения = Новый Структура;
		СтруктураОповещения.Вставить("Источник", Источник);
		СтруктураОповещения.Вставить("Объект",   Автомобиль);
		Оповестить("атПомощникПродаж_Запрос_Добавлен", СтруктураОповещения);
	КонецЕсли;
	
	ВыбранныйАвтомобиль.Источник    = Источник;
	ВыбранныйАвтомобиль.Автомобиль  = Автомобиль;
	ВыбранныйАвтомобиль.Каталог     = Каталог;
	ВыбранныйАвтомобиль.Модель      = Модель;
	ВыбранныйАвтомобиль.Модификация = Модификация;
	
	Если НовыйАвтомобиль Тогда
		Элементы.ГруппаОбщая.Заголовок = ВыбранныйАвтомобиль.Источник+" - "+ВыбранныйАвтомобиль.Автомобиль+":     "+Модель.Наименование+" ("+Модификация.Наименование+")";
		ЗаполнитьРабочийЛистНаСервере();
	КонецЕсли;
	
	Элементы.ГруппаОбщая.Доступность = Истина;
	
	Если НовыйАвтомобиль
		ИЛИ ПараметрыЗапроса.Catalog   <> ВыбранныйАвтомобиль.Catalog
		ИЛИ ПараметрыЗапроса.VehicleId <> ВыбранныйАвтомобиль.VehicleId
		ИЛИ ПараметрыЗапроса.ssd       <> ВыбранныйАвтомобиль.ssd Тогда
		
		НоваяМодификация = Истина;
		
		Если Не НовыйАвтомобиль Тогда
			ОчиститьТаблицыКаталога();
			ВыбранныйАвтомобиль.CategoryId   = -1;
			ВыбранныйАвтомобиль.CategorySsd  = "";
			ВыбранныйАвтомобиль.QuickGroupId = 0;
			ВыбранныйАвтомобиль.UnitId       = 0;
			ВыбранныйАвтомобиль.UnitSsd      = "";
		КонецЕсли;
		
		ВыбранныйАвтомобиль.Catalog   = ПараметрыЗапроса.Catalog;
		ВыбранныйАвтомобиль.VehicleId = ПараметрыЗапроса.VehicleId;
		ВыбранныйАвтомобиль.ssd       = ПараметрыЗапроса.ssd;
		
		Если ПараметрыЗапроса.Свойство("Filter")
			И ЗначениеЗаполнено(ПараметрыЗапроса.Filter)
			И ПараметрыЗапроса.Filter.Количество() > 0 Тогда
			Для Каждого Строка Из ПараметрыЗапроса.Filter Цикл
				СтрокаFilter = ВыбранныйАвтомобильФильтр.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаFilter, Строка);
			КонецЦикла;
			Элементы.ВыбранныйАвтомобильФильтр.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НоваяМодификация Тогда
		СтрокиКаталога = КаталогиВсе.НайтиСтроки(Новый Структура("code", ВыбранныйАвтомобиль.Catalog));
		Если СтрокиКаталога.Количество() > 0 Тогда
			СтрокаКаталога = СтрокиКаталога[0];
		КонецЕсли;
		Если СтрокаКаталога = Неопределено Тогда
			ЗаблокироватьФорму("Выбранный каталог устарел!");
			Возврат;
		КонецЕсли;
		
		ВыбранныйАвтомобиль.Производитель = СтрокаКаталога.Производитель; 
		
		ЭтаФорма.Элементы.СтраницыНавигации.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаИллюстрации;
		ЭтаФорма.Элементы.ГруппаБыстрыеГруппы.Видимость = СтрокаКаталога.supportquickgroups;
		
		ЗаполнитьДатаКартаНаСервере();
		ЗаполнитьГруппыКатегорииНаСервере(СтрокаКаталога.supportquickgroups);
	КонецЕсли;
	
	// Могли передать ПараметрыДетали для открытия каталога
	Если ПараметрыОткрытия.Свойство("ПараметрыДетали") Тогда
		ПараметрыДетали = ПараметрыОткрытия.ПараметрыДетали;
		Если НЕ (ПараметрыДетали.UnitId = ВыбранныйАвтомобиль.UnitId
			   И ПараметрыДетали.UnitSsd = ВыбранныйАвтомобиль.UnitSsd) Тогда
			ЗаполнитьЗначенияСвойств(ВыбранныйАвтомобиль, ПараметрыДетали);
			Если ВыбранныйАвтомобиль.QuickGroupId > 0 Тогда
				ВыборНоменклатурныхГруппНаСервере();
			Иначе
				ВыборКатегорииУзловНаСервере();
			КонецЕсли;
			ВыбранныйУзел = ПараметрыДетали.ВыбранныйУзел;
			ВыбратьУзел();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция SaveInHistoryНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "SaveInHistory", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ОбновитьЗапросРабочегоЛистаНаСервере(Знач Источник, Знач Объект, Знач Каталог, Знач Модель, Знач Модификация, Параметры, ИдентификаторЗапроса)
	Возврат атСервер.ОбновитьЗапросРабочегоЛиста(Источник, Объект, Новый Структура("Код, Наименование", "", "Laximo.OEM"), Каталог,, Модель, Модификация, Параметры, ИдентификаторЗапроса);
КонецФункции

&НаКлиенте
Процедура СохранитьАвтомобильВИсторию()
	
	Если Не ЗначениеЗаполнено(ВыбранныйАвтомобиль.Автомобиль) Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Новый Структура;
	Если ТипЗнч(ВыбранныйАвтомобиль.Автомобиль) = Тип("СправочникСсылка.Автомобили") Тогда
		Данные.Вставить("КодИБ", ВыбранныйАвтомобиль.Автомобиль.Код);
		Данные.Вставить("Наименование", ВыбранныйАвтомобиль.Автомобиль.Наименование);
	Иначе
		Данные.Вставить("ВидНомера", ВыбранныйАвтомобиль.Источник);
		Данные.Вставить("Номер", ВыбранныйАвтомобиль.Автомобиль);
	КонецЕсли;
	
	Данные.Вставить("Параметры", Новый Структура);
	Данные.Параметры.Вставить("Name",      ВыбранныйАвтомобиль.МодификацияНаименование);
	Данные.Параметры.Вставить("Catalog",   ВыбранныйАвтомобиль.Catalog);
	Данные.Параметры.Вставить("VehicleId", Формат(ВыбранныйАвтомобиль.VehicleId, "ЧГ=0"));
	Данные.Параметры.Вставить("ssd",       ВыбранныйАвтомобиль.ssd);
	Данные.Параметры.Вставить("Filter",    Новый Массив);
	
	Если ВыбранныйАвтомобильФильтр.Количество() > 0 Тогда
		Для Каждого Строка Из ВыбранныйАвтомобильФильтр Цикл
			Данные.Параметры.Filter.Добавить(Новый Структура("name, value, note, SSDModification", Строка.name, Строка.value, Строка.note, Строка.SSDModification));
		КонецЦикла;
	КонецЕсли;
	
	Ошибка = "";
	Результат = SaveInHistoryНаСервере(Данные, Ошибка);
	
	Если Результат = Неопределено Тогда
		Предупреждение(Ошибка);
		Возврат;
	КонецЕсли;
	
	ВыбранныйАвтомобиль.Каталог     = Результат.Каталог;
	ВыбранныйАвтомобиль.Модель      = Результат.Модель;
	ВыбранныйАвтомобиль.Модификация = Результат.Модификация;
	
	Если ОбновитьЗапросРабочегоЛистаНаСервере(ВыбранныйАвтомобиль.Источник, ВыбранныйАвтомобиль.Автомобиль, ВыбранныйАвтомобиль.Каталог, ВыбранныйАвтомобиль.Модель, ВыбранныйАвтомобиль.Модификация, Данные.Параметры, ВыбранныйАвтомобиль.ИдентификаторЗапроса) = Истина Тогда
		// Реально добавлен новый запрос
		СтруктураОповещения = Новый Структура;
		СтруктураОповещения.Вставить("Источник", ВыбранныйАвтомобиль.Источник);
		СтруктураОповещения.Вставить("Объект",   ВыбранныйАвтомобиль.Автомобиль);
		Оповестить("атПомощникПродаж_Запрос_Добавлен", СтруктураОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВыбранныйАвтомобиль = СоздатьСтруктуруВыбранныйАвтомобильНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ атКлиент.АвторизацияПройдена() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	КаталогиВсе.Очистить();
	
	Ошибка = "";
	СтруктураРезультата = ЗаполнитьВсеКаталогиНаСервере(Ошибка);
	
	Если Не ЗначениеЗаполнено(СтруктураРезультата) ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ СтруктураРезультата.row = Неопределено Тогда
		Предупреждение("OEM каталоги не доступны!"+Символы.ПС+Ошибка);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДанных Из СтруктураРезультата.row Цикл
		СтрокаКаталогаВсе = КаталогиВсе.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКаталогаВсе, СтрокаДанных);
	КонецЦикла;
	
	ВыбранныйАвтомобиль = СоздатьСтруктуруВыбранныйАвтомобильНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазобратьСтрокуСвойств(Знач СтрокаДляРазбора, Знач Разделитель="")
	
	МассивСтроки = Новый Массив();
	
	Если Не ЗначениеЗаполнено(Разделитель) Тогда //Определяем разделитель
		ЧислоСтрок = СтрЧислоСтрок(СтрокаДляРазбора);
		ЧислоТочекСЗапятой = СтрЧислоВхождений(СтрокаДляРазбора, ";");
		ЧислоЗапятых = СтрЧислоВхождений(СтрокаДляРазбора, ",");
		Если ЧислоСтрок > ЧислоТочекСЗапятой+1 И ЧислоСтрок > ЧислоЗапятых+1 Тогда
			Разделитель = Символы.ПС;
		Иначе
			Если ЧислоЗапятых > ЧислоТочекСЗапятой Тогда
				Разделитель = ",";
			Иначе
				Разделитель = ";";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Пока СтрДлина(СтрокаДляРазбора) > 0 Цикл
		ПозицияРазделителя = Найти(СтрокаДляРазбора, Разделитель);
		Если ПозицияРазделителя = 0 Тогда
			ПозицияРазделителя = СтрДлина(СтрокаДляРазбора) + 1;
		КонецЕсли;
		СтрокаПараметр = Лев(СтрокаДляРазбора, ПозицияРазделителя - 1);
		СтрокаДляРазбора = Сред(СтрокаДляРазбора, ПозицияРазделителя + 1);
		МассивСтроки.Добавить(УбратьКавычки(СтрокаПараметр));
	КонецЦикла;
	
	Если МассивСтроки.Количество() = 0 Тогда
		МассивСтроки.Добавить("");
	КонецЕсли;
	
	Возврат МассивСтроки;
	
КонецФункции

&НаСервереБезКонтекста
Функция УбратьКавычки(Знач СтрокаПараметр)
	СтрокаПараметр = СокрЛП(СтрокаПараметр);
	Если Лев(СтрокаПараметр, 1) = """" Тогда
		СтрокаПараметр = Сред(СтрокаПараметр, 2);
	КонецЕсли;
	Если Прав(СтрокаПараметр, 1) = """" Тогда
		СтрокаПараметр = Лев(СтрокаПараметр, СтрДлина(СтрокаПараметр)-1);
	КонецЕсли;
	СтрокаПараметр = СокрЛП(СтрокаПараметр);
	Возврат СтрокаПараметр;
КонецФункции

&НаСервереБезКонтекста
Функция GetVehicleInfoНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "GetVehicleInfo", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервере
Процедура ЗаполнитьДатаКартаНаСервере()
	
	ДатаКарта.ПолучитьЭлементы().Очистить();
	
	Ошибка = "";
	ТекVehicleId = Формат(ВыбранныйАвтомобиль.vehicleid, "ЧГ=0");
	Данные = Новый Структура("Catalog, VehicleId, ssd", ВыбранныйАвтомобиль.catalog, ТекVehicleId, ВыбранныйАвтомобиль.ssd);
	СтруктураРезультата = GetVehicleInfoНаСервере(Данные, Ошибка);
	
	Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ СтруктураРезультата.row = Неопределено Тогда
		Сообщить(Ошибка);
		Возврат;
	КонецЕсли;
	
	мСвойстваМодификации = СтруктураРезультата.row;
	ПреобразоватьАттрибутыВРеквизиты(мСвойстваМодификации);
	
	Для Каждого КлючИЗначение ИЗ мСвойстваМодификации Цикл
		
		Ключ = КлючИЗначение.Ключ;
		
		Если ВыбранныйАвтомобиль.Свойство(Ключ) Тогда
			// Это служебный ключ
			Продолжить;
		КонецЕсли;
		
		Строка = ДатаКарта.ПолучитьЭлементы().Добавить();
		
		ЗаголовокСвойства = "";
		ПредставлениеСвойства = "";
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Структура") Тогда
			// Это перенесенные из аттрибутов в реквизиты
			Если Не КлючИЗначение.Значение.Свойство("name", ЗаголовокСвойства) Тогда
				// или Ссылка на словарь
				Если КлючИЗначение.Значение.Свойство("Тип") Тогда
					ЗаголовокСвойства = "Словарь:"+КлючИЗначение.Значение.Тип; // Это тип словаря
				КонецЕсли;
			КонецЕсли;
			Если Не КлючИЗначение.Значение.Свойство("value", ПредставлениеСвойства) Тогда
				КлючИЗначение.Значение.Свойство("Наименование", ПредставлениеСвойства); // Это наименование элемента словаря
			КонецЕсли;
		Иначе
			ЗаголовокСвойства = КлючИЗначение.Ключ;
			ПредставлениеСвойства = КлючИЗначение.Значение;
		КонецЕсли;
			
		Строка.Заголовок = ЗаголовокСвойства;
		
		Если СтрДлина(ПредставлениеСвойства) >= 50 ИЛИ НРег(Ключ) = "options" Тогда
			МассивСвойств = РазобратьСтрокуСвойств(ПредставлениеСвойства);
			Если МассивСвойств.Количество()>1 Тогда
				Для Каждого Свойство2 Из МассивСвойств Цикл
					Строка2 = Строка.ПолучитьЭлементы().Добавить();
					Строка2.ЗначениеСвойства = Свойство2;
				КонецЦикла;
			Иначе
				Строка.ЗначениеСвойства = МассивСвойств[0];
			КонецЕсли;
		Иначе
			Строка.ЗначениеСвойства = ПредставлениеСвойства;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ListQuickGroupНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "ListQuickGroup", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ListCategoriesНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "ListCategories", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервере
Процедура ЗаполнитьГруппыКатегорииНаСервере(supportquickgroups)
	
	ТекVehicleId = Формат(ВыбранныйАвтомобиль.vehicleid, "ЧГ=0");
	
	//мНоменклатурныеГруппыДеревоОсновные = РеквизитФормыВЗначение("НоменклатурныеГруппыДеревоОсновные");
	//мНоменклатурныеГруппыДеревоОсновные.Строки.Очистить();
	мНоменклатурныеГруппыДерево = РеквизитФормыВЗначение("НоменклатурныеГруппыДерево");
	мНоменклатурныеГруппыДерево.Строки.Очистить();
	мКатегорииДерево = РеквизитФормыВЗначение("КатегорииДерево");
	мКатегорииДерево.Строки.Очистить();
	
	Ошибка = "";
	Если supportquickgroups Тогда
		
		Данные = Новый Структура("Catalog, VehicleId, ssd", ВыбранныйАвтомобиль.catalog, ТекVehicleId, ВыбранныйАвтомобиль.ssd);
		СтруктураРезультата = ListQuickGroupНаСервере(Данные, Ошибка);
		
		Если СтруктураРезультата <> Неопределено Тогда
			Если ТипЗнч(СтруктураРезультата) = Тип("Структура") Тогда
				ТаблицаLaximo = Новый Массив;
				ТаблицаLaximo.Добавить(СтруктураРезультата);
			Иначе
				ТаблицаLaximo = СтруктураРезультата;
			КонецЕсли;
			
			Для Каждого Группа Из ТаблицаLaximo Цикл
				ЗаполнитьДеревоБыстрыхГруппНаСервере(мНоменклатурныеГруппыДерево, Группа.row.row);
			КонецЦикла;
		Иначе
			Сообщить(Ошибка);
		КонецЕсли;
	КонецЕсли;
	
	Данные = Новый Структура("Catalog, VehicleId, ssd, CategoryId", ВыбранныйАвтомобиль.catalog, ТекVehicleId, ВыбранныйАвтомобиль.ssd, "-1");
	СтруктураРезультата = ListCategoriesНаСервере(Данные, Ошибка);
	
	Если СтруктураРезультата <> Неопределено И СтруктураРезультата.Свойство("row") И ЗначениеЗаполнено(СтруктураРезультата.row) Тогда
		Row = СтруктураРезультата.row;
		Для Каждого Группа Из Row Цикл
			Если ЗначениеЗаполнено(Группа.ParentCategoryId) Тогда
				Родитель = мКатегорииДерево.Строки.Найти(Группа.ParentCategoryId, "CategoryId");
				Если Родитель = Неопределено Тогда
					Родитель = мКатегорииДерево;
				КонецЕсли;
			Иначе
				Родитель = мКатегорииДерево;
			КонецЕсли;
			СтрокаДанных = Родитель.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанных, Группа);
		КонецЦикла;
	Иначе
		Сообщить(Ошибка);
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(мНоменклатурныеГруппыДерево, "НоменклатурныеГруппыДеревоОсновные");
	ЗначениеВРеквизитФормы(мНоменклатурныеГруппыДерево, "НоменклатурныеГруппыДерево");
	
	мКатегорииДерево.Строки.Сортировать("name возр", Истина);
	ЗначениеВРеквизитФормы(мКатегорииДерево, "КатегорииДерево");
	
	ВыборКатегорииУзловНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоБыстрыхГруппНаСервере(Родитель, ТаблицаДанных)
	Если ТипЗнч(ТаблицаДанных) = Тип("Структура") Тогда
		Группа = ТаблицаДанных;
		СтрокаДанных = Родитель.Строки.Добавить();
		СтрокаДанных.quickgroupid = Группа.quickgroupid;
		СтрокаДанных.name = Группа.name;
		СтрокаДанных.link = Группа.link;
		Если Группа.Свойство("row") Тогда
			ЗаполнитьДеревоБыстрыхГруппНаСервере(СтрокаДанных, Группа.row);
		КонецЕсли;
	Иначе
		Для Каждого Группа Из ТаблицаДанных Цикл
			СтрокаДанных = Родитель.Строки.Добавить();
			СтрокаДанных.quickgroupid = Группа.quickgroupid;
			СтрокаДанных.name = Группа.name;
			СтрокаДанных.link = Группа.link;
			Если Группа.Свойство("row") Тогда
				ЗаполнитьДеревоБыстрыхГруппНаСервере(СтрокаДанных, Группа.row);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ПреобразоватьАттрибутыВРеквизиты(СтрокаДанных)
	Если НЕ ЗначениеЗаполнено(СтрокаДанных) Тогда
		Возврат;
	КонецЕсли;
	
	attribute = Новый Массив;
	Если СтрокаДанных.Свойство("attribute", attribute) = Истина Тогда
		Если ТипЗнч(attribute) = Тип("Массив") Тогда
			Для Каждого СтрокаМассива  Из attribute Цикл
				СтрокаДанных.Вставить(СтрЗаменить(СтрокаМассива.key, " ", "_"), Новый Структура("name, value", СтрокаМассива.name, СтрокаМассива.value)); 
			КонецЦикла;
		ИначеЕсли ТипЗнч(attribute) = Тип("Структура") Тогда
			СтрокаДанных.Вставить(СтрЗаменить(attribute.key, " ", "_"), Новый Структура("name, value", attribute.name, attribute.value)); 
		КонецЕсли;
		СтрокаДанных.Удалить("attribute");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФильтрБыстрыхГруппОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Элемент.ТекстРедактирования) Тогда
		Если Элемент.ТекстРедактирования <> ФильтрБыстрыхГрупп Тогда
			НоменклатурныеГруппыДерево.ПолучитьЭлементы().Очистить();
			
			Текст = Элемент.ТекстРедактирования; 
			НайтиБыстрыеГруппыНаСервере(Текст);
			
			Для каждого Строка Из НоменклатурныеГруппыДерево.ПолучитьЭлементы() Цикл
				Элементы.НоменклатурныеГруппыДерево.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
			КонецЦикла;
		КонецЕсли;
	Иначе
		СкопироватьНоменклатурныеГруппыДеревоНаСервере();
	КонецЕсли;
	ФильтрБыстрыхГрупп = Текст;
КонецПроцедуры

&НаСервере
Процедура НайтиБыстрыеГруппыНаСервере(Текст)
	
	МассивСтрок = Новый Массив;
	МассивПодстрок = РазобратьСтрокуTXT(СокрЛП(Текст), " ");
	
	НайтиБыстрыеГруппыПоФильтру(0, МассивПодстрок, МассивСтрок);
	
	Для Каждого НайденнаяСтрока Из МассивСтрок Цикл
		 
		Родитель = НайтиРодителя(НайденнаяСтрока);
		
		мНоменклатурныеГруппыДерево = РеквизитФормыВЗначение("НоменклатурныеГруппыДерево");
		
		Если Родитель = Неопределено Тогда
			Родитель = мНоменклатурныеГруппыДерево;
		Иначе
			Родитель = мНоменклатурныеГруппыДерево.Строки.Найти(Родитель.quickgroupid, "quickgroupid", Истина);
		КонецЕсли;
		
		НоваяСтрока = Родитель.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока,, "Родитель");
		
		ЗначениеВРеквизитФормы(мНоменклатурныеГруппыДерево, "НоменклатурныеГруппыДерево"); 
		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура НайтиБыстрыеГруппыПоФильтру(Родитель, МассивСтрокПоиска, МассивНайденныхСтрок)
	
	Если Родитель = 0 Тогда 
		Родитель = РеквизитФормыВЗначение("НоменклатурныеГруппыДеревоОсновные");
	КонецЕсли;
	
	Для Каждого СтрокаДерева Из Родитель.Строки Цикл
		
		Нашли = Истина;
		
		Для Каждого СтрокаПоиска Из МассивСтрокПоиска Цикл
			Если Найти(ВРег(СтрокаДерева.name), ВРег(СтрокаПоиска)) = 0 Тогда
				Нашли = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Нашли Тогда
			МассивНайденныхСтрок.Добавить(СтрокаДерева);
		КонецЕсли;
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			НайтиБыстрыеГруппыПоФильтру(СтрокаДерева, МассивСтрокПоиска, МассивНайденныхСтрок);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НайтиРодителя(ПодчиненнаяСтрока)
	
	Если ПодчиненнаяСтрока.Уровень() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	мНоменклатурныеГруппыДерево = РеквизитФормыВЗначение("НоменклатурныеГруппыДерево");
	
	Родитель = мНоменклатурныеГруппыДерево.Строки.Найти(ПодчиненнаяСтрока.Родитель.quickgroupid, "quickgroupid", Истина);
	
	Если Родитель = Неопределено Тогда
		
		Родитель = НайтиРодителя(ПодчиненнаяСтрока.Родитель);
		
		Если Родитель = Неопределено Тогда
			
			Родитель = мНоменклатурныеГруппыДерево.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(Родитель, ПодчиненнаяСтрока.Родитель,, "Родитель");
			
			ЗначениеВРеквизитФормы(мНоменклатурныеГруппыДерево, "НоменклатурныеГруппыДерево");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Родитель;
	
КонецФункции

&НаСервере
Процедура СкопироватьНоменклатурныеГруппыДеревоНаСервере()
	
	мНоменклатурныеГруппыДерево = РеквизитФормыВЗначение("НоменклатурныеГруппыДерево");
	мНоменклатурныеГруппыДеревоОсновные = РеквизитФормыВЗначение("НоменклатурныеГруппыДеревоОсновные");
	
	мНоменклатурныеГруппыДерево = мНоменклатурныеГруппыДеревоОсновные.Скопировать();
	
	ЗначениеВРеквизитФормы(мНоменклатурныеГруппыДерево, "НоменклатурныеГруппыДерево");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазобратьСтрокуTXT(Знач СтрокаДляРазбора,Разделитель=";",МаксПоле=0) Экспорт
	МассивСтрокиФайлаПрайсЛиста=Новый Массив();
	Пока СтрДлина(СтрокаДляРазбора)>0 Цикл
		ПозицияРазделителя=Найти(СтрокаДляРазбора,Разделитель);
		Если ПозицияРазделителя=0 Тогда
			ПозицияРазделителя=СтрДлина(СтрокаДляРазбора)+1;
		КонецЕсли;
		СтрокаПараметр=Лев(СтрокаДляРазбора,ПозицияРазделителя-1);
		СтрокаДляРазбора=Сред(СтрокаДляРазбора,ПозицияРазделителя+1);
		МассивСтрокиФайлаПрайсЛиста.Добавить(СокрЛП(СтрокаПараметр));
	КонецЦикла; 
	Если МаксПоле>0 Тогда
		Пока МаксПоле>МассивСтрокиФайлаПрайсЛиста.ВГраница() Цикл
			МассивСтрокиФайлаПрайсЛиста.Добавить("");
		КонецЦикла; 
	КонецЕсли; 
	Возврат МассивСтрокиФайлаПрайсЛиста;
КонецФункции

&НаКлиенте
Процедура ФильтрБыстрыхГруппОчистка(Элемент, СтандартнаяОбработка)
	СкопироватьНоменклатурныеГруппыДеревоНаСервере();
КонецПроцедуры

&НаКлиенте
Функция НайтиПомеченнуюСтрокуРекурсивно(Дерево)
	Для Каждого СтрокаДерева Из Дерево.ПолучитьЭлементы() Цикл
		Если СтрокаДерева.Выбрана Тогда
			Возврат СтрокаДерева;
		КонецЕсли;
		ПомеченнаяСтрока = НайтиПомеченнуюСтрокуРекурсивно(СтрокаДерева);
		Если ПомеченнаяСтрока <> Неопределено Тогда
			Возврат ПомеченнаяСтрока;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

&НаКлиенте
Процедура УстановитьПометкуВыбранаРекурсивно(Дерево, Пометка=Ложь)
	Для Каждого СтрокаДерева Из Дерево.ПолучитьЭлементы() Цикл
		СтрокаДерева.Выбрана = Пометка;
		УстановитьПометкуВыбранаРекурсивно(СтрокаДерева, Пометка);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КатегорииДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Элементы.КатегорииДерево.Развернуть(ВыбраннаяСтрока);
	
	УстановитьПометкуВыбранаРекурсивно(НоменклатурныеГруппыДерево, Ложь);
	УстановитьПометкуВыбранаРекурсивно(КатегорииДерево, Ложь);
	
	ТекСтрока = КатегорииДерево.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ТекСтрока.Выбрана = Истина;
	
	ВыбранныйАвтомобиль.QuickGroupId = 0;
	
	ВыбранныйАвтомобиль.CategoryId = ТекСтрока.CategoryId;
	ВыбранныйАвтомобиль.CategorySsd = ТекСтрока.Ssd;
	
	Элементы.НадписьПримечаниеКУзлу.Видимость = Ложь;
	Элементы.НадписьПримечаниеКУзлу.Заголовок = "";
	
	ВыборКатегорииУзловНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьВыборУзловНаСервере()
	
	//Обработки.атПомощникПродаж.ПолучитьМакет("Laximo").ПолучитьТекст();
	ТекстHTML = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.атПомощникПродаж")).ПолучитьМакет("Laximo").ПолучитьТекст();
	ПолеHtml = СтрЗаменить(ТекстHTML, "%Сообщение%", "Не выбрана иллюстрация или группа номенклатуры");
	
	ДеревоУзлов.ПолучитьЭлементы().Очистить();
	ДеталиУзла.Очистить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ListUnitsНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "ListUnits", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервере
Процедура ВыборКатегорииУзловНаСервере()
	 
	ОчиститьВыборУзловНаСервере();
	ВыбранныйУзел = 0;
	
	VehicleId = Формат(ВыбранныйАвтомобиль.VehicleId, "ЧГ=0");
	CategoryId = Формат(?(ВыбранныйАвтомобиль.CategoryId > 0, ВыбранныйАвтомобиль.CategoryId, -1), "ЧГ=0");
	CategorySsd = ?(ВыбранныйАвтомобиль.CategoryId > 0, ВыбранныйАвтомобиль.CategorySsd, ВыбранныйАвтомобиль.Ssd);
	
	Ошибка = "";
	Данные = Новый Структура("Catalog, VehicleId, CategoryId, ssd", ВыбранныйАвтомобиль.Catalog, VehicleId, CategoryId, CategorySsd);
	СтруктураРезультата = ListUnitsНаСервере(Данные, Ошибка);
	
	Если СтруктураРезультата <> Неопределено И СтруктураРезультата.Свойство("row") И СтруктураРезультата.row <> Неопределено Тогда
		
		Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
			СтрокиДанных = СтруктураРезультата.row;
		Иначе
			СтрокиДанных = Новый Массив;
			СтрокиДанных.Добавить(СтруктураРезультата.row);
		КонецЕсли;
		
		ДеревоУзлов.ПолучитьЭлементы().Очистить();
		
		ВсегоУзлов = СтрокиДанных.Количество();
		
		Для Каждого Узел Из СтрокиДанных Цикл
			
			Если Не ЗначениеЗаполнено(Узел) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаУзел = ДеревоУзлов.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаУзел, Узел);
			СтрокаУзел.CategoryId = ВыбранныйАвтомобиль.CategoryId;
			//СтрокаУзел.QuickGroupId = 0;
			
			Attributes = Новый Массив;
			Если Узел.Свойство("attribute") Тогда
				Если ТипЗнч(Узел.attribute) = Тип("Структура") Тогда
					attribute = Новый Структура(Новый ФиксированнаяСтруктура(Узел.attribute));
					Attributes.Добавить(attribute);
				Иначе
					// Массив
					attribute = Новый Массив(Новый ФиксированныйМассив(Узел.attribute));
					Attributes = attribute;
				КонецЕсли;
				Узел.Удалить("attribute");
				
				ПримОсновное = "";
				ПримФильтра = "";
				Для Каждого Примечание Из Attributes Цикл
					Если Примечание.key = "filter_note" Тогда
						ПримФильтра = Примечание.value;
					ИначеЕсли Примечание.key = "note" Тогда 
						ПримОсновное = Примечание.value;
					КонецЕсли;
				КонецЦикла;
				СтрокаУзел.Note = ПримОсновное + " " + ПримФильтра;
			КонецЕсли;
			//СтрокаУзел.Attributes = Attributes;
			
		КонецЦикла;
	Иначе
		Сообщить(Ошибка);
	КонецЕсли;
	
	ЗаполнитьВыборУзловНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВыборУзловНаСервере()
	
	Элементы.НадписьПримечаниеКУзлу.Видимость = Ложь;
	Элементы.НадписьПримечаниеКУзлу.Заголовок = "";
	
	мОбъект = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.атПомощникПродаж"));
	
	Если ДеревоУзлов.ПолучитьЭлементы().Количество() = 0 Тогда
		ТекстHTML = мОбъект.ПолучитьМакет("Laximo").ПолучитьТекст();
		ТекстHTML = СтрЗаменить(ТекстHTML, "%Сообщение%", "Нет информации для отображения!");
		
	Иначе
		PictureSizeLaximo = Новый Соответствие;
		PictureSizeLaximo.Вставить(1,"150");
		PictureSizeLaximo.Вставить(2,"175");
		PictureSizeLaximo.Вставить(3,"200");
		PictureSizeLaximo.Вставить(4,"225");
		PictureSizeLaximo.Вставить(5,"250");
		PictureSizeLaximo.Вставить(6,"source"); //Оригинальный размер
		
		FileName_jquery_js = "http://wsdemo.laximo.ru/components/com_guayaquil/guayaquillib/render/jquery.js?1348830397";
		ТекстHTML = мОбъект.ПолучитьМакет("NodeList").ПолучитьТекст();
		ТекстHTML = СтрЗаменить(ТекстHTML,"%FileName_jquery_js%","<SCRIPT type=text/javascript src="""+FileName_jquery_js+"""></SCRIPT>");
		
		ТекстDiv = мОбъект.ПолучитьМакет("DivNode").ПолучитьТекст();
		
		Для Сч = 1 По ДеревоУзлов.ПолучитьЭлементы().Количество() Цикл
			// http://img.laximo.net/BMW0712/%size%/2635.gif
			
			Узел = ДеревоУзлов.ПолучитьЭлементы()[Сч-1];
			
			НовыйDiv = ТекстDiv;
			НовыйDiv = СтрЗаменить(НовыйDiv,"%nodeid%","nodeNo"+Формат(Сч,"ЧГ=0"));
			НовыйDiv = СтрЗаменить(НовыйDiv,"%imageurl%", Узел.ImageURL);
			НовыйDiv = СтрЗаменить(НовыйDiv,"%size%", PictureSizeLaximo[5]);
			НовыйDiv = СтрЗаменить(НовыйDiv,"%nodename%", Узел.Name);
			НовыйDiv = СтрЗаменить(НовыйDiv,"%nodecode%", Узел.Code);
			НовыйDiv = СтрЗаменить(НовыйDiv,"%nodenote%", СокрЛП(""+Узел.Note));
			
			ТекстHTML = СтрЗаменить(ТекстHTML,"%НовыйNode%",НовыйDiv);
			
		КонецЦикла;
		
		ТекстHTML = СтрЗаменить(ТекстHTML,"%НовыйNode%","");
	КонецЕсли;
	
	ПолеHtml = ТекстHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьУзел()
	
	мДеревоУзлов = ДеревоУзлов.ПолучитьЭлементы();
	
	Если ВыбранныйУзел = 0 ИЛИ ВыбранныйУзел > мДеревоУзлов.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(мДеревоУзлов[ВыбранныйУзел-1].Filter) Тогда
		
		СтрокаУзла = мДеревоУзлов[ВыбранныйУзел-1];
		
		UnitSsd = СтрокаУзла.Ssd;
		Для каждого Filter Из ВыбранныйАвтомобильФильтр Цикл
			UnitSsd = UnitSsd + Filter.SSDModification;
		КонецЦикла; 
		
		Ошибка = "";
		Данные = Новый Структура("Catalog, VehicleId, UnitId, Filter, ssd", ВыбранныйАвтомобиль.Catalog, Формат(ВыбранныйАвтомобиль.VehicleId, "ЧГ=0"), Формат(СтрокаУзла.UnitId, "ЧГ=0"), СтрокаУзла.Filter, UnitSsd);
		СтруктураРезультата = GetFilterByUnitНаСервере(Данные, Ошибка);
		
		Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ СтруктураРезультата.row = Неопределено Тогда
			Сообщить(Ошибка);
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
			ФильтрыДляУзла = СтруктураРезультата.row;
		Иначе
			ФильтрыДляУзла = Новый Массив;
			ФильтрыДляУзла.Добавить(СтруктураРезультата.row);
		КонецЕсли;
		
		Если ФильтрыДляУзла.Количество() > 0 Тогда
			
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("Данные", ФильтрыДляУзла);
			
			#Если ТонкийКлиент Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьФильтрДляУзла",ЭтаФорма);
				ОткрытьФорму("Обработка.атПомощникПродаж.Форма.СервисLaximoOEM_ВыборУсловия", СтруктураПараметров, ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				
			#Иначе
				Результат = ОткрытьФормуМодально("Обработка.атПомощникПродаж.Форма.СервисLaximoOEM_ВыборУсловия", СтруктураПараметров);
				Если Результат <> Неопределено Тогда
					Для Каждого СтрокаРезультата Из Результат Цикл
						Если СтрокаРезультата.Value <> "" Тогда
							НовыйФильтр = ВыбранныйАвтомобильФильтр.Добавить();
							ЗаполнитьЗначенияСвойств(НовыйФильтр, СтрокаРезультата);
						КонецЕсли;
					КонецЦикла;
					Если ВыбранныйАвтомобильФильтр.Количество() <> 0 Тогда
						Элементы.ВыбранныйАвтомобильФильтр.Видимость = Истина;
					КонецЕсли;
					СохранитьАвтомобильВИсторию();
				КонецЕсли;
				
			#КонецЕсли
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВыбратьУзелНаСервере();
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ListDetailByUnitНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	
	СтруктураРезультата = атСервер.ВыполнитьЗапрос("laximooem", "ListDetailByUnit", ТелоЗапроса,,, Ошибка);
	
	Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ НЕ ЗначениеЗаполнено(СтруктураРезультата.row) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
		Row = СтруктураРезультата.row;
	Иначе
		Row = Новый Массив;
		Row.Добавить(СтруктураРезультата.row);
	КонецЕсли;
	
	Возврат Row;
КонецФункции

&НаСервереБезКонтекста
Функция ListImageMapByUnitНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	
	СтруктураРезультата = атСервер.ВыполнитьЗапрос("laximooem", "ListImageMapByUnit", ТелоЗапроса,,, Ошибка);
	
	Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ НЕ ЗначениеЗаполнено(СтруктураРезультата.row) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
		Row = СтруктураРезультата.row;
	Иначе
		Row = Новый Массив;
		Если ЗначениеЗаполнено(СтруктураРезультата.row) Тогда
			Row.Добавить(СтруктураРезультата.row);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Row;
КонецФункции

&НаСервере
Процедура ВыбратьУзелНаСервере()
	
	мОбъект = ДанныеФормыВЗначение(Объект, Тип("ОбработкаОбъект.атПомощникПродаж"));
	
	ДеталиУзла.Очистить();
	
	мДеревоУзлов = ДеревоУзлов.ПолучитьЭлементы();
	СтрокаДанных = мДеревоУзлов[ВыбранныйУзел-1];
	
	Элементы.НадписьПримечаниеКУзлу.Видимость = ЗначениеЗаполнено(СтрокаДанных.Note);
	Элементы.НадписьПримечаниеКУзлу.Заголовок = СтрокаДанных.Note;
	
	ВыбранныйАвтомобиль.UnitId = СтрокаДанных.UnitId;
	ВыбранныйАвтомобиль.UnitSsd = СтрокаДанных.Ssd;
	
	PictureSizeLaximo = Новый Соответствие;
	PictureSizeLaximo.Вставить(1,"150");
	PictureSizeLaximo.Вставить(2,"175");
	PictureSizeLaximo.Вставить(3,"200");
	PictureSizeLaximo.Вставить(4,"225");
	PictureSizeLaximo.Вставить(5,"250");
	PictureSizeLaximo.Вставить(6,"source"); //Оригинальный размер

	UrlКартинки = СтрЗаменить(СтрокаДанных.ImageURL, "%size%", PictureSizeLaximo[6]);
	//NodeЗаголовок = СтрокаДанных.Name + "<BR/>" + ?(ЗначениеЗаполнено(СтрокаДанных.Note), " ("+СтрокаДанных.Note+")", "");
	NodeЗаголовок = СтрокаДанных.Name;
	
	НазадВидимость  = НЕ (ВсегоУзлов = 0 ИЛИ ВыбранныйУзел <= 1);
	ВпередВидимость = НЕ (ВсегоУзлов = 0 ИЛИ ВыбранныйУзел >= ВсегоУзлов);
	
	/////////////////ПОЛУЧИМ ПЕРЕЧЕНЬ ДЕТАЛЕЙ В АГРЕГАТЕ АВТОМОБИЛЯ/////////////////
	
	UnitSsd = СтрокаДанных.Ssd;
	Для каждого Filter Из ВыбранныйАвтомобильФильтр Цикл
		UnitSsd = UnitSsd + Filter.SSDModification;
	КонецЦикла; 
	
	Ошибка = "";
	Данные = Новый Структура("Catalog, UnitId, ssd", ВыбранныйАвтомобиль.Catalog, Формат(СтрокаДанных.UnitId, "ЧГ=0"), UnitSsd);
	Row = ListDetailByUnitНаСервере(Данные, Ошибка);
	
	Если Row = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Деталь Из Row Цикл
		Если Не ЗначениеЗаполнено(Деталь) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаДетали = ДеталиУзла.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДетали, Деталь);
		СтрокаДетали.АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Деталь.OEM);
		
		Если Деталь.Свойство("amount") И ЗначениеЗаполнено(Деталь.amount) Тогда
			Попытка
				СтрокаДетали.Количество = Число(Деталь.amount);
			Исключение КонецПопытки;
		КонецЕсли;
		
		Если Деталь.Свойство("attribute") Тогда
			Если ТипЗнч(Деталь.attribute) = Тип("Структура") Тогда
				attributes = Новый Массив;
				attributes.Добавить(Новый Структура(Новый ФиксированнаяСтруктура(Деталь.attribute)));
			Иначе
				attributes = Новый Массив(Новый ФиксированныйМассив(Деталь.attribute));
			КонецЕсли;
			Деталь.Удалить("attribute");
			
			//Заполним свойства детали
			СтрокаДетали.ПримечанияДетали.ПолучитьЭлементы().Очистить();
			Для Каждого attribute Из attributes Цикл
				
				Если НРег(attribute.key) = "amount" И ЗначениеЗаполнено(attribute.value) Тогда
					Попытка
						СтрокаДетали.Количество = Число(attribute.value);
					Исключение КонецПопытки;
				КонецЕсли;
				
				СтрокаПримечания = СтрокаДетали.ПримечанияДетали.ПолучитьЭлементы().Добавить();
				
				МассивСвойств = РазобратьСтрокуСвойств(attribute.value);
				СтрокаПримечания.СвойствоДетали = attribute.name;
				Если МассивСвойств.Количество()>1 Тогда
					Для Каждого Свойство2 Из МассивСвойств Цикл
						Строка2 = СтрокаПримечания.ПолучитьЭлементы().Добавить();
						Строка2.ЗначениеСвойства = Свойство2;
					КонецЦикла;
				Иначе
					СтрокаПримечания.ЗначениеСвойства = attribute.value;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ДеталиУзла.Сортировать("Match Убыв, CodeOnImage Возр");
	
	Элементы.ДеталиУзлаDetailId.Видимость = ДеталиУзла.Итог("DetailId")>0;
	
	/////////////////ПОЛУЧИТЬ КООРДИНАТЫ "ГОРЯЧИХ" ОБЛАСТЕЙ НА КАРТИНКЕ АГРЕГАТА АВТОМОБИЛЯ/////////////////
	
	Row = ListImageMapByUnitНаСервере(Данные, Ошибка);
	
	Если Row = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	FileName_jquery_js = "http://wsdemo.laximo.ru/components/com_guayaquil/guayaquillib/render/jquery.js?1348830397";
	FileName_dragscrollable_js = "http://wsdemo.laximo.ru/components/com_guayaquil/guayaquillib/render/dragscrollable.js?1318476915";
	FileName_jquery_mousewheel_js = "http://wsdemo.laximo.ru/components/com_guayaquil/guayaquillib/render/jquery.mousewheel.js?1348830397";

	ТекстHTML = мОбъект.ПолучитьМакет("Node").ПолучитьТекст();
	ТекстHTML = СтрЗаменить(ТекстHTML, "%FileName_jquery_js%", "<SCRIPT type=text/javascript src="""+FileName_jquery_js+"""></SCRIPT>");
	ТекстHTML = СтрЗаменить(ТекстHTML, "%FileName_dragscrollable_js%", "<SCRIPT type=text/javascript src="""+FileName_dragscrollable_js+"""></SCRIPT>");
	ТекстHTML = СтрЗаменить(ТекстHTML, "%FileName_jquery_mousewheel_js%", "<SCRIPT type=text/javascript src="""+FileName_jquery_mousewheel_js+"""></SCRIPT>");
	ТекстHTML = СтрЗаменить(ТекстHTML, "%NodeName%", NodeЗаголовок);
	ТекстHTML = СтрЗаменить(ТекстHTML, "%back‹%", ?(НазадВидимость, "‹‹", ""));
	ТекстHTML = СтрЗаменить(ТекстHTML, "%forward›%", ?(ВпередВидимость, "››", ""));
	ТекстHTML = СтрЗаменить(ТекстHTML, "%back%", ?(НазадВидимость, "back", ""));
	ТекстHTML = СтрЗаменить(ТекстHTML, "%forward%", ?(ВпередВидимость, "forward", ""));
	
	DivТекст = мОбъект.ПолучитьМакет("Div").ПолучитьТекст();
	
	МассивНеПрименяется.Очистить();
	
	Для Каждого Координата Из Row Цикл
		CodeOnImage = Координата.Code;
		DivTitle = "";
		НовыйDiv = DivТекст;
		НовыйDiv = СтрЗаменить(НовыйDiv,"%DivName%", CodeOnImage);
		НовыйDiv = СтрЗаменить(НовыйDiv,"%DivWidth%", Формат(Цел((Координата.X2 - Координата.X1)*1.1),"ЧГ=0"));
		НовыйDiv = СтрЗаменить(НовыйDiv,"%DivHeight%", Формат(Цел((Координата.Y2 - Координата.Y1)*1.1),"ЧГ=0"));
		НовыйDiv = СтрЗаменить(НовыйDiv,"%DivMarginTop%", Формат(Координата.Y1-5,"ЧГ=0"));
		НовыйDiv = СтрЗаменить(НовыйDiv,"%DivMarginLeft%", Формат(Координата.X1-5,"ЧГ=0"));
		НайденныеСтроки = ДеталиУзла.НайтиСтроки(Новый Структура("CodeOnImage", CodeOnImage));
		Если НайденныеСтроки.Количество() = 0 Тогда
			DivTitle = "Не применяется";
			МассивНеПрименяется.Добавить(CodeOnImage);
		Иначе
			Для Каждого Строка Из НайденныеСтроки Цикл
				DivTitle = Строка.Name; 
				Строка.Type = Координата.Type;
			КонецЦикла;
		КонецЕсли;
		НовыйDiv = СтрЗаменить(НовыйDiv, "%DivTitle%" ,DivTitle);
		ТекстHTML = СтрЗаменить(ТекстHTML, "%НовыйDiv%", НовыйDiv);
	КонецЦикла;
	
	ТекстHTML = СтрЗаменить(ТекстHTML, "%НовыйDiv%", "<IMG class=dragger onload=""rescaleImage(-100);"" style=""MARGIN-LEFT: 0px; MARGIN-TOP: 0px"" src=""%ImgLaximo%""></IMG>");
	ТекстHTML = СтрЗаменить(ТекстHTML, "%ImgLaximo%", UrlКартинки);
	
	ПолеHtml = ТекстHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФильтрДляУзла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаРезультата Из Результат Цикл
		Если СтрокаРезультата.Value <> "" Тогда
			НовыйФильтр = ВыбранныйАвтомобильФильтр.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйФильтр, СтрокаРезультата);
		КонецЕсли;
	КонецЦикла;
	
	Если ВыбранныйАвтомобильФильтр.Количество() <> 0 Тогда
		Элементы.ВыбранныйАвтомобильФильтр.Видимость = Истина;
	КонецЕсли;
	
	СохранитьАвтомобильВИсторию();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция GetFilterByUnitНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "GetFilterByUnit", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаКлиенте
Процедура ПометитьУзел(Id)
	Сч = 1;
	Для Каждого Узел Из ДеревоУзлов.ПолучитьЭлементы() Цикл
		Попытка
			Элементы.ПолеHtml.Документ.parentWindow.highlightdetail("nodeNo"+Формат(Сч, "ЧГ=0"), "false");
		Исключение
		КонецПопытки;
		Сч = Сч + 1;
	КонецЦикла;
	
	Попытка
		Элементы.ПолеHtml.Документ.parentWindow.highlightdetail(Id, "true");
	Исключение
	КонецПопытки;
	
	ПометитьУзелНаСервере();
	ПометитьДеталиУзла(Истина);
КонецПроцедуры

&НаСервере
Процедура ПометитьУзелНаСервере()
	
	ДеталиУзла.Очистить();
	
	мДеревоУзлов = ДеревоУзлов.ПолучитьЭлементы();
	СтрокаДанных = мДеревоУзлов[ВыбранныйУзел-1];
	
	ПримечаниеКУзлу = СтрокаДанных.Note;
	
	ВыбранныйАвтомобиль.UnitId = СтрокаДанных.UnitId;
	ВыбранныйАвтомобиль.UnitSsd = СтрокаДанных.Ssd;
	
	Если ЗначениеЗаполнено(СтрокаДанных.Filter) Тогда
		ПримечаниеКУзлу = ПримечаниеКУзлу + Символы.ПС + "(требуется уточнение автомобиля)";
	КонецЕсли;
	
	Элементы.НадписьПримечаниеКУзлу.Видимость = ЗначениеЗаполнено(ПримечаниеКУзлу);
	Элементы.НадписьПримечаниеКУзлу.Заголовок = ПримечаниеКУзлу;
	
	Если НЕ ТипЗнч(СтрокаДанных.Details) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Деталь Из СтрокаДанных.Details Цикл
		Если Не ЗначениеЗаполнено(Деталь) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаДетали = ДеталиУзла.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДетали, Деталь);
		СтрокаДетали.АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Деталь.OEM);
		
		Если Деталь.Свойство("amount") И ЗначениеЗаполнено(Деталь.amount) Тогда
			Попытка
				СтрокаДетали.Количество = Число(Деталь.amount);
			Исключение КонецПопытки;
		КонецЕсли;
		
		Если Деталь.Свойство("attribute") Тогда
			Если ТипЗнч(Деталь.attribute) = Тип("Структура") Тогда
				attributes = Новый Массив;
				attributes.Добавить(Новый Структура(Новый ФиксированнаяСтруктура(Деталь.attribute)));
			Иначе
				attributes = Новый Массив(Новый ФиксированныйМассив(Деталь.attribute));
			КонецЕсли;
			Деталь.Удалить("attribute");
			
			//Заполним свойства детали
			СтрокаДетали.ПримечанияДетали.ПолучитьЭлементы().Очистить();
			Для Каждого attribute Из attributes Цикл
				
				Если НРег(attribute.key) = "amount" И ЗначениеЗаполнено(attribute.value) Тогда
					Попытка
						СтрокаДетали.Количество = Число(attribute.value);
					Исключение КонецПопытки;
				КонецЕсли;
				
				СтрокаПримечания = СтрокаДетали.ПримечанияДетали.ПолучитьЭлементы().Добавить();
				
				МассивСвойств = РазобратьСтрокуСвойств(attribute.value);
				СтрокаПримечания.СвойствоДетали = attribute.name;
				Если МассивСвойств.Количество()>1 Тогда
					Для Каждого Свойство2 Из МассивСвойств Цикл
						Строка2 = СтрокаПримечания.ПолучитьЭлементы().Добавить();
						Строка2.ЗначениеСвойства = Свойство2;
					КонецЦикла;
				Иначе
					СтрокаПримечания.ЗначениеСвойства = attribute.value;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ДеталиУзла.Сортировать("Match Убыв, CodeOnImage Возр");
	
	Элементы.ДеталиУзлаDetailId.Видимость = ДеталиУзла.Итог("DetailId")>0;
	
КонецПроцедуры


&НаКлиенте
Процедура НадписьВлево1Нажатие(Элемент)
	
	ВыбранныйУзел = ВыбранныйУзел - 1;
	
	Элементы.НадписьПримечаниеКУзлу.Видимость = Ложь;
	Элементы.НадписьПримечаниеКУзлу.Заголовок = "";
	
	ВыбратьУзел();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьВправо1Нажатие(Элемент)
	
	ВыбранныйУзел = ВыбранныйУзел + 1;
	
	Элементы.НадписьПримечаниеКУзлу.Видимость = Ложь;
	Элементы.НадписьПримечаниеКУзлу.Заголовок = "";
	
	ВыбратьУзел();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеHtmlДокументСформирован(Элемент)
	Если МассивНеПрименяется.Количество() > 0 Тогда
		Попытка
			ЗаполнитьНеПрименяется();
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	ПометитьДеталиУзла();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНеПрименяется()
	Если Элементы.ПолеHtml.Документ <> Неопределено Тогда	
		Если Элементы.ПолеHtml.Документ.parentWindow <> Неопределено Тогда
			Для Каждого CodeOnImage Из МассивНеПрименяется Цикл
				Элементы.ПолеHtml.Документ.parentWindow.nodetail(CodeOnImage.Значение, "true");
				//Элементы.ПолеHtml.Документ.defaultView.nodetail(CodeOnImage.Значение, "true");
				//Элементы.ПолеHtml.Документ.frames.nodetail(CodeOnImage.Значение, "true");
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолеHtmlПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	pEvtObj = ДанныеСобытия.Event;
	
	Попытка
		Id = pEvtObj.srcElement.id;
	Исключение
		Попытка
			Id = pEvtObj.target.id;
		Исключение
			Попытка
				Если pEvtObj.target.nodeType = 3 Тогда
					Id = pEvtObj.target.parentNode.id;
				КонецЕсли;
			Исключение
				Возврат;
			КонецПопытки;
		КонецПопытки;
	КонецПопытки;
	
	CodeOnImage = "";
	
	Если ЗначениеЗаполнено(Id) Тогда
		Если Id = "back" Тогда
			НадписьВлево1Нажатие(Неопределено);
			Возврат;
			
		ИначеЕсли Id = "forward" Тогда
			НадписьВправо1Нажатие(Неопределено);
			Возврат;
			
		ИначеЕсли Найти(Id, "nodeNo") > 0 Тогда
			// Клик по узлу
			Попытка
				КодУзла = Число(СтрЗаменить(Id, "nodeNo",""));
			Исключение
				Возврат;
			КонецПопытки;
			
			Если ВыбранныйУзел = КодУзла Тогда
				ВыбратьУзел();
			Иначе
				// Первый клик по узлу надо отработать
				ВыбранныйУзел = КодУзла;
				ПометитьУзел(Id);
				
			КонецЕсли;
			Возврат;
			
		ИначеЕсли Найти(Id, "ReturnList") > 0 Тогда
			Элементы.НадписьПримечаниеКУзлу.Видимость = Ложь;
			Элементы.НадписьПримечаниеКУзлу.Заголовок = "";
			
			ЗаполнитьВыборУзловНаСервере();
			Возврат;
			
		ИначеЕсли Найти(Id, "ReturnNode") > 0 Тогда
			// Возврат к выбору узлов
			ВыбранныйУзел = 1;
			ВыбратьУзел();
			Возврат;
			
		ИначеЕсли Найти(Id, "CodeOnImage") > 0 Тогда
			CodeOnImage = СтрЗаменить(Id, "CodeOnImage", "");
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(CodeOnImage) Тогда
		Попытка
			CodeOnImage = pEvtObj.srcElement.parentElement.attributes["name"].value;
		Исключение
			Возврат;
		КонецПопытки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(CodeOnImage) Тогда
		Возврат;
	КонецЕсли;
	
	ТекСтрока = Элементы.ДеталиУзла.ТекущиеДанные;
	НадоАктивизироватьСтроку = Истина;
	
	НайденныеСтроки = ДеталиУзла.НайтиСтроки(Новый Структура("CodeOnImage", CodeOnImage));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			//Если ТекСтрока = НайденнаяСтрока Тогда
			//	НадоАктивизироватьСтроку = Ложь;
			//КонецЕсли;
		КонецЦикла;
		Если НадоАктивизироватьСтроку Тогда
			Элементы.ДеталиУзла.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатурныеГруппыДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элементы.НоменклатурныеГруппыДерево.Развернуть(ВыбраннаяСтрока);
	
	ТекСтрока = НоменклатурныеГруппыДерево.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	ВыбранныйАвтомобиль.CategoryId = -1;
	ВыбранныйАвтомобиль.CategorySsd = "";
	
	ВыбранныйАвтомобиль.QuickGroupId = ТекСтрока.QuickGroupId;
	
	Если Не ТекСтрока.Link Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПометкуВыбранаРекурсивно(НоменклатурныеГруппыДерево, Ложь);
	УстановитьПометкуВыбранаРекурсивно(КатегорииДерево, Ложь);
	
	ТекСтрока.Выбрана = Истина;
	
	ВыборНоменклатурныхГруппНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ListQuickDetailНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("laximooem", "ListQuickDetail", ТелоЗапроса,,, Ошибка);
КонецФункции

&НаСервере
Процедура ВыборНоменклатурныхГруппНаСервере()
	
	ОчиститьВыборУзловНаСервере();
	
	Если ВыбранныйАвтомобиль.QuickGroupId <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = "";
	Данные = Новый Структура("Catalog, VehicleId, QuickGroupId, All, ssd", ВыбранныйАвтомобиль.Catalog, Формат(ВыбранныйАвтомобиль.VehicleId, "ЧГ=0"), Формат(ВыбранныйАвтомобиль.QuickGroupId, "ЧГ=0"), "0", ВыбранныйАвтомобиль.ssd);
	СтруктураРезультата = ListQuickDetailНаСервере(Данные, Ошибка);
	
	Если СтруктураРезультата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураРезультата.Свойство("Category") Тогда
		Если ТипЗнч(СтруктураРезультата.category) = Тип("Массив") Тогда
			СтрокиДанных = СтруктураРезультата.category;
		Иначе
			СтрокиДанных = Новый Массив;
			СтрокиДанных.Добавить(СтруктураРезультата.category);
		КонецЕсли;
	Иначе
		//значит у нас СтруктураРезультата.row => нет данных
		СтрокиДанных = Новый Массив;
	КонецЕсли;
	
	ВсегоУзлов = 0;
	
	мДеревоУзлов = РеквизитФормыВЗначение("ДеревоУзлов");
	мДеревоУзлов.Строки.Очистить();
	
	Родитель = мДеревоУзлов;
	
	Для Каждого Категория Из СтрокиДанных Цикл
		
		Если Не Категория.Свойство("Unit") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(Категория.Unit) = Тип("Массив") Тогда
			СтрокиУзлов = Категория.Unit;
		Иначе
			СтрокиУзлов = Новый Массив;
			СтрокиУзлов.Добавить(Категория.Unit);
		КонецЕсли;
		
		Для Каждого Узел Из СтрокиУзлов Цикл
			ВсегоУзлов = ВсегоУзлов + 1;
			СтрокаУзел = Родитель.Строки.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаУзел, Категория);
			ЗаполнитьЗначенияСвойств(СтрокаУзел, Узел);
			
			СтрокаУзел.QuickGroupId = ВыбранныйАвтомобиль.QuickGroupId;
			
			Attributes = Новый Массив;
			Если Узел.Свойство("attribute") Тогда
				Если ТипЗнч(Узел.attribute) = Тип("Структура") Тогда
					attribute = Новый Структура(Новый ФиксированнаяСтруктура(Узел.attribute));
					Attributes.Добавить(attribute);
				Иначе
					// Массив
					attribute = Новый Массив(Новый ФиксированныйМассив(Узел.attribute));
					Attributes = attribute;
				КонецЕсли;
				Узел.Удалить("attribute");
			КонецЕсли;
			СтрокаУзел.Attributes = Attributes;
			
			Details = Новый Массив;
			Если Узел.Свойство("Detail") Тогда
				Если ТипЗнч(Узел.Detail) = Тип("Структура") Тогда
					Detail = Новый Структура(Новый ФиксированнаяСтруктура(Узел.Detail));
					Details.Добавить(Detail);
				Иначе
					// Массив
					Detail = Новый Массив(Новый ФиксированныйМассив(Узел.Detail));
					Details = Detail;
				КонецЕсли;
				Узел.Удалить("Detail");
			КонецЕсли;
			
			Для Каждого Detail Из Details Цикл
				
				Если Не ЗначениеЗаполнено(Detail) Тогда
					Продолжить;
				КонецЕсли;
				
				attributes = Новый Массив;
				Если Detail.Свойство("attribute") Тогда
					Если ТипЗнч(Detail.attribute) = Тип("Структура") Тогда
						attribute = Новый Структура(Новый ФиксированнаяСтруктура(Detail.attribute));
						attributes.Добавить(attribute);
					Иначе
						// массив
						attribute = Новый Массив(Новый ФиксированныйМассив(Detail.attribute));
						attributes = attribute;
					КонецЕсли;
					Detail.Удалить("attribute");
				КонецЕсли;
				Detail.Вставить("attributes", attributes);
				
				Если НЕ Detail.Свойство("match") Тогда
					Detail.Вставить("match", "");
				КонецЕсли;
				
			КонецЦикла;
			
			СтрокаУзел.Details = Details;
			
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(мДеревоУзлов, "ДеревоУзлов");
	
	ВыбранныйУзел = 0;
	
	ЗаполнитьВыборУзловНаСервере();
	
КонецПроцедуры


&НаКлиенте
Процедура ДеталиУзлаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекСтрока = Элемент.ТекущиеДанные;
	
	ИмяНовогоНабора = "";
	
	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Filter) Тогда
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить(0, "Уточнить параметры автомобиля");
	Иначе
		СписокВыбора = СформироватьСписокВыбораНаборов(ТекСтрока.АртикулДляПоиска);
	КонецЕсли;
	
	Если СписокВыбора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйПункт = ВыбратьИзМеню(СписокВыбора, ТекущийЭлемент);
	Если ВыбранныйПункт = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйПункт.Значение = 0 Тогда 
		
		ФильтрыДляДетали = GetFilterByDetailНаКлиенте(ВыбраннаяСтрока);
		
		Если ФильтрыДляДетали.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Данные",ФильтрыДляДетали);
		
		#Если ТонкийКлиент Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьФильтрДляДетали", ЭтаФорма);
			ОткрытьФорму("Обработка.атПомощникПродаж.Форма.СервисLaximoOEM_ВыборУсловия", СтруктураПараметров, ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		#Иначе
			Результат = ОткрытьФормуМодально("Обработка.атПомощникПродаж.Форма.СервисLaximoOEM_ВыборУсловия", СтруктураПараметров);
			Если Результат = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			Для Каждого СтрокаРезультата Из Результат Цикл
				Если СтрокаРезультата.Value <> "" Тогда
					НовыйФильтр = ВыбранныйАвтомобильФильтр.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйФильтр, СтрокаРезультата);
				КонецЕсли;
			КонецЦикла;
			
			Если ВыбранныйАвтомобильФильтр.Количество() <> 0 Тогда
				Элементы.ВыбранныйАвтомобильФильтр.Видимость = Истина;
			КонецЕсли;
			
			СохранитьАвтомобильВИсторию();
			
			// Перевыберем узел
			ВыбратьУзел();
			
		#КонецЕсли
		
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйПункт.Пометка Тогда
		
		УдалитьНомерДетали(ВыбранныйПункт.Значение, ТекСтрока.АртикулДляПоиска);
		
	Иначе
		
		Если Не ЗначениеЗаполнено(ВыбранныйАвтомобиль.ИдентификаторЗапроса) Тогда
			ДобавитьЗапрос();
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыбранныйАвтомобиль.ИдентификаторЗапроса) Тогда
			Возврат;
		КонецЕсли;
		
		ИмяНабора = ВыбранныйПункт.Значение;
		Наименование = ТекСтрока.name;
		
		#Если ТонкийКлиент Тогда
		#Иначе
			Результат = ОткрытьФормуМодально("Обработка.атПомощникПродаж.Форма.СервисLaximoOEM_ВводНаименования", Новый Структура("ИмяНабора, Наименование", ИмяНабора, Наименование));
			Если Результат = Неопределено Тогда
				Возврат;
			КонецЕсли;
			ИмяНабора = Результат.ИмяНабора;
			Наименование = Результат.Наименование;
		#КонецЕсли
		
		//Если ВыбранныйПункт.Представление = "в < Новый набор >" Тогда
		//	#Если ТонкийКлиент Тогда
		//	#Иначе
		//		Если Не ВвестиСтроку(ИмяНабора, "Имя нового набора", 100, Ложь) Тогда
		//			Возврат;
		//		КонецЕсли;
		//	#КонецЕсли
		//КонецЕсли;
		
		// В параметры номера детали нужно добавить текущий Unit
		ПараметрыНомераДетали = Новый Структура;
		ПараметрыНомераДетали.Вставить("ВыбранныйУзел",ВыбранныйУзел);
		ПараметрыНомераДетали.Вставить("QuickGroupId", ВыбранныйАвтомобиль.QuickGroupId);
		ПараметрыНомераДетали.Вставить("CategoryId",   ВыбранныйАвтомобиль.CategoryId);
		ПараметрыНомераДетали.Вставить("CategorySsd",  ВыбранныйАвтомобиль.CategorySsd);
		ПараметрыНомераДетали.Вставить("UnitId",       ВыбранныйАвтомобиль.UnitId);
		ПараметрыНомераДетали.Вставить("UnitSsd",      ВыбранныйАвтомобиль.UnitSsd);
		
		ДобавитьНомерДетали(ИмяНабора, ТекСтрока.OEM, Наименование, ТекСтрока.Количество, ПараметрыНомераДетали);
		
	КонецЕсли;
	
	ПометитьДетальУзла(ТекСтрока.АртикулДляПоиска);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьДетальУзла(Знач АртикулДляПоиска, Знач ТолькоВСписке = Ложь)
	
	ЕстьВНаборе = НомераДеталей.НайтиСтроки(Новый Структура("АртикулДляПоиска", АртикулДляПоиска)).Количество() > 0;
	
	СтрокиДеталей = ДеталиУзла.НайтиСтроки(Новый Структура("АртикулДляПоиска", АртикулДляПоиска));
	Для Каждого СтрокаДетали Из СтрокиДеталей Цикл
		СтрокаДетали.Пометка = ЕстьВНаборе;
	КонецЦикла;
	
	Если ТолькоВСписке Тогда
		Возврат;
	КонецЕсли;
	
	МассивCodeOnImage = Новый Массив;
	Для Каждого СтрокаДетали Из СтрокиДеталей Цикл
		Если ЗначениеЗаполнено(СтрокаДетали.CodeOnImage) И МассивCodeOnImage.Найти(СтрокаДетали.CodeOnImage) = Неопределено Тогда
			МассивCodeOnImage.Добавить(СтрокаДетали.CodeOnImage);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого CodeOnImage Из МассивCodeOnImage Цикл
		Попытка
		Элементы.ПолеHtml.Документ.parentWindow.lockdetail(CodeOnImage, ?(ЕстьВНаборе,"true","false"));
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьДеталиУзла(Знач ТолькоВСписке=Ложь)
	Для Каждого СтрокаДеталь Из ДеталиУзла Цикл
		ПометитьДетальУзла(СтрокаДеталь.АртикулДляПоиска, ТолькоВСписке);
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Функция СформироватьСписокВыбораНаборов(Знач АртикулДляПоиска)
	
	Если Не ЗначениеЗаполнено(ВыбранныйАвтомобиль.Автомобиль) Тогда
		Возврат Новый СписокЗначений;
	КонецЕсли;
	
	СписокВыбора = СписокВыбораНаборов.Скопировать();
	
	Для Каждого ЭлементСписка Из СписокВыбора Цикл
		Если ЭлементСписка.Значение = "" Тогда
			ЭлементСписка.Представление = "< Не указано >";
		КонецЕсли;
		ЭлементСписка.Пометка = НомераДеталей.НайтиСтроки(Новый Структура("ИмяНабора, АртикулДляПоиска, Автор", ЭлементСписка.Значение, АртикулДляПоиска, ПараметрыСеанса.Пользователь)).Количество() > 0;
	КонецЦикла;
	
	ПомеченнаяСтрока = НайтиПомеченнуюСтрокуРекурсивно(НоменклатурныеГруппыДерево);
	Если ПомеченнаяСтрока = Неопределено Тогда
		ПомеченнаяСтрока = НайтиПомеченнуюСтрокуРекурсивно(КатегорииДерево);
	КонецЕсли;
	
	Если ПомеченнаяСтрока = Неопределено Тогда
		ИмяНовогоНабора = Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd");
	Иначе
		ИмяНовогоНабора = ПомеченнаяСтрока.name;
	КонецЕсли;
	
	СписокВыбора.Добавить(ИмяНовогоНабора, "в < Новый набор >", Ложь);
	
	Возврат СписокВыбора;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьФильтрДляДетали(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаРезультата Из Результат Цикл
		Если СтрокаРезультата.Value <> "" Тогда
			НовыйФильтр = ВыбранныйАвтомобильФильтр.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйФильтр, СтрокаРезультата);
		КонецЕсли;
	КонецЦикла;
	
	Если ВыбранныйАвтомобильФильтр.Количество() <> 0 Тогда
		Элементы.ВыбранныйАвтомобильФильтр.Видимость = Истина;
	КонецЕсли;
	
	СохранитьАвтомобильВИсторию();
	
	// Здесь надо перевыбрать узел!!
	ВыбратьУзел();
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция GetFilterByDetailНаСервере(Знач Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	
	СтруктураРезультата = атСервер.ВыполнитьЗапрос("laximooem", "GetFilterByDetail", ТелоЗапроса,,, Ошибка);
	
	Если СтруктураРезультата = Неопределено ИЛИ НЕ СтруктураРезультата.Свойство("row") ИЛИ СтруктураРезультата.row = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураРезультата.row) = Тип("Массив") Тогда
		СтрокиДанных = СтруктураРезультата.row;
	Иначе
		СтрокиДанных = Новый Массив;
		СтрокиДанных.Добавить(СтруктураРезультата.row);
	КонецЕсли;
	
	Возврат СтрокиДанных;
КонецФункции

&НаКлиенте
Функция GetFilterByDetailНаКлиенте(Знач ВыбраннаяСтрока)
	СтрокаДетали = ДеталиУзла.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	DetailSsd = СтрокаДетали.Ssd;
	Для каждого Filter Из ВыбранныйАвтомобильФильтр Цикл
		DetailSsd = DetailSsd + Filter.SSDModification;
	КонецЦикла; 
	
	Данные = Новый Структура("Catalog, UnitId, DetailId, Filter, ssd", ВыбранныйАвтомобиль.Catalog, Формат(ВыбранныйАвтомобиль.UnitId, "ЧГ=0"), Формат(СтрокаДетали.DetailId, "ЧГ=0"), СтрокаДетали.Filter, DetailSsd);
	
	Ошибка = "";
	СтруктураРезультата = GetFilterByDetailНаСервере(Данные, Ошибка);
	
	Если СтруктураРезультата = Неопределено Тогда
		Предупреждение(Ошибка);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СтруктураРезультата;
КонецФункции

&НаКлиенте
Процедура ДеталиУзлаПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено ИЛИ Не ТекСтрока.ВременнаяПометка Тогда
		НайденныеСтроки = ДеталиУзла.НайтиСтроки(Новый Структура("ВременнаяПометка", Истина));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Попытка
				Элементы.ПолеHtml.Документ.parentWindow.highlightdetail(НайденныеСтроки[0].CodeOnImage, "false");
			Исключение КонецПопытки;
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НайденнаяСтрока.ВременнаяПометка = Ложь;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если Не ТекСтрока.ВременнаяПометка Тогда
		НайденныеСтроки = ДеталиУзла.НайтиСтроки(Новый Структура("CodeOnImage",ТекСтрока.CodeOnImage));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Попытка
				Элементы.ПолеHtml.Документ.parentWindow.highlightdetail(НайденныеСтроки[0].CodeOnImage, "true");
			Исключение КонецПопытки;
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НайденнаяСтрока.ВременнаяПометка = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьЗапросНаСервере(Данные, Ошибка)
	ТелоЗапроса = атСервер.ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
	Возврат атСервер.ВыполнитьЗапрос("worksheets", "ДобавитьЗапрос", ТелоЗапроса,,, Ошибка);
КонецФункции


&НаКлиенте
Процедура ДобавитьЗапрос()
	
	СохранитьАвтомобильВИсторию();
	
КонецПроцедуры


&НаКлиенте
Процедура ДобавитьНомерДетали(Знач ИмяНабора, Знач Артикул, Знач Наименование, Знач Количество, Знач ПараметрыНомераДетали)
	
	// Добавляем деталь
	Количество = ?(ЗначениеЗаполнено(Количество), Количество, 1);
	АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул);
	
	СтрокаДетали = НомераДеталей.Добавить();
	СтрокаДетали.ИдентификаторЗапроса = ВыбранныйАвтомобиль.ИдентификаторЗапроса;
	СтрокаДетали.ИмяНабора            = ИмяНабора;
	СтрокаДетали.Артикул              = Артикул;
	СтрокаДетали.АртикулДляПоиска     = АртикулДляПоиска;
	СтрокаДетали.Наименование         = Наименование;
	СтрокаДетали.Количество           = Количество;
	СтрокаДетали.Автор                = ПараметрыСеанса.Пользователь;
	
	Если СписокВыбораНаборов.НайтиПоЗначению(ИмяНабора) = Неопределено Тогда
		СписокВыбораНаборов.Добавить(ИмяНабора);
		СписокВыбораНаборов.СортироватьПоЗначению(НаправлениеСортировки.Возр);
	КонецЕсли;
	
	СтруктураОповещения = Новый Структура;
	
	СтруктураОповещения.Вставить("Источник",             ВыбранныйАвтомобиль.Источник);
	СтруктураОповещения.Вставить("Объект",               ВыбранныйАвтомобиль.Автомобиль);
	СтруктураОповещения.Вставить("ИдентификаторЗапроса", СтрокаДетали.ИдентификаторЗапроса);
	СтруктураОповещения.Вставить("ИмяНабора",            СтрокаДетали.ИмяНабора);
	СтруктураОповещения.Вставить("Артикул",              СтрокаДетали.Артикул);
	СтруктураОповещения.Вставить("АртикулДляПоиска",     СтрокаДетали.АртикулДляПоиска);
	СтруктураОповещения.Вставить("Производитель",        ВыбранныйАвтомобиль.Производитель);
	СтруктураОповещения.Вставить("Наименование",         СтрокаДетали.Наименование);
	СтруктураОповещения.Вставить("Количество",           СтрокаДетали.Количество);
	СтруктураОповещения.Вставить("Параметры",            ПараметрыНомераДетали);
	
	Оповестить("атПомощникПродаж_НомерДетали_Добавлен", СтруктураОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНомерДетали(Знач ИмяНабора, Знач АртикулДляПоиска)
	// Удаляем деталь из набора
	УдаляемыеСтроки = НомераДеталей.НайтиСтроки(Новый Структура("ИмяНабора, АртикулДляПоиска, Автор", ИмяНабора, АртикулДляПоиска, ПараметрыСеанса.Пользователь));
	Для Каждого НомерДетали Из УдаляемыеСтроки Цикл
		НомераДеталей.Удалить(НомераДеталей.Индекс(НомерДетали));
		
		СтруктураОповещения = Новый Структура;
		СтруктураОповещения.Вставить("Источник",             ВыбранныйАвтомобиль.Источник);
		СтруктураОповещения.Вставить("Объект",               ВыбранныйАвтомобиль.Автомобиль);
		СтруктураОповещения.Вставить("ИдентификаторЗапроса", НомерДетали.ИдентификаторЗапроса);
		СтруктураОповещения.Вставить("ИмяНабора",            ИмяНабора);
		СтруктураОповещения.Вставить("АртикулДляПоиска",     АртикулДляПоиска);
		СтруктураОповещения.Вставить("Производитель",        ВыбранныйАвтомобиль.Производитель);
		Оповестить("атПомощникПродаж_НомерДетали_Удален",    СтруктураОповещения);
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ВыбранныйАвтомобильФильтрПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныйАвтомобильФильтрПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры



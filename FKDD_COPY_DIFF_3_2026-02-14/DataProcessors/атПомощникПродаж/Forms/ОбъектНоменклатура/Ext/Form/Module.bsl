
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ПараметрыСеанса.атКодСостояния%10 <> 3 Тогда // Не авторизован
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	//Если Параметры.Свойство("ЭтоСсылка") Тогда
		Ошибка = "";
		ЗаполнитьДанныеОбъекта(Параметры, Ошибка, Отказ);
		Если Отказ И ЗначениеЗаполнено(Ошибка) Тогда
			Сообщить(Ошибка, СтатусСообщения.Внимание);
		КонецЕсли;
	//КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура НайтиНоменклатуру(Команда)
	
	Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") И ЗначениеЗаполнено(Производитель) Тогда
		
		Если ЗначениеЗаполнено(АртикулДляПоиска) Тогда
			НоменклатураСсылка = НайтиНоменклатуруНаСервере(АртикулДляПоиска, Производитель);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НоменклатураСсылка) Тогда
			Если ЗначениеЗаполнено(Модель) ИЛИ Свойства.Количество() > 0 Тогда
				МассивСвойств = Новый Массив;
				Для Каждого СвойствоНоменклатуры Из Свойства Цикл
					МассивСвойств.Добавить(Новый Структура("ВидСвойства, Значение, Ключевое", СвойствоНоменклатуры.ВидСвойства, СвойствоНоменклатуры.Значение, СвойствоНоменклатуры.Ключевое));
				КонецЦикла;
				ТаблицаНоменклатуры = НайтиНоменклатуруПоСвойствамНаСервере(МассивСвойств, Производитель, Модель);
				Если ТаблицаНоменклатуры.Количество() > 0 Тогда
					ВыбраннаяСтрока = ТаблицаНоменклатуры.ВыбратьСтроку("Найденная номенклатура");
					Если ВыбраннаяСтрока <> Неопределено Тогда
						НоменклатураСсылка = ВыбраннаяСтрока.Ссылка;
					КонецЕсли;
				КонецЕсли;
			Иначе
				ВыбратьИзСпискаНоменклатуры = Истина;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		ВыбратьИзСпискаНоменклатуры = Истина;
	КонецЕсли;
	
	Если ВыбратьИзСпискаНоменклатуры = Истина Тогда
		ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
		НоменклатураСсылка = ОткрытьФормуМодально("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		УстановитьНоменклатуру(НоменклатураСсылка);
		ПрочитатьДанныеИБ();
		УправлениеВидимостьюКнопок();
		УправлениеВидимостьюСтраниц();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуру(Команда)
	Если ОбновитьШапкуНаКлиенте() Тогда
		Элементы.СоздатьНоменклатуру.КнопкаПоУмолчанию = Ложь;
		Элементы.СоздатьНоменклатуру.Видимость = Ложь;
		УправлениеВидимостьюСтраниц();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьШапку(Команда)
	
	ОбновитьШапкуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАналоги(Команда)
	
	АналогиНоменклатура.Очистить();
	ОбновитьАналогиСервер();
	ПрочитатьДанныеИБ_Аналоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСвойства(Команда)
	
	СвойстваНоменклатура.Очистить();
	ОбновитьСвойстваСервер();
	ПрочитатьДанныеИБ_Свойства();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьШтрихКоды(Команда)
	
	ШтрихкодыНоменклатура.Очистить();
	ОбновитьШтрихКодыСервер();
	ПрочитатьДанныеИБ_ШтрихКоды();
	
КонецПроцедуры



&НаСервереБезКонтекста
Функция ПолучитьНастройкуНаСервере(Знач Объект, Знач ВидНастройки, Знач ИспользоватьКеш=Ложь)
	Возврат атСервер.ПолучитьНастройку(Объект, ВидНастройки, ИспользоватьКеш);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОбъектНаСервере(Знач СтруктураСловаря, Знач Код=Неопределено, ВыбранноеЗначение=Неопределено, Ошибка="")
	Возврат атСервер.ПолучитьОбъект(СтруктураСловаря, Код, ВыбранноеЗначение, Ошибка);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСопоставленнуюСсылкуНаСервере(Знач Объект, Знач ВозвращатьНаименование=Неопределено, Знач ЗаписыватьОбъект=Ложь)
	Возврат атСервер.ПолучитьСопоставленнуюСсылку(Объект, ВозвращатьНаименование, ЗаписыватьОбъект);
КонецФункции

&НаСервереБезКонтекста
Функция НайтиНоменклатуруНаСервере(Знач АртикулДляПоиска, Знач Производитель)
	Возврат атСервер.НайтиНоменклатуру(АртикулДляПоиска, Производитель);
КонецФункции

&НаСервереБезКонтекста
Функция НайтиНоменклатуруПоСвойствамНаСервере(Знач МассивСвойств, Знач Производитель, Знач МодельНоменклатуры)
	Возврат атСервер.НайтиНоменклатуруПоСвойствам(МассивСвойств, Производитель, МодельНоменклатуры);
КонецФункции

&НаСервереБезКонтекста
Функция ИдентификаторГруппыАналогов(Производитель, АртикулДляПоиска) Экспорт
	Запрос=Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ГруппыАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы
		|ИЗ
		|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
		|ГДЕ
		|	ГруппыАналогов.АртикулДляПоиска = &Артикул
		|	И ГруппыАналогов.Производитель = &Производитель";
	Запрос.УстановитьПараметр("Производитель", Производитель);
	Запрос.УстановитьПараметр("Артикул", АртикулДляПоиска);
	
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИдентификаторГруппы;
	КонецЕсли;
	Возврат Строка(Новый УникальныйИдентификатор);
КонецФункции

&НаСервереБезКонтекста
Функция ПреобразоватьВАртикулДляПоискаНаСервере(Знач Артикул, Знач Производитель)
	Возврат Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, Производитель);
КонецФункции


&НаКлиенте
Функция ОбновитьШапкуНаКлиенте()
	
	МассивНесопоставленных = НайтиНесопоставленныеОбъекты();
	
	Если МассивНесопоставленных.Количество() > 0 Тогда
		
		//ТекстСообщения = "Есть не сопоставленные объекты";
		ТекстСообщения = "Следующие объекты не сопоставлены:";
		Если МассивНесопоставленных.Количество() > 5 Тогда
			ВГраница = 4;
		Иначе
			ВГраница = МассивНесопоставленных.ВГраница();
		КонецЕсли;
		
		Для НомерЭлемента = 0 По ВГраница Цикл
			ТекстСообщения = ТекстСообщения + Символы.ПС + Символы.Таб + МассивНесопоставленных[НомерЭлемента].Тип + ": (" + МассивНесопоставленных[НомерЭлемента].Код + ") " + МассивНесопоставленных[НомерЭлемента].Наименование;
		КонецЦикла;
		
		Если ВГраница < МассивНесопоставленных.ВГраница() Тогда
			ТекстСообщения= ТекстСообщения + Символы.ПС + Символы.Таб + "... и еще " + (МассивНесопоставленных.ВГраница()-ВГраница) + " объектов";
		КонецЕсли;
		
		ТекстСообщения = ТекстСообщения + "
			|
			|	<Ок> - Сопоставить вручную.";
		Ответ = Вопрос(ТекстСообщения, РежимДиалогаВопрос.ОКОтмена,,, "Создать сопоставления");
		
		Если Ответ = КодВозвратаДиалога.ОК Тогда
			
			ПараметрыФормы = Новый Структура("МассивНесопоставленных", МассивНесопоставленных);
			
			ОткрытьФормуМодально("Обработка.атПомощникПродаж.Форма.БыстроеСопоставление", ПараметрыФормы, ЭтаФорма);
			
			Если НайтиНесопоставленныеОбъекты().Количество() > 0 Тогда
				Возврат Ложь;
			КонецЕсли;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Номенклатура.Наименование = Наименование;
	Номенклатура.Артикул = Артикул;
	Номенклатура.АртикулДляПоиска = АртикулДляПоиска;
	Номенклатура.Производитель = Производитель;
	Номенклатура.ТипНоменклатуры = ТипНоменклатуры;
	
	НоменклатураВес = Вес;
	НоменклатураОбъем = Объем;
	
	Если ЗначениеЗаполнено(Номенклатура.ТипНоменклатуры) Тогда
		
		Если Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
			Элементы.НоменклатураВес.Заголовок = "Вес, кг";
		ИначеЕсли ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2 Тогда
			Элементы.НоменклатураВес.Заголовок = "Ед.изм: Вес, кг";
		КонецЕсли;
		
		Номенклатура.ВидНоменклатуры = ТипНоменклатуры.ВидНоменклатуры;
		Номенклатура.БазоваяЕдиницаИзмерения = ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения;
		
	КонецЕсли;
	
	ЭтаФорма.Модифицированность = Истина;
	
	Элементы.ОбновитьВсе.Доступность = Ложь;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УправлениеВидимостьюКнопок()
	Элементы.ОбновитьВсе.Доступность = Номенклатура.Наименование <> Наименование
										ИЛИ Номенклатура.АртикулДляПоиска <> АртикулДляПоиска
										ИЛИ Номенклатура.Производитель <> Производитель
										ИЛИ Номенклатура.ТипНоменклатуры <> ТипНоменклатуры
										ИЛИ НоменклатураВес <> Вес
										ИЛИ НоменклатураОбъем <> Объем;
	Элементы.ОбновитьВсе.Видимость = НЕ Элементы.СоздатьНоменклатуру.Видимость;
КонецПроцедуры

&НаСервере
Процедура УстановитьНоменклатуру(НоменклатураСсылка)
	ЗначениеВРеквизитФормы(НоменклатураСсылка.ПолучитьОбъект(), "Номенклатура");
	
	Элементы.СоздатьНоменклатуру.КнопкаПоУмолчанию = Ложь;
	Элементы.СоздатьНоменклатуру.Видимость = Ложь;
	Элементы.ГруппаИБ.Заголовок = "Номенклатура: " + Номенклатура.Наименование;
	
КонецПроцедуры

&НаСервере
Процедура НоваяНоменклатура()
	ЗначениеВРеквизитФормы(Справочники.Номенклатура.СоздатьЭлемент(), "Номенклатура");
	
	Элементы.СоздатьНоменклатуру.Видимость = Истина;
	Элементы.СоздатьНоменклатуру.КнопкаПоУмолчанию = Истина;
	Элементы.ГруппаИБ.Заголовок = "Новая номенклатура";
	
	Номенклатура.Родитель = ПолучитьНастройкунаСервере("Номенклатура", "НоменклатурнаяГруппа");
	
	Номенклатура.СтавкаНДС = Номенклатура.Родитель.СтавкаНДС;
	Если Не ЗначениеЗаполнено(Номенклатура.СтавкаНДС) Тогда
		Номенклатура.СтавкаНДС = ПредопределенноеЗначение("Справочник.СтавкиНДС.ОсновнаяСтавкаНДС");
	КонецЕсли;
	
	Номенклатура.ВалютаУчета = Номенклатура.Родитель.ВалютаУчета;
	Если Не ЗначениеЗаполнено(Номенклатура.ВалютаУчета) Тогда
		Номенклатура.ВалютаУчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОбъекта(ПараметрыОткрытия, Ошибка="", Отказ = Ложь)
	
	ДанныеОбъекта = Неопределено;
	Если ПараметрыОткрытия.Свойство("Номенклатура") И ТипЗнч(ПараметрыОткрытия.Номенклатура) = Тип("СправочникСсылка.Номенклатура") И ЗначениеЗаполнено(ПараметрыОткрытия.Номенклатура) Тогда
		НоменклатураСсылка = ПараметрыОткрытия.Номенклатура;
		ДанныеОбъекта = атСервер.ПолучитьСловарьПоСсылке(ПараметрыОткрытия.Номенклатура, Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта = Неопределено Тогда
		// Сначала проверим есть ли сопоставленная ссылка
		СсылкаСловаря = Новый Структура("ЭтоСсылка, Тип, Код, Артикул, Производитель", Истина, "Номенклатура", ПараметрыОткрытия.Код, ПараметрыОткрытия.Артикул, ПараметрыОткрытия.Производитель);
		НоменклатураСсылка = ПолучитьСопоставленнуюСсылкуНаСервере(СсылкаСловаря,, Ложь);
		
		Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
			// Ранее был сопоставлен, оповещать никого не будем
			ДанныеОбъекта = ПолучитьОбъектНаСервере(СсылкаСловаря,,, Ошибка);
		Иначе
			// При получении данных могли создать новую номенклатуру или найти какую-нибудь
			ВыбранноеЗначение = Неопределено;
			ДанныеОбъекта = ПолучитьОбъектНаСервере(СсылкаСловаря,, ВыбранноеЗначение, Ошибка);
			Если ВыбранноеЗначение <> Неопределено Тогда
				НоменклатураСсылка = ВыбранноеЗначение;
			КонецЕсли;
		КонецЕсли;
	КОнецЕсли;
	
	Если ДанныеОбъекта = Неопределено ИЛИ ЗначениеЗаполнено(Ошибка) Тогда
		// Это ошибка
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПрочитатьДанныеОбъекта();
	
	Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		// Форма существующего объекта
		УстановитьНоменклатуру(НоменклатураСсылка);
	Иначе
		// Форма нового объекта!!
		НоваяНоменклатура();
	КонецЕсли;
	
	ПрочитатьДанныеИБ();
	
	УправлениеВидимостьюКнопок();
	УправлениеВидимостьюСтраниц();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеОбъекта()
	
	Наименование = ДанныеОбъекта.Наименование;
	Код          = ДанныеОбъекта.Код;
	ДанныеОбъекта.Свойство("Артикул", Артикул);
	ДанныеОбъекта.Свойство("АртикулДляПоиска", АртикулДляПоиска);
	ДанныеОбъекта.Свойство("Модель", Модель);
	ДанныеОбъекта.Свойство("Описание", Описание);
	
	// Типа номенклатуры может не быть!
	Если ДанныеОбъекта.Свойство("ТипНоменклатуры") И ЗначениеЗаполнено(ДанныеОбъекта.ТипНоменклатуры) Тогда
		ТипНоменклатуры = ПолучитьСопоставленнуюСсылкуНаСервере(ДанныеОбъекта.ТипНоменклатуры, Истина, Истина);
		Если ТипЗнч(ТипНоменклатуры) = Тип("СправочникСсылка.ТипыНоменклатуры") И ТипНоменклатуры.Пустая() Тогда
			ТипНоменклатуры = ДанныеОбъекта.ТипНоменклатуры.Наименование;
			Сообщить("Выбранный тип номенклатуры запрещено использовать в программе!");
			Элементы.ТипНоменклатуры.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
		Иначе
			Элементы.ТипНоменклатуры.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
		КонецЕсли;
	Иначе
		ТипНоменклатуры = ПредопределенноеЗначение("Справочник.ТипыНоменклатуры.Штучный");
	КонецЕсли;
	
	// Производитель есть всегда!
	Производитель = ПолучитьСопоставленнуюСсылкуНаСервере(ДанныеОбъекта.Производитель, Истина, Истина);
	Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") И Производитель.Пустая() Тогда
		Производитель = ДанныеОбъекта.Производитель.Наименование;
		Сообщить("Выбранного производителя запрещено использовать в программе!");
		Элементы.Производитель.ЦветТекста = ЦветаСтиля.ТекстПредупреждающейНадписи;
	Иначе
		Элементы.Производитель.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
	КонецЕсли;
	
	// Основные данные
	Аналоги.Очистить();
	Свойства.Очистить();
	Штрихкоды.Очистить();
	Изображения.Очистить();
	
	// Дополнительные
	Аналоги_Все.Очистить();
	СвойстваАртикула.Очистить();
	Применяемость.Очистить();
	ОригинальныеНомера.Очистить();
	
	Элементы.ОтборПрименяемости.СписокВыбора.Очистить();
	Элементы.ОтборОригинальныхНомеров.СписокВыбора.Очистить();
	
	Если ДанныеОбъекта.Свойство("Аналоги") Тогда
		
		Для Каждого СтрокаАналога ИЗ ДанныеОбъекта.Аналоги Цикл
			
			ПроизводительАналога = ПолучитьСопоставленнуюСсылкуНаСервере(СтрокаАналога.Производитель, Истина, Истина);
			Если ТипЗнч(ПроизводительАналога) = Тип("СправочникСсылка.Производители") И ПроизводительАналога.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Аналоги_Все.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаАналога,, "Производитель");
			НоваяСтрока.Производитель = ПроизводительАналога;
			НоваяСтрока.ПроизводительКод = СтрокаАналога.Производитель.Код;
			
			Если СтрокаАналога.Раздел = 4 Тогда
				НоваяСтрока_ = Аналоги.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока_, НоваяСтрока);
			КонецЕсли;
			
		КонецЦикла;
		
		Аналоги.Сортировать("АртикулДляПоиска");
		Аналоги_Все.Сортировать("Раздел, АртикулДляПоиска");
		
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("СвойстваНоменклатуры") Тогда
		
		Для Каждого Структура ИЗ ДанныеОбъекта.СвойстваНоменклатуры Цикл
			
			ВидСвойства = ПолучитьСопоставленнуюСсылкуНаСервере(Структура.ВидСвойства,, Истина);
			Если ТипЗнч(ВидСвойства) = Тип("ПланВидовХарактеристикСсылка.СвойстваОбъектов") И ВидСвойства.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(Структура.Значение) = Тип("Структура") Тогда
				Если ЗначениеЗаполнено(ВидСвойства) Тогда
					Значение = ПолучитьСопоставленнуюСсылкуНаСервере(Структура.Значение,, Истина);
					Если ТипЗнч(Значение) = Тип("СправочникСсылка.ЗначенияСвойств") И Значение.Пустая() Тогда
						Продолжить;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(Значение) Тогда // Значение = Неопределено
						Значение = Структура.Значение;
					КонецЕсли;
				Иначе // ВидСвойства = Неопределено
					Значение = Структура.Значение;
				КонецЕсли;
				ЗначениеКод = Структура.Значение.Код;
				ЗначениеНаименование = Структура.Значение.Наименование;
			Иначе
				Значение = Структура.Значение;
				ЗначениеКод = "";
				ЗначениеНаименование = "" + Структура.Значение;
			КонецЕсли;
			
			НоваяСтрока = Свойства.Добавить();
			Если ЗначениеЗаполнено(ВидСвойства) Тогда
				НоваяСтрока.ВидСвойства = ВидСвойства; // Справочник
			Иначе
				НоваяСтрока.ВидСвойства = Структура.ВидСвойства; // Еще не сопоставлен
			КонецЕсли;
			НоваяСтрока.ВидСвойстваКод = Структура.ВидСвойства.Код;
			НоваяСтрока.ВидСвойстваНаименование = Структура.ВидСвойства.Наименование;
			
			НоваяСтрока.Значение = Значение;
			НоваяСтрока.ЗначениеКод = ЗначениеКод;
			НоваяСтрока.ЗначениеНаименование = ЗначениеНаименование;
			
			Если Структура.Свойство("Ключевое") Тогда
				НоваяСтрока.Ключевое = Структура.Ключевое;
			Иначе
				НоваяСтрока.Ключевое = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("Штрихкоды") Тогда
		Для Каждого Штрихкод ИЗ ДанныеОбъекта.Штрихкоды Цикл
			НоваяСтрока = Штрихкоды.Добавить();
			НоваяСтрока.Штрихкод = Штрихкод;
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("СвойстваАртикула") Тогда
		Для Каждого Структура ИЗ ДанныеОбъекта.СвойстваАртикула Цикл
			НоваяСтрока = СвойстваАртикула.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Структура);
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеОбъекта.Свойство("Применяемость") Тогда
		Для Каждого Марка ИЗ ДанныеОбъекта.Применяемость Цикл
			
			Элементы.ОтборПрименяемости.СписокВыбора.Добавить(Марка.Марка.Код, Марка.Марка.Наименование);
			
			Для Каждого Модификация ИЗ Марка.Модификации Цикл
				НоваяСтрока = Применяемость.Добавить();
				НоваяСтрока.Марка = Марка.Марка.Наименование;
				НоваяСтрока.МаркаКод = Марка.Марка.Код;
				НоваяСтрока.Модификация = Модификация.Модификация.Наименование;
				НоваяСтрока.МодификацияКод = Модификация.Модификация.Код;
				НоваяСтрока.Модель = Модификация.Модель.Наименование;
				НоваяСтрока.МодельКод = Модификация.Модель.Код;
				НоваяСтрока.Каталог = Модификация.Каталог.Наименование;
				НоваяСтрока.КаталогКод = Модификация.Каталог.Код;
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	Элементы.ОтборПрименяемости.СписокВыбора.Добавить("Все", "Все");
	ОтборПрименяемости = "Все";
	
	Если ДанныеОбъекта.Свойство("ОригинальныеНомера") Тогда
		Для Каждого Структура ИЗ ДанныеОбъекта.ОригинальныеНомера Цикл
			
			Элементы.ОтборОригинальныхНомеров.СписокВыбора.Добавить(Структура.Марка.Код, Структура.Марка.Наименование);
			
			Для Каждого ОригинальныйНомер ИЗ Структура.Артикулы Цикл
				НоваяСтрока = ОригинальныеНомера.Добавить();
				НоваяСтрока.Артикул = ОригинальныйНомер;
				НоваяСтрока.Марка = Структура.Марка.Наименование;
				НоваяСтрока.МаркаКод = Структура.Марка.Код;
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	Элементы.ОтборОригинальныхНомеров.СписокВыбора.Добавить("Все", "Все");
	ОтборОригинальныхНомеров = "Все";
	
	Если ДанныеОбъекта.Свойство("Изображения") Тогда
		Для Каждого ИмяФайла ИЗ ДанныеОбъекта.Изображения Цикл
			НоваяСтрока = Изображения.Добавить();
			НоваяСтрока.ИмяФайла = ИмяФайла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПрочитатьДанныеИБ()
	
	АналогиНоменклатура.Очистить();
	СвойстваНоменклатура.Очистить();
	ШтрихкодыНоменклатура.Очистить();
	ИзображенияНоменклатура.Очистить();
	
	НоменклатураВес = 0;
	НоменклатураОбъем = 0;
	
	ПрочитатьДанныеИБ_Аналоги();
	
	Если Номенклатура.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
		Элементы.НоменклатураВес.Заголовок = "Вес, кг";
		НоменклатураВес = Номенклатура.Вес;
		НоменклатураОбъем = Номенклатура.Объем;
	ИначеЕсли ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2 Тогда
		Элементы.НоменклатураВес.Заголовок = "Ед.изм: Вес, кг";
		НоменклатураВес = Номенклатура.ОсновнаяЕдиницаИзмерения.Вес;
		НоменклатураОбъем = Номенклатура.ОсновнаяЕдиницаИзмерения.Объем;
	КонецЕсли;
	
	ПрочитатьДанныеИБ_Свойства();
	ПрочитатьДанныеИБ_ШтрихКоды();
	ПрочитатьДанныеИБ_Изображения();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИБ_ШтрихКоды()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура.Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ Различные
		|	ШтрихКоды.ШтрихКод
		|ИЗ
		|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
		|ГДЕ
		|	ШтрихКоды.Объект = &Номенклатура";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ШтрихкодыНоменклатура.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИБ_Свойства()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура.Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Свойство КАК ВидСвойства,
		|	ЗначенияСвойствОбъектов.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Объект = &Номенклатура";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СвойстваНоменклатура.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИБ_Аналоги()
	
	Если Не (ЗначениеЗаполнено(АртикулДляПоиска) И ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители")) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Артикул", Артикул);
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	Запрос.УстановитьПараметр("Производитель", Производитель);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ГруппыАналогов.ИдентификаторГруппы
		|ПОМЕСТИТЬ ИскомыеГруппы
		|ИЗ
		|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
		|ГДЕ
		|	(ГруппыАналогов.АртикулДляПоиска = &АртикулДляПоиска
		|	ИЛИ ГруппыАналогов.АртикулДляПоиска = &Артикул)
		|	И ГруппыАналогов.Производитель = &Производитель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыАналогов.Артикул,
		|	ГруппыАналогов.АртикулДляПоиска,
		|	ГруппыАналогов.Производитель,
		|	ГруппыАналогов.Наименование
		|ИЗ
		|	ИскомыеГруппы КАК ИскомыеГруппы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
		|		ПО ИскомыеГруппы.ИдентификаторГруппы = ГруппыАналогов.ИдентификаторГруппы
		|";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = АналогиНоменклатура.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИБ_Изображения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура.Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КартинкиИФайлы.Идентификатор,
		|	КартинкиИФайлы.ИмяФайла,
		|	КартинкиИФайлы.Данные,
		|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
		|	КартинкиИФайлы.Картинка
		|ИЗ
		|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
		|ГДЕ
		|	КартинкиИФайлы.Объект = &Номенклатура";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Картинка Тогда
			НоваяСтрока = ИзображенияНоменклатура.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Если Изображения.Количество() > 0 Тогда
		Элементы.СтраницаИзображения.ШрифтЗаголовка = Новый Шрифт(,,Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюСтраниц()
	
	// СтраницаАналоги
	Элементы.СтраницаАналоги.Видимость = Аналоги.Количество()>0 ИЛИ АналогиНоменклатура.Количество()>0;
	Элементы.СохранятьАналоги.Видимость = Элементы.СтраницаАналоги.Видимость;
	
	// СтраницаАналогиВсе
	Элементы.СтраницаАналогиВсе.Видимость = Аналоги_Все.Количество()>0;
	
	// СтраницаСвойства
	Элементы.СтраницаСвойства.Видимость = Свойства.Количество()>0 ИЛИ СвойстваНоменклатура.Количество()>0;
	Элементы.СохранятьСвойства.Видимость = Элементы.СтраницаСвойства.Видимость;
	
	// СтраницаШтрихКоды
	Элементы.СтраницаШтрихКоды.Видимость = Штрихкоды.Количество()>0 ИЛИ ШтрихкодыНоменклатура.Количество()>0;
	Элементы.СохранятьШтрихкоды.Видимость = Элементы.СтраницаШтрихКоды.Видимость;
	
	// СтраницаОригинальныеНомера
	Элементы.СтраницаОригинальныеНомера.Видимость = ОригинальныеНомера.Количество()>0;
	
	// СтраницаПрименяемость
	Элементы.СтраницаПрименяемость.Видимость = Применяемость.Количество()>0;
	
	// СтраницаСвойстваАртикула
	Элементы.СтраницаСвойстваАртикула.Видимость = СвойстваАртикула.Количество()>0;
	
	// СтраницаИзображения
	Элементы.СтраницаИзображения.Видимость = Изображения.Количество()>0 ИЛИ ИзображенияНоменклатура.Количество()>0;
	Элементы.СохранятьИзображения.Видимость = Элементы.СтраницаИзображения.Видимость;
	
	// СтраницаОписания
	Элементы.СтраницаОписания.Видимость = ЗначениеЗаполнено(Описание);
	
	Элементы.ДекорацияСохранятьАвтоматически.Видимость =
		Элементы.СохранятьАналоги.Видимость
		ИЛИ Элементы.СохранятьСвойства.Видимость
		ИЛИ Элементы.СохранятьШтрихкоды.Видимость
		ИЛИ Элементы.СохранятьИзображения.Видимость;
	
	Элементы.НайтиНоменклатуру.Видимость = Элементы.СоздатьНоменклатуру.Видимость;
	Элементы.ГруппаИБ.Видимость = Не Элементы.СоздатьНоменклатуру.Видимость;
	Элементы.ГруппаСохранятьАвтоматически.Видимость = Элементы.ГруппаИБ.Видимость;
	
	Элементы.ОбновитьСвойства.Видимость = Элементы.ГруппаИБ.Видимость;
	Элементы.СвойстваНоменклатура.Видимость = Элементы.ГруппаИБ.Видимость;
	
	Элементы.ОбновитьШтрихКоды.Видимость = Элементы.ГруппаИБ.Видимость;
	Элементы.ШтрихкодыНоменклатура.Видимость = Элементы.ГруппаИБ.Видимость;
	
КонецПроцедуры


&НаСервере
Процедура ОбновитьАналогиСервер()
	// Аналоги нужно обновлять для Артикула, а не для Номенклатуры
КонецПроцедуры

&НаСервере
Процедура ОбновитьСвойстваСервер()
	
	Если Номенклатура.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаСвойств ИЗ Свойства Цикл
		
		Если ТипЗнч(СтрокаСвойств.ВидСвойства) = Тип("Структура") ИЛИ ТипЗнч(СтрокаСвойств.Значение) = Тип("Структура") Тогда
			// Не сопоставлен
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаСвойств.ВидСвойства) ИЛИ Не ЗначениеЗаполнено(СтрокаСвойств.Значение) Тогда
			// Сопоставлен пустой ссылке или неопределено
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = Номенклатура.Ссылка;
		МенеджерЗаписи.Свойство = СтрокаСвойств.ВидСвойства;
		МенеджерЗаписи.Значение = СтрокаСвойств.Значение;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьШтрихКодыСервер()
	
	Для Каждого СтрокаШтришкода ИЗ Штрихкоды Цикл
		
		МенеджерЗаписи = РегистрыСведений.ШтрихКоды.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = Номенклатура.Ссылка;
		МенеджерЗаписи.ШтрихКод = СтрокаШтришкода.Штрихкод;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИзображенияСервер()
	
	Если Номенклатура.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаИзображения ИЗ Изображения Цикл
		
		Данные = атСервер.ПолучитьКартинку(СтрокаИзображения.ИмяФайла);
		Если Данные = Неопределено Тогда
			Сообщить("Не удалось получить картинку " + СтрокаИзображения.ИмяФайла);
			Продолжить;
		КонецЕсли;

		МенеджерЗаписи = РегистрыСведений.КартинкиИФайлы.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = Номенклатура.Ссылка;
		МенеджерЗаписи.Идентификатор = Лев(СтрокаИзображения.ИмяФайла,50);
		МенеджерЗаписи.ДатаСохранения = ТекущаяДата();
		МенеджерЗаписи.ИмяФайла = СтрокаИзображения.ИмяФайла;
		МенеджерЗаписи.Картинка = Истина;
		МенеджерЗаписи.Данные = Новый ХранилищеЗначения(Данные);
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиНесопоставленныеОбъекты()
	
	Результат = Новый Массив;
	
	МассивНесопоставленныхПроизводителей = Новый Массив;
	
	Если ТипЗнч(Производитель) = Тип("Строка") И НЕ ПустаяСтрока(Производитель) Тогда
		СтруктураСловаря = Новый Структура;
		Для Каждого КлючЗначение Из ДанныеОбъекта.Производитель Цикл
			СтруктураСловаря.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
		Результат.Добавить(СтруктураСловаря);
		
		МассивНесопоставленныхПроизводителей.Добавить(ДанныеОбъекта.Производитель.Код);
	КонецЕсли;
	
	Если ТипЗнч(ТипНоменклатуры) = Тип("Строка") И НЕ ПустаяСтрока(ТипНоменклатуры) Тогда
		СтруктураСловаря = Новый Структура;
		Для Каждого КлючЗначение Из ДанныеОбъекта.ТипНоменклатуры Цикл
			СтруктураСловаря.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
		Результат.Добавить(СтруктураСловаря);
	КонецЕсли;
	
	Если СохранятьСвойства Тогда
		МассивНесопоставленныхВидовСвойств = Новый Массив;
		
		Для Каждого СтрокаСвойств ИЗ Свойства Цикл
			
			Если ТипЗнч(СтрокаСвойств.ВидСвойства) = Тип("Структура") Тогда
				Если МассивНесопоставленныхВидовСвойств.Найти(СтрокаСвойств.ВидСвойства.Код) = Неопределено Тогда
					СтруктураСловаря = Новый Структура;
					Для Каждого КлючЗначение Из СтрокаСвойств.ВидСвойства Цикл
						СтруктураСловаря.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
					КонецЦикла;
					Результат.Добавить(СтруктураСловаря);
					МассивНесопоставленныхВидовСвойств.Добавить(СтрокаСвойств.ВидСвойства.Код);
				КонецЕсли;
			КонецЕсли;
			
			Если ТипЗнч(СтрокаСвойств.Значение) = Тип("Структура") Тогда
				СтруктураСловаря = Новый Структура;
				Для Каждого КлючЗначение Из СтрокаСвойств.Значение Цикл
					СтруктураСловаря.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
				КонецЦикла;
				Результат.Добавить(СтруктураСловаря);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если СохранятьАналоги Тогда
		Для Каждого СтрокаАналога ИЗ Аналоги Цикл
			Если СтрокаАналога.Раздел = "4"
				И НЕ ЗначениеЗаполнено(СтрокаАналога.Производитель)
				И НЕ ПустаяСтрока(СтрокаАналога.ПроизводительКод)
				И МассивНесопоставленныхПроизводителей.Найти(СтрокаАналога.ПроизводительКод) = Неопределено Тогда
				
				Результат.Добавить(Новый Структура("Тип, Код, Наименование, ВладелецТип, ВладелецКод", "Производители", СтрокаАналога.ПроизводительКод, ""+СтрокаАналога.Производитель, "", ""));
				МассивНесопоставленныхПроизводителей.Добавить(СтрокаАналога.ПроизводительКод);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтборОригинальныхНомеровПриИзменении(Элемент)
	
	Если ОтборОригинальныхНомеров = "Все" Тогда
		Элементы.ОригинальныеНомера.ОтборСтрок = Неопределено;
	Иначе
		Элементы.ОригинальныеНомера.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("МаркаКод",ОтборОригинальныхНомеров));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтборПрименяемостиПриИзменении(Элемент)
	
	Если ОтборПрименяемости = "Все" Тогда
		Элементы.Применяемость.ОтборСтрок = Неопределено;
	Иначе
		Элементы.Применяемость.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("МаркаКод",ОтборПрименяемости));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзображенияПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элементы.Изображения.ТекущиеДанные;
	
	Если ТекСтрока <> Неопределено Тогда
		АдресКартинки = ТекСтрока.ИмяФайла;
	Иначе
		АдресКартинки = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "атПомощникПродаж_ОбъектНоменклатура_ПриОткрытии" Тогда
		Отказ = Ложь;
		Ошибка = "";
		ЗаполнитьДанныеОбъекта(Параметр, Ошибка, Отказ);
		Если Отказ Тогда
			Если ЗначениеЗаполнено(Ошибка) Тогда
				Предупреждение(Ошибка);
			КонецЕсли;
			ЭтаФорма.Закрыть();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "атПомощникПродаж_УстановленоСоответствие" Тогда
		
		СсылкаСловаря = Параметр.СсылкаСловаря;
		СсылкаИБ = Параметр.СсылкаИБ;
		
		Если СсылкаСловаря.Тип = "ТипыНоменклатуры" И СсылкаСловаря.Код = ДанныеОбъекта.ТипНоменклатуры.Код Тогда
			ТипНоменклатуры = СсылкаИБ;
			
		ИначеЕсли СсылкаСловаря.Тип = "ВидыСвойств" Тогда
			НайденныеСтроки = Свойства.НайтиСтроки(Новый Структура("ВидСвойстваКод", СсылкаСловаря.Код));
			Для каждого Строка Из НайденныеСтроки Цикл
				Строка.ВидСвойства = СсылкаИБ;
			КонецЦикла;
			
		ИначеЕсли СсылкаСловаря.Тип = "ЗначенияСвойств" Тогда
			НайденныеСтроки = Свойства.НайтиСтроки(Новый Структура("ЗначениеКод", СсылкаСловаря.Код));
			Для каждого Строка Из НайденныеСтроки Цикл
				Строка.Значение = СсылкаИБ;
			КонецЦикла;
			
		ИначеЕсли СсылкаСловаря.Тип = "Производители" Тогда
			Если СсылкаСловаря.Код = ДанныеОбъекта.Производитель.Код Тогда
				Производитель = СсылкаИБ;
			КонецЕсли;
			
			НайденныеСтроки = Аналоги.НайтиСтроки(Новый Структура("ПроизводительКод", СсылкаСловаря.Код));
			
			Для каждого Строка Из НайденныеСтроки Цикл
				Строка.Производитель = СсылкаИБ;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзображенияНоменклатураВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекСтрока = ИзображенияНоменклатура.Получить(ВыбраннаяСтрока);
	ОткрываемыйФайл = Новый Файл(ТекСтрока.ИмяФайла);
	Если ТекСтрока.ДанныеВоВнешнемФайле Тогда
		Если ОткрываемыйФайл.Существует() Тогда
			ЗапуститьПриложение(ОткрываемыйФайл.ПолноеИмя);
		Иначе
			Предупреждение("Указанный файл " + ОткрываемыйФайл.ПолноеИмя + " не существует", 10);
		КонецЕсли;
	Иначе
		ДанныеФайла = ТекСтрока.Данные.Получить();
		ИмяКаталога = КаталогВременныхФайлов();
		ИмяВременногоФайла = ИмяКаталога + ОткрываемыйФайл.Имя;
		Попытка
			ДанныеФайла.Записать(ИмяВременногоФайла);
		Исключение
			Предупреждение("Невозможно записать файл " + ИмяВременногоФайла, 10);
			Сообщить("Ошибка: " + ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		ЗапуститьПриложение(ИмяВременногоФайла);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ЭтаФорма.Модифицированность = Ложь;
	атКлиент.УстановитьСоответствие(ДанныеОбъекта, Номенклатура.Ссылка, Истина);
	
	УправлениеВидимостьюКнопок();
	
	Если СохранятьАналоги Тогда
		ОбновитьАналогиСервер();
	КонецЕсли;
	
	Если СохранятьСвойства Тогда
		ОбновитьСвойстваСервер();
	КонецЕсли;
	
	Если СохранятьШтрихкоды Тогда
		ОбновитьШтрихКодыСервер();
	КонецЕсли;
	
	Если СохранятьИзображения Тогда
		ОбновитьИзображенияСервер();
	КонецЕсли;
	
	ПрочитатьДанныеИБ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Элементы.СоздатьНоменклатуру.Видимость Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Номенклатура.Производитель) Тогда
		Предупреждение("Производитель не сопоставлен!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Номенклатура.ТипНоменклатуры) Тогда
		Предупреждение("Тип номенклатуры не сопоставлен!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
		Номенклатура.Вес = НоменклатураВес;
		Номенклатура.Объем = НоменклатураОбъем;
	ИначеЕсли ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2 Тогда
		Номенклатура.Вес = 0;
		Номенклатура.Объем = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ Номенклатура.Ссылка.Пустая() И Не ЗначениеЗаполнено(Номенклатура.ОсновнаяЕдиницаИзмерения) Тогда
		
		спрЕдиницыИзмерения = Справочники.ЕдиницыИзмерения;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕдиницыИзмерения.Ссылка,
		|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент,
		|	ВЫБОР
		|		КОГДА ЕдиницыИзмерения.Наименование = &ЕдиницаПоКлассификатору ТОГДА
		|			0
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ КАК ПолеСортировки
		|ИЗ
		|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
		|ГДЕ
		|	ЕдиницыИзмерения.Владелец = &Владелец И 
		|	ЕдиницыИзмерения.ПометкаУдаления = ЛОЖЬ И 
		|	ЕдиницыИзмерения.ЕдиницаПоКлассификатору = &БазоваяЕдиницаИзмерения
		|УПОРЯДОЧИТЬ ПО
		|	Коэффициент ВОЗР,
		|	ПолеСортировки ВОЗР
		|";
		Если Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
			Запрос.УстановитьПараметр("Владелец",            Номенклатура.ТипНоменклатуры);
		Иначе
			Запрос.УстановитьПараметр("Владелец",            Номенклатура.Ссылка);
		КонецЕсли;
		Запрос.УстановитьПараметр("БазоваяЕдиницаИзмерения", Номенклатура.БазоваяЕдиницаИзмерения);
		Запрос.УстановитьПараметр("ЕдиницаПоКлассификатору", Номенклатура.БазоваяЕдиницаИзмерения.Наименование);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			НоваяЕдиницаИзмерения = Выборка.Ссылка;
		Иначе
			НоваяЕдиницаИзмерения = спрЕдиницыИзмерения.СоздатьЭлемент();
			Если Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
				НоваяЕдиницаИзмерения.Владелец = Номенклатура.ТипНоменклатуры;
			Иначе
				НоваяЕдиницаИзмерения.Владелец = Номенклатура.Ссылка;
				НоваяЕдиницаИзмерения.Вес = НоменклатураВес;
				НоваяЕдиницаИзмерения.Объем = НоменклатураОбъем;
			КонецЕсли;
			НоваяЕдиницаИзмерения.ЕдиницаПоКлассификатору = Номенклатура.БазоваяЕдиницаИзмерения;
			НоваяЕдиницаИзмерения.Наименование = Номенклатура.БазоваяЕдиницаИзмерения.Наименование;
			НоваяЕдиницаИзмерения.Коэффициент = 1;
			НоваяЕдиницаИзмерения.УстановитьНовыйКод("");
			НоваяЕдиницаИзмерения.Записать();
		КонецЕсли;
		
		Номенклатура.ОсновнаяЕдиницаИзмерения = НоваяЕдиницаИзмерения.Ссылка;
		
		Записать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	//НоменклатураСсылка = Номенклатура.Ссылка;
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураАртикулПриИзменении(Элемент)
	Номенклатура.АртикулДляПоиска = ПреобразоватьВАртикулДляПоискаНаСервере(Номенклатура.Артикул, Номенклатура.Производитель);
	УправлениеВидимостьюКнопок();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураРеквизитПриИзменении(Элемент)
	Модифицированность = Истина;
	УправлениеВидимостьюКнопок();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураТипНоменклатурыПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Номенклатура.ТипНоменклатуры) Тогда
		
		Номенклатура.ВидНоменклатуры = Номенклатура.ТипНоменклатуры.ВидНоменклатуры;
		Номенклатура.БазоваяЕдиницаИзмерения = Номенклатура.ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения;
		
		Если Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
			Элементы.НоменклатураВес.Заголовок = "Вес, кг";
		ИначеЕсли ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2 Тогда
			Элементы.НоменклатураВес.Заголовок = "Ед.изм: Вес, кг";
		КонецЕсли;
		
	КонецЕсли;
	
	Модифицированность = Истина;
	УправлениеВидимостьюКнопок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	//СтандартнаяОбработка = Ложь;
	//Если Не Номенклатура.Ссылка.Пустая() Тогда
	//	ЭтаФорма.Закрыть(Номенклатура.Ссылка);
	//Иначе
	//	ЭтаФорма.Закрыть(Неопределено);
	//КонецЕсли;
КонецПроцедуры



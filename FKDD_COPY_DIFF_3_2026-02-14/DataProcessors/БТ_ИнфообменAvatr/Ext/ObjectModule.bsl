Перем Адрес Экспорт;
Перем Заголовки Экспорт;
Перем Соединение Экспорт;
Перем ОбновитьКонтрагента Экспорт; 


Процедура УстановитьСоединение() Экспорт
	
	ПрофильПодключения = Справочники.БТ_ПрофилиПодключенияHTTP.Avatr;
	URL = ПрофильПодключения.URL;
	ИмяДоступа = ПрофильПодключения.ИмяПользователя;
	ПарольДоступа = ПрофильПодключения.ПарольПользователя;
	
	Соединение = Новый HTTPСоединение(URL, ,
                ИмяДоступа,
                ПарольДоступа,,
                ,
                ?(ПрофильПодключения.HTTPS, Новый ЗащищенноеСоединениеOpenSSL, Неопределено));
    
    Заголовки = Новый Соответствие;
	
	СтрокаАвторизации = ПолучитьBase64СтрокуИзДвоичныхДанных(
		ПолучитьДвоичныеДанныеИзСтроки(
			СокрЛП(ИмяДоступа) + ":" + ПарольДоступа, КодировкаТекста.UTF8, Ложь));
	
	
	Заголовки.Вставить("Content-Type", "application/json;charset=utf-8");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Authorization", ПрофильПодключения.Authorization);
	
КонецПроцедуры	

Процедура ВыгрузитьДанные() Экспорт

	Если Соединение = Неопределено Тогда
		
		УстановитьСоединение();
      	
	КонецЕсли;	
	
	Если ЗаказНаряды Тогда
		ВыгрузитьЗаказНаряды();
	КонецЕсли;	
	
КонецПроцедуры

Процедура ВыгрузитьЗаказНаряды()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанныхAvatr.Данные1С КАК Данные1С,
	                      |	БТ_СопоставлениеДанныхAvatr.Период КАК Период,
	                      |	БТ_СопоставлениеДанныхAvatr.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений.БТ_СопоставлениеДанныхAvatr КАК БТ_СопоставлениеДанныхAvatr
	                      |ГДЕ
	                      |	(БТ_СопоставлениеДанныхAvatr.ID = 0
	                      |				И НЕ &ОбновлениеДанных
	                      |			ИЛИ &ОбновлениеДанных
	                      |				И БТ_СопоставлениеДанныхAvatr.Данные1С = &ОдинЗН)
	                      |	И БТ_СопоставлениеДанныхAvatr.Тип = ""ЗаказНаряд""
	                      |	И НЕ БТ_СопоставлениеДанныхAvatr.Данные1С.НеВыгружатьИмпортеру");
	
	Запрос.УстановитьПараметр("ОбновлениеДанных", ОбновитьЗН);
	Запрос.УстановитьПараметр("ОдинЗН", ОдинЗН);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(ОдинЗН) и Выборка.Данные1С <> ОдинЗН Тогда
			ПродолжитЬ;
		КонецЕсли;	
		
		Ошибка = "";
		
		идЗН = ВыгрузитьЗН(Выборка.Данные1С, Ошибка, Выборка.ID); 
		
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНаряд", идЗН, Ошибка);
		
	КонецЦикла; 
	
	
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанныхAvatr.Данные1С КАК Данные1С,
						  |	БТ_СопоставлениеДанныхAvatr.ДопДанные1С КАК ДопДанные1С,
	                      |	БТ_ДанныеПоЗН.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений.БТ_СопоставлениеДанныхAvatr КАК БТ_СопоставлениеДанныхAvatr
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БТ_СопоставлениеДанныхAvatr КАК БТ_ДанныеПоЗН
	                      |		ПО (БТ_СопоставлениеДанныхAvatr.Данные1С = БТ_ДанныеПоЗН.Данные1С
	                      |				И БТ_ДанныеПоЗН.Тип = ""ЗаказНаряд""
	                      |				И БТ_ДанныеПоЗН.ID <> 0)
	                      |ГДЕ
	                      |	БТ_СопоставлениеДанныхAvatr.ID = 0
	                      |	И БТ_СопоставлениеДанныхAvatr.Тип = ""ЗаказНарядРаботы""");
	 
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Ошибка = "";  
		
		Если ЗначениеЗаполнено(ОдинЗН) и Выборка.Данные1С <> ОдинЗН Тогда
			ПродолжитЬ;
		КонецЕсли;
		
		Работа = Выборка.ДопДанные1С;
		
		идРаботЗН = ВыгрузитьРаботыЗН(Выборка.Данные1С, Выборка.ID, Работа, Ошибка); 
		
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНарядРаботы", идРаботЗН, Ошибка, Работа);
		
	КонецЦикла; 
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанныхAvatr.Данные1С КАК Данные1С,
						  |	БТ_СопоставлениеДанныхAvatr.ДопДанные1С КАК ДопДанные1С,
	                      |	БТ_ДанныеПоЗН.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений.БТ_СопоставлениеДанныхAvatr КАК БТ_СопоставлениеДанныхAvatr
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БТ_СопоставлениеДанныхAvatr КАК БТ_ДанныеПоЗН
	                      |		ПО (БТ_СопоставлениеДанныхAvatr.Данные1С = БТ_ДанныеПоЗН.Данные1С
	                      |				И БТ_ДанныеПоЗН.Тип = ""ЗаказНаряд""
	                      |				И БТ_ДанныеПоЗН.ID <> 0)
	                      |ГДЕ
	                      |	БТ_СопоставлениеДанныхAvatr.ID = 0
	                      |	И БТ_СопоставлениеДанныхAvatr.Тип = ""ЗаказНарядТовары""");
	 
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Ошибка = "";
		
		Если ЗначениеЗаполнено(ОдинЗН) и Выборка.Данные1С <> ОдинЗН Тогда
			ПродолжитЬ;
		КонецЕсли;
		
		Товар = Выборка.ДопДанные1С;
		
		идТовараЗН = ВыгрузитьТоварыЗН(Выборка.Данные1С, Выборка.ID, Товар, Ошибка); 
		
		ДобавитьВыгруженныеДанные(Выборка.Данные1С, "ЗаказНарядТовары", идТовараЗН, Ошибка, Товар);
		
	КонецЦикла; 


КонецПроцедуры	

Процедура ДобавитьВыгруженныеДанные(Ссылка, Тип, Ид, Комментарий = "", ДопДанные = Неопределено)
	Наб = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
	Наб.Отбор.Данные1С.Установить(Ссылка);
	
	Если ЗначениеЗаполнено(ДопДанные) Тогда
		Наб.Отбор.ДопДанные1С.Установить(ДопДанные);
	КонецЕсли;	                                  
	
	Наб.Прочитать();
	
	Если Наб.Количество() = 0 тогда
		Мен = Наб.Добавить();
		Мен.Период = ТекущаяДата();
		Мен.Тип = Тип;
		Мен.Данные1С = Ссылка;
	Иначе
		Мен = Наб[0];
	КонецЕсли;	
	
	мен.Комментарий = Комментарий;
	Мен.ID = Ид;
	Мен.ДатаВыгрузки = ТекущаяДатаСеанса();
	
	Наб.Записать();

КонецПроцедуры	

Функция ВыгрузитьРаботыЗН(ЗН, идЗН, Знач Работа, Ошибка) 
	Если 1 = 2 Тогда
		ЗН = Документы.ЗаказНаряд.ПустаяСсылка();
	КонецЕсли;	
	
	Массив = Новый Массив;
	
	СтрокаРабот = Зн.Работы.Найти(Работа);
	Если СтрокаРабот <> Неопределено Тогда
		Структура = Новый Структура;
		Структура.Вставить("purchase_id", идЗН);
		Структура.Вставить("code", ?(ЗначениеЗаполнено(СтрокаРабот.Работа.Артикул),СтрокаРабот.Работа.Артикул,СтрокаРабот.Работа.Код));
		Структура.Вставить("name", СтрокаРабот.Работа.Наименование);
		Структура.Вставить("price", КорректноеЧисло(СтрокаРабот.Цена));
		Структура.Вставить("quantity", КорректноеЧисло(СтрокаРАбот.Количество * СтрокаРАбот.Коэффициент));
		Структура.Вставить("discount", КорректноеЧисло(СтрокаРАбот.СуммаСкидки));
		Структура.Вставить("sum", КорректноеЧисло(СтрокаРАбот.СуммаВсего));
		Структура.Вставить("status", 0);
		
		Ошибка = "";
		идРабот = ОтправитьЗапрос("/api/purchase-work", Структура, Ошибка);
		
		Возврат идРабот;
	Иначе
		Ошибка = "Работа не найдена в ЗН";
	КонецЕсли;
	
КонецФункции	

Функция КорректноеЧисло(ч)
	Возврат СтрЗаменить(формат(ч, "ЧДЦ=4; ЧРД=.; ЧН="), Символы.НПП, "");
КонецФункции	

Функция ВыгрузитьТоварыЗН(ЗН, идЗН, Товар, Ошибка) 
	
	Если 1 = 2 Тогда
		ЗН = Документы.ЗаказНаряд.ПустаяСсылка();
	КонецЕсли;	
	
	Массив = Новый Массив;
	
	СтрокаТоваров = Зн.Товары.Найти(Товар);
	Если СтрокаТоваров <> Неопределено Тогда
		Структура = Новый Структура;
		Структура.Вставить("purchase_id", идЗН);
		Структура.Вставить("code", СтрокаТоваров.Номенклатура.Артикул);
		Структура.Вставить("name", СтрокаТоваров.Номенклатура.Наименование);
		Структура.Вставить("type", ?(Найти(нрег(ЗН.ВидРемонта), "гарант") > 0, 0, 1));
		Структура.Вставить("price", КорректноеЧисло(СтрокаТоваров.Цена));
		Структура.Вставить("quantity", КорректноеЧисло(СтрокаТоваров.Количество));
		Структура.Вставить("discount", КорректноеЧисло(СтрокаТоваров.СуммаСкидки));
		Структура.Вставить("sum", КорректноеЧисло(СтрокаТоваров.СуммаВсего));
		Структура.Вставить("status", 0);
		
		Ошибка = "";
		идРабот = ОтправитьЗапрос("/api/purchase-spare", Структура, Ошибка);
		
		Возврат идРабот;
	Иначе
		Ошибка = "Товар не найдена в ЗН";
	КонецЕсли;

КонецФункции	

Функция ВыгрузитьЗН(ЗН, Ошибка, идОбновления = 0)
	
	Если 1 = 2 Тогда
		ЗН = Документы.ЗаказНаряд.ПустаяСсылка();
	КонецЕсли;	
	
	_Контрагент = ЗН.Контрагент;     
	
	ВидРемонта = ВидРемонтаAvatr(ЗН.ВидРемонта);
	Если не ЗначениеЗаполнено(ВидРемонта) тогда
		Ошибка = "Не сопоставлен вид ремонта";
		Возврат 0;
	КонецЕсли;	

	Если ВидРемонта = 5 или ВидРемонта = 25 Тогда
		_Контрагент = ЗН.Заказчик;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(_Контрагент) Тогда
		_Контрагент = Справочники.Контрагенты.НайтиПоКоду("ЦБ000440");
	КонецЕсли;	
	ИдКлиента = 0;
	
	Если ЗначениеЗаполнено(_Контрагент) Тогда
		ИдКлиента = НайтиВыгруженныйОбъект(_Контрагент);
		
		Если Не ЗначениеЗаполнено(ИдКлиента) Тогда
		
			Ошибка = "";
			ИдКлиента = ВыгрузитьКонтрагента(_Контрагент, Ошибка);
			
			ДобавитьВыгруженныеДанные(_Контрагент, "Контрагент", ИдКлиента, Ошибка);
			
			Если Не ЗначениеЗаполнено(ИдКлиента) Тогда
				Ошибка = "Не сопоставлен контрагент";
				Возврат 0;
			КонецЕсли;
		Иначе
			Если ОбновитьКонтрагента = Истина Тогда
				ВыгрузитьКонтрагента(_Контрагент, Ошибка, ИдКлиента);
			КонецЕсли;
		КонецЕсли;	     
		
	КонецЕсли;	
	
	ИдАвто = НайтиВыгруженныйОбъект(ЗН.Автомобиль);
	
	Если Не ЗначениеЗаполнено(ИдАвто) Тогда
		
		Ошибка = "";
		ИдАвто = ВыгрузитьАвтомобиль(ЗН.Автомобиль, Ошибка);
		
		Если ЗначениеЗаполнено(ИдАвто) или не ПустаяСтрока(Ошибка) Тогда
			ДобавитьВыгруженныеДанные(ЗН.Автомобиль, "Автомобиль", ИдАвто, Ошибка);
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(ИдАВто) Тогда 
			Ошибка = "Не сопоставлен автомобиль";
			Возврат 0;
		КонецЕсли;
		
	Иначе
		
		Если Не ОбновитьАвто Тогда
			ОбновитьАвтомобиль(ЗН.Автомобиль, Ошибка, ИдАвто);
		Иначе
			ВыгрузитьАвтомобиль(ЗН.Автомобиль, Ошибка, ИдАвто)		
		КонецЕсли;
		
	КонецЕсли;	
	
	Ошибка = "";
	
	Структура = Новый Структура;
	Если ЗначениеЗаполнено(идОбновления) Тогда
		Структура.Вставить("ID", идОбновления);
	КонецЕсли;	
	
	Структура.Вставить("code", СокрЛП(ЗН.Номер));
	Структура.Вставить("dateOpen", Формат(ЗН.ДатаСоздания, "ДФ=dd.MM.yyyy"));
	Структура.Вставить("dateClose", Формат(?(ЗначениеЗаполнено(ЗН.ДатаЗакрытия), ЗН.ДатаЗакрытия, ТекущаяДатаСеанса()), "ДФ=dd.MM.yyyy"));
	
	Если ЗначениеЗаполнено(ЗН.Мастер) Тогда
		ИдМастера = НайтиВыгруженныйОбъект(ЗН.Мастер);
		
		Если ЗначениеЗаполнено(ИдМастера) Тогда
			Структура.Вставить("acceptor_id", ИдМастера);
		Иначе
			Ошибка = "";
			ИдМастера = ВыгрузитьСотрудника(ЗН.Мастер, Ошибка);
			
			Если Не ЗначениеЗаполнено(ИдМастера) Тогда 
				Ошибка = "Не сопоставлен мастер-приемщик";
				Возврат 0;
			КонецЕсли;	
			
			Структура.Вставить("acceptor_id", ИдМастера);
		КонецЕсли;	
	КонецЕсли;	
	
	Структура.Вставить("client_id", ИдКлиента);
	
	// БИЗТЕХ КОВАЛЬ 26.03.2025 ++
	КонтактноеЛицо = НайтиКонтакт(_Контрагент);
	Если ЗначениеЗаполнено(КонтактноеЛицо) И КонтактноеЛицо.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		
		ИдКонтакта = НайтиВыгруженныйОбъект(КонтактноеЛицо);
		
		Если Не ЗначениеЗаполнено(ИдКонтакта) Тогда
			ИдКонтакта = ВыгрузитьКонтрагента(КонтактноеЛицо, Ошибка);
			
			Если ЗначениеЗаполнено(ИдКонтакта) Тогда
				ДобавитьВыгруженныеДанные(КонтактноеЛицо, "Контрагент", ИдКонтакта, Ошибка);
            КонецЕсли;
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(ИдКонтакта) Тогда
			Структура.Вставить("contact_person_id", ИдКонтакта);
		Иначе
			Структура.Вставить("contact_person_id", "");
		КонецЕсли;	
	Иначе
	    Структура.Вставить("contact_person_id", "");
    КонецЕсли;
	// БИЗТЕХ КОВАЛЬ 26.03.2025 --
	
	Структура.Вставить("car_id", ИдАвто);
	Структура.Вставить("purchase_type_id", 1); //?(найти(нрег(Зн.Цех), "кузов") > 0, 5, 2)
	
	ТипОплаты = ТипОплаты(ЗН.ВидРемонта);
	Если не ЗначениеЗаполнено(ТипОплаты) тогда
		Ошибка = "Не сопоставлен тип оплаты";
		Возврат 0;
	КонецЕсли;	
	
	Структура.Вставить("purchase_group_id", ТипОплаты);

	
	Структура.Вставить("status", "closed");
	Структура.Вставить("verify", "draft");
	Структура.Вставить("request_reason", ?(Не пустаяСтрока(ЗН.ПричинаОбращения), СокрЛП(ЗН.ПричинаОбращения), СокрЛП(ЗН.ВидРемонта)));
	
	// БИЗТЕХ КОВАЛЬ 27.10.2025 ++
	// Правка пробега на пробег из документа
	
	//пробег = Пробег(зн.автомобиль);
	
	пробег = зн.Пробег;
	
	// БИЗТЕХ КОВАЛЬ 27.10.2025 --
	
	Если ЗначениеЗаполнено(пробег) Тогда
		Структура.Вставить("mileage",  пробег);
	КонецЕсли;	
	
	МассивВидовРемонта = Новый Массив;
	МассивВидовРемонта.Добавить(Новый Структура("id,name,status", ВидРемонта, ЗН.ВидРемонта.Наименование, 0));
	//Структура.Вставить("tags", МассивВидовРемонта);  
	Структура.Вставить("tags", ВидРемонта);  
	Структура.Вставить("recommendations", СокрЛП(ЗН.Рекомендации));  
	
	Ошибка = "";
	//идЗН = ОтправитьЗапрос("/api/purchase", Структура, Ошибка);
	
	Если Не ЗначениеЗаполнено(идОбновления) Тогда
		идЗН = ОтправитьЗапрос("/api/purchase", Структура, Ошибка);
	Иначе     
		идЗН = ОтправитьЗапрос("api/purchase/"+СтрЗаменить(идОбновления, Символы.НПП, ""), Структура, Ошибка, "PUT");
    КонецЕсли;
	
	Если ЗначениеЗаполнено(идЗН) Тогда 
		
		Если ЗначениеЗаполнено(идОбновления) Тогда
			
			Наб = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
			Наб.Отбор.Данные1С.Установить(ЗН);
			Наб.Отбор.Тип.Установить("ЗаказНарядРаботы");
			Наб.Записать();
			
			Наб = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
			Наб.Отбор.Данные1С.Установить(ЗН);
			Наб.Отбор.Тип.Установить("ЗаказНарядТовары");
			Наб.Записать();
			
		КонецЕсли;	
        
		Для Каждого СтрокаРабот Из ЗН.Работы Цикл
			НоваяЗапись = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьМенеджерЗаписи();
			НоваяЗапись.Период = ТекущаяДатаСеанса();
			НоваяЗапись.Тип = "ЗаказНарядРаботы";
			НоваяЗапись.Данные1С = ЗН;
			НоваяЗапись.ДопДанные1С = СтрокаРАбот.Работа;
			НоваяЗапись.Записать();  
			
			Ошибка = "";
			ИдРаботЗН = ВыгрузитьРаботыЗН(ЗН, идЗН, СтрокаРабот.Работа, Ошибка);
			
			ДобавитьВыгруженныеДанные(ЗН, "ЗаказНарядРаботы", идРаботЗН, Ошибка, СтрокаРабот.Работа);
		КонецЦикла;		
		
		Для Каждого СтрокаТоваров Из ЗН.Товары Цикл
			НоваяЗапись = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьМенеджерЗаписи();
			НоваяЗапись.Период = ТекущаяДатаСеанса();
			НоваяЗапись.Тип = "ЗаказНарядТовары";
			НоваяЗапись.Данные1С = ЗН;
			НоваяЗапись.ДопДанные1С = СтрокаТоваров.Номенклатура;
			Попытка
				НоваяЗапись.Записать();
			
				Ошибка = "";
				ИдТоваровЗН = ВыгрузитьТоварыЗН(ЗН, идЗН, СтрокаТоваров.Номенклатура, Ошибка);
				
				ДобавитьВыгруженныеДанные(ЗН, "ЗаказНарядТовары", идТоваровЗН, Ошибка, СтрокаТоваров.Номенклатура);
			исключение
			КонецПопытки;	
		КонецЦикла;
		
		Структура = Новый Структура; 
		Структура.Вставить("verify", "approved");	
		ОтправитьЗапрос("api/purchase/"+СтрЗаменить(идЗН, Символы.НПП, ""), Структура, Ошибка, "PUT");
	КонецЕсли;	
	
	
	
	//БИЗТЕХ КОВАЛЬ 25.02.2025 ++
	//Создание "Согласий" на портале
	ЗНОшибка = Ошибка;
	Ошибка = "";
	ОбработатьСогласия(_Контрагент,ЗН,Ошибка);
	Ошибка = ЗНОшибка;
	//БИЗТЕХ КОВАЛЬ 25.02.2025 --
	
	
	Возврат идЗН;
КонецФункции

Функция ВыгрузитьСотрудника(Сотрудник, Ошибка)
	Если 1 = 2 Тогда
		Сотрудник = Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;	
	
	Структура = Новый Структура;
	Структура.Вставить("code", Сотрудник.Код);
	
	ФИО = РазобратьФИО(Сотрудник);
	ДеньРождения = Сотрудник.ДатаРождения;

	Если ФИО.Количество() > 0 Тогда
		Структура.Вставить("first_name", ФИО[0]);
		
		Если ФИО.Количество() > 1 Тогда
			Структура.Вставить("last_name", ФИО[1]);
		КонецЕсли;	
		
		Если ФИО.Количество() > 2 Тогда
			Структура.Вставить("middle_name", ФИО[2]);
        КонецЕсли;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ДеньРождения) Тогда
		Структура.Вставить("birthday", Формат(ДеньРождения,"ДФ=dd.MM.yyyy"));
	Иначе
		Структура.Вставить("birthday", "");
	КонецЕсли;	
	
	емейл = Емейл(Сотрудник);
	
	Если ЗначениеЗаполнено(Емейл) Тогда
		Структура.Вставить("email", емейл);
	Иначе
		Структура.Вставить("email", "no"+СокрЛП(Сред(Сотрудник.Код, 3))+"@mail.ru");
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Сотрудник.ДатаПриема) Тогда
		Структура.Вставить("employment", Формат(Сотрудник.ДатаПриема, "ДФ=dd.MM.yyyy"));
	КонецЕсли;	
	
	ИдДолжности = НайтиВыгруженныйОбъект(Сотрудник.Должность);
	Если ЗначениеЗаполнено(ИдДолжности) Тогда
		Массив = Новый Массив;
		Массив.Вставить(ИдДолжности);
		
		Структура.Вставить("position_list", Массив);
	КонецЕсли;	
	
	Структура.Вставить("status", "active");
	
	Ошибка = "";
	идСотрудника = ОтправитьЗапрос("/api/manager", Структура, Ошибка);
	
	ДобавитьВыгруженныеДанные(Сотрудник, "Сотрудник", идСотрудника, Ошибка);
	
	Возврат идСотрудника;
КонецФункции	

функция ВидРемонтаAvatr(ВидРемонта)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанныхAvatr.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений.БТ_СопоставлениеВидовРемонтаAvatr КАК БТ_СопоставлениеВидовРемонтаAvatr
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БТ_СопоставлениеДанныхAvatr КАК БТ_СопоставлениеДанныхAvatr
	                      |		ПО (БТ_СопоставлениеВидовРемонтаAvatr.ДанныеAvatr = БТ_СопоставлениеДанныхAvatr.Name
	                      |				И БТ_СопоставлениеДанныхAvatr.Тип = ""ВидРемонта"")
	                      |ГДЕ
	                      |	БТ_СопоставлениеВидовРемонтаAvatr.ВидРемонта = &ВидРемонта");
	Запрос.УстановитьПараметр("ВидРемонта", ВидРемонта);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ID;
	КонецЕсли;	
КонецФункции

функция ТипОплаты(ВидРемонта)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БТ_СопоставлениеДанныхAvatr.ID КАК ID
	                      |ИЗ
	                      |	РегистрСведений.БТ_СопоставлениеВидовРемонтаAvatr КАК БТ_СопоставлениеВидовРемонтаAvatr
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БТ_СопоставлениеДанныхAvatr КАК БТ_СопоставлениеДанныхAvatr
	                      |		ПО (БТ_СопоставлениеВидовРемонтаAvatr.ТипОплатыAvatr = БТ_СопоставлениеДанныхAvatr.Name
	                      |				И БТ_СопоставлениеДанныхAvatr.Тип = ""ТипОплаты"")
	                      |ГДЕ
	                      |	БТ_СопоставлениеВидовРемонтаAvatr.ВидРемонта = &ВидРемонта");
	Запрос.УстановитьПараметр("ВидРемонта", ВидРемонта);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ID;
	КонецЕсли;	
КонецФункции

Функция Пробег(авто)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	АвтомобилиСрезПоследних.Значение КАК Значение
	                      |ИЗ
	                      |	РегистрСведений.Автомобили.СрезПоследних(
	                      |			,
	                      |			Автомобиль = &Авто
	                      |				И ВидЗначения = &Видзначения) КАК АвтомобилиСрезПоследних");  
	Запрос.УстановитьПараметр("Авто", Авто);
	Запрос.УстановитьПараметр("ВидЗначения", Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Значение;
	КонецЕсли;	                 

	Возврат 0;
	
КонецФункции	

Функция ВыгрузитьАвтомобиль(Автомобиль, Ошибка, ИдОбновления = 0)
	Если 1 = 2 Тогда
		Автомобиль = Справочники.Автомобили.ПустаяСсылка();
	КонецЕсли;
	
	ИдБренда = НайтиВыгруженныйОбъект(Автомобиль.Модель.Родитель);
	
	Если Не ЗначениеЗаполнено(ИдБренда) Тогда
		ДобавитьВыгруженныеДанные(Автомобиль, "Автомобиль", 0, "Не найден ID бренда " + Автомобиль.Модель.Родитель);
		Возврат 0;
	КонецЕсли;	
	
	ИдМодели = НайтиВыгруженныйОбъект(Автомобиль.Модель);
	
	Если Не ЗначениеЗаполнено(ИдМодели) Тогда
		ДобавитьВыгруженныеДанные(Автомобиль, "Автомобиль", 0, "Не найден ID модели " + Автомобиль.Модель);
		Возврат 0;
	КонецЕсли;	
	
	пробег = Пробег(автомобиль);
	//ЗаписьЖурналаРегистрации("ИнфообменAvatr_пробег", УровеньЖурналаРегистрации.Информация,,Автомобиль, пробег);
	
	Структура = Новый Структура; 
	//Если ЗначениеЗаполнено(ИдОбновления) Тогда
	//	Структура.Вставить("ID", ИдОбновления);
	//КонецЕсли;	
	Структура.Вставить("brand_id", ИдБренда);
	Структура.Вставить("model_id", ИдМодели);
	Структура.Вставить("code", Автомобиль.Код);
	Структура.Вставить("version", СокрЛП(Автомобиль.ВариантКомплектации));
	Структура.Вставить("color", СокрЛП(Автомобиль.Цвет));
	Структура.Вставить("vin", автомобиль.VIN); 
	Структура.Вставить("mileage",  пробег);
	Структура.Вставить("is_test_drive", 0);  
	
	ГосНомер = СокрЛП(Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер));
	Если Не ПустаяСтрока(ГосНомер) Тогда
		Структура.Вставить("reg_number", ГосНомер);  
	КонецЕсли;	

	
	Если Не ЗначениеЗаполнено(идОбновления) Тогда
		идАвто = ОтправитьЗапрос("/api/vehicle", Структура, Ошибка);
		
	Иначе
		ОтправитьЗапрос("/api/vehicle/"+СтрЗаменить(идОбновления, Символы.НПП, ""), Структура, Ошибка, "PUT");
		
		идАвто = идОбновления;
	КонецЕсли;
	
	//Если ЗначениеЗаполнено(идАвто) Тогда
	//	Наб = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
	//	Наб.Отбор.Данные1С.Установить(Автомобиль);
	//	Наб.Записать();

	//КонецЕсли;	
	
	Возврат идАвто;
	
КонецФункции

Функция ОбновитьАвтомобиль(Автомобиль, Ошибка, идАвто = 0)
	Если 1 = 2 Тогда
		Автомобиль = Справочники.Автомобили.ПустаяСсылка();
	КонецЕсли;
	
	//ИдБренда = НайтиВыгруженныйОбъект(Автомобиль.Модель.Родитель);
	//
	//Если Не ЗначениеЗаполнено(ИдБренда) Тогда
	//	Возврат 0;
	//КонецЕсли;	
	//
	//ИдМодели = НайтиВыгруженныйОбъект(Автомобиль.Модель);
	//
	//Если Не ЗначениеЗаполнено(ИдМодели) Тогда
	//	Возврат 0;
	//КонецЕсли;	
	//
	//пробег = Пробег(автомобиль);
	////ЗаписьЖурналаРегистрации("ИнфообменAvatr_пробег", УровеньЖурналаРегистрации.Информация,,Автомобиль, пробег);
	
	пробег = Пробег(автомобиль);
	
	Если Не ЗначениеЗаполнено(пробег) Тогда
		Возврат Ложь;
	КонецЕсли;	
		
	Структура = Новый Структура; 
	//Если ЗначениеЗаполнено(ИдОбновления) Тогда
	//	Структура.Вставить("ID", ИдОбновления);
	//КонецЕсли;	
	Структура.Вставить("mileage",  пробег);
	//Структура.Вставить("brand_id", ИдБренда);
	//Структура.Вставить("model_id", ИдМодели);
	//Структура.Вставить("code", Автомобиль.Код);
	//
	ОтправитьЗапрос("api/vehicle/"+СтрЗаменить(идавто, Символы.НПП, ""), Структура, Ошибка, "PUT");
	
	//Если ЗначениеЗаполнено(идАвто) Тогда
	//	Наб = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
	//	Наб.Отбор.Данные1С.Установить(Автомобиль);
	//	Наб.Записать();

	//КонецЕсли;	
	
	Возврат идАвто;
	
КонецФункции

Функция НайтиКонтакт(Контрагент)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КонтактныеЛицаКонтрагентов.Ведомый КАК Ссылка
	                      |ИЗ
	                      |	РегистрСведений.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
	                      |ГДЕ
	                      |	КонтактныеЛицаКонтрагентов.Ведущий = &Ведущий
	                      |	И КонтактныеЛицаКонтрагентов.Использование
	                      |	И КонтактныеЛицаКонтрагентов.Основной"); 
	//Запрос = Новый Запрос("ВЫБРАТЬ
	//                      |	КонтактныеЛица.Ссылка КАК Ссылка
	//                      |ИЗ
	//                      |	Справочник.КонтактныеЛица КАК КонтактныеЛица
	//                      |ГДЕ
	//                      |	КонтактныеЛица.Владелец = &Владелец
	//                      |	И НЕ КонтактныеЛица.ПометкаУдаления");
	Запрос.УстановитьПараметр("Ведущий", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.Ссылка;
		
	КонецЕсли;	
	
КонецФункции	 

Функция РазобратьФИО(Наименование)
	_Наименование = СокрЛП(СтрЗаменить(Наименование, "  ", " "));
	
	МасКомп = СтрРазделить(_Наименование, " ");
	Мас = Новый Массив;
	
	Если МасКомп.Количество() = 0 тогда
	ИначеЕсли МасКомп.Количество() = 1 Тогда
		Мас.Добавить(МасКомп[0]);
	Иначе
		Мас.Добавить(МасКомп[1]);
		Мас.Добавить(МасКомп[0]);
		
		Если МасКомп.Количество() > 2 Тогда
			Мас.Добавить(МасКомп[2]);
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Мас;
КонецФункции	

Функция Телефоны(Контрагент, ЛюбойТелефон = "")
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 2
	                      |	КонтактнаяИнформация.Представление КАК Представление,
	                      |	ВЫБОР
	                      |		КОГДА КонтактнаяИнформация.Вид = &Мобильный
	                      |			ТОГДА 1
	                      |		ИНАЧЕ 2
	                      |	КОНЕЦ КАК Тип
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект");
	Запрос.УстановитьПараметр("Мобильный", Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	МассивДанных = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	спУже = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		Если СпУже.НайтиПоЗначению(Выборка.Представление) <> Неопределено Тогда
			ПродолжитЬ;
		КонецЕсли;	   
		СпУже.Добавить(Выборка.Представление);
		
		ЛюбойТелефон = Выборка.Представление;
		
		Структура = Новый структура("number,type", Выборка.Представление,выборка.ТИп);
		МассивДанных.Добавить(Структура);
	КонецЦикла;
	
	Возврат МассивДанных;
КонецФункции	

Функция Адрес(Контрагент, ВидКонтактнойИнфо = Неопределено)
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект
	                      |	И (КонтактнаяИнформация.Вид = &Вид
	                      |			ИЛИ &все)");
	Запрос.УстановитьПараметр("Вид", ВидКонтактнойИнфо);
	Запрос.УстановитьПараметр("Все", ВидКонтактнойИнфо = Неопределено);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	МассивДанных = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Представление;
	КонецЕсли;
	
КонецФункции             

Функция Емейл(Контрагент)
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект");
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	МассивДанных = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат КорректныйТелефон(Выборка.Представление);
	КонецЕсли;

КонецФункции 

Функция КорректныйТелефон(Телефон)
	поз = найти(Телефон, "<");
	Если поз = 0 Тогда
		Возврат Телефон;
	КонецЕсли;	        
	
	_Телефон = Сред(Телефон, поз + 1);
	Поз2 = Найти(_Телефон, ">");
	
	Если Поз2 = 0 Тогда
		Возврат _Телефон;
	Иначе
		Возврат Лев(_Телефон, поз2 - 1);
	КонецЕсли;	
КонецФункции	

Функция ВыгрузитьКонтрагента(Контрагент, Ошибка, ИдОбновления = 0)
	
	Если 1 = 2 Тогда
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	//ИдОбновления в строку
	Если ЗначениеЗаполнено(ИдОбновления) Тогда
		ИдОбновленияСтрока = СтрЗаменить(идОбновления, Символы.НПП, "");
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("code", Контрагент.Код);
	Структура.Вставить("type", ?(Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо, 0, 1));
	
	ДеньРождения = Неопределено;
	ФИО = "";
	
	ДаноСОПД = Ложь;
	Имя = "";
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		КонтактноеЛицо = НайтиКонтакт(Контрагент);
		
		Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			ФИО = РазобратьФИО(КонтактноеЛицо);
			ДеньРождения = КонтактноеЛицо.ДатаРождения;
			
			Если ТипЗнч(КонтактноеЛицо) = Тип("СправочникСсылка.Контрагенты") Тогда
				Имя = КонтактноеЛицо.Имя;   
				ДаноСОПД = (КонтактноеЛицо.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да);
			КонецЕсли;	
		Иначе
			//Структура.Вставить("name", СокрЛП(Контрагент));
		КонецЕсли;
		
	Иначе
		ФИО = РазобратьФИО(Контрагент);  
		ДеньРождения = Контрагент.ДатаРождения;
		
		Имя = Контрагент.Имя;
		
		ДаноСОПД = (Контрагент.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ФИО) Тогда
		Структура.Вставить("name", ?(Не ПустаяСтрока(Имя), Имя, ФИО[0]));
		
		Если ДаноСОПД Тогда
		
			Если ФИО.Количество() > 1 Тогда
				Структура.Вставить("last_name", ФИО[1]);
			КонецЕсли;	
			
			Если ФИО.Количество() > 2 Тогда
				Структура.Вставить("middle_name", ФИО[2]);
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	

	ЛюбойТелефон = "";
	
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		Телефоны = Телефоны(КонтактноеЛицо, ЛюбойТелефон);
		емейл = Емейл(КонтактноеЛицо);
		адрес = Адрес(КонтактноеЛицо);
	Иначе
		Телефоны = Телефоны(Контрагент, ЛюбойТелефон);
		емейл = Емейл(Контрагент);
		адрес = Адрес(Контрагент);
	КонецЕсли;	                      
	
	Если Контрагент.ФормаСобственности <> Перечисления.ФормыСобственности.ЧастноеЛицо ИЛИ ДаноСОПД Тогда
		Если ЗначениеЗаполнено(Телефоны) Тогда
			Структура.Вставить("phones", Телефоны);
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Емейл) Тогда
			Структура.Вставить("email", емейл);
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Адрес) Тогда
			Структура.Вставить("address", Адрес);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДеньРождения) Тогда
			Структура.Вставить("birthday", Формат(ДеньРождения, "ДФ=dd.MM.yyyy"));
		Иначе
			Структура.Вставить("birthday", "");
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ЛюбойТелефон) Тогда
			Структура.Вставить("phone_hash", МД5(ЛюбойТелефон));
		КонецЕсли;	
	КонецЕсли;	
	
	Если Контрагент.ФормаСобственности <> Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		
		Структура.Вставить("company_name", СокрЛП(Контрагент.НаименованиеПолное));
		Структура.Вставить("company_address", Адрес(Контрагент, Справочники.ВидыКонтактнойИнформации.АдресЮридический));
		Структура.Вставить("company_inn", Контрагент.ИНН);
		Структура.Вставить("company_kpp", Контрагент.КПП);
		Структура.Вставить("company_okpo", Контрагент.КодПоОКПО);
		Структура.Вставить("company_email", емейл(Контрагент));
		
	Иначе
		
		Структура.Вставить("client_confirm_communication", ?(Контрагент.СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да, 1, 0));
		Структура.Вставить("may_process_personal_data", Структура.client_confirm_communication)
		
	КонецЕсли;

	Если Не ЗначениеЗаполнено(идОбновления) Тогда
		Возврат ОтправитьЗапрос("/api/client", Структура, Ошибка);
	Иначе
		Возврат ОтправитьЗапрос("/api/client/" + ИдОбновленияСтрока, Структура, Ошибка, "PUT");
	КонецЕсли;
	
КонецФункции    

Функция МД5(ЛюбойТелефон) Экспорт
	
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
 
	Хеш.Добавить(ЛюбойТелефон);
	 
	Возврат нрег(ПолучитьHexСтрокуИзДвоичныхДанных(Хеш.ХешСумма));
	
КонецФункции	

Функция ОтправитьЗапрос(Метод, Структура, Ошибка, ВызываемыйМетод = "POST")
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Структура);
			
	СтрокаДанных = ЗаписьJSON.Закрыть();
	
	Запрос = Новый HTTPЗапрос(Метод, Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаДанных, КодировкаТекста.UTF8);
	
    Ответ = Соединение.ВызватьHTTPМетод(ВызываемыйМетод, Запрос); 

	Если Ответ.КодСостояния >= 200 и Ответ.КодСостояния < 400 Тогда 
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		
		МассивДанных = ПрочитатьJSON(Чтение);
		
		Возврат МассивДанных.id

	Иначе	
		Ошибка = "Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		Возврат 0;
	КонецЕсли;	
	
КонецФункции

Функция НайтиВыгруженныйОбъект(Ссылка)
	
	Набор = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
	Набор.Отбор.Данные1С.Установить(Ссылка);
    Набор.Прочитать();
	
	Если Набор.Количество() Тогда
		
		Возврат Набор[0].ID;
		
	КонецЕсли;	
	
КонецФункции	

Функция ОбновитьПробегВЗН(идОбновления, Автомобиль, Ошибка)
	
	пробег = Пробег(автомобиль);
	
	Если Не ЗначениеЗаполнено(пробег) Тогда
		Возврат Ложь;
	КонецЕсли;	
		
	Структура = Новый Структура; 
	//Если ЗначениеЗаполнено(ИдОбновления) Тогда
	//	Структура.Вставить("ID", ИдОбновления);
	//КонецЕсли;	
	Структура.Вставить("mileage",  пробег);
	//Структура.Вставить("brand_id", ИдБренда);
	//Структура.Вставить("model_id", ИдМодели);
	//Структура.Вставить("code", Автомобиль.Код);
	//
	ОтправитьЗапрос("api/purchase/"+СтрЗаменить(идОбновления, Символы.НПП, ""), Структура, Ошибка, "PUT");

КонецФункции

//БИЗТЕХ КОВАЛЬ 25.02.2025 ++
//Создание "Согласий" на портале
Функция НайтиВыгруженноеСогласие(Ссылка)
	
	Набор = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
	Набор.Отбор.Тип.Установить("Согласие");
	Набор.Отбор.Данные1С.Установить(Ссылка);
    Набор.Прочитать();
	
	Если Набор.Количество() Тогда
		
		Возврат Набор[0].ID;
		
	КонецЕсли;
	
КонецФункции
Функция ОбработатьСогласия(Контрагент,ЗаказНаряд,Ошибка)
	//Реквизиты поиска
	Имя = Неопределено;
	Фамилия = Неопределено;
	Отчество = Неопределено;
	Телефон = Неопределено;
	//Определение контрагента для поиска
	КонтрагентДляПоиска = Неопределено;
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		КонтактноеЛицо = НайтиКонтакт(Контрагент);
		Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			КонтрагентДляПоиска = КонтактноеЛицо;
		КонецЕсли;
	Иначе
		КонтрагентДляПоиска = Контрагент;
	КонецЕсли;
	//Если не заполнено контактное лицо
	Если НЕ ЗначениеЗаполнено(КонтрагентДляПоиска) Тогда
		Ошибка = "Не найдено контактное лицо для поиска Согласий";
		Возврат Ложь;
	КонецЕсли;
	//Если нет СОПД
	Если КонтрагентДляПоиска.СогласиеНаОбработкуПерсональныхДанных <> Перечисления.ВариантыОтветов.Да Тогда
		Возврат Истина;
	КонецЕсли;
	//Если уже создано согласие
	Если ЗначениеЗаполнено(НайтиВыгруженноеСогласие(КонтрагентДляПоиска)) Тогда
		Возврат Истина;
	КонецЕсли;
	//Обработка телефона контрагента
	ТелефонКонтрагента = ПолучитьОсновнойТелефонИЕгоТип(Контрагент);
	Если ТелефонКонтрагента <> Неопределено Тогда
		ТелефонКонтрагента = ТелефонКонтрагента.Представление;
	Иначе
		Ошибка = "Не заполнен основной номер телефона у контрагента";
		Возврат Ложь;
	КонецЕсли;
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"(","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,")","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента," ","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"+","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"-","");
	ТелефонКонтрагента = СокрЛП(ТелефонКонтрагента);
	//ФОрмирование структуры для поиска
	СтруктураПоискаСогласий = новый Структура;
	СтруктураПоискаСогласий.Вставить("firstName",	СокрЛП(КонтрагентДляПоиска.Имя));
	СтруктураПоискаСогласий.Вставить("lastName",	СокрЛП(КонтрагентДляПоиска.Фамилия));
	СтруктураПоискаСогласий.Вставить("middleName",	СокрЛП(КонтрагентДляПоиска.Отчество));
	СтруктураПоискаСогласий.Вставить("phone",		СокрЛП(ТелефонКонтрагента));
	//Поиск существующего согласия
	НайденныеСогласия = НайтиСогласияПоРеквизитам(СтруктураПоискаСогласий);
	Если НайденныеСогласия = Ложь тогда
		Возврат Ложь;
	КонецЕсли;
	//Если согласие не нашли
	Если НЕ НайденныеСогласия.Количество() Тогда
		
		Набор = РегистрыСведений.БТ_СопоставлениеДанныхAvatr.СоздатьНаборЗаписей();
		Набор.Отбор.Данные1С.Установить(КонтрагентДляПоиска);
		Набор.Отбор.Тип.Установить("Согласие");
		Набор.Прочитать();
		
		Если Набор.Количество() = 0 тогда
			ТекущаяЗапись = Набор.Добавить();
			ТекущаяЗапись.Тип = "Согласие";
			ТекущаяЗапись.Период = ТекущаяДата();
			ТекущаяЗапись.Данные1С = КонтрагентДляПоиска;
		Иначе
			ТекущаяЗапись = Набор[0];
		КонецЕсли;
		
		Ошибка = "";
		IDСогласия = СоздатьНовоеСогласие(КонтрагентДляПоиска,ЗаказНаряд,Ошибка);
		//Если ошибка создания
		Если IDСогласия = Ложь Тогда
			ТекущаяЗапись.Комментарий = Ошибка;
			Набор.Записать();
		Иначе
			ТекущаяЗапись.Комментарий = "";
			ТекущаяЗапись.ID = IDСогласия;
			ТекущаяЗапись.ДатаВыгрузки = ТекущаяДатаСеанса();
			Набор.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции
Функция НайтиСогласияПоРеквизитам(СтруктураПоиска)
    
	СтрокаЗапросаПоиска = "?";
	Для каждого РеквизитПоиска из СтруктураПоиска Цикл
		Если ЗначениеЗаполнено(РеквизитПоиска.Значение) Тогда
			СтрокаЗапросаПоиска = СтрокаЗапросаПоиска + РеквизитПоиска.Ключ + "=" + РеквизитПоиска.Значение + "&";
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Запрос = Новый HTTPЗапрос("/api/personal-data-agreement/event"+СтрокаЗапросаПоиска, Заголовки);
    	Ответ = Соединение.ВызватьHTTPМетод("GET", Запрос); 
	Исключение
		Ошибка = "Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		Возврат Ложь;
	КонецПопытки;

	Если Ответ.КодСостояния = 200 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Если ТелоОтвета = "null" тогда
			Возврат Новый Массив();
		КонецЕсли;
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(ТелоОтвета);
		МассивДанных = ПрочитатьJSON(Чтение);
		Возврат МассивДанных;
	Иначе	
		Ошибка = "Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
Функция СоздатьНовоеСогласие(Контрагент,ЗаказНаряд,Ошибка)
		
	//Пол контрагента
	ПолКонтрагента = Неопределено;
	Если Контрагент.Пол = Перечисления.ПолФизическихЛиц.Мужской Тогда
		ПолКонтрагента = 1;
	ИначеЕсли Контрагент.Пол = Перечисления.ПолФизическихЛиц.Женский Тогда
		ПолКонтрагента = 2;
	Иначе
		Ошибка = "Не указан пол контрагента";
		Возврат Ложь;
	КонецЕсли;
	
	//Обработка телефона контрагента
	СтруктураТелефона = ПолучитьОсновнойТелефонИЕгоТип(Контрагент);
	Если СтруктураТелефона <> Неопределено Тогда
		ТелефонКонтрагента = СтруктураТелефона.Представление;
		ТипТелефонаКонтрагента = СтруктураТелефона.Тип;
	Иначе
		Ошибка = "Не заполнен основной номер телефона у контрагента";
		Возврат Ложь;
	КонецЕсли;
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"(","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,")","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента," ","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"+","");
	ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"-","");
	ТелефонКонтрагента = СокрЛП(ТелефонКонтрагента);
	
	//Почта контрагента
	Если ЗначениеЗаполнено(ПолучитьОсновнуюПочту(Контрагент)) Тогда
		ПочтаКонтрагента = ПолучитьОсновнуюПочту(Контрагент);
	Иначе
		ПочтаКонтрагента = "";
	КонецЕсли;
	
	//Дата Рождения
	Если ЗначениеЗаполнено(Контрагент.ДатаРождения) Тогда
		ДатаРожденияСтрокой = Формат(Контрагент.ДатаРождения,"ДФ=dd.MM.yyyy");
	Иначе
		Ошибка = "Не заполнена дата рождения контрагента";
		Возврат Ложь;	
	КонецЕсли;
	
	//Паспорт
	СтруктураПаспорта = ПолучитьПаспорт(Контрагент);
	Если СтруктураПаспорта = Неопределено Тогда
		Ошибка = "Не заполнен паспорт контрагента";
		Возврат Ложь;
	КонецЕсли;
	
	//Адрес
	КонтрагентАдрес = "";
	Если НЕ обЗначениеНеЗаполнено(киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресФактический)) Тогда
		КонтрагентАдрес = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресФактический);	
	ИначеЕсли НЕ обЗначениеНеЗаполнено(киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресПочтовый)) Тогда
		КонтрагентАдрес = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
	ИначеЕсли НЕ обЗначениеНеЗаполнено(киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический)) Тогда
		КонтрагентАдрес = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	Иначе
		Ошибка = "Не заполнен адрес контрагента";
		Возврат Ложь;
	КонецЕсли;
	
	VINАвтомобиля = ЗаказНаряд.Автомобиль.VIN;
	Если НЕ ЗначениеЗаполнено(ЗаказНаряд.ДатаМашинозаезда) Тогда
		Ошибка = "Не заполнена Дата машинозаезда в ЗН";
		Возврат Ложь;
	Иначе
		ДатаПосещения = Формат(ЗаказНаряд.ДатаМашинозаезда,"ДФ=dd.MM.yyyy");
	КонецЕсли;
		
	//Структура согласия
	СтруктураСогласия = новый Структура;
	СтруктураСогласия.Вставить("first_name",			СокрЛП(Контрагент.Имя));
	СтруктураСогласия.Вставить("last_name",			СокрЛП(Контрагент.Фамилия));
	СтруктураСогласия.Вставить("middle_name",		СокрЛП(Контрагент.Отчество));
	СтруктураСогласия.Вставить("gender",			ПолКонтрагента);
	СтруктураСогласия.Вставить("phone",				ТелефонКонтрагента);
	СтруктураСогласия.Вставить("phoneType",			ТипТелефонаКонтрагента);
	СтруктураСогласия.Вставить("email",				ПочтаКонтрагента);
	СтруктураСогласия.Вставить("birthdayAt",		ДатаРожденияСтрокой);
	СтруктураСогласия.Вставить("passportType",		1);
	СтруктураСогласия.Вставить("passportSeries",	СтрЗаменить(СтруктураПаспорта.Серия," ",""));
	СтруктураСогласия.Вставить("passportNumber",	СтруктураПаспорта.Номер);
	СтруктураСогласия.Вставить("passportIssueDate",	Формат(Дата(СтруктураПаспорта.ДатаВыдачи),"ДФ=dd.MM.yyyy"));
	СтруктураСогласия.Вставить("passportAuthority",	СтруктураПаспорта.КемВыдан);
	//СтруктураСогласия.Вставить("postal_code",		СокрЛП(ТелефонКонтрагента));
	СтруктураСогласия.Вставить("address",			КонтрагентАдрес);
	СтруктураСогласия.Вставить("role",				1);
	СтруктураСогласия.Вставить("type",				2);
	СтруктураСогласия.Вставить("vin",				VINАвтомобиля);
	СтруктураСогласия.Вставить("date",				ДатаПосещения);
	
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтруктураСогласия);
	СтрокаДанных = ЗаписьJSON.Закрыть();
	
	Запрос = Новый HTTPЗапрос("/api/personal-data-agreement/event", Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаДанных, КодировкаТекста.UTF8);
	
    Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос); 

	Если Ответ.КодСостояния >= 200 и Ответ.КодСостояния < 400 Тогда 
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		МассивДанных = ПрочитатьJSON(Чтение);
		Возврат МассивДанных.id
	Иначе	
		Ошибка = "Код ответа при отправке согласия " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку();
		Возврат Ложь;
	КонецЕсли;
	
Конецфункции
Функция ПолучитьОсновнойТелефонИЕгоТип(Контрагент)
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление,
	                      |	ВЫБОР
	                      |		КОГДА КонтактнаяИнформация.Вид = &Мобильный
	                      |			ТОГДА 1
	                      |		ИНАЧЕ 2
	                      |	КОНЕЦ КАК Тип
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект
	                      |	И КонтактнаяИнформация.ЗначениеПоУмолчанию = ИСТИНА");
	Запрос.УстановитьПараметр("Мобильный", Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Объект", Контрагент);

	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() Тогда
		Возврат Таблица[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции 
Функция ПолучитьОсновнуюПочту(Контрагент)
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактнаяИнформация.Представление КАК Представление
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Тип = &Тип
	                      |	И КонтактнаяИнформация.Объект = &Объект
	                      |	И КонтактнаяИнформация.ЗначениеПоУмолчанию = ИСТИНА");
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("Объект", Контрагент);

	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() Тогда
		Возврат Таблица[0].Представление;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции
Функция ПолучитьПаспорт(Контрагент)
	Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПодтверждающиеДокументы.Ссылка КАК Документ,
		|	ПодтверждающиеДокументы.КемВыдан КАК ДокументКемВыдан,
		|	ПодтверждающиеДокументы.ДатаВыдачи КАК ДокументДатаВыдачи,
		|	ПодтверждающиеДокументы.ВидПодтверждающегоДокумента КАК ВидДокумента
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.Владелец = &Владелец
		|	И ПодтверждающиеДокументы.Текущий = ИСТИНА
		|	И ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = &ВидПодтверждающегоДокумента";
		Запрос.УстановитьПараметр("Владелец", Контрагент);		
		Запрос.УстановитьПараметр("ВидПодтверждающегоДокумента", Перечисления.ВидыДокументов.Паспорт);		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		Если ВыборкаДокументов.Количество()=0 Тогда
			Возврат Неопределено;
		Иначе 
			ВыборкаДокументов.Следующий();
			СтруктураПаспорта = Новый Структура();
			СтруктураПаспорта.Вставить("Серия",				СокрЛП(ВыборкаДокументов.Документ.Серия));
			СтруктураПаспорта.Вставить("Номер",				СокрЛП(ВыборкаДокументов.Документ.Номер));
			СтруктураПаспорта.Вставить("КодПодразделения",	СокрЛП(ВыборкаДокументов.Документ.НомерБланка));
			СтруктураПаспорта.Вставить("КемВыдан",			СокрЛП(ВыборкаДокументов.Документ.КемВыдан));
			СтруктураПаспорта.Вставить("ДатаВыдачи",		СокрЛП(ВыборкаДокументов.Документ.ДатаВыдачи)); 
			Возврат СтруктураПаспорта;
		КонецЕсли;
		
КонецФункции
//БИЗТЕХ КОВАЛЬ 25.02.2025 --
// Модуль обработки АРМКлиентыИАвтомобили

Перем Права Экспорт; 					// права пользователя
Перем Дата Экспорт; 					// Дата
Перем Номер Экспорт; 					// номер корзины
Перем Проект Экспорт;					// Проект
Перем Пользователь Экспорт; 			// Пользователь
Перем Автор Экспорт; 					// Автор
Перем Менеджер Экспорт; 				// Менеджер
Перем Организация Экспорт; 				// Организация
Перем ПодразделениеКомпании Экспорт;	// Подразделение
Перем ВалютаДокумента Экспорт; 			// валюта документа для расчета цен товаров в корзине
Перем КурсДокумента Экспорт; 			// курс документа для расчета цен товаров в корзине
//Перем Контрагент Экспорт; 			// Контрагент
//Перем ДоговорВзаиморасчетов Экспорт;	// ДоговорВзаиморасчетов
//Перем ТипЦен Экспорт; 				// тип цен для расчета цен товаров в корзине
Перем ПричинаОтказа Экспорт;			// Причина отказа от корзины

Перем Карточка Экспорт;
Перем ЗначениеСкидкиНаценки Экспорт;
Перем СуммаСкидкиНаценки Экспорт;

Перем ОбработкаРасчетСкидок Экспорт;	// Для хранения ссылки на обработку "Расчет скидок"


// переменные для корректной обработки реквизитов, в том числе в форме "ЦеныИВалюта"
Перем ХозОперация Экспорт;				// ХозОперация
Перем Ссылка Экспорт;					// Ссылка
Перем ДополнительныеСвойства Экспорт;	// ДополнительныеСвойства

Перем ПараметрыПоиска Экспорт;              // параметры поиска клиентов и автомобилей
Перем СписокПолейПоискаПоУмолчанию Экспорт; // список полей поиска клиентов и автомобилей

// Обработка изменения реквизитов 
// Параметры
//  Имя			– Строка - Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма	 – Ссылка на форму документа. Если значение неопределено,
//                         производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	Если Имя = "Организация" Тогда
		ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
		
		ТаблицаТоваров = ЭтотОбъект.Товары;
		Для Каждого СтрокаТоваров Из ТаблицаТоваров Цикл
			Если ОсвобожденОтНДС Тогда
				СтрокаТоваров.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			Иначе
				СтрокаТоваров.СтавкаНДС = СтрокаТоваров.Номенклатура.СтавкаНДС;
			КонецЕсли; 
			ЭтотОбъект.ОбработкаРеквизита("Товары.Цена", СтрокаТоваров, ЭтаФорма, ДопПараметры);
		КонецЦикла;
		
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		
		Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
		#Если Клиент Тогда
			Если ЭтаФорма<>Неопределено Тогда
				ЭтаФорма.ОбновитьПодборЗамен();
			КонецЕсли; 
		#КонецЕсли
		
		Если обЗначениеНеЗаполнено(ТекСтрока.ИдентификаторНомераДетали) Тогда
			ТекСтрока.ИдентификаторГруппыАналогов = НайтиИдентификаторГруппыАналогов(ТекСтрока.Номенклатура,,, ТекСтрока.ИдентификаторНомераДетали);
		Иначе
			ТекСтрока.ИдентификаторГруппыАналогов = НайтиИдентификаторГруппыАналогов(ТекСтрока.Номенклатура);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "ВариантыПоставки.Номенклатура" Тогда
		
		Рез = Истина;
		
		// проверим а не набор ли у нас...
		Если ТипЗнч(ТекСтрока.Номенклатура) <> Тип("СправочникСсылка.Номенклатура") ИЛИ ТекСтрока.Номенклатура.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Набор Тогда
			Попытка БлокироватьВидыНоменклатуры = ЭтотОбъект.ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры(); Исключение БлокироватьВидыНоменклатуры = Неопределено; КонецПопытки;
			Если БлокироватьВидыНоменклатуры <> Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры) = Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество() > 0 Тогда
				Попытка ЕстьОшибка = (БлокироватьВидыНоменклатуры[ТекСтрока.Номенклатура.ВидНоменклатуры] <> Неопределено); Исключение ЕстьОшибка = Ложь; КонецПопытки;
				Если ЕстьОшибка Тогда
					#Если Клиент Тогда
						Предупреждение("В таблицу нельзя вводить номенклатуру вида """ + СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры) + """",5);
					#КонецЕсли
					ТекСтрока.Номенклатура = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			РезПроверки = дкПроверитьВозможностьОперацийСНоменклатурой(ТекСтрока, ЭтаФорма, ЭтотОбъект);
			
			Если РезПроверки.ЕстьБлок Тогда
				ТекСтрока.Номенклатура = Неопределено;
			КонецЕсли;
			
			Если (РезПроверки.ЕстьПредупреждение ИЛИ РезПроверки.ЕстьБлок) И НЕ ПустаяСтрока(РезПроверки.Сообщение) Тогда
				#Если Клиент Тогда
					Сообщить(РезПроверки.Сообщение, СтатусСообщения.Внимание);
				#КонецЕсли
			КонецЕсли;
			
			// установим количество
			Если ТекСтрока.Количество = 0 Тогда ТекСтрока.Количество = 1 КонецЕсли;
			
			ОсвобожденОтНДС = ЭтотОбъект.ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ ЭтотОбъект.Организация.ОсвобожденОтНДС;
			
			Если ОсвобожденОтНДС Тогда
				СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
			ИначеЕсли ТипЗнч(ТекСтрока.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
				СтавкаНДС = ТекСтрока.Номенклатура.СтавкаНДС; 
			Иначе
				СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС; 
			КонецЕсли;
			
			ТекСтрока.СтавкаНДС = СтавкаНДС;
			
			// Расчет цены. Цена зависит от трех реквизитов табличной части: номенклатура,
			// характеристика номенклатуры и единица измерения.
			// Изменение одного из них ведет к пересчету цены. Поэтому, чтобы не происходило тройного
			// расчета цен (в худшем случае), в доп. параметры
			// мы передадим признак отказа от пересчета цены.
			
			Попытка
				ДопПараметры.Вставить("ПересчитатьЦену", Ложь);
			Исключение
				ДопПараметры = Новый Структура;
				ДопПараметры.Вставить("ПересчитатьЦену", Ложь);
			КонецПопытки;
			
			// ХАРАКТЕРИСТИКА: сбросим характеристику, если она не подходит
			Если НЕ ТекСтрока.ХарактеристикаНоменклатуры.Пустая() Тогда
				Если (ТекСтрока.ХарактеристикаНоменклатуры.Владелец <> ТекСтрока.Номенклатура) И (ТекСтрока.ХарактеристикаНоменклатуры.Владелец <> ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
					ТекСтрока.ХарактеристикаНоменклатуры = Неопределено;
					ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.ХарактеристикаНоменклатуры", ТекСтрока, ЭтаФорма, ДопПараметры);
				КонецЕсли;
			КонецЕсли; 
			ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры; 
			
			// ЕД. ИЗМЕРЕНИЯ: заполним единицу измерения
			НоваяЕдиницаИзмерения = Неопределено;
			Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
				ДопПараметры.Свойство("ЕдиницаИзмерения", НоваяЕдиницаИзмерения);
			КонецЕсли; 
			Если НоваяЕдиницаИзмерения = Неопределено Тогда
				ТекЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
				Если (ТекЕдиницаИзмерения.Владелец = ТекСтрока.Номенклатура) ИЛИ (ТекЕдиницаИзмерения.Владелец = ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
					НоваяЕдиницаИзмерения = ТекЕдиницаИзмерения;
				Иначе
					НоваяЕдиницаИзмерения = ТекСтрока.Номенклатура.ОсновнаяЕдиницаИзмерения;
				КонецЕсли;
			КонецЕсли; 
			
			ТекСтрока.ЕдиницаИзмерения = НоваяЕдиницаИзмерения;
			ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.ЕдиницаИзмерения", ТекСтрока, ЭтаФорма, ДопПараметры);
			ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
			
			ДопПараметры.Удалить("ПересчитатьЦену");
			
			ВариантПоставки = Новый Структура;
			ВариантПоставки.Вставить("Поставщик",            ТекСтрока.Поставщик);
			ВариантПоставки.Вставить("Закупка",              Ложь);
			ВариантПоставки.Вставить("КлючСтрокиПоставщика", ТекСтрока.КлючСтрокиПоставщика);
			ВариантПоставки.Вставить("СрокПоставки",         ТекСтрока.СрокПоставкиВСтроке);
			ВариантПоставки.Вставить("СтавкаНДС",            ТекСтрока.СтавкаНДС);
			#Если Клиент Тогда
				ВариантПоставки.Вставить("ИнтерактивныйВвод", Истина);
			#Иначе
				ВариантПоставки.Вставить("ИнтерактивныйВвод", Ложь);
			#КонецЕсли
			
			ТекСтрока.Цена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура,,, ВалютаДокумента,, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании,, ВариантПоставки);
			
			ТекСтрока.Поставщик            = ВариантПоставки.Поставщик;
			ТекСтрока.КлючСтрокиПоставщика = ВариантПоставки.КлючСтрокиПоставщика;
			ТекСтрока.СрокПоставкиВСтроке  = ВариантПоставки.СрокПоставки;
			
			Рез = ОбработкаРеквизита("ВариантыПоставки.Цена", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
			
			//Обработка отображения интерфейса
			#Если Клиент Тогда
				Если ЭтаФорма <> Неопределено Тогда
					// покажем если надо колонку "характеристика номенклатуры"
					Если ТипЗнч(ТекСтрока.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
						ТипНоменклатуры = ТекСтрока.Номенклатура.ТипНоменклатуры;
						Если обПраво("АвтоматическиСкрыватьКолонкуХарактеристик", ЭтотОбъект.Права,, ЭтотОбъект) Тогда
							Если НЕ ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
								ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.Видимость = (ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ ТипНоменклатуры.ИспользованиеХарактеристик = 2);
							КонецЕсли; 
						КонецЕсли;
						//установим свойства колонки характеристик в зависимости от типа номенклатуры
						РучноеСписаниеХарактеристик = (ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание ИЛИ ТипНоменклатуры.АвтоСписаниеХарактеристик.Пустая());
						ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.ЭлементУправления.ОтметкаНезаполненного = РучноеСписаниеХарактеристик;
						ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.ПропускатьПриВводе = НЕ РучноеСписаниеХарактеристик;
					Иначе
						Если НЕ ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
							ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.Видимость = Ложь;
						КонецЕсли; 
						ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.ЭлементУправления.ОтметкаНезаполненного = Ложь;
						ЭтаФорма.ЭлементыФормы.ВариантыПоставки.Колонки.ХарактеристикаНоменклатуры.ПропускатьПриВводе = Истина;
					КонецЕсли; 
				КонецЕсли; 
			#КонецЕсли
		Иначе
			#Если Клиент Тогда
				Предупреждение("В таблицу нельзя вводить номенклатуру вида """ + СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры) + """",5);
			#КонецЕсли
			ТекСтрока.Номенклатура = Неопределено;
		КонецЕсли;
		
		// Вопрос что делать с идентификтатором номера детали?
		Если обЗначениеНеЗаполнено(ТекСтрока.ИдентификаторНомераДетали) Тогда
			ТекСтрока.ИдентификаторГруппыАналогов = НайтиИдентификаторГруппыАналогов(ТекСтрока.Номенклатура, ТекСтрока.АртикулДляПоиска, ТекСтрока.Производитель, ТекСтрока.ИдентификаторНомераДетали);
		Иначе
			ТекСтрока.ИдентификаторГруппыАналогов = НайтиИдентификаторГруппыАналогов(ТекСтрока.Номенклатура, ТекСтрока.АртикулДляПоиска, ТекСтрока.Производитель);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "ВариантыПоставки.ХарактеристикаНоменклатуры" Тогда
		
		Рез = орХарактеристикаНоменклатурыПриИзменении(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Рез = ?(Рез = Неопределено, Истина, Рез);
		
		// ЦЕНА: Пересчитываем цену.
		Попытка // Определяем, а нужно ли вообще пересчитывать цену.
			ПересчитатьЦену = ДопПараметры.ПересчитатьЦену;
		Исключение 
			ПересчитатьЦену = Истина;
		КонецПопытки;
		
		Если ПересчитатьЦену Тогда
			// Получаем тип цен.
			Если ТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
				ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
				ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
				
				ВариантПоставки = Новый Структура;
				ВариантПоставки.Вставить("Поставщик",            ТекСтрока.Поставщик);
				ВариантПоставки.Вставить("Закупка",              Ложь);
				ВариантПоставки.Вставить("КлючСтрокиПоставщика", ТекСтрока.КлючСтрокиПоставщика);
				ВариантПоставки.Вставить("СрокПоставки",         ТекСтрока.СрокПоставкиВСтроке);
				ВариантПоставки.Вставить("СтавкаНДС",            ТекСтрока.СтавкаНДС);
				ВариантПоставки.Вставить("ИнтерактивныйВвод",    Ложь);
				
				НоваяЦена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура,,, ВалютаДокумента,, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании,, ВариантПоставки);
				
				ТекСтрока.Поставщик            = ВариантПоставки.Поставщик;
				ТекСтрока.КлючСтрокиПоставщика = ВариантПоставки.КлючСтрокиПоставщика;
				ТекСтрока.СрокПоставкиВСтроке  = ВариантПоставки.СрокПоставки;
				
				Если НоваяЦена > 0 И ТекСтрока.Цена <> НоваяЦена Тогда
					ТекСтрока.Цена = НоваяЦена;
					Рез = ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.Цена", ТекСтрока, ЭтаФорма, ДопПараметры) И Рез;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "ВариантыПоставки.Количество" Тогда
		
		Возврат ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.Коэффициент", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "ВариантыПоставки.ЕдиницаИзмерения" Тогда
		
		Рез = Истина;
		
		Если ТекСтрока.ЕдиницаИзмерения.Пустая() Тогда
			ТекСтрока.Коэффициент = 1;
		Иначе
			ТекСтрока.Коэффициент = ТекСтрока.ЕдиницаИзмерения.Коэффициент;
			
			// ЦЕНА: Пересчитываем цену.
			Попытка // Определяем, а нужно ли вообще пересчитывать цену.
				ПересчитатьЦену = ДопПараметры.ПересчитатьЦену;
			Исключение 
				ПересчитатьЦену = Истина;
			КонецПопытки;
			Если ПересчитатьЦену Тогда
				// Получаем тип цен.
				Если ТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
					ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
					ХарактеристикаНоменклатуры = ТекСтрока.ХарактеристикаНоменклатуры;
					
					ВариантПоставки = Новый Структура;
					ВариантПоставки.Вставить("Поставщик",            ТекСтрока.Поставщик);
					ВариантПоставки.Вставить("Закупка",              Ложь);
					ВариантПоставки.Вставить("КлючСтрокиПоставщика", ТекСтрока.КлючСтрокиПоставщика);
					ВариантПоставки.Вставить("СрокПоставки",         ТекСтрока.СрокПоставкиВСтроке);
					ВариантПоставки.Вставить("СтавкаНДС",            ТекСтрока.СтавкаНДС);
					ВариантПоставки.Вставить("ИнтерактивныйВвод",    Ложь);
					
					НоваяЦена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура,,, ВалютаДокумента,, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании,, ВариантПоставки);
					
					ТекСтрока.Поставщик            = ВариантПоставки.Поставщик;
					ТекСтрока.КлючСтрокиПоставщика = ВариантПоставки.КлючСтрокиПоставщика;
					ТекСтрока.СрокПоставкиВСтроке  = ВариантПоставки.СрокПоставки;
					
					Если НоваяЦена > 0 И ТекСтрока.Цена <> НоваяЦена Тогда
						ТекСтрока.Цена = НоваяЦена;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			// Там же и пересчитывается при необходимости "ВариантыПоставки.Цена"
			Рез = ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры) И Рез;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "ВариантыПоставки.Коэффициент" Тогда
		
		ТекСтрока.КоличествоБазовое = ТекСтрока.Количество * ?(ТекСтрока.Коэффициент = 0, 1, ТекСтрока.Коэффициент);
		Возврат ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.КоличествоБазовое", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "ВариантыПоставки.КоличествоБазовое" Тогда
		
		Рез = Истина;
		
		Если орКоличествоБазовоеПриИзменении(ЭтотОбъект, ТекСтрока, ЭтаФорма, ДопПараметры) = Неопределено Тогда
			Если обПраво("ОтображатьБазовоеКоличество", ЭтотОбъект.Права,, ЭтотОбъект) Тогда
				Количество = Цел(ТекСтрока.КоличествоБазовое/ТекСтрока.Коэффициент);
				Если Количество <> 0 Тогда ТекСтрока.Количество = Количество; КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Попытка // Определяем, а нужно ли вообще пересчитывать цену.
			ПересчитатьЦену = ДопПараметры.ПересчитатьЦену;
		Исключение 
			ПересчитатьЦену = Истина;
		КонецПопытки;
		// Если мы попали сюда из секции "ВариантыПоставки.Номенклатура", то рассчитывать цену не нужно. 
		Если ПересчитатьЦену Тогда 
			Рез = ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.Цена", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "ВариантыПоставки.Цена" Тогда
		
		ТекСтрока.ЦенаОригинальная = ТекСтрока.Цена;
		
		ТекСтрока.Сумма = ТекСтрока.Цена * ТекСтрока.Количество * ?(ТекСтрока.Коэффициент = 0, 1, ТекСтрока.Коэффициент);
		
		Если ТипЗнч(ДопПараметры) <> Тип("Структура") Тогда
			ДопПараметры = Новый Структура;
		КонецЕсли;
		
		НеПерерасчитыватьЦенуСтарое = Неопределено;
		ДопПараметры.Свойство("НеПерерасчитыватьЦену", НеПерерасчитыватьЦенуСтарое);
		ДопПараметры.Вставить("НеПерерасчитыватьЦену", Истина);
		
		Рез = ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.Сумма", ТекСтрока, ЭтаФорма, ДопПараметры);
		
		Если НеПерерасчитыватьЦенуСтарое = Неопределено Тогда
			ДопПараметры.Удалить("НеПерерасчитыватьЦену");
		Иначе
			ДопПараметры.Вставить("НеПерерасчитыватьЦену", НеПерерасчитыватьЦенуСтарое);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "ВариантыПоставки.Сумма" Тогда
		
		ТекКоличество = ТекСтрока.Количество * ?(ТекСтрока.Коэффициент = 0, 1, ТекСтрока.Коэффициент);
		
		НеПерерасчитыватьЦену=Ложь;
		Если ТипЗнч(ДопПараметры)=Тип("Структура") Тогда
			ДопПараметры.Свойство("НеПерерасчитыватьЦену",НеПерерасчитыватьЦену);
			Если НеПерерасчитыватьЦену = Неопределено Тогда
				НеПерерасчитыватьЦену = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если (НЕ НеПерерасчитыватьЦену) И ТекКоличество<>0 Тогда
			Попытка ТекСтрока.Цена = Окр(ТекСтрока.Сумма/ТекКоличество,2); Исключение КонецПопытки;
		КонецЕсли;
		
		Возврат ЭтотОбъект.ОбработкаРеквизита("ВариантыПоставки.СтавкаНДС", ТекСтрока, ЭтаФорма, ДопПараметры);
		
	ИначеЕсли Имя = "ВариантыПоставки.СтавкаНДС" Тогда
		
		СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
		ТекСтрока.СуммаНДС = Окр((ТекСтрока.Сумма * СтавкаНДС)/(100 + СтавкаНДС), 2);
		
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.СуммаНДС" Тогда
		
		Возврат Истина;
		
	ИначеЕсли Имя = "ВариантыПоставки.Поставщик" Тогда
		
		Рез = Истина;
		
		ВариантПоставки = Новый Структура;
		ВариантПоставки.Вставить("Закупка",              Ложь);
		ВариантПоставки.Вставить("Поставщик",            ТекСтрока.Поставщик);
		ВариантПоставки.Вставить("КлючСтрокиПоставщика", ТекСтрока.КлючСтрокиПоставщика);
		ВариантПоставки.Вставить("СрокПоставки",         ТекСтрока.СрокПоставкиВСтроке);
		ВариантПоставки.Вставить("СтавкаНДС",            ТекСтрока.СтавкаНДС);
		#Если Клиент Тогда
			ВариантПоставки.Вставить("ИнтерактивныйВвод", Истина);
		#Иначе
			ВариантПоставки.Вставить("ИнтерактивныйВвод", Ложь);
		#КонецЕсли
		
		НоваяЦена = обПолучитьЦену(ТипЦен, ТекСтрока.Номенклатура,,, ВалютаДокумента,, ТекСтрока.ХарактеристикаНоменклатуры, ТекСтрока.ЕдиницаИзмерения, ПодразделениеКомпании,, ВариантПоставки);
		
		ТекСтрока.Поставщик            = ВариантПоставки.Поставщик;
		ТекСтрока.КлючСтрокиПоставщика = ВариантПоставки.КлючСтрокиПоставщика;
		ТекСтрока.СрокПоставкиВСтроке  = ВариантПоставки.СрокПоставки;
		
		Если НоваяЦена > 0 И ТекСтрока.Цена <> НоваяЦена Тогда
			ТекСтрока.Цена = НоваяЦена;
			Возврат ОбработкаРеквизита("ВариантыПоставки.Цена", ТекСтрока, ЭтаФорма);
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "Контрагент" Тогда
		
		Рез = Истина;
		
		Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
			Рез = дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		Иначе
			Попытка
				Если (Не ЭтотОбъект.Карточка.Пустая()) И (ЭтотОбъект.Карточка.Объект <> ЭтотОбъект.Контрагент) Тогда
					ЭтотОбъект.Карточка = Неопределено;
					Рез = дкОбработкаРеквизита(ЭтотОбъект,"Карточка",,ЭтаФорма,ДопПараметры);
				КонецЕсли;
			Исключение
			КонецПопытки;
			Попытка
				ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
				Рез = дкОбработкаРеквизита(ЭтотОбъект, "ДоговорВзаиморасчетов",, ЭтаФорма, ДопПараметры) И Рез; 
			Исключение 
			КонецПопытки; 
		КонецЕсли;
		
		Если ЭтаФорма <> Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.кнДобавитьКонтрагента.Доступность = (ТипЗнч(Контрагент) = Тип("Строка") И ЗначениеЗаполнено(Контрагент));
		КонецЕсли;
		
		КодРегиона = "";
		КодГорода = "";
		НомерТелефона = "";
		ВнутреннийНомер = "";
		ПредставлениеТелефона = "";
		ВидТелефона = Неопределено;
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Телефоны = киПолучитьСписокТелефоновОбъекта(Контрагент);
			Если Телефоны <> Неопределено И Телефоны.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, Телефоны[0].Значение);
			КонецЕсли;
		КонецЕсли;
		
		Возврат Рез;
		
	ИначеЕсли Имя = "Автомобиль" Тогда
		
		Если ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автомобили") И ЗначениеЗаполнено(Автомобиль) Тогда
			Модель = Автомобиль.Модель;
		КонецЕсли;
		
		Если ЭтаФорма <> Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.кнДобавитьАвтомобиль.Доступность = (ТипЗнч(Автомобиль) = Тип("Строка") И ЗначениеЗаполнено(Автомобиль));
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя = "Модель" Тогда
		
		Если ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автомобили") И Автомобиль.Модель <> Модель Тогда
			Автомобиль = "";
		КонецЕсли;
		
		Если ЭтаФорма <> Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.кнДобавитьАвтомобиль.Доступность = (ТипЗнч(Автомобиль) = Тип("Строка") И ЗначениеЗаполнено(Автомобиль));
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли Имя = "Товары.Цена" Тогда
		
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
		
	Иначе
		Возврат дкОбработкаРеквизита(ЭтотОбъект, Имя, ТекСтрока, ЭтаФорма, ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// Возвращает структуру обязательных / уникальных реквизитов документа
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 2-Уникальный, 3-Уникальный и обязательный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	// Обязательные поля таблицы товаров
	ОбязательныеТовары=Новый Структура();
	ОбязательныеТовары.Вставить("Номенклатура",3);
	ОбязательныеТовары.Вставить("Количество",1);
	ОбязательныеТовары.Вставить("ЕдиницаИзмерения",3);
	ОбязательныеТовары.Вставить("Коэффициент",1);
	ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры",2);

	// Обязательные реквизиты документа
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Организация",1);
	ОбязательныеРеквизиты.Вставить("ПодразделениеКомпании",1);
	ОбязательныеРеквизиты.Вставить("Автор",1);
	ОбязательныеРеквизиты.Вставить("Контрагент",1);
	ОбязательныеРеквизиты.Вставить("ВалютаДокумента",1);
	ОбязательныеРеквизиты.Вставить("КурсДокумента",1);
	ОбязательныеРеквизиты.Вставить("ТипЦен",1);
	ОбязательныеРеквизиты.Вставить("ИсточникЗапроса",1);
	//ОбязательныеРеквизиты.Вставить("ХозОперация",1);
	//ОбязательныеРеквизиты.Вставить("ДоговорВзаиморасчетов",1);
	//ОбязательныеРеквизиты.Вставить("СкладКомпании",1);
	ОбязательныеРеквизиты.Вставить("Товары", ОбязательныеТовары);
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта
//
// Параметры:
//
// Возвращаемое значение:
//  Булево - Возвращает Истина если все заполнено корректно и Ложь иначе.
// 
Функция ПроверитьКорректность() Экспорт
	
	Ошибки = "";
	Результат = Истина;
	
	// получим список обязательных у объекта и его метаданные
	Реквизиты = ПолучитьОбязательныеРеквизиты();
	ЭтотОбъектМетаданные = ЭтотОбъект.Метаданные();
	
	Для каждого Реквизит Из Реквизиты Цикл
		Если ТипЗнч(Реквизит.Значение) = Тип("Структура") Тогда
			// имеем табличную часть
			ТабличнаяЧасть = ЭтотОбъект[Реквизит.Ключ];
		
			СписокНайденныхДублей = Новый СписокЗначений();
			// идем по табличной части
			Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
				
				// создадим структуру отбора для проверки уникальности реквизитов
				СтруктураОтбора = Новый Структура;
				// переберем обязательные реквизиты
				Для каждого РеквизитТаблицы Из Реквизит.Значение Цикл
					Если (РеквизитТаблицы.Значение <> 2) И (РеквизитТаблицы.Значение > 0)
						И обЗначениеНеЗаполнено(СтрокаТаблицы[РеквизитТаблицы.Ключ]) Тогда
						Результат = Ложь;
						дкДобавитьОшибку(Ошибки, "Таблица < " + обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные, , Реквизит.Ключ)
						 + " > значение колонки < " + обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные, РеквизитТаблицы.Ключ, Реквизит.Ключ)
						 + " > не заполнено ! Строка номер " + СокрЛП(СтрокаТаблицы.НомерСтроки));
						Продолжить; // Если реквизит таблицы не заполнен, проверять его уникальность нет смысла
					КонецЕсли;
					// заполним структуру отбора для проверки уникальности реквизитов
					Если РеквизитТаблицы.Значение > 1 Тогда
						СтруктураОтбора.Вставить(СокрЛП(РеквизитТаблицы.Ключ),СтрокаТаблицы[РеквизитТаблицы.Ключ]);
					КонецЕсли;
				КонецЦикла;
				
				// проверка уникальности
				Если СтруктураОтбора.Количество() > 0 И СписокНайденныхДублей.НайтиПоЗначению(СтрокаТаблицы) = Неопределено Тогда
					
					// поищем строки удовлетворяющие структуре отбора
					НайденныеСтроки = ТабличнаяЧасть.НайтиСтроки(СтруктураОтбора);
					// если нашли и их больше 1, то строки не уникальные
					Если НайденныеСтроки.Количество() > 1 Тогда
						// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
						Результат = Ложь;
						ДублирующиесяСтроки = ""; 
						// выведем строку сообщения...
						Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
							ДублирующиесяСтроки = ДублирующиесяСтроки + "," + СокрЛП(НайденнаяСтрока.НомерСтроки);
							// добавим строку в список найденных дублей, что бы не сообщать о ней еще раз
							СписокНайденныхДублей.Добавить(НайденнаяСтрока);
						КонецЦикла;
						СтрокаРеквизитов = "";
						Для каждого РеквизитТаблицы Из СтруктураОтбора Цикл
							СтрокаРеквизитов = СтрокаРеквизитов + ?(ПустаяСтрока(СтрокаРеквизитов),"",",") + РеквизитТаблицы.Ключ;
						КонецЦикла;
						дкДобавитьОшибку(Ошибки, "Таблица < " + обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,,Реквизит.Ключ) + " > значения колон" + ?(СтруктураОтбора.Количество() > 1,"ок","ки") + " < " + СтрокаРеквизитов + " > не уникальны ! Строки: " + Сред(ДублирующиесяСтроки,2));
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли Реквизит.Значение > 0 Тогда
			Если обЗначениеНеЗаполнено(ЭтотОбъект[Реквизит.Ключ]) Тогда
				Результат = Ложь;
				дкДобавитьОшибку(Ошибки,"Реквизит < " + обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,Реквизит.Ключ) + " > не заполнен !");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Товары.Количество()=0 Тогда
		дкДобавитьОшибку(Ошибки,"В корзине нет товаров !");
		Результат = Ложь;
	КонецЕсли;
	
	Если НЕ Результат Тогда
		// Выведем все накопившиеся ошибки 
		Попытка
			СообщениеОбОшибке = ЭтотОбъект.СообщениеОбОшибке;
			Если ТипЗнч(СообщениеОбОшибке) <> Тип("Строка") Тогда
				СообщениеОбОшибке = Неопределено;
			КонецЕсли; 
		Исключение
			СообщениеОбОшибке = Неопределено;
		КонецПопытки; 
		Если СообщениеОбОшибке = Неопределено Тогда
			Сообщить("При записи корзины №" + Номер + " обнаружены ошибки :",СтатусСообщения.Внимание);
			Сообщить(Ошибки,СтатусСообщения.БезСтатуса);
			Сообщить("Из-за возникших ошибок операция записи была отменена.",СтатусСообщения.Важное); 
		Иначе
			Если НЕ ПустаяСтрока(СокрЛП(СообщениеОбОшибке)) Тогда
				СообщениеОбОшибке = СообщениеОбОшибке + "
				|";
			КонецЕсли; 
			СообщениеОбОшибке = СообщениеОбОшибке + Ошибки;
			ЭтотОбъект.СообщениеОбОшибке = СообщениеОбОшибке; 
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Возвращает новый номер корзины
Функция ПолучитьНовыйНомер() Экспорт
	ПоследнийНомер = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Корзина.Номер КАК Номер
		|ИЗ
		|	РегистрСведений.Корзина КАК Корзина
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		ПоследнийНомер = Выборка.Номер;
	КонецЕсли;
	
	Если ПоследнийНомер <> "" Тогда
		Возврат Формат(Число(ПоследнийНомер) + 1,"ЧЦ=10; ЧВН=; ЧГ=");
	Иначе
		Возврат Формат(1,"ЧЦ=10; ЧВН=; ЧГ=");
	КонецЕсли;
КонецФункции

// Выполняет резервирование номенклатуры по заказу покупателя
Процедура ЗаказПокупателяРезервировать(ЗаказПокупателя,Номенклатура,Характеристика=Неопределено) Экспорт
	
	ЕдиницаИзмерения = Номенклатура.БазоваяЕдиницаИзмерения;
	СкладКомпании = ЗаказПокупателя.СкладКомпании;
	
	Запрос = Новый Запрос;
	Если обЗначениеНеЗаполнено(Номенклатура.ТипНоменклатуры) 
		ИЛИ Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание 
	 	ИЛИ Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.ПустаяСсылка() Тогда
		ЗАпрос.Текст = "ВЫБРАТЬ
		               |	ЗаказыПокупателейОстатки.СкладКомпании КАК СкладКомпании,
		               |	СУММА(ЗаказыПокупателейОстатки.РезервОстаток) КАК РезервОстаток,
		               |	ЗаказыПокупателейОстатки.ЗаказаноОстаток,
		               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
		               |ПОМЕСТИТЬ Заказы
		               |ИЗ
		               |	РегистрНакопления.ЗаказыПокупателей.Остатки(
		               |			,
		               |			Номенклатура = &Номенклатура
		               |				И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ЗаказыПокупателейОстатки.СкладКомпании,
		               |	ЗаказыПокупателейОстатки.ЗаказаноОстаток,
		               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОстаткиТоваровКомпанииОстатки.СкладКомпании,
		               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток КАК СвободныйОстаток,
		               |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
		               |ПОМЕСТИТЬ Остатки
		               |ИЗ
		               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		               |			,
		               |			Номенклатура = &Номенклатура
		               |				И СкладКомпании = &СкладКомпании) КАК ОстаткиТоваровКомпанииОстатки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Заказы.СкладКомпании,
		               |	ЕСТЬNULL(Остатки.СвободныйОстаток, 0) КАК СвободныйОстаток,
		               |	Заказы.ЗаказаноОстаток КАК ЗаказаноОстаток,
		               |	Заказы.РезервОстаток КАК РезервОстаток,
		               |	Заказы.ХарактеристикаНоменклатуры
		               |ИЗ
		               |	Заказы КАК Заказы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
		               |		ПО Заказы.СкладКомпании = Остатки.СкладКомпании
		               |			И Заказы.ХарактеристикаНоменклатуры = Остатки.ХарактеристикаНоменклатуры
		               |ИТОГИ
		               |	СУММА(СвободныйОстаток),
		               |	СУММА(ЗаказаноОстаток),
		               |	СУММА(РезервОстаток)
		               |ПО
		               |	ОБЩИЕ";
 	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗаказыПокупателейОстатки.СкладКомпании КАК СкладКомпании,
		               |	СУММА(ЗаказыПокупателейОстатки.РезервОстаток) КАК РезервОстаток,
		               |	ЗаказыПокупателейОстатки.ЗаказаноОстаток,
		               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
		               |ПОМЕСТИТЬ Заказы
		               |ИЗ
		               |	РегистрНакопления.ЗаказыПокупателей.Остатки(
		               |			,
		               |			Номенклатура = &Номенклатура
		               |				И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ЗаказыПокупателейОстатки.СкладКомпании,
		               |	ЗаказыПокупателейОстатки.ЗаказаноОстаток,
		               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОстаткиТоваровКомпанииОстатки.СкладКомпании,
		               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток КАК СвободныйОстаток
		               |ПОМЕСТИТЬ Остатки
		               |ИЗ
		               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		               |			,
		               |			Номенклатура = &Номенклатура
		               |				И СкладКомпании = &СкладКомпании) КАК ОстаткиТоваровКомпанииОстатки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Заказы.СкладКомпании,
		               |	ЕСТЬNULL(Остатки.СвободныйОстаток, 0) КАК СвободныйОстаток,
		               |	Заказы.ЗаказаноОстаток КАК ЗаказаноОстаток,
		               |	Заказы.РезервОстаток КАК РезервОстаток,
		               |	Заказы.ХарактеристикаНоменклатуры
		               |ИЗ
		               |	Заказы КАК Заказы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
		               |		ПО Заказы.СкладКомпании = Остатки.СкладКомпании
		               |ИТОГИ
		               |	МАКСИМУМ(СвободныйОстаток),
		               |	СУММА(ЗаказаноОстаток),
		               |	СУММА(РезервОстаток)
		               |ПО
		               |	ОБЩИЕ";
	КонецЕсли;
	Запрос.УстановитьПараметр("Заказ",ЗаказПокупателя);		
	Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);			
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);			
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если Выборка.Количество()=0 Тогда
		ТекстОшибки = "Ошибка: невозможно зарезервировать больше, чем заказано! 
			| Заказ: <" + ЗаказПокупателя + ">, 
			| Номенклатура: <" + Номенклатура + ">,
			| Заказано: " + 0 + " " + ЕдиницаИзмерения + ",
			| Из них в резерве: " + 0 + " " + ЕдиницаИзмерения;
			#Если Клиент Тогда
				Предупреждение(ТекстОшибки);
			#КонецЕсли
		Возврат;
	КонецЕсли;
	Выборка.Следующий();
	
	ОшибкаСтрокаЗаказа = " 
				| Заказ: <" + ЗаказПокупателя + "> , 
				| Номенклатура: <" + Номенклатура + ">, 
				| Заказано: " + Выборка.ЗаказаноОстаток + " " + Строка(ЕдиницаИзмерения) + ",
				| Из них в резерве: " + Выборка.РезервОстаток + " " + Строка(ЕдиницаИзмерения);
	
	Заказано = Мин(Выборка.ЗаказаноОстаток - Выборка.РезервОстаток,Выборка.СвободныйОстаток);
	Заказано = Макс(Заказано,0);
	Если Заказано = 0 Тогда
		Если Выборка.СвободныйОстаток <= 0 Тогда
			Ошибки = "Ошибка: недостаточно товара на складе для резервирования!" + ОшибкаСтрокаЗаказа + Символы.ПС; 
			#Если Клиент Тогда
				Предупреждение(Ошибки);
			#КонецЕсли
			Возврат;
		Иначе
			Ошибки = "Ошибка: невозможно зарезервировать больше, чем заказано!" + ОшибкаСтрокаЗаказа + Символы.ПС; 
			#Если Клиент Тогда
				Предупреждение(Ошибки);
			#КонецЕсли
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//получим количество для выполнения операции 
	Форма = ПолучитьФорму("ФормаСозданияЗаказа");
	Форма.Кнопка = "Резервировать";
	Форма.Заказ = ЗаказПокупателя; 
	ВыборкаДетали = Выборка.Выбрать();
	Пока ВыборкаДетали.Следующий() Цикл
		Заказано = Мин(ВыборкаДетали.ЗаказаноОстаток - ВыборкаДетали.РезервОстаток,ВыборкаДетали.СвободныйОстаток);
		Заказано = Макс(Заказано,0);
		Если Заказано=0 Тогда 
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Форма.ТаблицаОперации.Добавить();
		НоваяСтрока.Номенклатура = Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаДетали.ХарактеристикаНоменклатуры;
		НоваяСтрока.Заказано = Заказано;
	КонецЦикла;
	
	Форма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = Форма.ЭлементыФормы.ОсновнаяПанель.Страницы.Резервирование;
	СтруктураВозврата = Форма.ОткрытьМодально();
	
	Если СтруктураВозврата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураВозврата.Товары.Итог("Количество")=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураВозврата) = Тип("Структура") Тогда
		КомментарийКДокументу = СтруктураВозврата.КомментарийКДокументу;
	КонецЕсли;
	
	Документ = Документы.РезервированиеЗаказовПокупателя.СоздатьДокумент();
	Документ.Заполнить(ЗаказПокупателя);
	Документ.Комментарий = КомментарийКДокументу;
	Документ.Товары.Очистить();
	
	Для Каждого СтрокаТаблицы Из СтруктураВозврата.Товары Цикл
		Если СтрокаТаблицы.Количество > 0 Тогда 
			НоваяСтрока = Документ.Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрокаТаблицы.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицы.ХарактеристикаНоменклатуры;
			НоваяСтрока.МестоРазмещения = СкладКомпании;
			НоваяСтрока.ЗаказПокупателя = ЗаказПокупателя;
			Документ.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
			НоваяСтрока.Количество = СтрокаТаблицы.Количество;
			Документ.ОбработкаРеквизита("Товары.Количество",НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Попытка 
		Документ.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Сообщить("Сформирован и проведен документ <"+СокрЛП(Документ)+">.");
	Исключение 
		Ошибки = "Ошибка: не удалось записать документ ""Резервирование заказов покупателя" + ОшибкаСтрокаЗаказа + Символы.ПС; 		
		#Если Клиент Тогда
			Предупреждение(Ошибки);
		#КонецЕсли
	КонецПопытки;
	
КонецПроцедуры // ЗаказПокупателяСнятьРезерв()

// Выполняет списание резерва номенклатуры с заказа покупателя
Процедура ЗаказПокупателяСнятьРезерв(ЗаказПокупателя,Номенклатура,Характеристика=Неопределено) Экспорт
	
	ЕдиницаИзмерения = Номенклатура.БазоваяЕдиницаИзмерения;
	
	Запрос = Новый Запрос;
	ЗАпрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыПокупателейОстатки.СкладКомпании КАК СкладКомпании,
	               |	СУММА(ЗаказыПокупателейОстатки.РезервОстаток) КАК РезервОстаток,
	               |	СУММА(ЗаказыПокупателейОстатки.ЗаказаноОстаток) КАК ЗаказаноОстаток,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	               |	ЗаказыПокупателейОстатки.Заказ КАК Заказ,
	               |	ЗаказыПокупателейОстатки.СкладКомпании КАК СкладКомпании1
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПокупателей.Остатки(
	               |			,
	               |			Номенклатура = &Номенклатура
	               |				И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаказыПокупателейОстатки.СкладКомпании,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	               |	ЗаказыПокупателейОстатки.Заказ,
	               |	ЗаказыПокупателейОстатки.СкладКомпании
	               |ИТОГИ
	               |	СУММА(РезервОстаток),
	               |	СУММА(ЗаказаноОстаток)
	               |ПО
	               |	Заказ";
	Запрос.УстановитьПараметр("Заказ",ЗаказПокупателя);			
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);			
	//Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры",Характеристика);			
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если Выборка.Количество()=0 Тогда
		ТекстОшибки = "Ошибка: невозможно снять больше, чем зарезервировано. 
			| Заказ: <" + ЗаказПокупателя + "> , 
			| Номенклатура: <" + Номенклатура + ">,
			| Остаток в резерве: " + 0 + " " + ЕдиницаИзмерения;
			#Если Клиент Тогда
				Предупреждение(ТекстОшибки);
			#КонецЕсли
		Возврат;
	КонецЕсли;
	Выборка.Следующий();
	
	ОшибкаСтрокаЗаказа = " 
				| Заказ: <" + ЗаказПокупателя + "> , 
				| Номенклатура: <" + Номенклатура + ">,
				| Остаток в резерве: " + Выборка.РезервОстаток + " " + Строка(ЕдиницаИзмерения) ;
	
	Заказано = Выборка.РезервОстаток;
	Если Заказано <= 0 Тогда
		Ошибки = "Ошибка: невозможно снять больше, чем зарезервировано." + ОшибкаСтрокаЗаказа + Символы.ПС; 
		#Если Клиент Тогда
			Предупреждение(Ошибки);
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	//получим количество для выполнения операции 
	Форма = ПолучитьФорму("ФормаСозданияЗаказа");
	//Форма.ВладелецФормы = ЭтаФорма;
	Форма.Кнопка = "СнятьРезерв";
	Форма.Заказ = ЗаказПокупателя;
	ВыборкаДетали = Выборка.Выбрать();
	Пока ВыборкаДетали.Следующий() Цикл
		Заказано = ВыборкаДетали.РезервОстаток;
		Если Заказано > 0 Тогда
			НоваяСтрока = Форма.ТаблицаОперации.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаДетали.ХарактеристикаНоменклатуры;
			НоваяСтрока.СкладКомпании = ВыборкаДетали.СкладКомпании;
			НоваяСтрока.Заказано = Заказано;
		КонецЕсли;
	КонецЦикла;
	Форма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = Форма.ЭлементыФормы.ОсновнаяПанель.Страницы.СнятиеРезерва;
	СтруктураВозврата = Форма.ОткрытьМодально();
	КоличествоПользователь=0;
	
	Если СтруктураВозврата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураВозврата.Товары.Итог("Количество")=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураВозврата) = Тип("Структура") Тогда
		КомментарийКДокументу = СтруктураВозврата.КомментарийКДокументу;
	КонецЕсли;
	
	Документ = Документы.СнятиеРезервовЗаказовПокупателя.СоздатьДокумент();
	Документ.Заполнить(ЗаказПокупателя);
	Документ.Комментарий = КомментарийКДокументу;
	Документ.КорректировкаЗаказа = Ложь;	
	Документ.Товары.Очистить();
	
	Для Каждого СтрокаТаблицы Из СтруктураВозврата.Товары Цикл
		Если СтрокаТаблицы.Количество > 0 Тогда 
			НоваяСтрока = Документ.Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрокаТаблицы.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицы.ХарактеристикаНоменклатуры;
			НоваяСтрока.МестоРазмещения = СтрокаТаблицы.СкладКомпании;
			НоваяСтрока.ЗаказПокупателя = ЗаказПокупателя;
			Документ.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
			НоваяСтрока.Количество = СтрокаТаблицы.Количество;
			Документ.ОбработкаРеквизита("Товары.Количество",НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Попытка 
		Документ.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Сообщить("Сформирован и проведен документ <"+СокрЛП(Документ)+">.");
	Исключение 
		Ошибки = "Ошибка: невозможно снять больше, чем зарезервировано." + ОшибкаСтрокаЗаказа + Символы.ПС; 
		#Если Клиент Тогда
			Предупреждение(Ошибки);
		#КонецЕсли
	КонецПопытки;
	
КонецПроцедуры // ЗаказПокупателяСнятьРезерв()

// Выполняет отмену заказа и списание резерва номенклатуры с заказа покупателя
Процедура ЗаказПокупателяОтменитьЗаказ(ЗаказПокупателя,Номенклатура,Характеристика=Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗАпрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыПокупателейОстатки.Заказ.СкладКомпании КАК СкладКомпании,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗаказыПокупателейОстатки.ЗаказаноОстаток КАК Заказано
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПокупателей.Остатки(
	               |			,
	               |			Номенклатура = &Номенклатура
	               |				И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ХарактеристикаНоменклатуры Убыв
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыПокупателейОстатки.СкладКомпании КАК СкладКомпании,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗаказыПокупателейОстатки.РезервОстаток КАК Резерв
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПокупателей.Остатки(
	               |			,
	               |			Номенклатура = &Номенклатура
	               |				И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры.Сортировка";
				   
	Запрос.УстановитьПараметр("Заказ",ЗаказПокупателя);			
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);			
	
	//отдельно получим заказанное и зарезервированное количество
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаЗаказов = МассивРезультатов[0].Выгрузить();
	ТаблицаРезервов = МассивРезультатов[1].Выгрузить();
	
	//получим количество для выполнения операции 
	Форма = ПолучитьФорму("ФормаСозданияЗаказа");
	Форма.Кнопка = "ОтменитьЗаказ";
	Форма.Заказ = ЗаказПокупателя;
	
	Для Каждого СтрокаЗаказа Из ТаблицаЗаказов Цикл
		
		КоличествоЗаказа = СтрокаЗаказа.Заказано;
		Отбор = Новый Структура("ХарактеристикаНоменклатуры",СтрокаЗаказа.ХарактеристикаНоменклатуры);
		
		//распределим заказанное количество по резервам
		Если ЗначениеЗаполнено(СтрокаЗаказа.ХарактеристикаНоменклатуры) Тогда
			НайденныеРезервы = ТаблицаРезервов.НайтиСтроки(Отбор);
			Для Каждого СтрокаРезервов Из НайденныеРезервы Цикл
				
				Если КоличествоЗаказа=0 Тогда 
					Прервать; 
				КонецЕсли;
				
				НоваяСтрока = Форма.ТаблицаОперации.Добавить();
				НоваяСтрока.Номенклатура = Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаРезервов.ХарактеристикаНоменклатуры;
				НоваяСтрока.СкладКомпании = СтрокаРезервов.СкладКомпании;
				НоваяСтрока.Заказано = Мин(СтрокаРезервов.Резерв,КоличествоЗаказа);
				НоваяСтрока.Резерв = НоваяСтрока.Заказано;		
				КоличествоЗаказа = КоличествоЗаказа - НоваяСтрока.Заказано;
				
				СтрокаРезервов.Резерв = СтрокаРезервов.Резерв - НоваяСтрока.Резерв; 
				Если СтрокаРезервов.Резерв=0 Тогда
					ТаблицаРезервов.Удалить(СтрокаРезервов);
				КонецЕслИ;
				
			КонецЦикла;
		Иначе
			Для Каждого СтрокаРезервов Из ТаблицаРезервов Цикл
				
				Если КоличествоЗаказа=0 Тогда 
					Прервать; 
				КонецЕсли;
				
				НоваяСтрока = Форма.ТаблицаОперации.Добавить();
				НоваяСтрока.Номенклатура = Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаРезервов.ХарактеристикаНоменклатуры;
				НоваяСтрока.СкладКомпании = СтрокаРезервов.СкладКомпании;
				НоваяСтрока.Заказано = Мин(СтрокаРезервов.Резерв,КоличествоЗаказа);
				НоваяСтрока.Резерв = НоваяСтрока.Заказано;		
				КоличествоЗаказа = КоличествоЗаказа - НоваяСтрока.Заказано;
				
			КонецЦикла;
		КонецЕсли;
		
		//если заказано оказалось больше чем зарезервировано - просто перенесем остаток заказа
		Если КоличествоЗаказа > 0 Тогда 
			НоваяСтрока = Форма.ТаблицаОперации.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаЗаказа.ХарактеристикаНоменклатуры;
			НоваяСтрока.СкладКомпании = СтрокаЗаказа.СкладКомпании;
			НоваяСтрока.Заказано = КоличествоЗаказа;
			НоваяСтрока.Резерв = 0;		
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.ТаблицаОперации.Свернуть("Номенклатура,СкладКомпании,ХарактеристикаНоменклатуры","Количество,Заказано,Резерв");
	Если Форма.ТаблицаОперации.Итог("Заказано")=0 Тогда
		Ошибки = "Ошибка: невозможно отменить больше, чем заказано. 
				| Заказ: <" + ЗаказПокупателя + "> , 
				| Номенклатура: <" + Номенклатура + ">,
				| Остаток по заказу: " + 0;
			#Если Клиент Тогда
				Предупреждение(Ошибки);
			#КонецЕсли
		Возврат;
	КонецЕсли;
	
	ОшибкаСтрокаЗаказа = "
				| Заказ: <" + ЗаказПокупателя + "> , 
				| Номенклатура: <" + Номенклатура + ">";
	
	Форма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = Форма.ЭлементыФормы.ОсновнаяПанель.Страницы.ПередачаКлиенту;
	СтруктураВозврата = Форма.ОткрытьМодально();
	Если СтруктураВозврата = Неопределено ИЛИ СтруктураВозврата.Товары.Итог("Количество")=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураВозврата) = Тип("Структура") Тогда
		КомментарийКДокументу = СтруктураВозврата.КомментарийКДокументу;
	КонецЕсли;
	
	ДокСнятиеРезервов = Документы.СнятиеРезервовЗаказовПокупателя.СоздатьДокумент();
	ДокСнятиеРезервов.Заполнить(ЗаказПокупателя);
	ДокСнятиеРезервов.Комментарий = КомментарийКДокументу;
	ДокСнятиеРезервов.КорректировкаЗаказа = Истина;	
	ДокСнятиеРезервов.Товары.Очистить();
	
	ДокКорректировка = Документы.КорректировкаЗаказаПокупателя.СоздатьДокумент();
	ДокКорректировка.Заполнить(ЗаказПокупателя);
	ДокКорректировка.Комментарий = КомментарийКДокументу;
	ТзТоваров = ДокКорректировка.Товары.Выгрузить();	
	ДокКорректировка.Товары.Очистить();
	
	Для Каждого СтрокаТаблицы Из СтруктураВозврата.Товары Цикл
		Если СтрокаТаблицы.Количество > 0 Тогда 
			Если СтрокаТаблицы.Резерв > 0 Тогда	
				НоваяСтрока = ДокСнятиеРезервов.Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрокаТаблицы.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицы.ХарактеристикаНоменклатуры;
				НоваяСтрока.МестоРазмещения = СтрокаТаблицы.СкладКомпании;
				НоваяСтрока.ЗаказПокупателя = ЗаказПокупателя;
				ДокСнятиеРезервов.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				НоваяСтрока.Количество = Мин(СтрокаТаблицы.Резерв,СтрокаТаблицы.Количество);
			КонецЕсли;
		
			//количество по ресурсу "заказано", не снятое "Снятием резервов", спишем корректировкой.
			Если СтрокаТаблицы.Резерв < СтрокаТаблицы.Количество Тогда
				СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",СтрокаТаблицы.Номенклатура,СтрокаТаблицы.ХарактеристикаНоменклатуры);
				МассивСтрок = ТзТоваров.НайтиСтроки(СтруктураПоиска);
				Если МассивСтрок.Количество() > 0 Тогда
					НоваяСтрока = ДокКорректировка.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,МассивСтрок[0]);
					НоваяСтрока.Количество = Макс(СтрокаТаблицы.Заказано-СтрокаТаблицы.Количество,0);
					ДокКорректировка.ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НачатьТранзакцию();
	Ошибки = "";
	
	Если ДокСнятиеРезервов <> Неопределено И ДокСнятиеРезервов.Товары.Количество()=0 Тогда
		ДокСнятиеРезервов = Неопределено;
	КонецЕсли;
	Если ДокСнятиеРезервов <> Неопределено Тогда
		Попытка 
			ДокСнятиеРезервов.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Исключение 
			Ошибки = Ошибки + "Ошибка: не удалось записать документ ""Снятие резервов заказов покупателя""" + ОшибкаСтрокаЗаказа + Символы.ПС;
		КонецПопытки;
	КонецЕсли;
	
	Если ДокКорректировка <> Неопределено И ДокКорректировка.Товары.Количество()=0 Тогда
		ДокКорректировка = Неопределено;
	КонецЕсли;
	Если ДокКорректировка <> Неопределено Тогда
		Попытка 
			ДокКорректировка.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Исключение 
			Ошибки = Ошибки + "Ошибка: не удалось записать документ ""Корректировка заказа покупателя""" + ОшибкаСтрокаЗаказа + Символы.ПС;
		КонецПопытки;
	КонецЕсли;
	
	Если Ошибки<>"" Тогда
		ОтменитьТранзакцию();	
		#Если Клиент Тогда
			Предупреждение(Ошибки);
		#КонецЕсли
	Иначе
		Если ДокСнятиеРезервов <> Неопределено Тогда
			Сообщить("Сформирован и проведен документ <"+СокрЛП(ДокСнятиеРезервов)+">.");
		КонецЕсли;
		Если ДокКорректировка <> Неопределено Тогда
			Сообщить("Сформирован и проведен документ <"+СокрЛП(ДокКорректировка)+">.");
		КонецЕсли;
		ЗафиксироватьТранзакцию();	
	КонецЕсли;
	
КонецПроцедуры // ЗаказПокупателяОтменитьЗаказ()

// Выполняет отмену заказа и списание резерва номенклатуры с заказа покупателя,
// затем создает и резервирование на данную номенклатуру под другого контрагента
Процедура ЗаказПокупателяОтдатьЗаказ(ЗаказПокупателя,Номенклатура,Характеристика=Неопределено) Экспорт

	Запрос = Новый Запрос;
	ЗАпрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыПокупателейОстатки.Заказ.СкладКомпании КАК СкладКомпании,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗаказыПокупателейОстатки.ЗаказаноОстаток КАК Заказано
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПокупателей.Остатки(
	               |			,
	               |			Номенклатура = &Номенклатура
	               |				И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ХарактеристикаНоменклатуры Убыв
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыПокупателейОстатки.СкладКомпании КАК СкладКомпании,
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗаказыПокупателейОстатки.РезервОстаток КАК Резерв
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПокупателей.Остатки(
	               |			,
	               |			Номенклатура = &Номенклатура
	               |				И Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры.Сортировка";
				   
	Запрос.УстановитьПараметр("Заказ",ЗаказПокупателя);			
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);			
	
	//отдельно получим заказанное и зарезервированное количество
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаЗаказов = МассивРезультатов[0].Выгрузить();
	ТаблицаРезервов = МассивРезультатов[1].Выгрузить();
	
	//получим количество для выполнения операции 
	Форма = ПолучитьФорму("ФормаСозданияЗаказа");
	Форма.Кнопка = "ОтдатьЗаказ";
	Форма.Заказ = ЗаказПокупателя;
	
	Для Каждого СтрокаЗаказа Из ТаблицаЗаказов Цикл
		
		КоличествоЗаказа = СтрокаЗаказа.Заказано;
		Отбор = Новый Структура("ХарактеристикаНоменклатуры",СтрокаЗаказа.ХарактеристикаНоменклатуры);
		
		//распределим заказанное количество по резервам
		Если ЗначениеЗаполнено(СтрокаЗаказа.ХарактеристикаНоменклатуры) Тогда
			НайденныеРезервы = ТаблицаРезервов.НайтиСтроки(Отбор);
			Для Каждого СтрокаРезервов Из НайденныеРезервы Цикл
				
				Если КоличествоЗаказа=0 Тогда 
					Прервать; 
				КонецЕсли;
				
				НоваяСтрока = Форма.ТаблицаОперации.Добавить();
				НоваяСтрока.Номенклатура = Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаРезервов.ХарактеристикаНоменклатуры;
				НоваяСтрока.СкладКомпании = СтрокаРезервов.СкладКомпании;
				НоваяСтрока.Заказано = Мин(СтрокаРезервов.Резерв,КоличествоЗаказа);
				НоваяСтрока.Резерв = НоваяСтрока.Заказано;		
				КоличествоЗаказа = КоличествоЗаказа - НоваяСтрока.Заказано;
				
				СтрокаРезервов.Резерв = СтрокаРезервов.Резерв - НоваяСтрока.Резерв; 
				Если СтрокаРезервов.Резерв=0 Тогда
					ТаблицаРезервов.Удалить(СтрокаРезервов);
				КонецЕслИ;
				
			КонецЦикла;
		Иначе
			Для Каждого СтрокаРезервов Из ТаблицаРезервов Цикл
				
				Если КоличествоЗаказа=0 Тогда 
					Прервать; 
				КонецЕсли;
				
				НоваяСтрока = Форма.ТаблицаОперации.Добавить();
				НоваяСтрока.Номенклатура = Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаРезервов.ХарактеристикаНоменклатуры;
				НоваяСтрока.СкладКомпании = СтрокаРезервов.СкладКомпании;
				НоваяСтрока.Заказано = Мин(СтрокаРезервов.Резерв,КоличествоЗаказа);
				НоваяСтрока.Резерв = НоваяСтрока.Заказано;		
				КоличествоЗаказа = КоличествоЗаказа - НоваяСтрока.Заказано;
				
			КонецЦикла;
		КонецЕсли;
		
		//если заказано оказалось больше чем зарезервировано - просто перенесем остаток заказа
		Если КоличествоЗаказа > 0 Тогда 
			НоваяСтрока = Форма.ТаблицаОперации.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаЗаказа.ХарактеристикаНоменклатуры;
			НоваяСтрока.СкладКомпании = СтрокаЗаказа.СкладКомпании;
			НоваяСтрока.Заказано = КоличествоЗаказа;
			НоваяСтрока.Резерв = 0;		
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.ТаблицаОперации.Свернуть("Номенклатура,СкладКомпании,ХарактеристикаНоменклатуры","Количество,Заказано,Резерв");
	Если Форма.ТаблицаОперации.Итог("Заказано")=0 Тогда
		Ошибки = "Ошибка: невозможно отменить больше, чем заказано. 
				| Заказ: <" + ЗаказПокупателя + "> , 
				| Номенклатура: <" + Номенклатура + ">,
				| Остаток по заказу: " + 0;
			#Если Клиент Тогда
				Предупреждение(Ошибки);
			#КонецЕсли
		Возврат;
	КонецЕсли;
	
	ОшибкаСтрокаЗаказа = "
				| Заказ: <" + ЗаказПокупателя + "> , 
				| Номенклатура: <" + Номенклатура + ">";
	
	Форма.ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = Форма.ЭлементыФормы.ОсновнаяПанель.Страницы.ПередачаКлиенту;
	СтруктураВозврата = Форма.ОткрытьМодально();
	Если СтруктураВозврата = Неопределено ИЛИ СтруктураВозврата.Товары.Итог("Количество")=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураВозврата) = Тип("Структура") Тогда
		КомментарийКДокументу = СтруктураВозврата.КомментарийКДокументу;
		НовыйКлиент = СтруктураВозврата.НовыйКлиент;
	КонецЕсли;
	
	ДокСнятиеРезервов = Документы.СнятиеРезервовЗаказовПокупателя.СоздатьДокумент();
	ДокСнятиеРезервов.Заполнить(ЗаказПокупателя);
	ДокСнятиеРезервов.Комментарий = КомментарийКДокументу;
	ДокСнятиеРезервов.КорректировкаЗаказа = Истина;	
	ДокСнятиеРезервов.Товары.Очистить();
	
	ДокКорректировка = Документы.КорректировкаЗаказаПокупателя.СоздатьДокумент();
	ДокКорректировка.Заполнить(ЗаказПокупателя);
	ДокКорректировка.Комментарий = КомментарийКДокументу;
	ТзТоваров = ДокКорректировка.Товары.Выгрузить();	
	ДокКорректировка.Товары.Очистить();
	
	Для Каждого СтрокаТаблицы Из СтруктураВозврата.Товары Цикл
		Если СтрокаТаблицы.Количество > 0 Тогда 
			Если СтрокаТаблицы.Резерв > 0 Тогда	
				НоваяСтрока = ДокСнятиеРезервов.Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрокаТаблицы.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицы.ХарактеристикаНоменклатуры;
				НоваяСтрока.МестоРазмещения = СтрокаТаблицы.СкладКомпании;
				НоваяСтрока.ЗаказПокупателя = ЗаказПокупателя;
				ДокСнятиеРезервов.ОбработкаРеквизита("Товары.Номенклатура",НоваяСтрока);
				НоваяСтрока.Количество = Мин(СтрокаТаблицы.Резерв,СтрокаТаблицы.Количество);
			КонецЕсли;
		
			//количество по ресурсу "заказано", не снятое "Снятием резервов", спишем корректировкой.
			Если СтрокаТаблицы.Резерв < СтрокаТаблицы.Количество Тогда
				СтруктураПоиска = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",СтрокаТаблицы.Номенклатура,СтрокаТаблицы.ХарактеристикаНоменклатуры);
				МассивСтрок = ТзТоваров.НайтиСтроки(СтруктураПоиска);
				Если МассивСтрок.Количество() > 0 Тогда
					НоваяСтрока = ДокКорректировка.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,МассивСтрок[0]);
					НоваяСтрока.Количество = Макс(СтрокаТаблицы.Заказано-СтрокаТаблицы.Количество,0);
					ДокКорректировка.ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДокЗаказ = Документы.ЗаказПокупателя.СоздатьДокумент();
	ДокЗаказ.ХозОперация = Справочники.ХозОперации.ЗаказРезервированиеПокупателя;
	ДокЗаказ.Заполнить(ЗаказПокупателя);
	ДокЗаказ.Контрагент = НовыйКлиент;
	дкОбработкаРеквизита(ДокЗаказ,"Контрагент");
	ДокЗаказ.СуммаПредоплаты = 0;
	ДокЗаказ.ПроцентПредоплаты = 50;
	ДокЗаказ.Комментарий = КомментарийКДокументу;
	
	СтруктураВозврата.Товары.Свернуть("Номенклатура,ХарактеристикаНоменклатуры","Количество");
	
	ДокЗаказ.Товары.Очистить();
	Для Каждого СтрокаТаблицы Из СтруктураВозврата.Товары Цикл
		Если СтрокаТаблицы.Количество > 0 Тогда 
			НоваяСтрока = ДокЗаказ.Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрокаТаблицы.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицы.ХарактеристикаНоменклатуры;
			ДокЗаказ.ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
			НоваяСтрока.Количество = СтрокаТаблицы.Количество;
			НоваяСтрока.Резерв = СтрокаТаблицы.Количество;
			ДокЗаказ.ОбработкаРеквизита("Товары.Количество", НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	НачатьТранзакцию();
	Ошибки = "";
	
	Если ДокСнятиеРезервов <> Неопределено И ДокСнятиеРезервов.Товары.Количество()=0 Тогда
		ДокСнятиеРезервов = Неопределено;
	КонецЕсли;
	Если ДокСнятиеРезервов <> Неопределено Тогда
		Попытка 
			ДокСнятиеРезервов.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Исключение 
			Ошибки = Ошибки + "Ошибка: не удалось записать документ ""Снятие резервов заказов покупателя""" + ОшибкаСтрокаЗаказа + Символы.ПС;
		КонецПопытки;
	КонецЕсли;
	
	Если ДокКорректировка <> Неопределено И ДокКорректировка.Товары.Количество()=0 Тогда
		ДокКорректировка = Неопределено;
	КонецЕсли;
	Если ДокКорректировка <> Неопределено Тогда
		Попытка 
			ДокКорректировка.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Исключение 
			Ошибки = Ошибки + "Ошибка: не удалось записать документ ""Корректировка заказа покупателя""" + ОшибкаСтрокаЗаказа + Символы.ПС;
		КонецПопытки;
	КонецЕсли;
	
	Если ДокЗаказ <> Неопределено И ДокЗаказ.Товары.Количество()=0 Тогда
		ДокЗаказ = Неопределено;
	КонецЕсли;
	Если ДокЗаказ <> Неопределено Тогда
		Попытка 
			ДокЗаказ.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Исключение 
			Ошибки = Ошибки + "Ошибка: не удалось записать документ ""Заказ покупателя""" + ОшибкаСтрокаЗаказа + Символы.ПС;
		КонецПопытки;
	КонецЕсли;
	
	Если Ошибки<>"" Тогда
		ОтменитьТранзакцию();	
		#Если Клиент Тогда
			Предупреждение(Ошибки);
		#КонецЕсли
	Иначе
		Если ДокСнятиеРезервов <> Неопределено Тогда
			Сообщить("Сформирован и проведен документ <"+СокрЛП(ДокСнятиеРезервов)+">.");
		КонецЕсли;
		Если ДокКорректировка <> Неопределено Тогда
			Сообщить("Сформирован и проведен документ <"+СокрЛП(ДокКорректировка)+">.");		
		КонецЕсли;
		Если ДокЗаказ <> Неопределено Тогда
			Сообщить("Сформирован и проведен документ <"+СокрЛП(ДокЗаказ)+">.");
		КонецЕсли;
		ЗафиксироватьТранзакцию();	
	КонецЕсли;
	
КонецПроцедуры //ЗаказПокупателяОтдатьЗаказ()

// Функция возвращает идентификатор группы аналогов
Функция НайтиИдентификаторГруппыАналогов(Знач Номенклатура, Знач АртикулДляПоиска=Неопределено, Знач Производитель=Неопределено, ИдентификаторНомераДетали=Неопределено) Экспорт
	
	Возврат атСервер.НайтиИдентификаторГруппыАналогов(Номенклатура, АртикулДляПоиска, Производитель, ИдентификаторНомераДетали);
	
КонецФункции // НайтиИдентификаторГруппыАналогов() 


// Определение, если ли хоть одна пометка в списке
//
// Параметры
//  Список  – СписокЗначений - список, который проверяем
// Возвращаемое значение:
//   Булево – флаг, есть ли пометка
//
Функция ЕстьПометка(Список) Экспорт
	ЕстьПометка = Ложь;
	Для Каждого Значение Из Список Цикл
		Если Значение.Пометка Тогда
			ЕстьПометка = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат ЕстьПометка;
КонецФункции // ЕстьПометка()

Функция ВСтрокеТолькоЦифры(СтрокаПоиска)
	ТолькоЦифрыНомера = "";
	Для а = 1 По СтрДлина(СтрокаПоиска) Цикл
		Симв = Сред(СтрокаПоиска, а, 1);
		Если Найти("1234567890", Симв) > 0 Тогда
			ТолькоЦифрыНомера = ТолькоЦифрыНомера + Сред(СтрокаПоиска,а,1);
		ИначеЕсли Найти("(+- )", Симв) > 0 Тогда
			Продолжить;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат ТолькоЦифрыНомера;
КонецФункции

// Выполнение поиска и формирование таблицы найденных ссылок
//
//
// Параметры:
//  ТаблицаРезультата - ТаблицаЗначение - таблица результата поиска,
//  ВыполнятьПроверки - Булево - определяет выполнять проверками на указание параметров поиска. 
//
Процедура ВыполнитьПоиск(ТаблицаРезультата, ВыполнятьПроверки = Истина) Экспорт 
	
	стСтрокаПоиска = СокрЛП(ПараметрыПоиска.СтрокаПоиска);
	
	Если ВыполнятьПроверки Тогда
		ТаблицаРезультата.Очистить();
		
		Если ПустаяСтрока(стСтрокаПоиска) Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ЕстьПометка(ПараметрыПоиска.СписокПолейПоиска) Тогда
			#Если Клиент Тогда
				Предупреждение("Не выбран ни один вариант поиска! Поиск невозможен.",, "Внимание!");
			#ИначеЕсли Сервер Тогда
				Сообщить("Не выбран ни один вариант поиска! Поиск невозможен.", СтатусСообщения.Внимание);
			#КонецЕсли
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТолькоЦифры = ВСтрокеТолькоЦифры(стСтрокаПоиска);
	ИскатьПоТелефону = Ложь;
	ИскатьПоИНН = Ложь;
	
	Если ТолькоЦифры <> Неопределено И СтрДлина(ТолькоЦифры) > 2 Тогда
		ИскатьПоТелефону = Истина;
		Если ТолькоЦифры = стСтрокаПоиска Тогда
			ИскатьПоИНН = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ИскатьАвтомобиль    = ПараметрыПоиска.СписокПолейПоиска.НайтиПоЗначению("Автомобиль").Пометка;
	ИскатьКонтрагента   = ПараметрыПоиска.СписокПолейПоиска.НайтиПоЗначению("Контрагент").Пометка;
	ИскатьПоТелефону    = ИскатьПоТелефону И ИскатьКонтрагента;
	ИскатьПоИНН         = ИскатьПоИНН И ИскатьКонтрагента;
	ИскатьВКомментариях = ПараметрыПоиска.СписокПолейПоиска.НайтиПоЗначению("ИскатьВКомментариях").Пометка;
	
	СписокПолейПоискаВКИ = Новый СписокЗначений;
	Для ин = 2 По 4 Цикл
		Элемент = ПараметрыПоиска.СписокПолейПоиска.Получить(ин);
		Если Элемент.Пометка Тогда
			СписокПолейПоискаВКИ.Добавить(Элемент.Значение,, Элемент.Пометка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("стСтрокаПоиска", "%"+СтрЗаменить(стСтрокаПоиска, """", """""")+"%");
	
	ТекстЗапроса = "";
	ТекстПоместить = "ПОМЕСТИТЬ втКонтрагенты ";
	ПоискКлиента = Ложь;
	
	Если ИскатьКонтрагента ИЛИ ИскатьВКомментариях Тогда
		ПоискКлиента = Истина;
		// Поиск по наименованию
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Контрагенты.Ссылка КАК Контрагент
		|" + ТекстПоместить + "
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.ЭтоГруппа
		|	И (ЛОЖЬ
		|";
		Если ИскатьКонтрагента Тогда
			ТекстЗапроса = ТекстЗапроса +
			"	ИЛИ Контрагенты.Наименование ПОДОБНО &стСтрокаПоиска
			|	ИЛИ Контрагенты.НаименованиеПолное ПОДОБНО &стСтрокаПоиска
			|";
		КонецЕсли;
		Если ИскатьВКомментариях Тогда
			ТекстЗапроса = ТекстЗапроса +
			"	ИЛИ Контрагенты.Комментарий ПОДОБНО &стСтрокаПоиска
			|";
		КонецЕсли;
		Если ИскатьПоИНН Тогда
			ТекстЗапроса = ТекстЗапроса +
			"	ИЛИ Контрагенты.ИНН ПОДОБНО &стСтрокаПоиска
			|";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ")
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеЛисты.Клиент КАК Контрагент
		|ИЗ
		|	РегистрСведений.атРабочиеЛистыИстория КАК РабочиеЛисты
		|ГДЕ
		|	ВЫРАЗИТЬ(РабочиеЛисты.Клиент КАК СТРОКА(100)) ПОДОБНО &стСтрокаПоиска
		|";
		
		ТекстПоместить = "";
		
	КонецЕсли;
	
	Если ИскатьПоТелефону Тогда
		ПоискКлиента = Истина;
		Запрос.УстановитьПараметр("НомерТелефона", "%" + ТолькоЦифры + "%");
		Если ТекстЗапроса <> "" Тогда
			ТекстЗапроса = ТекстЗапроса + 
			"
			|ОБЪЕДИНИТЬ
			|";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫРАЗИТЬ(сфпНомераТелефонов.Объект КАК Справочник.Контрагенты).Ссылка КАК Контрагент
		|" + ТекстПоместить + "
		|ИЗ
		|	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефонов
		|ГДЕ
		|	сфпНомераТелефонов.Объект ССЫЛКА Справочник.Контрагенты
		|	И сфпНомераТелефонов.НомерТелефона ПОДОБНО &НомерТелефона
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеЛисты.Клиент КАК Контрагент
		|ИЗ
		|	РегистрСведений.атРабочиеЛистыИстория КАК РабочиеЛисты
		|ГДЕ
		|	РабочиеЛисты.Телефон ПОДОБНО &стСтрокаПоиска
		|";
		
		ТекстПоместить = "";
	КонецЕсли;
	
	Если СписокПолейПоискаВКИ.Количество() > 0 Тогда 
		ПоискКлиента = Истина;
		// Поиск в контактной информации
		Если ТекстЗапроса <> "" Тогда
			ТекстЗапроса = ТекстЗапроса + 
			"
			|ОБЪЕДИНИТЬ
			|";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КонтактнаяИнформация.Объект КАК Контрагент
		|" + ТекстПоместить + "
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Тип.Ссылка В(&СписокТипов)
		|	И (КонтактнаяИнформация.Объект ССЫЛКА Справочник.Контрагенты)
		|	И (КонтактнаяИнформация.Представление ПОДОБНО &стСтрокаПоиска
		|";
		Если ИскатьВКомментариях Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ИЛИ
			|	КонтактнаяИнформация.Комментарий ПОДОБНО &стСтрокаПоиска
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ")";
		ТекстПоместить = "";
		
		СписокТипов = Новый СписокЗначений;
		Для Каждого Значение Из СписокПолейПоискаВКИ Цикл
			Если Значение.Пометка Тогда
				СписокТипов.Добавить(Значение.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Запрос.УстановитьПараметр("СписокТипов", СписокТипов);
	КонецЕсли;
	
	Если ИскатьАвтомобиль Тогда
		
		ТекстЗапроса = ТекстЗапроса + ?(ПоискКлиента, "
		|;
		|", "")+"
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникАвтомобили.Ссылка КАК Автомобиль,
		|	СправочникАвтомобили.Модель КАК Модель
		|ПОМЕСТИТЬ втАвтомобили
		|ИЗ
		|	Справочник.Автомобили КАК СправочникАвтомобили
		|ГДЕ
		|	(СправочникАвтомобили.VIN ПОДОБНО &стСтрокаПоиска
		|			ИЛИ СправочникАвтомобили.НомерКузова ПОДОБНО &стСтрокаПоиска
		|			ИЛИ СправочникАвтомобили.ОригинальныйVIN ПОДОБНО &стСтрокаПоиска
		|			ИЛИ СправочникАвтомобили.Наименование ПОДОБНО &стСтрокаПоиска)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеЛисты.Автомобиль,
		|	РабочиеЛисты.Модель
		|ИЗ
		|	РегистрСведений.атРабочиеЛистыИстория КАК РабочиеЛисты
		|ГДЕ
		|	ВЫРАЗИТЬ(РабочиеЛисты.Автомобиль КАК СТРОКА(100)) ПОДОБНО &стСтрокаПоиска
		|";
		
	КонецЕсли;
	
	Если ПоискКлиента ИЛИ ИскатьАвтомобиль Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|" + ?(ПоискКлиента, "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Контрагенты.Контрагент КАК Контрагент,
		|	ЕСТЬNULL(Автомобили.Автомобиль, """") КАК Автомобиль,
		|	ЕСТЬNULL(Автомобили.Автомобиль.Модель, ЗНАЧЕНИЕ(Справочник.Модели.ПустаяСсылка)) КАК Модель
		|ПОМЕСТИТЬ втКонтрагентыАвтомобили
		|ИЗ
		|	втКонтрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)) КАК Автомобили
		|		ПО ВЫРАЗИТЬ(Контрагенты.Контрагент КАК Справочник.Контрагенты).Ссылка = ВЫРАЗИТЬ(Автомобили.Значение КАК Справочник.Контрагенты).Ссылка
		//|ГДЕ
		//|	Контрагенты.Контрагент ССЫЛКА Справочник.Контрагенты
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Контрагенты.Контрагент,
		|	ВЫБОР КОГДА РабочиеЛисты.Автомобиль = НЕОПРЕДЕЛЕНО ТОГДА """" ИНАЧЕ РабочиеЛисты.Автомобиль КОНЕЦ,
		|	РабочиеЛисты.Модель
		|ИЗ
		|	втКонтрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.атРабочиеЛистыИстория КАК РабочиеЛисты
		|		ПО Контрагенты.Контрагент = РабочиеЛисты.Клиент
		|", "") + "
		|" + ?(ПоискКлиента И ИскатьАвтомобиль, "ОБЪЕДИНИТЬ", "") + "
		|" + ?(ИскатьАвтомобиль, "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ВЫРАЗИТЬ(Контрагенты.Значение КАК Справочник.Контрагенты).Ссылка, """") КАК Контрагент,
		|	Автомобили.Автомобиль КАК Автомобиль,
		|	Автомобили.Модель КАК Модель
		|" + ?(ПоискКлиента, "", "ПОМЕСТИТЬ втКонтрагентыАвтомобили") + "
		|ИЗ
		|	втАвтомобили КАК Автомобили
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)) КАК Контрагенты
		|		ПО Автомобили.Автомобиль = Контрагенты.Автомобиль
		|ГДЕ Автомобили.Автомобиль ССЫЛКА Справочник.Автомобили
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеЛисты.Клиент КАК Контрагент,
		|	Автомобили.Автомобиль КАК Автомобиль,
		|	Автомобили.Модель КАК Модель
		|ИЗ
		|	втАвтомобили КАК Автомобили
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.атРабочиеЛистыИстория КАК РабочиеЛисты
		|		ПО Автомобили.Автомобиль = РабочиеЛисты.Автомобиль
		|", "") + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КонтрагентыАвтомобили.Контрагент КАК Контрагент,
		|	КонтрагентыАвтомобили.Автомобиль КАК Автомобиль,
		|	КонтрагентыАвтомобили.Модель КАК Модель,
		|	МАКСИМУМ(ЕСТЬNULL(АвтомобилиСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК Период,
		|	МАКСИМУМ(История.Телефон) КАК Телефон,
		|	МАКСИМУМ(ВЫБОР КОГДА РЛ_ВариантыПоставки.Автор ЕСТЬ NULL ТОГДА Неопределено ИНАЧЕ Истина КОНЕЦ) КАК РЛ_К_ВП,
		|	МАКСИМУМ(ВЫБОР КОГДА РЛ_Клиент.Автор ЕСТЬ NULL ТОГДА Неопределено ИНАЧЕ Истина КОНЕЦ) КАК РЛ_К,
		|	МАКСИМУМ(ВЫБОР КОГДА РЛ_Автомобиль.Автор ЕСТЬ NULL ТОГДА Неопределено ИНАЧЕ Истина КОНЕЦ) КАК РЛ_А,
		|	МАКСИМУМ(ВЫБОР КОГДА РЛ_Модель.Автор ЕСТЬ NULL ТОГДА Неопределено ИНАЧЕ Истина КОНЕЦ) КАК РЛ_М
		|ПОМЕСТИТЬ втКонтрагентыАвтомобилиГосНомера
		|ИЗ
		|	втКонтрагентыАвтомобили КАК КонтрагентыАвтомобили
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили КАК АвтомобилиСрезПоследних
		|		ПО КонтрагентыАвтомобили.Автомобиль = АвтомобилиСрезПоследних.Автомобиль
		|			И (АвтомобилиСрезПоследних.ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атРабочиеЛистыИстория КАК История
		|		ПО КонтрагентыАвтомобили.Контрагент = История.Клиент
		|			И КонтрагентыАвтомобили.Автомобиль = История.Автомобиль
		|			И КонтрагентыАвтомобили.Модель = История.Модель
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атРабочиеЛисты КАК РЛ_ВариантыПоставки
		|		ПО НЕ РЛ_ВариантыПоставки.Запросы
		|			И РЛ_ВариантыПоставки.ВариантыПоставки
		|			И (""Клиент: "" + ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК СТРОКА(100)) = ВЫРАЗИТЬ(РЛ_ВариантыПоставки.Объект КАК СТРОКА(100))
		|			ИЛИ ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).Ссылка = ВЫРАЗИТЬ(РЛ_ВариантыПоставки.Объект КАК Справочник.Контрагенты).Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атРабочиеЛисты КАК РЛ_Клиент
		|		ПО НЕ РЛ_Клиент.Запросы
		|			И НЕ РЛ_Клиент.ВариантыПоставки
		|			И РЛ_Клиент.ВидОбъекта = ""Клиент""
		|			И (""Клиент: "" + ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК СТРОКА(100)) = ВЫРАЗИТЬ(РЛ_Клиент.Объект КАК СТРОКА(100))
		|			ИЛИ ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).Ссылка = ВЫРАЗИТЬ(РЛ_Клиент.Объект КАК Справочник.Контрагенты).Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атРабочиеЛисты КАК РЛ_Автомобиль
		|		ПО НЕ РЛ_Автомобиль.Запросы
		|			И НЕ РЛ_Автомобиль.ВариантыПоставки
		|			И РЛ_Автомобиль.ВидОбъекта = ""Автомобиль""
		|			И (""Автомобиль: "" + ВЫРАЗИТЬ(КонтрагентыАвтомобили.Автомобиль КАК СТРОКА(100)) = ВЫРАЗИТЬ(РЛ_Автомобиль.Объект КАК СТРОКА(100))
		|			ИЛИ ВЫРАЗИТЬ(КонтрагентыАвтомобили.Автомобиль КАК Справочник.Автомобили).Ссылка = ВЫРАЗИТЬ(РЛ_Автомобиль.Объект КАК Справочник.Автомобили).Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атРабочиеЛисты КАК РЛ_Модель
		|		ПО  НЕ РЛ_Модель.Запросы
		|			И НЕ РЛ_Модель.ВариантыПоставки
		|			И РЛ_Модель.ВидОбъекта = ""Модель""
		|			И КонтрагентыАвтомобили.Модель = РЛ_Модель.Объект
		|
		|СГРУППИРОВАТЬ ПО
		|	КонтрагентыАвтомобили.Контрагент,
		|	КонтрагентыАвтомобили.Автомобиль,
		|	КонтрагентыАвтомобили.Модель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втКонтрагенты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втКонтрагентыАвтомобили
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Контрагент ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).Ссылка
		|		ИНАЧЕ ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК СТРОКА(100))
		|	КОНЕЦ КАК Контрагент,
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Контрагент ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент.НаименованиеПолное КАК СТРОКА(255))
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ПолноеНаименование,
		|	ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).ФормаСобственности КАК ТипКлиента,
		|	ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).ВидКонтрагента КАК ВидКонтрагента,
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Автомобиль ССЫЛКА Справочник.Автомобили
		|			ТОГДА КонтрагентыАвтомобили.Автомобиль
		|		ИНАЧЕ ВЫРАЗИТЬ(КонтрагентыАвтомобили.Автомобиль КАК СТРОКА(100))
		|	КОНЕЦ КАК Автомобиль,
		|	КонтрагентыАвтомобили.Модель КАК Модель,
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Автомобиль ССЫЛКА Справочник.Автомобили
		|			ТОГДА ВЫРАЗИТЬ(КонтрагентыАвтомобили.Автомобиль КАК Справочник.Автомобили).VIN
		|		ИНАЧЕ КонтрагентыАвтомобили.Автомобиль
		|	КОНЕЦ КАК VIN,
		|	ТаблицаГосНомер.Значение КАК ГосНомер,
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Контрагент ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).ИНН
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ИНН,
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Контрагент ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).ОсновнойТелефон
		|		ИНАЧЕ ЕСТЬNULL(КонтрагентыАвтомобили.Телефон, """")
		|	КОНЕЦ КАК ОсновнойТелефон,
		|	КонтрагентыАвтомобили.РЛ_К_ВП КАК ЕстьВариантыПоставки,
		|	КонтрагентыАвтомобили.РЛ_К КАК ЕстьРабочийЛистКлиент,
		|	КонтрагентыАвтомобили.РЛ_А КАК ЕстьРабочийЛистАвтомобиль,
		|	КонтрагентыАвтомобили.РЛ_М КАК ЕстьРабочийЛистМодель
		|ИЗ
		|	втКонтрагентыАвтомобилиГосНомера КАК КонтрагентыАвтомобили
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили КАК ТаблицаГосНомер
		|		ПО (ТаблицаГосНомер.ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер))
		|			И КонтрагентыАвтомобили.Автомобиль = ТаблицаГосНомер.Автомобиль
		|			И КонтрагентыАвтомобили.Период = ТаблицаГосНомер.Период
		|
		|ГДЕ НЕ (КонтрагентыАвтомобили.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|	ИЛИ КонтрагентыАвтомобили.Контрагент = """")
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Контрагент ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК Справочник.Контрагенты).Наименование
		|		ИНАЧЕ ВЫРАЗИТЬ(КонтрагентыАвтомобили.Контрагент КАК СТРОКА(100))
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА КонтрагентыАвтомобили.Автомобиль ССЫЛКА Справочник.Автомобили
		|			ТОГДА КонтрагентыАвтомобили.Автомобиль.VIN
		|		ИНАЧЕ ВЫРАЗИТЬ(КонтрагентыАвтомобили.Автомобиль КАК СТРОКА(100))
		|	КОНЕЦ,
		|	КонтрагентыАвтомобили.Модель.Наименование
		|ИТОГИ ПО
		|	Контрагент
		|";
		
		Запрос.Текст = ТекстЗапроса;
		
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаРезультата = РезультатЗапроса.Выгрузить().СкопироватьКолонки();
		
		ВыборкаКонтрагентов = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагентов.Следующий() Цикл
			Выборка = ВыборкаКонтрагентов.Выбрать();
			НеДобавлятьПустойАвтомобиль = Выборка.Количество() > 1;
			Пока Выборка.Следующий() Цикл
				Если НеДобавлятьПустойАвтомобиль И обЗначениеНеЗаполнено(Выборка.Автомобиль) И обЗначениеНеЗаполнено(Выборка.Модель) Тогда
					НеДобавлятьПустойАвтомобиль = Ложь;
					Продолжить;
				КонецЕсли;
				НоваяСтрока = ТаблицаРезультата.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьПоиск()
 
////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

СписокПолейПоискаПоУмолчанию = Новый СписокЗначений;
СписокПолейПоискаПоУмолчанию.Добавить("Автомобиль", "Автомобиль (Наименование, VIN)", Истина); // + Оригинальный VIN, + Номер кузова
СписокПолейПоискаПоУмолчанию.Добавить("Контрагент", "Клиент (Наименование, ИНН, Телефон)", Истина);
//СписокПолейПоискаПоУмолчанию.Добавить("ИНН", "ИНН клиента", Истина);
//СписокПолейПоискаПоУмолчанию.Добавить("Телефон", "Телефоны клиента", Истина);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.Адрес, "Клиент (Адрес)", Ложь);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, "Клиент (Электронная почта)", Ложь);
СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.ВебСтраница, "Клиент (Web сайт)", Ложь);
//СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.НомерICQ, "Клиент (ICQ номер)", Ложь);
//СписокПолейПоискаПоУмолчанию.Добавить(Перечисления.ТипыКонтактнойИнформации.Другое, "Клиент (Произвольная информация)", Ложь);
СписокПолейПоискаПоУмолчанию.Добавить("ИскатьВКомментариях", "Комментарии", Ложь);

ХозОперация = Справочники.ХозОперации.РеализацияТоваров;
Ссылка = Неопределено;
ДополнительныеСвойства = Новый Структура;

ОбработкаРасчетСкидок=Обработки.РасчетСкидок.Создать();
ОбработкаРасчетСкидок.ИмяРеквизитаСкладКомпании="СкладКомпании";

Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


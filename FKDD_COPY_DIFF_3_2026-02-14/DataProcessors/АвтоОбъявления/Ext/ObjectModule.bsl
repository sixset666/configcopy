////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;				          // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

Перем АвтоклаудРезультатВыгрузки Экспорт;     // Таблица, в которой собираются результаты всех операций выгрузки
Перем АвтоклаудПоследнийВызовСервиса Экспорт; // Дата и время последнего вызова сервиса
Перем АвтоклаудИдентификатор Экспорт;         // Идентификатор сеанса - резльутат авторизации

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПреобразоватьДату(ЗначениеДата, НужныСекунды = Ложь) Экспорт
	
	Год    = Формат(Год(ЗначениеДата),    "ЧГ=0");
	Месяц  = Формат(Месяц(ЗначениеДата),  "ЧЦ=2; ЧН=; ЧВН=");
	День   = Формат(День(ЗначениеДата),   "ЧЦ=2; ЧН=; ЧВН=");
	Час    = Формат(Час(ЗначениеДата),    "ЧЦ=2; ЧН=; ЧВН=");
	Минута = Формат(Минута(ЗначениеДата), "ЧЦ=2; ЧН=; ЧВН="); 	
	
	Если НужныСекунды Тогда
		Секунда = Формат(Секунда(ЗначениеДата), "ЧЦ=2; ЧН=; ЧВН="); 	
		
		Возврат ""+Год+Месяц+День+"T"+Час+Минута+Секунда+"0000";
	Иначе
		Возврат ""+Год+Месяц+День+"T"+Час+Минута;	
	КонецЕсли;	                                    	
	
КонецФункции // ПреобразоватьДату()	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ДЕРЕВОМ ДАННЫХ

// Формирует дерево данных по переданному макету сервиса
Функция ДеревоОбъявления(Макет) Экспорт
	
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	ШиринаТаблицы = Макет.ШиринаТаблицы;
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	Для НомерКолонки = 1 По ШиринаТаблицы Цикл
		ОбластьШапки = Макет.Область(1,НомерКолонки);
		НазваниеКолонки = ОбластьШапки.Текст;
		ТаблицаЗначений.Колонки.Добавить(НазваниеКолонки);
	КонецЦикла;
	
	Для НомерСтроки = 2 По ВысотаТаблицы Цикл
		НоваяСтрока = ТаблицаЗначений.Добавить();
		Для НомерКолонки = 0 По ШиринаТаблицы-1 Цикл
			НоваяСтрока.Установить(НомерКолонки, Макет.Область(НомерСтроки, НомерКолонки + 1).Текст);
		КонецЦикла
	КонецЦикла;
	ТаблицаЗначений.Колонки.Добавить("ПолныйПуть");
	ТаблицаЗначений.Колонки.Сдвинуть("ПолныйПуть", -ШиринаТаблицы);
	КолУровней = 0;
	
	ДеревоЗначений = Новый ДеревоЗначений;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		ДеревоЗначений.Колонки.Добавить(Колонка.Имя, Новый ОписаниеТипов("Строка"));
		Если Найти(Колонка.Имя, "Уровень") > 0 Тогда
			НомерУровня = Число(Сред(Колонка.Имя, 8, 2));
			Если НомерУровня > КолУровней Тогда
				КолУровней = НомерУровня;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПолныйПуть = "";
	РекурсивноЗаполнитьСтрокиДерева(ДеревоЗначений, 1, КолУровней, ПолныйПуть, ТаблицаЗначений, 0);
	
	Возврат ДеревоЗначений;
	
КонецФункции // ДеревоОбъявления()

// Рекурсивно заполняет дерево строками по макету сервиса
Процедура РекурсивноЗаполнитьСтрокиДерева(ДеревоЗначений, Знач НомерУровня, КолУровней, Знач ПолныйПутьВДереве, ТЗ, НомерСтрокиТЗ)
	
	ЛокПолныйПуть = ПолныйПутьВДереве;
	СтрокаТекУровня = Неопределено;
	Пока НомерСтрокиТЗ < ТЗ.Количество() Цикл
		СтрокаТЗ = ТЗ[НомерСтрокиТЗ];
		Для Сч = НомерУровня По КолУровней Цикл
			ИдТекУровня = "Уровень" + Сч;
			Если ТЗ.Колонки.Найти(ИдТекУровня) <> Неопределено И ЗначениеЗаполнено(СтрокаТЗ[ИдТекУровня]) Тогда
				Если НомерУровня < Сч Тогда
					РекурсивноЗаполнитьСтрокиДерева(СтрокаТекУровня, Сч, КолУровней, ЛокПолныйПуть, ТЗ, НомерСтрокиТЗ);
				ИначеЕсли НомерУровня = Сч Тогда
					СтрокаТекУровня = ДеревоЗначений.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТекУровня, СтрокаТЗ);
					ЛокПолныйПуть = ?(ПолныйПутьВДереве = "", "", ПолныйПутьВДереве + ".") + СтрокаТекУровня["Уровень" + Сч];
					СтрокаТекУровня.ПолныйПуть = ЛокПолныйПуть;
				КонецЕсли;
				НомерСтрокиТЗ = НомерСтрокиТЗ + 1;
				Прервать;
			КонецЕсли;
			Если Сч >= КолУровней Тогда
				НомерСтрокиТЗ = НомерСтрокиТЗ - 1;
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // РекурсивноЗаполнитьСтрокиДерева()

// Вставляет значение в имеющуюся строку дерева по полному пути
Процедура ВставитьЗначениеВДерево(ДеревоДанных, ИмяРеквизита, ЗначениеРеквизита)
	
	НовСтрока = ДеревоДанных.Строки.Найти(ИмяРеквизита, "ПолныйПуть", Истина);
	Если НовСтрока = Неопределено Тогда
		НовСтрока = ДеревоДанных.Строки.Добавить();
		НомерУровня = СтрЧислоВхождений(ИмяРеквизита, ".") + 1;
		НовСтрока.ПолныйПуть = ИмяРеквизита;
		НовСтрока["Уровень" + НомерУровня] = НазваниеКолонки(ИмяРеквизита);
	КонецЕсли;
	НовСтрока.Значение = ЗначениеРеквизита;

КонецПроцедуры // ВставитьЗначениеВДерево()

// Добавляет значение в новую строку дерева 
Процедура ДобавитьЗначениеВДерево(ДеревоДанных, ИмяРеквизита, ЗначениеРеквизита)
	
	НовСтрока = ДеревоДанных.Строки.Добавить();
	НомерУровня = СтрЧислоВхождений(ИмяРеквизита, ".") + 1;
	НовСтрока.ПолныйПуть = ИмяРеквизита;
	НовСтрока["Уровень" + НомерУровня] = НазваниеКолонки(ИмяРеквизита);
	НовСтрока.Значение = ЗначениеРеквизита;

КонецПроцедуры // ДобавитьЗначениеВДерево()

Функция НазваниеКолонки(ПолныйПуть) Экспорт
	
	Перем ТекущаяПозиция;
	
	МассивСтрок = Новый Массив;
	
	НачальнаяПозиция = 1;
	
	Для ТекущаяПозиция = 1 По СтрДлина(ПолныйПуть) Цикл
		ТекущийСимвол = Сред(ПолныйПуть, ТекущаяПозиция, 1);
		Если ТекущийСимвол = "." Или ТекущийСимвол = "/" Или ТекущийСимвол = "\" Тогда
			ТекущийФрагмент = Сред(ПолныйПуть, НачальнаяПозиция, ТекущаяПозиция - НачальнаяПозиция);
			НачальнаяПозиция = ТекущаяПозиция + 1;
			МассивСтрок.Добавить(ТекущийФрагмент);
		КонецЕсли;
	КонецЦикла;
	
	Если НачальнаяПозиция <> ТекущаяПозиция Тогда
		ТекущийФрагмент = Сред(ПолныйПуть, НачальнаяПозиция, ТекущаяПозиция - НачальнаяПозиция);
		МассивСтрок.Добавить(ТекущийФрагмент);
	КонецЕсли;

	Возврат МассивСтрок[МассивСтрок.Количество()-1];
	
КонецФункции // НазваниеКолонки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С XML, XDTO, CML

Функция ПолучитьТипЗначенияCML(Тип, ВерсияСхемы) Экспорт
	
	Попытка		
		ТипЗначения = ФабрикаXDTO.Тип(ВерсияСхемы, Тип);
	Исключение		
		ТипЗначения = Неопределено;
	КонецПопытки;
	
	Возврат ТипЗначения;
	
КонецФункции // ПолучитьТипЗначенияCML()

Функция ПолучитьТипОбъектаCML(Тип, ВерсияСхемы)
	
	МассивПути = СтрокаРазделить(Тип, ".");
	
	ПервыйЭлемент = МассивПути[0];
	Если Лев(ПервыйЭлемент,1) = "{" И Прав(ПервыйЭлемент,1) = "}" Тогда
		ИмяПакета = Сред(ПервыйЭлемент, 2, СтрДлина(ПервыйЭлемент) - 2);
		Коллекция = ФабрикаXDTO.Пакеты.Получить(ИмяПакета).КорневыеСвойства;
	Иначе
		ТипОбъекта = ФабрикаXDTO.Тип(ВерсияСхемы, ПервыйЭлемент);
		Коллекция = ТипОбъекта.Свойства;
	КонецЕсли;
	
	МассивПути.Удалить(0);
	Пока МассивПути.Количество() > 0 Цикл
		
		Если Коллекция = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Свойство = Коллекция.Получить(МассивПути[0]);
		Если Свойство = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ТипОбъекта = Свойство.Тип;
		МассивПути.Удалить(0);
		Попытка
			Коллекция = ТипОбъекта.Свойства;
		Исключение
			Коллекция = Неопределено;
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ТипОбъекта;
	
КонецФункции // ПолучитьТипОбъектаCML()

Функция СтрокаРазделить(Знач Строка, Разделитель)
	
	Результат = Новый Массив;
	Если ПустаяСтрока(Строка) Тогда
		Возврат Результат;
	КонецЕсли;
	
	НачПозицияПервогоЭлемента = Найти(Строка, "{");
	КонПозицияПервогоЭлемента = Найти(Строка, "}");
	Если НачПозицияПервогоЭлемента > 0 И КонПозицияПервогоЭлемента > 0 Тогда
		ПервыйЭлемент = Сред(Строка, НачПозицияПервогоЭлемента, КонПозицияПервогоЭлемента);
		Результат.Добавить(СокрЛП(ПервыйЭлемент));
		Строка = СокрЛП(Сред(Строка,КонПозицияПервогоЭлемента + 2));
	КонецЕсли;
	
	Пока Истина Цикл
		Позиция = Найти(Строка,Разделитель);
		Если Позиция = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Результат.Добавить(СокрЛП(Лев(Строка,Позиция - 1)));
		Строка = СокрЛП(Сред(Строка,Позиция + 1));
	КонецЦикла;
	
	Результат.Добавить(СокрЛП(Строка));
	
	Возврат Результат;
	
КонецФункции // СтрокаРазделить() 

Функция ПолучитьОбъектТипаCML(Тип, ВерсияСхемы)
	
	Если ТипЗнч(Тип) = Тип("Строка") Тогда
		ТипОбъекта = ПолучитьТипОбъектаCML(Тип, ВерсияСхемы);
	Иначе
		ТипОбъекта = Тип;
	КонецЕсли;
	
	Если ТипОбъекта = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйОбъект = ФабрикаXDTO.Создать(ТипОбъекта);
	
	Возврат НовыйОбъект;
	
КонецФункции // ПолучитьОбъектТипаCML()

Процедура УстановитьЗначениеXDTO(ОбъектXDTO, ИмяСвойства, Значение, ТекстОшибки)
	
	Попытка
		ОбъектXDTO.Установить(ИмяСвойства, Значение);
	Исключение
		ШаблонСообщения = НСтр("ru = 'Ошибка установки значения свойства ""%1""!'");;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("Авто-объявление Автоклауд",УровеньЖурналаРегистрации.Ошибка,,"Ошибка", "Заполнение XDTO "+ТекстОшибки + "Имя свойства " + ИмяСвойства + " Значение " + Значение);
	КонецПопытки
	
КонецПроцедуры // УстановитьЗначениеXDTO()

// Заполняет свойство объекта XDTO.
//
// Параметры
//  ОбъектXDTO   – объект заполнения,
//  ИмяСвойства  - свойство объекта,
//  Значение     - устанавливаемое значение,
//  Обязательное - булево - признак обязательности заполнения свойства,
//  ТекстОшибки  - строка - текст ошибки в случае неудачного заполнения
//
Процедура ЗаполнитьСвойствоXDTO(ОбъектXDTO, ИмяСвойства, Значение, Обязательное = Ложь, ТекстОшибки = "", УстанавливатьПустыеЗначения = Ложь) Экспорт
	
	Если ТипЗнч(Значение) = Тип("ЗначениеXDTO") ИЛИ ТипЗнч(Значение) = Тип("ОбъектXDTO") Тогда
		УстановитьЗначениеXDTO(ОбъектXDTO, ИмяСвойства, Значение, ТекстОшибки);
	Иначе
		Если Обязательное ИЛИ ЗначениеЗаполнено(Значение) ИЛИ УстанавливатьПустыеЗначения Тогда
			УстановитьЗначениеXDTO(ОбъектXDTO, ИмяСвойства, Значение, ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСвойствоXDTO()

Функция ПрочитатьXMLВXDTO(СтрокаXML, ТипЗначенияXDTO) Экспорт
	
	ОбъектXML = Новый ЧтениеXML;	
	ОбъектXML.УстановитьСтроку(СтрокаXML);

	Возврат ФабрикаXDTO.ПрочитатьXML(ОбъектXML, ТипЗначенияXDTO); 
	
КонецФункции // ПрочитатьXMLВXDTO()	

////////////////////////////////////////////////////////////////////////////////
// ВСЕ СЕРВИСЫ

Процедура СоздатьОбъявления(ПарметрыСоздания) Экспорт
	
КонецПроцедуры	

Процедура ЗаполнитьАвтоОбъявления() Экспорт
	
	АвтоОбъявления.Очистить();  	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сервис",    Сервис);
	Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияАвтоОбъявлений.ОжидаетОтправки);
	Запрос.Текст = "ВЫБРАТЬ
	|	ИСТИНА КАК Использование,
	|	АвтоОбъявление.Ссылка КАК Автообъявление
	|ИЗ
	|	Документ.АвтоОбъявление КАК АвтоОбъявление
	|ГДЕ
	|	НЕ АвтоОбъявление.ПометкаУдаления
	|	И АвтоОбъявление.Сервис = &Сервис
	|	И АвтоОбъявление.Состояние = &Состояние";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		АвтоОбъявления.Загрузить(РезультатЗапроса.Выгрузить());
	КонецЕсли;	
	
КонецПроцедуры // ЗаполнитьАвтоОбъявления()

// Вызывает функции создания дерева данных объявления
// в зависимости от переданного сервиса
Функция ДеревоДанныхОбъявления(СервисОбъявления) Экспорт
	
	Если СервисОбъявления = Перечисления.СервисыАвтоОбъявлений.Автоклауд Тогда
		ДеревоДанных = АвтоклаудДеревоДанныхОбъявления();
	Иначе
		// Вставить вызовы для других сервисов
	КонецЕсли;	
	
	Возврат ДеревоДанных;
	
КонецФункции // СоздатьДеревоДанныхОбъявления()

#Если НЕ ВнешнееСоединение Тогда

// Заполняет переданное дерево данными переданного объявления
// в зависимости от переданного сервиса
Процедура ЗаполнитьДеревоДанныхОбъявления(СервисОбъявления, ДеревоДанных, ОбъявлениеОбъект) Экспорт

	Если СервисОбъявления = Перечисления.СервисыАвтоОбъявлений.Автоклауд Тогда
		АвтоклаудЗаполнитьДеревоДанныхОбъявления(ДеревоДанных, ОбъявлениеОбъект);
	Иначе
		// Вставить вызовы для других сервисов
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьДеревоДанныхОбъявления()

#КонецЕсли

Процедура ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, ПолныйПуть, ЗначениеАльфа, ЗначениеПоУмолчанию = "") Экспорт
	
	НайденнаяСтрокаДерева = ДеревоДанных.Строки.Найти(ПолныйПуть, "ПолныйПуть", Истина);
	Если Не НайденнаяСтрокаДерева = Неопределено Тогда
		
		Значение = АвтоклаудЗначениеРеквизитаСервиса(ПолныйПуть, ЗначениеАльфа);
		
		Если ЗначениеЗаполнено(Значение) Тогда
			НайденнаяСтрокаДерева.Значение = Значение;
		Иначе
			НайденнаяСтрокаДерева.Значение = ЗначениеПоУмолчанию;
		КонецЕсли;
		
		НайденнаяСтрокаДерева.ЗначениеАльфа = ЗначениеАльфа;		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьЗначениеВДеревеДанных()	

Функция ПолучитьЗначениеИзДереваДанных(ДеревоДанных, ПолныйПуть)
	
	НайденнаяСтрокаДерева = ДеревоДанных.Строки.Найти(ПолныйПуть, "ПолныйПуть", Истина);
	Если НайденнаяСтрокаДерева = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат НайденнаяСтрокаДерева.Значение;		
	КонецЕсли;
	
КонецФункции // ПолучитьЗначениеИзДереваДанных()

Функция СформироватьФайлОбъявления(СервисОбъявления, ДеревоДанных) Экспорт
	
	Если СервисОбъявления = Перечисления.СервисыАвтоОбъявлений.Автоклауд Тогда
		Возврат АвтоклаудСформироватьФайлОбъявления(ДеревоДанных);
	Иначе
		// Вставить вызовы для других сервисов
	КонецЕсли;
	
КонецФункции // СформироватьФайлОбъявления()	

Процедура ПрочитатьФайлОбъявления(СервисОбъявления, ДеревоДанных, ОбъявлениеXML) Экспорт
	
	Если СервисОбъявления = Перечисления.СервисыАвтоОбъявлений.Автоклауд Тогда
		АвтоклаудПрочитатьФайлОбъявления(ДеревоДанных, ОбъявлениеXML);
	Иначе
		// Вставить вызовы для других сервисов
	КонецЕсли;
	
КонецПроцедуры // ПрочитатьФайлОбъявления()	

Процедура ПоместитьФайлОбъявленияВХранилищеДокумента(ОбъявлениеXML, ОбъявлениеОбъект) Экспорт
	
	Сжатие                = Новый СжатиеДанных(9);	
	ОбъявлениеОбъект.Файл = Новый ХранилищеЗначения(ОбъявлениеXML, Сжатие)
	
КонецПроцедуры // ПоместитьФайлОбъявленияВХранилищеДокумента()

Функция ПолучитьФайлОбъявленияИзХранилищаДокумента(ОбъявлениеОбъект) Экспорт
	
	Возврат ОбъявлениеОбъект.Файл.Получить();	
	
КонецФункции // ПолучитьФайлОбъявленияИзХранилищаДокумента()

Процедура СохранитьФайлОбъявленияНаДиск(ОбъявлениеXML, ПолноеИмяФайла) Экспорт
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ОбъявлениеXML);
	
	Попытка 
		ТекстовыйДокумент.Записать(ПолноеИмяФайла, "UTF-8");	
	Исключение
		
	КонецПопытки;		
	
КонецПроцедуры // СохранитьФайлОбъявленияНаДиск()	

#Если НЕ ВнешнееСоединение Тогда

Процедура ВыгрузкаОбъявленийВСервис(ОбъявленияДляВыгрузки) Экспорт
	
	АвтоклаудРезультатВыгрузки.Очистить();
	
	Для Каждого ОбъявлениеДляВыгрузки Из ОбъявленияДляВыгрузки Цикл
		
		Если ОбъявлениеДляВыгрузки.Сервис = Перечисления.СервисыАвтоОбъявлений.Автоклауд Тогда
			АвтоклаудВыгрузкаОбъявленияВСервис(ОбъявлениеДляВыгрузки);
		Иначе
			// Вставить вызовы для других сервисов
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры // ВыгрузкаОбъявленийВСервис()	

#КонецЕсли

Процедура ДобавитьРезультатВыгрузки(АвтоОбъявление, ВидРезультата, Результат)
	
	НоваяСтрокаРезультата = АвтоклаудРезультатВыгрузки.Добавить();
	НоваяСтрокаРезультата.АвтоОбъявление = АвтоОбъявление;
	НоваяСтрокаРезультата.ВидРезультата  = ВидРезультата;
	НоваяСтрокаРезультата.Результат      = Результат;	
	
КонецПроцедуры // ДобавитьРезультатВыгрузки()	

Процедура УстановитьСостояниеОбъявления(АвтоОбъявление, Состояние)
	
	АвтоОбъявлениеОбъект = АвтоОбъявление.ПолучитьОбъект();
	АвтоОбъявлениеОбъект.Состояние = Состояние;
	
	Попытка
		АвтоОбъявлениеОбъект.Записать();
		ДобавитьРезультатВыгрузки(АвтоОбъявление, 
			                      "Обновление состояния объявления", 
			                      "Состояние объявления установлено в <Отправлено>");
	Исключение
		ДобавитьРезультатВыгрузки(АвтоОбъявление, 
			                      "Обновление состояния объявления", 
			                      "Не удалось обновить состояние объявления.");
	КонецПопытки;	
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// АВТОКЛАУД

// Создает дерево данных для сервиса Автоклауд
Функция АвтоклаудДеревоДанныхОбъявления()
	
	МакетАвтоклауд = ЭтотОбъект.ПолучитьМакет("Автоклауд");
	
	Возврат ДеревоОбъявления(МакетАвтоклауд);
	
КонецФункции // АвтоклаудСоздатьДеревоДанныхОбъявления()

#Если НЕ ВнешнееСоединение Тогда
	
// Заполняет дерево данных для сервиса Автоклауд 
Процедура АвтоклаудЗаполнитьДеревоДанныхОбъявления(ДеревоДанных, ОбъявлениеОбъект) Экспорт
	#Если Клиент Тогда
	зфАвтоклаудЗаполнитьДеревоДанныхОбъявления(ДеревоДанных, ОбъявлениеОбъект, Сервис);
	#Иначе
	зфЗащищенныеФункцииСервер.АвтоклаудЗаполнитьДеревоДанныхОбъявления(ДеревоДанных, ОбъявлениеОбъект, Сервис);
	#КонецЕсли
КонецПроцедуры // АвтоклаудЗаполнитьДеревоДанныхОбъявления	

#КонецЕсли

// Читает переданное в формате XML объявление и заполняет его данными
// переданное дерево значений
Процедура АвтоклаудПрочитатьФайлОбъявления(ДеревоДанных, ОбъявлениеXML)
	
	ОбъявлениеXDTO = ПрочитатьXMLВXDTO(ОбъявлениеXML, 
	ПолучитьТипЗначенияCML("feed", "http://aaa24.ru"));
	
	// car
	car = ОбъявлениеXDTO.cars[0].car[0];
	
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.id",     car.id);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.date",   car.date);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.update", car.update);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.price",  car.price);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.state",  car.state);
	
	params = car.params;
	
	//!! catalog-id
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.mark",         params.mark);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.model",        params.model);	
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.modification", params.modification);	
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.displacement", params.displacement);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.power",        params.power);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.body",         params.body);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.engine",       params.engine);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.gear",         params.gear);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.transmission", params.transmission);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.year",         params.year);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.doors",,       params.doors);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.run",,         params.run);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.vin",,         params.vin);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.wheel",        params.wheel);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.params.color",        params.color);
	
	// seller	
	seller = car.seller;
	
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.seller.id",      seller.id);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.seller.name",    seller.name);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.seller.phone",   seller.phone);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.seller.city",    seller.city);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.seller.address", seller.address);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.seller.email",   seller.email);
	ЗаполнитьЗначениеВДеревеДанных(ДеревоДанных, "feed.cars.car.seller.site",    seller.site);
	
КонецПроцедуры // АвтоклаудПрочитатьФайлОбъявления()	

// Формирует объявление в формате XML по переданному дереву данных и объявлению 
Функция АвтоклаудСформироватьФайлОбъявления(ДеревоДанных) Экспорт
	
	ОбъявлениеОбъект = "";
	
	ПространствоИменСхемы = "http://aaa24.ru";
	
	ТекстОшибки = "";
		
	ЗаписьXML = Новый ЗаписьXML;
	
	Параметры = Новый ПараметрыЗаписиXML("UTF-8", "1.0");
	ЗаписьXML.УстановитьСтроку(Параметры);
	
	// feed
	feed = ПолучитьОбъектТипаCML("feed", ПространствоИменСхемы);
	
	// -cars
	cars = ПолучитьОбъектТипаCML("cars", ПространствоИменСхемы);
	
	// --car
	car = ПолучитьОбъектТипаCML("car", ПространствоИменСхемы);
	
	Идентификатор = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.id");
	Дата          = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.date");
	Обновление    = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.update");
	Цена          = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.price");
	Состояние     = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.state");
	
	ЗаполнитьСвойствоXDTO(car, "id",     XMLСтрока(Идентификатор), Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(car, "date",   XMLСтрока(Дата),          Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(car, "update", XMLСтрока(Обновление),    Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(car, "price",  XMLСтрока(Цена),          Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(car, "state",  XMLСтрока(Состояние),     Истина, ТекстОшибки);
	
	// ---params
	params = ПолучитьОбъектТипаCML("params", ПространствоИменСхемы); 
	
	//Автомобиль = ОбъявлениеОбъект.Автомобиль;
	
	//!! catalog-id
	Марка               = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.mark");
	Модель              = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.model");
	ВариантКомплектации = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.modification");
	ОбъемДвигателя      = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.displacement");
	МощностьДвигателя   = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.power");
	Кузов               = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.body");
	Двигатель           = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.engine");
	Привод              = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.gear");
	Трансмиссия         = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.transmission");
	ГодВыпуска          = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.year");
	КоличествоДверей    = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.doors");
	Пробег              = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.run");
	VIN                 = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.vin");
	ПоложениеРуля       = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.wheel");
	Цвет                = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.params.color");
	
	ЗаполнитьСвойствоXDTO(params, "catalog_id",   XMLСтрока(""),                  Истина, ТекстОшибки); //!! 		
	ЗаполнитьСвойствоXDTO(params, "mark",         XMLСтрока(Марка),               Истина, ТекстОшибки);  		
	ЗаполнитьСвойствоXDTO(params, "model",        XMLСтрока(Модель),              Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "modification", XMLСтрока(ВариантКомплектации), Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "displacement", XMLСтрока(ОбъемДвигателя),      Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "power",        XMLСтрока(МощностьДвигателя),   Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "body",         XMLСтрока(Кузов),               Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "engine",       XMLСтрока(Двигатель),           Истина, ТекстОшибки);		
	ЗаполнитьСвойствоXDTO(params, "gear",         XMLСтрока(Привод),              Истина, ТекстОшибки);		
	ЗаполнитьСвойствоXDTO(params, "transmission", XMLСтрока(Трансмиссия),         Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "year",         XMLСтрока(ГодВыпуска),          Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "doors",        XMLСтрока(КоличествоДверей),    Истина, ТекстОшибки); 
	ЗаполнитьСвойствоXDTO(params, "run",          XMLСтрока(Пробег),              Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "vin",          XMLСтрока(VIN),                 Истина, ТекстОшибки);
	ЗаполнитьСвойствоXDTO(params, "wheel",        XMLСтрока(ПоложениеРуля),       Истина, ТекстОшибки); 
	ЗаполнитьСвойствоXDTO(params, "color",        XMLСтрока(Цвет),                Истина, ТекстОшибки);
	
	// ---params
	ЗаполнитьСвойствоXDTO(car, "params", params, Истина, ТекстОшибки);
	
	// ---images
	images = ПолучитьОбъектТипаCML("images", ПространствоИменСхемы);	
	
	// ---images
	ЗаполнитьСвойствоXDTO(car, "images", images, Истина, ТекстОшибки);
	
	// ---seller
	seller = ПолучитьОбъектТипаCML("seller", ПространствоИменСхемы);
	
	ИдентификаторПродавца = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.seller.id");
	НазваниеПродавца      = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.seller.name");
	ТелефонПродавца       = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.seller.phone");
	ГородПродавца         = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.seller.city");
	АдресПродавца         = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.seller.address");
	ЭлПочтаПродавца       = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.seller.email");
	СайтПродавца          = ПолучитьЗначениеИзДереваДанных(ДеревоДанных, "feed.cars.car.seller.site");
	
	ЗаполнитьСвойствоXDTO(seller, "id",      XMLСтрока(ИдентификаторПродавца), Истина, ТекстОшибки);	
	ЗаполнитьСвойствоXDTO(seller, "name",    XMLСтрока(НазваниеПродавца),      Истина, ТекстОшибки);   	
	ЗаполнитьСвойствоXDTO(seller, "phone",   XMLСтрока(ТелефонПродавца),       Истина, ТекстОшибки);   	
	ЗаполнитьСвойствоXDTO(seller, "city",    XMLСтрока(ГородПродавца),         Истина, ТекстОшибки);      	
	ЗаполнитьСвойствоXDTO(seller, "address", XMLСтрока(АдресПродавца),         Истина, ТекстОшибки);   	
	ЗаполнитьСвойствоXDTO(seller, "email",   XMLСтрока(ЭлПочтаПродавца),       Истина, ТекстОшибки);   	
	ЗаполнитьСвойствоXDTO(seller, "site",    XMLСтрока(СайтПродавца),          Истина, ТекстОшибки);       
	
	// ---seller	
	ЗаполнитьСвойствоXDTO(car, "seller", seller, Истина, ТекстОшибки);
	
	// --car
	cars.car.Добавить(car);
	
	// -cars
	feed.cars.Добавить(cars);
	
	// feed
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, feed, , , , НазначениеТипаXML.Явное);

	Возврат ЗаписьXML.Закрыть();	
	
КонецФункции // АвтоклаудСформироватьФайлОбъявления()	

Функция АвтоклаудЗначениеРеквизитаСервиса(ИмяРеквизитаСервиса, ЗначениеАльфа)
	
	Если Не ЗначениеЗаполнено(ЗначениеАльфа) Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	Сервис = Перечисления.СервисыАвтоОбъявлений.Автоклауд;
	
	Набор = РегистрыСведений.СоответствиеЗначенийАвтоОбъявлений.СоздатьНаборЗаписей();
	Набор.Отбор.СервисАвтоОбъявлений.Установить(Сервис);
	Набор.Отбор.РеквизитСервиса.Установить(ИмяРеквизитаСервиса);
	Набор.Отбор.ЗначениеРеквизитаАльфаАвто.Установить(ЗначениеАльфа);
	Набор.Прочитать();
	
	Если Набор.Количество() = 1 Тогда
		Возврат Набор[0].ЗначениеРеквизитаОбъявления;
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЗначениеАльфа)) Тогда
		Возврат ЗначениеАльфа.Наименование;	
	Иначе
		Возврат ЗначениеАльфа;
	КонецЕсли;
	
КонецФункции // АвтоклаудЗначениеРеквизитаСервиса()	

// Выполняет авторизацию для сервиса Автоклауд
// Также используется для проверки соединения с сервисом
Процедура АвтоклаудАвторизация(Логин, Пароль) Экспорт
	
	// Идентификатор сеанса Автоклауд "живет" 15 минут с последнего вызова
	МинутСПоследнегоВызова = (ТекущаяДата() - АвтоклаудПоследнийВызовСервиса) / 60;
	УИДЗаполнен            = ЗначениеЗаполнено(АвтоклаудИдентификатор);
	
	Если  УИДЗаполнен
		И МинутСПоследнегоВызова <= 14
		Тогда
		Возврат;
	КонецЕсли;	
	
	// Необходимые параметры и строковые переменные 
	Сервер          = "aaa24.ru";
	Ресурс          = "/webapi/auth.php";
	ИмяФайлаЗапроса = ПолучитьИмяВременногоФайла("xml");
	
	// Создаем HTTP соединение 
	HTTP = Новый HTTPСоединение(Сервер);		
	
	// Создаем и наполняем xml-файл для запроса 
	ФайлЗапроса = Новый ТекстовыйДокумент;
	ФайлЗапроса.ДобавитьСтроку("<?xml version=""1.0"" encoding=""UTF-8""?>");
	ФайлЗапроса.ДобавитьСтроку("<request>");
	ФайлЗапроса.ДобавитьСтроку("<function name=""login"">");
	ФайлЗапроса.ДобавитьСтроку("<param name=""login"">"  + Логин + "</param>");
	ФайлЗапроса.ДобавитьСтроку("<param name=""password"">" + Пароль + "</param>");
	ФайлЗапроса.ДобавитьСтроку("</function>");
	ФайлЗапроса.ДобавитьСтроку("</request>");
	ФайлЗапроса.Записать(ИмяФайлаЗапроса, КодировкаТекста.UTF8);
	
	// Создаем xml-файл для результата запроса
	// Данными его наполнит сервис - результат HTTP-запроса
	ИмяФайлаРезультата = ПолучитьИмяВременногоФайла("xml");
	
	// Отправка запроса 
	// +BasY
	//HTTP.ОтправитьДляОбработки(ИмяФайлаЗапроса, Ресурс, ИмяФайлаРезультата);
	ЗапросHTTP				= Новый HTTPЗапрос;
	ЗапросHTTP.АдресРесурса	= Ресурс;
	ЗапросHTTP.УстановитьИмяФайлаТела(ИмяФайлаЗапроса);
	HTTP.ОтправитьДляОбработки(ЗапросHTTP, ИмяФайлаРезультата);
	// -BasY
	
	// В результате запроса нам интересен уникальный идентификатор сессии сервиса
	// Он используется в каждом обращении к сервису, без него никак
	АвтоклаудИдентификатор = "";
	
	// Чтение и разбор результата запроса 
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайлаРезультата);
	
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ОтветСервиса = ЧтениеXML.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
	// Удаление временных файлов
	УдалитьФайлы(ИмяФайлаЗапроса);
	УдалитьФайлы(ИмяФайлаРезультата);
	
	// Возвращаем результат авторизации	
	Если    ОтветСервиса = "" 
		Или ОтветСервиса = "wrong username or password" 
		Тогда			
		АвтоклаудИдентификатор = "";
		ДобавитьРезультатВыгрузки("", "Авторизация", "Авторизация на сервисе НЕ выполнена.");		
		
	Иначе
		АвтоклаудИдентификатор         = ОтветСервиса;
		АвтоклаудПоследнийВызовСервиса = ТекущаяДата();
		
		ДобавитьРезультатВыгрузки("", "Авторизация", "Авторизация на сервисе выполнена успешно.");
	КонецЕсли;
	
КонецПроцедуры // АвтоклаудАвторизация()	

Функция АвтоклаудПараметрыВыгрузки()
	
	Логин         = обПраво("ЛогинАвтоклауд",         Права);
	Пароль        = обПраво("ПарольАвтоклауд",        Права);
	КодАвтосалона = обПраво("КодАвтосалонаАвтоклауд", Права);
	
	// Формирование структуры параметров для выгрузки
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("Сервер", "aaa24.ru");
	ПараметрыВыгрузки.Вставить("Ресурс", "/webapi/cars.php"); 	
	ПараметрыВыгрузки.Вставить("Логин",  Логин);
	ПараметрыВыгрузки.Вставить("Пароль", Пароль);
	ПараметрыВыгрузки.Вставить("КодАвтосалона", КодАвтосалона);

	Возврат ПараметрыВыгрузки;
	
КонецФункции // АвтоклаудПараметрыВыгрузки()	

#Если НЕ ВнешнееСоединение Тогда

Функция АвтоклаудДополнитьXML(ОбъявлениеДляВыгрузки, ОбъявлениеXML, ПараметрыВыгрузки)
	#Если Клиент Тогда
	Возврат зфАвтоклаудДополнитьXML(ОбъявлениеДляВыгрузки, ОбъявлениеXML, ПараметрыВыгрузки, АвтоклаудИдентификатор, АвтоклаудРезультатВыгрузки);
	#Иначе
	Возврат зфЗащищенныеФункцииСервер.АвтоклаудДополнитьXML(ОбъявлениеДляВыгрузки, ОбъявлениеXML, ПараметрыВыгрузки, АвтоклаудИдентификатор, АвтоклаудРезультатВыгрузки);
	#КонецЕсли
КонецФункции // АвтоклаудДополнитьXML()	

#КонецЕсли

#Если НЕ ВнешнееСоединение Тогда

Процедура АвтоклаудВыгрузкаОбъявленияВСервис(ОбъявлениеДляВыгрузки) Экспорт
	
	ПараметрыВыгрузки = АвтоклаудПараметрыВыгрузки();	
		
	// Отправка изображений на ФТП - проверять не выгружали ли ранее
	// Отдельная функция отправки изображений на ФТП - в обработку на форму 	
	АвтоклаудВыгрузкаКартинокНаFTP(ОбъявлениеДляВыгрузки, ПараметрыВыгрузки);	
	
	// Получение XML для выгрузки
	ОбъявлениеXML = ПолучитьФайлОбъявленияИзХранилищаДокумента(ОбъявлениеДляВыгрузки);
	
	Если ОбъявлениеXML = Неопределено Тогда
		ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, 
			                      "Выгрузка объявления", 
			                      "Не сформирован XML-файл для выгрузки.");
		Возврат;
	КонецЕсли;		
	
	// Авторизация на сервиса
	АвтоклаудАвторизация(ПараметрыВыгрузки.Логин, ПараметрыВыгрузки.Пароль);
	
	// Дополняем XML переменными данными - uid, update, картинки
	ОбъявлениеXML = АвтоклаудДополнитьXML(ОбъявлениеДляВыгрузки, ОбъявлениеXML, ПараметрыВыгрузки);
	
	// Выгрузка XML в сервис	
	АвтоклаудВыгрузкаXMLВСервис(ОбъявлениеДляВыгрузки, ОбъявлениеXML, ПараметрыВыгрузки);
	
КонецПроцедуры // АвтоклаудВыгрузкаОбъявленияВСервис()	

#КонецЕсли

Процедура АвтоклаудВыгрузкаXMLВСервис(ОбъявлениеДляВыгрузки, ОбъявлениеXML, ПараметрыВыгрузки)
	
	// Необходимые параметры и строковые переменные
	ИмяФайлаЗапроса    = ПолучитьИмяВременногоФайла("xml");
	ИмяФайлРезультата  = ПолучитьИмяВременногоФайла("xml");
	
	HTTP = Новый HTTPСоединение(ПараметрыВыгрузки.Сервер);	
		
	ОбъявлениеТекст = Новый ЗаписьТекста(ИмяФайлаЗапроса, КодировкаТекста.UTF8);
	ОбъявлениеТекст.Записать(ОбъявлениеXML);
	ОбъявлениеТекст.Закрыть();
	// +BasY
	//HTTP.ОтправитьДляОбработки(ИмяФайлаЗапроса, ПараметрыВыгрузки.Ресурс, ИмяФайлРезультата);
	ЗапросHTTP				= Новый HTTPЗапрос;
	ЗапросHTTP.АдресРесурса	= ПараметрыВыгрузки.Ресурс;
	ЗапросHTTP.УстановитьИмяФайлаТела(ИмяФайлаЗапроса);
	HTTP.ОтправитьДляОбработки(ЗапросHTTP, ИмяФайлРезультата);
	// -BasY
	
	РезультатФайл = Новый ТекстовыйДокумент;
	РезультатФайл.Прочитать(ИмяФайлРезультата, "UTF-8");
	
	РезультатТекстXML = РезультатФайл.ПолучитьТекст();
    	
	РезультатТекст = АвтоклаудПрочитатьРезультатВыгрузки(РезультатТекстXML);
	
	Если Найти(РезультатТекстXML, "Parse complete") <> 0 Тогда
		ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, "Выгрузка объявления", "Объявление успешно выгружено.");
		// Обновление статуса объявления
		УстановитьСостояниеОбъявления(ОбъявлениеДляВыгрузки, Перечисления.СостоянияАвтоОбъявлений.Отправлено);
	Иначе
		// Факт НЕ выгрузки
		ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, "Выгрузка объявления", "Объявление НЕ выгружено.");
		// Описание ошибки
		ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, "Выгрузка объявления", РезультатТекст);
	КонецЕсли;
		
	УдалитьФайлы(ИмяФайлаЗапроса);
	УдалитьФайлы(ИмяФайлРезультата);		
	
КонецПроцедуры // АвтоклаудВыгрузкаXMLВСервис()

Функция АвтоклаудПрочитатьРезультатВыгрузки(РезультатТекстXML)
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(РезультатТекстXML);
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	
	СписокЭлементовDOM = ДокументDOM.ПолучитьЭлементыПоИмени("message");
	
	Если СписокЭлементовDOM.Количество() > 0 Тогда
		Возврат ДокументDOM.ПолучитьЭлементыПоИмени("message")[0].ТекстовоеСодержимое;
	КонецЕсли;
	
	СписокЭлементовDOM = ДокументDOM.ПолучитьЭлементыПоИмени("error");
	
	Если СписокЭлементовDOM.Количество() > 0 Тогда
		Возврат ДокументDOM.ПолучитьЭлементыПоИмени("error")[0].ТекстовоеСодержимое;
	КонецЕсли;
	
КонецФункции // АвтоклаудПрочитатьРезультатВыгрузки()

Функция АвтоклаудИмяКартинкиДляFTP(ИдентификаторОбъявления, НомерКартинки, ИмяКартинки)
	
	Возврат ИдентификаторОбъявления + "_" + НомерКартинки + "_" + ИмяКартинки;
	
КонецФункции // АвтоклаудИмяКартинкиДляFTP()

Процедура АвтоклаудВыгрузкаКартинокНаFTP(ОбъявлениеДляВыгрузки, ПараметрыВыгрузки)	
	
	ИменаКартинокНаFTP = Новый Массив;	
	
	НаборЗаписейКартинкиИФайлы = РегистрыСведений.КартинкиИФайлы.СоздатьНаборЗаписей();
	НаборЗаписейКартинкиИФайлы.Отбор.Объект.Значение      = ОбъявлениеДляВыгрузки;
	НаборЗаписейКартинкиИФайлы.Отбор.Объект.ВидСравнения  = ВидСравнения.Равно;
	НаборЗаписейКартинкиИФайлы.Отбор.Объект.Использование = Истина;	
	НаборЗаписейКартинкиИФайлы.Прочитать();
	
	// Если в объявлении нет картинок, то создавать соединение - 
	// только время терять	
	Если НаборЗаписейКартинкиИФайлы.Количество() = 0 Тогда
		ПараметрыВыгрузки.Вставить("Картинки", ИменаКартинокНаFTP);
		Возврат;
	КонецЕсли;                                                     	
	
	// Создание соединения с ФТП
	Попытка
		FTP = Новый FTPСоединение(ПараметрыВыгрузки.Сервер, , НРег(ПараметрыВыгрузки.Логин), НРег(ПараметрыВыгрузки.Пароль));
	Исключение
		ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, 
			                      "Выгрузка картинок на FTP", 
			                      "Не удалось установить соединение с FTP-сервером!");
		Возврат;						  
	КонецПопытки;	
	
	// Закидываем картинки в цикле
	Для Каждого СтрокаКартинки Из НаборЗаписейКартинкиИФайлы Цикл  		
		
		Если Не СтрокаКартинки.Картинка Тогда
			Продолжить;
		КонецЕсли;	
		
		Если СтрокаКартинки.ДанныеВоВнешнемФайле Тогда 			
			ИмяФайла = СтрокаКартинки.ИмяФайла;
		Иначе
			Картинка       = СтрокаКартинки.Данные.Получить();  			
			КартинкаФормат = Строка(Картинка.Формат());
			ИмяФайла       = ПолучитьИмяВременногоФайла(КартинкаФормат);
			
			Картинка.Записать(ИмяФайла);			
		КонецЕсли;	
		
		ФайлКартинки = Новый Файл(ИмяФайла);
		
		// Если файла нет - пишем в результат выгрузки			
		Если Не ФайлКартинки.Существует() Тогда
			ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, 
			                          "Выгрузка картинок на FTP", 
			                          "Файл картинки " + СтрокаКартинки.Идентификатор + " не существует!");
			Продолжить;
		КонецЕсли;
		
		// Для ФТП делаем сложное имя, чтобы не было дублей
		ИмяКартинкиНаFTP = АвтоклаудИмяКартинкиДляFTP(ОбъявлениеДляВыгрузки.Идентификатор, СтрокаКартинки.НомерСтроки+1, СтрокаКартинки.Идентификатор);
		ИмяКартинкиНаFTP = ИмяКартинкиНаFTP+ФайлКартинки.Расширение;
		
		//!!
		// Если картинка с таким именем уже есть на ФТП - действуем по настройкам
		// Настроек, пока, нет - всегда перезатираем
		Если FTP.НайтиФайлы(ИмяКартинкиНаFTP).Количество() <> 0 Тогда
			FTP.Удалить(ИмяКартинкиНаFTP);	
		КонецЕсли;  		
		
		Попытка 
			FTP.Записать(ИмяФайла, ИмяКартинкиНаFTP);			
			ИменаКартинокНаFTP.Добавить(ИмяКартинкиНаFTP);
		Исключение
			ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, 
			                          "Выгрузка картинок на FTP", 
			                          "Ошибка выгрузки картинки " + СтрокаКартинки.Идентификатор + " на FTP!");
		КонецПопытки;
								  
		УдалитьФайлы(ИмяФайла);						  
	КонецЦикла;
	
	ПараметрыВыгрузки.Вставить("Картинки", ИменаКартинокНаFTP);
	
	ДобавитьРезультатВыгрузки(ОбъявлениеДляВыгрузки, 
			                  "Выгрузка картинок на FTP", 
			                  "Выгружено картинок на FTP: "+ИменаКартинокНаFTP.Количество());
	
КонецПроцедуры // АвтоклаудВыгрузкаКартинокНаFTP()	

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

АвтоклаудРезультатВыгрузки = Новый ТаблицаЗначений;
АвтоклаудРезультатВыгрузки.Колонки.Добавить("АвтоОбъявление", , "Объявление");
АвтоклаудРезультатВыгрузки.Колонки.Добавить("ВидРезультата",  , "Вид результата");
АвтоклаудРезультатВыгрузки.Колонки.Добавить("Результат");

АвтоклаудПоследнийВызовСервиса = Дата(1,1,1);

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли

#Если Клиент Тогда
	
Перем МассивДнейНедели Экспорт;               // массив с днями недели
Перем СтруктураПокраски Экспорт;              // структура для раскраски элементов формы
Перем СтруктураКартинок Экспорт;              // структура с картинками
Перем СтруктураДобавляемыхДокументов Экспорт; // структура документов, добавляемых с помощью подменю "Добавить" командной панели формы
Перем СтруктураЗначенийФильтров Экспорт;      // хранит значения фильтров

////////////////////////////////////////////////////////////////////
//// ОБЩИЕ

// Получение таблицы значимых событий
Процедура ПолучитьТаблицуРасписаний(НужноОбновить = Истина) Экспорт
	Если НужноОбновить Тогда
		Запрос = Новый Запрос;
		
		//условия для напоминаний
		ТекстЗапросаГдеДляНапоминания = ?(ТолькоАктуальные, "И НЕ Напоминания.Завершено", "");
		
		//Формирование текста запроса по отображаемым документам
		Если СписокОтображаемыхДокументов.Количество()=0 Тогда
			ЗаполнитьСписокОтображаемыхДокументовИзМакета();
		КонецЕсли;
		
		ТекстЗапросаПоОтображаемымДокументам = "";
		Для Каждого Документ Из СписокОтображаемыхДокументов Цикл
			Документ = Документ.Значение;				
			ТекстЗапросаПоОтображаемымДокументам = ТекстЗапросаПоОтображаемымДокументам + ПолучитьТекстЗапросаОбъединить(Документ);
		КонецЦикла;	
		
		// Сформируем текст запроса по напоминаниям и объединим с запросом по документам
		ТекстЗапроса = "ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(Напоминания.РеальнаяДатаОповещения, ДЕНЬ) КАК ДатаСобытия,
		|	ИСТИНА КАК Напоминание,
		|	Напоминания.РеальнаяДатаОповещения КАК НачалоСобытия,
		|	Напоминания.РеальнаяДатаОповещения КАК ОкончаниеСобытия,
		|	Напоминания.РеальнаяДатаОповещения КАК НачалоСобытияДата,
		|	Напоминания.РеальнаяДатаОповещения КАК ОкончаниеСобытияДата,
		|	Напоминания.Тема КАК ПредставлениеОбъекта,
		|	""Напоминание"" КАК Событие,
		|	Напоминания.Тема КАК Тема,
		|	Напоминания.Пользователь КАК Пользователь,
		|	Напоминания.Автор КАК Автор,
		|	Напоминания.РеальнаяДатаОповещения КАК РеальнаяДатаОповещения,
		|	Напоминания.Объект КАК Объект,
		|	""Напоминание"" КАК ЦветПокраски,
		|	""Оповещение"" КАК ИмяИконки,
		|	Напоминания.Завершено  КАК Завершено
		|ИЗ
		|	РегистрСведений.Напоминания КАК Напоминания
		|ГДЕ
		|   Напоминания.Пользователь = &Пользователь
		|	" + ТекстЗапросаГдеДляНапоминания + ТекстЗапросаПоОтображаемымДокументам +
		" УПОРЯДОЧИТЬ ПО
		|	ДатаСобытия,
		|	НачалоСобытия";
		
		Запрос.Текст = ТекстЗапроса;
		
		Запрос.УстановитьПараметр("Пользователь", ПользовательКалендаря);
		Запрос.УстановитьПараметр("СотрудникПользователь", ПользовательКалендаря.Сотрудник);
		Запрос.УстановитьПараметр("СостояниеСобытиеНеЗавершено", Перечисления.СостоянияСобытий.Запланировано);
		
		// Выгружаем данные
		РасписаниеСобытий.Загрузить(Запрос.Выполнить().Выгрузить());
		
		ЗаполнитьИменаИконокВТаблицеРасписаний();
	КонецЕсли;	
КонецПроцедуры // ПолучитьТаблицуРасписаний()

// Заполняет имена иконок в таблице расписание
Процедура ЗаполнитьИменаИконокВТаблицеРасписаний()
	Для Каждого СтрокаРасписания Из РасписаниеСобытий Цикл
			//имена иконок
			Если ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументССылка.Событие") Тогда
				СтрокаРасписания.ПредставлениеОбъекта = Формат(СтрокаРасписания.Событие.ДатаНачала, "ДФ=ЧЧ:мм; ДП=00:00") + " " + СтрокаРасписания.Событие.Тема;
				Если СтрокаРасписания.Событие.ВидСобытия = Перечисления.ВидыСобытий.ЛичнаяВстреча Тогда
					СтрокаРасписания.ИмяИконки = "ЛичнаяВстреча"; 
				ИначеЕсли СтрокаРасписания.Событие.ВидСобытия = Перечисления.ВидыСобытий.ПочтовоеПисьмо	Тогда
					СтрокаРасписания.ИмяИконки = "Письмо";
				ИначеЕсли СтрокаРасписания.Событие.ВидСобытия = Перечисления.ВидыСобытий.Прочее	Тогда
					СтрокаРасписания.ИмяИконки = "ПрочееСобытие";
				ИначеЕсли СтрокаРасписания.Событие.ВидСобытия = Перечисления.ВидыСобытий.ТелефонныйЗвонок Тогда
					СтрокаРасписания.ИмяИконки = "КонтактнаяИнформацияТелефон";
				ИначеЕсли СтрокаРасписания.Событие.ВидСобытия = Перечисления.ВидыСобытий.ЭлектронноеПисьмо Тогда
					СтрокаРасписания.ИмяИконки = "ЭлектронноеПисьмо";
				КонецЕсли;	
			ИначеЕсли НЕ СтрокаРасписания.Напоминание Тогда 
				ПозицияНачалаДаты = Найти(СтрокаРасписания.ПредставлениеОбъекта," от "); 
				Если ПозицияНачалаДаты <> 0 Тогда
					СтрокаРасписания.ПредставлениеОбъекта = Лев(СтрокаРасписания.ПредставлениеОбъекта,ПозицияНачалаДаты-1);
				КонецЕсли;	
			КонецЕсли;		
		КонецЦикла;
КонецПроцедуры //ЗаполнитьИменаИконокВТаблицеРасписаний()	

// получить текст запроса объединить
Функция ПолучитьТекстЗапросаОбъединить(Документ, ПереданаТЧ = Ложь)
	//документ будет выведен, если этому не помешают настройки принадлежности пользователю 
	ВыводитьДокумент = Истина;
		
	//если документ - имя ТЧ, то к нему добавляем ".Ссылка"
	СтрокаСсылка = ?(ПереданаТЧ,".Ссылка","");
	Если Документ = "Событие" ИЛИ (Найти(Документ,"ЗаявкаНаРемонт")>0) ИЛИ (Найти(Документ,"ЗаказНаряд")>0) Тогда 
		ПолеНачалоСобытия = СтрЗаменить(Документ,".","")+СтрокаСсылка+".ДатаНачала";
		ПолеОкончаниеСобытия = СтрЗаменить(Документ,".","")+СтрокаСсылка+".ДатаОкончания"; 
	Иначе
		ПолеНачалоСобытия = Документ+".Дата";
		ПолеОкончаниеСобытия = Документ+".Дата"; 
	КонецЕсли;
	
	//у событий есть тема, у других документов ее нет
	Если Документ = "Событие" Тогда
    	ПолеТема = Документ+".Тема";
	Иначе
		ПолеТема = "NULL";
	КонецЕсли;
	
	// условия по фильтрам
	ТекстЗапросаПоРесурсамДляДокумента = "";
	
	// условие для только актуальных событий
	Если Документ = "Событие" И ТолькоАктуальные Тогда 
		ТекстЗапросаГдеДляДокумента = ?(ПустаяСтрока(ТекстЗапросаПоРесурсамДляДокумента)," ГДЕ ", ТекстЗапросаПоРесурсамДляДокумента + " И ") + Документ + ".Состояние = &СостояниеСобытиеНеЗавершено"
	КонецЕсли;
		
	// условие по принадлежности документов пользователю
	Если ПринадлежностьСобытийПользователю = "" ИЛИ ПринадлежностьСобытийПользователю = "ВсеДокументы" Тогда
		//условий нет
	ИначеЕсли ПринадлежностьСобытийПользователю = "МоиДокументы" Тогда
		Если Метаданные.Документы[Документ].Реквизиты.Найти("Автор") <> Неопределено Тогда
			//условие по автору
			ТекстЗапросаГдеДляДокумента = ?(ПустаяСтрока(ТекстЗапросаГдеДляДокумента)," ГДЕ ", ТекстЗапросаГдеДляДокумента + " И ") + Документ + ".Автор = &Пользователь";
		Иначе
			ВыводитьДокумент = Ложь;
		КонецЕсли;
	ИначеЕсли ПринадлежностьСобытийПользователю = "ДокументыДляКонтроля" Тогда
		Если Метаданные.Документы[Документ].Реквизиты.Найти("Менеджер") <> Неопределено Тогда
			//условие по менеджеру
			ТекстЗапросаГдеДляДокумента = ?(ПустаяСтрока(ТекстЗапросаГдеДляДокумента)," ГДЕ ", ТекстЗапросаГдеДляДокумента + " И ") + Документ + ".Менеджер = &СотрудникПользователь";
		Иначе
			ВыводитьДокумент = Ложь;	
		КонецЕсли;
	ИначеЕсли ПринадлежностьСобытийПользователю = "НазначенныеРаботы" Тогда
		Если (Найти(Документ,"ЗаявкаНаРемонт")>0) ИЛИ (Найти(Документ,"ЗаказНаряд")>0) Тогда
			Если ПереданаТЧ Тогда
				//условие по исполнителям
				ТекстЗапросаГдеДляДокумента = ?(ПустаяСтрока(ТекстЗапросаГдеДляДокумента)," ГДЕ ", ТекстЗапросаГдеДляДокумента + " И ")+ СтрЗаменить(Документ,".","") + ".Исполнитель = &СотрудникПользователь";
			Иначе	
				//условие по мастеру
				ТекстЗапросаГдеДляДокумента = ?(ПустаяСтрока(ТекстЗапросаГдеДляДокумента)," ГДЕ ", ТекстЗапросаГдеДляДокумента + " И ")+ Документ + ".Мастер = &СотрудникПользователь";
				ТекстЗапросаГдеДляДокумента = ТекстЗапросаГдеДляДокумента + ПолучитьТекстЗапросаОбъединить(Документ+".Исполнители",Истина);
			КонецЕсли;
		Иначе
			ВыводитьДокумент = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// собираем текст запроса по документам
	Если ВыводитьДокумент Тогда
		ТекстЗапросаПоОтображаемымДокументам = " 
		|ОБЪЕДИНИТЬ ВСЕ 
		|	ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА("+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Дата, ДЕНЬ),
		|	ЛОЖЬ,
		|	"+ПолеНачалоСобытия+",
		|	"+ПолеОкончаниеСобытия+",
		|	"+ПолеНачалоСобытия+",
		|	"+ПолеОкончаниеСобытия+",
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Представление,
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Ссылка,
		|	"+ПолеТема+",
		|	NULL,
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Автор,
		|	NULL,
		|	NULL,
		|	""Событие1"",
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Представление,
		|	ЛОЖЬ
		|ИЗ
		|	Документ."+Документ+" КАК "+СтрЗаменить(Документ,".","")+" " + ТекстЗапросаГдеДляДокумента;	
	КонецЕсли;	
	Возврат ТекстЗапросаПоОтображаемымДокументам;
КонецФункции // ПолучитьТекстЗапросаОбъединить()

// проверить фильтры
Функция ПроверитьФильтры(ДокументСсылка)
	Если СтруктураЗначенийФильтров.Количество()=0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	ДокументМетаданные = ДокументОбъект.Метаданные();
	Для Каждого Фильтр Из СтруктураЗначенийФильтров Цикл
		
		ПромежРез = Ложь;
		
		Для Каждого Реквизит Из ДокументМетаданные.Реквизиты Цикл
			Если Реквизит.Тип.СодержитТип(Тип("СправочникСсылка."+Фильтр.Ключ)) Тогда
				Если ДокументОбъект[Реквизит.Имя] = Фильтр.Значение Тогда
					ПромежРез = Истина;
					Продолжить;
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;	
		
		Если НЕ ПромежРез Тогда
			Для Каждого ТабличнаяЧасть Из ДокументМетаданные.ТабличныеЧасти Цикл
				Для Каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл
					Если Реквизит.Тип.СодержитТип(Тип("СправочникСсылка."+Фильтр.Ключ)) Тогда
						Колонка = ДокументОбъект[ТабличнаяЧасть.Имя].ВыгрузитьКолонку(Реквизит.Имя);
						Если Колонка.Найти(Фильтр.Значение) <> Неопределено Тогда
							ПромежРез = Истина;
							Продолжить;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;	
		
		Если Не ПромежРез Тогда
			Возврат Ложь;
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат Истина;
КонецФункции //ПроверитьФильтры()

// заполнить структуру значений фильтров
Процедура ЗаполнитьСтруктуруЗначенийФильтров() Экспорт
	СтруктураЗначенийФильтров = Новый Структура;
	Если ЗначениеЗаполнено(ФильтрКонтрагент) Тогда
		СтруктураЗначенийФильтров.Вставить("Контрагенты",ФильтрКонтрагент);
	КонецЕсли;
		Если ЗначениеЗаполнено(ФильтрКонтактноеЛицо) Тогда
		СтруктураЗначенийФильтров.Вставить("КонтактныеЛица",ФильтрКонтактноеЛицо);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрСотрудник) Тогда
		СтруктураЗначенийФильтров.Вставить("Сотрудники",ФильтрСотрудник);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрПодразделение) Тогда
		СтруктураЗначенийФильтров.Вставить("ПодразделенияКомпании",ФильтрПодразделение);
	КонецЕсли;
		Если ЗначениеЗаполнено(ФильтрСклад) Тогда
		СтруктураЗначенийФильтров.Вставить("СкладыКомпании",ФильтрСклад);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрЦех) Тогда
		СтруктураЗначенийФильтров.Вставить("Цеха",ФильтрЦех);
	КонецЕсли;
КонецПроцедуры // ЗаполнитьСтруктуруЗначенийФильтров()

// Заполнение структуры цветов
Процедура ЗаполнитьСтруктуруЦветов() Экспорт
	СтруктураПокраски = Новый Структура;
	МакетОформления = Справочники.ГрафикиРаботы.ПолучитьМакет("МакетОформления");
	ОбластьОформления = МакетОформления.ПолучитьОбласть("Оформление");
	Для ПеременнаяЦикла = 1 По ОбластьОформления.ВысотаТаблицы Цикл
		ТекущаяОбласть = ОбластьОформления.Область(ПеременнаяЦикла, 1);
		Если обЗначениеНеЗаполнено(ТекущаяОбласть.Текст) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПокраски.Вставить(ТекущаяОбласть.Текст, ТекущаяОбласть.ЦветФона);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСтруктуруЦветов()

// Процедура предназначена для создания документа событие.
//
// Параметры:
//  НаДату 		- Дата на которую создается документ,
//  ВидСобытия  - Перечисление виды событий, подставляется в документ, если не заполнено подставляется прочее событие
Процедура СоздатьСобытие(НаДату, ВидСобытия = Неопределено) Экспорт
	
	// Создаем документ
	ДокументСобытие = Документы.Событие.СоздатьДокумент();
	
	// Заполняем документ
	ДокументСобытие.Заполнить(Неопределено);
	ДокументСобытие.ДатаНачала = НаДату;
	ДокументСобытие.Дата = НаДату;
	Если обЗначениеНеЗаполнено(ВидСобытия) Тогда
		ДокументСобытие.ВидСобытия = Перечисления.ВидыСобытий.Прочее;
	Иначе
		ДокументСобытие.ВидСобытия = ВидСобытия;
	КонецЕсли;
	
	// Открываем
	ФормаДокумента = ДокументСобытие.ПолучитьФорму("ФормаДокумента");
	Если НЕ ФормаДокумента.Открыта() Тогда
		ФормаДокумента.Открыть();
	КонецЕсли;
	
КонецПроцедуры // СоздатьСобытие()

// Процедура предназначена для создания напоминания.
//
// Параметры:
//  НаДату 		- Дата на которую создается документ,
Процедура СоздатьНапоминание(НаДату) Экспорт
	ФормаРегистра = РегистрыСведений.Напоминания.ПолучитьФормуРедактированияЗаписи();
	ФормаРегистра.ЭтотОбъект.Пользователь = ПараметрыСеанса.Пользователь;
	ФормаРегистра.ЭтотОбъект.Автор = ПараметрыСеанса.Пользователь;
	ФормаРегистра.ЭтотОбъект.Завершено = Ложь;
	ФормаРегистра.РеальнаяДатаОповещения = НаДату;
	ФормаРегистра.ЭтотОбъект.ДатаНачала = НаДату;
	ФормаРегистра.ЭтотОбъект.ДатаОповещения = НаДату;
	ФормаРегистра.Открыть();
КонецПроцедуры // СоздатьНапоминание()

// открытие напоминания
Процедура ОткрытьНапоминание(Данные) Экспорт
	МенеджерЗаписи = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Данные);
	МенеджерЗаписи.Завершено = Ложь;
	МенеджерЗаписи.Прочитать();
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Данные);
		МенеджерЗаписи.Завершено = Истина;
		МенеджерЗаписи.Прочитать();
	КонецЕсли;
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.ПолучитьФорму().Открыть();
	КонецЕсли;
КонецПроцедуры // ОткрытьНапоминание()

// заполнить список документов из макета
Процедура ЗаполнитьСписокДобавляемыхДокументовИзМакета() Экспорт
	СписокДобавляемыхДокументов.Очистить();
	МакетДокументов = ПолучитьМакет("МакетДокументов");
	
	ОбластьДокументов = МакетДокументов.ПолучитьОбласть("Документы");
	Для ПеременнаяЦикла = 1 По ОбластьДокументов.ВысотаТаблицы Цикл
		ИмяОбъекта = ОбластьДокументов.Область(ПеременнаяЦикла, 1).Текст;
		ПредставлениеОбъекта = ОбластьДокументов.Область(ПеременнаяЦикла, 2).Текст;
		
		Если ИмяОбъекта <> "" Тогда
			Если СписокДобавляемыхДокументов.НайтиПоЗначению(ИмяОбъекта) = Неопределено Тогда
				СписокДобавляемыхДокументов.Добавить(ИмяОбъекта,ПредставлениеОбъекта);
			КонецЕсли;
		КонецЕсли;	
    КонецЦикла;
	
	СписокДобавляемыхДокументов.Добавить("Напоминание","Напоминание");
КонецПроцедуры // ЗаполнитьСписокДобавляемыхДокументовИзМакета()	

// Заполняет структуру документов из списка
Функция ЗаполнитьСтруктуруДобавляемыхДокументовИзСписка() Экспорт
	СтруктураДобавляемыхДокументов = Новый Структура;
	Для Каждого Документ Из СписокДобавляемыхДокументов Цикл
		Если Документ.Значение <> "Напоминание" Тогда
			СтруктураДобавляемыхДокументов.Вставить("Новый" + Документ.Значение, Новый Структура("Объект, ИмяОбъекта", "Документ", Документ.Значение));
		КонецЕсли;	
	КонецЦикла;
	СтруктураДобавляемыхДокументов.Вставить("НовыйНапоминание", Новый Структура("Объект, ИмяОбъекта", "РегистрСведений", "Напоминание"));
	Возврат СтруктураДобавляемыхДокументов;
КонецФункции // ЗаполнитьСписокДобавляемыхДокументовИзМакета()	

// заполнение списка отображаемых документов из макета
Процедура ЗаполнитьСписокОтображаемыхДокументовИзМакета() Экспорт
	СписокОтображаемыхДокументов.Очистить();
	МакетДокументов = ПолучитьМакет("МакетДокументов");
	ОбластьДокументовПросмотра = МакетДокументов.ПолучитьОбласть("ДокументыПросмотр");
	Для ПеременнаяЦикла = 1 По ОбластьДокументовПросмотра.ВысотаТаблицы Цикл
		ИмяДокумента = ОбластьДокументовПросмотра.Область(ПеременнаяЦикла, 1).Текст;
		ПредставлениеДокумента = ОбластьДокументовПросмотра.Область(ПеременнаяЦикла, 2).Текст;
		Если ИмяДокумента <> "" Тогда 
			Если СписокОтображаемыхДокументов.НайтиПоЗначению(ИмяДокумента) = Неопределено Тогда
				СписокОтображаемыхДокументов.Добавить(ИмяДокумента,ПредставлениеДокумента);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры // ЗаполнитьСписокОтображаемыхДокументовИзМакета()

// создание меню
Процедура СоздатьМенюФормы(Форма) Экспорт
	СтруктураКартинок = Новый Структура;
	МакетКартинок = ПолучитьМакет("МакетКартинок");
	ОбластьКартинок = МакетКартинок.ПолучитьОбласть("Картинки");
	Для ПеременнаяЦикла = 1 По ОбластьКартинок.ВысотаТаблицы Цикл
		ИмяСобытия = ОбластьКартинок.Область(ПеременнаяЦикла, 1).Текст;
		ИмяКартинки = ОбластьКартинок.Область(ПеременнаяЦикла, 2).Текст;
		Если обЗначениеНеЗаполнено(ИмяСобытия) ИЛИ обЗначениеНеЗаполнено(ИмяКартинки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураКартинок.Вставить(ИмяСобытия, ИмяКартинки);
	КонецЦикла;
	
	Панель = Форма.ЭлементыФормы.КоманднаяПанельФормыДополнительно;
	Панель.Кнопки.Добавить.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
	Панель.Кнопки.Добавить.Картинка = БиблиотекаКартинок.Новый;                                                                      
	КнопкиСоздать = Панель.Кнопки.Добавить.Кнопки;
	КнопкиСоздать.Очистить();
	МенюКалендаря = Форма.ЭлементыФормы.КоманднаяПанельКалендаря.Кнопки.МенюКалендаря;
	КнопкиМенюКалендаряСоздать = МенюКалендаря.Кнопки;
	КнопкиМенюКалендаряСоздать.Очистить();
		
	Если (СписокДобавляемыхДокументов.Количество()=0) Тогда
		ЗаполнитьСписокДобавляемыхДокументовИзМакета();
	КонецЕсли;
	
	Если (СтруктураДобавляемыхДокументов = Неопределено) Тогда 
   		СтруктураДобавляемыхДокументов = ЗаполнитьСтруктуруДобавляемыхДокументовИзСписка(); 
	КонецЕсли;
	
	Для Каждого Документ Из СписокДобавляемыхДокументов Цикл
		
		ИмяКнопки = "Новый" + Документ.Значение; 	
	    ВидКнопки = ТипКнопкиКоманднойПанели.Действие;
		ЗаголовокКнопки = Документ.Представление;	
					
		Если ИмяКнопки <> "" Тогда 
			КнопкиСоздать.Добавить(ИмяКнопки,ВидКнопки,ЗаголовокКнопки,Новый Действие("ДействияФормыНовоеСобытие"));
			КнопкиСоздать[ИмяКнопки].Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
			КнопкиМенюКалендаряСоздать.Добавить(ИмяКнопки,ВидКнопки,ЗаголовокКнопки,Новый Действие("МенюКалендаряНовоеСобытие"));
			КнопкиМенюКалендаряСоздать[ИмяКнопки].Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
			Попытка	
				КнопкиСоздать[ИмяКнопки].Картинка = БиблиотекаКартинок[СтруктураКартинок[ИмяКнопки]]; 
				КнопкиМенюКалендаряСоздать[ИмяКнопки].Картинка = БиблиотекаКартинок[СтруктураКартинок[ИмяКнопки]];
			Исключение 
			КонецПопытки;
		КонецЕсли;

	КонецЦикла;	
	КнопкиМенюКалендаряСоздать.Добавить("Открыть", ТипКнопкиКоманднойПанели.Подменю,"Открыть");
КонецПроцедуры // СоздатьМенюФормы()

Функция ПериодыПересекаются(ДатаНачала1,ДатаКонца1,ДатаНачала2,ДатаКонца2)
	Если (ДатаНачала1<=ДатаКонца1) И (ДатаНачала2<=ДатаКонца2) Тогда
        ДатаН = Макс(ДатаНачала1,ДатаНачала2);
        ДатаК = Мин(ДатаКонца1,ДатаКонца2);
        Если ДатаН<=ДатаК Тогда
            Рез=Истина;
        Иначе
            Рез=Ложь;
        КонецЕсли;    
    Иначе
        Рез = Ложь;
    КонецЕсли;
        
    Возврат Рез;
КонецФункции //ПериодыПересекаются()

// формирование списка событий
Процедура СформироватьСписокСобытий(ТаблицаСпискаСобытий, НужноОбновить = Истина) Экспорт
	ПолучитьТаблицуРасписаний(НужноОбновить);
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,Событие,Тема,Пользователь,Автор,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки","");
	МассивСтрок = Новый Массив;
	ТаблицаРасписания = РасписаниеСобытий.Выгрузить();
    Для Каждого СтрокаРасписания ИЗ ТаблицаРасписания Цикл
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;
			
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.ДатаСобытия,СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.НачалоСобытияДата,СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
        	МассивСтрок.Добавить(СтрокаРасписания);			
        КонецЕсли;	
	КонецЦикла;
	ТаблицаСпискаСобытий.Значение = ТаблицаРасписания.Скопировать(МассивСтрок); 
КонецПроцедуры // СформироватьСписокСобытий()

////////////////////////////////////////////////////////////////////
//// ДЕНЬ

// Создание колонок для дневного графика
Процедура СоздатьКолонкиДляДневногоГрафика(ТаблицаДневногоРасписания)
	
	// Очищаем таблицу
	ТаблицаДневногоРасписания.Значение.Очистить();
	ТаблицаДневногоРасписания.Значение.Колонки.Очистить();
	ТаблицаДневногоРасписания.Колонки.Очистить();
	
	// Создание колонок
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("Время", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)), "Время");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("Время", "Время");
	Колонка.Данные = "Время";
	Колонка.Формат = "ДФ=ЧЧ:мм; ДП=00:00";
	Колонка.Ширина = 7;
	Колонка.ШрифтТекста = Новый Шрифт(,,Истина);
	Колонка.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("ПредставлениеСобытий", Новый ОписаниеТипов("Строка"), "ПредставлениеСобытий");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("ПредставлениеСобытий", "События");
	Колонка.Данные = "ПредставлениеСобытий";
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
		
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("МассивОбъектов", Новый ОписаниеТипов("Массив"), "МассивОбъектов");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("МассивОбъектов", "МассивОбъектов");
	Колонка.Данные = "МассивОбъектов";
	Колонка.Видимость = Ложь;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("ЦветПокраски", Новый ОписаниеТипов("Строка"), "ЦветПокраски");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("ЦветПокраски", "ЦветПокраски");
	Колонка.Данные = "ЦветПокраски";
	Колонка.Видимость = Ложь;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("ПеречислениеСсылка.Важность"), "Приоритет");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("Приоритет", "Приоритет");
	Колонка.Данные = "Приоритет";
	Колонка.Видимость = Ложь;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("ИмяИконки", Новый ОписаниеТипов("Строка"), "ИмяИконки");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("ИмяИконки", "ИмяИконки");
	Колонка.Данные = "ИмяИконки";
	Колонка.Видимость = Ложь;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("СмещениеИконки", Новый ОписаниеТипов("Строка"), "СмещениеИконки");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("СмещениеИконки", "СмещениеИконки");
	Колонка.Данные = "СмещениеИконки";
	Колонка.Видимость = Ложь;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	
КонецПроцедуры // СоздатьКолонкиДляДневногоГрафика()

// Разбиение таблицы на кванты и покраска по базовому графику
Процедура СозданиеТаблицыДневногоРасписанияИПокраскаПоБазовомуГрафику(ДневноеРасписание)
	КраситьПоБазовомуГрафику = Истина;
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если обЗначениеНеЗаполнено(БазовыйГрафик) Тогда
		КраситьПоБазовомуГрафику = Ложь;
	Иначе
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаНачала), НачалоДня(ДатаНачала + (60*60*24)));
	КонецЕсли;
	
	Если ТаблицаБазовогоГрафика.Количество() = 0 Тогда
		КраситьПоБазовомуГрафику = Ложь;
	КонецЕсли;
	
	// Оформим таблицу формы
	Если КраситьПоБазовомуГрафику Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Дата", НачалоДня(ДатаНачала));
		МассивСтрокБазовогоГрафика = ТаблицаБазовогоГрафика.НайтиСтроки(СтруктураОтбора);
	КонецЕсли;
	
	ТекущееВремя = '00010101';
	Пока ТекущееВремя < '00010101235959' Цикл
		
		СтрокаДневногоРасписания = ДневноеРасписание.Добавить();
		СтрокаДневногоРасписания.Время = ТекущееВремя;
		
		Если КраситьПоБазовомуГрафику Тогда
			Цвет = "";
			Если МассивСтрокБазовогоГрафика.Количество() <> 0 Тогда
				УстановилиЦвет = Ложь;
				Для Индекс = 0 По МассивСтрокБазовогоГрафика.ВГраница() Цикл
					НачалоРабВремени = МассивСтрокБазовогоГрафика[Индекс].НачалоРабочегоВремени;
					КонецРабВремени = МассивСтрокБазовогоГрафика[Индекс].КонецРабочегоВремени;
					Если КонецРабВремени = '00010101235959' Тогда
						КонецРабВремени = '00010102';
					КонецЕсли;
					
					Если ТекущееВремя >= НачалоРабВремени И (ТекущееВремя + (КвантВремени - '00010101')) <= КонецРабВремени Тогда
						Если НЕ обЗначениеНеЗаполнено(МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала) Тогда
							УстановилиЦвет = Истина;
							Если МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Перечисления.ВидИнтервала.ОбеденныйПерерыв Тогда
								Цвет = "ОбеденныйПерерыв";
							ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Перечисления.ВидИнтервала.РабочееВремя Тогда
								Цвет = "РабочееВремя";
							ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Перечисления.ВидИнтервала.ТехническийПерерыв Тогда
								Цвет = "ТехническийПерерыв";
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ УстановилиЦвет Тогда
					Если МассивСтрокБазовогоГрафика.Количество() <> 0 Тогда
						Если МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Выходной Тогда
							Цвет = "Выходной";
						ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Праздник Тогда
							Цвет = "Праздник";
						ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
							Цвет = "Предпраздничный";
						ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Рабочий Тогда
							Цвет = "НеЗаполнено";
						КонецЕсли;
					Иначе
						Цвет = "НеЗаполнено";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;			
			
			СтрокаДневногоРасписания.ЦветПокраски = Цвет;
		Иначе
			СтрокаДневногоРасписания.ЦветПокраски = "Рабочий";
		КонецЕсли;
		
		// Увеличиваем ТекущееВремя
		Если ((ТекущееВремя - '00010101') + (КвантВремени - '00010101')) >= 86400 Тогда
			ТекущееВремя = '00010101235959';
		Иначе
			ТекущееВремя = ТекущееВремя + (КвантВремени - '00010101');
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // СозданиеТаблицыДневногоРасписанияИПокраскаПоБазовомуГрафику()

// Заполнение таблицы 
Процедура ЗаполнитьТаблицуДневногоРасписания(ДневноеРасписание)
	Для Каждого СтрокаРасписания ИЗ РасписаниеСобытий Цикл
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата)
			И (СтрокаРасписания.НачалоСобытияДата<>СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;
		
		Если Не СтрокаРасписания.Напоминание Тогда
			СоответствуетФильтрам = ПроверитьФильтры(СтрокаРасписания.Событие);	
		Иначе
			СоответствуетФильтрам = Истина;
		КонецЕсли;	
			
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.ДатаСобытия,СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.НачалоСобытияДата,СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
			
			Если Не СтрокаРасписания.Напоминание Тогда
				СоответствуетФильтрам = ПроверитьФильтры(СтрокаРасписания.Событие);	
			Иначе
				СоответствуетФильтрам = Истина;
			КонецЕсли;

			Если СоответствуетФильтрам Тогда
				КоличествоСекундВНачале = СтрокаРасписания.НачалоСобытия - '00010101';
				КоличествоСекундВКванте = КвантВремени - '00010101';
				НомерСтроки = Цел(КоличествоСекундВНачале/КоличествоСекундВКванте);
	            ДатаНачалаСобытияВПериоде =  ?(СтрокаРасписания.НачалоСобытияДата >= ДатаНачала, СтрокаРасписания.НачалоСобытияДата, ДатаНачала);
				ДатаНачалаСобытияВПериоде = ?(ПротяженноеСобытие,ДатаНачалаСобытияВПериоде,СтрокаРасписания.ДатаСобытия);
				
				//распределение протяженных документов
				Если ПротяженноеСобытие Тогда
					Если ДатаНачалаСобытияВПериоде < НачалоДня(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
						КоличествоСекундВКонце = 24*3600;                                          
					Иначе
						КоличествоСекундВКонце = СтрокаРасписания.ОкончаниеСобытия - '00010101';
					КонецЕсли;	
					НомерСтрокиКонец = Цел(КоличествоСекундВКонце/КоличествоСекундВКванте)-1;
				Иначе	
					НомерСтрокиКонец = НомерСтроки;
		        КонецЕсли;

				ВыводитьСобытие = Истина;
				Пока НомерСтроки <= НомерСтрокиКонец Цикл
					Если ВыводитьСобытие Тогда

						Если НЕ обЗначениеНеЗаполнено(ДневноеРасписание[НомерСтроки].ПредставлениеСобытий) Тогда
							ДневноеРасписание[НомерСтроки].ПредставлениеСобытий = ДневноеРасписание[НомерСтроки].ПредставлениеСобытий + Символы.ПС + СтрокаРасписания.ПредставлениеОбъекта;
						Иначе
							ДневноеРасписание[НомерСтроки].ПредставлениеСобытий = СтрокаРасписания.ПредставлениеОбъекта;
							Если ДневноеРасписание[НомерСтроки].ЦветПокраски = "РабочееВремя"
								ИЛИ ДневноеРасписание[НомерСтроки].ЦветПокраски = "Рабочий" 
								ИЛИ ДневноеРасписание[НомерСтроки].ЦветПокраски = "ЗанятоеВремя" Тогда 
								ДневноеРасписание[НомерСтроки].ЦветПокраски = СтрокаРасписания.ЦветПокраски;
							КонецЕсли;	
							ДневноеРасписание[НомерСтроки].ИмяИконки = СтрокаРасписания.ИмяИконки;
						КонецЕсли;
						
						Если СтрокаРасписания.Напоминание Тогда
							СтруктураНапоминания = Новый Структура;
							СтруктураНапоминания.Вставить("Тема", СтрокаРасписания.Тема);
							СтруктураНапоминания.Вставить("Пользователь", СтрокаРасписания.Пользователь);
							СтруктураНапоминания.Вставить("Автор", СтрокаРасписания.Автор);
							СтруктураНапоминания.Вставить("РеальнаяДатаОповещения", СтрокаРасписания.РеальнаяДатаОповещения);
							СтруктураНапоминания.Вставить("Объект", СтрокаРасписания.Объект);
							СтруктураНапоминания.Вставить("Завершено", СтрокаРасписания.Завершено);
							
							ДневноеРасписание[НомерСтроки].МассивОбъектов.Добавить(СтруктураНапоминания);
						Иначе
							ДневноеРасписание[НомерСтроки].МассивОбъектов.Добавить(СтрокаРасписания.Событие);
						КонецЕсли;   
						ВыводитьСобытие = Ложь;

					ИначеЕсли ДневноеРасписание[НомерСтроки].ЦветПокраски = "РабочееВремя" 
						ИЛИ ДневноеРасписание[НомерСтроки].ЦветПокраски = "Рабочий" Тогда 
						ДневноеРасписание[НомерСтроки].ЦветПокраски = "ЗанятоеВремя";
					КонецЕсли;
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;
			КонецЕсли;	
		КонецЕсли	
	КонецЦикла;
КонецПроцедуры // ЗаполнитьТаблицуДневногоРасписания()

// Формирование дневного расписания
Процедура СформироватьДневноеРасписание(ТаблицаДневногоРасписания,НужноОбновить = Истина) Экспорт
		
	// Получение таблицы событий
	ПолучитьТаблицуРасписаний(НужноОбновить);
		
	// Свернём таблицу
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,Событие,Тема,Пользователь,Автор,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки","");
	
	// Создание колонок дневного расписания
	СоздатьКолонкиДляДневногоГрафика(ТаблицаДневногоРасписания);
	
	ДневноеРасписание = ТаблицаДневногоРасписания.Значение;
	
	// Создаем таблицу и красим по графику
	СозданиеТаблицыДневногоРасписанияИПокраскаПоБазовомуГрафику(ДневноеРасписание);
	
	// Выводим события
	ЗаполнитьТаблицуДневногоРасписания(ДневноеРасписание);
	
	// Заполним структуру цветов
	ЗаполнитьСтруктуруЦветов();
КонецПроцедуры // СформироватьДневноеРасписание()

////////////////////////////////////////////////////////////////////
//// НЕДЕЛЯ

// Создание колонок для недельного графика
Процедура СоздатьКолонкиДляНедельногоГрафика(ТаблицаНедельногоРасписания, КоличествоДней);
	ТаблицаНедельногоРасписания.Значение.Очистить();
	ТаблицаНедельногоРасписания.Значение.Колонки.Очистить();
	ТаблицаНедельногоРасписания.Колонки.Очистить();
	
	ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("Время", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)), "Время");
	Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("Время", "Время");
	Колонка.Данные = "Время";
	Колонка.Формат = "ДФ=ЧЧ:мм; ДП=00:00";
	Колонка.Ширина = 7;
	Колонка.ШрифтТекста = Новый Шрифт(,,Истина);
	Колонка.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	Колонка.ПропускатьПриВводе = Истина;
	Колонка.Доступность = Ложь;
	
	Для ПеременнаяЦикла = 0 По КоличествоДней Цикл
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("ПредставлениеСобытий" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "ПредставлениеСобытий" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("ПредставлениеСобытий" + ПеременнаяЦикла,
			Формат(ДатаНачала + (ПеременнаяЦикла*60*60*24), "ДФ='ддд дд МММ'"));
		Колонка.Данные = "ПредставлениеСобытий" + ПеременнаяЦикла;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
			
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("МассивОбъектов" + ПеременнаяЦикла, Новый ОписаниеТипов("Массив"), "МассивОбъектов" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("МассивОбъектов" + ПеременнаяЦикла, "МассивОбъектов" + ПеременнаяЦикла);
		Колонка.Данные = "МассивОбъектов" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("ЦветПокраски" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "ЦветПокраски" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("ЦветПокраски" + ПеременнаяЦикла, "ЦветПокраски" + ПеременнаяЦикла);
		Колонка.Данные = "ЦветПокраски" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
				
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("ИмяИконки" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "ИмяИконки" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("ИмяИконки" + ПеременнаяЦикла, "ИмяИконки" + ПеременнаяЦикла);
		Колонка.Данные = "ИмяИконки" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("СмещениеИконки" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "СмещениеИконки" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("СмещениеИконки" + ПеременнаяЦикла, "СмещениеИконки" + ПеременнаяЦикла);
		Колонка.Данные = "СмещениеИконки" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
	КонецЦикла;
	
	ТаблицаНедельногоРасписания.ФиксацияСлева = 1;
КонецПроцедуры // СоздатьКолонкиДляНедельногоГрафика()

// Разбиение таблицы на кванты и покраска по базовому графику
Процедура СозданиеТаблицыНедельногоРасписанияИПокраскаПоБазовомуГрафику(НедельноеРасписание)
	КраситьПоБазовомуГрафику = Истина;
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если обЗначениеНеЗаполнено(БазовыйГрафик) Тогда
		КраситьПоБазовомуГрафику = Ложь;
	Иначе
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаНачала), НачалоДня(ДатаОкончания));
	КонецЕсли;
	
	Если ТаблицаБазовогоГрафика.Количество() = 0 Тогда
		КраситьПоБазовомуГрафику = Ложь;
	КонецЕсли;
	
	КоличествоДней = (ДатаОкончания - ДатаНачала)/60/60/24;
	
	Для ПеременнаяЦикла = 0 По КоличествоДней Цикл
		
		Если КраситьПоБазовомуГрафику Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Дата", НачалоДня(ДатаНачала) + (ПеременнаяЦикла*60*60*24));
			МассивСтрокБазовогоГрафика = ТаблицаБазовогоГрафика.НайтиСтроки(СтруктураОтбора);
		КонецЕсли;
		
		ТекущееВремя = '00010101';
		Пока ТекущееВремя < '00010101235959' Цикл
			
			Если ПеременнаяЦикла = 0 Тогда
				СтрокаНедельногоРасписания = НедельноеРасписание.Добавить();
				СтрокаНедельногоРасписания.Время = ТекущееВремя;
			Иначе
				СтрокаНедельногоРасписания = НедельноеРасписание.Найти(ТекущееВремя, "Время");
			КонецЕсли;
			
			Если КраситьПоБазовомуГрафику Тогда
				Цвет = "";
				Если МассивСтрокБазовогоГрафика.Количество() <> 0 Тогда
					УстановилиЦвет = Ложь;
					Для Индекс = 0 По МассивСтрокБазовогоГрафика.ВГраница() Цикл
						НачалоРабВремени = МассивСтрокБазовогоГрафика[Индекс].НачалоРабочегоВремени;
						КонецРабВремени = МассивСтрокБазовогоГрафика[Индекс].КонецРабочегоВремени;
						Если КонецРабВремени = '00010101235959' Тогда
							КонецРабВремени = '00010102';
						КонецЕсли;
						
						Если ТекущееВремя >= НачалоРабВремени И (ТекущееВремя + (КвантВремени - '00010101')) <= КонецРабВремени Тогда
							Если НЕ обЗначениеНеЗаполнено(МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала) Тогда
								УстановилиЦвет = Истина;
								Если МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Перечисления.ВидИнтервала.ОбеденныйПерерыв Тогда
									Цвет = "ОбеденныйПерерыв";
								ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Перечисления.ВидИнтервала.РабочееВремя Тогда
									Цвет = "РабочееВремя";
								ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Перечисления.ВидИнтервала.ТехническийПерерыв Тогда
									Цвет = "ТехническийПерерыв";
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
					Если НЕ УстановилиЦвет Тогда
						Если МассивСтрокБазовогоГрафика.Количество() <> 0 Тогда
							Если МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Выходной Тогда
								Цвет = "Выходной";
							ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Праздник Тогда
								Цвет = "Праздник";
							ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
								Цвет = "Предпраздничный";
							ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Рабочий Тогда
								Цвет = "НеЗаполнено";
							КонецЕсли;
						Иначе
							Цвет = "НеЗаполнено";
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				СтрокаНедельногоРасписания["ЦветПокраски" + ПеременнаяЦикла] = Цвет;
			Иначе
				СтрокаНедельногоРасписания["ЦветПокраски" + ПеременнаяЦикла] = "Рабочий";
			КонецЕсли;
			
			// Увеличиваем ТекущееВремя
			Если ((ТекущееВремя - '00010101') + (КвантВремени - '00010101')) >= 86400 Тогда
				ТекущееВремя = '00010101235959';
			Иначе
				ТекущееВремя = ТекущееВремя + (КвантВремени - '00010101');
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // СозданиеТаблицыНедельногоРасписанияИПокраскаПоБазовомуГрафику()

// Заполнение таблицы
Процедура ЗаполнитьТаблицуНедельногоРасписания(НедельноеРасписание)
	КоличествоСекундВКванте = КвантВремени - '00010101';
	
	Для Каждого СтрокаРасписания ИЗ РасписаниеСобытий Цикл	
		//определим, протяженное ли событие
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата)
			И (СтрокаРасписания.НачалоСобытияДата<>СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;
		
		Если Не СтрокаРасписания.Напоминание Тогда
			СоответствуетФильтрам = ПроверитьФильтры(СтрокаРасписания.Событие);	
		Иначе
			СоответствуетФильтрам = Истина;
		КонецЕсли;	

		//определим, входит ли событие в наш период
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала,ДатаОкончания, СтрокаРасписания.ДатаСобытия,СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала,ДатаОкончания, СтрокаРасписания.НачалоСобытияДата,СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
			
			Если Не СтрокаРасписания.Напоминание Тогда
				СоответствуетФильтрам = ПроверитьФильтры(СтрокаРасписания.Событие);	
			Иначе
				СоответствуетФильтрам = Истина;
			КонецЕсли;

			Если СоответствуетФильтрам Тогда
				//получим начальный номер колонки
				ДатаНачалаСобытияВПериоде = ?(СтрокаРасписания.НачалоСобытияДата >= ДатаНачала, СтрокаРасписания.НачалоСобытияДата, ДатаНачала);
				ДатаНачалаСобытияВПериоде = ?(ПротяженноеСобытие,ДатаНачалаСобытияВПериоде,СтрокаРасписания.ДатаСобытия);
				Смещение = (НачалоДня(ДатаНачалаСобытияВПериоде) - ДатаНачала)/60/60/24;
				
				//распределение протяженных документов по столбцам
				Если ПротяженноеСобытие Тогда
					Если ДатаНачалаСобытияВПериоде < НачалоНедели(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
						СмещениеКонец = 6;
					Иначе
						СмещениеКонец = ДеньНедели(СтрокаРасписания.ОкончаниеСобытияДата)-1;
					КонецЕсли;		
				Иначе	
					СмещениеКонец = Смещение;
				КонецЕсли;
				
				Пока Смещение <= СмещениеКонец Цикл
					КоличествоСекундВНачале = СтрокаРасписания.НачалоСобытия - '00010101';
					НомерСтроки = Цел(КоличествоСекундВНачале/КоличествоСекундВКванте);
					Если НЕ обЗначениеНеЗаполнено(НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение]) Тогда
						НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение] = НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение] + Символы.ПС + СтрокаРасписания.ПредставлениеОбъекта;
					Иначе
						НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение] = СтрокаРасписания.ПредставлениеОбъекта;
						Если НедельноеРасписание[НомерСтроки]["ЦветПокраски" + Смещение] = "РабочееВремя" 
							ИЛИ НедельноеРасписание[НомерСтроки]["ЦветПокраски" + Смещение] = "Рабочий" Тогда 
							НедельноеРасписание[НомерСтроки]["ЦветПокраски" + Смещение] =  СтрокаРасписания.ЦветПокраски;
						КонецЕсли;
						НедельноеРасписание[НомерСтроки]["ИмяИконки" + Смещение] = СтрокаРасписания.ИмяИконки;
					КонецЕсли;
					
					Если СтрокаРасписания.Напоминание Тогда
						СтруктураНапоминания = Новый Структура;
						СтруктураНапоминания.Вставить("Тема", СтрокаРасписания.Тема);
						СтруктураНапоминания.Вставить("Пользователь", СтрокаРасписания.Пользователь);
						СтруктураНапоминания.Вставить("Автор", СтрокаРасписания.Автор);
						СтруктураНапоминания.Вставить("РеальнаяДатаОповещения", СтрокаРасписания.РеальнаяДатаОповещения);
						СтруктураНапоминания.Вставить("Объект", СтрокаРасписания.Объект);
						СтруктураНапоминания.Вставить("Завершено", СтрокаРасписания.Завершено);
						
						НедельноеРасписание[НомерСтроки]["МассивОбъектов" + Смещение].Добавить(СтруктураНапоминания);
					Иначе
						НедельноеРасписание[НомерСтроки]["МассивОбъектов" + Смещение].Добавить(СтрокаРасписания.Событие);
					КонецЕсли;				
					Смещение = Смещение + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры // ЗаполнитьТаблицуНедельногоРасписания()

// Формирование недельного расписания
Процедура СформироватьНедельноеРасписание(ТаблицаНедельногоРасписания,НужноОбновить = Истина) Экспорт
	
	// Получение таблицы событий
	ПолучитьТаблицуРасписаний(НужноОбновить);
			
	// Свернём таблицу
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,Событие,Тема,Пользователь,Автор,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки","");
	
	// Создание колонок расписания
	СоздатьКолонкиДляНедельногоГрафика(ТаблицаНедельногоРасписания, (ДатаОкончания - ДатаНачала)/60/60/24);
	
	НедельноеРасписание = ТаблицаНедельногоРасписания.Значение;
	
	// Создаем таблицу и красим по графику
	СозданиеТаблицыНедельногоРасписанияИПокраскаПоБазовомуГрафику(НедельноеРасписание);
	
	// Выводим события
	ЗаполнитьТаблицуНедельногоРасписания(НедельноеРасписание);
	
	// Заполним структуру цветов
	ЗаполнитьСтруктуруЦветов();
КонецПроцедуры // СформироватьНедельноеРасписание()

// Формирование недельной таблицы заказов
Процедура СформироватьТаблицуЗаказов(Заказы) Экспорт
	Заказы.Значение.Очистить();
	Заказы.Значение.Колонки.Очистить();
	Заказы.Колонки.Очистить();

	КоличествоДней = (ДатаОкончания - ДатаНачала)/60/60/24;
		
	Заказы.Значение.Колонки.Добавить("Номер", Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(2,0)), "Номер");
	Колонка = Заказы.Колонки.Добавить("Номер", "Номер");
	Колонка.Данные = "Номер";
	Колонка.Ширина = 7;
	Колонка.ШрифтТекста = Новый Шрифт(,,Истина);
	Колонка.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	Колонка.ЭлементУправления.АвтоПереносСтрок = Ложь;
	Колонка.ВысотаЯчейки = 1;
	Колонка.АвтоВысотаЯчейки = Ложь;
	Колонка.ПропускатьПриВводе = Истина;
	Колонка.Доступность = Ложь;
	
	Для ПеременнаяЦикла = 0 По КоличествоДней Цикл
		Заказы.Значение.Колонки.Добавить("ПредставлениеСобытий" + ПеременнаяЦикла, Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя"), "ПредставлениеСобытий" + ПеременнаяЦикла);
		Колонка = Заказы.Колонки.Добавить("ПредставлениеСобытий" + ПеременнаяЦикла,
			Формат(ДатаНачала + (ПеременнаяЦикла*60*60*24), "ДФ='ддд дд МММ'"));
		Колонка.Данные = "ПредставлениеСобытий" + ПеременнаяЦикла;
		Колонка.ЭлементУправления.АвтоПереносСтрок = Ложь;
		Колонка.ВысотаЯчейки = 1;
		Колонка.АвтоВысотаЯчейки = Ложь;
    КонецЦикла;
		
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(ФильтрКонтрагент) Тогда
        Запрос.УстановитьПараметр("Контрагент",ФильтрКонтрагент);
		СтрокаУсловия = " И ЗаказПокупателя.Контрагент = &Контрагент ";
	Иначе
		СтрокаУсловия = "";
	КонецЕсли;	
	Запрос.УстановитьПараметр("ДатаНач",НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКон",КонецДня(ДатаОкончания));	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Ссылка КАК Ссылка,
	               |	ЗаказПокупателя.СрокПоставки
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.СрокПоставки МЕЖДУ &ДатаНач И &ДатаКон " + СтрокаУсловия + "
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Ссылка)
	               |ПО
	               |	ЗаказПокупателя.СрокПоставки";
	ВыборкаНеделя = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	КоличествоСтрок = 0;
	Пока ВыборкаНеделя.Следующий() Цикл
		Если ВыборкаНеделя.Ссылка > КоличествоСтрок Тогда 
			КоличествоСтрок = ВыборкаНеделя.Ссылка;
		КонецЕсли;
	КонецЦикла;	
    ВыборкаНеделя.Сбросить();
	Для Счетчик = 0 По КоличествоСтрок - 1 Цикл
		Заказы.Значение.Добавить();
	КонецЦикла;
	Пока ВыборкаНеделя.Следующий() Цикл
		Столбец = ДеньНедели(ВыборкаНеделя.СрокПоставки)-ДеньНедели(ДатаНачала)+1;
		ВыборкаДень = ВыборкаНеделя.Выбрать();
		Строка = -1;
		Пока ВыборкаДень.Следующий() Цикл
			Строка = Строка + 1;
		    Заказы.Значение[Строка][Столбец] = ВыборкаДень.Ссылка;
			Заказы.Значение[Строка][0] = СокрЛП(Строка+1);
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицуЗаказов()

////////////////////////////////////////////////////////////////////
//// МЕСЯЦ

// Создание колонок для месячного графика
Процедура СоздатьКолонкиДляМесячногоГрафика(ТаблицаМесячногоРасписания)
	ТаблицаМесячногоРасписания.Значение.Очистить();
	ТаблицаМесячногоРасписания.Значение.Колонки.Очистить();
	ТаблицаМесячногоРасписания.Колонки.Очистить();
		
	Для Индекс = 0 По МассивДнейНедели.ВГраница() Цикл
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ДатаСобытия" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Дата"), "ДатаСобытия" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ДатаСобытия" + МассивДнейНедели[Индекс], "Дата");
		Колонка.Данные = "ДатаСобытия" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ПредставлениеСобытий" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Строка"), "ПредставлениеСобытий" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ПредставлениеСобытий" + МассивДнейНедели[Индекс], МассивДнейНедели[Индекс]);
		Колонка.Данные = "ПредставлениеСобытий" + МассивДнейНедели[Индекс];
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;

		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("МассивОбъектов" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Массив"), "МассивОбъектов" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("МассивОбъектов" + МассивДнейНедели[Индекс], "МассивОбъектов" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "МассивОбъектов" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ЦветПокраски" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Строка"), "ЦветПокраски" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ЦветПокраски" + МассивДнейНедели[Индекс], "ЦветПокраски" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "ЦветПокраски" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ИмяИконки" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Строка"), "ИмяИконки" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ИмяИконки" + МассивДнейНедели[Индекс], "ИмяИконки" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "ИмяИконки" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("СмещениеИконки" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Число"), "СмещениеИконки" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("СмещениеИконки" + МассивДнейНедели[Индекс], "СмещениеИконки" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "СмещениеИконки" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
	КонецЦикла;
КонецПроцедуры // СоздатьКолонкиДляМесячногоГрафика()

// Разбиение таблицы на кванты и покраска по базовому графику
Процедура СозданиеТаблицыМесячногоРасписанияИПокраскаПоБазовомуГрафику(МесячноеРасписание)
	КраситьПоБазовомуГрафику = Истина;
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если обЗначениеНеЗаполнено(БазовыйГрафик) Тогда
		КраситьПоБазовомуГрафику = Ложь;
	Иначе
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаНачала), НачалоДня(ДатаОкончания));
	КонецЕсли;
	
	Если ТаблицаБазовогоГрафика.Количество() = 0 Тогда
		КраситьПоБазовомуГрафику = Ложь;
	КонецЕсли;
	
	НачальныйНомерНедели = НеделяГода(ДатаНачала);
	КонечныйНомерНедели = НеделяГода(ДатаОкончания);
	
	ТекущаяДата = НачалоНедели(ДатаНачала);
	
	Для ПеременнаяЦикла = НачальныйНомерНедели По КонечныйНомерНедели Цикл
		СтрокаМесячногоРасписания = МесячноеРасписание.Добавить();
		
		Для Индекс = 0 По МассивДнейНедели.ВГраница() Цикл
			СтрокаМесячногоРасписания["ДатаСобытия" + МассивДнейНедели[Индекс]] =  ТекущаяДата;
			СтрокаМесячногоРасписания["ИмяИконки" + МассивДнейНедели[Индекс]] = "ДниДляМесяца";
			СтрокаМесячногоРасписания["СмещениеИконки" + МассивДнейНедели[Индекс]] = Число(Формат(ТекущаяДата, "ДФ=дд"))-1;
			Если КраситьПоБазовомуГрафику Тогда
				
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("Дата", ТекущаяДата);
				МассивСтрокБазовогоГрафика = ТаблицаБазовогоГрафика.НайтиСтроки(СтруктураОтбора);
				Цвет = "";
				Если МассивСтрокБазовогоГрафика.Количество() <> 0 Тогда
					
					Если МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Выходной Тогда
						Цвет = "Выходной";
					ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Праздник Тогда
						Цвет = "Праздник";
					ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
						Цвет = "Предпраздничный";
					ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Рабочий Тогда
						Цвет = "Рабочий";
					КонецЕсли;
									
					Если обЗначениеНеЗаполнено(Цвет) Тогда
						Цвет = "НеЗаполнено";
					КонецЕсли;
				Иначе
					Цвет = "НеЗаполнено";
				КонецЕсли;
				
				СтрокаМесячногоРасписания["ЦветПокраски" + МассивДнейНедели[Индекс]] = Цвет;
			Иначе
				СтрокаМесячногоРасписания["ЦветПокраски" + МассивДнейНедели[Индекс]] = "Рабочий";
			КонецЕсли;
			ТекущаяДата = ТекущаяДата + 60*60*24;
			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // СозданиеТаблицыМесячногоРасписанияИПокраскаПоБазовомуГрафику()

// Заполнение таблицы
Процедура ЗаполнитьТаблицуМесячногоРасписания(МесячноеРасписание)
	НачальныйНомерНедели = НеделяГода(ДатаНачала);
		
	Для Каждого СтрокаРасписания ИЗ РасписаниеСобытий Цикл
		//определим, протяженное ли событие
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата)
			И (СтрокаРасписания.НачалоСобытияДата<>СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;	
		
		//определим, входит ли событие в наш период
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала,ДатаОкончания, СтрокаРасписания.ДатаСобытия,СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала,ДатаОкончания, СтрокаРасписания.НачалоСобытияДата,СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
			
			Если Не СтрокаРасписания.Напоминание Тогда
				СоответствуетФильтрам = ПроверитьФильтры(СтрокаРасписания.Событие);	
			Иначе
				СоответствуетФильтрам = Истина;
			КонецЕсли;
		
			Если СоответствуетФильтрам Тогда
	            //получим начальные номера строки и колонки
				ДатаНачалаСобытияВПериоде = ?(СтрокаРасписания.НачалоСобытияДата >= ДатаНачала, СтрокаРасписания.НачалоСобытияДата, ДатаНачала); 
				ДатаНачалаСобытияВПериоде = ?(ПротяженноеСобытие,ДатаНачалаСобытияВПериоде,СтрокаРасписания.ДатаСобытия);
				НомерСтроки = НеделяГода(ДатаНачалаСобытияВПериоде) - НачальныйНомерНедели;
				
				//распределение протяженных документов по строкам
				Если ПротяженноеСобытие Тогда
					Если ДатаНачалаСобытияВПериоде < НачалоМесяца(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
						НомерСтрокиКонец = НеделяГода(ДатаОкончания) - НачальныйНомерНедели; 
					Иначе
						НомерСтрокиКонец = НеделяГода(СтрокаРасписания.ОкончаниеСобытияДата) - НачальныйНомерНедели;
					КонецЕсли;	
				Иначе
					НомерСтрокиКонец = НомерСтроки;
		        КонецЕсли;

				Пока НомерСтроки <= НомерСтрокиКонец Цикл

					Если (НеделяГода(ДатаНачалаСобытияВПериоде) - НачальныйНомерНедели = НомерСтроки) Тогда
						НомерКолонки = ДеньНедели(ДатаНачалаСобытияВПериоде);
					Иначе
						НомерКолонки = 1;
					КонецЕсли;

					//распределение протяженных документов по столбцам
					Если ПротяженноеСобытие Тогда
						Если НачалоНедели(ДатаНачалаСобытияВПериоде+НомерСтроки*3600*24*7) < НачалоНедели(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
							Если НеделяГода(ДатаНачалаСобытияВПериоде+НомерСтроки*3600*24*7)=НеделяГода(ДатаОкончания) Тогда
								НомерКолонкиКонец = ДеньНедели(ДатаОкончания);
							Иначе
								НомерКолонкиКонец = 7;
							КонецЕсли;
						Иначе
							НомерКолонкиКонец = ДеньНедели(СтрокаРасписания.ОкончаниеСобытияДата);
						КонецЕсли;	
					Иначе
						НомерКолонкиКонец = НомерКолонки;
					КонецЕсли;	
						
					Пока НомерКолонки <= НомерКолонкиКонец Цикл
				
						ЯчейкаСобытия = МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]];
						Если НЕ обЗначениеНеЗаполнено(ЯчейкаСобытия) Тогда
							МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]] = МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]] + Символы.ПС + СтрокаРасписания.ПредставлениеОбъекта;
						Иначе
							МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]] = СтрокаРасписания.ПредставлениеОбъекта;
						КонецЕсли;
						
						Если МесячноеРасписание[НомерСтроки]["ЦветПокраски" + МассивДнейНедели[НомерКолонки-1]] = "Рабочий" 
							ИЛИ МесячноеРасписание[НомерСтроки]["ЦветПокраски" + МассивДнейНедели[НомерКолонки-1]] = "Рабочий" Тогда
							МесячноеРасписание[НомерСтроки]["ЦветПокраски" + МассивДнейНедели[НомерКолонки-1]] = "ЗанятоеВремя"; 
						КонецЕсли;	
							
						Если СтрокаРасписания.Напоминание Тогда
							СтруктураНапоминания = Новый Структура;
							СтруктураНапоминания.Вставить("Тема", СтрокаРасписания.Тема);
							СтруктураНапоминания.Вставить("Пользователь", СтрокаРасписания.Пользователь);
							СтруктураНапоминания.Вставить("Автор", СтрокаРасписания.Автор);
							СтруктураНапоминания.Вставить("РеальнаяДатаОповещения", СтрокаРасписания.РеальнаяДатаОповещения);
							СтруктураНапоминания.Вставить("Объект", СтрокаРасписания.Объект);
							СтруктураНапоминания.Вставить("Завершено", СтрокаРасписания.Завершено);
							МесячноеРасписание[НомерСтроки]["МассивОбъектов" + МассивДнейНедели[НомерКолонки-1]].Добавить(СтруктураНапоминания);
						Иначе
							МесячноеРасписание[НомерСтроки]["МассивОбъектов" + МассивДнейНедели[НомерКолонки-1]].Добавить(СтрокаРасписания.Событие);
						КонецЕсли;
						
						НомерКолонки = НомерКолонки + 1;
					КонецЦикла;	
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;	
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры // ЗаполнитьТаблицуМесячногоРасписания()

// Формирование месячного расписания
Процедура СформироватьМесячноеРасписание(ТаблицаМесячногоРасписания,НужноОбновить = Истина) Экспорт
		
	// Получение таблицы событий
	ПолучитьТаблицуРасписаний(НужноОбновить);
		
	// Свернём таблицу
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,Событие,Тема,Пользователь,Автор,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки, Завершено","");
	
	// Создание колонок расписания
	СоздатьКолонкиДляМесячногоГрафика(ТаблицаМесячногоРасписания);
	
	МесячноеРасписание = ТаблицаМесячногоРасписания.Значение;
	
	// Создаем таблицу и красим по графику
	СозданиеТаблицыМесячногоРасписанияИПокраскаПоБазовомуГрафику(МесячноеРасписание);
	
	// Выводим события
	ЗаполнитьТаблицуМесячногоРасписания(МесячноеРасписание);
	
	// Заполним структуру цветов
	ЗаполнитьСтруктуруЦветов();
КонецПроцедуры // СформироватьМесячноеРасписание()

#КонецЕсли
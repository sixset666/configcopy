// Модуль объекта обработки "АРМ Календарь"

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем Права Экспорт; 							// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем МассивДнейНедели Экспорт;					// массив с днями недели
Перем ЦветаРаскраски Экспорт;				// структура для раскраски элементов формы
Перем СтруктураКартинок Экспорт;				// структура с картинками
Перем СтруктураДобавляемыхДокументов Экспорт; // структура документов, добавляемых с помощью подменю "Добавить" командной панели формы
Перем СтруктураЗначенийФильтров Экспорт;		// хранит значения фильтров
Перем КэшРасписаниеСобытий Экспорт;				// Кэш табличной части Расписание событий


///////////////////////////////////////////////////////////////////////////////
//	ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Получает общие настройки АРМ и значения для них по умолчанию
//
Функция ПолучитьСписокНастроекАРМ()
	
	СтруктураНастроек = Новый Структура;
	Если ЗначениеЗаполнено(ПараметрыСеанса.Пользователь) 
		И ЗначениеЗаполнено(ПараметрыСеанса.Пользователь.Подразделение) Тогда
		СтруктураНастроек.Вставить("БазовыйГрафик", ПараметрыСеанса.Пользователь.Подразделение.ГрафикРаботы);
	КонецЕсли;
	СтруктураНастроек.Вставить("ИнтервалКалендаря", 60);
	СтруктураНастроек.Вставить("ИспользованиеГрафиковРабот", 2);
	СтруктураНастроек.Вставить("РежимКалендаря","Неделя");	
	СтруктураНастроек.Вставить("СкрытьНапоминания",Ложь);	
	СтруктураНастроек.Вставить("СкрытьВыполненные",Ложь);		
	СтруктураНастроек.Вставить("ФильтрКонтрагент",Неопределено);	
	СтруктураНастроек.Вставить("ФильтрКонтактноеЛицо",Неопределено);	
	СтруктураНастроек.Вставить("ФильтрАвтор",Неопределено);	
	СтруктураНастроек.Вставить("ФильтрПодразделение",Неопределено);	
	СтруктураНастроек.Вставить("ФильтрОснование",Неопределено);	
	СтруктураНастроек.Вставить("ФильтрВидСобытия",Неопределено);
	СтруктураНастроек.Вставить("СписокДобавляемыхДокументов",Новый СписокЗначений);	
	СтруктураНастроек.Вставить("СписокОтображаемыхДокументов",Новый СписокЗначений);		
	Возврат СтруктураНастроек;
	
КонецФункции

// Сохраняет настройки АРМ в хранилище настроек 
//
Процедура СохранитьНастройкиОбщие() Экспорт
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("БазовыйГрафик", БазовыйГрафик);
	СтруктураНастроек.Вставить("ИнтервалКалендаря", ИнтервалКалендаря);
	СтруктураНастроек.Вставить("ИспользованиеГрафиковРабот", ИспользованиеГрафиковРабот);
	СтруктураНастроек.Вставить("РежимКалендаря",РежимКалендаря);	
	СтруктураНастроек.Вставить("СкрытьНапоминания",СкрытьНапоминания);	
	СтруктураНастроек.Вставить("СкрытьВыполненные",СкрытьВыполненные);		
	СтруктураНастроек.Вставить("ФильтрКонтрагент",ФильтрКонтрагент);	
	СтруктураНастроек.Вставить("ФильтрКонтактноеЛицо",ФильтрКонтактноеЛицо);	
	СтруктураНастроек.Вставить("ФильтрАвтор",ФильтрАвтор);	
	СтруктураНастроек.Вставить("ФильтрПодразделение",ФильтрПодразделение);	
	СтруктураНастроек.Вставить("ФильтрОснование",ФильтрОснование);	
	СтруктураНастроек.Вставить("ФильтрВидСобытия",ФильтрВидСобытия);
	СтруктураНастроек.Вставить("СписокДобавляемыхДокументов",СписокДобавляемыхДокументов);	
	СтруктураНастроек.Вставить("СписокОтображаемыхДокументов",СписокОтображаемыхДокументов);		
	ХранилищеСистемныхНастроек.Сохранить("Обработка.АРМКалендарь/Общие",, СтруктураНастроек);
	
КонецПроцедуры

// Восстанавливает настройки АРМ из хранилища настроек или устанавливает значения по умолчанию
//
Процедура ВосстановитьНастройкиОбщие() Экспорт
	
	СохраненныеНастройки = ХранилищеСистемныхНастроек.Загрузить("Обработка.АРМКалендарь/Общие");
	Если СохраненныеНастройки = Неопределено ИЛИ ТипЗнч(СохраненныеНастройки) <> Тип("Структура") Тогда
		СохраненныеНастройки = Новый Структура;
	КонецЕсли;
	
	НастройкиПоУмолчанию = ПолучитьСписокНастроекАРМ();
	Для Каждого ТекНастройка Из НастройкиПоУмолчанию Цикл
		Если СохраненныеНастройки.Свойство(ТекНастройка.Ключ) Тогда
			СохраненнаяНастройка = СохраненныеНастройки[ТекНастройка.Ключ];
		КонецЕсли;
		ЭтотОбъект[ТекНастройка.Ключ] = ?(ЗначениеЗаполнено(СохраненнаяНастройка),СохраненнаяНастройка,ТекНастройка.Значение);	
	КонецЦикла;
	
КонецПроцедуры

// Процедура предназначена для создания документа событие.
//
// Параметры:
//  НаДату 		- Дата на которую создается документ,
//  ВидСобытия  - Перечисление виды событий, подставляется в документ, если не заполнено подставляется прочее событие
Процедура СоздатьСобытие(НаДату, ВидСобытия = Неопределено, Основание=Неопределено, СсылкаНаСобытие=Неопределено, Тема=Неопределено) Экспорт
	
	// Создаем документ
	ДокументСобытие = Документы.Событие.СоздатьДокумент();
	
	// Заполняем документ
	ДокументСобытие.Заполнить(Основание);
	ДокументСобытие.ДатаНачала		= НаДату;
	ДокументСобытие.ДатаОкончания	= НаДату+600;  
	ДокументСобытие.Дата			= НаДату;
	ДокументСобытие.Тема			= Тема;
	Если обЗначениеНеЗаполнено(ВидСобытия) Тогда
		ДокументСобытие.ВидСобытия	= Перечисления.ВидыСобытий.Прочее;
	Иначе
		ДокументСобытие.ВидСобытия	= ВидСобытия;
	КонецЕсли;
	
	// Открываем
	ФормаДокумента = ДокументСобытие.ПолучитьФорму("ФормаДокумента");
	Если НЕ ФормаДокумента.Открыта() Тогда
		ФормаДокумента.Открыть();
		СсылкаНаСобытие	= ДокументСобытие.Ссылка;
	КонецЕсли;
	
КонецПроцедуры // СоздатьСобытие()

// Процедура предназначена для создания напоминания.
//
// Параметры:
//  НаДату 		- Дата на которую создается документ,
Процедура СоздатьНапоминание(НаДату,Тема) Экспорт
	ФормаРегистра = РегистрыСведений.Напоминания.ПолучитьФормуРедактированияЗаписи();
	ФормаРегистра.ЭтотОбъект.Пользователь = ПараметрыСеанса.Пользователь;
	ФормаРегистра.ЭтотОбъект.Автор = ПараметрыСеанса.Пользователь;
	ФормаРегистра.ЭтотОбъект.Завершено = Ложь;
	ФормаРегистра.ЭтотОбъект.Тема = Тема;
	ФормаРегистра.РеальнаяДатаОповещения = НаДату;
	ФормаРегистра.ЭтотОбъект.ДатаНачала = НаДату;
	ФормаРегистра.ЭтотОбъект.ДатаОповещения = НаДату;
	ФормаРегистра.Открыть();
КонецПроцедуры // СоздатьНапоминание()

// открытие напоминания
//
Процедура ОткрытьНапоминание(Данные) Экспорт
	МенеджерЗаписи	= РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Данные);
	МенеджерЗаписи.Завершено = Ложь;
	МенеджерЗаписи.Прочитать();
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Данные);
		МенеджерЗаписи.Завершено = Истина;
		МенеджерЗаписи.Прочитать();
	КонецЕсли;
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.ПолучитьФорму().Открыть();
	КонецЕсли;
КонецПроцедуры // ОткрытьНапоминание()

// заполнить структуру значений фильтров
//
Процедура ЗаполнитьСтруктуруЗначенийФильтров() Экспорт
	СтруктураЗначенийФильтров = Новый Структура();
	Если ЗначениеЗаполнено(ФильтрКонтрагент) Тогда
		СтруктураЗначенийФильтров.Вставить("Контрагент",ФильтрКонтрагент);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрКонтактноеЛицо) Тогда
		СтруктураЗначенийФильтров.Вставить("КонтактноеЛицо",ФильтрКонтактноеЛицо);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрАвтор) Тогда
		СтруктураЗначенийФильтров.Вставить("Автор",ФильтрАвтор);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрПодразделение) Тогда
		СтруктураЗначенийФильтров.Вставить("ПодразделениеКомпании",ФильтрПодразделение);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрОснование) Тогда
		СтруктураЗначенийФильтров.Вставить("ДокументОснование",ФильтрОснование);
	КонецЕсли;
	Если ЗначениеЗаполнено(ФильтрВидСобытия) Тогда
		СтруктураЗначенийФильтров.Вставить("ВидСобытия",ФильтрВидСобытия);
	КонецЕсли;
КонецПроцедуры // ЗаполнитьСтруктуруЗначенийФильтров()

// формирование списка событий
//
Процедура СформироватьСписокСобытий(ТаблицаСпискаСобытий, НужноОбновить = Истина) Экспорт
	ПолучитьТаблицуРасписаний(НужноОбновить);
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,ПредставлениеОбъекта2,Событие,Тема,Пользователь,Автор,ПодразделениеКомпании,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки,Завершено");
	МассивСтрок	= Новый Массив();
	ТаблицаРасписания = РасписаниеСобытий.Выгрузить();
    Для Каждого СтрокаРасписания ИЗ ТаблицаРасписания Цикл
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаАвтомобиль")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.РабочийЛист")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;
			
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.ДатаСобытия,СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.НачалоСобытияДата,СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
			Если СтрокаРасписания.Напоминание Тогда
				Если ПроверитьФильтрыДляНапоминания(СтрокаРасписания) Тогда
	        		МассивСтрок.Добавить(СтрокаРасписания);			
				КонецЕсли;
			Иначе
				Если ПроверитьФильтрыДляДокумента(СтрокаРасписания.Событие) Тогда
	        		МассивСтрок.Добавить(СтрокаРасписания);			
				КонецЕсли;
			КонецЕсли;
        КонецЕсли;	
	КонецЦикла;
	Если ТипЗнч(ТаблицаСпискаСобытий.Значение)=Тип("ОбработкаТабличнаяЧасть.АРМКалендарь.РасписаниеСобытий") Тогда
		ТаблицаСпискаСобытий.Значение.Загрузить(ТаблицаРасписания.Скопировать(МассивСтрок));
	Иначе
		ТаблицаСпискаСобытий.Значение	= ТаблицаРасписания.Скопировать(МассивСтрок);
	КонецЕсли;
КонецПроцедуры // СформироватьСписокСобытий()

// Получение таблицы значимых событий
//
Процедура ПолучитьТаблицуРасписаний(НужноОбновить = Истина) Экспорт
	Если НужноОбновить ИЛИ НЕ ЗначениеЗаполнено(КэшРасписаниеСобытий) Тогда
		Запрос	= Новый Запрос();
		
		//условия для напоминаний
		ТекстЗапросаГдеДляНапоминания = ?(СкрытьВыполненные, "И НЕ Напоминания.Завершено", "");
		
		//Формирование текста запроса по отображаемым документам
		Если СписокОтображаемыхДокументов.Количество()=0 Тогда
			ЗаполнитьСписокОтображаемыхДокументовИзМакета();
		КонецЕсли;
		
		ТекстЗапросаПоОтображаемымДокументам	= "";
		Для Каждого Документ Из СписокОтображаемыхДокументов Цикл
			Документ	= Документ.Значение;				
			ТекстЗапросаПоОтображаемымДокументам = ТекстЗапросаПоОтображаемымДокументам + ПолучитьТекстЗапросаОбъединить(Документ);
		КонецЦикла;	
		
		// Сформируем текст запроса по напоминаниям и объединим с запросом по документам
		ТекстЗапроса = "ВЫБРАТЬ
		               |	НАЧАЛОПЕРИОДА(Напоминания.РеальнаяДатаОповещения, ДЕНЬ) КАК ДатаСобытия,
		               |	ИСТИНА КАК Напоминание,
		               |	Напоминания.РеальнаяДатаОповещения КАК НачалоСобытия,
		               |	Напоминания.РеальнаяДатаОповещения КАК ОкончаниеСобытия,
		               |	Напоминания.РеальнаяДатаОповещения КАК НачалоСобытияДата,
		               |	Напоминания.РеальнаяДатаОповещения КАК ОкончаниеСобытияДата,
		               |	Напоминания.Тема КАК ПредставлениеОбъекта,
		               |	Напоминания.Тема КАК ПредставлениеОбъекта2,
		               |	""Напоминание"" КАК Событие,
		               |	""Напоминание"" КАК ВидСобытия,
		               |	Напоминания.Тема КАК Тема,
		               |	Напоминания.Пользователь КАК Пользователь,
		               |	Напоминания.Автор КАК Автор,
		               |	Напоминания.Автор.Подразделение КАК ПодразделениеКомпании,
		               |	Напоминания.РеальнаяДатаОповещения КАК РеальнаяДатаОповещения,
		               |	Напоминания.Объект КАК Объект,
		               |	""Напоминание"" КАК ЦветПокраски,
		               |	""Оповещение"" КАК ИмяИконки,
		               |	Напоминания.Завершено КАК Завершено
		               |ИЗ
		               |	РегистрСведений.Напоминания КАК Напоминания
		               |ГДЕ
		               |	Напоминания.Пользователь = &Пользователь " + ТекстЗапросаГдеДляНапоминания + ТекстЗапросаПоОтображаемымДокументам +
		" УПОРЯДОЧИТЬ ПО
		|	ДатаСобытия,
		|	НачалоСобытия";
		
		Запрос.Текст = ТекстЗапроса;
		
		СписокСостоянийВыполняется = Новый СписокЗначений;
		СписокСостоянийВыполняется.Добавить(Перечисления.СостоянияСобытий.Выполняется);
		СписокСостоянийВыполняется.Добавить(Перечисления.СостоянияСобытий.Запланировано);
		
		Запрос.УстановитьПараметр("Пользователь", ПользовательКалендаря);
		Запрос.УстановитьПараметр("СотрудникПользователь", ПользовательКалендаря.Сотрудник);
		Запрос.УстановитьПараметр("СписокСостоянийВыполняется", СписокСостоянийВыполняется);
		
		// Выгружаем данные
		РасписаниеСобытий.Загрузить(Запрос.Выполнить().Выгрузить());
		
		ЗаполнитьИменаИконокВТаблицеРасписаний();
		
		КэшРасписаниеСобытий = РасписаниеСобытий.Выгрузить();
	Иначе
		Если ЗначениеЗаполнено(КэшРасписаниеСобытий) Тогда
			РасписаниеСобытий.Загрузить(КэшРасписаниеСобытий);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры // ПолучитьТаблицуРасписаний()

// Заполняет имена иконок в таблице расписание
//
Процедура ЗаполнитьИменаИконокВТаблицеРасписаний()
	Для Каждого СтрокаРасписания Из РасписаниеСобытий Цикл
		Если ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументССылка.Событие") Тогда
			СтрокаРасписания.ПредставлениеОбъекта = Формат(СтрокаРасписания.НачалоСобытия, "ДФ=ЧЧ:мм; ДП=00:00") + " " + СтрокаРасписания.Тема;
			СтрокаРасписания.ПредставлениеОбъекта2 = Формат(СтрокаРасписания.НачалоСобытия, "ДФ=ЧЧ:мм; ДП=00:00") + " (" + Формат(СтрокаРасписания.ДатаСобытия, "ДФ=dd.MM.yy") +  ") " + СтрокаРасписания.Тема;
			СтрокаРасписания.ИмяИконки = Перечисления.ВидыСобытий.ПолучитьИмяКартинки(СтрокаРасписания.ВидСобытия);
		ИначеЕсли НЕ СтрокаРасписания.Напоминание Тогда 
			ПозицияНачалаДаты = Найти(СтрокаРасписания.ПредставлениеОбъекта," от "); 
			Если ПозицияНачалаДаты <> 0 Тогда
				СтрокаРасписания.ПредставлениеОбъекта = Лев(СтрокаРасписания.ПредставлениеОбъекта,ПозицияНачалаДаты-1);
			КонецЕсли;	
		КонецЕсли;		
	КонецЦикла;
КонецПроцедуры //ЗаполнитьИменаИконокВТаблицеРасписаний()	

// получить текст запроса объединить
//
Функция ПолучитьТекстЗапросаОбъединить(Документ, ПереданаТЧ = Ложь)
	//документ будет выведен, если этому не помешают настройки принадлежности пользователю 
	ВыводитьДокумент = Истина;
		
	//если документ - имя ТЧ, то к нему добавляем ".Ссылка"
	СтрокаСсылка = ?(ПереданаТЧ,".Ссылка","");
	Если Документ = "Событие" ИЛИ (Найти(Документ,"ЗаявкаНаРемонт")>0) ИЛИ (Найти(Документ,"ЗаказНаряд")>0) Тогда 
		ПолеНачалоСобытия = СтрЗаменить(Документ,".","")+СтрокаСсылка+".ДатаНачала";
		ПолеОкончаниеСобытия = СтрЗаменить(Документ,".","")+СтрокаСсылка+".ДатаОкончания"; 
	Иначе
		ПолеНачалоСобытия = Документ+".Дата";
		ПолеОкончаниеСобытия = Документ+".Дата"; 
	КонецЕсли;
	
	//у событий есть тема, у других документов ее нет
	Если Документ = "Событие" Тогда
    	ПолеТема = Документ+".Тема";
	Иначе
		ПолеТема = "NULL";
	КонецЕсли;
	
	// условия по фильтрам
	ТекстЗапросаПоРесурсамДляДокумента = "";
	
	// условие для только актуальных событий
	ЭтоСобытие = (Документ = "Событие");
	Если Документ = "Событие" И СкрытьВыполненные Тогда 
		ТекстЗапросаГдеДляДокумента = ?(ПустаяСтрока(ТекстЗапросаПоРесурсамДляДокумента)," ГДЕ ", ТекстЗапросаПоРесурсамДляДокумента + " И ") + Документ + ".Состояние В (&СписокСостоянийВыполняется)";
	КонецЕсли;

	ЕстьКонтрагент = Ложь;
	Если Метаданные.Документы[Документ].Реквизиты.Найти("Контрагент")<>Неопределено Тогда
		ЕстьКонтрагент = Истина;
	КонецЕсли;
	
	// собираем текст запроса по документам
	Если ВыводитьДокумент Тогда
		ТекстЗапросаПоОтображаемымДокументам = " 
		|ОБЪЕДИНИТЬ ВСЕ 
		|	ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА("+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Дата, ДЕНЬ),
		|	ЛОЖЬ,
		|	"+ПолеНачалоСобытия+",
		|	"+ПолеОкончаниеСобытия+",
		|	"+ПолеНачалоСобытия+",
		|	"+ПолеОкончаниеСобытия+",
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Представление,
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Представление,
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Ссылка,
		|	"+?(ЭтоСобытие,СтрЗаменить(Документ,".","")+СтрокаСсылка+".ВидСобытия","NULL") + ",
		|	"+ПолеТема+",
		|	NULL,
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".Автор,
		|	"+СтрЗаменить(Документ,".","")+СтрокаСсылка+".ПодразделениеКомпании,
		|	NULL,
		|	"+?(ЕстьКонтрагент,СтрЗаменить(Документ,".","")+СтрокаСсылка+".Контрагент","NULL") + ",
		|	""ЗагрузкаМин"",
		|	""Документ"",
		|	"+?(ЭтоСобытие,СтрЗаменить(Документ,".","")+СтрокаСсылка+".Состояние","ЛОЖЬ") + "
		|ИЗ
		|	Документ."+Документ+" КАК "+СтрЗаменить(Документ,".","")+" " + ТекстЗапросаГдеДляДокумента;	
	КонецЕсли;	
	Возврат ТекстЗапросаПоОтображаемымДокументам;
КонецФункции // ПолучитьТекстЗапросаОбъединить()

// проверить фильтры
//
Функция ПроверитьФильтрыДляДокумента(ДокументСсылка) Экспорт
	Если СтруктураЗначенийФильтров.Количество()=0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ДокументОбъект		= ДокументСсылка.ПолучитьОбъект();
	ДокументМетаданные	= ДокументОбъект.Метаданные();
	Для Каждого Фильтр Из СтруктураЗначенийФильтров Цикл
		
		ПромежРез = Ложь;
		РеквизитНайден = Ложь;
		
		Если Фильтр.Ключ = "ДокументОснование" Тогда
			Если ДокументМетаданные.Реквизиты.Найти("ДокументОснование")<>Неопределено И
				ТипЗнч(Фильтр.Значение)=Тип("СписокЗначений") Тогда
				РеквизитНайден = Истина;	
				Для Каждого ТекДокумент Из Фильтр.Значение Цикл
					Если ДокументОбъект[Фильтр.Ключ] = ТекДокумент.Значение Тогда
						ПромежРез = Истина;
						Продолжить;
					КонецЕсли;		
				КонецЦикла;
			КонецЕсли;
		ИначеЕсли Фильтр.Ключ = "Автор" Тогда
			//реквизит шапки
			Если ДокументМетаданные.Реквизиты.Найти(Фильтр.Ключ)<>Неопределено Тогда
				РеквизитНайден = Истина;	
				Если ДокументОбъект[Фильтр.Ключ] = Фильтр.Значение Тогда
					ПромежРез = Истина;
					Продолжить;
				КонецЕсли;		
			КонецЕсли;
			//реквизит табличной части
			Если НЕ ПромежРез И ДокументМетаданные.ТабличныеЧасти.Найти("Пользователи")<>Неопределено Тогда
				РеквизитНайден = Истина;	
				Для Каждого СтрокаПользователей Из ДокументОбъект["Пользователи"] Цикл
					Если СтрокаПользователей.Пользователь = Фильтр.Значение Тогда
					ПромежРез = Истина;
					Продолжить;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе 
			Если ДокументМетаданные.Реквизиты.Найти(Фильтр.Ключ)<>Неопределено Тогда
				РеквизитНайден = Истина;	
				Если ДокументОбъект[Фильтр.Ключ] = Фильтр.Значение Тогда
					ПромежРез = Истина;
					Продолжить;
				КонецЕсли;		
			КонецЕсли;
		КонецЕсли;
		
		//реквизита нет в документе - документ не соответствует фильтру
		Если НЕ РеквизитНайден Тогда
			Возврат Ложь;	
		КонецЕсли;
		
		//значение реквизита документа не равно значению фильтра
		Если Не ПромежРез Тогда
			Возврат Ложь;
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат Истина;
КонецФункции //ПроверитьФильтрыДляДокумента()

// проверить фильтры
//
Функция ПроверитьФильтрыДляНапоминания(СтрокаРасписания)
	Если СкрытьНапоминания Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СтруктураЗначенийФильтров.Количество()=0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого Фильтр Из СтруктураЗначенийФильтров Цикл
		
		ПромежРез = Ложь;
		РеквизитНайден = Ложь;
		
		Если Фильтр.Ключ = "ПодразделениеКомпании" ИЛИ Фильтр.Ключ = "Автор" Тогда
			РеквизитНайден = Истина;	
			Если СтрокаРасписания[Фильтр.Ключ] = Фильтр.Значение Тогда
				ПромежРез = Истина;
				Продолжить;
			КонецЕсли;		
		ИначеЕсли (Фильтр.Ключ = "Контрагент" ИЛИ Фильтр.Ключ = "КонтактноеЛицо") И ТипЗнч(СтрокаРасписания.Объект) = ТипЗнч(Фильтр.Значение) Тогда
			РеквизитНайден = Истина;	
			Если СтрокаРасписания.Объект = Фильтр.Значение Тогда
				ПромежРез = Истина;
				Продолжить;
			КонецЕсли;		
		КонецЕсли;
		
		//реквизита нет в документе - документ не соответствует фильтру
		Если НЕ РеквизитНайден Тогда
			Возврат Ложь;	
		КонецЕсли;
		
		//значение реквизита документа не равно значению фильтра
		Если Не ПромежРез Тогда
			Возврат Ложь;
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат Истина;
КонецФункции //ПроверитьФильтрыДляДокумента()

// Заполнение структуры цветов
//
Процедура ПолучитьЦветаРаскраски() Экспорт
	ЦветаРаскраски = Новый Соответствие;
	МакетОформления = Справочники.ГрафикиРаботы.ПолучитьМакет("МакетОформления");
	ОбластьОформления = МакетОформления.ПолучитьОбласть("Оформление");
	Для ПеременнаяЦикла = 1 По ОбластьОформления.ВысотаТаблицы Цикл
		ТекущаяОбласть = ОбластьОформления.Область(ПеременнаяЦикла, 1);
		Если обЗначениеНеЗаполнено(ТекущаяОбласть.Текст) Тогда
			Продолжить;
		КонецЕсли;
		ЦветаРаскраски.Вставить(ТекущаяОбласть.Текст, ТекущаяОбласть.ЦветФона);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьСтруктуруЦветов()

// Возвращает цвет раскраски ячейки в зависимости от вида интервала
//
Функция ПолучитьЦветРаскраскиИнтервала(МассивСтрокБазовогоГрафика, ТекущееВремя)
	
	Цвет = "";
	УстановилиЦвет = Ложь;
	
	Если МассивСтрокБазовогоГрафика.Количество() <> 0 Тогда
		Для Индекс = 0 По МассивСтрокБазовогоГрафика.ВГраница() Цикл
			НачалоРабВремени = МассивСтрокБазовогоГрафика[Индекс].НачалоРабочегоВремени;
			КонецРабВремени = МассивСтрокБазовогоГрафика[Индекс].КонецРабочегоВремени;
			Если КонецРабВремени = '00010101235959' Тогда
				КонецРабВремени = '00010102';
			КонецЕсли;
			Если ТекущееВремя >= НачалоРабВремени И (ТекущееВремя + ИнтервалКалендаря*60) <= КонецРабВремени Тогда
				Если ЗначениеЗаполнено(МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала) Тогда
					УстановилиЦвет = Истина;
					Если МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Справочники.ВидыИнтервалов.Работа Тогда
						Цвет = "Работа";
					ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Справочники.ВидыИнтервалов.ОбеденныйПерерыв Тогда
						Цвет = "ОбеденныйПерерыв";
					ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала = Справочники.ВидыИнтервалов.ТехническийПерерыв Тогда
						Цвет = "ТехническийПерерыв";
					ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидИнтервала.РабочийИнтервал Тогда
						Цвет = "Работа";	
					Иначе
						Цвет = "НерабочийИнтервал";	
					КонецЕсли;
				Иначе
					УстановилиЦвет = Истина;
					Если МассивСтрокБазовогоГрафика[Индекс].ВидДня = Перечисления.ВидДня.Выходной Тогда
						Цвет = "Выходной";
					ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидДня = Перечисления.ВидДня.Праздник Тогда
						Цвет = "Праздник";
					ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
						Цвет = "Предпраздничный";
					ИначеЕсли МассивСтрокБазовогоГрафика[Индекс].ВидДня = Перечисления.ВидДня.Рабочий Тогда
						Цвет = "НеЗаполнено";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		
	КонецЕсли;	
	
	Если НЕ УстановилиЦвет Тогда
		Цвет = "НеЗаполнено";	
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции

// заполнить список документов из макета
//
Процедура ЗаполнитьСписокДобавляемыхДокументовИзМакета() Экспорт
	СписокДобавляемыхДокументов.Очистить();
	МакетДокументов = ПолучитьМакет("МакетДокументов");
	
	ОбластьДокументов = МакетДокументов.ПолучитьОбласть("Документы");
	Для ПеременнаяЦикла = 1 По ОбластьДокументов.ВысотаТаблицы Цикл
		ИмяОбъекта = ОбластьДокументов.Область(ПеременнаяЦикла, 1).Текст;
		ПредставлениеОбъекта = ОбластьДокументов.Область(ПеременнаяЦикла, 2).Текст;
		
		Если ИмяОбъекта <> "" Тогда
			Если СписокДобавляемыхДокументов.НайтиПоЗначению(ИмяОбъекта) = Неопределено Тогда
				СписокДобавляемыхДокументов.Добавить(ИмяОбъекта,ПредставлениеОбъекта);
			КонецЕсли;
		КонецЕсли;	
    КонецЦикла;
	
	СписокДобавляемыхДокументов.Добавить("Напоминание","Напоминание");
КонецПроцедуры // ЗаполнитьСписокДобавляемыхДокументовИзМакета()	

// Заполнение списка отображаемых документов из макета
//
Процедура ЗаполнитьСписокОтображаемыхДокументовИзМакета() Экспорт
	СписокОтображаемыхДокументов.Очистить();
	МакетДокументов				= ПолучитьМакет("МакетДокументов");
	ОбластьДокументовПросмотра	= МакетДокументов.ПолучитьОбласть("ДокументыПросмотр");
	Для ПеременнаяЦикла = 1 По ОбластьДокументовПросмотра.ВысотаТаблицы Цикл
		ИмяДокумента = ОбластьДокументовПросмотра.Область(ПеременнаяЦикла, 1).Текст;
		ПредставлениеДокумента = ОбластьДокументовПросмотра.Область(ПеременнаяЦикла, 2).Текст;
		Если ИмяДокумента<>"" Тогда 
			Если СписокОтображаемыхДокументов.НайтиПоЗначению(ИмяДокумента) = Неопределено Тогда
				СписокОтображаемыхДокументов.Добавить(ИмяДокумента,ПредставлениеДокумента);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры // ЗаполнитьСписокОтображаемыхДокументовИзМакета()

// Заполняет структуру документов из списка
Функция ЗаполнитьСтруктуруДобавляемыхДокументовИзСписка() Экспорт
	СтруктураДобавляемыхДокументов = Новый Структура;
	Для Каждого Документ Из СписокДобавляемыхДокументов Цикл
		Если Документ.Значение <> "Напоминание" Тогда
			СтруктураДобавляемыхДокументов.Вставить("Новый" + Документ.Значение, Новый Структура("Объект, ИмяОбъекта", "Документ", Документ.Значение));
		КонецЕсли;	
	КонецЦикла;
	СтруктураДобавляемыхДокументов.Вставить("НовыйНапоминание", Новый Структура("Объект, ИмяОбъекта", "РегистрСведений", "Напоминание"));
	Возврат СтруктураДобавляемыхДокументов;
КонецФункции // ЗаполнитьСписокДобавляемыхДокументовИзМакета()	

// Процедура проверяет пересечение периодов
//
Функция ПериодыПересекаются(ДатаНачала1,ДатаКонца1,ДатаНачала2,ДатаКонца2)
	Если (ДатаНачала1<=ДатаКонца1) И (ДатаНачала2<=ДатаКонца2) Тогда
        ДатаН = Макс(ДатаНачала1,ДатаНачала2);
        ДатаК = Мин(ДатаКонца1,ДатаКонца2);
        Если ДатаН<=ДатаК Тогда
            Рез=Истина;
        Иначе
            Рез=Ложь;
        КонецЕсли;    
    Иначе
        Рез = Ложь;
    КонецЕсли;
    Возврат Рез;
КонецФункции //ПериодыПересекаются()


////////////////////////////////////////////////////////////////////
// ДЕНЬ

#Если Клиент Тогда

// Создание колонок для дневного графика
//
Процедура СоздатьКолонкиДляДневногоГрафика(ТаблицаДневногоРасписания)
	
	// Очищаем таблицу
	ТаблицаДневногоРасписания.Значение.Очистить();
	ТаблицаДневногоРасписания.Значение.Колонки.Очистить();
	ТаблицаДневногоРасписания.Колонки.Очистить();
	
	// Создание колонок
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("Время", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)), "Время");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("Время", "Время");
	Колонка.Данные			= "Время";
	Колонка.Формат			= "ДФ=ЧЧ:мм; ДП=00:00";
	Колонка.Ширина			= 7;
	Колонка.ШрифтТекста		= Новый Шрифт(,,Истина);
	Колонка.ИзменениеРазмера= ИзменениеРазмераКолонки.НеИзменять;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("ПредставлениеСобытий", Новый ОписаниеТипов("Строка"), "ПредставлениеСобытий");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("ПредставлениеСобытий", "События");
	Колонка.Данные			= "ПредставлениеСобытий";
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
		
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("МассивОбъектов", Новый ОписаниеТипов("Массив"), "МассивОбъектов");
	Колонка					= ТаблицаДневногоРасписания.Колонки.Добавить("МассивОбъектов", "МассивОбъектов");
	Колонка.Данные			= "МассивОбъектов";
	Колонка.Видимость		= Ложь;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("ЦветПокраски", Новый ОписаниеТипов("Строка"), "ЦветПокраски");
	Колонка					= ТаблицаДневногоРасписания.Колонки.Добавить("ЦветПокраски", "ЦветПокраски");
	Колонка.Данные			= "ЦветПокраски";
	Колонка.Видимость		= Ложь;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("ЦветПокраскиПоГрафику", Новый ОписаниеТипов("Строка"), "ЦветПокраски");
	Колонка					= ТаблицаДневногоРасписания.Колонки.Добавить("ЦветПокраскиПоГрафику", "ЦветПокраскиПоГрафику");
	Колонка.Данные			= "ЦветПокраскиПоГрафику";
	Колонка.Видимость		= Ложь;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("ПеречислениеСсылка.Важность"), "Приоритет");
	Колонка					= ТаблицаДневногоРасписания.Колонки.Добавить("Приоритет", "Приоритет");
	Колонка.Данные			= "Приоритет";
	Колонка.Видимость		= Ложь;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("ИмяИконки", Новый ОписаниеТипов("Строка"), "ИмяИконки");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("ИмяИконки", "ИмяИконки");
	Колонка.Данные			= "ИмяИконки";
	Колонка.Видимость		= Ложь;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
	ТаблицаДневногоРасписания.Значение.Колонки.Добавить("СмещениеИконки", Новый ОписаниеТипов("Строка"), "СмещениеИконки");
	Колонка = ТаблицаДневногоРасписания.Колонки.Добавить("СмещениеИконки", "СмещениеИконки");
	Колонка.Данные			= "СмещениеИконки";
	Колонка.Видимость		= Ложь;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
КонецПроцедуры // СоздатьКолонкиДляДневногоГрафика()

// Разбиение таблицы на кванты и покраска по базовому графику
//
Процедура СозданиеТаблицыДневногоРасписанияИПокраскаПоБазовомуГрафику(ДневноеРасписание)
	КраситьПоБазовомуГрафику = Истина;
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если ИспользованиеГрафиковРабот = 0 ИЛИ НЕ ЗначениеЗаполнено(БазовыйГрафик) Тогда
		КраситьПоБазовомуГрафику = Ложь;
	Иначе
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаНачала), КонецДня(ДатаНачала));
	КонецЕсли;
	
	Если ТаблицаБазовогоГрафика.Количество() = 0 Тогда
		КраситьПоБазовомуГрафику = Ложь;
	КонецЕсли;
	
	// Оформим таблицу формы
	Если КраситьПоБазовомуГрафику Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Дата", НачалоДня(ДатаНачала));
		МассивСтрокБазовогоГрафика = ТаблицаБазовогоГрафика.НайтиСтроки(СтруктураОтбора);
	КонецЕсли;
	
	ТекущееВремя = '00010101';
	Пока ТекущееВремя < '00010101235959' Цикл
		
		СтрокаДневногоРасписания		= ДневноеРасписание.Добавить();
		СтрокаДневногоРасписания.Время	= ТекущееВремя;
		
		Если КраситьПоБазовомуГрафику Тогда
			СтрокаДневногоРасписания.ЦветПокраски = ПолучитьЦветРаскраскиИнтервала(МассивСтрокБазовогоГрафика, ТекущееВремя);
		Иначе
			СтрокаДневногоРасписания.ЦветПокраски = "Рабочий";
		КонецЕсли;
		СтрокаДневногоРасписания.ЦветПокраскиПоГрафику = СтрокаДневногоРасписания.ЦветПокраски;
		
		// Увеличиваем ТекущееВремя
		Если ((ТекущееВремя - '00010101') + ИнтервалКалендаря*60) >= 86400 Тогда
			ТекущееВремя = '00010101235959';
		Иначе
			ТекущееВремя = ТекущееВремя + ИнтервалКалендаря*60;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // СозданиеТаблицыДневногоРасписанияИПокраскаПоБазовомуГрафику()

// Заполнение таблицы
//
Процедура ЗаполнитьТаблицуДневногоРасписания(ДневноеРасписание)
	
	НомерСтрокиПериодНачало = 999999;
	НомерСтрокиПериодКонец = 0;
	
	Для Каждого СтрокаРасписания ИЗ РасписаниеСобытий Цикл
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата)
			И (СтрокаРасписания.НачалоСобытияДата<>СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;
		
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.ДатаСобытия,СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.НачалоСобытияДата,СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
			
			Если СтрокаРасписания.Напоминание Тогда
				СоответствуетФильтрам = ПроверитьФильтрыДляНапоминания(СтрокаРасписания);
			Иначе
				СоответствуетФильтрам = ПроверитьФильтрыДляДокумента(СтрокаРасписания.Событие);	
			КонецЕсли;

			Если СоответствуетФильтрам Тогда
				КоличествоСекундВНачале		= СтрокаРасписания.НачалоСобытия - '00010101';
				КоличествоСекундВКванте		= ИнтервалКалендаря*60;
				НомерСтроки					= ?(СтрокаРасписания.НачалоСобытияДата >= ДатаНачала, Цел(КоличествоСекундВНачале/КоличествоСекундВКванте), 0);
	            ДатаНачалаСобытияВПериоде	= ?(СтрокаРасписания.НачалоСобытияДата >= ДатаНачала, СтрокаРасписания.НачалоСобытияДата, ДатаНачала);
				ДатаНачалаСобытияВПериоде	= ?(ПротяженноеСобытие,ДатаНачалаСобытияВПериоде,СтрокаРасписания.ДатаСобытия);
				
				//распределение протяженных документов
				Если ПротяженноеСобытие Тогда
					Если ДатаНачалаСобытияВПериоде < НачалоДня(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
						КоличествоСекундВКонце = 24*3600;                                          
					Иначе
						КоличествоСекундВКонце = СтрокаРасписания.ОкончаниеСобытия - '00010101';
					КонецЕсли;	
					НомерСтрокиКонец = ?(КоличествоСекундВКонце%КоличествоСекундВКванте=0,КоличествоСекундВКонце/КоличествоСекундВКванте-1,Цел(КоличествоСекундВКонце/КоличествоСекундВКванте));
				Иначе	
					НомерСтрокиКонец = НомерСтроки;
				КонецЕсли;

				Если НомерСтроки < НомерСтрокиПериодНачало Тогда
					НомерСтрокиПериодНачало = НомерСтроки;
				КонецЕсли;
				
				Если НомерСтрокиКонец > НомерСтрокиПериодКонец Тогда
					НомерСтрокиПериодКонец = НомерСтрокиКонец;
				КонецЕсли;
				
				ВыводитьСобытие	= Истина;
				СобытиеВОдинЧас	= (НомерСтроки-НомерСтрокиКонец)=1; 
				Пока (НомерСтроки <= НомерСтрокиКонец) ИЛИ СобытиеВОдинЧас Цикл
					
					Если ВыводитьСобытие Тогда

						Если СтрокаРасписания.НачалоСобытияДата >= ДатаНачала Тогда
							ПредставлениеОбъекта = СтрокаРасписания.ПредставлениеОбъекта;									
						Иначе
							ПредставлениеОбъекта = СтрокаРасписания.ПредставлениеОбъекта2;									
						КонецЕсли;
						
						Если НЕ обЗначениеНеЗаполнено(ДневноеРасписание[НомерСтроки].ПредставлениеСобытий) Тогда
							ДневноеРасписание[НомерСтроки].ПредставлениеСобытий = ДневноеРасписание[НомерСтроки].ПредставлениеСобытий + Символы.ПС + ПредставлениеОбъекта;
						Иначе
							ДневноеРасписание[НомерСтроки].ПредставлениеСобытий = ПредставлениеОбъекта;
							Если СтрокаРасписания.ЦветПокраски = "ЗагрузкаМин" 	ИЛИ СтрокаРасписания.ЦветПокраски = "Напоминание" Тогда 
								ДневноеРасписание[НомерСтроки].ЦветПокраски = СтрокаРасписания.ЦветПокраски;
							КонецЕсли;	
							ДневноеРасписание[НомерСтроки].ИмяИконки = СтрокаРасписания.ИмяИконки;
						КонецЕсли;
						
						ТекущееСобытие = СтрокаРасписания.Событие;
						Если СтрокаРасписания.Напоминание Тогда
							СтруктураНапоминания = Новый Структура;
							СтруктураНапоминания.Вставить("Тема", СтрокаРасписания.Тема);
							СтруктураНапоминания.Вставить("Пользователь", СтрокаРасписания.Пользователь);
							СтруктураНапоминания.Вставить("Автор", СтрокаРасписания.Автор);
							СтруктураНапоминания.Вставить("РеальнаяДатаОповещения", СтрокаРасписания.РеальнаяДатаОповещения);
							СтруктураНапоминания.Вставить("Объект", СтрокаРасписания.Объект);
							СтруктураНапоминания.Вставить("Завершено", СтрокаРасписания.Завершено);
							ТекущееСобытие = СтруктураНапоминания;
						КонецЕсли;   
						
						ДневноеРасписание[НомерСтроки].МассивОбъектов.Добавить(ТекущееСобытие);
						
						ВыводитьСобытие = Ложь;

					ИначеЕсли СтрокаРасписания.ЦветПокраски = "ЗагрузкаМин" ИЛИ СтрокаРасписания.ЦветПокраски = "Напоминание" Тогда 
						ДневноеРасписание[НомерСтроки].ЦветПокраски = "ЗагрузкаМин";
					КонецЕсли;
					
					Если СобытиеВОдинЧас Тогда
						Прервать;
					КонецЕсли;
					
					НомерСтроки = НомерСтроки + 1;
					
				КонецЦикла;
			КонецЕсли;	
		КонецЕсли	
	КонецЦикла;
	
	//удаление строк, которые не входят в график работы
	Если ИспользованиеГрафиковРабот > 0 И ЗначениеЗаполнено(БазовыйГрафик) Тогда
		Сч = ДневноеРасписание.Количество()-1;
		Пока Сч >= 0 Цикл
			Если ДневноеРасписание[Сч].ЦветПокраски = "НеЗаполнено" 
				И (Сч > НомерСтрокиПериодКонец ИЛИ Сч < НомерСтрокиПериодНачало ) Тогда
				ДневноеРасписание.Удалить(ДневноеРасписание[Сч]);	
			КонецЕсли;
			Сч = Сч - 1;
		КонецЦикла
	КонецЕсли;	
		
КонецПроцедуры // ЗаполнитьТаблицуДневногоРасписания()

// Формирование дневного расписания
//
Процедура СформироватьДневноеРасписание(ТаблицаДневногоРасписания,НужноОбновить = Истина) Экспорт
		
	// Получение таблицы событий
	ПолучитьТаблицуРасписаний(НужноОбновить);
		
	// Свернём таблицу
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,ПредставлениеОбъекта2,Событие,Тема,Пользователь,Автор,ПодразделениеКомпании,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки,Завершено","");
	
	// Создание колонок дневного расписания
	СоздатьКолонкиДляДневногоГрафика(ТаблицаДневногоРасписания);
	
	ДневноеРасписание = ТаблицаДневногоРасписания.Значение;
	
	// Создаем таблицу и красим по графику
	СозданиеТаблицыДневногоРасписанияИПокраскаПоБазовомуГрафику(ДневноеРасписание);
	
	// Выводим события
	ЗаполнитьТаблицуДневногоРасписания(ДневноеРасписание);
	
КонецПроцедуры // СформироватьДневноеРасписание()

#КонецЕсли


////////////////////////////////////////////////////////////////////
// НЕДЕЛЯ

#Если Клиент Тогда

// Создание колонок для недельного графика
//
Процедура СоздатьКолонкиДляНедельногоГрафика(ТаблицаНедельногоРасписания, КоличествоДней);
	ТаблицаНедельногоРасписания.Значение.Очистить();
	ТаблицаНедельногоРасписания.Значение.Колонки.Очистить();
	ТаблицаНедельногоРасписания.Колонки.Очистить();
	
	ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("Время", Новый ОписаниеТипов("Дата",, Новый КвалификаторыДаты(ЧастиДаты.Время)), "Время");
	Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("Время", "Время");
	Колонка.Данные = "Время";
	Колонка.Формат = "ДФ=ЧЧ:мм; ДП=00:00";
	Колонка.Ширина = 7;
	Колонка.ШрифтТекста = Новый Шрифт(,,Истина);
	Колонка.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	Колонка.ВысотаЯчейки = 10;
	Колонка.АвтоВысотаЯчейки = Истина;
	Колонка.ПропускатьПриВводе = Истина;
	Колонка.Доступность = Ложь;
	
	ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("ЦветПокраскиПоГрафику", Новый ОписаниеТипов("Строка"), "ЦветПокраски");
	Колонка					= ТаблицаНедельногоРасписания.Колонки.Добавить("ЦветПокраскиПоГрафику", "ЦветПокраскиПоГрафику");
	Колонка.Данные			= "ЦветПокраскиПоГрафику";
	Колонка.Видимость		= Ложь;
	Колонка.ВысотаЯчейки	= 10;
	Колонка.АвтоВысотаЯчейки= Истина;
	
	Для ПеременнаяЦикла = 0 По КоличествоДней Цикл
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("ПредставлениеСобытий" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "ПредставлениеСобытий" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("ПредставлениеСобытий" + ПеременнаяЦикла,
			Формат(ДатаНачала + (ПеременнаяЦикла*60*60*24), "ДФ='ддд дд МММ'"));
		Колонка.Данные = "ПредставлениеСобытий" + ПеременнаяЦикла;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
			
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("МассивОбъектов" + ПеременнаяЦикла, Новый ОписаниеТипов("Массив"), "МассивОбъектов" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("МассивОбъектов" + ПеременнаяЦикла, "МассивОбъектов" + ПеременнаяЦикла);
		Колонка.Данные = "МассивОбъектов" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("ЦветПокраски" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "ЦветПокраски" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("ЦветПокраски" + ПеременнаяЦикла, "ЦветПокраски" + ПеременнаяЦикла);
		Колонка.Данные = "ЦветПокраски" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("ИмяИконки" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "ИмяИконки" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("ИмяИконки" + ПеременнаяЦикла, "ИмяИконки" + ПеременнаяЦикла);
		Колонка.Данные = "ИмяИконки" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаНедельногоРасписания.Значение.Колонки.Добавить("СмещениеИконки" + ПеременнаяЦикла, Новый ОписаниеТипов("Строка"), "СмещениеИконки" + ПеременнаяЦикла);
		Колонка = ТаблицаНедельногоРасписания.Колонки.Добавить("СмещениеИконки" + ПеременнаяЦикла, "СмещениеИконки" + ПеременнаяЦикла);
		Колонка.Данные = "СмещениеИконки" + ПеременнаяЦикла;
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
	КонецЦикла;
	
	ТаблицаНедельногоРасписания.ФиксацияСлева = 1;
КонецПроцедуры // СоздатьКолонкиДляНедельногоГрафика()

// Разбиение таблицы на кванты и покраска по базовому графику
//
Процедура СозданиеТаблицыНедельногоРасписанияИПокраскаПоБазовомуГрафику(НедельноеРасписание)
	КраситьПоБазовомуГрафику = Истина;
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если ИспользованиеГрафиковРабот = 0 ИЛИ НЕ ЗначениеЗаполнено(БазовыйГрафик) Тогда
		КраситьПоБазовомуГрафику = Ложь;
	Иначе
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаНачала), НачалоДня(ДатаОкончания));
	КонецЕсли;
	
	Если ТаблицаБазовогоГрафика.Количество() = 0 Тогда
		КраситьПоБазовомуГрафику = Ложь;
	КонецЕсли;
	
	КоличествоДней = (ДатаОкончания - ДатаНачала)/60/60/24;
	
	Для ПеременнаяЦикла = 0 По КоличествоДней Цикл
		
		Если КраситьПоБазовомуГрафику Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Дата", НачалоДня(ДатаНачала) + (ПеременнаяЦикла*60*60*24));
			МассивСтрокБазовогоГрафика = ТаблицаБазовогоГрафика.НайтиСтроки(СтруктураОтбора);
		КонецЕсли;
		
		ТекущееВремя = '00010101';
		Пока ТекущееВремя < '00010101235959' Цикл
			
			Если ПеременнаяЦикла = 0 Тогда
				СтрокаНедельногоРасписания = НедельноеРасписание.Добавить();
				СтрокаНедельногоРасписания.Время = ТекущееВремя;
			Иначе
				СтрокаНедельногоРасписания = НедельноеРасписание.Найти(ТекущееВремя, "Время");
			КонецЕсли;
			
			Если КраситьПоБазовомуГрафику Тогда
				СтрокаНедельногоРасписания["ЦветПокраски" + ПеременнаяЦикла] = ПолучитьЦветРаскраскиИнтервала(МассивСтрокБазовогоГрафика, ТекущееВремя);
			Иначе
				СтрокаНедельногоРасписания["ЦветПокраски" + ПеременнаяЦикла] = "Рабочий";
			КонецЕсли;
			
			Если ПеременнаяЦикла = 1 Тогда
				СтрокаНедельногоРасписания["ЦветПокраскиПоГрафику"] = СтрокаНедельногоРасписания["ЦветПокраски" + ПеременнаяЦикла];
			КонецЕсли;
			
			// Увеличиваем ТекущееВремя
			Если ((ТекущееВремя - '00010101') + ИнтервалКалендаря*60) >= 86400 Тогда
				ТекущееВремя = '00010101235959';
			Иначе
				ТекущееВремя = ТекущееВремя + ИнтервалКалендаря*60;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // СозданиеТаблицыНедельногоРасписанияИПокраскаПоБазовомуГрафику()

// Заполнение таблицы
//
Процедура ЗаполнитьТаблицуНедельногоРасписания(НедельноеРасписание)
	
	НомерСтрокиПериодНачало = 999999;
	НомерСтрокиПериодКонец = 0;
	
	КоличествоСекундВКванте = ИнтервалКалендаря*60;
	
	Для Каждого СтрокаРасписания ИЗ РасписаниеСобытий Цикл	
		//определим, протяженное ли событие
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата)
			И (СтрокаРасписания.НачалоСобытияДата<>СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;
		
		//определим, входит ли событие в наш период
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.ДатаСобытия,СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала,КонецДня(ДатаОкончания), СтрокаРасписания.НачалоСобытияДата,СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
			
			Если СтрокаРасписания.Напоминание Тогда
				СоответствуетФильтрам = ПроверитьФильтрыДляНапоминания(СтрокаРасписания);
			Иначе
				СоответствуетФильтрам = ПроверитьФильтрыДляДокумента(СтрокаРасписания.Событие);	
			КонецЕсли;

			Если СоответствуетФильтрам Тогда
				//получим начальный номер колонки
				ДатаНачалаСобытияВПериоде = ?(СтрокаРасписания.НачалоСобытияДата >= ДатаНачала, СтрокаРасписания.НачалоСобытияДата, ДатаНачала);
				ДатаНачалаСобытияВПериоде = ?(ПротяженноеСобытие,ДатаНачалаСобытияВПериоде,СтрокаРасписания.ДатаСобытия);
				Смещение = (НачалоДня(ДатаНачалаСобытияВПериоде) - ДатаНачала)/60/60/24;
				
				//распределение протяженных документов по столбцам
				Если ПротяженноеСобытие Тогда
					Если ДатаНачалаСобытияВПериоде < НачалоНедели(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
						СмещениеКонец = 6;
					Иначе
						СмещениеКонец = ДеньНедели(СтрокаРасписания.ОкончаниеСобытияДата)-1;
					КонецЕсли;		
				Иначе	
					СмещениеКонец = Смещение;
				КонецЕсли;
				
				Пока Смещение <= СмещениеКонец Цикл
					
					ТекущийДень = ДатаНачала + Смещение*24*60*60; 
					КоличествоСекундВНачале		= СтрокаРасписания.НачалоСобытия - '00010101';
					КоличествоСекундВКванте		= ИнтервалКалендаря*60;
					НомерСтроки					= ?(СтрокаРасписания.НачалоСобытияДата >= ТекущийДень,Цел(КоличествоСекундВНачале/КоличествоСекундВКванте),0);
		            ДатаНачалаСобытияВПериоде	= ?(СтрокаРасписания.НачалоСобытияДата >= ТекущийДень, СтрокаРасписания.НачалоСобытияДата, ТекущийДень);
					ДатаНачалаСобытияВПериоде	= ?(ПротяженноеСобытие,ДатаНачалаСобытияВПериоде,СтрокаРасписания.ДатаСобытия);
					
					//распределение протяженных документов
					Если ПротяженноеСобытие Тогда
						Если ДатаНачалаСобытияВПериоде < НачалоДня(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
							КоличествоСекундВКонце = 24*3600;                                          
						Иначе
							КоличествоСекундВКонце = СтрокаРасписания.ОкончаниеСобытия - '00010101';
						КонецЕсли;	
						НомерСтрокиКонец = ?(КоличествоСекундВКонце%КоличествоСекундВКванте=0,КоличествоСекундВКонце/КоличествоСекундВКванте-1,Цел(КоличествоСекундВКонце/КоличествоСекундВКванте));
					Иначе	
						НомерСтрокиКонец = НомерСтроки;
					КонецЕсли;

					Если НомерСтроки < НомерСтрокиПериодНачало Тогда
						НомерСтрокиПериодНачало = НомерСтроки;
					КонецЕсли;
					
					Если НомерСтрокиКонец > НомерСтрокиПериодКонец Тогда
						НомерСтрокиПериодКонец = НомерСтрокиКонец;
					КонецЕсли;
					
					ВыводитьСобытие	= Истина;
					СобытиеВОдинЧас	= (НомерСтроки-НомерСтрокиКонец)=1; 
					Пока (НомерСтроки <= НомерСтрокиКонец) ИЛИ СобытиеВОдинЧас Цикл
						
						Если ВыводитьСобытие Тогда

							Если СтрокаРасписания.НачалоСобытияДата >= ТекущийДень Тогда
								ПредставлениеОбъекта = СтрокаРасписания.ПредставлениеОбъекта;									
							Иначе
								ПредставлениеОбъекта = СтрокаРасписания.ПредставлениеОбъекта2;									
							КонецЕсли;
							
							Если НЕ обЗначениеНеЗаполнено(НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение]) Тогда
								НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение] = НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение] + Символы.ПС + ПредставлениеОбъекта;
							Иначе
								НедельноеРасписание[НомерСтроки]["ПредставлениеСобытий" + Смещение] = ПредставлениеОбъекта;
								НедельноеРасписание[НомерСтроки]["ИмяИконки" + Смещение] = СтрокаРасписания.ИмяИконки;
							КонецЕсли;
							
							ТекущееСобытие = СтрокаРасписания.Событие;
							Если СтрокаРасписания.Напоминание Тогда
								СтруктураНапоминания = Новый Структура;
								СтруктураНапоминания.Вставить("Тема", СтрокаРасписания.Тема);
								СтруктураНапоминания.Вставить("Пользователь", СтрокаРасписания.Пользователь);
								СтруктураНапоминания.Вставить("Автор", СтрокаРасписания.Автор);
								СтруктураНапоминания.Вставить("РеальнаяДатаОповещения", СтрокаРасписания.РеальнаяДатаОповещения);
								СтруктураНапоминания.Вставить("Объект", СтрокаРасписания.Объект);
								СтруктураНапоминания.Вставить("Завершено", СтрокаРасписания.Завершено);
								ТекущееСобытие = СтруктураНапоминания;
							КонецЕсли;   
							
							НедельноеРасписание[НомерСтроки]["МассивОбъектов" + Смещение].Добавить(ТекущееСобытие);
							
							ВыводитьСобытие = Ложь;

						КонецЕсли;
						
						Если СтрокаРасписания.ЦветПокраски = "ЗагрузкаМин" ИЛИ СтрокаРасписания.ЦветПокраски = "Напоминание" Тогда 
							НедельноеРасписание[НомерСтроки]["ЦветПокраски" + Смещение] = СтрокаРасписания.ЦветПокраски;
						КонецЕсли;	
						
						Если СобытиеВОдинЧас Тогда
							Прервать;
						КонецЕсли;
						
						НомерСтроки = НомерСтроки + 1;
				
					КонецЦикла;
					
					Смещение = Смещение + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	
	//удаление строк, которые не входят в график работы
	Если ИспользованиеГрафиковРабот > 0 И ЗначениеЗаполнено(БазовыйГрафик) Тогда
		Сч = НедельноеРасписание.Количество()-1;
		Пока Сч >= 0 Цикл
			Если НедельноеРасписание[Сч]["ЦветПокраски0"] = "НеЗаполнено" 
				И (Сч > НомерСтрокиПериодКонец ИЛИ Сч < НомерСтрокиПериодНачало ) Тогда
				НедельноеРасписание.Удалить(НедельноеРасписание[Сч]);	
			КонецЕсли;
			Сч = Сч - 1;
		КонецЦикла
	КонецЕсли;	
	
КонецПроцедуры // ЗаполнитьТаблицуНедельногоРасписания()

// Формирование недельного расписания
//
Процедура СформироватьНедельноеРасписание(ТаблицаНедельногоРасписания,НужноОбновить = Истина) Экспорт
	
	// Получение таблицы событий
	ПолучитьТаблицуРасписаний(НужноОбновить);
			
	// Свернём таблицу
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,ПредставлениеОбъекта2,Событие,Тема,Пользователь,Автор,ПодразделениеКомпании,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки,Завершено","");
	
	// Создание колонок расписания
	СоздатьКолонкиДляНедельногоГрафика(ТаблицаНедельногоРасписания, (ДатаОкончания - ДатаНачала)/60/60/24);
	
	НедельноеРасписание = ТаблицаНедельногоРасписания.Значение;
	
	// Создаем таблицу и красим по графику
	СозданиеТаблицыНедельногоРасписанияИПокраскаПоБазовомуГрафику(НедельноеРасписание);
	
	// Выводим события
	ЗаполнитьТаблицуНедельногоРасписания(НедельноеРасписание);
	
КонецПроцедуры // СформироватьНедельноеРасписание()

#КонецЕсли


////////////////////////////////////////////////////////////////////
// МЕСЯЦ

#Если Клиент Тогда

// Создание колонок для месячного графика
//
Процедура СоздатьКолонкиДляМесячногоГрафика(ТаблицаМесячногоРасписания)
	ТаблицаМесячногоРасписания.Значение.Очистить();
	ТаблицаМесячногоРасписания.Значение.Колонки.Очистить();
	ТаблицаМесячногоРасписания.Колонки.Очистить();
		
	Для Индекс = 0 По МассивДнейНедели.ВГраница() Цикл
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ДатаСобытия" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Дата"), "ДатаСобытия" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ДатаСобытия" + МассивДнейНедели[Индекс], "Дата");
		Колонка.Данные = "ДатаСобытия" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ПредставлениеСобытий" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Строка"), "ПредставлениеСобытий" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ПредставлениеСобытий" + МассивДнейНедели[Индекс], МассивДнейНедели[Индекс]);
		Колонка.Данные = "ПредставлениеСобытий" + МассивДнейНедели[Индекс];
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;

		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("МассивОбъектов" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Массив"), "МассивОбъектов" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("МассивОбъектов" + МассивДнейНедели[Индекс], "МассивОбъектов" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "МассивОбъектов" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ЦветПокраски" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Строка"), "ЦветПокраски" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ЦветПокраски" + МассивДнейНедели[Индекс], "ЦветПокраски" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "ЦветПокраски" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("ИмяИконки" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Строка"), "ИмяИконки" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("ИмяИконки" + МассивДнейНедели[Индекс], "ИмяИконки" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "ИмяИконки" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
		
		ТаблицаМесячногоРасписания.Значение.Колонки.Добавить("СмещениеИконки" + МассивДнейНедели[Индекс], Новый ОписаниеТипов("Число"), "СмещениеИконки" + МассивДнейНедели[Индекс]);
		Колонка = ТаблицаМесячногоРасписания.Колонки.Добавить("СмещениеИконки" + МассивДнейНедели[Индекс], "СмещениеИконки" + МассивДнейНедели[Индекс]);
		Колонка.Данные = "СмещениеИконки" + МассивДнейНедели[Индекс];
		Колонка.Видимость = Ложь;
		Колонка.ВысотаЯчейки = 10;
		Колонка.АвтоВысотаЯчейки = Истина;
	КонецЦикла;
КонецПроцедуры // СоздатьКолонкиДляМесячногоГрафика()

// Разбиение таблицы на кванты и покраска по базовому графику
//
Процедура СозданиеТаблицыМесячногоРасписанияИПокраскаПоБазовомуГрафику(МесячноеРасписание)
	КраситьПоБазовомуГрафику = Истина;
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если ИспользованиеГрафиковРабот = 0 ИЛИ НЕ ЗначениеЗаполнено(БазовыйГрафик) Тогда
		КраситьПоБазовомуГрафику = Ложь;
	Иначе
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаНачала), НачалоДня(ДатаОкончания));
	КонецЕсли;
	
	Если ТаблицаБазовогоГрафика.Количество() = 0 Тогда
		КраситьПоБазовомуГрафику = Ложь;
	КонецЕсли;
	
	НачальныйНомерНедели = НеделяГода(ДатаНачала);
	КонечныйНомерНедели = НеделяГода(ДатаОкончания);
	
	ТекущаяДата = НачалоНедели(ДатаНачала);
	
	Для ПеременнаяЦикла = НачальныйНомерНедели По КонечныйНомерНедели Цикл
		СтрокаМесячногоРасписания = МесячноеРасписание.Добавить();
		
		Для Индекс = 0 По МассивДнейНедели.ВГраница() Цикл
			СтрокаМесячногоРасписания["ДатаСобытия" + МассивДнейНедели[Индекс]] =  ТекущаяДата;
			СтрокаМесячногоРасписания["ИмяИконки" + МассивДнейНедели[Индекс]] = "ДниДляМесяца";
			СтрокаМесячногоРасписания["СмещениеИконки" + МассивДнейНедели[Индекс]] = Число(Формат(ТекущаяДата, "ДФ=дд"))-1;
			Если КраситьПоБазовомуГрафику Тогда
				
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("Дата", ТекущаяДата);
				МассивСтрокБазовогоГрафика = ТаблицаБазовогоГрафика.НайтиСтроки(СтруктураОтбора);
				Цвет = "";
				Если МассивСтрокБазовогоГрафика.Количество() <> 0 Тогда
					
					Если МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Выходной Тогда
						Цвет = "Выходной";
					ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Праздник Тогда
						Цвет = "Праздник";
					ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
						Цвет = "Предпраздничный";
					ИначеЕсли МассивСтрокБазовогоГрафика[0].ВидДня = Перечисления.ВидДня.Рабочий Тогда
						Цвет = "Рабочий";
					КонецЕсли;
									
					Если обЗначениеНеЗаполнено(Цвет) Тогда
						Цвет = "НеЗаполнено";
					КонецЕсли;
				Иначе
					Цвет = "НеЗаполнено";
				КонецЕсли;
				
				СтрокаМесячногоРасписания["ЦветПокраски" + МассивДнейНедели[Индекс]] = Цвет;
			Иначе
				СтрокаМесячногоРасписания["ЦветПокраски" + МассивДнейНедели[Индекс]] = "Рабочий";
			КонецЕсли;
			ТекущаяДата = ТекущаяДата + 60*60*24;
			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // СозданиеТаблицыМесячногоРасписанияИПокраскаПоБазовомуГрафику()

// Заполнение таблицы
//
Процедура ЗаполнитьТаблицуМесячногоРасписания(МесячноеРасписание)
	НачальныйНомерНедели = НеделяГода(ДатаНачала);
	ПервыйДеньНаКалендаре = НачалоНедели(ДатаНачала);
	ПоследнийДеньМесяца = КонецМесяца(ДатаНачала);
		
	Для Каждого СтрокаРасписания ИЗ РасписаниеСобытий Цикл
		//определим, протяженное ли событие
		Если (ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.Событие")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаявкаНаРемонт")
			ИЛИ ТипЗнч(СтрокаРасписания.Событие) = Тип("ДокументСсылка.ЗаказНаряд")) 
			И ЗначениеЗаполнено(СтрокаРасписания.НачалоСобытияДата)
			И ЗначениеЗаполнено(СтрокаРасписания.ОкончаниеСобытияДата)
			И (СтрокаРасписания.НачалоСобытияДата<>СтрокаРасписания.ОкончаниеСобытияДата) Тогда
			ПротяженноеСобытие = Истина;
		Иначе
			ПротяженноеСобытие = Ложь;
		КонецЕсли;	
		
		//определим, входит ли событие в наш период
		Если ((НЕ ПротяженноеСобытие) И  ПериодыПересекаются(ДатаНачала, КонецДня(ДатаОкончания), СтрокаРасписания.ДатаСобытия, СтрокаРасписания.ДатаСобытия)) 
			ИЛИ (ПротяженноеСобытие И ПериодыПересекаются(ДатаНачала, КонецДня(ДатаОкончания), СтрокаРасписания.НачалоСобытияДата, СтрокаРасписания.ОкончаниеСобытияДата)) Тогда
			
			Если СтрокаРасписания.Напоминание Тогда
				СоответствуетФильтрам = ПроверитьФильтрыДляНапоминания(СтрокаРасписания);
			Иначе
				СоответствуетФильтрам = ПроверитьФильтрыДляДокумента(СтрокаРасписания.Событие);	
			КонецЕсли;
		
			Если СоответствуетФильтрам Тогда
	            //получим начальные номера строки и колонки
				ДатаНачалаСобытияВПериоде = ?(СтрокаРасписания.НачалоСобытияДата >= ДатаНачала, СтрокаРасписания.НачалоСобытияДата, ДатаНачала); 
				ДатаНачалаСобытияВПериоде = ?(ПротяженноеСобытие,ДатаНачалаСобытияВПериоде,СтрокаРасписания.ДатаСобытия);
				НомерСтроки = НеделяГода(ДатаНачалаСобытияВПериоде) - НачальныйНомерНедели;
				
				//распределение протяженных документов по строкам
				Если ПротяженноеСобытие Тогда
					Если ДатаНачалаСобытияВПериоде < НачалоМесяца(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
						НомерСтрокиКонец = НеделяГода(ДатаОкончания) - НачальныйНомерНедели; 
					Иначе
						НомерСтрокиКонец = НеделяГода(СтрокаРасписания.ОкончаниеСобытияДата) - НачальныйНомерНедели;
					КонецЕсли;	
				Иначе
					НомерСтрокиКонец = НомерСтроки;
		        КонецЕсли;

				Пока НомерСтроки <= НомерСтрокиКонец Цикл

					Если (НеделяГода(ДатаНачалаСобытияВПериоде) - НачальныйНомерНедели = НомерСтроки) Тогда
						НомерКолонки = ДеньНедели(ДатаНачалаСобытияВПериоде);
					Иначе
						НомерКолонки = 1;
					КонецЕсли;

					//распределение протяженных документов по столбцам
					Если ПротяженноеСобытие Тогда
						Если НачалоНедели(ДатаНачалаСобытияВПериоде+НомерСтроки*3600*24*7) < НачалоНедели(СтрокаРасписания.ОкончаниеСобытияДата) Тогда
							Если НеделяГода(ДатаНачалаСобытияВПериоде+НомерСтроки*3600*24*7)=НеделяГода(ДатаОкончания) Тогда
								НомерКолонкиКонец = ДеньНедели(ДатаОкончания);
							Иначе
								НомерКолонкиКонец = 7;
							КонецЕсли;
						Иначе
							НомерКолонкиКонец = ДеньНедели(СтрокаРасписания.ОкончаниеСобытияДата);
						КонецЕсли;	
					Иначе
						НомерКолонкиКонец = НомерКолонки;
					КонецЕсли;	
						
					Пока НомерКолонки <= НомерКолонкиКонец Цикл
						
						ТекущийДень = ПервыйДеньНаКалендаре + (НомерСтроки*7+(НомерКолонки-1))*24*60*60; 
						Если ТекущийДень >= ПоследнийДеньМесяца Тогда 
							Прервать;
						КонецЕсли;
						
						Если СтрокаРасписания.НачалоСобытияДата >= ТекущийДень Тогда
							ПредставлениеОбъекта = СтрокаРасписания.ПредставлениеОбъекта;									
						Иначе
							ПредставлениеОбъекта = СтрокаРасписания.ПредставлениеОбъекта2;									
						КонецЕсли;
							
						ЯчейкаСобытия = МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]];
						Если НЕ обЗначениеНеЗаполнено(ЯчейкаСобытия) Тогда
							МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]] = МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]] + Символы.ПС + ПредставлениеОбъекта;
						Иначе
							МесячноеРасписание[НомерСтроки]["ПредставлениеСобытий" + МассивДнейНедели[НомерКолонки-1]] = ПредставлениеОбъекта;
						КонецЕсли;
						
						Если МесячноеРасписание[НомерСтроки]["ЦветПокраски" + МассивДнейНедели[НомерКолонки-1]] = "Рабочий" 
							ИЛИ МесячноеРасписание[НомерСтроки]["ЦветПокраски" + МассивДнейНедели[НомерКолонки-1]] = "Рабочий" Тогда
							МесячноеРасписание[НомерСтроки]["ЦветПокраски" + МассивДнейНедели[НомерКолонки-1]] = "ЗагрузкаМин"; 
						КонецЕсли;	
							
						Если СтрокаРасписания.Напоминание Тогда
							СтруктураНапоминания = Новый Структура;
							СтруктураНапоминания.Вставить("Тема", СтрокаРасписания.Тема);
							СтруктураНапоминания.Вставить("Пользователь", СтрокаРасписания.Пользователь);
							СтруктураНапоминания.Вставить("Автор", СтрокаРасписания.Автор);
							СтруктураНапоминания.Вставить("РеальнаяДатаОповещения", СтрокаРасписания.РеальнаяДатаОповещения);
							СтруктураНапоминания.Вставить("Объект", СтрокаРасписания.Объект);
							СтруктураНапоминания.Вставить("Завершено", СтрокаРасписания.Завершено);
							МесячноеРасписание[НомерСтроки]["МассивОбъектов" + МассивДнейНедели[НомерКолонки-1]].Добавить(СтруктураНапоминания);
						Иначе
							МесячноеРасписание[НомерСтроки]["МассивОбъектов" + МассивДнейНедели[НомерКолонки-1]].Добавить(СтрокаРасписания.Событие);
						КонецЕсли;
						
						НомерКолонки = НомерКолонки + 1;
					КонецЦикла;	
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;	
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры // ЗаполнитьТаблицуМесячногоРасписания()

// Формирование месячного расписания
//
Процедура СформироватьМесячноеРасписание(ТаблицаМесячногоРасписания,НужноОбновить = Истина) Экспорт
		
	// Получение таблицы событий
	ПолучитьТаблицуРасписаний(НужноОбновить);
		
	// Свернём таблицу
	РасписаниеСобытий.Свернуть("ДатаСобытия,Напоминание,НачалоСобытия,ОкончаниеСобытия,НачалоСобытияДата,ОкончаниеСобытияДата,ПредставлениеОбъекта,ПредставлениеОбъекта2,Событие,Тема,Пользователь,Автор,ПодразделениеКомпании,РеальнаяДатаОповещения,Объект,ЦветПокраски,ИмяИконки, Завершено","");
	
	// Создание колонок расписания
	СоздатьКолонкиДляМесячногоГрафика(ТаблицаМесячногоРасписания);
	
	МесячноеРасписание = ТаблицаМесячногоРасписания.Значение;
	
	// Создаем таблицу и красим по графику
	СозданиеТаблицыМесячногоРасписанияИПокраскаПоБазовомуГрафику(МесячноеРасписание);
	
	// Выводим события
	ЗаполнитьТаблицуМесячногоРасписания(МесячноеРасписание);
	
КонецПроцедуры // СформироватьМесячноеРасписание()

#КонецЕсли


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// цвета раскраски ячеек
ЦветаРаскраски = Новый Соответствие;
ПолучитьЦветаРаскраски();
 
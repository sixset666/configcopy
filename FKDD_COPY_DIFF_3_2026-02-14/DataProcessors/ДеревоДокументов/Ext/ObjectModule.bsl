#Если Клиент Тогда
Перем ОписаниеТиповДок; // Описание типов документов
Перем ТаблицаКонтроляПовтора;

// Процедура получения дерева документов
// ДеревоДокументов - ДеревоЗначений ,  результат выполнения процедуры
// Использует Реквизит Отчета - ВыбранныйДокумент - ссылка на выбранный документ
//
// Параметр:
// ДеревоДокументов		- ДеревоЗначений	- Дерево документов
//
Процедура ПолучитьДеревоДокументов(ДеревоДокументов) Экспорт
	ДеревоДокументов.Строки.Очистить();
	ДеревоДокументов.Колонки.Очистить();
	ТаблицаКонтроляПовтора = Новый Массив;
	Если ВыбДокумент  =  Неопределено Тогда
		Возврат;			
	КонецЕсли; 
	МассивТипов  =  Новый Массив;
	Для Каждого Документ Из Метаданные.Документы Цикл
		МассивТипов.Добавить(Тип("ДокументСсылка."+Документ.Имя));
	КонецЦикла;
	ОписаниеТиповДок  =  Новый ОписаниеТипов(МассивТипов ,  ,  , );
	ОписаниеТиповДата = Новый ОписаниеТипов("Дата");
	ДеревоДокументов.Колонки.Добавить("ВидДокумента" ,  , "Операция" , 40);
	ДеревоДокументов.Колонки.Добавить("Номер" ,  , "Номер" , 11);
	ДеревоДокументов.Колонки.Добавить("Дата" , ОписаниеТиповДата , "Дата" , 10);
	ДеревоДокументов.Колонки.Добавить("Организация" ,  , "Организация" , 15);
	ДеревоДокументов.Колонки.Добавить("Подразделение" ,  , "Подразделение" , 15);
	ДеревоДокументов.Колонки.Добавить("Автор" ,  , "Автор" , 15);	
	ДеревоДокументов.Колонки.Добавить("СуммаДокумента" ,  , "Сумма" , );
	ДеревоДокументов.Колонки.Добавить("Документ" , ОписаниеТиповДок , "Документ" , 1);
	ТаблицаКонтроляПовтора.Добавить(ВыбДокумент);
	
	ДокКорень = ПолучитьКореньДокумента(ВыбДокумент);
	СтрокаДерева = ДеревоДокументов.Строки.Добавить();
	СтрокаДерева.Документ		= ДокКорень;
	СтрокаДерева.ВидДокумента	= ДокКорень.Метаданные().Синоним;
	СтрокаДерева.Дата			= ДокКорень.Дата;
	СтрокаДерева.Номер			= ДокКорень.Номер;
	Если Метаданные.Документы[ДокКорень.Метаданные().Имя].Реквизиты.Найти("Организация") <> Неопределено Тогда
		СтрокаДерева.Организация = ДокКорень.Организация;
	КонецЕсли;
	Если Метаданные.Документы[ДокКорень.Метаданные().Имя].Реквизиты.Найти("ПодразделениеКомпании") <> Неопределено Тогда
		СтрокаДерева.Подразделение = ДокКорень.ПодразделениеКомпании;
	КонецЕсли;
	Если Метаданные.Документы[ДокКорень.Метаданные().Имя].Реквизиты.Найти("Автор") <> Неопределено Тогда
		СтрокаДерева.Автор = ДокКорень.Автор;
	КонецЕсли;		
	Если Метаданные.Документы[ДокКорень.Метаданные().Имя].Реквизиты.Найти("СуммаДокумента") <> Неопределено Тогда
		СтрокаДерева.СуммаДокумента = ДокКорень.СуммаДокумента;
	КонецЕсли;
	ТаблицаКонтроляПовтора = Новый Массив;
	ТаблицаКонтроляПовтора.Добавить(ДокКорень);
	ДобавитьВДерево(СтрокаДерева , ДокКорень);
КонецПроцедуры // ПолучитьДеревоДокументов()

// Выводит дерево документов на форму
// Параметры:
//	дзДок - дерево для вывода
//
Процедура ВывестиДеревоДокументов(дзДок) Экспорт
	ФормаОтчета = ЭтотОбъект.ПолучитьФорму("Форма");
	Таб = ФормаОтчета.ЭлементыФормы["ПолеТабличногоДокументаДерево"];
	Таб.Очистить();
	Макет = ЭтотОбъект.ПолучитьМакет("ДеревоДокументов");
	Область = Макет.ПолучитьОбласть("Кнопки");
	Таб.Вывести(Область);
	ГлубинаДерева = 0;
	ПолучитьГлубинуДерева(ДеревоДокументов,0,ГлубинаДерева);
	спУровни = Новый Массив();
	Для К = 1 По ГлубинаДерева Цикл
		спУровни.Вставить(К,0);
	КонецЦикла; 
	Корень = дзДок.Строки[0];
	Если ГлубинаДерева > 1 Тогда
		спУровни[1] = 1;	
	КонецЕсли;
	Если Корень.Документ = ВыбДокумент Тогда
		Область = Макет.ПолучитьОбласть("КореньТекДок");
	Иначе
		Область = Макет.ПолучитьОбласть("Заголовок");
	КонецЕсли;
	Документ = Корень.Документ;
	Область.Параметры.Расшифровка	= Новый Структура("Документ" , Документ);
	Попытка
		СтрокаСумма = ?(Документ.СуммаДокумента = 0,"",Символы.ПС + "Сумма: " + Документ.СуммаДокумента + " " + Документ.ВалютаДокумента);
	Исключение
		СтрокаСумма = "";
	КонецПопытки; 
	Область.Параметры.ПечДок		= Документ.ХозОперация.Наименование + " № "+СокрЛП(Документ.Номер) + " от "+Формат(Документ.Дата,"ДФ=dd.MM.yyyy hh:mm:ss") + СтрокаСумма;
	Область.Параметры.Док			= Документ;
	Область.Рисунки[0].Картинка		= ?(Документ.ПометкаУдаления, БиблиотекаКартинок.ДокументПомеченНаУдаление, ?(Документ.Проведен, БиблиотекаКартинок.ДокументПроведен, БиблиотекаКартинок.ДокументНеПроведен));
	Таб.Вывести(Область);
	
	Для Каждого Строка Из Корень.Строки Цикл
		ВывестиСтрокуДерева(дзДок,Строка,Макет,Таб,1,спУровни);
	КонецЦикла;
	
	Таб.ТолькоПросмотр		= ИСТИНА;
	Таб.ОтображатьСетку		= ЛОЖЬ;
	Таб.ОтображатьЗаголовки	= Ложь;
	Если НЕ ФормаОтчета.Открыта() Тогда
		ФормаОтчета.Открыть();
	КонецЕсли;
КонецПроцедуры // ВывестиДеревоДокументов()

// Процедура форматирования дерева значений
// ПолеДеревоЗначений - ПолеДеревоЗначений ,  объект в который выводятся данные
//
// Параметр:
//	ПолеДеревоЗначений	- ДеревоЗначений	- Объект вывода данных
//
Процедура НастроитьПолеДеревоДокументов(ПолеДеревоЗначений = Неопределено) Экспорт
	Если ПолеДеревоЗначений = Неопределено Тогда
		ПолеДеревоЗначений = ЭтотОбъект.ПолучитьФорму().ЭлементыФормы["ПолеДеревоДокументов"];		
	КонецЕсли;
	ПолеДеревоЗначений.СоздатьКолонки();
	ПолеДеревоЗначений.Колонки.ВидДокумента.КартинкиСтрок = БиблиотекаКартинок.ккМаркеры;
	ПолеДеревоЗначений.Колонки.Дата.Формат = "ДЛФ=Д";
	ПолеДеревоЗначений.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	ПолеДеревоЗначений.Колонки.Дата.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	ПолеДеревоЗначений.Колонки.Номер.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	Если ПолеДеревоЗначений.Колонки.Найти("Документ")<>Неопределено Тогда
		ПолеДеревоЗначений.Колонки.Документ.Видимость = Ложь;
		ПолеДеревоЗначений.Колонки.Номер.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Лево;
	КонецЕсли;
	СтрокаВыбранногоДокумента = ДеревоДокументов.Строки.Найти(ВыбДокумент, "Документ", Истина);
	ПолеДеревоЗначений.ТекущаяСтрока = СтрокаВыбранногоДокумента;
КонецПроцедуры // НастроитьПолеДеревоДокументов()

//функция, получения ссылки на корень дерева документов
//
// Параметр:
//	Док		- ДокументСсылка	- Переданный документ
//
// Возвращаемое значение:
//	ДокументСсылка		- Корень документа
//
Функция ПолучитьКореньДокумента(Док)
	Перем ЗнРеквизит; // Значение реквизита
	ЗнРеквизит  =  Неопределено;
	ВидДок  =  Док.Метаданные().Имя;
	
	//обходим реквизиты в поисках подходящего корня
	Для Каждого Реквизит Из Метаданные.Документы[ВидДок].Реквизиты Цикл
		ЗнРеквизит  =  Док[Реквизит.Имя];
		Если ПроверкаПодчиненияРеквизиту(Док,ЗнРеквизит) Тогда
			ТаблицаКонтроляПовтора.Добавить(ЗнРеквизит);
			Возврат ПолучитьКореньДокумента(ЗнРеквизит);
		КонецЕсли;
	КонецЦикла; 
	
	//если в реквизитах ничего не было найдено, обходим реквизиты табличных частей
	//перебираем все ТЧ
	Для Каждого ТЧ Из Метаданные.Документы[ВидДок].ТабличныеЧасти Цикл
		//перебираем все строки ТЧ
		Для ИндексСтроки = 0 По Док[ТЧ.Имя].Количество()-1 Цикл
			//перебираем все реквизиты строки
			Для Каждого РеквизитТЧ Из ТЧ.Реквизиты Цикл
				ИмяРеквизитТЧ  =  Метаданные.Документы[ВидДок].ТабличныеЧасти[ТЧ.Имя].Реквизиты[РеквизитТЧ.Имя];
				ЗнРеквизит =  Док[ТЧ.Имя][ИндексСтроки][ИмяРеквизитТЧ.Имя];
				Если ПроверкаПодчиненияРеквизиту(Док,ЗнРеквизит) Тогда
					ТаблицаКонтроляПовтора.Добавить(ЗнРеквизит);
					Возврат ПолучитьКореньДокумента(ЗнРеквизит);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла; 
	
	Возврат Док;
	
КонецФункции // ПолучитьКореньДокумента()

//функция проверяет, может ли документ являться подчиненным реквизиту
//
// Параметр:
//	Док		- ДокументСсылка	- Переданный документ
//	Док		- Реквизит			- Переданный реквизит
//
// Возвращаемое значение:
//	Булево						- Результат проверки
//
Функция ПроверкаПодчиненияРеквизиту(ДокументСсылка,ЗнРеквизит)
	Если (ЗнРеквизит <> Неопределено) И  (ОписаниеТиповДок.СодержитТип(ТипЗнч(ЗнРеквизит))) Тогда
		Если (ЗнРеквизит<>Документы[ЗнРеквизит.Метаданные().Имя].ПустаяСсылка()) И (ЗнРеквизит<>ДокументСсылка) Тогда
			
			//проверка на дубль
			Если НЕ (ТаблицаКонтроляПовтора.Найти(ЗнРеквизит) = Неопределено) Тогда
				Возврат Ложь;
			КонецЕсли;
			
			//проверка, что текущий документ может быть подчиненным для предполагаемого корня 
			ТекстЗапроса = "ВЫБРАТЬ
			               |	ПодчиненныеДокументы.Ссылка КАК Документ
			               |ИЗ
			               |	КритерийОтбора.ПодчиненныеДокументы(&Основание) КАК ПодчиненныеДокументы
			               |ГДЕ
			               |	ПодчиненныеДокументы.Ссылка = &ДокументСсылка";
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Основание",ЗнРеквизит);
			Запрос.УстановитьПараметр("ДокументСсылка",ДокументСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Количество() > 0 Тогда
				Возврат Истина;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Ложь;
КонецФункции // ПолучитьКореньДокумента()

//функция, получает максимальную глубину дерева документов
//
// Параметр:
//	Дерево			- ДеревоЗначений	- Дерево
//	ТекГлубина		- Число				- Текущая глубина
//	ГлубинаДерева	- Число				- Глубина дерева
//
// Возвращаемое значение:
// Число			- Максимальная глубина дерева
//
Функция ПолучитьГлубинуДерева(Дерево, ТекГлубина, ГлубинаДерева)
	ПредГлубина  =  ТекГлубина;
	Для Каждого Строка Из Дерево.Строки Цикл
		ТекГлубина  =  ПолучитьГлубинуДерева(Строка , ТекГлубина+1 , ГлубинаДерева);
	КонецЦикла;
	Если ПредГлубина < ТекГлубина Тогда
		ГлубинаДерева  =  ГлубинаДерева + 1;
	КонецЕсли;
	Возврат ТекГлубина;    	
КонецФункции // ПолучитьГлубинуДерева()

Функция ПолучитьТекстЗапроса(ДокументКорень)
	
	ТекстЗапроса = "";
	Для Каждого Элемент Из Метаданные.КритерииОтбора.ПодчиненныеДокументы.Состав Цикл
		Если Элемент.Тип.СодержитТип(ТипЗнч(ДокументКорень)) Тогда
			ПолноеИмя = Элемент.ПолноеИмя();
			Позиция = Найти(ПолноеИмя, ".Реквизит.");
			ТаблицаОбъекта = Лев(ПолноеИмя, Позиция-1);
			ИмяРеквизита   = Сред(ПолноеИмя, Позиция+10);
			
			ПутьКДате = "";
			Если Найти(ТаблицаОбъекта, ".ТабличнаяЧасть.")>0 Тогда
				ПутьКДате = "Ссылка.Дата";
				ТаблицаОбъекта = СтрЗаменить(ТаблицаОбъекта, ".ТабличнаяЧасть.", ".");
			Иначе
				ПутьКДате = "Дата";
			КонецЕсли;
			
			ТекстЗапроса = ТекстЗапроса + ?(ТекстЗапроса = "", "" , "
			|	ОБЪЕДИНИТЬ ВСЕ
			|") + "
			|	ВЫБРАТЬ
			|		ТаблицаОбъекта.Ссылка КАК Документ,
			|		ТаблицаОбъекта."+ПутьКДате+" КАК Дата
			|	ИЗ
			|		"+ТаблицаОбъекта+" КАК ТаблицаОбъекта
			|	ГДЕ
			|		ТаблицаОбъекта."+ИмяРеквизита+" = &Основание
			|";
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТекстЗапроса = "" Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодчиненныеДокументы.Документ КАК Документ,
		|	ПодчиненныеДокументы.Дата КАК ДатаДокумента
		|ИЗ
		|	("+ТекстЗапроса+") КАК ПодчиненныеДокументы
		|УПОРЯДОЧИТЬ ПО
		|	ПодчиненныеДокументы.Дата";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

//обход дерева документов и добавление в дерево
//
// Параметр:
//	Строка			- Массив			- Массив строк			
//	ДокументКорень	- ДокументСсылка	- Документ корень
//
Процедура ДобавитьВДерево(Строка, ДокументКорень)
	
	ТекстЗапроса = ПолучитьТекстЗапроса(ДокументКорень);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Основание",ДокументКорень);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ (ТаблицаКонтроляПовтора.Найти(Выборка.Документ) = Неопределено) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаДерева = Строка.Строки.Добавить();
		ТаблицаКонтроляПовтора.Добавить(Выборка.Документ);
		текДок = Выборка.Документ;
		СтрокаДерева.Документ = текДок;
		СтрокаДерева.ВидДокумента = текДок.Метаданные().Синоним;
		СтрокаДерева.Дата = текДок.Дата;
		СтрокаДерева.Номер = текДок.Номер;
		Если Метаданные.Документы[текДок.Метаданные().Имя].Реквизиты.Найти("Организация") <> Неопределено Тогда
			СтрокаДерева.Организация = текДок.Организация;
		КонецЕсли;
		Если Метаданные.Документы[текДок.Метаданные().Имя].Реквизиты.Найти("ПодразделениеКомпании") <> Неопределено Тогда
			СтрокаДерева.Подразделение = текДок.ПодразделениеКомпании;
		КонецЕсли;
		Если Метаданные.Документы[текДок.Метаданные().Имя].Реквизиты.Найти("Автор") <> Неопределено Тогда
			СтрокаДерева.Автор = текДок.Автор;
		КонецЕсли;		
		Если Метаданные.Документы[текДок.Метаданные().Имя].Реквизиты.Найти("СуммаДокумента") <> Неопределено Тогда
			СтрокаДерева.СуммаДокумента = текДок.СуммаДокумента;
		КонецЕсли;	
		ДобавитьВДерево(СтрокаДерева,текДок);
	КонецЦикла;
КонецПроцедуры // ДобавитьВДерево()

//обход дерева и печать элементов                     
//
// Параметр:
//	Дерево			- ДеревоЗначений	- Дерево
//	СтрокаДерева	- Структура			- Строка дерева
//	Макет           - ТабличныйДокумент - Макет
//	Таб             - ТабличныйДокумент - Табличный документ
//	Уровень         - Число				- Уровень
//	спУровни        - Число				- Уровни
//
Процедура ВывестиСтрокуДерева(Дерево, СтрокаДерева, Макет, Таб, Уровень, спУровни)
	Документ= СтрокаДерева.Документ;
	Область	= Макет.ПолучитьОбласть("ГрУгол|В");
	Таб.Вывести(Область);
	Для к = 1 По Уровень-1 Цикл
		Область	= Макет.ПолучитьОбласть(?(спУровни[к]=1,"ГрЛиния|Линии","ГрПуст|Линии"));
		Таб.Присоединить(Область);
	КонецЦикла;
	
	Если СтрокаДерева.Родитель.Строки.Индекс(СтрокаДерева)+1 = СтрокаДерева.Родитель.Строки.Количество() Тогда
		Область = Макет.ПолучитьОбласть("ГрУгол|Линии");
		спУровни[Уровень] = 0;
	Иначе
		Область = Макет.ПолучитьОбласть("ГрДвСекция|Линии");
		спУровни[Уровень] = 1;
	КонецЕсли;
	Таб.Присоединить(Область);
	
	Если Документ = ВыбДокумент Тогда
		Область = Макет.ПолучитьОбласть("ТекДок|Тело");
	Иначе
		Область = Макет.ПолучитьОбласть("Док|Тело");
	КонецЕсли;
	Область.Параметры.Расшифровка	= Новый Структура("Документ" , Документ);
	Попытка
		СтрокаСумма = ?(Документ.СуммаДокумента = 0,"",Символы.ПС + "Сумма: " + Документ.СуммаДокумента + " " + Документ.ВалютаДокумента);
	Исключение
		СтрокаСумма = "";
	КонецПопытки; 
	Область.Параметры.ПечДок		= Документ.ХозОперация.Наименование + " № "+СокрЛП(Документ.Номер) + " от "+Формат(Документ.Дата,"ДФ=dd.MM.yyyy hh:mm:ss") + СтрокаСумма;
	Область.Параметры.Док			= Документ;
	Область.Рисунки[0].Картинка		= ?(Документ.ПометкаУдаления, БиблиотекаКартинок.ДокументПомеченНаУдаление, ?(Документ.Проведен, БиблиотекаКартинок.ДокументПроведен, БиблиотекаКартинок.ДокументНеПроведен));
	Таб.Присоединить(Область);
	
	Для Каждого Строка Из СтрокаДерева.Строки Цикл
		ВывестиСтрокуДерева(Дерево, Строка, Макет, Таб, Уровень+1, спУровни);
	КонецЦикла;
КонецПроцедуры // ВывестиСтрокуДерева()

#КонецЕсли

ТаблицаКонтроляПовтора = Новый Массив;
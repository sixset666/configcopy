// Обработка СофтФон, Проф, ред.1
// Предназначена для обработки звонков IP телефонии, полученных от сервера
// Может быть врезана в качестве объекта-обработчика в произвольную конфигурацию
// так же имеет встроенную форму СофтФона "Телефон" для
// индикации состояния текущих звонков и управления ими

// Обработка загружает и использует внешнюю компоненту защиты,
// загружаемую при создании объекта-обработки и хранимую в глобальной переменной
// компонента подключается к серверу и получает от него события от станции.

// Компонента получает следующие события:
//  Change – возникает при изменении параметров звонка на линии. В качестве данных события 
//           передается идентификатор звонка (hCall). Для получения параметров звонка приложение 
//           должно вызвать метод GetCallInfo.
//
// 			 Описание структуры звонка компоненты возвращаемой GetCallInfo
// 			 	hCall				– Число, идентификатор звонка
// 			 	hLine				- Число, уникальный идентификатор линии
// 			 	Line 				– Строка, имя линии
// 				CallerId 			– Строка, номер вызывающего абонента (Номер А)
// 				CalledId 			– Строка, набранный номер (Номер Б)
// 				CallerInfoNamе		– Строка, имя номера абонента в телефонной станции (Номер А)
// 				CalledInfoNamе 		– Строка, имя номера абонента в телефонной станции (Номер Б)
// 				State 				– Строка, состояние звонка, вида (CONNECTED, OFFERING и т.д.)
// Определяется по структуре ВидыСобытийСтанции
// 				StateDescr		 	- Строка, описание состояния звонка
// 				Origin 				– Число, тип звонка, определяется по структуре ВидыВходящихИсходящихСобытий
// 				OriginDescr		 	- Строка, описание типа звонка
//				LineCaption 		– имя на телефонном аппарате
//				ConnectedId 		– уникальный идентификатор соединения, существует при соединении 2 телефонов
//				RelatedCallId 		– уникальный идентификатор связанных звонков
//				RedirectionId 		– уникальный идентификатор перенаправленного звонка
//				RedirectingId 		- уникальный идентификатор перенаправляемого звонка
//				CallData 			– данные связанные со звонком (уникальный идентификатор передающийся при переводе звонка)
//				Trunk 				– транк звонка
//				Reason				– причина звонка
//				LineDialableAddr	- номер телефона привязанного к линии
//              VirtualLineName     - виртуальное имя линии
//              VirtualLineCaption  - виртуальное название линии
//              VirtualDialableAddr - виртуальный номер линии

//
//  LineCloseOpen – возникает при открытие или закрытии линии на сервере.
//                  В качестве данных события передается имя линии (LineName). Для получения информации о линии 
//                  надо вызвать метод ПолучитьОткрытиеЗакрытиеЛинии
//			 
//			 Описание структуры линии компоненты возвращаемой ПолучитьОткрытиеЗакрытиеЛинии
//				coType				- признак открытия линия: 0-закрыта, 1-закрыта 
//	  			hLine 				– уникальный идентификатор линии
//				LineName 			– имя линии
//				LineDialableAddr 	– номер телефона, привязанный к линии
//				LineBusyState       - признак состояния линии, 0 - неопределено, 1 - свободна, 2 - занята 
//              VirtualLineName     - виртуальное имя линии
//              VirtualLineCaption  - виртуальное название линии
//              VirtualDialableAddr - виртуальный номер линии
//
//  LoginStateChange – возникает при изменение состояния подключения к серверу. 
//В качестве данных события передается номер события подключения.
//					   После получения описания события необходимо вызвать метод ПолучитьОписаниеСтатусаПодключения
//
//			 Описание номеров событий получаемых от сервера
//				0 					– Неопределенное состояние
//				1 					– Исходное состояние
//				2 					– Клиент ожидает датограмму с баннером
//				3 					– Сервер ожидает авторизационную информацию 
//				4 					– Клиент ожидает информацию о сервере
//				5 					– Сервер ожидает информацию о клиенте
//				6 					- Клиент готов к работе
//				7 					– Клиент завершил работу
//
//  LineLock 	– возникает когда линия становится недоступной для вызова. В качестве данных передается hLine линии 
//				  для которой пришло событие.
//
//  LineUnLock  – возникает когда линия становится доступной для вызова. В качестве данных передается hLine линии 
//				  для которой пришло событие.
//
//  LogParams   – возникает, когда сервер передает параметры ведения лога. Структуру параметров подключения необходимо
//				  получить вызвав метод ПолучитьПараметрыВеденияЛога
//
//			 Описание структуры параметров ведения лога на сервере, полученных из ПолучитьПараметрыВеденияЛога
//				Loging 				- 0 – лог не ведется, 1 –лог ведется
//				Server 				– имя сервера
//				Auth 				– тип авторизации: 0 – на уровне SQL server, 1 – на уровне Windows
//				Login 				– логин, если авторизация на уровне SQL server
//				Password 			– пароль, если авторизация на уровне SQL server
//				CatalogName 		– имя базы данных (рабочий каталог в терминах SQL server)
//				ConnectionTimeout   - время которое клиент ждет ответ сервера

//	Для того, что бы избежать возможных ошибок работы различных билдов сервера
// и компонеты, есть метод ПолучитьBнутрВерсию, GetIntVer.
//		ПолучитьBнутрВерсию - возвращает структуру содержащую внутреннюю версию сервера
// и внешней компоненты. Билды должны совпадать.
//
//			 Возвращаемое значение:
//				SrvVer 				– внутренняя версия сервера
//				CmpVer				– внутренняя версия внешней компоненты

/////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБРАБОТКИ

#Если Клиент Тогда
	
Перем ВидыСобытийСтанции Экспорт;            // Структура содержит набор состояний возвращаемых станцией
Перем ВидыВходящихИсходящихСобытий Экспорт;  // Структура содержит набор входящих/исходящий событий
Перем ВидыВходящихИсходящихСобытийОписание Экспорт; // Структура содержит описание набора входящих/исходящий событий
Перем ШаблонСтруктураТелефона Экспорт;       // Структура общего шаблона номера телефона
Перем ТипВходящийИсходящийЗвонок Экспорт;    // Структура содержит тип входящий или исходящий звонок
Перем ТипВнутреннийВнешнийЗвонок Экспорт;    // Структура содержит тип внутренний или внешний звонок
Перем ТипыПоддерживаемогоОборудования Экспорт; // Структура содержит поддерживаемые типы оборудования

Перем НабранныйКонтакт Экспорт;              // При наборе номера хранит структуру номера и контакт из базы данных с которым идет соединение

Перем ФормаТелефон Экспорт;                  // Ссылается на открытую форму телефона

Перем ИмяСервера Экспорт;					 // Имя сервера 
Перем Порт Экспорт;							 // Порт сервера
Перем ТекущийСтатусПодключения Экспорт;		 // Текущий статус подключения к серверу

Перем ПровайдерИспользуемойЛинии Экспорт;    // номер провайдера линии к которой мы подключены

// Определяет кэш таблицу хранящую результат запроса к серверу SQL, содержащему список телефонов
Перем ШаблонТаблицыРезультатаSQL Экспорт;	 // Таблица шаблон, хранить шаблон колонок заполняемых при выполнении
											 //  запроса к серверу SQL, содержащему список телефонов
Перем УникальныйУказательНаЛинию Экспорт;	 // Число, Используется только при работе с SQL сервером, отличается от Указателя на линию, тем 
											 //  что указатель на линию измениться если сервер перестартовать, а уникальный нет.
Перем ПризнакПервогоЗапуска Экспорт;		 // Признак того, что обработка была запущена первый раз. Т.е. старт системы. 
											 //  Используется для определения пропущенных вызовов при старте.
Перем ИдентификаторСобытияСтанцииПоследнее;  // Хранит предыдущее состояние звонка
Перем ИдентификаторПеревода;  				 // Содержит идентификатор того действия, которое необходимо выполнить со звонком.
											 // Например, "БезусловныйПеревод" означает, что выполняется безусловный перевод.

Перем ТаблицаКЭШТелефонныхНомеров Экспорт;   // Таблица инициализируется при старте СофтФона, и содержит таблицу телефонных
											 //  номеров ФизЛиц и Пользователей. Используется в списке телефонов, для того, 
											 //  чтобы ускорить запуск и отображение состояния телефонов сотрудников
Перем глСписокДоступныхЛинийДляПерехвата Экспорт; //список значений который содержит номера телефонов 
												  //которые можно перехватывать в данный момент
												  
Перем СоздаватьНовоеСобытие Экспорт;      	 // Создавать ли Событие при звонке из события

Перем СтатусПодключения Экспорт;
Перем СтатусПодключенияSIP Экспорт;
												  
												  /////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ДЛЯ РАБОТЫ С SQL СЕРВЕРОМ

Перем sqlЛогВедется Экспорт; 						 // Булево, ведется ли лог на сервере. Да или нет.ю
Перем sqlПровайдер Экспорт;                          // Строка, содержит провайдера подключения к sql.
Перем sqlИмяСервера Экспорт;                         // Строка, имя сервера sql. 
Перем sqlИмяБазыДанных Экспорт;                      // Строка, имя базы данных. В терминах sql каталог.
Перем sqlИмяПользователя Экспорт;                    // Строка, имя пользователя, если авторизация на уровне sql.
Перем sqlПарольПользователя Экспорт;                 // Строка, пароль пользователя, если авторизация на уровне sql.
Перем sqlМестоВыполненияЗапроса Экспорт;			 // Число, место выполнения запроса, сервер или клиент. CursorLocation
Перем sqlТипАвторизации Экспорт;					 // Число, тип авторизации, авторизация на уровне sql (0) или windows (1).
Перем sqlТаймАут Экспорт;							 // Число, тайм аут время доступа к серверу и выполнения запросов.

// БАННЕР
Перем ДанныеБаннера Экспорт;                     // ТаблицаЗначений, данные баннера
Перем СписокОбработанныхЗвонковБаннером Экспорт; // СписокЗначений, список обработанных баннером звонков

/////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ИНТЕГРАЦИИ С ВНЕШНЕЙ SQL таблицей

// Находит контакт в sql базе, по связанному звонку
//  Если контакт был найден генерируется оповещение в которое
//  передается структура найденного контакта и номера телефона
Функция SQLНайтиКонтакт(СтрокаЗвонок)
	Результат = Ложь;
	
	// Если строка звонка пуста, то сразу выходим
	Если СтрокаЗвонок = Неопределено Тогда Возврат Результат; КонецЕсли;
	
	////////////////////////////////
	////////////////
	//// ВЫПОЛНЯЕМ ПОИСК СВЯЗАННОГО ЗВОНКА
	//
	
	ТекстЗапроса = "
	|set DateFormat dmy
	|EXEC GetCallDataContact @CallData = '"+СтрокаЗвонок.GUIDЗвонка+"',
	|	@StartTime = '"+Строка(НачалоДня(ТекущаяДата() - (24*60*60)))+".000', 
	|	@EndTime = '"+Строка(КонецДня(ТекущаяДата()))+".999'";
	
	// Создаем com объект, предназначенный для подключения к sql
	Connection = Новый ComОбъект("ADODB.Connection");
	
	РезультатПоиска = Неопределено;
	// Выполняем подключение к серверу
	Если SQLПодлючитьсяКСерверу(Connection) Тогда
		
		// Создаем com объект, будет содержать результат запроса
		RecordSet =  Новый ComОбъект("ADODB.RecordSet");
		Попытка
			RecordSet.Open(СокрЛП(ТекстЗапроса), Connection, 1, 3);
			
			Пока НЕ RecordSet.EOF Цикл
			                           
				GUIDСоединенС = СокрЛП(RecordSet.Fields(0).Value);  //GUIDConnected
				Телефон = СокрЛП(RecordSet.Fields(1).Value);  //PhoneNumber
				// Если номер телефона совпадает с номером телефона для которого ищем, тогда 
				//  нашли нужный контакт
				Если Телефон = СтрокаЗвонок.НомерТелефона Тогда
					Если ЗначениеЗаполнено(GUIDСоединенС) Тогда
						РезультатПоиска = НайтиКонтактПоGUID(GUIDСоединенС);
						// Если значение было найдено, то заполним структуру номера телефона, и передадим позже в форму
						Если НЕ РезультатПоиска = Неопределено Тогда
							Поле1 = СокрЛП(RecordSet.Fields(2).Value);        	// CodeCountry
							Поле2 = СокрЛП(RecordSet.Fields(3).Value);        	// CodeCity
							Поле3 = СокрЛП(RecordSet.Fields(4).Value);			// ExternalPhoneNumber
							Поле4 = СокрЛП(RecordSet.Fields(5).Value);			// InternalPhoneNumber
							СтруктураКонтакта = CRMПреобразоватьКонтактВСтруктуру(РезультатПоиска, Поле1, Поле2, Поле3, Поле4);
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЕсли;
				RecordSet.MoveNext();
			КонецЦикла;
		
			SQLОтключитьсяОтСервера(Connection);
		Исключение
			Сообщить("Подключиться к SQL серверу не удалось. База: "+sqlИмяБазыДанных, СтатусСообщения.Важное);
			Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
			Сообщить("Текст запроса: "+ТекстЗапроса);
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ РезультатПоиска = Неопределено Тогда 
		СтруктураКонтакта.Вставить("ИдентификаторЗвонка", СтрокаЗвонок.ИдентификаторЗвонка);
		СтруктураКонтакта.Вставить("ИмяСобытия", "ВыбранКонтакт");
		ФормаТелефон.ОбработкаВыбора(СтруктураКонтакта, ФормаТелефон);
		Результат = Истина; 
	КонецЕсли;
	
	// ПОИСК СВЯЗАННЫХ ЗВОНКОВ ЗАКОНЧИЛИ
	//////////////
	//////////////////////////////
	
	Возврат Результат;

КонецФункции

// Получает параметры от sql сервера содержащего списки звонков
//  настраивается на сервере.
Процедура SQLПолучитьПараметры()
	// Проверим статус подключения если он Готов, то можно получать иначе сбрасываем параметры
	Если ТекущийСтатусПодключения = 6 Тогда
		ПараметрыСервераСпискаЗвонков = РарусСофтФонКомпонента.ПолучитьПораметрыВеденияЛога();
		// Определяет вообще ведется лог или нет.
		sqlЛогВедется				= Булево(ПараметрыСервераСпискаЗвонков.Loging);
		sqlПровайдер          		= "sqloledb";
		sqlИмяСервера         		= ПараметрыСервераСпискаЗвонков.Server;
		sqlИмяБазыДанных      		= ПараметрыСервераСпискаЗвонков.CatalogName;
		sqlИмяПользователя    		= ПараметрыСервераСпискаЗвонков.Login;
		sqlПарольПользователя 		= ПараметрыСервераСпискаЗвонков.Password;
		sqlМестоВыполненияЗапроса 	= 2; //
		sqlТипАвторизации			= Число(ПараметрыСервераСпискаЗвонков.Auth);
		sqlТаймАут					= ПараметрыСервераСпискаЗвонков.ConnectionTimeout;
		// Проверяем были ли зарегистрированы непринятые звонки,
		//  если были то на форме необходимо отметить признак
		Если SQLЕстьНеотвеченныеЗвонки() Тогда
			ФормаТелефон.ПризнакПропущенногоЗвонка = Истина;
			ФормаТелефон.УправлениеИндикациейПропущенныхЗвонков();
		КонецЕсли;
	Иначе
		sqlЛогВедется 				= Ложь;			// Ведение лога отключено
		sqlПровайдер				= "sqloledb"; 	// Провайдер работы с SQL Server
		sqlМестоВыполненияЗапроса	= 2; //		  	// Место выполнения запроса, на сервере
		sqlТипАвторизации           = 1;		  	// Тип авторизации Windows
		sqlТаймАут                  = 0;		  	// Время ожидания ответа - 0
	КонецЕсли;
КонецПроцедуры

// Функция предназначена для подключения с sql серверу содержащему список звонков
Функция SQLПодлючитьсяКСерверу(Connection, ТекстЗапроса="")
	// Если лог на сервере не ведется то и коннектится ни к чему не нужно
	Если НЕ sqlЛогВедется ИЛИ (УникальныйУказательНаЛинию = 0) Тогда Возврат Ложь; КонецЕсли;
		
	///////////////////////////////////////////
	/////////////
	///// Работа с прямым указанием всех необходимых параметров
	
	// Провайдер работы с SQL
	Connection.Provider = sqlПровайдер;
	Connection.CursorLocation = sqlМестоВыполненияЗапроса;
	// Свойства имя сервера и имя базы данных (каталог)
	Connection.Properties("Data Source").Value = sqlИмяСервера;
	Connection.Properties("Initial Catalog").Value = sqlИмяБазыДанных;
	
	// Выбираем тип авторизации на сервере
	// Два варианта как пользователь форточек (1)
	//  или как пользователь SQL (0)
	
	Если sqlТипАвторизации = 0 Тогда // sqlТипАвторизации = 0 или "SQL"
		Connection.Properties("User ID").Value = sqlИмяПользователя;
		Connection.Properties("Password").Value = sqlПарольПользователя;
	Иначе // sqlТипАвторизации = 1 или "Windows"
		Connection.Properties("Integrated Security").Value = "SSPI";
	КонецЕсли;

	// Открываем соединение с сервером
	Попытка
		Connection.Open();
		// 1. Если запрос не передан, то создаем коннект, для дальнейшего получения recordset
		// 2. Если текст запроса передан, то результат не важен нужно просто выполнить
		Результат = Истина;
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда 
			Connection.BeginTrans();
			Попытка
				Connection.Execute(ТекстЗапроса, 0, 128); 
				Connection.CommitTrans();
			Исключение
				Connection.RollBackTrans();
				Сообщить("Подключиться к SQL серверу не удалось. База: "+sqlИмяБазыДанных, СтатусСообщения.Важное);
				Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
				Сообщить("Текст запроса: "+ТекстЗапроса);
				Результат = Ложь;
			КонецПопытки;
		КонецЕсли;
	Исключение
		Сообщить("Подключиться к SQL серверу не удалось. База: "+sqlИмяБазыДанных, СтатусСообщения.Важное);
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
		Результат = Ложь;
	КонецПопытки;
	
	Возврат Результат;
КонецФункции

// Выполняет отключение от сервера
Функция SQLОтключитьсяОтСервера(Connection)
	Connection.Close();
	Connection = Неопределено;
КонецФункции

// Функция в таблицу отслеживания звонка загружает результат запроса
Функция SQLВернутьРезультатЗапросаВТаблицуЗначений(ТекстЗапроса) Экспорт
	ТаблицаРезультат = ШаблонТаблицыРезультатаSQL.Скопировать();
	
	// Удалим строки если они каким либо образом были заполнены
	Если НЕ ТаблицаРезультат.Количество() = 0 Тогда
		ТаблицаРезультат.Очистить();
		ШаблонТаблицыРезультатаSQL.Очистить();
	КонецЕсли;
	
	// Создаем com объект, предназначенный для подключения к sql
	Connection = Новый ComОбъект("ADODB.Connection");
	
	// Выполняем подключение к серверу
	Если SQLПодлючитьсяКСерверу(Connection) Тогда
		
		// Создаем com объект, будет содержать результат запроса
		RecordSet =  Новый ComОбъект("ADODB.RecordSet");
		ЗапросВыполнен = Истина;
		Попытка 		
			RecordSet.Open(СокрЛП(ТекстЗапроса), Connection, 1, 3);
		Исключение
			Сообщить("Подключиться к SQL серверу не удалось. База: "+sqlИмяБазыДанных, СтатусСообщения.Важное);
			Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
			Сообщить("Текст запроса: "+ТекстЗапроса);
			ЗапросВыполнен = Ложь;
		КонецПопытки;
		
		Если ЗапросВыполнен Тогда
			Пока НЕ RecordSet.EOF Цикл
				
				НоваяСтрока = ТаблицаРезультат.Добавить();
				НоваяСтрока.ИдентификаторЗвонка = Число(RecordSet.Fields(4).Value); // hCallUnique
				
				НоваяСтрока.ИмяТелефона 		= СокрЛП(RecordSet.Fields(3).Value); // PhoneName
				НоваяСтрока.GUIDЗвонка 			= СокрЛП(RecordSet.Fields(6).Value); // CallData
				
				НоваяСтрока.ВремяНачала 		= Дата(RecordSet.Fields(7).Value); // StartTime
				НоваяСтрока.ВремяОкончания 		= Дата(RecordSet.Fields(8).Value); // StopTime
				
				// TAPI не всегда четко определяет внешние и внутренние звонки,
				//  так вот если номер телефона меньше или равен внутреннему номер установим его четко
				НоваяСтрока.ТелефонныйНомер		= ?(ПустаяСтрока(СокрЛП(RecordSet.Fields(14).Value)), СокрЛП(RecordSet.Fields(2).Value), СокрЛП(RecordSet.Fields(14).Value)); // PhoneNumber(2) и ConnectedId(14) 
				НоваяСтрока.ТелефонныйНомер		= CRMУбратьИзНомераТелефонаВсеБуквы(НоваяСтрока.ТелефонныйНомер);
				
				НоваяСтрока.ВнутреннийЗвонок 	= CRMОпределитьВнутреннийЗвонокПоНомеру(НоваяСтрока.ТелефонныйНомер, глМаксимальнаяДлинаВнутреннегоНомера);
				НоваяСтрока.ВходящийЗвонок 		= Булево(RecordSet.Fields(0).Value); // Incomming
				
				Если ПровайдерИспользуемойЛинии = 1 ИЛИ ПровайдерИспользуемойЛинии = 2 Тогда
					IsAnsw = RecordSet.Fields(16).Value;
					Если НЕ IsAnsw = Null Тогда
						НоваяСтрока.НепринятыйЗвонок	= Булево(RecordSet.Fields(16).Value); // IsAnswer
					КонецЕсли;
				Иначе
					НоваяСтрока.НепринятыйЗвонок	= НЕ Булево(RecordSet.Fields(1).Value); // Answer
				КонецЕсли;

				НоваяСтрока.НепросмотренныйЗвонок = НЕ Булево(RecordSet.Fields(15).Value); // Marked - не просмотренный пропущенный
				
				// Выполним поиск ссылки 
				ЗаполнитьПоляНомераТелефонаИзБазыДанных = Ложь;
				GUIDСоединенС = СокрЛП(RecordSet.Fields(9).Value); // GUIDConnected
				РезультатПоиска = Неопределено;
				Если ЗначениеЗаполнено(GUIDСоединенС) Тогда
					РезультатПоиска = НайтиКонтактПоGUID(GUIDСоединенС);
				КонецЕсли;
				Если РезультатПоиска = Неопределено Тогда
					Результат = CRMНайтиТелефонВКонтактнойИнформации(Неопределено, НоваяСтрока.ТелефонныйНомер, глКоличествоХранимыхЦифрТелефона);
					Если Результат.Пустой() Тогда
						Если СтрДлина(СокрЛП(НоваяСтрока.ТелефонныйНомер)) > глМаксимальнаяДлинаВнутреннегоНомера И СтрДлина(СокрЛП(НоваяСтрока.ТелефонныйНомер)) <= 7 Тогда
							Результат = CRMНайтиТелефонВКонтактнойИнформации(Неопределено, глКодГорода + НоваяСтрока.ТелефонныйНомер, глКоличествоХранимыхЦифрТелефона);
						КонецЕсли;
					КонецЕсли;
					Если НЕ Результат.Пустой() Тогда
						Выборка = Результат.Выбрать();
						Выборка.Следующий();
						РезультатПоиска = Выборка.Объект;
					КонецЕсли;
				КонецЕсли;	
				Если НЕ РезультатПоиска = Неопределено Тогда
					НоваяСтрока.Объект = РезультатПоиска;
					ЗаполнитьПоляНомераТелефонаИзБазыДанных = Истина;
				КонецЕсли;
				Если CRMОпределитьВнутреннийЗвонокПоНомеру(НоваяСтрока.ТелефонныйНомер, глМаксимальнаяДлинаВнутреннегоНомера) Тогда
					НоваяСтрока.Поле4 = НоваяСтрока.ТелефонныйНомер;
				Иначе
					НоваяСтрока.Поле3 = НоваяСтрока.ТелефонныйНомер;
				КонецЕсли;
				НоваяСтрока.Представление = НоваяСтрока.ТелефонныйНомер;
				RecordSet.MoveNext();
			КонецЦикла;
			
		КонецЕсли;
		
		SQLОтключитьсяОтСервера(Connection);
		
	КонецЕсли;
	
	Возврат ТаблицаРезультат;
КонецФункции

// Функция в таблицу отслеживания звонка загружает результат запроса
Функция SQLВыполнитьЗапрос(ТекстЗапроса) Экспорт
	// Создаем com объект, предназначенный для подключения к sql
	Connection = Новый ComОбъект("ADODB.Connection");
	
	Результат = Ложь;
	// Выполняем подключение к серверу
	Если SQLПодлючитьсяКСерверу(Connection, СокрЛП(ТекстЗапроса)) Тогда
		
		SQLОтключитьсяОтСервера(Connection);
		
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Функция получает запрос с сервера и определяет были ли пропущенные вызовы
//  используется при старте системы (и первом подключении к серверу), 
//  нужна для индикации пропущенных вызовов
//  зарегистрированных в тот момент когда 1С:Предприятие запущено не было.
Функция SQLЕстьНеотвеченныеЗвонки() Экспорт
	// Если не первый запуск, то выходим
	Если НЕ ПризнакПервогоЗапуска Тогда Возврат Ложь; КонецЕсли;
	
	Результат = Ложь; ДнейМинус = 1;
	
	//Если провайдер панасоник номер провайдера 1 или 2 тогда нужно использовать GetNotAnswerCallEX 
	если ПровайдерИспользуемойЛинии = 1 или ПровайдерИспользуемойЛинии = 2 Тогда
		ХранимаяПроцедура = "GetNotAnswerCallEx"
	Иначе
		ХранимаяПроцедура = "GetNotAnswerCall"
	КонецЕсли;
	
	ТекстЗапроса = "
	|set DateFormat dmy
	|EXEC "+ХранимаяПроцедура+" @LineID = "+CRMФорматЧислоВСтроку(УникальныйУказательНаЛинию)+",
	|	@StartTime = '"+Строка(НачалоДня(ТекущаяДата() - ДнейМинус*(24*60*60)))+".000', 
	|	@EndTime = '"+Строка(КонецДня(ТекущаяДата()))+".999'";
	
	// Создаем com объект, предназначенный для подключения к sql
	Connection = Новый ComОбъект("ADODB.Connection");
	
	// Выполняем подключение к серверу
	Если SQLПодлючитьсяКСерверу(Connection) Тогда
		
		// Создаем com объект, будет содержать результат запроса
		RecordSet =  Новый ComОбъект("ADODB.RecordSet");
		ЗапросВыполнен = Истина;
		Попытка 		
			RecordSet.Open(СокрЛП(ТекстЗапроса), Connection, 1, 3);
		Исключение
			Сообщить("Подключиться к SQL серверу не удалось. База: "+sqlИмяБазыДанных, СтатусСообщения.Важное);
			Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
			Сообщить("Текст запроса: "+ТекстЗапроса);
			ЗапросВыполнен = Ложь;
		КонецПопытки;
		
		Если ЗапросВыполнен Тогда
			// Пройдем по полученному recordset-у как только найден неотвеченный вызов
			//  сразу выходим
			Пока НЕ RecordSet.EOF Цикл
				                               
				Если Число(RecordSet.Fields(1).Value) = 0 Тогда // Answer
					Результат = Истина;
					Прервать;
				КонецЕсли;								  
				
				RecordSet.MoveNext();
			КонецЦикла;
		КонецЕсли;
		
		SQLОтключитьсяОтСервера(Connection);
		
	КонецЕсли;
	
	// пометим что первый запуск прошел и 
	//  более вызывать проверку пропущенный звонков не следует
	ПризнакПервогоЗапуска = Ложь;
	
	Возврат Результат;
КонецФункции

// Функция выполняет пометку пропущенных вызовов
//  как просмотренные пользователем, и соответственно
//  они не будут больше показываться
Функция SQLПометитьПропущенныеЗвонкиПросмотренными(ТаблицаПропущенных) Экспорт
	ТекстЗапроса = "";
	Для Каждого ТекущийПропущенныхЗвонок Из ТаблицаПропущенных Цикл
		// Только идентификатор определен, иначе запрос вернет ошибку
		Если НЕ ТекущийПропущенныхЗвонок.ИдентификаторЗвонка = 0 Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|EXEC SetNotAnswerCallFlagView @LineID = "+CRMФорматЧислоВСтроку(УникальныйУказательНаЛинию)+",
			|	@hCallUnique = "+CRMФорматЧислоВСтроку(ТекущийПропущенныхЗвонок.ИдентификаторЗвонка);
		КонецЕсли;
	КонецЦикла;
	
	// Создаем com объект, предназначенный для подключения к sql
	Connection = Новый ComОбъект("ADODB.Connection");
	
	ЗапросВыполнен = Ложь;
	// Выполняем подключение к серверу
	Если SQLПодлючитьсяКСерверу(Connection, СокрЛП(ТекстЗапроса)) Тогда
		
		SQLОтключитьсяОтСервера(Connection);
		
		ЗапросВыполнен = Истина;
	КонецЕсли;
	
	Возврат ЗапросВыполнен;
КонецФункции

// Процедура регистрирует, изменяет дополняет данные о звонке в базе данных
//  в зависимости от статуса.
Процедура SQLЗарегистрироватьЗвонок(ТекущийЗвонок) Экспорт
	Если НЕ ЗначениеЗаполнено(ТекущийЗвонок.GUIDЗвонка) Тогда Возврат; КонецЕсли;
	
	НетКонтрагент       = НЕ ЗначениеЗаполнено(ТекущийЗвонок.Контрагент);
	НетКонтактноеЛицо 	= НЕ ЗначениеЗаполнено(ТекущийЗвонок.КонтактноеЛицо);
	НеСоединятьС		= НЕ ЗначениеЗаполнено(ТекущийЗвонок.СоединитьССотрудником);
	
	СоединенныйСотрудник = Неопределено;
	Если НЕ НетКонтактноеЛицо Тогда
		СоединенныйСотрудник = ТекущийЗвонок.КонтактноеЛицо;
	ИначеЕсли НЕ НетКонтрагент Тогда
		СоединенныйСотрудник = ТекущийЗвонок.Контрагент;
	ИначеЕсли НЕ НеСоединятьС Тогда
		СоединенныйСотрудник = ТекущийЗвонок.СоединитьССотрудником;
	КонецЕсли;
	
	// Если не с кем не соединяли, то никаких изменений в базу записывать не нужно
	Если СоединенныйСотрудник = Неопределено Тогда Возврат; КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(CRMФорматЧислоВСтроку(ТекущийЗвонок.УникальныйИдентификаторЗвонка)) Тогда
		Возврат;
	КонецЕсли;
	
	// Обновляем поля звонка найденной контактной информацией
	ТекстЗапроса = "
	|set DateFormat dmy
	|EXEC SetGUIDConnected @StartTime = '"+Строка(НачалоДня(ТекущаяДата() - (24*60*60)))+".000', 
	|	@EndTime = '"+Строка(КонецДня(ТекущаяДата()))+".999',
	|   @hCallUnique = '"+CRMФорматЧислоВСтроку(ТекущийЗвонок.УникальныйИдентификаторЗвонка)+"',
	|   @GUIDConnected = '"+СокрЛП(СоединенныйСотрудник.УникальныйИдентификатор())+"',
	|   @GUIDCollaborator = "+?(НЕ НетКонтрагент, "'"+СокрЛП(ТекущийЗвонок.Контрагент.УникальныйИдентификатор())+"'", "NULL")+",
	|   @GUIDContact = "+?(НЕ НетКонтактноеЛицо, "'"+СокрЛП(ТекущийЗвонок.КонтактноеЛицо.УникальныйИдентификатор())+"'", "NULL")+",
	|   @GUIDConnectTo = "+?(НЕ НеСоединятьС, "'"+СокрЛП(ТекущийЗвонок.СоединитьССотрудником.УникальныйИдентификатор())+"'", "NULL");
	
	Если SQLВыполнитьЗапрос(ТекстЗапроса) Тогда
	КонецЕсли;
	
КонецПроцедуры                                 

// Процедура изменяет GUID, у текущего звонка на новый.
//  Может такое быть, что сначала один GUID, а затем на сервере происходит связывание
//  звонков, и текущему звонку присваивается новый GUID.
Процедура SQLИзменитьGUIDТекущегоЗвонка(ТекущийЗвонок, СтарыйGUID) Экспорт
	Если НЕ ЗначениеЗаполнено(ТекущийЗвонок.GUIDЗвонка) Тогда Возврат; КонецЕсли;
	
	ТекстЗапроса = "
	|set DateFormat dmy
	|EXEC SetNewCallData @StartTime = '"+Строка(НачалоДня(ТекущаяДата() - (24*60*60)))+".000', 
	|	@EndTime = '"+Строка(КонецДня(ТекущаяДата()))+".999',
	|   @CallDataOld = '"+СтарыйGUID+"',
	|   @CallDataNew = '"+ТекущийЗвонок.GUIDЗвонка+"'";
	
	Если SQLВыполнитьЗапрос(ТекстЗапроса) Тогда
	КонецЕсли;
	
КонецПроцедуры                                 

/////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ИНТЕГРАЦИИ С БАЗОЙ ДАННЫХ

// {{{ СофтФон, Проф }}} НАЧАЛО Код встраивания в произвольную конфигурацию

// Функция определяет используется ли СофтФон в системе и для текущего пользователя
Функция ИспользоватьСофтФон() Экспорт
		
	глИспользоватьСофтФонПроф = (Константы.CRM_ИспользуемыйСофтФон.Получить() = Перечисления.CRM_ИспользуемыйСофтФон.СофтФонПроф);
	глИспользоватьСофтФонПрофТекущийПользователь = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпИспользоватьСофтФон");
	
	// Проверим если в настройках учета не выбрано "Использовать СофтФон, Проф" ругнемся на 
	//  пользователя и выйдем
	Если глИспользоватьСофтФонПрофТекущийПользователь И НЕ глИспользоватьСофтФонПроф Тогда
		Если НЕ Константы.CRM_ИспользуемыйСофтФон.Получить() = Перечисления.CRM_ИспользуемыйСофтФон.СофтФонПроф Тогда
			Предупреждение("        Внимание!!! В настройках параметров учета, на закладке <СофтФон>"+Символы.ПС+
					"не выбрано значение использовать <Колл-Центр>. Загрузка СофтФона, Проф отменена.",
					30,
					"Внимание!!!");
		КонецЕсли;
	КонецЕсли;
	
	Возврат глИспользоватьСофтФонПроф И глИспользоватьСофтФонПрофТекущийПользователь;
КонецФункции

// Выполняем подключение Софтфона, вызов функции располагается в модуле приложения
//  в процедуре ПриСтартеСистемы
Функция ПодключитьСофтФон() Экспорт
	// Устанавливаем имя компоненты и актуальный релиз
	глИмяКомпоненты = "rtup";
	глИмяОборудования = "СофтФонПроф";
	глАктуальныйРелиз = 3;
	РезультатПодключенияСофтФона = Ложь;
	Попытка
		// Можно использовать софт фон
		Если ИспользоватьСофтФон() Тогда
			// Установим значения по умолчанию настроек пользователя.
			УстановитьЗначенияПоУмолчанию();
			// Если форма уже определена, то вызовем ее
			Если ФормаТелефон = Неопределено Тогда
				ФормаТелефон = РарусСофтФонПроф.ПолучитьФорму("Телефон");
			КонецЕсли;
			Если глИспользоватьСофтФонПрофТекущийПользователь Тогда
				Если ФормаТелефон.Открыта() Тогда
					ФормаТелефон.Активизировать();
				Иначе
					ФормаТелефон.Открыть();
				КонецЕсли;
				Если НЕ (РарусСофтФонКомпонента = Неопределено) Тогда
					РезультатПодключенияСофтФона = Истина;
					ЗаполнитьСписокЛинийПерехвата();
				КонецЕсли;	
			Иначе
				// Если флаг изменился, то принудительно закроем форму
				Если ФормаТелефон.Открыта() Тогда
					ФормаТелефон.Закрыть();
				КонецЕсли;
				// Отключение СофтФона, результат отработки процедуры истина
				РезультатПодключенияСофтФона = Истина;
			КонецЕсли;
		Иначе
			// Если флаг изменился, то принудительно закроем форму
			Если НЕ (ФормаТелефон = Неопределено) Тогда
				Если ФормаТелефон.Открыта() Тогда
					ФормаТелефон.Закрыть();
				КонецЕсли;
				// Отключение СофтФона, результат отработки процедуры истина
				РезультатПодключенияСофтФона = Истина;
			КонецЕсли;
		КонецЕсли;
	Исключение
		Сообщить("ВНИМАНИЕ!!! Инициализация СофтФона, Проф не выполнена.", СтатусСообщения.ОченьВажное);
		Сообщить(ОписаниеОшибки());
		РезультатПодключенияСофтФона = Ложь;
	КонецПопытки;
	Возврат РезультатПодключенияСофтФона;
КонецФункции

// Функция выполняется перед завершении работы
//  завершает работу 
Функция ЗавершитьСофтФон() Экспорт
	Если НЕ РарусСофтФонКомпонента = Неопределено Тогда
		// Отключаем сервер
		ОтключитьСервер();
		// завершаем работу
		РарусСофтФонКомпонента.ЗавершитьРаботуСофтфона();
	КонецЕсли;
	Возврат Истина;
КонецФункции

// Функция преобразует параметры в строковый вид в виде 
// [Параметр1]          [Параметр2]         [Параметр3][Параметр4][Параметр5]    [Параметр6]
// [ИдентификаторЗвонка][Входящий/Исходящий][КодСтраны][КодГорода][НомерТелефона][Внутренний]
Функция ПреобразоватьПараметрыВСтроку(ТекущаяСтрока) Экспорт
	Результат = "["+СокрЛП(ТекущаяСтрока.ИдентификаторЗвонка)+"]";
				
	Номер = "";
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.Поле3) И НЕ ЗначениеЗаполнено(ТекущаяСтрока.Поле4) Тогда
		Номер = СокрЛП(ТекущаяСтрока.НомерТелефона);
	Иначе
		Номер = ?(НЕ ЗначениеЗаполнено(ТекущаяСтрока.Поле3), СокрЛП(ТекущаяСтрока.Поле4), СокрЛП(ТекущаяСтрока.Поле3));
	КонецЕсли;
	
	ТипЗвонка = СокрЛП(ОпределитьВнутреннийВнешнийЗвонок(ТекущаяСтрока.ВходящееИсходящееСобытие, Номер));
	Результат = Результат + "["+ТипЗвонка+"]";
	
	Если Число(ТипЗвонка) = ТипВнутреннийВнешнийЗвонок.Внутренний Тогда
		ТмпКодСтраны 	= "";
		ТмпКодГорода 	= "";
		ТмпНомер 		= "";
		ТмпВнутренний 	= Номер;
	Иначе
		ТмпКодСтраны 	= ТекущаяСтрока.Поле1;
		ТмпКодГорода 	= ТекущаяСтрока.Поле2;
		ТмпНомер 		= Номер;
		ТмпВнутренний 	= "";
	КонецЕсли;
	
	Результат = Результат + "["+ТмпКодСтраны+"]"+
							"["+ТмпКодГорода+"]"+
							"["+ТмпНомер+"]"+
							"["+ТмпВнутренний+"]";
	
	Возврат Результат;
КонецФункции

// Функция разбирает строку на параметры
//  возвращает структура вида Параметр1, Параметр2, и т.д.
Функция ПреобразоватьСтрокуВПараметры(Знач ТекущаяСтрока) Экспорт
	КолОткрывающих = СтрЧислоВхождений(ТекущаяСтрока, "[");
	КолЗакрывающих = СтрЧислоВхождений(ТекущаяСтрока, "]");
	// Если открывающих меньше чем закрывающий то выходим
	Если НЕ КолОткрывающих = КолЗакрывающих Тогда Возврат Неопределено КонецЕсли;
	Результат = Новый Структура();
	Для Ном = 1 По КолОткрывающих Цикл
		НомНачала = Найти(ТекущаяСтрока, "[");
		НомКонца  = Найти(ТекущаяСтрока, "]");
		Значение = Сред(ТекущаяСтрока, НомНачала + 1, НомКонца - НомНачала - 1);
		Результат.Вставить("Параметр" + Строка(Ном), Значение);
		ТекущаяСтрока = Прав(ТекущаяСтрока, СтрДлина(ТекущаяСтрока) - НомКонца);
	КонецЦикла;
	Если КолОткрывающих < 6 Тогда
		Для Ном = КолОткрывающих + 1 По 6 Цикл
			Результат.Вставить("Параметр" + Строка(Ном), "");
		КонецЦикла;
	КонецЕсли;	
	Возврат Результат;
КонецФункции

// Открывает контакт по переданной строке, если конечно заполнены
//  поля контрагент, контактное лицо
Процедура ОткрытьКонтакт(Строка, Колонка) Экспорт
	//нужно проверить а нужно ли открывать контакт
	//Если НЕ сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпОткрыватьДокументПриСнятииТрубки") Тогда
	//	Возврат
	//КонецЕсли;
	
	Попытка
		
		Если Не Колонка = "КонтрагентЗаголовок" И Не Колонка = "КонтрагентТекст" И 
			 Не Колонка = "КонтактноеЛицоЗаголовок" И Не Колонка = "КонтактноеЛицоТекст" И 
			 Не Колонка = "СоединитьССотрудникомЗаголовок" И Не Колонка = "СоединитьССотрудникомТекст" Тогда
			
			СтрокаПараметров = ПреобразоватьПараметрыВСтроку(Строка);
			НайденныйДокумент = Строка.СсылкаНаДокумент;
			ВыбранныйДокументДляОткрытия = "Событие";
			НазваниеФормыДокументы = "ФормаДокумента";
			Если (НЕ НайденныйДокумент = Неопределено) И (НЕ НайденныйДокумент.Пустая()) Тогда
				ФормаСобытия = НайденныйДокумент.ПолучитьФорму(НазваниеФормыДокументы, ФормаТелефон, СтрокаПараметров);
			Иначе	
				ФормаСобытия = Документы[ВыбранныйДокументДляОткрытия].ПолучитьФормуНовогоДокумента(НазваниеФормыДокументы, ФормаТелефон, СтрокаПараметров);
			КонецЕсли;
			// Если форма открыта то она была найдена и ее заполнять не нужно
			Если ФормаСобытия.Открыта() Тогда 
				ФормаСобытия.ОригинальныйТелефонныйНомер = Строка.НомерТелефона;
				ФормаСобытия.Активизировать();
			Иначе
				Если ВыбранныйДокументДляОткрытия = "Событие" Тогда
					ФормаСобытия.Автор			= сфпСофтФонПроСервер.сфпТекущийПользователь();
					ФормаСобытия.ВидСобытия		= Перечисления.ВидыСобытий.ТелефонныйЗвонок;
					Если ОпределитьВходящееИсходящееСобытие(Строка.ВходящееИсходящееСобытие) = ТипВходящийИсходящийЗвонок.Входящий Тогда
						ФормаСобытия.ТипСообщения	= Перечисления.ТипыСообщений.Входящее;
					Иначе	
						ФормаСобытия.ТипСообщения	= Перечисления.ТипыСообщений.Исходящее;
					КонецЕсли;
					ФормаСобытия.Состояние		= Перечисления.СостоянияСобытий.НеОбработано;
					ФормаСобытия.ДатаНачала		= сфпСофтФонПроСервер.сфпТекущаяДата();
					ФормаСобытия.Тема 			= НСтр("ru='!!!НЕ РАЗОБРАНО!!!'");
					ФормаСобытия.Контрагент		= Строка.Контрагент;
					ФормаСобытия.КонтактноеЛицо	= Строка.КонтактноеЛицо;
					ФормаСобытия.CRM_GUIDЗвонка	= Строка.GUIDЗвонка;
					ФормаСобытия.CRM_CLONНомерА	= Строка.НомерА;
					ФормаСобытия.CRM_CLONНомерБ	= Строка.НомерБ;
					ФормаСобытия.CRM_CLONДатаВремяЗвонка	= ФормаСобытия.ДатаНачала;
					ФормаСобытия.ОригинальныйТелефонныйНомер= Строка.НомерТелефона;
					ФормаСобытия.КлючУникальности 			= СтрокаПараметров;
					ФормаСобытия.Открыть();
				КонецЕсли;
			КонецЕсли;
		Иначе
			// Открываем существующего контрагента
			Если НЕ Строка.Контрагент.Пустая() И (Колонка = "КонтрагентЗаголовок" Или Колонка = "КонтрагентТекст") Тогда 
				ФормаЭлемента = Строка.Контрагент.ПолучитьФорму("ФормаЭлемента", ФормаТелефон);
				Если ФормаЭлемента.Открыта() Тогда
					ФормаЭлемента.Активизировать();
				Иначе
					ФормаЭлемента.Открыть();
				КонецЕсли;
			ИначеЕсли НЕ Строка.КонтактноеЛицо.Пустая() И (Колонка = "КонтактноеЛицоЗаголовок" Или Колонка = "КонтактноеЛицоТекст") Тогда 
				ФормаЭлемента = Строка.КонтактноеЛицо.ПолучитьФорму("ФормаЭлемента", ФормаТелефон);
				Если НЕ ФормаЭлемента.Открыта() Тогда
					ФормаЭлемента.Активизировать();
				Иначе
					ФормаЭлемента.Открыть();
				КонецЕсли;
			// Иначе открываем существующего сотрудника
			ИначеЕсли НЕ (Строка.СоединитьССотрудником = Неопределено) И (Колонка = "СоединитьССотрудникомЗаголовок" Или Колонка = "СоединитьССотрудникомТекст") Тогда 
				ФормаЭлемента = Строка.СоединитьССотрудником.ПолучитьФорму(, ФормаТелефон);
				Если ФормаЭлемента.Открыта() Тогда
					ФормаЭлемента.Активизировать();
				Иначе
					ФормаЭлемента.Открыть();
				КонецЕсли;
			// Если не определен ни контрагент, ни контактное лицо, ни сотрудник, то создаем нового
			ИначеЕсли Строка.Контрагент.Пустая() И Строка.КонтактноеЛицо.Пустая() И (Строка.СоединитьССотрудником = Неопределено) Тогда
				// Определим в какой графе пользователь кликнул и соответственно создадим от этого нужный контакт
				ИмяОбъект = "";
				Если Колонка = "КонтрагентЗаголовок" Или Колонка = "КонтрагентТекст" Тогда
					ИмяОбъект = "Контрагенты";
				ИначеЕсли Колонка = "КонтактноеЛицоЗаголовок" Или Колонка = "КонтактноеЛицоТекст" Тогда
					ИмяОбъект = "Контрагенты";
				ИначеЕсли Колонка = "СоединитьССотрудникомЗаголовок" Или Колонка = "СоединитьССотрудникомТекст" Тогда
					ИмяОбъект = "Контрагенты";
					СписокВыбораОбъекта = Новый СписокЗначений;
					МассивТиповОбъекта = Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения.Объект.Тип.Типы();
					Для каждого ТекущийТип Из МассивТиповОбъекта Цикл
						ОбъектМетаданных = Метаданные.НайтиПоТипу(ТекущийТип);
						Если НЕ (ОбъектМетаданных.Имя = "Контрагенты") Тогда
							СписокВыбораОбъекта.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Синоним);
						КонецЕсли;
					КонецЦикла;
					Значение = СписокВыбораОбъекта.ВыбратьЭлемент("Выбрать объект");
					ИмяОбъект = ?(Значение = Неопределено, "", Значение.Значение);
				КонецЕсли;
				// Если объект не определен, то выходим
				Если НЕ ЗначениеЗаполнено(ИмяОбъект) Тогда Возврат; КонецЕсли;
				// Открываем объект
				НовыйОбъект = Справочники[ИмяОбъект].СоздатьЭлемент();
				// {{{ КОНФИГУРАЦИЯ }}} НАЧАЛО
				// CRM
 				Если ИмяОбъект = "Контрагенты" Или ИмяОбъект = "КонтактныеЛица" Тогда
					ФормаОбъекта = НовыйОбъект.ПолучитьФорму("ФормаЭлемента", ФормаТелефон);
 				Иначе
 					ФормаОбъекта = НовыйОбъект.ПолучитьФорму(, ФормаТелефон);
 				КонецЕсли;
				ФормаОбъекта.Открыть();
				// Заполняем номер телефона в набор КИ объекта
				Если ЗначениеЗаполнено(Строка.Поле3) ИЛИ ЗначениеЗаполнено(Строка.Поле4) Тогда
					НомерТелефона = CRMСформироватьСтруктуруНомераИзПолей(Строка);
				Иначе
					НомерТелефона = ПреобразоватьНомерСУчетомПрефиксов(Строка.НомерТелефона, Строка.ВходящееИсходящееСобытие, ТипВходящийИсходящийЗвонок.Входящий);
				КонецЕсли;
				ТекущаяКИ = ФормаОбъекта.НаборКонтактнойИнформации.Добавить();
				ТекущаяКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				ТекущаяКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонРабочий;
				ТекущаяКИ.Объект = НовыйОбъект.Ссылка;
				CRMЗаполнитьЗаписьРегистраСтруктуройТелефона(ТекущаяКИ, НомерТелефона, глМаксимальнаяДлинаВнутреннегоНомера, глКоличествоХранимыхЦифрТелефона);
				// Отобразим актуальное состояние
				ИзменитьАктуальноеСостояниеКИ(ФормаОбъекта.НаборКонтактнойИнформации, "ИзменениеСтатусаЛинии");
				ПараметрыЗвонка = Новый Структура();
				ПараметрыЗвонка.Вставить("ИдентификаторЗвонка",   Строка.ИдентификаторЗвонка);
				ПараметрыЗвонка.Вставить("СсылкаНаЗапись", 		  ТекущаяКИ);
				ПараметрыЗвонка.Вставить("СсылкаНаФормуТелефона", ФормаТелефон);
				ФормаОбъекта.мПараметрыЗвонка = ПараметрыЗвонка;
				ФормаОбъекта.ЭлементыФормы.КонтактнаяИнформация.ТекущаяСтрока = ТекущаяКИ;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
КонецПроцедуры

// Находит контакт по номеру телефона в базе данных
//  Если телефонный номер был найден то вызывается обработка оповещения
//  в которую передается найденное значение. Если контактов больше одного, то
//  открывается форма выбора контакта.
Процедура НайтиКонтакт(СтрокаЗвонок, ИдентификаторСобытияСтанцииПоследнее) Экспорт
	
	// Если строка звонка пуста, то сразу выходим
	Если СтрокаЗвонок = Неопределено Тогда Возврат; КонецЕсли;
	
	// Если поля номера телефона или внутреннего заполнены, то возьмем из них
	Если НЕ ЗначениеЗаполнено(СтрокаЗвонок.Поле3) И НЕ ЗначениеЗаполнено(СтрокаЗвонок.Поле4) Тогда
		НомерЗвонкаВходящего = ВернутьНомерИлиИмяТелефонаТелефона(СтрокаЗвонок.ВходящееИсходящееСобытие, СтрокаЗвонок.НомерА, СтрокаЗвонок.НомерБ);
	Иначе
		// Если заполнен и номер внешнего и номер внутреннего телефона, т.е. номер с добавочным
		//  то запомним их в структуре иначе просто как строку
		Если ЗначениеЗаполнено(СтрокаЗвонок.Поле3) И ЗначениеЗаполнено(СтрокаЗвонок.Поле4) Тогда
			НомерЗвонкаВходящего = Новый Структура("НомерТелефона, Добавочный", СтрокаЗвонок.Поле3, СтрокаЗвонок.Поле4);
		ИначеЕсли ЗначениеЗаполнено(СтрокаЗвонок.Поле3) Тогда
			НомерЗвонкаВходящего = СтрокаЗвонок.Поле3;
		Иначе
			НомерЗвонкаВходящего = СтрокаЗвонок.Поле4;
		КонецЕсли;
	КонецЕсли;
	 
	// Если номера телефона пуст то искать в 1С смысла нет.
	// По поиск в SQL базе выполнить нужно, т.к. контакт мог быть сохранен там.
	// Но только при условии, что это не внешний входящий, т.к. он не мог быть сохранен физически
	// он только появился
	Если ПустаяСтрока(НомерЗвонкаВходящего) Тогда 
		Если ИдентификаторСобытияСтанцииПоследнее = "ПервоеСобытие" 
		  И (СтрокаЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.OFFERING 
		 Или СтрокаЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.CONNECTED) Тогда
			SQLНайтиКонтакт(СтрокаЗвонок); 
		ИначеЕсли ИдентификаторСобытияСтанцииПоследнее = ВидыСобытийСтанции.CONNECTED 
		       И (СтрокаЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.ONHOLD 
		 	  Или СтрокаЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.ONHOLDPENDTRANSFER
		 	  Или СтрокаЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.ONHOLDPENDCONF) Тогда
			SQLНайтиКонтакт(СтрокаЗвонок); 
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Результат = CRMНайтиТелефонВКонтактнойИнформации(СтрокаЗвонок.ВходящееИсходящееСобытие, НомерЗвонкаВходящего, глКоличествоХранимыхЦифрТелефона);
	
	Если Результат.Пустой() Тогда
		Если СтрДлина(СокрЛП(НомерЗвонкаВходящего)) > глМаксимальнаяДлинаВнутреннегоНомера И СтрДлина(СокрЛП(НомерЗвонкаВходящего)) <=7 Тогда
			Результат = CRMНайтиТелефонВКонтактнойИнформации(СтрокаЗвонок.ВходящееИсходящееСобытие, глКодГорода + НомерЗвонкаВходящего, глКоличествоХранимыхЦифрТелефона);
		КонецЕсли;
	КонецЕсли;
	
	
	// Если ничего не найдено то сразу выходим
	Если Результат.Пустой() Тогда Возврат; КонецЕсли;
	
	// Добавляем в выгрузку колонку с типом объекта, для ускорения поиска
	ТаблицаЗначений = Результат.Выгрузить();
	
	ТаблицаЗначений.Колонки.Добавить("ТипОбъекта");
	Для каждого ТекущаяСтрока Из ТаблицаЗначений Цикл
		ТекущаяСтрока.ТипОбъекта = ТипЗнч(ТекущаяСтрока.Объект);
	КонецЦикла; 
	
	// Если в таблицу был выгружен только один контакт, то вернем его сразу
	//  или если были найдена связка контрагент и контактныелица, принадлежащие это контрагенту, 
	//  с этим номером.
	СтруктураКонтакта = Неопределено;
	Если CRMОдинКонтактИлиСвязка(ТаблицаЗначений, СтруктураКонтакта) Тогда
		СтруктураКонтакта.Вставить("ИдентификаторЗвонка", СтрокаЗвонок.ИдентификаторЗвонка);
		СтруктураКонтакта.Вставить("ИмяСобытия", "ВыбранКонтакт");
		ФормаТелефон.ОбработкаВыбора(СтруктураКонтакта, ФормаТелефон);
		Возврат;
	КонецЕсли;
	
	// Если номер телефона был определен у нескольких контактов
	//  то выполним поиск в таблице SQL, если поиск результатом не увенчался успехом
	//  то покажем пользователю форму для выбора контакта
	
	Если SQLНайтиКонтакт(СтрокаЗвонок) Тогда Возврат; КонецЕсли;
	
	// И если не нашли ничего в таблице SQL то откроем для пользователя 
	//  окошко для выборе контакта
	
	ФормаВыбораКонтакта = ПолучитьОбщуюФорму("CRM_ВыборКонтакта", ФормаТелефон, СтрокаЗвонок.ИдентификаторЗвонка);
	ФормаВыбораКонтакта.мИдентификаторЗвонка = СтрокаЗвонок.ИдентификаторЗвонка;
	ФормаВыбораКонтакта.КлючУникальности = СтрокаЗвонок.ИдентификаторЗвонка;
	ФормаВыбораКонтакта.Заголовок = "Выбрать контакт (Номер: "+СтрокаЗвонок.НомерТелефона+")";
	ДеревоКонтактов = ФормаВыбораКонтакта.мКонтакты;
	ДеревоКонтактов.Строки.Очистить();
	
	// Cформируем связку контрагента и контактного лица, следующим образом. 
//Сначала идет контрагент, затем все относящиеся к нему
	//  контактные лица. Если найдутся контактные лица не относящиеся к контрагенту, 
//они будут добавлены в ветку контактных лиц.
	// >>>>> НАЧАЛО
	МассивКонтрагентов  = ТаблицаЗначений.НайтиСтроки(Новый Структура("ТипОбъекта", Тип("СправочникСсылка.Контрагенты")));
	МассивКонтактныхЛиц = ТаблицаЗначений.НайтиСтроки(Новый Структура("ТипОбъекта", Тип("СправочникСсылка.Контрагенты")));
	НетКонтрагентов = Истина; НетКонтактныхЛиц = Истина;
	
	// Создаем контактных лиц, вместе с контрагентами которым они принадлежат
	СтрокаРодительКонтрагентов = ДеревоКонтактов.Строки.Добавить();
	ФормаВыбораКонтакта.ЗаполнитьСтроку(СтрокаРодительКонтрагентов, Тип("СправочникСсылка.Контрагенты"), "Контрагенты", "СправочникОбъект");
	
	СтрокаРодительКонтактныхЛиц = ДеревоКонтактов.Строки.Добавить();
	ФормаВыбораКонтакта.ЗаполнитьСтроку(СтрокаРодительКонтактныхЛиц, Тип("СправочникСсылка.Контрагенты"), "Контрагенты", "СправочникОбъект");
	
	// Цикл по контактным лицам
	Для каждого СтрокаТекущееКонтактноеЛицо Из МассивКонтактныхЛиц Цикл
		// Если вид контактного лица не равен контрагенту, то добавляем его в группу Контактные лица
	//	Если СтрокаТекущееКонтактноеЛицо.Объект.ВидКонтактногоЛица = 
//Перечисления.ВидыКонтактныхЛиц.КонтактноеЛицоКонтрагента Тогда
			ВладелецКонтрагент = РаботаСКонтактнымиЛицами.ПолучитьВедущегоКонтрагента(СтрокаТекущееКонтактноеЛицо.Объект);
			// Пытаемся найти его в группе Контрагентов, возможно его уже добавляли
			ТекущийРодительКонтрагент = СтрокаРодительКонтрагентов.Строки.Найти(ВладелецКонтрагент, "Объект", Истина);
			// Если не нашли, то создаем его
			Если ТекущийРодительКонтрагент = Неопределено Тогда
				ТекущийРодительКонтрагент = СтрокаРодительКонтрагентов.Строки.Добавить();
				ФормаВыбораКонтакта.ЗаполнитьСтроку(ТекущийРодительКонтрагент, ВладелецКонтрагент, ВладелецКонтрагент, , СтрокаТекущееКонтактноеЛицо.Поле1, СтрокаТекущееКонтактноеЛицо.Поле2, СтрокаТекущееКонтактноеЛицо.Поле3, СтрокаТекущееКонтактноеЛицо.Поле4, СтрокаТекущееКонтактноеЛицо.Представление);
			КонецЕсли;
			
			НоваяСтрока = ТекущийРодительКонтрагент.Строки.Добавить();
			ФормаВыбораКонтакта.ЗаполнитьСтроку(НоваяСтрока, СтрокаТекущееКонтактноеЛицо.Объект, СтрокаТекущееКонтактноеЛицо.Объект, , СтрокаТекущееКонтактноеЛицо.Поле1, СтрокаТекущееКонтактноеЛицо.Поле2, СтрокаТекущееКонтактноеЛицо.Поле3, СтрокаТекущееКонтактноеЛицо.Поле4, СтрокаТекущееКонтактноеЛицо.Представление);
			НетКонтрагентов = Ложь;
		//Иначе
		//	НоваяСтрока = СтрокаРодительКонтактныхЛиц.Строки.Добавить();
		//	ФормаВыбораКонтакта.ЗаполнитьСтроку(НоваяСтрока, СтрокаТекущееКонтактноеЛицо.
//Объект, СтрокаТекущееКонтактноеЛицо.Объект, , СтрокаТекущееКонтактноеЛицо.Поле1, 
//СтрокаТекущееКонтактноеЛицо.Поле2, СтрокаТекущееКонтактноеЛицо.Поле3, 
//СтрокаТекущееКонтактноеЛицо.Поле4, СтрокаТекущееКонтактноеЛицо.Представление);
		//	НетКонтактныхЛиц = Ложь;
		//КонецЕсли;
	КонецЦикла;
	// Если контактных лиц не принадлежащих контрагентам не было ... то удалим и группу
	Если НетКонтактныхЛиц Тогда
		ДеревоКонтактов.Строки.Удалить(СтрокаРодительКонтактныхЛиц);
	КонецЕсли;
	// Если контрагентов не было ... то удалим и группу
	Если НетКонтрагентов Тогда
		ДеревоКонтактов.Строки.Удалить(СтрокаРодительКонтрагентов);
	КонецЕсли;
	
	// Проходим по контрагентам
	Если НетКонтрагентов Тогда
		СтрокаРодительКонтрагентов = ДеревоКонтактов.Строки.Добавить();
		ФормаВыбораКонтакта.ЗаполнитьСтроку(СтрокаРодительКонтрагентов, Тип("СправочникСсылка.Контрагенты"), Метаданные.Справочники.Контрагенты.Представление(), "СправочникОбъект");
	КонецЕсли;
	// Цикл по контрагентам
	Для каждого СтрокаТекущийКонтрагент Из МассивКонтрагентов Цикл
		// Проверим такой контрагент, мог быть добавлен при прохождении по контактным лицам
		Если СтрокаРодительКонтрагентов.Строки.Найти(СтрокаТекущийКонтрагент.Объект, "Объект", Истина) = Неопределено Тогда
			НоваяСтрока = СтрокаРодительКонтрагентов.Строки.Добавить();
			ФормаВыбораКонтакта.ЗаполнитьСтроку(НоваяСтрока, СтрокаТекущийКонтрагент.Объект, СтрокаТекущийКонтрагент.Объект, , СтрокаТекущийКонтрагент.Поле1, СтрокаТекущийКонтрагент.Поле2, СтрокаТекущийКонтрагент.Поле3, СтрокаТекущийКонтрагент.Поле4, СтрокаТекущийКонтрагент.Представление);
			НетКонтрагентов = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Если контрагентов не было ... то удалим и группу
	Если НетКонтрагентов Тогда
		ДеревоКонтактов.Строки.Удалить(СтрокаРодительКонтрагентов);
	КонецЕсли;
	// <<<<< КОНЕЦ
	
	// Определим типы значений, которые может принимать объект
	МассивТиповОбъектовКонтактнойИнформации = Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения.Объект.Тип.Типы();
	Для Каждого ТекущийТип Из МассивТиповОбъектовКонтактнойИнформации Цикл
		
		// исключим из просмотра контрагентов и контактных лиц, они были сформированы выше
		Если ТекущийТип = Тип("СправочникСсылка.Контрагенты") Тогда Продолжить; КонецЕсли;
		
		ЕстьЗначения = Ложь;
		СтрокаРодитель = ДеревоКонтактов.Строки.Добавить();
		ФормаВыбораКонтакта.ЗаполнитьСтроку(СтрокаРодитель, ТекущийТип, Метаданные.НайтиПоТипу(ТекущийТип).Представление(), "СправочникОбъект");

		НайденныеСтроки = ТаблицаЗначений.НайтиСтроки(Новый Структура("ТипОбъекта", ТекущийТип));
		Для каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = СтрокаРодитель.Строки.Добавить();
			ФормаВыбораКонтакта.ЗаполнитьСтроку(НоваяСтрока, ТекущаяСтрока.Объект, ?(ТекущаяСтрока.ЗначениеПоУмолчанию, "•"," ")+ТекущаяСтрока.Объект, , ТекущаяСтрока.Поле1, ТекущаяСтрока.Поле2, ТекущаяСтрока.Поле3, ТекущаяСтрока.Поле4, ТекущаяСтрока.Представление);
			ЕстьЗначения = Истина;
		КонецЦикла;
		
		// Если значений не было ... то удалим и группу
		Если НЕ ЕстьЗначения Тогда
			ДеревоКонтактов.Строки.Удалить(СтрокаРодитель);
		КонецЕсли;
		
	КонецЦикла;

	ФормаВыбораКонтакта.ЭлементыФормы.Контакты.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	Если НЕ ФормаВыбораКонтакта.Открыта() Тогда ФормаВыбораКонтакта.Открыть(); КонецЕсли;
КонецПроцедуры

// Процедура вызывается из карточек тех объектов, к которым можно вводить
//  контактную информацию, изменяет текущую запись регистра и вызывает 
//  обработку заполнения телефона, Идентификатор и Номер телефона передается из документа
//  в виде строки параметров СтрокаПараметров, а так же внутренний или внешний это номер
Процедура СоздатьНомерКИ(ТекущаяСтрока, ТекущийОбъект, ФормаВладелец, СтрокаПараметров) Экспорт 
	// Проверим может ли пользователь работать с СофтФоном
	Если НЕ CRMРаботаССофтФономРазрешена(Истина, глИспользоватьСофтФонПрофТекущийПользователь, сфпСофтФонПроСервер.сфпТекущийПользователь()) Тогда
		Возврат;
	ИначеЕсли ТекущаяСтрока = Неопределено Тогда
		Возврат;
	ИначеЕсли НЕ (ТекущаяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон) Тогда
		Возврат;
	ИначеЕсли СтрокаПараметров = Неопределено Тогда
		Возврат;
	ИначеЕсли ТекущийОбъект.ЭтоНовый() Тогда
		Если Вопрос("ВНИМАНИЕ!!! Объект [" + ТекущийОбъект + "] не записан в базу данных, записать?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, "ВНИМАНИЕ!!! Не записан объект.") = КодВозвратаДиалога.Да Тогда
			Попытка
				ТекущийОбъект.Записать();
			Исключение
				Возврат;
			КонецПопытки;
		Иначе	
			Возврат;
		КонецЕсли;
	КонецЕсли;
	СтруктураПараметров = ПреобразоватьСтрокуВПараметры(СтрокаПараметров);
	Если СтруктураПараметров.Количество() = 0 Тогда Возврат; КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураПараметров.Параметр3) ИЛИ ЗначениеЗаполнено(СтруктураПараметров.Параметр4) Тогда
		СтруктураТелефона = CRMСформироватьСтруктуруНомераИзПолей();
		СтруктураТелефона.КодСтраны 	= СтруктураПараметров.Параметр3;
		СтруктураТелефона.КодГорода 	= СтруктураПараметров.Параметр4;
		СтруктураТелефона.НомерТелефона = СтруктураПараметров.Параметр5;
		СтруктураТелефона.Внутренний	= СтруктураПараметров.Параметр6;
		CRMСформироватьПредставлениеТелефонаПоСтруктуре(СтруктураТелефона);
	Иначе
		СтруктураТелефона = ПреобразоватьНомерСУчетомПрефиксов(?(НЕ ЗначениеЗаполнено(СтруктураПараметров.Параметр5), СтруктураПараметров.Параметр6, СтруктураПараметров.Параметр5), СтруктураПараметров.Параметр2, ТипВходящийИсходящийЗвонок.Входящий);
	КонецЕсли;
	ТекущаяСтрока.Тип 		  	= ТекущаяСтрока.Тип;
	ТекущаяСтрока.Объект 	  	= ТекущийОбъект.Ссылка;
	ТекущаяСтрока.Поле1       	= СтруктураТелефона.КодСтраны;
	ТекущаяСтрока.Поле2         = СтруктураТелефона.КодГорода;
	ТекущаяСтрока.Поле3         = СтруктураТелефона.НомерТелефона;
	ТекущаяСтрока.Поле4         = СтруктураТелефона.Внутренний;
	ТекущаяСтрока.Представление	= СтруктураТелефона.Представление;
	ТекущаяСтрока.Вид 			= Справочники.ВидыКонтактнойИнформации.ТелефонРабочий;
	Если ЗначениеЗаполнено(СтруктураТелефона.Внутренний) И НЕ ЗначениеЗаполнено(СтруктураТелефона.НомерТелефона) Тогда
		ТекущаяСтрока.CRM_ПолеХраненияНомера = CRMПреобразоватьНомерДляСохранения(СтруктураТелефона.Внутренний, глКоличествоХранимыхЦифрТелефона);
	Иначе
		ТекущаяСтрока.CRM_ПолеХраненияНомера = CRMПреобразоватьНомерДляСохранения(СтруктураТелефона.НомерТелефона, глКоличествоХранимыхЦифрТелефона);
	КонецЕсли;
	ОбработкаТелефона = Обработки.РедактированиеКонтактнойИнформации.Создать();
	ОбработкаТелефона.мИдентификаторЗвонка = Число(СтруктураПараметров.Параметр1);
	ОбработкаТелефона.РедактироватьЗапись(ТекущаяСтрока, Истина, ФормаВладелец);
КонецПроцедуры

// Процедура вызывается из карточек тех объектов, у которых необходимо набрать номер
Процедура НабратьНомерКИ(ТекущаяСтрока) Экспорт
	// Проверим может ли пользователь работать с СофтФоном
	Если НЕ CRMРаботаССофтФономРазрешена(Истина, глИспользоватьСофтФонПрофТекущийПользователь, сфпСофтФонПроСервер.сфпТекущийПользователь()) Тогда Возврат; КонецЕсли;
	
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		Если ТекущаяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			СтруктураТелефона = CRMСформироватьСтруктуруНомераИзПолей(ТекущаяСтрока);
			Если ЗначениеЗаполнено(СтруктураТелефона.Представление) Тогда
				НабранныйКонтакт = CRMПреобразоватьКонтактВСтруктуру(ТекущаяСтрока.Объект, ТекущаяСтрока.Поле1, ТекущаяСтрока.Поле2, ТекущаяСтрока.Поле3, ТекущаяСтрока.Поле4);
				НабратьНомер(ПреобразоватьНомерСУчетомПрефиксов(СтруктураТелефона, "", ТипВходящийИсходящийЗвонок.Исходящий));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура выдает пользователю на экран список телефонных номеров
//   объектов, переданных в СписокОбъектов. И по выбранному выполняем дозвон
Процедура ПозвонитьВыбравТелефон(СписокОбъектов, СостояниеСобытия = Неопределено) Экспорт
	
	СписокТелефонов = Новый СписокЗначений;
	
	Для каждого ТекущийОбъект Из СписокОбъектов Цикл
		CRMПолучитьСписокТелефоновСофтФон(ТекущийОбъект.Значение, СписокТелефонов);
	КонецЦикла;
	
	Если СписокТелефонов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементСписка = СписокТелефонов.ВыбратьЭлемент("Выберите телефон");
	Если ЭлементСписка = Неопределено Тогда Возврат; КонецЕсли;
	
	ТелефонныйНомер = ЭлементСписка.Значение;
	Если ТелефонныйНомер.Количество() = 0 Тогда Возврат; КонецЕсли;
	
	Если НЕ СостояниеСобытия = Неопределено И НЕ СостояниеСобытия = Перечисления.СостоянияСобытий.Завершено Тогда СоздаватьНовоеСобытие = Ложь КонецЕсли;
	НабранныйКонтакт = CRMПреобразоватьКонтактВСтруктуру(ТелефонныйНомер.Объект, ТелефонныйНомер.КодСтраны, ТелефонныйНомер.КодГорода, ТелефонныйНомер.НомерТелефона, ТелефонныйНомер.Внутренний);
	НабратьНомер(ПреобразоватьНомерСУчетомПрефиксов(ТелефонныйНомер));
	
КонецПроцедуры

// Функция возвращает уникальный указатель на линию
//
// Параметры:
//	ИмяЛинии	- Строка - Имя линии
//
// Возвращаемое значение:
//	Число	- Уникательный указатель на линию
//
Функция сфпПолучитьУникальныйУказательНаЛинию(LineName)
	Если НЕ sqlЛогВедется Тогда
		SQLПолучитьПараметры();
	КонецЕсли;
	Если НЕ sqlЛогВедется Тогда Возврат 0; КонецЕсли;
	ЧисловойУникальныйИдентификатор = 0;
	ТекстЗапроса = "SELECT ID FROM DEVICES WHERE LINENAME = '" + LineName + "'";
	Попытка
		Connection = Новый COMObject("ADODB.Connection");
		Если sqlТипАвторизации = 0 Тогда 
			СтрокаПодключения = "Provider=SQLOLEDB.1;Persist Security Info=True;User ID=" + sqlИмяПользователя + ";Password=" + sqlПарольПользователя + ";Initial Catalog=" + sqlИмяБазыДанных + ";Data Source=" + sqlИмяСервера;	
		Иначе
			СтрокаПодключения = "Provider=SQLOLEDB.1;Integrated Security=SSPI;Persist Security Info=False;Initial Catalog=" + sqlИмяБазыДанных + ";Data Source=" + sqlИмяСервера;
		КонецЕсли;	
		Connection.Open(СокрЛП(СтрокаПодключения));
		RecordSet = Новый COMОбъект("ADODB.RecordSet");
		RecordSet.Open(ТекстЗапроса, Connection, 2);
		Пока НЕ RecordSet.EOF Цикл
			ЧисловойУникальныйИдентификатор = RecordSet.Fields(0).Value;
			Прервать;
		КонецЦикла;
	Исключение
	КонецПопытки;
	Возврат ЧисловойУникальныйИдентификатор;
КонецФункции // сфпПолучитьУникальныйУказательНаЛинию()	
// {{{ СофтФон, Проф }}} КОНЕЦ 

/////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//Процедура обрабатывает входящее событие от баннера
//Зарезервированные события: СнятьТрубку, ПоложитьТрубку, ПерехватитьЗвонок
//все остальные события от баннера игнорируются
Процедура ОбработкаСобытияБаннера(Событие,ИДБаннера) Экспорт 
	НайденныйБаннер = ДанныеБаннера.Найти(Число(ИДБаннера),"ИДБаннера");
	Если НЕ НайденныйБаннер = Неопределено Тогда
		Если Событие = "СнятьТрубку" Тогда
			//нужно найти строку звонков по которому пришло событие
			Строка = Звонки.Найти(НайденныйБаннер.ДанныеЗвонка,"ИдентификаторЗвонка");//"УникальныйИдентификаторЗвонка");
			Если НЕ Строка = Неопределено Тогда
				Если НЕ ОтветитьНаВызов(Строка.ИдентификаторЗвонка, Строка.ИдентификаторСобытияСтанции) Тогда
				ИначеЕсли сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпОткрыватьДокументПриСнятииТрубки") Тогда
					ОткрытьКонтакт(ФормаТелефон.ЭлементыФормы.Звонки.ТекущиеДанные, "НомерТелефонаЗаголовок");
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Событие = "ПоложитьТрубку" Тогда
			Строка = Звонки.Найти(НайденныйБаннер.ДанныеЗвонка,"ИдентификаторЗвонка");//"УникальныйИдентификаторЗвонка");
			Если НЕ Строка = Неопределено Тогда
				Если УничтожитьЗвонок(Строка.ИдентификаторЗвонка, Строка.ИдентификаторСобытияСтанции) Тогда
					// Возвращаем цветовые значения кнопки удержания
					CRMТриггерКнопкиHold(ФормаТелефон.ЭлементыФормы.HoldUnHold, "UnHold");
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Событие = "ПерехватитьЗвонок" Тогда
			СвояЛиния = Линии.Найти(Истина, "ОсновнаяЛиния").НомерПривязанныйКЛинии;
			КодОшибки = РарусСофтФонКомпонента.CallRedirect(НайденныйБаннер.ДанныеЗвонка,СвояЛиния,0);
			Если КодОшибки<0 тогда
				Сообщить("Произошла ошибка: "+РарусСофтФонКомпонента.TapiCheck(Число(КодОшибки)));
			КонецЕсли;
		ИначеЕсли Событие = "DESTROY" Тогда //нужно запомнить звонок а баннер удалить
		КонецЕсли;		
		СписокОбработанныхЗвонковБаннером.Добавить(НайденныйБаннер.ДанныеЗвонка);			
		//Удаляем баннер
		РарусСофтФонКомпонента.ЗакрытьБаннер(НайденныйБаннер.ИДБаннера);
		ДанныеБаннера.Удалить(НайденныйБаннер);
	КонецЕсли;
КонецПроцедуры

// Функция реализует логику интерпретации данных полученных из пришедшего звонка,
//  в зависимости от типа используемого оборудования
Процедура ЗаполнитьДанныеЗвонкаПоТипуОборудования(ТекущаяСтрока, Звонок)
	
	// Только при входящем звонке
	Если НЕ ОпределитьВходящееИсходящееСобытие(Число(Звонок.Origin)) = ТипВходящийИсходящийЗвонок.Входящий Тогда Возврат; КонецЕсли;
	
	Если Число(Звонок.Provider) = ТипыПоддерживаемогоОборудования.Avaya Тогда
		// Если состояние CONNECTED, то всегда показываем ConnectedId звонка, даже если он пустой,
		//  т.к. он всегда будет показывать с каким номеров телефона соединен данный аппарат.
		// Кроме внешнего звонка
		Если ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), "") = ТипВнутреннийВнешнийЗвонок.Внутренний Тогда
			// Внутренний звонок
			Если ТекущаяСтрока.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.CONNECTED Тогда
				ТекущаяСтрока.НомерА = Строка(Звонок.ConnectedId);
			КонецЕсли;
		Иначе
			// Внешний звонок
			Если ТекущаяСтрока.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.CONNECTED Тогда
				Если ЗначениеЗаполнено(Звонок.ConnectedId) Тогда
					ТекущаяСтрока.НомерА = Строка(Звонок.ConnectedId);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли  Число(Звонок.Provider) = ТипыПоддерживаемогоОборудования.PanasonicTD Тогда
		// Если состояние CONNECTED и ONHOLD, то всегда показываем ConnectedId звонка, даже если он пустой,
		//  т.к. он всегда будет показывать с каким номеров телефона соединен данный аппарат
		Если ТекущаяСтрока.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.CONNECTED 
		 Или ТекущаяСтрока.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.ONHOLD Тогда
			ТекущаяСтрока.НомерА = Строка(Звонок.ConnectedId);
		КонецЕсли;
		
	Иначе
		// по умолчанию, станция не определена
		// Если состояние CONNECTED и ONHOLD, то всегда показываем ConnectedId звонка, даже если он пустой,
		//  т.к. он всегда будет показывать с каким номеров телефона соединен данный аппарат
		Если ТекущаяСтрока.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.CONNECTED 
		 Или ТекущаяСтрока.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.ONHOLD Тогда
			ТекущаяСтрока.НомерА = ?(НЕ ЗначениеЗаполнено(Звонок.ConnectedId), Строка(Звонок.CallerId), Строка(Звонок.ConnectedId));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОТСЛЕЖИВАЮЩИЕ СОСТОЯНИЕ ЗВОНКА И СЕРВЕРА

// Процедура разбирает события TAPI и отображает текущие звонки в таблице
// вызывается при возникновении внешнего события с параметрами
// Источник = "rtup", Событие = "Change" из модуля приложения.
// Это Основная функция осуществляющая реакцию на телефонные события !
Процедура ИзменениеСостоянияЗвонка(Данные) Экспорт
	Звонок = РарусСофтФонКомпонента.GetCallInfo(Число(Данные));
	Если Звонок = Неопределено Тогда 
		// Не забудем удалить звонок из таблицы, если его больше нет, т.е. состояние IDLE
		// Закроем формы выбора контактов, принадлежащих данному идентификатору звонка
		Попытка
			ФормаВыбораКонтакта = ПолучитьОбщуюФорму("CRM_ВыборКонтакта", ФормаТелефон, Число(Данные));
			Если ФормаВыбораКонтакта.Открыта() Тогда ФормаВыбораКонтакта.Закрыть(); КонецЕсли;
		Исключение
		КонецПопытки;
		
		УдаленныйЗвонок = Звонки.Найти(Число(Данные), "ИдентификаторЗвонка");
		НабранныйКонтакт = Неопределено;
		Если НЕ УдаленныйЗвонок = Неопределено Тогда 
			СтруктураОповещения = Новый Структура;
			СтруктураОповещения.Вставить("ИдентификаторЗвонка", УдаленныйЗвонок.GUIDЗвонка);
			Оповестить("СофтФон_КонецРазговора", СтруктураОповещения);
			//не показываем индикацию пропущенного звонка если номер телефона неопределен
			Если ЗначениеЗаполнено(УдаленныйЗвонок.НомерТелефона) Тогда 
				// Подключим обработчик индикации пропущенных вызовов, если был зарегистрирован пропущенный звонок
				//  исключим из пропущенных входящие звонки
				
				// для панасоника есть метод который показывает был ли пропущен звонок
				Если ПровайдерИспользуемойЛинии = 1 или ПровайдерИспользуемойЛинии = 2 Тогда //это провайдеры панасоника
					Направление = ВидыВходящихИсходящихСобытий[СтрЗаменить(УдаленныйЗвонок.ПредставлениеВходящееИсходящееСобытие," ","")];
					СтатусЗавершенияВызова = РарусСофтФонКомпонента.IsAnswered(УдаленныйЗвонок.ИдентификаторСвязанныхзвонков, ПровайдерИспользуемойЛинии, Направление);
					Если СтатусЗавершенияВызова = 0 Тогда
						ФормаТелефон.ПризнакПропущенногоЗвонка = Ложь;
					ИначеЕсли СтатусЗавершенияВызова = 1 Тогда
						ФормаТелефон.ПризнакПропущенногоЗвонка = Истина;
					Иначе
						ПропущенЗвонок = (УдаленныйЗвонок.ПризнакПропущенногоЗвонка И ОпределитьВходящееИсходящееСобытие(УдаленныйЗвонок.ВходящееИсходящееСобытие) = ТипВходящийИсходящийЗвонок.Входящий);
						ФормаТелефон.ПризнакПропущенногоЗвонка = ФормаТелефон.ПризнакПропущенногоЗвонка Или ПропущенЗвонок;
					КонецЕсли;
				Иначе
					ПропущенЗвонок = (УдаленныйЗвонок.ПризнакПропущенногоЗвонка И ОпределитьВходящееИсходящееСобытие(УдаленныйЗвонок.ВходящееИсходящееСобытие) = ТипВходящийИсходящийЗвонок.Входящий);
					ФормаТелефон.ПризнакПропущенногоЗвонка = ФормаТелефон.ПризнакПропущенногоЗвонка Или ПропущенЗвонок;		
				КонецЕсли;		
				
				ФормаТелефон.УправлениеИндикациейПропущенныхЗвонков();
			КонецЕсли;
			
			// Если звонок бы модифицирован и изменилось состояние, то его необходимо записать в SQL 
			Если УдаленныйЗвонок.ПризнакМодифицированности И CRMСостояниеЗвонкаИзменилось(ВидыСобытийСтанции, "", ИдентификаторСобытияСтанцииПоследнее) Тогда
				УдаленныйЗвонок.ПризнакМодифицированности = Ложь;
				// Регистрируем измененное состояние в SQL таблице
				SQLЗарегистрироватьЗвонок(УдаленныйЗвонок);
			КонецЕсли;
			
			Звонки.Удалить(УдаленныйЗвонок); 
			CRMТриггерКнопкиHold(ФормаТелефон.ЭлементыФормы.HoldUnHold, "UnHold");
		КонецЕсли;
		Если ФормаТелефон = Неопределено Тогда 
			ФормаТелефон = ПолучитьФорму("Телефон");
		КонецЕсли;
		//++CRM
		СоздаватьНовоеСобытие = Истина;
		рЗакрыватьОкнаПослеПрекращенияЗвонка = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ЗакрыватьОкнаПослеПрекращенияЗвонка");
		Если ФормаТелефон.Открыта() и рЗакрыватьОкнаПослеПрекращенияЗвонка Тогда
			ФормаТелефон.Закрыть();	
		КонецЕсли;
		//--CRM 
		//также почистим информацию в таблице доступных для перехвата звонков
		НайденнаяСтрокаДляУдаления = глСписокДоступныхЛинийДляПерехвата.Найти(Данные,"Handle");
		Если НЕ НайденнаяСтрокаДляУдаления=Неопределено Тогда
			глСписокДоступныхЛинийДляПерехвата.Удалить(НайденнаяСтрокаДляУдаления);
			ФормаТелефон.УправлениеИндикациейПерехвата();
			//Нужно удалить баннер!!!
			НайденныйБаннер = ДанныеБаннера.Найти(Число(Данные),"ДанныеЗвонка");
			Если НЕ НайденныйБаннер=Неопределено Тогда
				СтатусБаннера = РарусСофтФонКомпонента.ПолучитьСостояниеБаннера(НайденныйБаннер.ИДБаннера);
				Если (НЕ СтатусБаннера=0)И(НЕ СтатусБаннера=3) Тогда
					Результат = РарусСофтФонКомпонента.ЗакрытьБаннер(НайденныйБаннер.ИДБаннера);
				КонецЕсли;
				ДанныеБаннера.Удалить(НайденныйБаннер);
			КонецЕсли;
		КонецЕсли;
		//и удалим баннер если был
		НайденныйБаннер = ДанныеБаннера.Найти(Число(Данные),"ДанныеЗвонка");
		Если НЕ НайденныйБаннер=Неопределено Тогда
			Результат = РарусСофтФонКомпонента.ЗакрытьБаннер(НайденныйБаннер.ИДБаннера);
		КонецЕсли;
		//выходим
		Возврат;
	КонецЕсли;
	
	//если звонок по моей линии то делаем что и делали раньше иначе пришло событие 
//что есть линии которые можно перехватывать
	//здесь проверить если моя линия то делаем тоже самое, 
//если нет то нужно смотреть статус внутренний вход звонок и статус оферинг или апцетедщ
	Если Звонок <> Неопределено Тогда
		СвояЛиния = Линии.Найти(ИмяИспользуемойЛинии, "ИмяЛинии");
		Если СвояЛиния = Неопределено ИЛИ (Звонок.Line = СвояЛиния.ИмяЛинии) или (Звонок.VirtualLineName = СвояЛиния.ИмяЛинии) Тогда
			НайденныйЗвонок = Звонки.Найти(Число(Данные), "ИдентификаторЗвонка");
			// Если звонок еще не определен в системе, то вводим его в табличную часть
			Если НайденныйЗвонок = Неопределено Тогда
				НоваяСтрока 							= Звонки.Добавить();
				НоваяСтрока.ИдентификаторЗвонка 		= Число(Звонок.hCall);
				НоваяСтрока.ИдентификаторСвязанныхЗвонков = Число(Звонок.RelatedCallId);
				НоваяСтрока.УникальныйИдентификаторЗвонка = Число(Звонок.hCallUnique);
				НоваяСтрока.НомерА 						= Звонок.CallerId;
				НоваяСтрока.НомерБ 						= Звонок.CalledId;
				НоваяСтрока.ПризнакМодифицированности	= Ложь;
				
				// Т.к. список событий гораздо больше, чем используется, то 
				//  может прийти событие не описанное в нашей структуре.
				Попытка НоваяСтрока.ИдентификаторСобытияСтанции	= ВидыСобытийСтанции[ВРег(Звонок.State)];
				Исключение НоваяСтрока.ИдентификаторСобытияСтанции = "НЕИЗВЕСТНОЕ СОСТОЯНИЕ ("+ВРег(Звонок.State)+")";
				КонецПопытки;
				
				// Запомним последнее состояние станции
				ИдентификаторСобытияСтанцииПоследнее = НоваяСтрока.ИдентификаторСобытияСтанции;
				
				// Функция реализует логику интерпретации данных полученных из пришедшего звонка,
				//  в зависимости от типа используемого оборудования
				ЗаполнитьДанныеЗвонкаПоТипуОборудования(НоваяСтрока, Звонок);
				
				ПризнакИзмененияНомера = Ложь;
				
				Если Число(Звонок.Provider) = 10 Тогда //Infra - у этой станции нужный нам номер всегда в поле CallerId независимо от направления вызова
					НомерТелефона = Звонок.CallerId;
				Иначе	
					НомерТелефона = ВернутьНомерИлиИмяТелефонаТелефона(Звонок.Origin, НоваяСтрока.НомерА, НоваяСтрока.НомерБ);
				КонецЕсли;	
				Если НЕ СокрЛП(НоваяСтрока.НомерТелефона) = НомерТелефона Тогда
					НоваяСтрока.НомерТелефона = НомерТелефона;
					ПризнакИзмененияНомера = Истина;
				КонецЕсли;
				
				// Если номер телефона сменился, то скидываем обязательно контакт
				Если ПризнакИзмененияНомера Тогда
					НоваяСтрока.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
					НоваяСтрока.КонтактноеЛицо = Справочники.Контрагенты.ПустаяСсылка();
					НоваяСтрока.СоединитьССотрудником = Неопределено;
				КонецЕсли;
				
				НоваяСтрока.НомерТелефонаСИменем   		= ?(НЕ ЗначениеЗаполнено(Звонок.Display), ВернутьНомерИлиИмяТелефонаТелефона(Звонок.Origin, Звонок.CallerInfoName, Звонок.CalledInfoName), СокрЛП(Звонок.Display));
				НоваяСтрока.ПризнакПропущенногоЗвонка	= ЗначениеЗаполнено(НоваяСтрока.НомерТелефона);
				НоваяСтрока.GUIDЗвонка					= СокрЛП(Звонок.CallData);
				
				// Определим признак пропущенного звонка, только при условии, что номер телефона определен
				Если ЗначениеЗаполнено(НоваяСтрока.НомерТелефона) Тогда
					НоваяСтрока.ПризнакПропущенногоЗвонка = НоваяСтрока.ПризнакПропущенногоЗвонка И НЕ НоваяСтрока.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.CONNECTED;
				КонецЕсли;
				
				НоваяСтрока.ВходящееИсходящееСобытие = Число(Звонок.Origin);
				Если (НоваяСтрока.ВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытий.ВнутреннийИсходящий) 
					И (ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НоваяСтрока.НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний) Тогда
					НоваяСтрока.ПредставлениеВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытийОписание.ВнешнийИсходящий;
				ИначеЕсли (НоваяСтрока.ВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытий.ВнутреннийВходящий) 
					И (ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НоваяСтрока.НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний) Тогда
					НоваяСтрока.ПредставлениеВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытийОписание.ВнешнийВходящий;
				Иначе
					НоваяСтрока.ПредставлениеВходящееИсходящееСобытие = ПолучитьОписаниеВходящегоИсходящегоСобытия(Число(Звонок.Origin));
				КонецЕсли;	
				
				// Если набранный контакт заполнен, то передаем его для заполнения в форме телефона
				Если НЕ НабранныйКонтакт = Неопределено И Число(НабранныйКонтакт.ИдентификаторЗвонка) = НоваяСтрока.ИдентификаторЗвонка Тогда
					// Передадим известный заранее контакт, когда звонок был произведен из базы данных
					НабранныйКонтакт.Вставить("ИмяСобытия", "ВыбранКонтакт");
					ФормаТелефон.ОбработкаВыбора(НабранныйКонтакт, ФормаТелефон);
				КонецЕсли;
				
				// Выполняем поиск только для входящих звонков
				Если ОпределитьВходящееИсходящееСобытие(НоваяСтрока.ВходящееИсходящееСобытие) = ТипВходящийИсходящийЗвонок.Входящий Тогда
					
					ПризнакЗаполненногоКонтакта = Истина;
					Если НабранныйКонтакт = Неопределено Тогда
						ПризнакЗаполненногоКонтакта = Ложь;
					Иначе
						Если НЕ Число(НабранныйКонтакт.ИдентификаторЗвонка) = НоваяСтрока.ИдентификаторЗвонка Тогда
							ПризнакЗаполненногоКонтакта = Ложь;
						КонецЕсли;
					КонецЕсли;
					
					// Если поля связанные с базой данных пусты, т.е. поиск не выполнялся и номер телефона изменился, то выполним поиск
					Если  ПризнакИзмененияНомера 
						Или (НЕ ЗначениеЗаполнено(НоваяСтрока.Контрагент) 
						И  НЕ ЗначениеЗаполнено(НоваяСтрока.КонтактноеЛицо) 
						И  НЕ ЗначениеЗаполнено(НоваяСтрока.СоединитьССотрудником) 
						И  НЕ ПризнакЗаполненногоКонтакта) Тогда
						
						//++CRM 
						ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов");
						Если ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов = Неопределено Тогда
							ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов = Ложь;
							сфпСофтФонПроСервер.сфпЗаписатьЗначениеНастройки("ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов", ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов);
						КонецЕсли;
						УчтённыйВнутреннийВнешнийЗвонок = (ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внутренний)
						И сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов");
						
						Если сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ОткрыватьОкноВыбораТолькоПриСнятииТрубки")
							ИЛИ УчтённыйВнутреннийВнешнийЗвонок Тогда
							// не трогаем окно
						Иначе									 
							// Выполняем поиск в базе данных, а затем уже в таблице SQL
							НайтиКонтакт(НоваяСтрока, "ПервоеСобытие");
						КонецЕсли;
						//--CRM  
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				// Если звонок уже был зарегистрирован, и была найдена строка с идентификатором звонка,
				//  то обновим данные звонка, состояние тел, и т.д.
				
				НайденныйЗвонок.ИдентификаторЗвонка 	= Число(Звонок.hCall);
				НайденныйЗвонок.УникальныйИдентификаторЗвонка = Число(Звонок.hCallUnique);
				НайденныйЗвонок.ИдентификаторСвязанныхЗвонков = Число(Звонок.RelatedCallId);
				НайденныйЗвонок.НомерА 					= Звонок.CallerId;
				НайденныйЗвонок.НомерБ 					= Звонок.CalledId;
				
				// Запомним последнее состояние станции
				ИдентификаторСобытияСтанцииПоследнее = НайденныйЗвонок.ИдентификаторСобытияСтанции;
				
				// Т.к. список событий гораздо больше, чем используется, то 
				//  может прийти событие не описанное в нашей структуре.
				Попытка НайденныйЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции[ВРег(Звонок.State)];
				Исключение НайденныйЗвонок.ИдентификаторСобытияСтанции = "НЕИЗВЕСТНОЕ СОСТОЯНИЕ ("+ВРег(Звонок.State)+")";
				КонецПопытки;
				
				// Функция реализует логику интерпретации данных полученных из пришедшего звонка,
				//  в зависимости от типа используемого оборудования
				ЗаполнитьДанныеЗвонкаПоТипуОборудования(НайденныйЗвонок, Звонок);
				
				ПризнакИзмененияНомера = Ложь;
				НомерТелефона = ВернутьНомерИлиИмяТелефонаТелефона(Звонок.Origin, НайденныйЗвонок.НомерА, НайденныйЗвонок.НомерБ);
				Если НЕ СокрЛП(НайденныйЗвонок.НомерТелефона) = НомерТелефона Тогда
					НайденныйЗвонок.НомерТелефона = НомерТелефона;
					ПризнакИзмененияНомера = Истина;
				КонецЕсли;
				НайденныйЗвонок.НомерТелефонаСИменем = ?(НЕ ЗначениеЗаполнено(Звонок.Display), ВернутьНомерИлиИмяТелефонаТелефона(Звонок.Origin, Звонок.CallerInfoName, Звонок.CalledInfoName), СокрЛП(Звонок.Display));
				
				// Если номер телефона сменился, то скидываем обязательно контакт
				Если ПризнакИзмененияНомера Тогда
					НайденныйЗвонок.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
					НайденныйЗвонок.КонтактноеЛицо = Справочники.Контрагенты.ПустаяСсылка();
					НайденныйЗвонок.СоединитьССотрудником = Неопределено;
				КонецЕсли;
				
				// Проверяем если изменился GUIDЗвонка, такое может быть при связанных звонках
				//  т.е. раньше их связать по какой-либо причине не удалось, а сейчас удалось, следовательно изменился GUID
				Если НЕ НайденныйЗвонок.GUIDЗвонка = СокрЛП(Звонок.CallData) Тогда
					СтарыйGUID = НайденныйЗвонок.GUIDЗвонка;
					НайденныйЗвонок.GUIDЗвонка = СокрЛП(Звонок.CallData);
					SQLИзменитьGUIDТекущегоЗвонка(НайденныйЗвонок, СтарыйGUID);
				КонецЕсли;
				
				// Если звонок бы модифицирован и изменилось состояние, то его необходимо записать в SQL 
				Если НайденныйЗвонок.ПризнакМодифицированности И CRMСостояниеЗвонкаИзменилось(ВидыСобытийСтанции, НайденныйЗвонок.ИдентификаторСобытияСтанции, ИдентификаторСобытияСтанцииПоследнее) Тогда
					НайденныйЗвонок.ПризнакМодифицированности = Ложь;
					// Регистрируем измененное состояние в SQL таблице
					SQLЗарегистрироватьЗвонок(НайденныйЗвонок);
				КонецЕсли;
				
				// Определим признак пропущенного звонка, только при условии, что номер телефона определен
				Если ЗначениеЗаполнено(НайденныйЗвонок.НомерТелефона) Тогда
					НайденныйЗвонок.ПризнакПропущенногоЗвонка = НайденныйЗвонок.ПризнакПропущенногоЗвонка И НЕ НайденныйЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.CONNECTED;
				КонецЕсли;
				
				НайденныйЗвонок.ВходящееИсходящееСобытие              = Число(Звонок.Origin);
				Если (НайденныйЗвонок.ВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытий.ВнутреннийИсходящий) 
					И (ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НайденныйЗвонок.НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний) Тогда
					НайденныйЗвонок.ПредставлениеВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытийОписание.ВнешнийИсходящий;
				ИначеЕсли (НайденныйЗвонок.ВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытий.ВнутреннийВходящий) 
					И (ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НайденныйЗвонок.НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний) Тогда
					НайденныйЗвонок.ПредставлениеВходящееИсходящееСобытие = ВидыВходящихИсходящихСобытийОписание.ВнешнийВходящий;
				Иначе
					НайденныйЗвонок.ПредставлениеВходящееИсходящееСобытие = ПолучитьОписаниеВходящегоИсходящегоСобытия(Число(Звонок.Origin));
				КонецЕсли;	
				
				// Если набранный контакт заполнен, то передаем его для заполнения в форме телефона
				Если НЕ НабранныйКонтакт = Неопределено И (Число(НабранныйКонтакт.ИдентификаторЗвонка) = НайденныйЗвонок.ИдентификаторЗвонка ИЛИ НабранныйКонтакт.НомерТелефона = НайденныйЗвонок.НомерА ИЛИ НабранныйКонтакт.НомерТелефона = НайденныйЗвонок.НомерБ) Тогда
					// Передадим известный заранее контакт, когда звонок был произведен из базы данных
					НабранныйКонтакт.Вставить("ИмяСобытия", "ВыбранКонтакт");
					ФормаТелефон.ОбработкаВыбора(НабранныйКонтакт, ФормаТелефон);
				КонецЕсли;
				
				// Выполняем поиск только для входящих звонков
				Если ОпределитьВходящееИсходящееСобытие(НайденныйЗвонок.ВходящееИсходящееСобытие) = ТипВходящийИсходящийЗвонок.Входящий Тогда
					
					ПризнакЗаполненногоКонтакта = Истина;
					Если НабранныйКонтакт = Неопределено Тогда
						ПризнакЗаполненногоКонтакта = Ложь;
					Иначе
						Если НЕ Число(НабранныйКонтакт.ИдентификаторЗвонка) = НайденныйЗвонок.ИдентификаторЗвонка Тогда
							ПризнакЗаполненногоКонтакта = Ложь;
						КонецЕсли;
					КонецЕсли;
					
					// Если поля связанные с базой данных пусты, т.е. поиск не выполнялся и номер телефона изменился, то выполним поиск
					Если  ПризнакИзмененияНомера 
						Или (НЕ ЗначениеЗаполнено(НайденныйЗвонок.Контрагент) 
						И  НЕ ЗначениеЗаполнено(НайденныйЗвонок.КонтактноеЛицо) 
						И  НЕ ЗначениеЗаполнено(НайденныйЗвонок.СоединитьССотрудником) 
						И НЕ ПризнакЗаполненногоКонтакта) Тогда
						
						//++CRM 
						ОткрыватьОкноВыбораТолькоПриСнятииТрубки = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ОткрыватьОкноВыбораТолькоПриСнятииТрубки");
						ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов");
						ТекущийВнешний = ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний;
						
						ОткрытьФорму = Истина;
						Если ОткрыватьОкноВыбораТолькоДляВнешнихТелефонов и НЕ ТекущийВнешний Тогда
							ОткрытьФорму = Ложь;
						КонецЕсли; 
						Если ОткрыватьОкноВыбораТолькоПриСнятииТрубки Тогда
							Если Найти("CONNECTED",Звонок.State) = 0 Тогда
								ОткрытьФорму = Ложь;
							КонецЕсли;
						КонецЕсли;
						//--CRM 
						Если ОткрытьФорму Тогда
							// Выполняем поиск в базе данных, а затем уже в таблице SQL
							НайтиКонтакт(НайденныйЗвонок, ИдентификаторСобытияСтанцииПоследнее);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				// Создадим документ, если нужно
				Если (НайденныйЗвонок.ИдентификаторСобытияСтанции = "Соединен") И СоздаватьНовоеСобытие Тогда
					Если НайденныйЗвонок.ВходящееИсходящееСобытие = 1 Тогда
						// Внешний исходящий или внутренний исходящий
						ОткрыватьДокументПриСнятииТрубкиПриИсходящем = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпОткрыватьДокументПриСнятииТрубкиПриИсходящем");
						ТекущийВнешний = (ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний);
						Если ОткрыватьДокументПриСнятииТрубкиПриИсходящем И ТекущийВнешний Тогда
							ОткрытьКонтакт(НайденныйЗвонок,"НомерТелефонаЗаголовок");
						КонецЕсли;
					ИначеЕсли НайденныйЗвонок.ВходящееИсходящееСобытие = 2 Тогда
						// Внешний входящий или внутренний входящий
						ОткрыватьДокументПриСнятииТрубки = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпОткрыватьДокументПриСнятииТрубки");
						ТекущийВнешний = (ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний);
						Если ОткрыватьДокументПриСнятииТрубки И ТекущийВнешний Тогда
							ОткрытьКонтакт(НайденныйЗвонок,"НомерТелефонаЗаголовок");
						КонецЕсли;
					ИначеЕсли НайденныйЗвонок.ВходящееИсходящееСобытие = 3 Тогда
						// Внешний входящий
						ОткрыватьДокументПриСнятииТрубки = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпОткрыватьДокументПриСнятииТрубки");
						Если ОткрыватьДокументПриСнятииТрубки Тогда
							ОткрытьКонтакт(НайденныйЗвонок,"НомерТелефонаЗаголовок");
						КонецЕсли;	
					ИначеЕсли НайденныйЗвонок.ВходящееИсходящееСобытие = 6 Тогда
						// Внешний исходящий
						ОткрыватьДокументПриСнятииТрубкиПриИсходящем = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпОткрыватьДокументПриСнятииТрубкиПриИсходящем");
						Если ОткрыватьДокументПриСнятииТрубкиПриИсходящем Тогда
							ОткрытьКонтакт(НайденныйЗвонок,"НомерТелефонаЗаголовок");
						КонецЕсли;	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			// Если идентификатор перевода имеет значение, то в зависимости от этого выполнить действия
			Если ЗначениеЗаполнено(ИдентификаторПеревода) И ТипЗнч(ИдентификаторПеревода) = Тип("Структура") Тогда
			
				Если ИдентификаторПеревода.ИмяМетода = "SetupTransferCall" Тогда
					
					ТекущийЗвонок = Звонки.Найти(ИдентификаторПеревода.ИдентификаторЗвонка, "ИдентификаторЗвонка");
					Если НЕ ТекущийЗвонок = Неопределено Тогда
						
						Если ТекущийЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.DIALTONE Тогда
							
							Результат = РарусСофтФонКомпонента.DialCall(ИдентификаторПеревода.ИдентификаторЗвонка, ИдентификаторПеревода.НомерТелефона);
							Если Результат = 0 Тогда
								Если ИдентификаторПеревода.Действие = "БезусловныйПеревод" Тогда
									ИдентификаторПеревода = Новый Структура("Действие, ИмяМетода, ИдентификаторЗвонка, НомерТелефона", "БезусловныйПеревод", "DialCall", ИдентификаторПеревода.ИдентификаторЗвонка, "");
								Иначе
									ИдентификаторПеревода = "";
								КонецЕсли;
							Иначе
								Сообщить("DialCall "+РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
								ИдентификаторПеревода = "";
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
				ИначеЕсли ИдентификаторПеревода.ИмяМетода = "DialCall" Тогда
					// Выполняется безусловный перевод
					ПереводимыйЗвонок = Звонки.Найти(ИдентификаторПеревода.ИдентификаторЗвонка, "ИдентификаторКонсультационногоЗвонка");
					КонсультационныйЗвонок = Звонки.Найти(ИдентификаторПеревода.ИдентификаторЗвонка, "ИдентификаторЗвонка");
					Если НЕ ПереводимыйЗвонок = Неопределено И НЕ КонсультационныйЗвонок = Неопределено Тогда
						
						Если ПереводимыйЗвонок.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.ONHOLDPENDTRANSFER Тогда
							
							Если ПодтвердитьУсловныйПеревод(ПереводимыйЗвонок.ИдентификаторЗвонка, ПереводимыйЗвонок.ИдентификаторКонсультационногоЗвонка, КонсультационныйЗвонок.ИдентификаторСобытияСтанции) Тогда
								ИдентификаторПеревода = "";
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			// Если использовать СофтФон пользователю не разрешено, то не показываем работу на экране.
			Если НЕ глИспользоватьСофтФонПрофТекущийПользователь Тогда Возврат; КонецЕсли;
			
			// Откроем либо активизируем форму софтфона
			Если Звонки.Количество() > 0 Тогда
				Если ФормаТелефон = Неопределено Тогда ФормаТелефон = ПолучитьФорму("Телефон");	КонецЕсли;
				
				//++CRM 
				ОткрыватьОкноСофтфонаТолькоПриСнятииТрубки = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ОткрыватьОкноСофтфонаТолькоПриСнятииТрубки");
				ОткрыватьОкноСофтфонаТолькоДляВнешнихТелефонов = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ОткрыватьОкноСофтфонаТолькоДляВнешнихТелефонов");
				ТекущийВнешний = ОпределитьВнутреннийВнешнийЗвонок(Число(Звонок.Origin), НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внешний;
				
				ОткрытьФорму = Истина;
				Если ОткрыватьОкноСофтфонаТолькоДляВнешнихТелефонов и НЕ ТекущийВнешний Тогда
					ОткрытьФорму = Ложь;
				КонецЕсли; 
				Если ОткрыватьОкноСофтфонаТолькоПриСнятииТрубки Тогда
					Если Найти("DIALTONE,CONNECTED",Звонок.State) = 0 Тогда
						ОткрытьФорму = Ложь;
					КонецЕсли;
				КонецЕсли;
				Если ОткрытьФорму Тогда
					Если НЕ ФормаТелефон.Открыта() Тогда 
						ФормаТелефон.Открыть();
					Иначе 
						ФормаТелефон.Активизировать();
					КонецЕсли;
				КонецЕсли;
				//--CRM 
				ИзменениеДоступностиКнопокУправленияЗвонками(ФормаТелефон.ЭлементыФормы);
				
				// Если звонок был найден, то спозиционируемся на него
				Если НайденныйЗвонок = Неопределено Тогда
					ФормаТелефон.ЭлементыФормы.Звонки.ТекущаяСтрока = НоваяСтрока;
					CRMТриггерКнопкиHold(ФормаТелефон.ЭлементыФормы.HoldUnHold, "UnHold");
				Иначе
					ФормаТелефон.ЭлементыФормы.Звонки.ТекущаяСтрока = НайденныйЗвонок;
					Если CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "UnHoldCall", НайденныйЗвонок.ИдентификаторСобытияСтанции) Тогда 
						CRMТриггерКнопкиHold(ФормаТелефон.ЭлементыФормы.HoldUnHold, "Hold");
					Иначе
						CRMТриггерКнопкиHold(ФормаТелефон.ЭлементыФормы.HoldUnHold, "UnHold");
					КонецЕсли;
				КонецЕсли;
				
				//показываем баннер!!!
				ПоказыватьБанер = Ложь;
				Если ПоказыватьБанер Тогда 
					Если НайденныйЗвонок = Неопределено Тогда
						ДанныеСтроки = НоваяСтрока;
					Иначе
						ДанныеСтроки = НайденныйЗвонок;
					КонецЕсли;
					УникальныйИдентификаторЗвонка = Число(Звонок.hCall);//hCallUnique);
					Баннер = ДанныеБаннера.Найти(УникальныйИдентификаторЗвонка,"ДанныеЗвонка");
					УжеУбилиБаннер = СписокОбработанныхЗвонковБаннером.НайтиПоЗначению(УникальныйИдентификаторЗвонка);
					Если Баннер=Неопределено И УжеУбилиБаннер=Неопределено Тогда //баннер еще не показывался по этому звонку
						Если Найти(Врег(ДанныеСтроки.ПредставлениеВходящееИсходящееСобытие),"ВХОДЯЩИЙ")>0 тогда
							ТекстЗаголовок = ДанныеСтроки.ПредставлениеВходящееИсходящееСобытие;
							Если (Найти(ТекстЗаголовок,"Внутренний")>0)И(СтрДлина(ДанныеСтроки.НомерТелефона)=Константы.CRM_МаксимальнаяДлинаВнутреннегоНомера.Получить()) Тогда		
								ТекстТема = ДанныеСтроки.НомерТелефона+":"+ДанныеСтроки.НомерТелефонаСИменем;
								ТекстКонтрагента = ДанныеСтроки.СоединитьССотрудником;
								СтрокаАHTML = "
								|<HTML><HEAD>
								|<META http-equiv=Content-Type content=""text/html; charset=windows-1251"">
								|<META content=""MSHTML 6.00.3790.3959"" name=GENERATOR></HEAD>
								|<BODY>
								|<P><STRONG>Сотрудник:</STRONG> "+ТекстКонтрагента+" </P>
								//|<P>||<a href=""СнятьТрубку"">Ответить</a>||<a href=""ПоложитьТрубку"">Положить трубку</a>||</P>
								|</BODY></HTML>
								|";
							Иначе
								ТекстТема = ?(ДанныеСтроки.НомерТелефона="","Номер не определен!!!",ДанныеСтроки.НомерТелефона);
								ТекстКонтрагента = ДанныеСтроки.Контрагент;
								ТекстКЛ = ДанныеСтроки.КонтактноеЛицо;
								СтрокаАHTML = "
								|<HTML><HEAD>
								|<META http-equiv=Content-Type content=""text/html; charset=windows-1251"">
								|<META content=""MSHTML 6.00.3790.3959"" name=GENERATOR></HEAD>
								|<BODY>
								|<P>"+?(ТекстКонтрагента="","","<STRONG><U>Контрагент:</U></STRONG> "+ТекстКонтрагента)+" "+?(ТекстКЛ="",""," <STRONG><U>Контактное лицо:</U></STRONG> "+ТекстКЛ)+"</P>
								//|<P>||<a href=""СнятьТрубку"">Ответить</a>||<a href=""ПоложитьТрубку"">Положить трубку</a>||</P>
								|</BODY></HTML>
								|";
							КонецЕсли;
							ИДБаннера = РарусСофтФонКомпонента.ПоказатьБаннер(ТекстЗаголовок, ТекстТема,СтрокаАHTML, 3, 0, Истина, "СнятьТрубку", Истина);
							НоваяСтрока = ДанныеБаннера.Добавить();
							НоваяСтрока.ИДБаннера = ИДБаннера;
							НоваяСтрока.ДанныеЗвонка = УникальныйИдентификаторЗвонка; 
						КонецЕсли;
					ИначеЕсли НЕ Баннер=Неопределено и УжеУбилиБаннер=Неопределено Тогда
						Если Найти(Врег(ДанныеСтроки.ПредставлениеВходящееИсходящееСобытие),"ВХОДЯЩИЙ")>0 тогда
							ТекстЗаголовок = ДанныеСтроки.ПредставлениеВходящееИсходящееСобытие;
							Если (Найти(ТекстЗаголовок,"Внутренний")>0)И(СтрДлина(ДанныеСтроки.НомерТелефона)=Константы.CRM_МаксимальнаяДлинаВнутреннегоНомера.Получить())Тогда
								ТекстТема = ДанныеСтроки.НомерТелефона+":"+ДанныеСтроки.НомерТелефонаСИменем;
								ТекстКонтрагента = ДанныеСтроки.СоединитьССотрудником;
								СтрокаАHTML = "
								|<HTML><HEAD>
								|<META http-equiv=Content-Type content=""text/html; charset=windows-1251"">
								|<META content=""MSHTML 6.00.3790.3959"" name=GENERATOR></HEAD>
								|<BODY>
								|<P><STRONG>Сотрудник:</STRONG> "+ТекстКонтрагента+" </P>
								//|<P>||<a href=""СнятьТрубку"">Ответить</a>||<a href=""ПоложитьТрубку"">Положить трубку</a>||</P>
								|</BODY></HTML>
								|";
							Иначе
								ТекстТема = ?(ДанныеСтроки.НомерТелефона="","Номер не определен!!!",ДанныеСтроки.НомерТелефона);
								ТекстКонтрагента = ДанныеСтроки.Контрагент;
								ТекстКЛ = ДанныеСтроки.КонтактноеЛицо;
								СтрокаАHTML = "
								|<HTML><HEAD>
								|<META http-equiv=Content-Type content=""text/html; charset=windows-1251"">
								|<META content=""MSHTML 6.00.3790.3959"" name=GENERATOR></HEAD>
								|<BODY>
								|<P>"+?(ТекстКонтрагента="","","<STRONG>Контрагент:</STRONG> "+ТекстКонтрагента)+" "+?(ТекстКЛ="",""," <STRONG>Контактное лицо:</STRONG> "+ТекстКЛ)+"</P>
								//|<P>||<a href=""СнятьТрубку"">Ответить</a>||<a href=""ПоложитьТрубку"">Положить трубку</a>||</P>
								|</BODY></HTML>
								|";
							КонецЕсли;
							Рез = РарусСофтФонКомпонента.ИзменитьБаннер(Баннер.ИДБаннера,ТекстЗаголовок, ТекстТема,СтрокаАHTML, 3, 0, "СнятьТрубку", Истина,0,0);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;	
				
			КонецЕсли;
			
		Иначе  //пришло событие от линии которую мы можем перехватить
			Если (Звонок.State = "OFFERING" ) или (Звонок.State="ACCEPTED")  или (Звонок.State="RINGBACK" и Звонок.Provider = ТипыПоддерживаемогоОборудования.Asterisk) Тогда //т.е. есть звонок на другом номере
				//Проверим пришло ли изменение звонка или пришел новый звонок
				Если глСписокДоступныхЛинийДляПерехвата.Найти(Данные,"Handle")=Неопределено Тогда
					//нужно добавить информацию в список линий которые мы можем перехватывать
					НоваяСтрока = глСписокДоступныхЛинийДляПерехвата.Добавить();
					НоваяСтрока.Handle = Данные;
					НоваяСтрока.Звонок = Звонок;
					НоваяСтрока.ИмяЛинии = Звонок.Line;
					//Нужно показать баннер!!!
					//ПоказыватьБанер = сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ПоказыватьБаннерДляПерехватаЗвонков");
					ПоказыватьБанер = Ложь;
					Если ПоказыватьБанер Тогда
						УникальныйИдентификаторЗвонка = Число(Звонок.hCall);
						Если ДанныеБаннера.Найти(УникальныйИдентификаторЗвонка,"ДанныеЗвонка")=Неопределено Тогда
							
							ТекстЗаголовок = Звонок.OriginDescr;
							ТекстТема = Звонок.CallerId+" "+Звонок.CallerInfoName+" "+" звонит на "+Звонок.CalledId;
							ТекстКонтрагента = "Есть возможность перехватить звонок!!!";
							СтрокаАHTML = "
							|<HTML><HEAD>
							|<META http-equiv=Content-Type content=""text/html; charset=windows-1251"">
							|<META content=""MSHTML 6.00.3790.3959"" name=GENERATOR></HEAD>
							|<BODY>
							|<P><STRONG>"+ТекстКонтрагента+"</STRONG> </P>
							//|<P>||<a href=""ПерехватитьЗвонок"">Перехватить</a>||</P>
							|</BODY></HTML>
							|";
							ИДБаннера = РарусСофтФонКомпонента.ПоказатьБаннер(ТекстЗаголовок, ТекстТема,СтрокаАHTML, 3, 0, Истина, "ПерехватитьЗвонок", Истина);
							НоваяСтрока = ДанныеБаннера.Добавить();
							НоваяСтрока.ИДБаннера = ИДБаннера;
							НоваяСтрока.ДанныеЗвонка = УникальныйИдентификаторЗвонка; 
						КонецЕсли;
					КонецЕсли;
					//Нужно показать что есть звонок который мы можем перехватить, нужно изменить текст надписи перехват звонка
					ФормаТелефон.УправлениеИндикациейПерехвата();
				КонецЕсли;
			Иначе
				//нужно убрать информацию о том что есть возможность перехвата звонка
				//убираем банер
				УникальныйИдентификаторЗвонка = Число(Звонок.hCall);
				НайденныйБаннер = ДанныеБаннера.Найти(УникальныйИдентификаторЗвонка,"ДанныеЗвонка");
				Если НЕ НайденныйБаннер=Неопределено Тогда
					Результат = РарусСофтФонКомпонента.ЗакрытьБаннер(НайденныйБаннер.ИДБаннера);
					ДанныеБаннера.Удалить(НайденныйБаннер);
				КонецЕсли;
				//убираем инфу из таблицы
				НайденнаяЛиния = глСписокДоступныхЛинийДляПерехвата.Найти(Звонок.hCall,"Handle");
				Если НЕ НайденнаяЛиния=Неопределено Тогда
					глСписокДоступныхЛинийДляПерехвата.Удалить(НайденнаяЛиния);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

// Источник = "rtup", Событие = "LineCloseOpen" из модуля приложения.
// Это Основная функция осуществляющая реакцию на изменение состояния линии.
// Открыта данная линия на сервере или нет.
Процедура ИзменениеСостоянияЛинии(Данные) Экспорт
	ИзменитьСписокЛиний(Данные);
КонецПроцедуры

// Источник = "rtup", Событие = "LoginStateChange" из модуля приложения.
// Это Основная функция осуществляющая реакцию изменение подключения к серверу
// СофтФона.
Процедура ИзменениеСостоянияПодключения(Данные) Экспорт
	СтатусПодключения = Число(Данные);
	СтатусПодключенияОписание = РарусСофтФонКомпонента.ПолучитьОписаниеСтатусаПодключения(СтатусПодключения);
	// Запомним статус подключения
	ТекущийСтатусПодключения = СтатусПодключения;
	// Если клиент отключен от сервера
	Если СтатусПодключения = 0 Тогда
		// Загрузим активные линии
		ОчиститьСписокЛиний();
		// Получаем параметры к sql серверу, содержащему списки звонков
		SQLПолучитьПараметры();
		//Звонки.Очистить();
		ОтключитьКонтрольЛинии();
		// Установим признак первого запуска, используется для
		//  проверки пропущенных вызовов, которые были зарегистрированы
		//  пока система не работала.
		ПризнакПервогоЗапуска = Истина;
	// Если клиент готов к работе
	ИначеЕсли СтатусПодключения = 6 Тогда
		// Установим признак первого запуска, используется для
		//  проверки пропущенных вызовов, которые были зарегистрированы
		//  пока система не работала.
		ПризнакПервогоЗапуска = Истина;
		// Проверяем версии сервера и компоненты, если они отличаются то вызываем дисконнект.
		Попытка
			ПараметрыВерсий = РарусСофтФонКомпонента.ПолучитьBнутрВерсию();
			ВерсияСервера   = Число(ПараметрыВерсий.SrvVer);
			ВерсияКомпонеты	= Число(ПараметрыВерсий.CmpVer);
			Если ВерсияСервера = ВерсияКомпонеты	Тогда
				// Запрашиваем параметры ведения лога, т.е. параметры подключения 
				//  к серверу, содержащему список телефонов
				// После запроса придет событие LogParams
				РарусСофтФонКомпонента.ЗапроситьПораметрыВеденияЛога();
				// Загрузим активные линии
				ЗагрузитьСписокЛиний();
				// Устанавливаем контроль над линией
				ВключитьКонтрольЛинии();
			Иначе
				Предупреждение("ВНИМАНИЕ!!! Различаются версия сервера и компоненты. Они должны быть идентичны.
				|Необходимо выполнить проверку версий сервера и компоненты из общего каталога.
				|Версия сервера: ["+ВерсияСервера+"], Версия компоненты: ["+ВерсияКомпонеты+"].
				|Будет выполнено отключение от сервера.", 60, "ОШИБКА!!!");
				ОтключитьСервер();
			КонецЕсли;
		Исключение
			Сообщить("Неудачная попытка подключения...");
			ОтключитьСервер();
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

// Источник = "rtup", Событие = "LineLock" и "LineUnLock" из модуля приложения.
// Это Основная функция осуществляющая реакцию на изменение состояния линии.
// Занята данная линия или свободна.
Процедура ИзменениеСатусаЛинии(Событие, Данные) Экспорт
	
	СвободнаяЛиния = Истина;
	Если Событие = "LineLock" Тогда
		СвободнаяЛиния = Ложь;
	ИначеЕсли Событие = "LineUnLock" Тогда
		СвободнаяЛиния = Истина;
	КонецЕсли;
	
	// Изменяем состояние линии, занята или свободна
	ИзменитьСостояниеЛинии(Число(Данные), ?(СвободнаяЛиния=Истина, 1, 2));
	
КонецПроцедуры

// Источник = "rtup", Событие = "LogParams" из модуля приложения.
// Это Основная функция осуществляющая реакцию на изменение параметров подключения к
// внешней таблице SQL.
Процедура ИзменениеПараметровСервераСпискаЗвонков(Событие, Данные) Экспорт
	
	// Получаем параметры к sql серверу, содержащему списки звонков
	SQLПолучитьПараметры();
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБАТЫВАЮЩИЕ РАЗНЫЕ СОСТОЯНИЯ ЗВОНКА

// Функция разбирает переданный номер телефона или структуру содержащую поля номера телефона в зависимости
//  от того какой это звонок входящий или исходящих. Если входящий звонок, то передается строка с номером телефона
//  а возвращаем структуру содержащую поля номера телефона. Так же проверяем если звонок внутренний, то сохраняем номер
//  как внутренний, иначе передаем его как есть, пользователь разберет номер в ручную.
//  Если звонок исходящий то получаем структуру в качестве номера, 
//а возвращаем преобразованную строку которую будет набирать
//  станция.
Функция ПреобразоватьНомерСУчетомПрефиксов(НомерТелефона, СостояниеЗвонка="", ВходящийИсходящийЗвонок=Неопределено) Экспорт
	// Подготавливаем структуру номера телефона который будем возвращать при входящем звонке
	//  при исходящем будем возвращать строку с номером телефона
	РезультатНомер = CRMСформироватьСтруктуруНомераИзПолей();
	// Если точно не определен входящий или исходящий звонок....то пытаемся определить его по состоянию звонка
	Если ВходящийИсходящийЗвонок = Неопределено Тогда
		ВходящийИсходящийЗвонок = ?(СостояниеЗвонка = "", ТипВходящийИсходящийЗвонок.Исходящий, ОпределитьВходящееИсходящееСобытие(СостояниеЗвонка));
	КонецЕсли;
	// Обрабатываем входящий звонок
	Если ВходящийИсходящийЗвонок = ТипВходящийИсходящийЗвонок.Входящий И ТипЗнч(НомерТелефона) = Тип("Строка") Тогда
		НомерТелефона = CRMУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
		// Если состояние звонка является внутренним номером, то никаких префиксов подставлять не нужно 
		//  просто сразу возвращаем номер телефона, структуру смотрим в ВидыВходящихИсходящихСобытий
		Если ОпределитьВнутреннийВнешнийЗвонок(СостояниеЗвонка, НомерТелефона) = ТипВнутреннийВнешнийЗвонок.Внутренний Тогда
			РезультатНомер.Внутренний = НомерТелефона;
		Иначе
			// Если длина телефона равна 7 цифрам или меньше, то это только номер
			Если СтрДлина(НомерТелефона) < 8 Тогда
				РезультатНомер.НомерТелефона = НомерТелефона;
			ИначеЕсли СтрДлина(НомерТелефона) = 10 Тогда
			// Если длина телефона равна 10 цифрам, то это код города и номер
				РезультатНомер.КодСтраны = глКодСтраны;
				// Если код города совпадает
				Если Найти(НомерТелефона, глКодГорода) = 1 Тогда
					РезультатНомер.КодГорода = глКодСтраны;
					РезультатНомер.НомерТелефона = Сред(НомерТелефона, СтрДлина(глКодСтраны) + 1);
				Иначе
				// Считаем, что длина кода города 3 цифры
					РезультатНомер.КодГорода = Лев(НомерТелефона, 3);
					РезультатНомер.НомерТелефона = Сред(НомерТелефона, 4);
				КонецЕсли;	
			Иначе	
			// Считаем, что длина номера 7 цифр, остальные код города
				РезультатНомер.КодСтраны     = глКодСтраны;
				РезультатНомер.НомерТелефона = Прав(НомерТелефона, 7);
				РезультатНомер.КодГорода	 = Лев(НомерТелефона, СтрДлина(НомерТелефона) - 7);
			КонецЕсли;
		КонецЕсли;
		// Сформируем представление
		CRMСформироватьПредставлениеТелефонаПоСтруктуре(РезультатНомер);
	ИначеЕсли ВходящийИсходящийЗвонок = ТипВходящийИсходящийЗвонок.Исходящий И ТипЗнч(НомерТелефона) = Тип("Структура") Тогда
		// Обрабатываем исходящий звонок
		// Необходимо позвонить по внутреннему номеру, префиксы не нужны
		Если (НЕ ПустаяСтрока(НомерТелефона.Внутренний) И ПустаяСтрока(НомерТелефона.НомерТелефона))
			Или (НЕ ПустаяСтрока(НомерТелефона.НомерТелефона) И ПустаяСтрока(НомерТелефона.Внутренний) 
			И СтрДлина(CRMУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона.НомерТелефона)) <= глМаксимальнаяДлинаВнутреннегоНомера) Тогда
			РезультатНомер = ?(НЕ ЗначениеЗаполнено(НомерТелефона.Внутренний), НомерТелефона.НомерТелефона, НомерТелефона.Внутренний);
		ИначеЕсли ПустаяСтрока(НомерТелефона.НомерТелефона) Тогда
			РезультатНомер = "";		
		Иначе
			КодСтраныНомера = ?(СокрЛП(НомерТелефона.КодСтраны) = "8", "7", СтрЗаменить(НомерТелефона.КодСтраны, "+", ""));
			КодГородаНомера = НомерТелефона.КодГорода;
			КодСтраныБезПлюса = СтрЗаменить(глКодСтраны, "+", "");
			// если не внутренний то необходимо использовать префиксы, либо выхода в город либо выхода на межгород
			// 1. Если это наш город и страна, то просто префикс выхода в город
			Если (КодСтраныНомера = КодСтраныБезПлюса Или ПустаяСтрока(КодСтраныНомера)) И (КодГородаНомера = глКодГорода Или ПустаяСтрока(КодГородаНомера)) Тогда
				РезультатНомер = глПрефиксВыходаВГород + НомерТелефона.НомерТелефона;
			// 2. Если это не наш город, но наша страна, то выход на межгород
			ИначеЕсли (НЕ КодГородаНомера = глКодГорода И НЕ ПустаяСтрока(КодГородаНомера)) И (КодСтраныНомера = КодСтраныБезПлюса Или ПустаяСтрока(КодСтраныНомера)) Тогда
				РезультатНомер = глПрефиксВыходаВГород + глПрефиксВыходаНаМежгород + КодГородаНомера + НомерТелефона.НомерТелефона;
			// 3. Если это не наша страна, то международный звонок через 10.
			Иначе
				РезультатНомер = глПрефиксВыходаВГород + глПрефиксВыходаНаМежгород + глПрефиксВыходаНаМеждународную + КодСтраныНомера + КодГородаНомера + НомерТелефона.НомерТелефона;
			КонецЕсли;
		КонецЕсли;
		РезультатНомер = CRMУбратьИзНомераТелефонаВсеПрефиксы(РезультатНомер);
	КонецЕсли;
	Возврат РезультатНомер;
КонецФункции

// По переданному входящему или исходящему событию в числовом виде, определим его значение
//  если определить не удается то по умолчанию передадим входящее сообщение
Функция ОпределитьВходящееИсходящееСобытие(Знач НомерСобытия) Экспорт
	// Расшифровка обозначения числовых кодов, дана в синониме перечисления
	//  фактически для каждой из компонент придется переопределять данное перечисление
	//  потому что они могут отличаться
	НомерСобытия = Число(НомерСобытия);
	Если НомерСобытия = ВидыВходящихИсходящихСобытий.ВнутреннийИсходящий Или 
		 НомерСобытия = ВидыВходящихИсходящихСобытий.ВнешнийИсходящий Тогда
		Возврат ТипВходящийИсходящийЗвонок.Исходящий;
	Иначе
		Возврат ТипВходящийИсходящийЗвонок.Входящий;
	КонецЕсли;
КонецФункции

// Функция определяет и возвращает какой это звонок внутренний или внешний
Функция ОпределитьВнутреннийВнешнийЗвонок(Знач СостояниеЗвонка, НомерТелефона) Экспорт
	СостояниеЗвонка = Число(СостояниеЗвонка);
	Если СостояниеЗвонка = ВидыВходящихИсходящихСобытий.ВнешнийВходящий Тогда
		Возврат ТипВнутреннийВнешнийЗвонок.Внешний;
	ИначеЕсли СостояниеЗвонка = ВидыВходящихИсходящихСобытий.ВнешнийИсходящий Тогда
		Возврат ТипВнутреннийВнешнийЗвонок.Внешний;
	ИначеЕсли ЗначениеЗаполнено(НомерТелефона) Тогда                  
		НомерТелефона = CRMУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
		Если ПустаяСтрока(НомерТелефона) Тогда
			Возврат ТипВнутреннийВнешнийЗвонок.Внешний;	
		ИначеЕсли CRMОпределитьВнутреннийЗвонокПоНомеру(НомерТелефона, глМаксимальнаяДлинаВнутреннегоНомера) Тогда
			Возврат ТипВнутреннийВнешнийЗвонок.Внутренний;
		Иначе
			Возврат ТипВнутреннийВнешнийЗвонок.Внешний;	
		КонецЕсли;	
	ИначеЕсли СостояниеЗвонка = ТипВнутреннийВнешнийЗвонок.Внутренний Тогда
		Возврат ТипВнутреннийВнешнийЗвонок.Внутренний;
	ИначеЕсли СостояниеЗвонка = ТипВнутреннийВнешнийЗвонок.Внешний Тогда
		Возврат ТипВнутреннийВнешнийЗвонок.Внешний;	
	Иначе
		Возврат ТипВнутреннийВнешнийЗвонок.Внешний;
	КонецЕсли;
КонецФункции

// Функция возвращает описание по типу входящего исходящего состояния
Функция ПолучитьОписаниеВходящегоИсходящегоСобытия(ВходящееИсходящееСобытие) Экспорт
	ВходящееИсходящееСобытие = Число(ВходящееИсходящееСобытие);
	ОписаниеРезультат = "Неопределено";
	Для Каждого ТекущееОписание Из ВидыВходящихИсходящихСобытий Цикл
		Если ТекущееОписание.Значение = ВходящееИсходящееСобытие Тогда
			ОписаниеРезультат = ВидыВходящихИсходящихСобытийОписание[ТекущееОписание.Ключ];
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат ОписаниеРезультат;
КонецФункции

// Определяет какое событие звонка,
//  входящее или исходящее по переданному Типу звонка и соответственно возвращает НомерА или НомерБ
Функция ВернутьНомерИлиИмяТелефонаТелефона(ТипЗвонка, НомерА, НомерБ) Экспорт
	ТипЗвонка = Число(ТипЗвонка);
	Если ОпределитьВходящееИсходящееСобытие(ТипЗвонка) = ТипВходящийИсходящийЗвонок.Исходящий Тогда
		Возврат СокрЛП(НомерБ);
	Иначе
		Возврат СокрЛП(НомерА);
	КонецЕсли;
КонецФункции

// Начальные доступности кнопок управления
Процедура НачальнаяДоступностьКнопокУправленияЗвонками(ЭлементыФормы) Экспорт
	//ЭлементыФормы.Ответить.Доступность					= Ложь;
	//ЭлементыФормы.HoldUnHold.Доступность 					= Ложь;
	//ЭлементыФормы.Перевести.Доступность					= Ложь;
	//ЭлементыФормы.УсловныйПеревод.Доступность 			= Ложь;
	//ЭлементыФормы.ПодтвердитьУсловныйПеревод.Доступность 	= Ложь;
	//ЭлементыФормы.Отбой.Доступность 						= Ложь;
КонецПроцедуры

// Изменение доступности кнопок управления 
Процедура ИзменениеДоступностиКнопокУправленияЗвонками(ЭлементыФормы, Строка = Неопределено) Экспорт
	
 
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ РАБОТЫ СО ЗВОНКОМ

//Снимает трубку при поступлении звонка
//Звонок должен быть в состоянии OFFERING 
Функция ОтветитьНаВызов(УказательНаЗвонок, Статус) Экспорт
	ИдентификаторПеревода = "";
	Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "AnswerCall", Статус);
	Если Рез Тогда 
		Результат = РарусСофтФонКомпонента.AnswerCall(Число(УказательНаЗвонок));
		Если Результат < 0 Тогда
			Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
			Рез = Ложь;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

//Вешает звонок на Hold
//Звонок должен быть в состоянии Connected
Функция УдержаниеВызова(УказательНаЗвонок, Статус) Экспорт
	ИдентификаторПеревода = "";
	Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "HoldCall", Статус);
	Если Рез Тогда
		Результат = РарусСофтФонКомпонента.HoldCall(Число(УказательНаЗвонок));
		Если Результат < 0 Тогда
			Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
			Рез = Ложь;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат Рез;
КонецФункции

//Снимает звонок с Hold
//Звонок должен быть в состоянии onHold, onHoldPendingTransfer, onHoldPendingConference 
Функция ОтменитьУдержаниеВызова(УказательНаЗвонок, Статус) Экспорт
	ИдентификаторПеревода = "";
	Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "UnHoldCall", Статус);
	Если Рез Тогда
		Результат = РарусСофтФонКомпонента.UnHoldCall(Число(УказательНаЗвонок));
		Если Результат < 0 Тогда
			Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
			Рез = Ложь;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Рез;
КонецФункции

//Уничтожить звонок
//Звонок должен быть в любом состоянии кроме Idle
Функция УничтожитьЗвонок(УказательНаЗвонок, Статус) Экспорт
	ИдентификаторПеревода = "";
	Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "DropCall", Статус);
	Если Рез Тогда
		Результат = РарусСофтФонКомпонента.DropCall(Число(УказательНаЗвонок));
		Если Результат < 0 Тогда
			Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
			Рез = Ложь;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Рез;
КонецФункции

//Безусловный перевод звонка
// СтруктураНабранногоКонтакта - приходит структура содержащая набор контактной информации, связанной с этим звонком,
// в функции безусловного звонка она нам не нужна, т.к. не имеет смысла, но оставлена специально для общего вида
// УказательНаКонсультационныйЗвонок - используется в том, случае если необходимо эмулировать безконсультационный звонок
Функция БезусловныйПереводЗвонок(СтрокаТекущегоЗвонка, НомерАбонента, УказательНаКонсультационныйЗвонок=0) Экспорт
	УказательНаЗвонок = Число(СтрокаТекущегоЗвонка.ИдентификаторЗвонка);	
	
	// Определяем тип используемой станции, т.к., например, PANASONIC отказался 
	//  от поддержки BlindTransferCall, будем его эмулировать с помощью условного перевода
	Звонок = РарусСофтФонКомпонента.GetCallInfo(УказательНаЗвонок);
	
	НабранныйКонтакт = Неопределено;
	// Если звонок не определен, то тип определить мы не можем, и следовательно по умолчанию вызываем BlindTransferCall
	//Если (Число(Звонок.Provider) = ТипыПоддерживаемогоОборудования.PanasonicTDA)
	//	ИЛИ(Число(Звонок.Provider) = ТипыПоддерживаемогоОборудования.Alcatel)
	//	ИЛИ(Число(Звонок.Provider) = ТипыПоддерживаемогоОборудования.Ericsson) Тогда
	//	
	//	Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "SetupTransferCall", СтрокаТекущегоЗвонка.ИдентификаторСобытияСтанции);
	//	Если Рез Тогда
	//		УказательНаКонсультационныйЗвонок = 0;
	//		Результат = РарусСофтФонКомпонента.SetupTransferCall(УказательНаЗвонок, УказательНаКонсультационныйЗвонок);
	//		Если Результат < 0 Тогда
	//			Сообщить("SetupTransferCall "+РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
	//			НабранныйКонтакт = Неопределено;
	//			Возврат Ложь;
	//		КонецЕсли;
	//		
	//		ИдентификаторПеревода = Новый Структура("Действие, ИмяМетода, ИдентификаторЗвонка, НомерТелефона", "БезусловныйПеревод", "SetupTransferCall", УказательНаКонсультационныйЗвонок, Строка(НомерАбонента));
	//		
	//	КонецЕсли;
	//	
	//Иначе
	
		Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "BlindTransferCall", СтрокаТекущегоЗвонка.ИдентификаторСобытияСтанции);
		Если Рез Тогда
			Если НЕ СтрокаТекущегоЗвонка.ИдентификаторСобытияСтанции = ВидыСобытийСтанции.OFFERING Тогда
				Результат = РарусСофтФонКомпонента.BlindTransferCall(УказательНаЗвонок, Строка(НомерАбонента));
			Иначе
				Результат = РарусСофтФонКомпонента.CallRedirect(УказательНаЗвонок, Строка(НомерАбонента),0);
			КонецЕсли;
			Если Результат < 0 Тогда
				Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
				Рез = Ложь;
				НабранныйКонтакт = Неопределено;
			КонецЕсли;	
		КонецЕсли;
	//КонецЕсли;
	
	Возврат Рез;
КонецФункции

//Условный перевод звонка
Функция УсловныйПереводЗвонок(УказательНаЗвонок, Статус, УказательНаКонсультационныйЗвонок, НомерАбонента) Экспорт
	Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "SetupTransferCall", Статус);
	Если Рез Тогда
		Звонок = РарусСофтФонКомпонента.GetCallInfo(УказательНаЗвонок);
		Если Число(Звонок.Provider) = ТипыПоддерживаемогоОборудования.Asterisk Тогда
			//Если это Астериск, то передаем номер сразу
			Результат = РарусСофтФонКомпонента.DialCall(Число(УказательНаЗвонок), НомерАбонента);
		Иначе
			Результат = РарусСофтФонКомпонента.SetupTransferCall(Число(УказательНаЗвонок), УказательНаКонсультационныйЗвонок);   
		КонецЕсли;
		Если Результат < 0 Тогда
			Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
			НабранныйКонтакт = Неопределено;
			Возврат Ложь;
		КонецЕсли;
		
		ИдентификаторПеревода = Новый Структура("Действие, ИмяМетода, ИдентификаторЗвонка, НомерТелефона", "УсловныйПеревод", "SetupTransferCall", УказательНаКонсультационныйЗвонок, Строка(НомерАбонента));
		
		// Если набор номера выполнен корректно, сохраним его в контакте
		Если НЕ НабранныйКонтакт = Неопределено Тогда
			НабранныйКонтакт.Вставить("ИдентификаторЗвонка", УказательНаКонсультационныйЗвонок);
			НабранныйКонтакт.Вставить("НомерТелефона",  ПреобразоватьНомерСУчетомПрефиксов(НомерАбонента, ""));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

//Подтверждение условного перевода звонка
//Переводимый звонок(ИдентификаторЗвонка) должен быть в состоянии onHoldPendingTransfer
//КонсультационныйЗвонок(ИдентификаторКонсультационногоЗвонка) 
//должен быть в состоянии Connected, Ringback, Busy или Proceeding
Функция ПодтвердитьУсловныйПеревод(УказательНаЗвонок, УказательНаКонсультационныйЗвонок, СтатусКонсультационногоЗвонка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтатусКонсультационногоЗвонка) Тогда
		СтатусКонсультационногоЗвонка = ВидыСобытийСтанции.CONNECTED;
	КонецЕсли;
	
	Рез = CRMИспользоватьМетодСУчетомСтатусаМожно(ВидыСобытийСтанции, "CompleteTransferCall", СтатусКонсультационногоЗвонка, УказательНаКонсультационныйЗвонок);
	Если Рез Тогда
		Результат = РарусСофтФонКомпонента.CompleteTransferCall(Число(УказательНаЗвонок), Число(УказательНаКонсультационныйЗвонок));
		Если Результат < 0 Тогда
			Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
			Рез = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

//Набирает номер абонента
Функция НабратьНомер(НомерАбонента) Экспорт
	Если НЕ РарусСофтФонКомпонента = Неопределено Тогда
		ИдентификаторПеревода = "";
		Рез = Истина;
		Сообщить("Был выполнен набор номера " + НомерАбонента + " .... ");
		УказательНаЗвонок = Неопределено;
		Результат = РарусСофтФонКомпонента.MakeCall(УказательНаЛинию, Строка(НомерАбонента), УказательНаЗвонок);
		Если Результат < 0 Тогда
			Сообщить(РарусСофтФонКомпонента.TapiCheck(Число(Результат)));
			Рез = Ложь;
			НабранныйКонтакт = Неопределено;
		КонецЕсли;
		// Если набор номера выполнен корректно, сохраним его в контакте
		Если Рез И НЕ НабранныйКонтакт = Неопределено Тогда
			НабранныйКонтакт.Вставить("ИдентификаторЗвонка", ?(ЗначениеЗаполнено(УказательНаЗвонок), УказательНаЗвонок, 0));
			НабранныйКонтакт.Вставить("НомерТелефона", ?(ЗначениеЗаполнено(УказательНаЗвонок), УказательНаЗвонок, НомерАбонента));
		КонецЕсли;
		
		Возврат Рез;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции	

/////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ИНИЦИАЛИЗАЦИИ СОФТ ФОНА

// Открываем переданную линии, если открывается нормально,
//  то все ок, иначе ошибку
Функция ОткрытьЛинию(ИмяЛинии) Экспорт
	Результат = Истина;
	Попытка
		Рез = РарусСофтФонКомпонента.OpenLine(ИмяЛинии, УказательНаЛинию);
		Если Рез <> 0 Тогда
			РарусСофтФонКомпонента.TAPICheck(Рез);
			Результат = Ложь;
		КонецЕсли;
	Исключение
		Результат = Ложь;	
	КонецПопытки;

	Звонки.Очистить();
	Возврат Результат;
КонецФункции

// Закрыть открытую линию
Функция ЗакрытьЛинию(УказательНаЛинию) Экспорт
	Результат = Истина;
	Попытка
		Рез = РарусСофтФонКомпонента.CloseLine(УказательНаЛинию);
		Если Рез <> 0 Тогда
			РарусСофтФонКомпонента.TAPICheck(Рез);
			Результат = Ложь;
		КонецЕсли;
	Исключение
		Результат = Ложь;
	КонецПопытки;
	
	Возврат Результат;
КонецФункции

// Функция выполняет поиск объекта в базе данных по значению переданного GUID
Функция НайтиКонтактПоGUID(GUIDСоединенС) Экспорт
	Результат = Ложь;
	Попытка
		УникальныйИдентификаторДляПоиска = Новый УникальныйИдентификатор(GUIDСоединенС);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Для каждого ТекущееИмяМенеджера Из Метаданные.Перечисления.ВидыОбъектовКонтактнойИнформации.ЗначенияПеречисления Цикл
		НайденныйОбъект = Справочники[ТекущееИмяМенеджера.Имя].ПолучитьСсылку(УникальныйИдентификаторДляПоиска);
		
		// Ссылка создается в любом лучае...Даже если объект не был найден
		//  но если объект найден не был, то в ссылке есть строка <Объект не найден>
		ЕстьОбъект = Найти(НайденныйОбъект, "<Объект не найден>");
		Если ЕстьОбъект = 0 Тогда Результат = Истина; Прервать; КонецЕсли;
	КонецЦикла;
	                                   
	Возврат ?(Результат, НайденныйОбъект, Неопределено);
КонецФункции

// Процедура очищает список линий и указатель на текущую линию
Процедура ОчиститьСписокЛиний()
	
	УстановитьПараметрыИспользуемойЛинии();	
	
	Линии.Очистить();
КонецПроцедуры

// Загружаем линии определенные на текущий момент в
//  системе по всем провайдерам
Процедура ЗагрузитьСписокЛиний() Экспорт
	// Если статус клиента отличает от клиент готов линии грузить нельзя
	Если НЕ (ТекущийСтатусПодключения = 6) Тогда Возврат; КонецЕсли;
	// Очищаем таблицы активных линий
	ОчиститьСписокЛиний();
	ИмяИспользуемойЛинии = СокрЛП(сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ИмяЛинии"));
	УказательНаЛинии = РарусСофтФонКомпонента.ПолучитьПараметрыЛиний();
	X = 0; Y = 0;
	// Не всегда компонента успевает вернуть полученный список, 
	//  бывает так, что после подключения список еще не определен
	ПризнакКорретногоПолученияСписка = Ложь;
	Попытка
		УказательНаЛинии.GetBounds(X, Y);
		ПризнакКорретногоПолученияСписка = Истина;
	Исключение
	КонецПопытки;
	СтруктураОповещения = Новый Структура("МаксимальноеЧислоЛиний, ТекущаяЗагружаемаяЛииния", 0, 0);
	ФормаТелефон.ОповеститьОбАктивизацииОбъекта(СтруктураОповещения);
	Если ПризнакКорретногоПолученияСписка Тогда
		Для НомY = 0 По Y - 1 Цикл
			ЛинияОткрыта				= УказательНаЛинии.GetValue(0,	НомY);
			Идентификатор				= УказательНаЛинии.GetValue(1,	НомY);
			ИмяЛинии					= УказательНаЛинии.GetValue(2,	НомY);
			ОписаниеЛинии				= УказательНаЛинии.GetValue(3,	НомY);
			НомерПривязанныйКЛинии		= УказательНаЛинии.GetValue(4,	НомY);
			СостояниеЛинии				= УказательНаЛинии.GetValue(5,	НомY);
			УникальныйИдентификатор		= УказательНаЛинии.GetValue(6,	НомY);
			Провайдер					= УказательНаЛинии.GetValue(7,	НомY);
			ВиртуальноеИмяЛинии			= УказательНаЛинии.GetValue(8,	НомY);
			ВиртуальноеНазваниеЛинии	= УказательНаЛинии.GetValue(9,	НомY);
			ВиртуальныйНомерЛинии		= УказательНаЛинии.GetValue(10,	НомY);
			// Если линия открыта тогда добавляем ее в список линий
			Если ЛинияОткрыта Тогда
				// Заполним визуальную таблицу
				НоваяЛиния = Линии.Добавить();
				НоваяЛиния.ИдентификаторЛинии = Число(Идентификатор);
				Если ВиртуальноеИмяЛинии = Неопределено Тогда
					НоваяЛиния.ИмяЛинии = СокрЛП(ИмяЛинии);
				Иначе
					НоваяЛиния.ИмяЛинии = СокрЛП(ВиртуальноеИмяЛинии);
				КонецЕсли;	
				Если ВиртуальныйНомерЛинии = Неопределено Тогда
					НоваяЛиния.НомерПривязанныйКЛинии = СокрЛП(НомерПривязанныйКЛинии);
				Иначе
					НоваяЛиния.НомерПривязанныйКЛинии = СокрЛП(ВиртуальныйНомерЛинии);
				КонецЕсли;
				НоваяЛиния.СостояниеЛинии = Число(СостояниеЛинии);
				НоваяЛиния.УникальныйУказательНаЛинию = Число(УникальныйИдентификатор);
				Если НоваяЛиния.УникальныйУказательНаЛинию = 0 Тогда					
					НоваяЛиния.УникальныйУказательНаЛинию = сфпПолучитьУникальныйУказательНаЛинию(НоваяЛиния.ИмяЛинии);
				КонецЕсли;	
				НоваяЛиния.Провайдер = Число(Провайдер);
				// Установим линии активной
				Если (ИмяЛинии = ИмяИспользуемойЛинии) или (ВиртуальноеИмяЛинии = ИмяИспользуемойЛинии) Тогда
					УстановитьПараметрыИспользуемойЛинии(НоваяЛиния);
					НоваяЛиния.ОсновнаяЛиния = Истина;
				КонецЕсли;
			КонецЕсли;
			// Выведем на индикаторе текущую загрузку линий
			СтруктураОповещения.МаксимальноеЧислоЛиний   = Y;
			СтруктураОповещения.ТекущаяЗагружаемаяЛииния = НомY;
			ФормаТелефон.ОповеститьОбАктивизацииОбъекта(СтруктураОповещения);
		КонецЦикла;
		Линии.Сортировать("ИмяЛинии ВОЗР");
	КонецЕсли;
	Если НЕ (НомY = Неопределено) Тогда
		// выведем на индикаторе текущую загрузку линий
		СтруктураОповещения.МаксимальноеЧислоЛиний   = Y;
		СтруктураОповещения.ТекущаяЗагружаемаяЛииния = НомY+1;
		ФормаТелефон.ОповеститьОбАктивизацииОбъекта(СтруктураОповещения);
		// Если линия не была выбрана, т.е. такой линии может уже не быть в списке контролируемых линий на сервере,
		//  то сбросим значение регистра
		Если УказательНаЛинию = 0 Тогда
			УстановитьПараметрыИспользуемойЛинии();
			сфпСофтФонПроСервер.сфпЗаписатьЗначениеНастройки("ИмяЛинии", СокрЛП(ИмяИспользуемойЛинии));
		КонецЕсли;
		СвояЛиния = Линии.Найти(ИмяИспользуемойЛинии, "ИмяЛинии");
		Если НЕ СвояЛиния = Неопределено Тогда
			ПровайдерИспользуемойЛинии = СвояЛиния.Провайдер;
		КонецЕсли;
		ЗаполнитьСписокЛинийПерехвата();
	Иначе
		Сообщить("Неудачная попытка подключения...");
	КонецЕсли;
КонецПроцедуры

// Функция включает контроль линии
// ПризнакОчищенияЗвонков - признак того, нужно ли очищать звонки
//  при включении линии. По умолчанию удалять.
Функция ВключитьКонтрольЛинии(ПризнакОчищенияЗвонков=Истина) Экспорт
	Результат = Ложь;
	
	// Если статус клиента отличает от клиент готов линии грузить нельзя
	Если НЕ ТекущийСтатусПодключения = 6 Тогда Возврат Результат; КонецЕсли;
	
	Если НЕ (РарусСофтФонКомпонента = Неопределено И УказательНаЛинию = 0 И НЕ ЗначениеЗаполнено(ИмяИспользуемойЛинии)) Тогда
		РарусСофтФонКомпонента.ВключитьКонтрольЛинии(ИмяИспользуемойЛинии);
		// Только если признак очищения звонков имеет истину
		Если ПризнакОчищенияЗвонков Тогда
			// Очищаем звонки
			Звонки.Очистить();
		КонецЕсли;
		
		Результат = Истина;
	
		ФормаТелефон.ВывестиТекущийСтатусПодключения("");
 
	КонецЕсли;
	
	СвояЛиния = Линии.Найти(ИмяИспользуемойЛинии, "ИмяЛинии");
	Если НЕ СвояЛиния = Неопределено Тогда
		ПровайдерИспользуемойЛинии = СвояЛиния.Провайдер;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Функция отключает контроль линии
Функция ОтключитьКонтрольЛинии() Экспорт
	Результат = Ложь;
	
	// Если статус клиента отличает от клиент готов линии грузить нельзя
	Если НЕ ТекущийСтатусПодключения = 6 Тогда Возврат Результат; КонецЕсли;
	
	Если НЕ РарусСофтФонКомпонента = Неопределено Тогда
		РарусСофтФонКомпонента.ОтключитьКонтрольЛинии();
		// Очищаем звонки
		Звонки.Очистить();
		
		Результат = Истина;
 		ФормаТелефон.ВывестиТекущийСтатусПодключения("");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// заполняет список линий перехвата
//
Процедура ЗаполнитьСписокЛинийПерехвата() Экспорт
	//нужно передать номера которые я хочу контролировать, при отключении нужно передать пустые параметры
	ОтборПользователь = Новый Структура("Пользователь");
	ОтборПользователь.Пользователь = сфпСофтФонПроСервер.сфпТекущийПользователь();
	СписокТелефоновДляПерехвата = РегистрыСведений.CRM_ГруппаТелефоновДляПерехвата.Выбрать(ОтборПользователь);
	КоличествоВнТелефоновДляПерехвата=0;
	Пока СписокТелефоновДляПерехвата.Следующий() Цикл
		КоличествоВнТелефоновДляПерехвата = КоличествоВнТелефоновДляПерехвата+1;
	КонецЦикла;
	Если КоличествоВнТелефоновДляПерехвата > 0 Тогда
		Параметры = РарусСофтФонКомпонента.СоздатьПараметры(1,КоличествоВнТелефоновДляПерехвата);
		ПеремЦикла = 0;
		СписокТелефоновДляПерехвата = РегистрыСведений.CRM_ГруппаТелефоновДляПерехвата.Выбрать(ОтборПользователь);
		Пока СписокТелефоновДляПерехвата.Следующий() Цикл
			Линия = Линии.Найти(СписокТелефоновДляПерехвата.НомерВнТелефона,"НомерПривязанныйКЛинии");
			Если Не Линия = Неопределено Тогда 
				ИмяЛинии = Линия.ИмяЛинии;
			КонецЕсли;
			Параметры.SetValue(0,ПеремЦикла,ИмяЛинии);
			ПеремЦикла = ПеремЦикла+1;
		КонецЦикла;
		РарусСофтФонКомпонента.SetIntrceptLineList(Параметры);
	Иначе
		Параметры = РарусСофтФонКомпонента.СоздатьПараметры(0,0);
		РарусСофтФонКомпонента.SetIntrceptLineList(Параметры);
	КонецЕсли;
КонецПроцедуры	

// Процедура изменяет состоянии линии
Процедура ИзменитьСостояниеЛинии(ТекущийУказательНаЛинию, СостояниеЛинии) Экспорт
	// Если указатель на линию не определен, то выходим
	Если ТекущийУказательНаЛинию < 1 Тогда Возврат; КонецЕсли;
	
	НайденнаяЛиния = Линии.Найти(ТекущийУказательНаЛинию, "ИдентификаторЛинии");
	Если НайденнаяЛиния = Неопределено Тогда Возврат; КонецЕсли;
	
	НайденнаяЛиния.СостояниеЛинии = СостояниеЛинии;
КонецПроцедуры

// Процедура изменяет список линий в зависимости от состояния линии
Процедура ИзменитьСписокЛиний(Данные) Экспорт
	// Управляем списком линий, на лету, только если клиент находится в постоянном коннекте
	Если НЕ ТекущийСтатусПодключения = 6 Тогда Возврат; КонецЕсли;
	ПараметрыЛинии = РарусСофтФонКомпонента.ПолучитьОткрытиеЗакрытиеЛинии(Данные);
	// Если линия была закрыта, то необходимо ее удалить из списка линий
	Если ПараметрыЛинии.coType = 0 Тогда
		НайденнаяЛиния = Линии.Найти(Число(ПараметрыЛинии.hLine), "ИдентификаторЛинии");
		Если НЕ НайденнаяЛиния = Неопределено Тогда
			// Была закрыта основная линия, сбросим указатель на линии, и сохраним значение пусто для линии
			//  пусть пользователь теперь сам настроит новую линию
			Если НайденнаяЛиния.ОсновнаяЛиния Тогда
				УстановитьПараметрыИспользуемойЛинии();
				Предупреждение("Внимание!!! На сервере была отключена используемая линия """+НайденнаяЛиния.ИмяЛинии+""""+Символы.ПС+
				"Необходимо открыть окно настроек СофтФона и выбрать другую линию основной.", 60, "Внимание!!!");
			КонецЕсли;
			// Линии могут быть очищены при выключении сервера.
			Попытка
				Линии.Удалить(НайденнаяЛиния);
			Исключение
			КонецПопытки
		КонецЕсли;
	Иначе
		// Заполним визуальную таблицу
		НоваяЛиния = Линии.Добавить();
		НоваяЛиния.ИдентификаторЛинии = Число(ПараметрыЛинии.hLine);
		НоваяЛиния.ИмяЛинии = ПараметрыЛинии.LineName;
		НоваяЛиния.НомерПривязанныйКЛинии = СокрЛП(ПараметрыЛинии.LineDialableAddr);
		НоваяЛиния.СостояниеЛинии = Число(ПараметрыЛинии.LineBusyState);
		НоваяЛиния.УникальныйУказательНаЛинию = Число(ПараметрыЛинии.LineID);
		Если НоваяЛиния.УникальныйУказательНаЛинию = 0 Тогда					
			НоваяЛиния.УникальныйУказательНаЛинию = сфпПолучитьУникальныйУказательНаЛинию(НоваяЛиния.ИмяЛинии);
		КонецЕсли;	
	КонецЕсли;
	Линии.Сортировать("ИмяЛинии ВОЗР");
КонецПроцедуры

// Процедура выполняет подключение сервера
Процедура ПодключитьСервер(Сервер="", ИмяПорта="") Экспорт
	Если НЕ РарусСофтФонКомпонента = Неопределено Тогда
		// Если были переданы значения сервера и порта, по пробуем подключиться к ним.
		Если ЗначениеЗаполнено(Сервер) И ЗначениеЗаполнено(ИмяПорта) Тогда
			ТмпСервер = Сервер;
			ТмпПорт   = ИмяПорта;
		Иначе
			ТмпСервер = ИмяСервера;
			ТмпПорт   = Порт;
		КонецЕсли;
		
		// Начинаем работу, иначе компонента работать не будет
		РарусСофтФонКомпонента.НачатьРаботуСофтфона();
		
		// Получаем имя пользователя
		
		глТекущийПользователь = сфпСофтФонПроСервер.сфпТекущийПользователь();
		//Подключаем SIP
		oUseSIP 	= ?(сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ИспользоватьSIP"),1,0);
		oSIPServer 	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("СерверSIP");
		oSIPLogin 	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ЛогинSIP");
		oSIPPass	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ПарольSIP");
		oSIPProxy	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ПроксиSIP");
		oUseSIPStun = ?(ЗначениеЗаполнено(СокрЛП(сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("STUN"))),1,0);
		oSIPStun	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("STUN");
		oAudioIn	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ВходноеАудиоУстройство");
		oAudioOut	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("ВыходноеАудиоУстройство");
		oAudioRing	= сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("СигнальноеАудиоУстройство");
		
		РарусСофтФонКомпонента.УстановитьНастройкиSIP(oUseSIP, oSIPServer, oSIPLogin, oSIPPass, oSIPProxy, oUseSIPStun, oSIPStun, oAudioIn, oAudioOut, oAudioRing);		

		
		ИмяПользователя = СокрЛП(глТекущийПользователь);
		// Подключаемся сначала с пустой линией. Далее уже получаем список линий
		РарусСофтФонКомпонента.Подключиться(ТмпСервер, ТмпПорт, ИмяПользователя);
	КонецЕсли;
КонецПроцедуры

// Процедура выполняет подключение сервера
Процедура ОтключитьСервер() Экспорт
	Если НЕ РарусСофтФонКомпонента = Неопределено Тогда
		РарусСофтФонКомпонента.Отключиться();
	КонецЕсли;
КонецПроцедуры

// Инициализируем компонету, инициализация происходит в обработке СтартСистемы
Функция Инициализация() Экспорт
	Результат = Ложь;
	СоздаватьНовоеСобытие = Истина;
	// Установим признак первого запуска, используется для
	//  проверки пропущенных вызовов, которые были зарегистрированы
	//  пока система не работала.
	ПризнакПервогоЗапуска = Истина;
	// Если компонента загружена, то не нужно грузить заново
	//  тем более компоненту можно загрузить только один раз.
	// Просто вернем, что инициализация была выполнена.
	Если РарусСофтФонКомпонента = Неопределено Тогда
		КаталогОборудования = уоПолучитьПутьККаталогуОборудования();
		ОбработкаЗащиты = Обработки.Защита.Создать();
		Результат = ОбработкаЗащиты.ЗагрузитьКомпонентуСофтФона(КаталогОборудования);
		// Заполним КЭШ таблицу содержащую телефоны
		//  Для ФизЛиц и Пользователей. Т.е. для сотрудников организации
		//  далее эта таблица будет передана в форму Списка телефонов
		Если Результат Тогда
			Состояние("Получение таблицы звонков");
			ТаблицаКЭШТелефонныхНомеров = CRMЗаполниитьКЭШТаблицуТелефонныхНомеров();
		КонецЕсли;
	Иначе
		Результат = Истина;
	КонецЕсли;
	глСписокДоступныхЛинийДляПерехвата = Новый ТаблицаЗначений;
	глСписокДоступныхЛинийДляПерехвата.Колонки.Добавить("Handle");
	глСписокДоступныхЛинийДляПерехвата.Колонки.Добавить("Звонок");
	глСписокДоступныхЛинийДляПерехвата.Колонки.Добавить("ИмяЛинии");
	
	Возврат Результат;
КонецФункции

// Функция определяет занят или не занят телефонный номер
//  Рассматриваются только внутренние номера, при условии,
//  что они мониторятся пользователем. Если номер внешний или
//  такая внутренняя линия не поддерживается то возвращается 
//  состояние НеПоддерживается - 0. 
// Если номер внутренний и линия контролируется возвращает
// состояние Занят - 2 и Свободен - 1  в зависимости от состояния, конкретной линии
//  СтрокаТелефонногоНомера, должна содержать поля 
//  Поле3 - номер городского телефона, Поле4 - номер внутреннего телефона
Функция ОпределитьСостояниеТелефона(СтрокаТелефонногоНомера) Экспорт
	// Определим имя картинки, т.е. проверим таблицу отслеживания звонков,
	//  если в ней есть текущий номер телефона, то следовательно он занят
	//  если отсутствует то свободен. Только для внутренних номеров, для 
	//  внешних будем использовать серенькую картинку телефона.
	Результат = 0;
	
	// Определяем внутренний ли телефон
	Если ЗначениеЗаполнено(СтрокаТелефонногоНомера.Поле4) И НЕ ЗначениеЗаполнено(СтрокаТелефонногоНомера.Поле3) Тогда
		Номер = CRMУбратьИзНомераТелефонаВсеПрефиксы(СокрЛП(СтрокаТелефонногоНомера.Поле4));
		// Определим есть ли такой номер в поддерживаемых линиях
		НайденнаяЛиния = Линии.Найти(Номер, "НомерПривязанныйКЛинии");
		Если НЕ НайденнаяЛиния = Неопределено Тогда
			Результат = НайденнаяЛиния.СостояниеЛинии;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Установить актуальное состояние у телефонных номеров
//  вызывается перед открытие формы объектов, у которых есть контактная информация
Процедура УстановитьАктуальноеСостояниеКИ(НаборКонтактнойИнформации) Экспорт
	// Для телефонных номеров если заполнено Поле5, очищаем его.
	// Используется для индикации занят телефонный номер или свободен
	// Хранить его не нужно просто удалим
	Для каждого ЗаписьРегистра Из НаборКонтактнойИнформации Цикл
		Если ЗаписьРегистра.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон И ЗначениеЗаполнено(ЗаписьРегистра.Представление) Тогда
			
			// Если СофтФон не используется следовательно всегда очищаем поле для того, 
			//  что бы картинка показывалась стандартно
			Если CRMРаботаССофтФономРазрешена(Ложь, глИспользоватьСофтФонПрофТекущийПользователь, сфпСофтФонПроСервер.сфпТекущийПользователь()) Тогда
				
				// Определим состояние телефонного номера
				Состояние = ОпределитьСостояниеТелефона(ЗаписьРегистра);
				
				Если Состояние = 2 Тогда ЗаписьРегистра.Поле5 = "13";
				ИначеЕсли Состояние = 1 Тогда ЗаписьРегистра.Поле5 = "12";
				Иначе ЗаписьРегистра.Поле5 = "";
				КонецЕсли;
			Иначе
				ЗаписьРегистра.Поле5 = "";
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры

// Установить актуальное состояние у телефонных номеров
//  вызывается в обработчике оповещения, у которых есть контактная информация
Процедура ИзменитьАктуальноеСостояниеКИ(НаборКонтактнойИнформации, ИмяСобытия) Экспорт
	// Обработка пары событий LineLock, LineUnLock ИЛИ пары событий LineClose, 
//LineOpen Или пары событий Disconnect (0), Connect (6)
	//  Или по событию ПредставлениеПриИзменении
	Если ИмяСобытия = "LineCloseOpen" Или 
		 ИмяСобытия = "LineLock" Или
		 ИмяСобытия = "LineUnLock" Или
		 ИмяСобытия = "LoginStateChange" Или
		 ИмяСобытия = "ПредставлениеПриИзменении" Тогда
		Для каждого ЗаписьРегистра Из НаборКонтактнойИнформации Цикл
			Если ЗаписьРегистра.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон И ЗначениеЗаполнено(ЗаписьРегистра.Представление) Тогда
				
				// Если СофтФон не используется следовательно всегда очищаем поле для того, 
				//  что бы картинка показывалась стандартно
				Если CRMРаботаССофтФономРазрешена(Ложь, глИспользоватьСофтФонПрофТекущийПользователь, сфпСофтФонПроСервер.сфпТекущийПользователь()) Тогда
					
					// Определим состояние телефонного номера
					Состояние = ОпределитьСостояниеТелефона(ЗаписьРегистра);
					
					Если Состояние = 2 Тогда ЗаписьРегистра.Поле5 = "13";
					ИначеЕсли Состояние = 1 Тогда ЗаписьРегистра.Поле5 = "12";
					Иначе ЗаписьРегистра.Поле5 = "";
					КонецЕсли;
				Иначе
					ЗаписьРегистра.Поле5 = "";
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Процедура устанавливаем кэшируемым переменным значения
//  настроек и констант
Процедура УстановитьЗначенияПоУмолчанию() Экспорт
	// Подключение к серверу
	ИмяСервера  = Константы.CRM_ИмяСервера.Получить();
	Порт 		= Константы.CRM_Порт.Получить();
КонецПроцедуры

// Процедура устанавливает параметры используемой линии из переданной строки, или очищает их
Процедура УстановитьПараметрыИспользуемойЛинии(СтрокаЛинии = Неопределено) Экспорт
	// Отключение от линии
	Если СтрокаЛинии = Неопределено Тогда
		// Удалим таблицу на сервере, а затем сбросим уникальное значение 
		//  линии
		
		УказательНаЛинию 		   = 0;
		УникальныйУказательНаЛинию = 0;
		ИмяИспользуемойЛинии       = "";
	Иначе
		УказательНаЛинию 		   = СтрокаЛинии.ИдентификаторЛинии;
		УникальныйУказательНаЛинию = СтрокаЛинии.УникальныйУказательНаЛинию;
		ИмяИспользуемойЛинии       = СокрЛП(СтрокаЛинии.ИмяЛинии);
		
	КонецЕсли;
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// функция по проверке доступности линии пользователю по номеру телефона
Функция ДоступностьНомераДляКонтроля(НомерТелефона)Экспорт
	ОтборПоПользователю = Новый Структура("Пользователь");
	ОтборПоПользователю.Пользователь = сфпСофтФонПроСервер.сфпТекущийПользователь();
	СписокДоступныхНомеров = РегистрыСведений.CRM_СписокДоступныхНомеровПользователя.Выбрать(ОтборПоПользователю);
	КоличествоРазрешенныхНомеров = 0;
	ЕстьРазрешение = Ложь;
	Пока СписокДоступныхНомеров.Следующий() Цикл
		КоличествоРазрешенныхНомеров = КоличествоРазрешенныхНомеров+1;
		Если СписокДоступныхНомеров.НомерТелефона = НомерТелефона Тогда //есть разрешение
			ЕстьРазрешение = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ЕстьРазрешение или КоличествоРазрешенныхНомеров=0 Тогда
		Возврат Истина
	Иначе
		Возврат Ложь
	КонецЕсли
КонецФункции

// Процедура используется для отображения истории связанных звонков
//  из документа Событие
Процедура ПоказатьИсториюРазговора(GUIDЗвонка) Экспорт

	// Выполним проверку GUID Звонка
	//  Если он равен пустому, а именно {00000000-0000-0000-0000-000000000000}
	Если GUIDЗвонка = "{00000000-0000-0000-0000-000000000000}" ИЛИ НЕ ЗначениеЗаполнено(GUIDЗвонка) Тогда
		Предупреждение("Данный звонок не был связан с другими. Просмотреть историю невозможно.",30,"Внимание!!!");
		Возврат;
	КонецЕсли;
		
	Отчет = Отчеты.CRM_ИсторияРазговора.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
		
	Форма = Отчет.ПолучитьОсновнуюФорму();
		
	ПостроительОтчета = Отчет.ОбщийОтчет.ПостроительОтчета;
		
	Отбор = ПостроительОтчета.Отбор.Добавить("Событие.CRM_GUIDЗвонка");
	Отбор.Установить(GUIDЗвонка);
		
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Отчет.СформироватьОтчетУпрощенный(Форма.ЭлементыФормы.ДокументРезультат);
		
	Форма.Открыть();
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
//Процедуры по работе с баннерами есть переменная для хранения информации об активных баннерах "глСписокЗвонковБанеров"

/////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// Сохраняем значения настроек в экспортные переменные.
УстановитьЗначенияПоУмолчанию();

// Установим текущий статус подключения в начальное состояние
ТекущийСтатусПодключения = 0;

// Установим по умолчанию идентификатор перевода
ИдентификаторПеревода = "";

// Установим по умолчанию значения подключения к SQL серверу
SQLПолучитьПараметры();

// Сбросим параметры используемой линии
УстановитьПараметрыИспользуемойЛинии();

// Определяем структуру содержащую набор состояний возвращаемых станцией
ВидыСобытийСтанции = Новый Структура();
ВидыСобытийСтанции.Вставить("CONNECTED", 			"Соединен");
ВидыСобытийСтанции.Вставить("OFFERING", 			"Звонок");
ВидыСобытийСтанции.Вставить("IDLE", 				"Пусто");
ВидыСобытийСтанции.Вставить("ONHOLD",				"Удержание");
ВидыСобытийСтанции.Вставить("ONHOLDPENDTRANSFER",	"Удержание при переводе");
ВидыСобытийСтанции.Вставить("ONHOLDPENDCONF",		"Удержание при переводе на конференцию");
ВидыСобытийСтанции.Вставить("DISCONNECTED", 		"Отключение");
ВидыСобытийСтанции.Вставить("RINGBACK", 			"Сигнал вызова");
ВидыСобытийСтанции.Вставить("BUSY", 				"Занято");
ВидыСобытийСтанции.Вставить("UNKNOWN", 				"Неизвестно");
ВидыСобытийСтанции.Вставить("PROCEEDING", 			"Идет переключение");
ВидыСобытийСтанции.Вставить("DIALTONE", 			"Гудок");
ВидыСобытийСтанции.Вставить("DIALING", 				"Набор номера");
ВидыСобытийСтанции.Вставить("ACCEPTED", 			"Доступно");
ВидыСобытийСтанции.Вставить("SPECIALINFO", 			"Специальная информация");
ВидыСобытийСтанции.Вставить("CONFERENCED", 			"Конференция");

// Определяем структуру входящего или исходящего звонка
ТипВходящийИсходящийЗвонок = Новый Структура();
ТипВходящийИсходящийЗвонок.Вставить("Входящий",		0);
ТипВходящийИсходящийЗвонок.Вставить("Исходящий",	1);

// Определяем структуру внутреннего или внешнего звонка
ТипВнутреннийВнешнийЗвонок = Новый Структура();
ТипВнутреннийВнешнийЗвонок.Вставить("Внешний",	    0);
ТипВнутреннийВнешнийЗвонок.Вставить("Внутренний",	1);

ВидыВходящихИсходящихСобытий = Новый Структура();
ВидыВходящихИсходящихСобытий.Вставить("Неопределено", 			0);
ВидыВходящихИсходящихСобытий.Вставить("ВнутреннийИсходящий", 	1);
ВидыВходящихИсходящихСобытий.Вставить("ВнутреннийВходящий", 	2);
ВидыВходящихИсходящихСобытий.Вставить("ВнешнийВходящий", 		3);
ВидыВходящихИсходящихСобытий.Вставить("Недоступен", 			4);
ВидыВходящихИсходящихСобытий.Вставить("Конференция", 			5);
ВидыВходящихИсходящихСобытий.Вставить("ВнешнийИсходящий",		6);

// Определяем структуру содержащую описание набора входящих / исходящих состояний станции
ВидыВходящихИсходящихСобытийОписание = Новый Структура();
ВидыВходящихИсходящихСобытийОписание.Вставить("Неопределено", 			"Неопределено");
ВидыВходящихИсходящихСобытийОписание.Вставить("ВнутреннийИсходящий", 	"Внутренний исходящий");
ВидыВходящихИсходящихСобытийОписание.Вставить("ВнутреннийВходящий", 	"Внутренний входящий");
ВидыВходящихИсходящихСобытийОписание.Вставить("ВнешнийВходящий", 		"Внешний входящий");
ВидыВходящихИсходящихСобытийОписание.Вставить("Недоступен", 			"Недоступен");
ВидыВходящихИсходящихСобытийОписание.Вставить("Конференция", 			"Конференция");
ВидыВходящихИсходящихСобытийОписание.Вставить("ВнешнийИсходящий",		"Внешний исходящий");

// Определяем типы используемого оборудования
ТипыПоддерживаемогоОборудования = Новый Структура();
ТипыПоддерживаемогоОборудования.Вставить("Unknown", 		0);      // тип станции не известен 
ТипыПоддерживаемогоОборудования.Вставить("PanasonicTD", 	1); 	 // тип станции панасоник TD
ТипыПоддерживаемогоОборудования.Вставить("PanasonicTDA",	2);      // тип станции панасоник TDA
ТипыПоддерживаемогоОборудования.Вставить("CiscoCME", 		3);	     // тип станции CiscoCME
ТипыПоддерживаемогоОборудования.Вставить("Avaya", 			4);	     // тип станции Avaya
ТипыПоддерживаемогоОборудования.Вставить("CiscoCCM",		5);	     // тип станции CiscoCCM
ТипыПоддерживаемогоОборудования.Вставить("LG",				6);	     // тип станции LG
ТипыПоддерживаемогоОборудования.Вставить("Samsung",			7);	     // тип станции Samsung
ТипыПоддерживаемогоОборудования.Вставить("Alcatel",			8);	     // тип станции Alcatel
ТипыПоддерживаемогоОборудования.Вставить("Ericsson",		9);		 // тип станции Ericsson
ТипыПоддерживаемогоОборудования.Вставить("Infra",		   10);	 	// тип станции Infra
ТипыПоддерживаемогоОборудования.Вставить("Asterisk",	   11);	 	// тип станции Infra

// Определяет кэш таблицу хранящую результат запроса к серверу SQL, содержащему список телефонов
ШаблонТаблицыРезультатаSQL = Новый ТаблицаЗначений();
МассивТипов = Новый Массив(); МассивТипов.Добавить(Тип("Число"));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("ИдентификаторЗвонка", 		Новый ОписаниеТипов(МассивТипов));

МассивТипов = Новый Массив(); МассивТипов.Добавить(Тип("Строка"));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("ТелефонныйНомер", 			Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("ИмяТелефона", 				Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("GUIDЗвонка", 				Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("Поле1", 					Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("Поле2", 					Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("Поле3", 					Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("Поле4", 					Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("Представление", 			Новый ОписаниеТипов(МассивТипов));

ТипыКИ = Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения.Объект.Тип.Типы();
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("Объект", 					Новый ОписаниеТипов(ТипыКИ));

МассивТипов = Новый Массив(); МассивТипов.Добавить(Тип("Дата"));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("ВремяНачала", 				Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("ВремяОкончания", 			Новый ОписаниеТипов(МассивТипов));

МассивТипов = Новый Массив(); МассивТипов.Добавить(Тип("Булево"));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("ВходящийЗвонок", 			Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("ВнутреннийЗвонок", 		Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("НепринятыйЗвонок", 		Новый ОписаниеТипов(МассивТипов));
ШаблонТаблицыРезультатаSQL.Колонки.Добавить("НепросмотренныйЗвонок",    Новый ОписаниеТипов(МассивТипов));

ДанныеБаннера = Новый ТаблицаЗначений;
ДанныеБаннера.Колонки.Добавить("ИДБаннера");
ДанныеБаннера.Колонки.Добавить("ДанныеЗвонка");

СписокОбработанныхЗвонковБаннером = Новый СписокЗначений;
#КонецЕсли
#Если Клиент Тогда // Блок существует исключительно на стороне клиента
	
Перем Рарус_Компонента Экспорт; // Локальная ссылка на внешнюю компоненту типового решения

//  Функция создает выходную таблицу по данным из терминала
// Параметры:
// 		ТабДок - табличный документ, в котором все и "рисуется"
//		ТаблицаДанных - таблица значений с данными из ТСД
//		Документ	  - (число) номер документа из ТСД, содержимое которого требуется вывести
//      ОписаниеОшибки - текст с описанием возникшей ошибки
// Возвращаемое значение:
// 		Ложь - если при работе возникла ошибка и Истина если все Ок
Функция ПостроитьОтчет(ТабДок,ТаблицаДанных,Документ,ОписаниеОшибки="") Экспорт
	
	Результат=Ложь;
	
	Попытка
		
		Если ТаблицаДанных.Количество() = 0 Тогда
			ОписаниеОшибки="Передана пустая таблица данных";
			Возврат(Результат);
		КонецЕсли;
			
		СписокТоваров=Новый СписокЗначений;
		МассивТоваров=Новый Массив;
		МассивТоваров=ТаблицаДанных.ВыгрузитьКолонку("Номенклатура");
		СписокТоваров.ЗагрузитьЗначения(МассивТоваров);
		ТипЦен=Оборудование.ОсновнойОтдел.ТипЦенРозничнойТорговли;
		
		Запрос = Новый Запрос;
		
		РабочийТипЦен = ТипЦен;
		ТекстЦена = "ЦеныСрезПоследних.Цена";
		// Если расчетная цена получим базовую цену
		Счетчик = 1;
		ТекстСоединенийПроцентыСкидкиНаценки = "";
		Пока РабочийТипЦен.Рассчитывается Цикл
			Если РабочийТипЦен.ПроцентыСкидкиНаценки.Количество()>0 Тогда
				ИмяТаблицы = "ПроцентыСкидкиНаценки" + Строка(Счетчик);
				ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК " + ИмяТаблицы + "
				|ПО
				|	" + ИмяТаблицы + ".Ссылка = &ТипЦены" + Строка(Счетчик) + " И 
				|	ЦеныСрезПоследних.Номенклатура.ТипНоменклатуры = " + ИмяТаблицы + ".ТипНоменклатуры";
				
				ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
				
				Запрос.УстановитьПараметр("ТипЦены" + Строка(Счетчик), РабочийТипЦен);
				
				Счетчик = Счетчик + 1;
			ИначеЕсли РабочийТипЦен.ПроцентСкидкиНаценки<>0 Тогда
				ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
			КонецЕсли;
			РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
		КонецЦикла;
		
		ТаблицаИерархииПодразделений = Новый ТаблицаЗначений;
		ТаблицаИерархииПодразделений.Колонки.Добавить("Подразделение",Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
		ТаблицаИерархииПодразделений.Колонки.Добавить("Уровень",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10)));
		
		ТекущееПодразделение = ПараметрыСеанса.ПодразделениеКомпании;
		Уровень = 0;
		Пока ЗначениеЗаполнено(ТекущееПодразделение) Цикл
			СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
			СтрокаПодразделения.Подразделение = ТекущееПодразделение;
			СтрокаПодразделения.Уровень = Уровень;
			// Получаем родителя подразделения
			ТекущееПодразделение = ТекущееПодразделение.Родитель;
			Уровень = Уровень + 1;
		КонецЦикла;
		СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
		СтрокаПодразделения.Подразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
		СтрокаПодразделения.Уровень = Уровень;
		
		ПоискПоХарактеристикам = (РабочийТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике);
		ПоискПоЕдиницам        = (РабочийТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения);
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаПодразделений.Подразделение КАК Подразделение,
		|	ТаблицаПодразделений.Уровень КАК Уровень
		|ПОМЕСТИТЬ
		|	ТаблицаПодразделений
		|ИЗ
		|	&ТаблицаИерархииПодразделений КАК ТаблицаПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ЦеныСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	"+ТекстЦена+" КАК Цена,
		|	ЕСТЬNULL(ПодразделенияКомпании.Уровень, 0 ) КАК Уровень
		|ПОМЕСТИТЬ
		|	ТаблицаЦен
		|ИЗ
		|	РегистрСведений.Цены.СрезПоследних(,
		|		Контрагент = &ПустойКонтрагент И 
		|		ТипЦен = &ТипЦен И 
		|		Номенклатура В (&СписокТоваров)" + ?(ПоискПоХарактеристикам, "", " И 
		|		ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)") + ?(ПоискПоЕдиницам, " И 
		|		ВЫБОР
		|			КОГДА ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				(НЕ Номенклатура.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров) И 
		|				ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|		КОНЕЦ", "")+") КАК ЦеныСрезПоследних" + ТекстСоединенийПроцентыСкидкиНаценки + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаПодразделений КАК ПодразделенияКомпании
		|ПО
		|	ЦеныСрезПоследних.ПодразделениеКомпании = ПодразделенияКомпании.Подразделение
		|ГДЕ
		|	ЦеныСрезПоследних.Цена > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЦен.Номенклатура,
		|	ТаблицаЦен.ХарактеристикаНоменклатуры,
		|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
		|ПОМЕСТИТЬ
		|	СрезПоследних
		|ИЗ
		|	ТаблицаЦен КАК ТаблицаЦен
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЦен.Номенклатура,
		|	ТаблицаЦен.ХарактеристикаНоменклатуры,
		|	ТаблицаЦен.ЕдиницаИзмерения
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпрТовары.Ссылка КАК Номенклатура,
		|	СпрТовары.Представление КАК НоменклатураПредставление,
		|	СпрТовары.БазоваяЕдиницаИзмерения КАК БазоваяЕдИзм,
		|	ЕСТЬNULL(СрезПоследних.ХарактеристикаНоменклатуры, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаНоменклатуры,
		|	ЕСТЬNULL(СрезПоследних.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ТаблицаЦен.Цена, 0) КАК Цена
		|ИЗ
		|	Справочник.Номенклатура КАК СпрТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	СрезПоследних КАК СрезПоследних
		|ПО
		|	СпрТовары.Ссылка = СрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаЦен КАК ТаблицаЦен
		|ПО
		|	СрезПоследних.Номенклатура               = ТаблицаЦен.Номенклатура И 
		|	СрезПоследних.ХарактеристикаНоменклатуры = ТаблицаЦен.ХарактеристикаНоменклатуры И 
		|	СрезПоследних.ЕдиницаИзмерения           = ТаблицаЦен.ЕдиницаИзмерения И 
		|	СрезПоследних.Уровень                    = ТаблицаЦен.Уровень
		|ГДЕ
		|	СпрТовары.Ссылка В(&СписокТоваров)";
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ТаблицаИерархииПодразделений", ТаблицаИерархииПодразделений);
		Запрос.УстановитьПараметр("СписокТоваров", СписокТоваров);
		Запрос.УстановитьПараметр("ТипЦен", РабочийТипЦен);
		Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
		ТаблицаЦен = Запрос.Выполнить().Выгрузить();
		ТаблицаЦен.Индексы.Добавить("Номенклатура");
		Если ПоискПоХарактеристикам Тогда
			ТаблицаЦен.Индексы.Добавить("ХарактеристикаНоменклатуры");
		КонецЕсли;
		
		// получаем объекты для построения табличного документа
		Макет=ПолучитьМакет("ДанныеТерминала");
		ОбластьШапки=Макет.ПолучитьОбласть("Шапка");
		ОбластьСтроки=Макет.ПолучитьОбласть("Строка");
		ОбластьСтрокиСХарактеристикой=Макет.ПолучитьОбласть("СтрокаСХарактеристикой");
		ОбластьОшибки=Макет.ПолучитьОбласть("Ошибка");
		ОбластьПодвал=Макет.ПолучитьОбласть("Подвал");
		СуммаДокумента=0;
		// выведем шапку отчета
		ОбластьШапки.Параметры.Примечание = СокрЛП(Оборудование)+"; " +
											?(Документ = -1,"Все документы", "Документ № " + Документ) +
											"; Тип цен <" + ТипЦен + ">";
		ТабДок.Вывести(ОбластьШапки);
		ТабДок.ФиксацияСверху = ТабДок.ВысотаТаблицы; 
		СчетчикВыведенныхСтрок=0;
		// выведем табличную часть
		ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		Для Каждого СтрокаТаблицы ИЗ ТаблицаДанных Цикл
			
			Если обЗначениеНеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
				ОбластьДетальная = ОбластьОшибки;
				ОбластьДетальная.Параметры.Заполнить(СтрокаТаблицы);
				ОбластьДетальная.Параметры.НоменклатураПредставление="Ошибка: товар со штрих-кодом  <"+СтрокаТаблицы.ШтрихКод+"> в БД не заполнен";
				СчетчикВыведенныхСтрок=СчетчикВыведенныхСтрок+1;
				// заполним вручную параметры которые не заполнились автоматически
				ОбластьДетальная.Параметры.Поз = СчетчикВыведенныхСтрок;
				ТабДок.Вывести(ОбластьДетальная);
				Продолжить;
			КонецЕсли;
			
			СтрокаПоиска = Неопределено;
			Если ПоискПоХарактеристикам Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.ХарактеристикаНоменклатуры) Тогда
					СтрокиПоиска = ТаблицаЦен.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", СтрокаТаблицы.Номенклатура, СтрокаТаблицы.ХарактеристикаНоменклатуры));
					Если СтрокиПоиска.Количество() > 0 Тогда
						СтрокаПоиска = СтрокиПоиска[0];
					КонецЕсли;
				КонецЕсли;
				// Если нет цен для характеристики поищим общую
				Если СтрокаПоиска = Неопределено Тогда
					СтрокиПоиска = ТаблицаЦен.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", СтрокаТаблицы.Номенклатура, ПустаяХарактеристика));
					Если СтрокиПоиска.Количество() > 0 Тогда
						СтрокаПоиска = СтрокиПоиска[0];
					КонецЕсли;
				КонецЕсли;
			Иначе
				СтрокаПоиска = ТаблицаЦен.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
			КонецЕсли;
			
			Если СтрокаПоиска = Неопределено Тогда
				ОбластьДетальная=ОбластьОшибки;
				ОбластьДетальная.Параметры.Заполнить(СтрокаТаблицы);
				ОбластьДетальная.Параметры.НоменклатураПредставление="Ошибка: товар со штрих-кодом  <"+СтрокаТаблицы.ШтрихКод+"> в БД не найден";
			Иначе
				Если ТолькоОшибки Тогда
					Продолжить;
				КонецЕсли;
				
				Если обЗначениеНеЗаполнено(СтрокаТаблицы.ХарактеристикаНоменклатуры) Тогда
					ОбластьДетальная=ОбластьСтроки;
				Иначе
					ОбластьДетальная=ОбластьСтрокиСХарактеристикой;
				КонецЕсли;
				
				ОбластьДетальная.Параметры.Заполнить(СтрокаТаблицы);
				ОбластьДетальная.Параметры.Заполнить(СтрокаПоиска);
				// проверим единицу измерения если не базовая то цену нужно будет пересчитать
				
				Цена = 0;
				КоэффициентЦены = 1;
				ЕдиницаИзмерения = Неопределено;
				Коэффициент      = 1;
				Если НЕ СтрокаПоиска = Неопределено Тогда
					Цена = СтрокаПоиска.Цена;
					Если ПоискПоЕдиницам И ЗначениеЗаполнено(СтрокаПоиска.ЕдиницаИзмерения) Тогда
						КоэффициентЦены = СтрокаПоиска.ЕдиницаИзмерения.Коэффициент;
					КонецЕсли;
					
					ЕдиницаИзмерения = СтрокаПоиска.БазоваяЕдИзм;
					Коэффициент      = 1;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаТаблицы.ЕдиницаИзмерения) Тогда
					ЕдиницаИзмерения = СтрокаТаблицы.ЕдиницаИзмерения;
					Коэффициент      = СтрокаТаблицы.ЕдиницаИзмерения.Коэффициент;
				КонецЕсли;
					
				ОбластьДетальная.Параметры.ЕдиницаИзмерения = ЕдиницаИзмерения;
				ОбластьДетальная.Параметры.Цена = ?(КоэффициентЦены = Коэффициент, Цена, Цена/КоэффициентЦены*Коэффициент);
				// подсчитаем сумму
				СуммаСтроки = Число(?(обЗначениеНеЗаполнено(ОбластьДетальная.Параметры.Цена),0,ОбластьДетальная.Параметры.Цена)) * Число(?(ОбластьДетальная.Параметры.Количество=NULL,0,ОбластьДетальная.Параметры.Количество));
				СуммаДокумента = СуммаДокумента + СуммаСтроки;
				ОбластьДетальная.Параметры.Сумма = СуммаСтроки;
				
			КонецЕсли;
			
			СчетчикВыведенныхСтрок=СчетчикВыведенныхСтрок+1;
			// заполним вручную параметры которые не заполнились автоматически
			ОбластьДетальная.Параметры.Поз = СчетчикВыведенныхСтрок;
			ТабДок.Вывести(ОбластьДетальная);
		
		КонецЦикла;
		// выведем итоговую секцию
		Если ТолькоОшибки Тогда
			Если СчетчикВыведенныхСтрок>0 Тогда
				ТекстПодвала="Позиций в документе: " + ТаблицаДанных.Количество() + "; Из них не найдено в БД: " + СчетчикВыведенныхСтрок;
			Иначе
				ТекстПодвала="Ошибочных позиций в документе не обнаружено.";
			КонецЕсли;
		Иначе
			ТекстПодвала="Всего товаров на сумму: "+Формат(СуммаДокумента,"ЧЦ=15; ЧДЦ=2")+" "+обВалютаТипаЦены(Неопределено,ТипЦен,Ложь);
		КонецЕсли;
		ОбластьПодвал.Параметры.ТекстИтого=ТекстПодвала;
		ТабДок.Вывести(ОбластьПодвал);
	    //
		Результат=Истина;
	Исключение
		ОписаниеОшибки="Внутреннее исключение: "+ОписаниеОшибки();	
	КонецПопытки;
	//
	Возврат(Результат);
	
КонецФункции // ПостроитьОтчет()

Рарус_Компонента = глРарус_Компонента;

#КонецЕсли
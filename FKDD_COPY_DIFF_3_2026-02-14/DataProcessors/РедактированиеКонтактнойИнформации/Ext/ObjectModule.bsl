Перем мРедактируемаяЗапись Экспорт; // редактируемая запись
Перем мФормаВладелец;               // форма владелец
Перем мНаборЗаписейКонтактнойИнформации Экспорт; // Набор записей контактной информации

#Если Клиент Тогда

// Функция обрабатывает событие закрытия формы редактирования записи.
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Булево - Закрывать или не закрывать форму
//
Функция ЗакрыватьФормуРедактирования() Экспорт

	ОтветНаВопрос = Вопрос("Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
		Возврат Истина;
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		Возврат НЕ Записать();
	Иначе
		Возврат Ложь;
	КонецЕсли; 

КонецФункции // ЗакрыватьФормуРедактирования()

// Процедура открывает определенную форму контактной информации.
//
Процедура ЗапуститьФорму(ФормаВладелец = Неопределено) Экспорт
	Если Тип = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
		Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗаписиАдреса", ФормаВладелец, "ФормаЗаписиАдреса");			
	ИначеЕсли Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда	
		Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗаписиАдресаЭП", ФормаВладелец, "ФормаЗаписиАдресаЭП");			
	ИначеЕсли Тип = Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
		Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗаписиВебСтраницы", ФормаВладелец, "ФормаЗаписиВебСтраницы");			
	ИначеЕсли Тип = Перечисления.ТипыКонтактнойИнформации.НомерICQ Тогда	
		Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗаписиНомераICQ", ФормаВладелец, "ФормаЗаписиНомераICQ");			
	ИначеЕсли Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда	
		Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗаписиТелефона", ФормаВладелец, "ФормаЗаписиТелефона");			
	Иначе	
		Форма = ЭтотОбъект.ПолучитьФорму("ФормаЗаписиПрочее", ФормаВладелец, "ФормаЗаписиПрочее");		
	КонецЕсли;
	Если Форма.Открыта() Тогда
		Форма.Активизировать();
	Иначе
		Форма.ПоложениеОкна = ВариантПоложенияОкна.Центрировать;
		Форма.Открыть();	
	КонецЕсли; 
КонецПроцедуры // ЗапуститьФорму()

// Процедура инициирует редактирование записи.
//
// Параметры:
//  СтруктураЗаписи - Структура, НаборЗаписейРегистраСведений, МенеджерЗаписи, ЗаписьНабораЗаписей
//                    Данные для редактирования
//  Записывать      - Булево, Записывать данные в ИД при окончании редактирования
//  ФормаВладелец   - Форма, откуда вызывается редактирование
//  СтруктураПредустановленныхЗначений - Структура, данные для заполнения формы по умолчанию,
//                    могут отличаться от редактируемых, например при копировании
//
Процедура РедактироватьЗапись(СтруктураЗаписи = Неопределено, Записывать = Ложь, ФормаВладелец = Неопределено, СтруктураПредустановленныхЗначений = Неопределено) Экспорт

	ЗаписыватьВРегистр = Записывать;
	мФормаВладелец = ФормаВладелец;
	
	Если ТипЗнч(СтруктураЗаписи) = Тип("РегистрСведенийЗапись.КонтактнаяИнформация")
	 ИЛИ ТипЗнч(СтруктураЗаписи) = Тип("РегистрСведенийМенеджерЗаписи.КонтактнаяИнформация") Тогда
		мРедактируемаяЗапись = СтруктураЗаписи;
		ЛокальнаяСтруктураЗаписи = киПолучитьСтруктуруЗаписиРегистра(СтруктураЗаписи);
	ИначеЕсли ТипЗнч(СтруктураЗаписи) = Тип("РегистрСведенийНаборЗаписей.КонтактнаяИнформация") Тогда
		// Значит вводим нового
		//Если СтруктураЗаписи.Отбор.Объект.Использование = Истина И СтруктураЗаписи.Отбор.Объект.Значение <> Неопределено Тогда
		//	ЛокальнаяСтруктураЗаписи = Новый Структура("Объект", СтруктураЗаписи.Отбор.Объект.Значение);
		//	мРедактируемаяЗапись = СтруктураЗаписи;
		//ИначеЕсли ТипЗнч(СтруктураПредустановленныхЗначений) = Тип("Структура") Тогда
		//	мРедактируемаяЗапись = СтруктураЗаписи;
		//КонецЕсли; 
		Если ТипЗнч(СтруктураПредустановленныхЗначений) = Тип("Структура") Тогда
			мРедактируемаяЗапись = СтруктураЗаписи;
		Иначе
			ЛокальнаяСтруктураЗаписи = Новый Структура("Объект", СтруктураЗаписи.Отбор.Объект.Значение);
			мРедактируемаяЗапись = СтруктураЗаписи;
		КонецЕсли; 
	ИначеЕсли ТипЗнч(СтруктураЗаписи) = Тип("Структура") Тогда
		ЛокальнаяСтруктураЗаписи = СтруктураЗаписи;
	КонецЕсли;
	
	Если СтруктураПредустановленныхЗначений <> Неопределено Тогда
		ЛокальнаяСтруктураЗаписи = СтруктураПредустановленныхЗначений;
	КонецЕсли; 
	
	Если ТипЗнч(ЛокальнаяСтруктураЗаписи) = Тип("Структура") Тогда
		Для Каждого ЭлементСтруктуры Из ЛокальнаяСтруктураЗаписи Цикл
			ЭтотОбъект[ЭлементСтруктуры.Ключ] = ЭлементСтруктуры.Значение;
		КонецЦикла; 
	КонецЕсли;

КонецПроцедуры // РедактироватьЗапись()

// Добавляет вид контактной информации
//
// Параметры:
// Тип - ПеречислениеСсылка ТипыКонтактнойИнформации.
//
Процедура ДобавитьВидКонтактнойИнформации(Тип = Неопределено) Экспорт
	НовыйВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.СоздатьЭлемент();
	ФормаЭлемента = НовыйВидКонтактнойИнформации.ПолучитьФорму("ФормаЭлемента");	
	Если НЕ Тип = Неопределено Тогда
		НовыйВидКонтактнойИнформации.Тип = Тип;
		НовыйВидКонтактнойИнформации.Наименование = Строка(Тип);
		ФормаЭлемента.ЭлементыФормы.Тип.Доступность = Ложь;
	КонецЕсли;	
	ФормаЭлемента.Открыть();
КонецПроцедуры // ДобавитьВидКонтактнойИнформации()

// Добавляет вид контактной информации в список выбора
//
// Параметры:
//  ЭлементВид - СправочникСсылка - ВидыКонтактнойИнформации
//  НовыйВидКИ - Произвольный - передан из ОбработкаОповещения.
//
Процедура ДобавитьВидКИВСписокВыбора(ЭлементВид, НовыйВидКИ) Экспорт
	НовыйВидКонтактнойИнформации = ЭлементВид.СписокВыбора.Добавить(НовыйВидКИ);
	ЭлементВид.Значение = НовыйВидКонтактнойИнформации.Значение;
	ЭлементВид.СписокВыбора.СортироватьПоЗначению();
КонецПроцедуры // ДобавитьВидКИВСписокВыбора()

#КонецЕсли

// Оставляет в номере телефона только цифры, все остальные символы удаляются
Функция УбратьИзНомераТелефонаВсеБуквы(НомерТелефона) Экспорт
	ТолькоЦифрыНомера = "";
	Для а=1 По СтрДлина(НомерТелефона) Цикл
		Если СтрЧислоВхождений("1234567890",Сред(НомерТелефона,а,1)) > 0 Тогда
			ТолькоЦифрыНомера = ТолькоЦифрыНомера + Сред(НомерТелефона,а,1);
		КонецЕсли;
	КонецЦикла;
	Возврат ТолькоЦифрыНомера;
КонецФункции

// Функция убирает из номера телефона все префиксы, например, 999-99*99, будет 9999999 
//  префиксы передаются в СтрокеПрефиксов
Функция УбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона, СтрокаПрефиксов=" /*-+=_.\,!;%:?()") Экспорт
	Для НомерПрефикс = 1 По СтрДлина(СтрокаПрефиксов) Цикл
		ТекущийПрефикс = Сред(СтрокаПрефиксов, НомерПрефикс, 1);
		НомерТелефона = СтрЗаменить(НомерТелефона, ТекущийПрефикс, "");
	КонецЦикла;
	Возврат НомерТелефона;
КонецФункции

// Проверка на наличие пустой строки
Функция ПроверкаПустойСтроки(ВыбСтрока, ПризнакЗапятой=Истина) Экспорт
	
	Если ПустаяСтрока(ВыбСтрока) Тогда
		Возврат "";
	Иначе
		Возврат ?(ПризнакЗапятой,",","")+" ";
	КонецЕсли; 
	
КонецФункции // ПроверкаПустойСтроки()

// Получает шаблоны телефонов из строки, которая хранится в настройках и представляет их в виде соответствия.
//
// Параметры:
//  НЕТ
//
// Возвращаемое значение:
//   Соответствие: Ключ-Количество цифр в номере; Значение-Строка шаблона
// 
Функция ПолучитьШаблоныВВидеСоответствия() Экспорт
	ШаблоныСоответствие = Новый Соответствие;	
	
	#Если Клиент Тогда
		СтрокаСШаблонами   = СокрЛП(обПраво("ШаблоныТелефонныхНомеров", глПрава));
	#Иначе
		СтрокаСШаблонами   = СокрЛП(обПраво("ШаблоныТелефонныхНомеров"));
	#КонецЕсли
	
	// Начинаем разбор форматной строки. удалим пробелы.
	СтрокаСШаблонами = СтрЗаменить(СтрокаСШаблонами, " ", "");	
	
	Если ПустаяСтрока(СтрокаСШаблонами) Тогда
		Возврат ШаблоныСоответствие;	
	КонецЕсли;
		
	// Лишних символов нет.
	МассивШаблонов = Новый Массив;
	Пока Истина Цикл
		НомерСимвола = Найти(СтрокаСШаблонами, ";");
		Если НомерСимвола=0 Тогда
			Если СтрДлина(СтрокаСШаблонами)<>0 Тогда
				МассивШаблонов.Добавить(СтрокаСШаблонами);	
			КонецЕсли;
			Прервать;
		Иначе		
			ВремСтрока = Лев(СтрокаСШаблонами, НомерСимвола-1); 
			Если СтрДлина(ВремСтрока)<>0 Тогда
				МассивШаблонов.Добавить(ВремСтрока);	
			КонецЕсли;
			Если НомерСимвола<>СтрДлина(СтрокаСШаблонами) Тогда				
				СтрокаСШаблонами = Сред(СтрокаСШаблонами, НомерСимвола+1);
			Иначе
				СтрокаСШаблонами = "";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Теперь у нас массив с шаблонами. Сделаем из него соответствие. При этом,
	// не должно быть шаблонов с одинаковым количеством цифр.
	Для Каждого ТекЭлемент Из МассивШаблонов Цикл  	
		КоличествоДевяток = СтрЧислоВхождений(ТекЭлемент, "9");
		Если (КоличествоДевяток<>0) И (ШаблоныСоответствие.Получить(КоличествоДевяток)=Неопределено) Тогда
			ШаблоныСоответствие.Вставить(КоличествоДевяток, ТекЭлемент);	
		КонецЕсли;
	КонецЦикла;
	
		
	Возврат ШаблоныСоответствие;
КонецФункции // ПолучитьШаблоныВВидеСоответствия() 

// Процедура формирует строковое представление адреса.
Процедура СформироватьПредставлениеТелефона(НаборПолей) Экспорт

	// Если внутренний номер заполнен и не заполнен основной, то сохраним его как внутренний
	// а поля кодов страны и города автоматом скинем
	Если (НЕ ПустаяСтрока(НаборПолей.Поле4)) И ПустаяСтрока(НаборПолей.Поле3) Тогда
		НаборПолей.Поле1 = "";
		НаборПолей.Поле2 = "";
		НаборПолей.Представление = СокрЛП(НаборПолей.Поле4);
		Возврат;
	КонецЕсли;
	
	ВремПоле3 = ПривестиНомерТелефонаКШаблону(НаборПолей.Поле3);
    ВремПоле4 = ПривестиНомерТелефонаКШаблону(НаборПолей.Поле4);
		
	НаборПолей.Представление = НаборПолей.Поле1;
	НаборПолей.Представление = НаборПолей.Представление + ?((НЕ ПустаяСтрока(НаборПолей.Поле2)),(ПроверкаПустойСтроки(НаборПолей.Представление, Ложь)+"(" + НаборПолей.Поле2 + ")"),"");
	НаборПолей.Представление = НаборПолей.Представление + ?((НЕ ПустаяСтрока(ВремПоле3)),(ПроверкаПустойСтроки(НаборПолей.Представление, Ложь) + ВремПоле3),"");
	Если НЕ ПустаяСтрока(НаборПолей.Представление) Тогда
		НаборПолей.Представление = НаборПолей.Представление + ?((НЕ ПустаяСтрока(ВремПоле4)),(ПроверкаПустойСтроки(НаборПолей.Представление) + "доб. " + ВремПоле4),"");
	Иначе
		НаборПолей.Представление = ВремПоле4;
	КонецЕсли;  

КонецПроцедуры // СформироватьПредставление()

// Функция приводит телефонный номер к одному из указанных в настройке шаблонов
//
// Параметры
//  НомерТЛФ – строка, номер телефона, который надо преобразовывать
//
// Возвращаемое значение:
//   Приведенный номер – строка, номер, приведенный к одному из шаблонов
//
Функция ПривестиНомерТелефонаКШаблону(Знач НомерТЛФ) Экспорт
	
	ТолькоЦифрыНомера = "";
	КоличествоЦифрНомера = 0;
	
	Для а=1 По СтрДлина(НомерТЛФ) Цикл
		Если СтрЧислоВхождений("1234567890",Сред(НомерТЛФ,а,1)) > 0 Тогда
			КоличествоЦифрНомера = КоличествоЦифрНомера + 1;
			ТолькоЦифрыНомера = ТолькоЦифрыНомера + Сред(НомерТЛФ,а,1);
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоЦифрНомера = 0 Тогда
		Возврат НомерТЛФ;
	КонецЕсли;
	
	СтруктураШаблонов = ПолучитьШаблоныВВидеСоответствия();
	Если ТипЗнч(СтруктураШаблонов)<>Тип("Соответствие") Тогда
		Возврат НомерТЛФ;
	КонецЕсли; 
	ПолученныйШаблон = СтруктураШаблонов.Получить(КоличествоЦифрНомера);
	
	Если ПолученныйШаблон=Неопределено Тогда
		Возврат НомерТЛФ;
	КонецЕсли;
	
	ПриведенныйНомер = "";
	НомерЦифры = 0;
	
	Для а=1 По СтрДлина(ПолученныйШаблон) Цикл
		Если Сред(ПолученныйШаблон,а,1) = "9" Тогда
			НомерЦифры = НомерЦифры + 1;
			ПриведенныйНомер = ПриведенныйНомер + Сред(ТолькоЦифрыНомера,НомерЦифры,1);
		Иначе
			ПриведенныйНомер = ПриведенныйНомер + Сред(ПолученныйШаблон,а,1);
		КонецЕсли;
	КонецЦикла; 

	Возврат ПриведенныйНомер;
	
КонецФункции // ПривестиКШаблону()

// Процедура заполняет запись данными из реквизитов обработки.
//
// Параметры:
//  Запись - Менеджер записи или запись из набора
//
Процедура ЗаполнитьЗапись(Запись)

	Запись.Объект        = Объект;
	Запись.Тип           = Тип;
	Запись.Вид           = Вид;
	Запись.Представление = Представление;
	Запись.Комментарий   = Комментарий;
	Запись.КраткоеПредставление = КраткоеПредставление;
	Запись.ГородскойРайон       = ГородскойРайон;
	Запись.СтанцияМетро         = СтанцияМетро;
	Запись.CRM_ПолеХраненияНомера   = ПолеХраненияНомера;
	Запись.ТипДома		        = ТипДома;
	Запись.ТипКвартиры          = ТипКвартиры;
	Запись.ТипКорпуса  			= ТипКорпуса;
	
	
	Для а = 1 По 12 Цикл
		Запись["Поле" + Строка(а)] = ЭтотОбъект["Поле" + Строка(а)];
	КонецЦикла;

КонецПроцедуры // ЗаполнитьЗапись()

// Функция производит запись данных в ИБ или просто в запись набора или менеджер записи.
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Булево - Удачно произведена запись или нет.
//
Функция Записать() Экспорт

	// Проверка на заполненность реквизитов
	СтрокаПустыхПараметров = "";
	Если НЕ РедактированиеБезВладельца Тогда
		СтрокаПустыхПараметров  = СтрокаПустыхПараметров + ?(обЗначениеНеЗаполнено(Объект), Символы.ПС + Символы.Таб + "- Владелец,", "");
	КонецЕсли;
	СтрокаПустыхПараметров  = СтрокаПустыхПараметров + ?(обЗначениеНеЗаполнено(Вид), Символы.ПС + Символы.Таб + "- Вид,", "");
	Если Тип = Перечисления.ТипыКонтактнойИнформации.Другое Тогда
		СтрокаПустыхПараметров  = СтрокаПустыхПараметров + ?(СокрЛП(Представление) = "", Символы.ПС + Символы.Таб + "- Значение,", "");
	ИначеЕсли Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
		СтрокаПустыхПараметров  = СтрокаПустыхПараметров + ?(СокрЛП(Представление) = "", Символы.ПС + Символы.Таб + "- Адрес электронной почты,", "");
	ИначеЕсли Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		СтрокаПустыхПараметров  = СтрокаПустыхПараметров + ?(СокрЛП(Представление) = "", Символы.ПС + Символы.Таб + "- " + Тип + ",", "");
		
		#Если Клиент Тогда
			ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(Представление, глКоличествоХранимыхЦифрТелефона);
		#Иначе
			ПолеХраненияНомера = кмсфпПреобразоватьНомерДляСохранения(Представление, Константы.CRM_КоличествоХранимыхЦифрТелефона.Получить());
		#КонецЕсли
		
	Иначе	
		СтрокаПустыхПараметров  = СтрокаПустыхПараметров + ?(СокрЛП(Представление) = "", Символы.ПС + Символы.Таб + "- " + Тип + ",", "");
	КонецЕсли;
	
	Если СтрокаПустыхПараметров <> "" Тогда
		СтрокаПустыхПараметров  = "Не заполнены следующие поля: " + Лев(СтрокаПустыхПараметров, СтрДлина(СтрокаПустыхПараметров)-1);
		Сообщить("Запись контактной информации невозможна!", СтатусСообщения.Внимание);
		Сообщить(СтрокаПустыхПараметров, СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
		
	Если НЕ обЗначениеНеЗаполнено(Объект) И Объект.ЭтоГруппа Тогда
		Сообщить("Нельзя использовать группу в качестве владельца контактной информации.",СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
	ЗаписиКонтактнойИнформацииОбъекта = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	// Все проверки сделаны, пытаемся записать
	Если ТипЗнч(мРедактируемаяЗапись) = Тип("РегистрСведенийЗапись.КонтактнаяИнформация")
	 ИЛИ ТипЗнч(мРедактируемаяЗапись) = Тип("РегистрСведенийМенеджерЗаписи.КонтактнаяИнформация") Тогда
		ЗаполнитьЗапись(мРедактируемаяЗапись);
		СтруктураОповещения = Новый Структура("Тип, Вид, Объект, Сортировать", мРедактируемаяЗапись.Тип, мРедактируемаяЗапись.Вид, мРедактируемаяЗапись.Объект, Ложь);
	ИначеЕсли ТипЗнч(мРедактируемаяЗапись) = Тип("РегистрСведенийНаборЗаписей.КонтактнаяИнформация") Тогда
		ЕстьДубль = Ложь;
		НайденнаяЗапись = Неопределено;
		Для Каждого Запись Из мРедактируемаяЗапись Цикл
			Если Запись.Вид = Вид Тогда
				НайденнаяЗапись = Запись;
				ЕстьДубль = Истина;
			КонецЕсли;                                                                
		КонецЦикла; 
		Если НЕ ЕстьДубль Тогда
			НоваяЗапись = мРедактируемаяЗапись.Добавить();
			ЗаполнитьЗапись(НоваяЗапись);
			СтруктураОповещения = Новый Структура("Тип, Вид, Объект, Сортировать", НоваяЗапись.Тип, НоваяЗапись.Вид, НоваяЗапись.Объект, Истина);
		Иначе
			ЗаполнитьЗапись(НайденнаяЗапись);
			СтруктураОповещения = Новый Структура("Тип, Вид, Объект, Сортировать", НайденнаяЗапись.Тип, НайденнаяЗапись.Вид, НайденнаяЗапись.Объект, Истина);
		КонецЕсли;
		ЗаписиКонтактнойИнформацииОбъекта = мРедактируемаяЗапись;
	КонецЕсли;
	
	ПараметрЗаписать = Ложь;
	Если ЗаписыватьВРегистр 
	   И ТипЗнч(мРедактируемаяЗапись) = Тип("РегистрСведенийМенеджерЗаписи.КонтактнаяИнформация") Тогда
		Попытка
			мРедактируемаяЗапись.Записать(Ложь);
			ЗаписиКонтактнойИнформацииОбъекта = киНаборЗаписейКонтактнойИнформацииОбъекта(Объект, Тип);
			ПараметрЗаписать = Истина;
		Исключение
			Сообщить(ОписаниеОшибки(),СтатусСообщения.Важное);
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
	киПроверитьНаличиеЗаписиПоУмолчанию(ЗаписиКонтактнойИнформацииОбъекта, Тип, ПараметрЗаписать);
	
	#Если Клиент Тогда
		Если ТипЗнч(мФормаВладелец) = Тип("Форма") И ТипЗнч(СтруктураОповещения) = Тип("Структура") Тогда
			Оповестить("ЗаписанаКИ", СтруктураОповещения, мФормаВладелец);
		КонецЕсли; 
	#КонецЕсли

	Возврат Истина;

КонецФункции // Записать()

мНаборЗаписейКонтактнойИнформации = Неопределено;


&НаКлиенте
Перем СтруктураСогласия Экспорт;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере(СтруктураСогласия = Неопределено)
	
	//Определим какие данные мы показываем
	Если ЗначениеЗаполнено(НомерСогласия) Тогда
		СтруктураДанных = Новый Структура();
		СтруктураДанных.Вставить("Паспорт", Истина);
		СтруктураДанных.Вставить("ЕстьОшибки", Ложь);
		СтруктураДанных.Вставить("ТипКлиентаПредставление", "-");
		Если СтруктураСогласия.gender = 1 Тогда
			СтруктураДанных.Вставить("ПолКонтрагентаПредставление", "Мужской");
		ИначеЕсли СтруктураСогласия.gender = 2 Тогда
			СтруктураДанных.Вставить("ПолКонтрагентаПредставление", "Женский");
		Иначе
			СтруктураДанных.Вставить("ПолКонтрагентаПредставление", "Неопределенно");
		КонецЕсли;
		СтруктураДанных.Вставить("Имя", СтруктураСогласия.first_name);
		СтруктураДанных.Вставить("Фамилия", СтруктураСогласия.last_name);
		СтруктураДанных.Вставить("Отчество", СтруктураСогласия.middle_name);
		СтруктураДанных.Вставить("ДатаРождения", СтруктураСогласия.birthday_at);
		Если ТипЗнч(СтруктураСогласия.phones) = Тип("Массив") И СтруктураСогласия.phones.Количество() Тогда
			СтруктураДанных.Вставить("Телефон", СтруктураСогласия.phones[0].phone);
		Иначе
			СтруктураДанных.Вставить("Телефон", "-");
		КонецЕсли;
		СтруктураДанных.Вставить("Адрес", СтруктураСогласия.address);
		СтруктураДанных.Вставить("Паспорт", Истина);
		СтруктураДанных.Вставить("ПаспортСерия", СтруктураСогласия.passportSeries);
		СтруктураДанных.Вставить("ПаспортНомер", СтруктураСогласия.passportNumber);
		СтруктураДанных.Вставить("ПаспортДатаВыдачи", СтруктураСогласия.passportIssueDate);
		СтруктураДанных.Вставить("ПаспортКемВыдан", СтруктураСогласия.passportAuthority);
		СтруктураДанных.Вставить("VIN", СтруктураСогласия.VIN);
		СтруктураДанных.Вставить("ДатаПосещения", СтруктураСогласия.date);
		
		// Получим текущий статус согласия
		СтатусСогласия = СтруктураСогласия.status;
		СоответствиеСтатусовСогласия = новый Соответствие;
		СоответствиеСтатусовСогласия.Вставить(1, "Ошибки заполнения");
		СоответствиеСтатусовСогласия.Вставить(2, "На рассмотрении");
		СоответствиеСтатусовСогласия.Вставить(3, "Подтверждена");
		СоответствиеСтатусовСогласия.Вставить(4, "Отклонена");
		СоответствиеСтатусовСогласия.Вставить(7, "Скан отсутствует");
		СоответствиеСтатусовСогласия.Вставить(9, "Удалена");
		СоответствиеСтатусовСогласия.Вставить(10, "Повторная");
		ОписаниеСтатусаСогласия = СоответствиеСтатусовСогласия.Получить(СтатусСогласия);
		//
		
		//Обновим заголовок со статусом
		Элементы.ТекущийСтатусСогласия.Заголовок = "Согласие найдено! Статус согласия <"+ОписаниеСтатусаСогласия+">";
		Элементы.ПредупреждениеОИсточникеДанных.Заголовок = "Данные загруженные из согласия на портале:"; 
		Элементы.ТекущийСтатусСогласия.ЦветТекста = WebЦвета.Зеленый;
		Элементы.ДекорацияЧтоНужноДелать.Заголовок = "Распечатайте печатную форму согласия клиенту для подписи и загрузите ее скан"+ Символы.ПС + 
		"Статус на портале обновляется не сразу.";
		//
		
		//Определим текущий этап
		Если СтатусСогласия = 7 Тогда //Отсутствует скан
			ТекущийСтатусОбработкиСогласий = 3;
		Иначе 
			ТекущийСтатусОбработкиСогласий = 5;
		КонецЕсли;
		
		
		
	Иначе
		//Обновим заголовок со статусом
		Элементы.ТекущийСтатусСогласия.Заголовок = "Согласие не найдено!";
		Элементы.ПредупреждениеОИсточникеДанных.Заголовок = "Данные из карточки контрагента:"; 
		Элементы.ТекущийСтатусСогласия.ЦветТекста = WebЦвета.Красный;
		Элементы.ДекорацияЧтоНужноДелать.Заголовок = "Проверьте корректность данных и отправьте анкету согласия на обработку персональных данных";
		//
		СтруктураДанных = ПолучитьСтруктуруКонтрагента(СсылкаКонтрагент, Документ);
		ТекущийСтатусОбработкиСогласий = 2;
	КонецЕсли;
	
	//Заполним форму данными
	ТипКлиента = СтруктураДанных.ТипКлиентаПредставление;
	ПолКлиента = СтруктураДанных.ПолКонтрагентаПредставление;
	ИмяКлиента = СтруктураДанных.Имя;
	ФамилияКлиента = СтруктураДанных.Фамилия;
	ОтчествоКлиента = СтруктураДанных.Отчество; 
	ДатаРожденияКлиента = СтруктураДанных.ДатаРождения;
	ТелефонКлиента = СтруктураДанных.Телефон;
	АдресКлиента = СтруктураДанных.Адрес;
	ОтправлятьЛиПаспорт = ?(СтруктураДанных.Паспорт = Истина, Истина, Ложь);
	ПаспортСерия = СтруктураДанных.ПаспортСерия;
	ПаспортНомер = СтруктураДанных.ПаспортНомер;
	ПаспортДатаВыдачи = СтруктураДанных.ПаспортДатаВыдачи;
	ПаспортКемВыдан = СтруктураДанных.ПаспортКемВыдан;
	VINАвтомобиля = СтруктураДанных.VIN;
	ДатаПосещения = СтруктураДанных.ДатаПосещения;
	
	// Обновим видимость элементов для паспорта
	Элементы.ПаспортДатаВыдачи.Доступность = ОтправлятьЛиПаспорт;
	Элементы.ПаспортКемВыдан.Доступность = ОтправлятьЛиПаспорт;
	Элементы.ПаспортНомер.Доступность = ОтправлятьЛиПаспорт;
	Элементы.ПаспортСерия.Доступность = ОтправлятьЛиПаспорт;
	
	// Если Есть ошибки заполнения то заблокируем кнопку отправки и покажем уведомление
	Если СтруктураДанных.ЕстьОшибки = Истина Тогда
		Элементы.ОтправитьАнкету.Доступность = ложь;
		Элементы.ПредупреждениеОнекорректныхДанных.Видимость = Истина;
	Иначе
		Элементы.ОтправитьАнкету.Доступность = Истина;
		Элементы.ПредупреждениеОнекорректныхДанных.Видимость = Ложь;
	КонецЕсли;
	//
	
	Если ТекущийСтатусОбработкиСогласий <> 3 И ТекущийСтатусОбработкиСогласий <> 4 Тогда
		Элементы.ГруппаПолучитьПечатнуюФорму.Доступность = Ложь;
		Элементы.ГруппаЗагрузитьСкан.Доступность = Ложь;
	КонецЕсли;
	
	Если ТекущийСтатусОбработкиСогласий <> 1 И ТекущийСтатусОбработкиСогласий <> 2 Тогда
		Элементы.ОтправитьАнкету.Доступность = Ложь;
		Элементы.ОтправлятьЛиПаспорт.Доступность = Ложь;
	КонецЕсли;
	
	// Обновим картинку статуса
	ОбновитьСтатусПрогрессаОбработки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере(СтруктураСогласия);
	ОбновитьСписокПринтеров();
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруКонтрагента(КонтрагентДляВыгрузки, СсылкаНаДокумент)
	
	ОбъектОбработки = РеквизитФормыВЗначение("Обработка");
	
	СтруктураКонтрагента = Новый Структура;
	
	СтруктураКонтрагента.Вставить("ИспользуетсяКарточкаПортала", Ложь);
	СтруктураКонтрагента.Вставить("ЕстьОшибки", Неопределено);
	
	//Определение контрагента для поиска
	Контрагент = КонтрагентДляВыгрузки;
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		КонтактноеЛицо = ОбъектОбработки.ПолучитьКонтактноеЛицо(Контрагент);
		Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			КонтрагентДляВыгрузки = КонтактноеЛицо;
			СтруктураКонтрагента.Вставить("ЭтоКонтактноеЛицо", Истина);
		КонецЕсли;
	Иначе
		КонтрагентДляВыгрузки = Контрагент;
		СтруктураКонтрагента.Вставить("ЭтоКонтактноеЛицо", Ложь);
	КонецЕсли;
	
	//Определение тип
	СтруктураКонтрагента.Вставить("ТипКлиентаПредставление", Контрагент.ФормаСобственности);
	
	//ОсновныеДанные
	Если Контрагент.Пол = Перечисления.ПолФизическихЛиц.Мужской Тогда
		СтруктураКонтрагента.Вставить("ПолКонтрагента", 1);
		СтруктураКонтрагента.Вставить("ПолКонтрагентаПредставление", КонтрагентДляВыгрузки.Пол);
	ИначеЕсли Контрагент.Пол = Перечисления.ПолФизическихЛиц.Женский Тогда
		СтруктураКонтрагента.Вставить("ПолКонтрагента", 2);
		СтруктураКонтрагента.Вставить("ПолКонтрагентаПредставление", КонтрагентДляВыгрузки.Пол);
	Иначе
		СтруктураКонтрагента.Вставить("ПолКонтрагента", Неопределено);
		СтруктураКонтрагента.Вставить("ПолКонтрагентаПредставление", "Не указан пол контрагента !");
		СтруктураКонтрагента.Вставить("ЕстьОшибки", Истина);
	КонецЕсли;
	
	СтруктураКонтрагента.Вставить("Имя", 		СокрЛП(Контрагент.Имя));
	СтруктураКонтрагента.Вставить("Фамилия", 	СокрЛП(Контрагент.Фамилия));
	СтруктураКонтрагента.Вставить("Отчество", 	СокрЛП(Контрагент.Отчество));
	
	//Обработка телефона контрагента
	СтруктураТелефона = ОбъектОбработки.ПолучитьОсновнойТелефонИЕгоТип(Контрагент);
	Если СтруктураТелефона <> Неопределено Тогда
		ТелефонКонтрагента = СтруктураТелефона.Представление;
		ТипТелефонаКонтрагента = СтруктураТелефона.Тип;
		//Чистка телефона
		ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"(","");
		ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,")","");
		ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента," ","");
		ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"+","");
		ТелефонКонтрагента = СтрЗаменить(ТелефонКонтрагента,"-","");
		ТелефонКонтрагента = СокрЛП(ТелефонКонтрагента);
		//
		СтруктураКонтрагента.Вставить("Телефон", 		ТелефонКонтрагента);
		СтруктураКонтрагента.Вставить("ТелефонТип", 	ТипТелефонаКонтрагента);
	Иначе
		СтруктураКонтрагента.Вставить("Телефон", 		"Не заполнен основной номер телефона у контрагента!");
		СтруктураКонтрагента.Вставить("ТелефонТип", 	Неопределено);
		СтруктураКонтрагента.Вставить("ЕстьОшибки", Истина);
	КонецЕсли;
	
	//Почта контрагента
	Если ЗначениеЗаполнено(ОбъектОбработки.ПолучитьОсновнуюПочту(Контрагент)) Тогда
		ПочтаКонтрагента = ОбъектОбработки.ПолучитьОсновнуюПочту(Контрагент);
	Иначе
		ПочтаКонтрагента = "";
	КонецЕсли;
	СтруктураКонтрагента.Вставить("ЭлектрПочта", ПочтаКонтрагента);
	
	//Дата Рождения
	Если ЗначениеЗаполнено(Контрагент.ДатаРождения) Тогда
		ДатаРожденияСтрокой = Формат(Контрагент.ДатаРождения,"ДФ=dd.MM.yyyy");
		СтруктураКонтрагента.Вставить("ДатаРождения", ДатаРожденияСтрокой);
	Иначе
		СтруктураКонтрагента.Вставить("ДатаРождения", "Не заполнена дата рождения контрагента!");
		СтруктураКонтрагента.Вставить("ЕстьОшибки", Истина);
	КонецЕсли; 
	
	//Паспорт
	СтруктураПаспорта = ОбъектОбработки.ПолучитьПаспорт(Контрагент);
	Если СтруктураПаспорта = Неопределено Тогда
		СтруктураКонтрагента.Вставить("Паспорт", 			"");
		СтруктураКонтрагента.Вставить("ПаспортСерия", 		"Не заполнен паспорт контрагента!");
		СтруктураКонтрагента.Вставить("ПаспортНомер", 		"");
		СтруктураКонтрагента.Вставить("ПаспортДатаВыдачи", 	"");
		СтруктураКонтрагента.Вставить("ПаспортКемВыдан", 	"");
	Иначе
		СтруктураКонтрагента.Вставить("ПаспортСерия", 		СтрЗаменить(СтруктураПаспорта.Серия," ",""));
		СтруктураКонтрагента.Вставить("ПаспортНомер", 		СтруктураПаспорта.Номер);
		СтруктураКонтрагента.Вставить("ПаспортДатаВыдачи", 	Формат(Дата(СтруктураПаспорта.ДатаВыдачи),"ДФ=dd.MM.yyyy"));
		СтруктураКонтрагента.Вставить("ПаспортКемВыдан", 	СтруктураПаспорта.КемВыдан);
		СтруктураКонтрагента.Вставить("Паспорт", Истина);
	КонецЕсли;
	
	
	//Адрес
	КонтрагентАдрес = "";
	КонтрагентАдресФактический 	= ОбъектОбработки.киПолучитьПредставлениеКИВыделен(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресФактический);
	КонтрагентАдресПочтовый 	= ОбъектОбработки.киПолучитьПредставлениеКИВыделен(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресПочтовый);
	КонтрагентАдресЮридический 	= ОбъектОбработки.киПолучитьПредставлениеКИВыделен(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	Если ЗначениеЗаполнено(КонтрагентАдресФактический) Тогда
		КонтрагентАдрес = КонтрагентАдресФактический;	
	ИначеЕсли ЗначениеЗаполнено(КонтрагентАдресПочтовый) Тогда
		КонтрагентАдрес = КонтрагентАдресПочтовый;
	ИначеЕсли ЗначениеЗаполнено(КонтрагентАдресЮридический) Тогда
		КонтрагентАдрес = КонтрагентАдресЮридический;
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентАдрес) Тогда
		СтруктураКонтрагента.Вставить("Адрес", КонтрагентАдрес);
	Иначе
		СтруктураКонтрагента.Вставить("Адрес", "Не заполнен адрес контрагента!");
		СтруктураКонтрагента.Вставить("ЕстьОшибки", Истина);
	КонецЕсли;
	
	// Данные автомобиля
	VINАвтомобиля = СсылкаНаДокумент.Автомобиль.VIN;
	Если ЗначениеЗаполнено(VINАвтомобиля) Тогда
		СтруктураКонтрагента.Вставить("VIN", VINАвтомобиля);
	Иначе
		СтруктураКонтрагента.Вставить("VIN", VINАвтомобиля);
		СтруктураКонтрагента.Вставить("ЕстьОшибки", Истина);
	КонецЕсли;
	
	//Дата посещения
	Если НЕ ЗначениеЗаполнено(СсылкаНаДокумент.ДатаМашинозаезда) Тогда
		СтруктураКонтрагента.Вставить("ДатаПосещения", "Не заполнена Дата машинозаезда в ЗН!");
		СтруктураКонтрагента.Вставить("ЕстьОшибки", Истина);
	Иначе
		ДатаПосещения = Формат(СсылкаНаДокумент.ДатаМашинозаезда,"ДФ=dd.MM.yyyy");
		СтруктураКонтрагента.Вставить("ДатаПосещения", ДатаПосещения);
	КонецЕсли;
	
	//Если ЗначениеЗаполнено(ЗаказНаряд.Автомобиль.Модель) Тогда
	//	ИдМодели = НайтиВыгруженныйОбъект(ЗаказНаряд.Автомобиль.Модель);
	//	Если Не ЗначениеЗаполнено(ИдМодели) Тогда
	//		Ошибка = "Не найден ID модели " + ЗаказНаряд.Автомобиль.Модель;
	//		Возврат Ложь;
	//	КонецЕсли;
	//Иначе
	//	Ошибка = "Не заполнена модель автомобиля" + ЗаказНаряд.Автомобиль;
	//	Возврат Ложь;
	//КонецЕсли;
	//
	//Если ЗначениеЗаполнено(ЗаказНаряд.Автомобиль.Модель.Родитель) Тогда
	//	ИдБренда = НайтиВыгруженныйОбъект(ЗаказНаряд.Автомобиль.Модель.Родитель);
	//	Если Не ЗначениеЗаполнено(ИдБренда) Тогда
	//		Ошибка = "Не найден ID бренда " + ЗаказНаряд.Автомобиль.Модель.Родитель;
	//		Возврат Ложь;
	//	КонецЕсли;
	//Иначе
	//	Ошибка = "Не заполнен бренд (родитель модели) автомобиля" + ЗаказНаряд.Автомобиль;
	//	Возврат Ложь;
	//КонецЕсли;
	
	Возврат СтруктураКонтрагента;

КонецФункции

&НаСервере
Функция ОбновитьСтатусПрогрессаОбработки()
	
	ОбъектОбработки = РеквизитФормыВЗначение("Обработка");
	
	ГрафПрогресс = ОбъектОбработки.ПолучитьМакетПрогресса();
	
	МассивЭтапов = Новый Массив;
	
	МассивЭтапов.Добавить(ГрафПрогресс.ЭлементыГрафическойСхемы.Этап1); 
	МассивЭтапов.Добавить(ГрафПрогресс.ЭлементыГрафическойСхемы.Этап2); 
	МассивЭтапов.Добавить(ГрафПрогресс.ЭлементыГрафическойСхемы.Этап3); 
	МассивЭтапов.Добавить(ГрафПрогресс.ЭлементыГрафическойСхемы.Этап4); 
	
	Если НЕ ЗначениеЗаполнено(ТекущийСтатусОбработкиСогласий) Тогда
		ТекущийСтатусОбработкиСогласий = 1;
	КонецЕсли;
	
	Для Каждого ЭтапЭлемент Из МассивЭтапов Цикл
		
		ИндексЭтапа = МассивЭтапов.Найти(ЭтапЭлемент);
		
		Если ИндексЭтапа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// отметим текущий этап
		Если ТекущийСтатусОбработкиСогласий-1 = ИндексЭтапа Тогда
			ЭтапЭлемент.ЦветФона = WebЦвета.Желтый;
		КонецЕсли;
		
		// Отметим пройденные этапы
		Если ТекущийСтатусОбработкиСогласий-1 > ИндексЭтапа Тогда
			ЭтапЭлемент.ЦветФона = WebЦвета.Зеленый;
		КонецЕсли;	
		
	КонецЦикла;
	
	КартинкаТекущегоПрогресса = ПоместитьВоВременноеХранилище(ГрафПрогресс.ПолучитьКартинку().ПолучитьДвоичныеДанные());
	
КонецФункции

&НаКлиенте
Процедура ОткрытьКарточкуКонтрагента(Команда)
	
	ПараметрыФормы = Новый Структура("Ключ", СсылкаКонтрагент);
    ОткрытьФормуМодально("Справочник.Контрагенты.ФормаОбъекта",ПараметрыФормы,ЭтаФорма);
	ПриОткрытииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеИзКарточкиКонтрагента(Команда)
	ПриОткрытииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьЛиПаспортПриИзменении(Элемент=Неопределено)
	Элементы.ПаспортДатаВыдачи.Доступность = ОтправлятьЛиПаспорт;
	Элементы.ПаспортКемВыдан.Доступность = ОтправлятьЛиПаспорт;
	Элементы.ПаспортНомер.Доступность = ОтправлятьЛиПаспорт;
	Элементы.ПаспортСерия.Доступность = ОтправлятьЛиПаспорт;
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьАнкету(Команда)
	ТекстОшибки = "";
	ОтправитьАнкетуНаСервере(ТекстОшибки, СтруктураСогласия);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		//@skip-check undefined-function-or-procedure
		Предупреждение(ТекстОшибки,,"Механизм согласий DNM");
	КонецЕсли; 
		
КонецПроцедуры

&НаСервере
Процедура ОтправитьАнкетуНаСервере(ТекстОшибки, СтруктураСогласия=Неопределено)
	
	//Установим соединение (выберем нужную учетную запись по подразделению документа)
	ОшибкаСоединения = "";
	
	ОбъектОбработки = РеквизитФормыВЗначение("Обработка");
	
	ОбъектОбработки.УстановитьСоединениеПоПрофилюЗаказНаряда(Документ.ПодразделениеКомпании, ОшибкаСоединения);
	
	Если ЗначениеЗаполнено(ОшибкаСоединения) Тогда
		ТекстОшибки = ОшибкаСоединения;
		Возврат;
	КонецЕсли;
	//
	
	// Получим актуальную структуру данных
	СтруктураДанных = ПолучитьСтруктуруКонтрагента(СсылкаКонтрагент, Документ);
	
	// Проверим есть ли ошибки заполнения
	Если СтруктураДанных.ЕстьОшибки = Истина Тогда
		ТекстОшибки = "Найдена ошибка в заполненных данных!";
		ПриОткрытииНаСервере();
		Возврат;
	КонецЕсли;
	//
	
	//Структура согласия
	СтруктураСогласия = новый Структура;
	СтруктураСогласия.Вставить("first_name",		СтруктураДанных.Имя);
	СтруктураСогласия.Вставить("last_name",			СтруктураДанных.Фамилия);
	СтруктураСогласия.Вставить("middle_name",		СтруктураДанных.Отчество);
	СтруктураСогласия.Вставить("gender",			СтруктураДанных.ПолКонтрагента);
	СтруктураСогласия.Вставить("phone",				СтруктураДанных.Телефон);
	СтруктураСогласия.Вставить("phoneType",			СтруктураДанных.ТелефонТип);
	СтруктураСогласия.Вставить("email",				СтруктураДанных.ЭлектрПочта);
	СтруктураСогласия.Вставить("birthdayAt",		СтруктураДанных.ДатаРождения);
	Если ОтправлятьЛиПаспорт Тогда
		СтруктураСогласия.Вставить("passportType",		1);
		СтруктураСогласия.Вставить("passportSeries",	СтруктураДанных.ПаспортСерия);
		СтруктураСогласия.Вставить("passportNumber",	СтруктураДанных.ПаспортНомер);
		СтруктураСогласия.Вставить("passportIssueDate",	СтруктураДанных.ПаспортДатаВыдачи);
		СтруктураСогласия.Вставить("passportAuthority",	СтруктураДанных.ПаспортКемВыдан);
	КонецЕсли;
	СтруктураСогласия.Вставить("address",			СтруктураДанных.Адрес);
	СтруктураСогласия.Вставить("role",				1);
	СтруктураСогласия.Вставить("type",				2);
	СтруктураСогласия.Вставить("vin",				СтруктураДанных.VIN);
	СтруктураСогласия.Вставить("date",				СтруктураДанных.ДатаПосещения);
	//СтруктураСогласия.Вставить("refused",			Истина);
	//СтруктураСогласия.Вставить("brand_id", ИдБренда);
	//СтруктураСогласия.Вставить("model_id", ИдМодели);
	//
	
	
	// Отправляем данные
	ОшибкаОтправки = "";
	ТелоОтвета = "";
	Ответ = ОбъектОбработки.ОтправитьСогласие(СтруктураСогласия, ОшибкаОтправки, ТелоОтвета);	
	//
	
	//Обработка ошибок
	Если Ответ = Ложь Тогда
		ПолныйТекстОшибок = "";
		// Разберем json на ошибки и объединим в одну строку
		Попытка
			Чтение = Новый ЧтениеJSON;
			Чтение.УстановитьСтроку(ТелоОтвета);
			МассивДанных = ПрочитатьJSON(Чтение);
			Для Каждого Сообщение из МассивДанных Цикл
				ПолныйТекстОшибок = ПолныйТекстОшибок + Сообщение.message + Символы.ПС;	
			КонецЦикла;
		Исключение
			ПолныйТекстОшибок = ОшибкаОтправки;
		Конецпопытки;
		
		//укажем ошибку
		ТекстОшибки = "Портал вернул ошибку при отправке данных: " + Символы.ПС + Символы.ПС + ПолныйТекстОшибок;
		ПриОткрытииНаСервере();
		Возврат;
		
	КонецЕсли;
	//
	
	НомерСогласия = Ответ.id;
	
	Набор = ОбъектОбработки.ПолучитьРегистрСопоставления().СоздатьНаборЗаписей();
	Набор.Отбор.Данные1С.Установить(СсылкаКонтрагент);
	Набор.Отбор.Тип.Установить("Согласие");
	Набор.Прочитать();
	
	Если Набор.Количество() = 0 тогда
		ТекущаяЗапись = Набор.Добавить();
		ТекущаяЗапись.Тип = "Согласие";
		ТекущаяЗапись.Период = ТекущаяДатаСеанса();
		ТекущаяЗапись.Данные1С = СсылкаКонтрагент;
	Иначе
		ТекущаяЗапись = Набор[0];
	КонецЕсли;

	ТекущаяЗапись.Комментарий = "";
	ТекущаяЗапись.ID = НомерСогласия;
	ТекущаяЗапись.ДатаВыгрузки = ТекущаяДатаСеанса();
	Набор.Записать();
	
	СтруктураСогласия = Ответ;
	
	ПриОткрытииНаСервере();
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьPDF(Команда)
	
	Состояние("Получаем файл с портала. Подождите.");
	PDFФайл = ПолучитьPDF(НомерСогласия, Истина);
	Состояние();
	Режим = РежимДиалогаВыбораФайла.Сохранение; 
    ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(Режим); 
    ДиалогСохраненияФайла.ПолноеИмяФайла = "Согласие на обработку ПД №"+СтрЗаменить(Строка(НомерСогласия)," ","") + " " + ИмяКлиента + " " + ФамилияКлиента + " " + ОтчествоКлиента; 
    Фильтр = "Файл PDF (*.pdf)|*.pdf";                 
    ДиалогСохраненияФайла.Фильтр = Фильтр; 
    ДиалогСохраненияФайла.МножественныйВыбор = Ложь; 
    ДиалогСохраненияФайла.Заголовок = "Выберите файл"; 
    Если ДиалогСохраненияФайла.Выбрать() Тогда 
        ПутьКФайлу = ДиалогСохраненияФайла.ПолноеИмяФайла;
		PDFФайл.Записать(ПутьКФайлу);
    КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьPDF(НомерСогласия, ВВидеДвоичныхДанных = Ложь)
	ОшибкаСоединения = "";
	
	ОбъектОбработки = РеквизитФормыВЗначение("Обработка");
	
	ОбъектОбработки.УстановитьСоединениеПоПрофилюЗаказНаряда(Документ.ПодразделениеКомпании, ОшибкаСоединения);
		
	Возврат ОбъектОбработки.ПолучитьPDFСогласия(НомерСогласия, ВВидеДвоичныхДанных);
	
КонецФункции

&НаКлиенте
Функция ПроверитьCOMСоединениеMSWord()
	Состояние("Проверяем работу Microsoft Word. Подождите.");
	Попытка
 		WordApp = Новый COMОбъект("Word.Application");
        WordApp.Quit();
		Состояние();
		Возврат Истина;
	Исключение
		Состояние();
		Возврат Ложь;
	КонецПопытки;
КонецФункции

&НаКлиенте
Процедура ОткрытьPDF(Команда)
	
	Заголовок = "Согласие на обработку ПД №"+СтрЗаменить(Строка(НомерСогласия)," ","") + " " + ИмяКлиента + " " + ФамилияКлиента + " " + ОтчествоКлиента;
	Состояние("Получаем файл с портала. Подождите.");
	PDFФайл = ПолучитьPDF(НомерСогласия);
	Состояние();
	PDFФайл.Показать();
	
КонецПроцедуры

&НаКлиенте
Функция ОбновитьТекущийПринтерЧерезMSWord() Экспорт 
	
	Если НЕ ЗначениеЗаполнено(ТекущийПринтер) Тогда
	
		Если ПроверитьCOMСоединениеMSWord() Тогда
			Состояние("Проверяем текущий принтер. Подождите.");
			WordApp = Новый COMОбъект("Word.Application");
			ТекущийПринтер = WordApp.ActivePrinter();
			WordApp.Quit();
			Состояние();
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСписокПринтеров()
	
	Состояние("Составляется список принтеров. Подождите.");
	Попытка
		Locator = Новый COMОбъект("WbemScripting.SWbemLocator");
		Сервисы = Locator.ConnectServer(".");
		Объекты = Сервисы.InstancesOf("Win32_Printer");
		// Теперь переберем все принтеры системы
		Для Каждого Принтер из Объекты Цикл
			ИмяПринтера = Принтер.Name;
			Пока Найти(ИмяПринтера,"\") > 0 Цикл
				ИмяПринтера = Сред(ИмяПринтера, Найти(ИмяПринтера,"\")+1);
			КонецЦикла;
			Если Принтер.Default = Истина Тогда
				ТекущийПринтер = ИмяПринтера;
			КонецЕсли;
			ИмяПринтера = СокрЛП(ИмяПринтера);
			Элементы.ТекущийПринтер.СписокВыбора.Добавить(Принтер.Name,ИмяПринтера,(Принтер.Name=ТекущийПринтер));
		КонецЦикла;
	Исключение
	КонецПопытки;
	
	ОбновитьТекущийПринтерЧерезMSWord();
	
	Состояние();
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьPDF(Команда)
	
	Состояние("Получаем файл с портала. Подождите.");
	PDFФайл = ПолучитьPDF(НомерСогласия, Истина);
	Состояние();
	
	Состояние("Запускаем печать через MS Word. Подождите.");
	Попытка
		WordApp = Новый COMОбъект("Word.Application");
	Исключение
		Состояние();	
		//@skip-check undefined-function-or-procedure
		Предупреждение("Компонента MS Word недоступна. Сохраните PDF файл и распечатайте в другой программе."); 
		Возврат;
	КонецПопытки;
	//Установим выбранный принтер
	Состояние("Устанавливаем выбранный принтер MS Word. Подождите.");
	Попытка
		WordApp.ActivePrinter = ТекущийПринтер;
	Исключение
		WordApp.Quit();
		Состояние();	
		//@skip-check undefined-function-or-procedure
		Предупреждение("Принтер <" + ТекущийПринтер + "> не устанавливается как принтер для печати в MS Word"); 
		Возврат;
	КонецПопытки;
	//Сохраним печатную форму во временный файл
	//@skip-check undefined-function
	Состояние("Сохраняем временный файл. Подождите.");
	ПутьКФайлу = ПолучитьИмяВременногоФайла(".pdf");
	Попытка
		PDFФайл.Записать(ПутьКФайлу);
	Исключение
		//@skip-check undefined-function-or-procedure
		Состояние();
		Предупреждение("Нет доступа к временному каталогу пользователя!"); 
		Возврат;
	КонецПопытки;
	Состояние("Открываем файл с помощью MS Word. Подождите.");
	Попытка
		ДокументWord = WordApp.Documents.Open(ПутьКФайлу);
	Исключение
		Состояние();
		WordApp.Quit();
		//@skip-check undefined-function-or-procedure
		Предупреждение("Для MS Word нет доступа к временному каталогу пользователя!"); 
		Возврат;
	КонецПопытки;
	
	Состояние("Отправляем команду на печать с помощью MS Word. Подождите.");
	ДокументWord.PrintOut();
	WordApp.Quit();
	
	Состояние();
	ТекущийСтатусОбработкиСогласий = 4;
	ОбновитьСтатусПрогрессаОбработки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлСкана(Команда)
	
	Режим = РежимДиалогаВыбораФайла.Открытие; 
    ДиалогВыбораФайла = Новый ДиалогВыбораФайла(Режим); 
    Фильтр = "Файлы изображений|*.jpg;*.jpeg;*.png|Все файлы|*.*";                 
    ДиалогВыбораФайла.Фильтр = Фильтр; 
    ДиалогВыбораФайла.МножественныйВыбор = Ложь; 
    ДиалогВыбораФайла.Заголовок = "Выберите файл со сканом";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
    Если ДиалогВыбораФайла.Выбрать() Тогда 
        ПутьФайлаСканаПечатнойФормы = ДиалогВыбораФайла.ПолноеИмяФайла;
		ИмяФайлаСканаПечатнойФормы = ПутьФайлаСканаПечатнойФормы;
		Пока Найти(ИмяФайлаСканаПечатнойФормы,"\") > 0 Цикл
			ИмяФайлаСканаПечатнойФормы = Сред(ИмяФайлаСканаПечатнойФормы, Найти(ИмяФайлаСканаПечатнойФормы,"\")+1);
		КонецЦикла;
		
		ДвоичныеДанные = Новый ДвоичныеДанные(ПутьФайлаСканаПечатнойФормы);	
		ЗагружаемыйФайлBase64 = Base64Строка(ДвоичныеДанные);
		
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСкан(Команда)
	ОтправитьСканНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтправитьСканНаСервере()
	
	ОшибкаСоединения = "";
	
	ОбъектОбработки = РеквизитФормыВЗначение("Обработка");
	
	ОбъектОбработки.УказатьСогласиеКонтрагентаСОПД(СсылкаКонтрагент);
	
	ОбъектОбработки.УстановитьСоединениеПоПрофилюЗаказНаряда(Документ.ПодразделениеКомпании, ОшибкаСоединения);	
	
	Если ЗначениеЗаполнено(ЗагружаемыйФайлBase64) И ЗначениеЗаполнено(ИмяФайлаСканаПечатнойФормы) Тогда
		Ответ = ОбъектОбработки.ЗагрузитьСКАНСогласия(НомерСогласия, ЗагружаемыйФайлBase64, ИмяФайлаСканаПечатнойФормы);
		Если Ответ <> Ложь Тогда
			ТекущийСтатусОбработкиСогласий = 5;
			ОбновитьСтатусПрогрессаОбработки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры




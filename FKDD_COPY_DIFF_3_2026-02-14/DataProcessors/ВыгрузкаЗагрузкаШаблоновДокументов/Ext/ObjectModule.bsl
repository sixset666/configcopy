Перем РеквизитыШапки; // Структура реквизитов шапки, предназначенных для сохранения в шаблон
Перем ТабличныеЧасти; // Структура реквизитов табличных, предназначенных для сохранения в шаблон
Перем ТекстОшибки;    // Текст ошибок, необходимых для вывода пользователю

//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СПЕЦИАЛЬНОГО НАЗНАЧЕНИЯ
//////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//////////////////////////////////////////////////////////////////////////////////

#Если Клиент Тогда

// Выгружает выбранный документ в либо файл либо регистр  в формате сериализованного объекта
// Параметры:
//  ДокументДляВыгрузки - выгружаемый документ
Процедура ВыгрузитьВШаблон(ДокументДляВыгрузки) Экспорт
	Если обЗначениеНеЗаполнено(ДокументДляВыгрузки) Тогда
		Сообщить("Документ для формирования шаблона не выбран");
		Возврат;
	КонецЕсли;	
	
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ДокументДляВыгрузки)) Тогда
		ДокументОбъект = ДокументДляВыгрузки.ПолучитьОбъект();
	Иначе
		ДокументОбъект = ДокументДляВыгрузки; 
	КонецЕсли;
	
	// а вдруг такой функции нет
	Попытка
		ШаблонныеРеквизиты = ДокументОбъект.ПолучитьШаблонныеРеквизиты();
	Исключение
		ШаблонныеРеквизиты = ВосстановитьЗначение("ШД:"+ДокументОбъект.Метаданные().Имя);
		Если ТипЗнч(ШаблонныеРеквизиты) <> Тип("Структура") Тогда
			ШаблонныеРеквизиты = Новый Структура();
		КонецЕсли;
	КонецПопытки;
	
	// составим дерево реквизитов объекта
	ДеревоМетаданных = Новый ДеревоЗначений();
	ДеревоМетаданных.Колонки.Добавить("ИмяРеквизита");
	ДеревоМетаданных.Колонки.Добавить("ПредставлениеРеквизита");
	ДеревоМетаданных.Колонки.Добавить("ТипРеквизита");
	ДеревоМетаданных.Колонки.Добавить("Пометка",Новый ОписаниеТипов("Булево"));
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	
	// дату добавим отдельно
	СтрокаДереваТабличнаяЧасть = ДеревоМетаданных.Строки.Добавить();
	СтрокаДереваТабличнаяЧасть.ИмяРеквизита = "Дата";
	СтрокаДереваТабличнаяЧасть.ПредставлениеРеквизита = "Дата документа";
	СтрокаДереваТабличнаяЧасть.ТипРеквизита = Тип("Дата");
	
	Для Каждого Реквизит Из МетаданныеДокумента.Реквизиты Цикл
		СтрокаДерева = ДеревоМетаданных.Строки.Добавить();
		СтрокаДерева.ИмяРеквизита = Реквизит.Имя;
		СтрокаДерева.ПредставлениеРеквизита = Реквизит.Синоним;
		СтрокаДерева.ТипРеквизита = Реквизит.Тип;
		// теперь рассчитаем пометку
		СтрокаДерева.Пометка = ШаблонныеРеквизиты.Свойство(Реквизит.Имя);
	КонецЦикла;	
	
	// добавим в дерево табличные части
	Для Каждого ТабличнаяЧасть Из МетаданныеДокумента.ТабличныеЧасти Цикл
		СтрокаДереваТабличнаяЧасть = ДеревоМетаданных.Строки.Добавить();
		СтрокаДереваТабличнаяЧасть.ИмяРеквизита = ТабличнаяЧасть.Имя;
		СтрокаДереваТабличнаяЧасть.ПредставлениеРеквизита = ТабличнаяЧасть.Синоним;
		СтрокаДереваТабличнаяЧасть.ТипРеквизита = ТипЗнч(ТабличнаяЧасть);
		// теперь рассчитаем пометку
		СоставТабличнойЧасти ="";
		ШаблонныеРеквизиты.Свойство(ТабличнаяЧасть.Имя,СоставТабличнойЧасти);
		СтрокаДереваТабличнаяЧасть.Пометка = ТипЗнч(СоставТабличнойЧасти)=Тип("Структура");
		
		Для Каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл
			СтрокаДерева = СтрокаДереваТабличнаяЧасть.Строки.Добавить();
			СтрокаДерева.ИмяРеквизита = Реквизит.Имя;
			СтрокаДерева.ПредставлениеРеквизита = Реквизит.Синоним;
			СтрокаДерева.ТипРеквизита = Реквизит.Тип;
			// теперь рассчитаем пометку
			Если ТипЗнч(СоставТабличнойЧасти)=Тип("Структура") Тогда
				СтрокаДерева.Пометка = СоставТабличнойЧасти.Свойство(Реквизит.Имя);
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;	
	
	ФормаВыборШаблонныхРеквизитов = ЭтотОбъект.ПолучитьФорму("ПометкаШаблонныхРеквизитов");
	ФормаВыборШаблонныхРеквизитов.СписокРеквизитов = ДеревоМетаданных.Скопировать();
	СписокРеквизитов = ФормаВыборШаблонныхРеквизитов.ОткрытьМодально();
	
	// если таки выбрали реквизиты для выгрузки, то и начнем выгружать
	Если НЕ ЗначениеЗаполнено(СписокРеквизитов) Тогда
		Возврат;
	Иначе
		СтруктураОтбора = Новый Структура("Пометка",Истина);
		Если СписокРеквизитов.Строки.НайтиСтроки(СтруктураОтбора).Количество()=0 Тогда
			Сообщить("Не выбрано ни одного реквизита для выгрузки шаблона");
			Возврат;
		КонецЕсли;
	КонецЕсли;	
	
	// теперь определимся с именем
	// НовоеИмяШаблонаДокумента = СтрЗаменить(
	// СокрЛП(Справочники.ХозОперации.ПолучитьИмяПредопределенного(ДокументДляВыгрузки.ХозОперация))," ","");
	НовоеИмяШаблонаДокумента = "";
		
	// читаем все файлы xml
	МаскаФайлов = "*.xml";
	
	ФормаВводаИмени = ЭтотОбъект.ПолучитьФорму("ФормаВводаИмени");
	ФормаВводаИмени.Хозоперация = ДокументДляВыгрузки.Хозоперация;
	ФормаВводаИмени.ИмяШаблона = НовоеИмяШаблонаДокумента;
	ФормаВводаИмени.Режим = "Выгрузка";
	ПараметрыИмени = ФормаВводаИмени.ОткрытьМодально();
	
	// если с именем файла определились, то собственно начнем выгрузку
	Если ТипЗнч(ПараметрыИмени)=Тип("Структура") И ЗначениеЗаполнено(ПараметрыИмени.ИмяШаблона) Тогда
		дкВыгрузитьРеквизитыДокументаВШаблон(ДокументОбъект,ПараметрыИмени,СписокРеквизитов);
	КонецЕсли;	
КонецПроцедуры	

// Загрузить из шаблона либо в переданный документ-объект, либо создать новый объект переданного вида
// Параметры:
//  ДокументОбъект - либо конкретный экземпляр документ-объект, либо новый объект документа
Функция ЗагрузитьИзШаблона(ДокументОбъект) Экспорт
	// если не пусто то будем работать
	Если обЗначениеНеЗаполнено(ДокументОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// ну что же, с самим объектом определились, теперь надо определиться с шаблоном откуда грузим
	// для этого сначала поймем хозоперацию переданного объекта
	Если ЗначениеЗаполнено(ДокументОбъект.Хозоперация) Тогда
		Хозоперация = ДокументОбъект.Хозоперация;
	Иначе
		// если не определена попробуем достать из функции объекта
		СписокХозопераций = ДокументОбъект.ПолучитьСписокХозОпераций();
		Для Каждого Хозоперация Из СписокХозопераций Цикл
			// нашли значит
			Если Хозоперация.Пометка Тогда
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	
	ФормаВводаИмени = ЭтотОбъект.ПолучитьФорму("ФормаВводаИмени");
	ФормаВводаИмени.Хозоперация = Хозоперация;
	ФормаВводаИмени.Режим = "Загрузка";
	ПараметрыИмени = ФормаВводаИмени.ОткрытьМодально();
	
	// если с именем файла определились, то собственно начнем загрузку
	
	Если ТипЗнч(ПараметрыИмени)=Тип("Структура") Тогда
		// в качестве второго параметра передадим строку с данными
		Возврат дкЗагрузитьРеквизитыДокументаИзШаблона(ДокументОбъект,ПараметрыИмени);
	Иначе
		Возврат Ложь;
	КонецЕсли;	
КонецФункции

// Загрузить из шаблона либо в переданный документ-объект, либо создать новый объект переданного вида
// Параметры:
//  ДокументОбъект - либо конкретный экземпляр документ-объект, либо новый объект документа
Функция СоздатьИзШаблона(ПараметрыИмени) Экспорт
	ДокументОбъект = Неопределено;
	Результат = дкЗагрузитьРеквизитыДокументаИзШаблона(ДокументОбъект,ПараметрыИмени);
	Если НЕ Результат Тогда
		Возврат Неопределено;
	Иначе
		Возврат ДокументОбъект;
	КонецЕсли;
КонецФункции

#КонецЕсли


//////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ
//////////////////////////////////////////////////////////////////////////////////

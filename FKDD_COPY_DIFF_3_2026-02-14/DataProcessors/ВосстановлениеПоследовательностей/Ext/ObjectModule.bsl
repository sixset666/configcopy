////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем ТаблицаПоследовательностей Экспорт; // хранит таблицу последовательностей
Перем СоответствиеПоследовательностей;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Функция ПолучитьМинимальнуюДатуГраниц(СтруктураПоследовательностей=Неопределено)
	
	МинимальнаяДата = Дата(1,1,1);
	ОтбиратьПоследовательности = (ТипЗнч(СтруктураПоследовательностей)=Тип("Структура"));
	ШаблонЗапроса = "ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|		$ИмяГраницы$Границы.МоментВремени КАК МоментВремени
	|	ИЗ
	|		РегистрСведений.$ИмяГраницы$ КАК $ИмяГраницы$Границы
	|";
	
	// Приступим к формированию запроса
	ТекстЗапроса = "//";
	Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
		Если ОтбиратьПоследовательности И (Не СтруктураПоследовательностей.Свойство(ТекПоследовательность.ИмяГраницы)) Тогда
			Продолжить;
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + СтрЗаменить(ШаблонЗапроса, "$ИмяГраницы$", ТекПоследовательность.ИмяГраницы);
	КонецЦикла;
	
	Если НЕ ТекстЗапроса = "//" Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	МИНИМУМ(Таблица.МоментВремени) КАК МоментВремени
		|ИЗ
		|	("+ТекстЗапроса+") КАК Таблица";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			МинимальнаяДата = Выборка.МоментВремени;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МинимальнаяДата;
	
КонецФункции //ПолучитьМинимальнуюДатуГраниц()

Функция ПолучитьТекстЗапросаРегистровНакопления(СтруктураПоследовательностей, МинимальнаяДата = '00010101', ПоТекДату = '00010101')
	
	ОтбиратьПоследовательности = (ТипЗнч(СтруктураПоследовательностей)=Тип("Структура"));
	ТекстЗапроса="";
	СтрокаУсловия="";
	Если (НЕ МинимальнаяДата = '00010101') И (НЕ ПоТекДату = '00010101') Тогда
		
		СтрокаУсловия = "ГДЕ ВЫБОР КОГДА ТИПЗНАЧЕНИЯ($ИмяТаблицы$.Регистратор) = ТИП(Документ.ЗаказНаряд) ТОГДА $ИмяТаблицы$.Период >= &МинимальнаяДата ИНАЧЕ $ИмяТаблицы$.Период МЕЖДУ &МинимальнаяДата И &ПоДату КОНЕЦ";
		
	ИначеЕсли (НЕ МинимальнаяДата = '00010101') Тогда
		
		СтрокаУсловия = "ГДЕ $ИмяТаблицы$.Период >= &МинимальнаяДата";
		
	ИначеЕсли (НЕ ПоТекДату = '00010101') Тогда
		
		СтрокаУсловия = "ГДЕ ВЫБОР КОГДА ТИПЗНАЧЕНИЯ($ИмяТаблицы$.Регистратор) = ТИП(Документ.ЗаказНаряд) ТОГДА $ИмяТаблицы$.Регистратор.Дата <= &ПоДату ИНАЧЕ $ИмяТаблицы$.Период <= &ПоДату КОНЕЦ";
	КонецЕсли;
	Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
		Если ОтбиратьПоследовательности И (Не СтруктураПоследовательностей.Свойство(ТекПоследовательность.ИмяГраницы)) Тогда
			Продолжить;
		КонецЕсли;
		ТекстЗапросаПоследовательности="";
		СчетчикТаблиц=0;
		ТекстПолейИндексов=ТекПоследовательность.ТекстПолейИндексов;
		ТекстПолей = "";
		ИмяТаблицы = "";
		Для Каждого РегистрНакопления Из ТекПоследовательность.РегистрыНакопления Цикл
			Если СчетчикТаблиц>0 Тогда
				ТекстЗапросаПоследовательности=ТекстЗапросаПоследовательности+"
				|ОБЪЕДИНИТЬ ВСЕ
				|";
			КонецЕсли;
			ИмяТаблицы = РегистрНакопления.Представление;
			ТекстПолей = "
			|	"+ИмяТаблицы+".Период КАК Период,
			|	"+ИмяТаблицы+".Регистратор КАК Регистратор,
			|	"+ИмяТаблицы+".МоментВремени КАК МоментВремени";
			Для Каждого Измерение Из РегистрНакопления.Значение Цикл
				ТекстПолей=ТекстПолей+",
				|	"+ИмяТаблицы+"."+Измерение.Значение+" КАК "+Измерение.Ключ;
			КонецЦикла;
			
			ТекстЗапросаПоследовательности=ТекстЗапросаПоследовательности+"ВЫБРАТЬ
			|	"+ТекстПолей+"
			|ИЗ
			|	РегистрНакопления."+ИмяТаблицы+" КАК "+ИмяТаблицы+"
			|"+СтрЗаменить(СтрокаУсловия,"$ИмяТаблицы$", ИмяТаблицы);
			СчетчикТаблиц=СчетчикТаблиц+1;
		КонецЦикла;
		ТекстИсточника="";
		Если СчетчикТаблиц=1 Тогда
			ТекстИсточника = "РегистрНакопления."+ИмяТаблицы+" КАК "+ИмяТаблицы+"
			|"+СтрЗаменить(СтрокаУсловия,"$ИмяТаблицы$", ИмяТаблицы);;
		Иначе
			ИмяТаблицы = "Запрос"+ТекПоследовательность.ИмяГраницы;
			ТекстПолей = "
			|	"+ИмяТаблицы+".Период КАК Период,
			|	"+ИмяТаблицы+".Регистратор КАК Регистратор,
			|	"+ИмяТаблицы+".МоментВремени КАК МоментВремени";
			Для Каждого ТекИзмерение Из ТекПоследовательность.Измерения Цикл
				ТекстПолей=ТекстПолей+",
				|	"+ИмяТаблицы+"."+ТекИзмерение+" КАК "+ТекИзмерение;
			КонецЦикла;
			ТекстИсточника = "("+ТекстЗапросаПоследовательности+") КАК Запрос"+ТекПоследовательность.ИмяГраницы;
		КонецЕсли;
		
		ТекстЗапроса=ТекстЗапроса+"
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	"+ТекстПолей+"
		|ПОМЕСТИТЬ Последовательность"+ТекПоследовательность.ИмяГраницы+"
		|ИЗ
		|	"+ТекстИсточника+"
		|"+?(ЗначениеЗаполнено(ТекстПолейИндексов), "ИНДЕКСИРОВАТЬ ПО "+ТекстПолейИндексов,"")+"
		|;";
		
	КонецЦикла;
	Возврат ТекстЗапроса;
	
КонецФункции //ПолучитьТекстЗапросаРегистровНакопления()

// Удаление границ
//
Процедура УдалитьАктуальныеГраницы(СтруктураПоследовательностей=Неопределено) Экспорт
	
	ОтбиратьПоследовательности = (ТипЗнч(СтруктураПоследовательностей)=Тип("Структура"));
	// если вызываем в первый раз, то сформируем временные таблицы
	ТекстЗапроса="";
	// сформируем временные таблицы
	Запрос = Новый Запрос();
	МинимальнаяДата = ПолучитьМинимальнуюДатуГраниц(СтруктураПоследовательностей);
	Запрос.УстановитьПараметр("МинимальнаяДата", МинимальнаяДата);
	ТекстЗапроса = ПолучитьТекстЗапросаРегистровНакопления(СтруктураПоследовательностей, МинимальнаяДата);
	
	СтруктураИзмерений = Новый Структура;
	Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
		Если ОтбиратьПоследовательности И (Не СтруктураПоследовательностей.Свойство(ТекПоследовательность.ИмяГраницы)) Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого ТекИзмерение Из ТекПоследовательность.Измерения Цикл
			СтруктураИзмерений.Вставить(ТекИзмерение);
		КонецЦикла;
	КонецЦикла;
	
	// Определим шаблоны фрагментов запроса
	ШаблонЗапроса = "ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	""$ИмяГраницы$"" КАК ИмяПоследовательности
	|	//ИЗМЕРЕНИЯ
	|ИЗ
	|	РегистрСведений.$ИмяГраницы$ КАК $ИмяГраницы$Границы
	|ЛЕВОЕ СОЕДИНЕНИЕ Последовательность$ИмяГраницы$ КАК $ИмяГраницы$
	|ПО 
	|	//СОЕДИНЕНИЕ ПО ИЗМЕРЕНИЯМ
	|СГРУППИРОВАТЬ ПО
	|	//СГРУППИРОВАТЬ
	|ИМЕЮЩИЕ МАКСИМУМ(ВЫБОР
	|		КОГДА $ИмяГраницы$Границы.МоментВремени = $ИмяГраницы$.Период И $ИмяГраницы$Границы.ДокументСсылка < $ИмяГраницы$.Регистратор ТОГДА
	|			ИСТИНА
	|		КОГДА $ИмяГраницы$Границы.МоментВремени < $ИмяГраницы$.Период ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ)=ЛОЖЬ
	|";
	
	// Приступим к формированию запроса
	ТекстЗапросаПоследовательностей = "//";
	Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
		Если ОтбиратьПоследовательности И (Не СтруктураПоследовательностей.Свойство(ТекПоследовательность.ИмяГраницы)) Тогда
			Продолжить;
		КонецЕсли;
		ИмяГраницы = ТекПоследовательность.ИмяГраницы;
		СтрокаИзмерения="";
		СтрокаУсловийСоединения = "";
		СтрокаСгруппировать = "";
		Для Каждого ТекИзмерение Из СтруктураИзмерений Цикл
			ИмяИзмерения = ТекИзмерение.Ключ;
			Если ТекПоследовательность.Измерения.Найти(ИмяИзмерения)=Неопределено Тогда
				СтрокаИзмерения = СтрокаИзмерения+", 
				|	NULL КАК "+ИмяИзмерения;
			Иначе
				СтрокаУсловийСоединения = СтрокаУсловийСоединения + ?(СтрокаУсловийСоединения="", "", " И "+Символы.ПС)+ИмяГраницы+"Границы."+ИмяИзмерения+" = "+ИмяГраницы+"."+ИмяИзмерения;
				СтрокаИзмерения = СтрокаИзмерения+",
				|	"+ИмяГраницы+"Границы."+ИмяИзмерения+" КАК "+ИмяИзмерения;
				СтрокаСгруппировать = ?(СтрокаСгруппировать="","",","+Символы.ПС)+"	"+ИмяГраницы+"Границы."+ИмяИзмерения;
			КонецЕсли;
		КонецЦикла;
		ТекШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "//СОЕДИНЕНИЕ ПО ИЗМЕРЕНИЯМ", СтрокаУсловийСоединения);
		ТекШаблонЗапроса = СтрЗаменить(ТекШаблонЗапроса, "//СГРУППИРОВАТЬ", СтрокаСгруппировать);
		ТекШаблонЗапроса = СтрЗаменить(ТекШаблонЗапроса, "//ИЗМЕРЕНИЯ", СтрокаИзмерения);
		ТекстЗапросаПоследовательностей = ТекстЗапросаПоследовательностей + СтрЗаменить(ТекШаблонЗапроса, "$ИмяГраницы$", ИмяГраницы);
	КонецЦикла;
	
	СтрокаИзмерения="";
	Для Каждого ТекИзмерение Из СтруктураИзмерений Цикл
		ИмяИзмерения = ТекИзмерение.Ключ;
		СтрокаИзмерения = СтрокаИзмерения + ","+ Символы.ПС + "	ИтоговаяТаблица."+ИмяИзмерения+" КАК "+ИмяИзмерения;
	КонецЦикла;
	
	Запрос.Текст = "
	|"+ ТекстЗапроса+"
	|ВЫБРАТЬ
	|	ИтоговаяТаблица.ИмяПоследовательности КАК ИмяПоследовательности"+СтрокаИзмерения+"
	|ИЗ ("+ТекстЗапросаПоследовательностей+") КАК ИтоговаяТаблица
	|
	|ИТОГИ
	|ПО
	|	ИмяПоследовательности";
	
	ВыборкаПоследовательностей = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоследовательностей.Следующий() Цикл
		ВыборкаДеталей=ВыборкаПоследовательностей.Выбрать();
		Если ВыборкаДеталей.Количество()>0 Тогда
			НаборЗаписей = РегистрыСведений[ВыборкаПоследовательностей.ИмяПоследовательности].СоздатьНаборЗаписей();
			ТекПоследовательность = ТаблицаПоследовательностей.Найти(ВыборкаПоследовательностей.ИмяПоследовательности, "ИмяГраницы");
			Пока ВыборкаДеталей.Следующий() Цикл
				Для Каждого ТекИзмерение Из ТекПоследовательность.Измерения Цикл
					НаборЗаписей.Отбор[ТекИзмерение].Установить(ВыборкаДеталей[ТекИзмерение]);
					НаборЗаписей.Прочитать();
					НаборЗаписей.Очистить();
					НаборЗаписей.Записать(Истина);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УдалитьАктуальныеГраницы()

// Возвращает таблицу документов с неактуальными движениями
//
// Параметр:
//	ПоТекДату	- Дата	- ПустаяДата
//
// Возвращаемое значение:
//	ТаблицаЗначений	- Таблица неактуальных документов
//
Функция ПолучитьТаблицуНеактуальныхДокументов(ПоТекДату = '00010101', Запрос = Неопределено, СтруктураПоследовательностей=Неопределено) Экспорт
	
	ОтбиратьПоследовательности = (ТипЗнч(СтруктураПоследовательностей)=Тип("Структура"));
	// если вызываем в первый раз, то сформируем временные таблицы
	ТекстЗапроса="";
	Если ТипЗнч(Запрос) <> Тип("Запрос") Тогда
		// сформируем временные таблицы
		Запрос = Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		МинимальнаяДата = ПолучитьМинимальнуюДатуГраниц(СтруктураПоследовательностей);
		Запрос.УстановитьПараметр("МинимальнаяДата", МинимальнаяДата);
		ТекстЗапроса = ПолучитьТекстЗапросаРегистровНакопления(СтруктураПоследовательностей, МинимальнаяДата, ПоТекДату);
	КонецЕсли;
	
	// Определим шаблоны фрагментов запроса
	ШаблонЗапроса = "ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица$ИмяГраницы$.ИмяПоследовательности,
	|	Таблица$ИмяГраницы$.Граница,
	|	Таблица$ИмяГраницы$.Документ,
	|	Таблица$ИмяГраницы$.МоментВремени
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		""$ИмяГраницы$"" КАК ИмяПоследовательности,
	|		$ИмяГраницы$Границы.ДокументСсылка КАК Граница,
	|		$ИмяГраницы$.Регистратор КАК Документ,
	|		$ИмяГраницы$.МоментВремени КАК МоментВремени
	|	ИЗ
	|		РегистрСведений.$ИмяГраницы$ КАК $ИмяГраницы$Границы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Последовательность$ИмяГраницы$ КАК $ИмяГраницы$
	|	ПО
	|		(НЕ $ИмяГраницы$Границы.ДокументСсылка = $ИмяГраницы$.Регистратор) И 
	|		ВЫБОР
	|			КОГДА $ИмяГраницы$Границы.МоментВремени = $ИмяГраницы$.Период ТОГДА
	|				$ИмяГраницы$Границы.ДокументСсылка < $ИмяГраницы$.Регистратор
	|			ИНАЧЕ
	|				$ИмяГраницы$Границы.МоментВремени < $ИмяГраницы$.Период
	|		КОНЕЦ
	|		//СОЕДИНЕНИЕ ПО ИЗМЕРЕНИЯМ
	|	УПОРЯДОЧИТЬ ПО
	|		МоментВремени) КАК Таблица$ИмяГраницы$
	|
	|";
	
	ШаблонУсловия= "И $ИмяГраницы$Границы.$ИмяИзмерения$ = $ИмяГраницы$.$ИмяИзмерения$
	|				";
	// Приступим к формированию запроса
	ТекстЗапросаПоследовательностей = "//";
	Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
		Если ОтбиратьПоследовательности И (Не СтруктураПоследовательностей.Свойство(ТекПоследовательность.ИмяГраницы)) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаУсловийСоединения = "";
		Для Каждого ТекИзмерение Из ТекПоследовательность.Измерения Цикл
			СтрокаУсловийСоединения = СтрокаУсловийСоединения + СтрЗаменить(ШаблонУсловия,"$ИмяИзмерения$",ТекИзмерение);
		КонецЦикла;
		ТекШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "//СОЕДИНЕНИЕ ПО ИЗМЕРЕНИЯМ", СтрокаУсловийСоединения);
		ТекстЗапросаПоследовательностей = ТекстЗапросаПоследовательностей + СтрЗаменить(ТекШаблонЗапроса, "$ИмяГраницы$", ТекПоследовательность.ИмяГраницы);
	КонецЦикла;
	ТекстЗапросаПоследовательностей = ТекстЗапросаПоследовательностей + "
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени";
	
	Запрос.Текст = ТекстЗапроса+Символы.ПС+ТекстЗапросаПоследовательностей;
	Запрос.УстановитьПараметр("ПоДату", ПоТекДату);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьТаблицуНеактуальныхДокументов()

// Функция производит восстановление последовательностей проведения документов
//
// Параметр:
//	СписокПоследовательностей 	- Массив	- Последовательности
//	ПоТекДату						- Дата		- Пустая дата
//
// Возвращаемое значение
//	Структура					- Отчет о результатах работы
//
Функция Восстановить(СписокПоследовательностей, ПоТекДату = '00010101') Экспорт
	// Подготовим структуру в которой будем запоминать все проведенные документы
	СписокПроведенныхДокументов = Новый СписокЗначений;
	НачалоРаботы = ТекущаяДата();
	Ошибка = ЛОЖЬ;
	
	Если ПустаяСтрока(ИмяПользователя()) Тогда
		ИмяПользователя           = "Администратор";
		ПолноеИмяПользователя     = "Администратор информационной базы";
	Иначе
		Если ПустаяСтрока(ПолноеИмяПользователя()) Тогда
			ПолноеИмяПользователя = ИмяПользователя();
		Иначе
			ПолноеИмяПользователя = ПолноеИмяПользователя();
		КонецЕсли;
	КонецЕсли;
	
	РазрешитьИзменениеЛичныхНастроек = Ложь;
	#Если Клиент Тогда
		Если обПраво("РазрешитьИзменениеЛичныхНастроек", глПрава, Ложь) Тогда
			РазрешитьИзменениеЛичныхНастроек = Истина;
		КонецЕсли;
	#Иначе
		Если НЕ обПраво("РазрешитьИзменениеЛичныхНастроек", , Ложь) Тогда
			РазрешитьИзменениеЛичныхНастроек = Истина;
		КонецЕсли;
	#КонецЕсли
	
	ВключитьЗначимыеСобытия = Ложь;
	СообщитьОбИзменениях = Ложь;
	#Если Клиент Тогда
		Если РазрешитьИзменениеЛичныхНастроек И обПраво("АктивностьЗначимыхСобытий", глПрава) Тогда
	#Иначе
		Если РазрешитьИзменениеЛичныхНастроек И обПраво("АктивностьЗначимыхСобытий") Тогда
	#КонецЕсли
		//Ответ = Вопрос("Значимые события включены. 
		//| на время выполнения обработки их необходимо отключить. Продолжить?", РежимДиалогаВопрос.ДаНет);
		МенеджерЗаписи = РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект         = Неопределено;
		МенеджерЗаписи.ПравоНастройка = обПолучитьСсылкуПВХПравИНастроек("АктивностьЗначимыхСобытий");
		МенеджерЗаписи.Значение       = Ложь;
		МенеджерЗаписи.Записать(Истина);
		
		ВключитьЗначимыеСобытия = Истина;
		
		СообщитьОбИзменениях = Истина;
		
	КонецЕсли;
	
	Если СообщитьОбИзменениях Тогда
		#Если Клиент Тогда
		глПрава = обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
		Сообщить("Отключены значимые события.", СтатусСообщения.Внимание);
		#КонецЕсли
	КонецЕсли;
	
	Сообщить(" ЗАПУЩЕН ПРОЦЕСС ВОССТАНОВЛЕНИЯ ПОСЛЕДОВАТЕЛЬНОСТЕЙ", СтатусСообщения.Информация);
	
	// Составим таблицу документов с неактуальными движениями
	Запрос = Неопределено;
	Если СписокПоследовательностей.Количество()=0 Тогда
		Текст = "Не выбраны последовательности";
		Сообщить(Текст, СтатусСообщения.Важное);
	Иначе
		ТаблицаДокументов = ПолучитьТаблицуНеактуальныхДокументов(ПоТекДату, Запрос, СписокПоследовательностей);
	КонецЕсли;
	
	Если Ошибка ИЛИ СписокПоследовательностей.Количество()=0 Тогда
		ДокументОбъект = Неопределено;
		//СписокПроведенныхДокументов.Добавить(Неопределено, ТекущаяДата(), Истина);
	Иначе
		// Открываем бесконечный цикл
		Пока ИСТИНА Цикл
			// Обработаем отказ пользователя от продолжения восстановления последовательности проведения документов
			#Если Клиент Тогда
				ОбработкаПрерыванияПользователя();
			#КонецЕсли
			
			// Найдем документ, который нужно восстановить
			Документ = Неопределено;
			Для Каждого ТекСтрока Из ТаблицаДокументов Цикл
					Документ = ТекСтрока.Документ;
				Прервать;
			КонецЦикла;
			
			// Проверим был ли найден документ который нужно восстановить, если нет - значит процесс восстановления завершен
			Если Документ = Неопределено Тогда
				Ошибка = ЛОЖЬ;
				Прервать;
			КонецЕсли;
			
			// Получим объект документа с неактуальными движениями и перепроведем его
			ДокументОбъект = Неопределено;
			Попытка
				ДокументОбъект = Документ.ПолучитьОбъект();
				Попытка
					ДатаДок = ДокументОбъект.ПолучитьДатуДокумента();
				Исключение
					ДатаДок = ДокументОбъект.Дата;
				КонецПопытки;
				
				Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказНаряд")
					И ПоТекДату <> '00010101'
					И ДокументОбъект.ДатаЗакрытия > ПоТекДату Тогда
					Прервать;
				КонецЕсли;
				
				Ссылка  = ДокументОбъект.Ссылка;
				ВидДок  = ДокументОбъект.Метаданные().Имя;
				ПредставлениеДокумента = ДокументОбъект.Метаданные().Синоним+" № "+СокрЛП(ДокументОбъект.Номер)+" от "+Формат(ДокументОбъект.Дата,"ДФ=dd.MM.yyyy");
				#Если Клиент Тогда
					Состояние("Перепроводится документ: " + ПредставлениеДокумента);
				#КонецЕсли
				Сообщить("Перепроводится документ: " + ПредставлениеДокумента, СтатусСообщения.Информация);
				ДокументОбъект.ДополнительныеСвойства.Вставить("ГрупповаяОбработкаДокументов",Истина);
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
				ДокументОбъект = Неопределено;
				СписокПроведенныхДокументов.Добавить(Документ, ТекущаяДата());
				
				// переместим границу последовательностей
				Для Каждого ТекЭлемент Из СписокПоследовательностей Цикл
					
					Если СоответствиеПоследовательностей[ТекЭлемент.Ключ][ВидДок]=Неопределено Тогда Продолжить; КонецЕсли;
					ТекПоследовательность = ТаблицаПоследовательностей.Найти(ТекЭлемент.Ключ, "ИмяГраницы");
					
					ТекстЗапроса="";
					Для Каждого Измерение Из ТекПоследовательность.Измерения Цикл
						ТекстЗапроса=ТекстЗапроса+?(ТекстЗапроса="","",","+Символы.ПС)+Измерение+" КАК "+Измерение;
					КонецЦикла;
					ТекстЗапроса="ВЫБРАТЬ
					|	"+ТекстЗапроса+"
					|
					|ИЗ
					|	Последовательность"+ТекПоследовательность.ИмяГраницы+" КАК ДвиженияДокументаПоПоследовательности
					|ГДЕ
					|	ДвиженияДокументаПоПоследовательности.Регистратор = &ВыбРегистратор";
					
					Запрос.Текст=ТекстЗапроса;
					Запрос.УстановитьПараметр("ВыбРегистратор", Ссылка);
					ТаблицаИзмерений = Запрос.Выполнить().Выгрузить();
					Если ТаблицаИзмерений.Количество()>0 Тогда
						НаборЗаписейПоследовательность = РегистрыСведений[ТекПоследовательность.ИмяГраницы].СоздатьНаборЗаписей();
						Для Каждого Измерение Из ТекПоследовательность.Измерения Цикл
							НаборЗаписейПоследовательность[Измерение] = ТаблицаИзмерений.ВыгрузитьКолонку(Измерение);
						КонецЦикла;
						НаборЗаписейПоследовательность.МоментВремени = ДатаДок;
						НаборЗаписейПоследовательность.ДокументСсылка = Ссылка;
						НаборЗаписейПоследовательность.ИзменитьГраницу(Истина);
					КонецЕсли;
				КонецЦикла;
			Исключение
				//Сообщить(ОписаниеОшибки());
				Ошибка = ИСТИНА;
				ДокументОбъект = Неопределено;
				СписокПроведенныхДокументов.Добавить(Документ, ТекущаяДата(), Истина);
				Прервать;
			КонецПопытки;
			
			//Закомментированно RADV пока не удалять!!!  вдруг понадобится.
			//// Посмотрим как изменились границы последовательностей
			//ТаблицаДокументовПосле = ПолучитьТаблицуНеактуальныхДокументов(ПоТекДату, Запрос);
			//Для Каждого СтрокаПосле Из ТаблицаДокументовПосле Цикл
			//	Если СписокПоследовательностей.Свойство(СтрокаПосле.ИмяПоследовательности) Тогда
			//		Продолжить;
			//	КонецЕсли;
			//	СтрокаДо = ТаблицаДокументов.Найти(СтрокаПосле.ИмяПоследовательности,"ИмяПоследовательности");
			//	//Если (ДокументДо = ДокументПосле) - Значит ничего не произошло! - граница не изменилась
			//	//Если (ДокументДо < ДокументПосле) - Случайно зацепили граничный документ последовательности
			//	//Если (ДокументДо > ДокументПосле) - Мы поломали эту последовательность, и ее необходимо восстановить
			//	Если (СтрокаДо = Неопределено)ИЛИ(СтрокаДо.МоментВремени.Сравнить(СтрокаПосле.МоментВремени) > 0) Тогда
			//		СписокПоследовательностей.Вставить(СтрокаПосле.ИмяПоследовательности,
			//		"Последовательность была нарушена в процессе восстановления другой. Восстановлена");
			//	КонецЕсли;
			//КонецЦикла;
			//ТаблицаДокументов = ТаблицаДокументовПосле;
			
			// Переполучим границы последовательностей
			ТаблицаДокументов = ПолучитьТаблицуНеактуальныхДокументов(ПоТекДату, Запрос, СписокПоследовательностей);
		КонецЦикла;
	КонецЕсли;
	КонецРаботы = ТекущаяДата();
	ВремяРаботы = (КонецРаботы - НачалоРаботы);
	
	//#Если Клиент Тогда
		Сообщить(" ", СтатусСообщения.БезСтатуса);
		Сообщить(" ПРОЦЕСС ВОССТАНОВЛЕНИЯ ЗАВЕРШЕН СО СТАТУСОМ: " +?(Ошибка,"НЕ","")+ "УСПЕШНО", ?(Ошибка, СтатусСообщения.Важное, СтатусСообщения.Информация));
		ТекстСообщения = 
		"	Начало работы:		" +НачалоРаботы+ "
		|	Окончание работы:	" +КонецРаботы + "
		|	Время работы:		" +Цел(ВремяРаботы / 60) + " мин. " +(ВремяРаботы - Цел(ВремяРаботы / 60))+ " сек. 
		|	Перепроведено		" +СписокПроведенныхДокументов.Количество()+ " документов";
		Сообщить(ТекстСообщения, СтатусСообщения.БезСтатуса);
	//#КонецЕсли
	
	// если у последовательности все записи актуальны, то очистим их в регистре
	Если СписокПоследовательностей.Количество()>0 Тогда
		УдалитьАктуальныеГраницы(СписокПоследовательностей);
	КонецЕсли;
	
	СообщитьОбИзменениях = Ложь;
	
	Если ВключитьЗначимыеСобытия Тогда
		МенеджерЗаписи = РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект         = Неопределено;
		МенеджерЗаписи.ПравоНастройка = обПолучитьСсылкуПВХПравИНастроек("АктивностьЗначимыхСобытий");
		МенеджерЗаписи.Значение       = Истина;
		МенеджерЗаписи.Записать(Истина);
		
		СообщитьОбИзменениях = Истина;
	КонецЕсли;
	
	Если СообщитьОбИзменениях Тогда
		#Если Клиент Тогда
		глПрава = обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
		Сообщить("Включены значимые события.", СтатусСообщения.Внимание);
		#КонецЕсли
	КонецЕсли;
	
	// Составим структуру которую заполним подробными данными о проделанной работе
	Отчет = Новый Структура("ПоДату,НачалоРаботы,СписокПроведенныхДокументов,КоличествоДокументов,Последовательности,КонецРаботы,ВремяРаботы,Ошибка",ПоТекДату, НачалоРаботы, СписокПроведенныхДокументов, СписокПроведенныхДокументов.Количество(), СписокПоследовательностей, КонецРаботы, ВремяРаботы, Ошибка);
	Возврат Отчет;
КонецФункции // Восстановить()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

НеОграничивать = ИСТИНА;

ТаблицаПоследовательностей = Новый ТаблицаЗначений();
ТаблицаПоследовательностей.Колонки.Добавить("ИмяГраницы");
ТаблицаПоследовательностей.Колонки.Добавить("Представление");
ТаблицаПоследовательностей.Колонки.Добавить("РегистрыНакопления");
ТаблицаПоследовательностей.Колонки.Добавить("Измерения");
ТаблицаПоследовательностей.Колонки.Добавить("ТекстПолейИндексов");

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыПартий";
НоваяСтрока.Представление = "Партии";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("СкладКомпании","СкладКомпании"), "ПартииТоваровКомпании");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("СкладКомпании");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "СкладКомпании";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыВзаиморасчетов";
НоваяСтрока.Представление = "Взаиморасчеты";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("ДоговорВзаиморасчетов","ДоговорВзаиморасчетов"), "ВзаиморасчетыКомпании");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("ДоговорВзаиморасчетов");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "ДоговорВзаиморасчетов";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыОрдерныйСклад";
НоваяСтрока.Представление = "Товары на ордерном складе";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("СкладКомпании","СкладКомпании"), "ОстаткиТоваровОрдерныйСклад");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("СкладКомпании");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "СкладКомпании";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыЗаказы";
НоваяСтрока.Представление = "Заказы";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("Заказ","Заказ"), "ЗаказыПокупателей");
СписокРегистровНакопления.Добавить(Новый Структура("Заказ","ЗаказПоставщику"), "ЗаказыПоставщикам");
СписокРегистровНакопления.Добавить(Новый Структура("Заказ","ЗаказПокупателя"), "ЗаказыРаспределение");
СписокРегистровНакопления.Добавить(Новый Структура("Заказ","ЗаказПоставщика"), "ЗаказыРаспределение");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("Заказ");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыПроизводство";
НоваяСтрока.Представление = "Производство";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("ЗаказНаряд","ЗаказНаряд"), "ТоварыВПроизводстве");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("ЗаказНаряд");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыАвтомобили";
НоваяСтрока.Представление = "Автомобили";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("СкладКомпании","СкладКомпании"), "ОстаткиАвтомобилей");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("СкладКомпании");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "СкладКомпании";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыКомплектация";
НоваяСтрока.Представление = "Комплектация";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("Автомобиль","Автомобиль"), "КомплектацияАвтомобилей");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("Автомобиль");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "Автомобиль";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыЗаказыНаАвтомобили";
НоваяСтрока.Представление = "Заказы на автомобили";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("Автомобиль","Автомобиль"), "ЗаказыАвтомобилей");
СписокРегистровНакопления.Добавить(Новый Структура("Автомобиль","Автомобиль"), "ЗаказыПоставщикамНаАвтомобили");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив();
МассивИзмерений.Добавить("Автомобиль");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "Автомобиль";

НоваяСтрока = ТаблицаПоследовательностей.Добавить();
НоваяСтрока.ИмяГраницы = "ГраницыАвтомобилиОрдерныйСклад";
НоваяСтрока.Представление = "Автомобили на ордерном складе";
СписокРегистровНакопления = Новый СписокЗначений;
СписокРегистровНакопления.Добавить(Новый Структура("СкладКомпании","СкладКомпании"), "ОстаткиАвтомобилейОрдерныйСклад");
НоваяСтрока.РегистрыНакопления = СписокРегистровНакопления;
МассивИзмерений = Новый Массив(); МассивИзмерений.Добавить("СкладКомпании");
НоваяСтрока.Измерения = МассивИзмерений;
НоваяСтрока.ТекстПолейИндексов = "СкладКомпании";

// заполним соответствие последовательностей
СоответствиеПоследовательностей = Новый Соответствие();
Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
	СтруктураРегистрыНакопления = Новый Структура;
	Для Каждого РегистрНакопления Из ТекПоследовательность.РегистрыНакопления Цикл
		СтруктураРегистрыНакопления.Вставить(РегистрНакопления.Представление);
	КонецЦикла;
	
	СоответсвиеДокументов = Новый Соответствие();
	Для Каждого МетДок Из Метаданные.Документы Цикл
		Для Каждого Рег Из МетДок.Движения Цикл
			Если СтруктураРегистрыНакопления.Свойство(Рег.Имя) Тогда
				СоответсвиеДокументов.Вставить(МетДок.Имя,Истина); Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	СоответствиеПоследовательностей.Вставить(ТекПоследовательность.ИмяГраницы,СоответсвиеДокументов);
КонецЦикла;
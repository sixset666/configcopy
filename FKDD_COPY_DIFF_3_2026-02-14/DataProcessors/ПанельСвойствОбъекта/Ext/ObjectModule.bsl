#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ ОБРАБОТКИ УПРАВЛЕНИЯ ПАНЕЛЬЮ СВОЙСТВ ОБЪЕКТА
// 
////////////////////////////////////////////////////////////////////////////////
// БЛОК ИНИЦИАЛИЗАЦИИ ПЕРЕМЕННЫХ МОДУЛЯ

Перем КлючУникальности; // Ключ уникальности

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру с именами страниц панели, которые будут отображаться на форме
// Ключ структуры	- имя страницы, которая должна быть видна
// Значение			- назначение неопределено
Функция ПолучитьСписокАктивныхСтраниц() Экспорт
	Страницы = Новый Структура();
	
	Если ТипЗнч(ТекОбъект) = Тип("СправочникОбъект.Номенклатура") Тогда
		Страницы.Вставить("ДополнительныеРеквизиты");
		Страницы.Вставить("СоставНабора");
		Страницы.Вставить("СвойстваОбъекта");
		Страницы.Вставить("Характеристики");
		Страницы.Вставить("Оборудование");
		Страницы.Вставить("ШтрихКоды");
		Страницы.Вставить("ЕдиницыИзмерения");
	Иначе
		// Проверим могут ли быть свойства у данного объекта, и если могут, то назначим их
		Если (ТекОбъект <> Неопределено) И Метаданные.ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Тип.СодержитТип(ТипЗнч(ТекОбъект.Ссылка)) Тогда
			Страницы.Вставить("СвойстваОбъекта");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Страницы;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Получаем форму панели свойств
Процедура ПолучитьФормуПанелиСвойств(КлючУникальности = Неопределено) Экспорт
	Если ТипЗнч(глФормаПанелиСвойств)<>Тип("Форма") ИЛИ глФормаПанелиСвойств.КлючУникальности <> КлючУникальности Тогда
		глФормаПанелиСвойств = ПолучитьФорму(,,КлючУникальности);
	КонецЕсли;
КонецПроцедуры

// Процедура установки нового владельца формы свойств
Процедура УстановитьВладельцаПанелиСвойств(НовыйВладелец = Неопределено, Объект = Неопределено, Кнопка = Неопределено) Экспорт
	//у старого владельца формы сбросим пометку кнопки
	Если глФормаПанелиСвойств.Кнопка <> Неопределено И глФормаПанелиСвойств.Кнопка <> Кнопка Тогда
		глФормаПанелиСвойств.Кнопка.Пометка = Ложь;
	КонецЕсли;
	
	// Установим нового владельца Формы панели
	глФормаПанелиСвойств.ВладелецФормы = НовыйВладелец;
	
	//запомним новое значение кнопки открытия обработки
	Если Кнопка <> Неопределено Тогда
		глФормаПанелиСвойств.Кнопка = Кнопка;
	Иначе
		Попытка глФормаПанелиСвойств.Кнопка = глФормаПанелиСвойств.ВладелецФормы.ЭлементыФормы.ДействияФормы.Кнопки.Свойства; Исключение Кнопка = Неопределено; КонецПопытки;
	КонецЕсли;
	
	Если глФормаПанелиСвойств.Кнопка <> Неопределено Тогда
		глФормаПанелиСвойств.Кнопка.Пометка = глФормаПанелиСвойств.Открыта();
	КонецЕсли;
	
	// У нового владельца состояние кнопки Свойства приведем в соответствие
	// с текущим состоянием Формы панели
	Попытка
		// Вернем активность окну хозяина и пошлем себе от его имени команду
		НовыйВладелец.Активизировать();
		НовыйВладелец.ОповеститьОбАктивизацииОбъекта(Объект);
	Исключение КонецПопытки;
КонецПроцедуры

// Выполняет все необходимые подготовки и проверки, а потом открывает форму панели
Процедура ОткрытьПанельСвойств(Объект, ФормаВладелец = Неопределено, Показывать = Неопределено, Кнопка = Неопределено, ОткрытаИзФормыСписка = Ложь) Экспорт
	//если параметр Показывать равен неопределено. вызов произошел "автоматом", при открытии формы
	ОткрытиеФормыЭлемента = Показывать=Неопределено;
	ПолучитьФормуПанелиСвойств(КлючУникальности);
	Если Показывать = Неопределено Тогда
		Показывать = глФормаПанелиСвойств.Открыта();
	КонецЕсли;
	// Закроем форму если этого просят
	Если (НЕ Показывать) ИЛИ (Объект = Неопределено) Тогда
		Если глФормаПанелиСвойств.Открыта() Тогда
			глФормаПанелиСвойств.Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если НЕ глФормаПанелиСвойств.Открыта() Тогда
		//Открываем новую панель свойств, если она не открыта
		глФормаПанелиСвойств.ВладелецФормы = ФормаВладелец;
		глФормаПанелиСвойств.Открыть();
	Иначе
		//получим тип объекта метаданных для которого была открыта форма подбора
		СписокНайден = Истина;
		Попытка
			ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(глФормаПанелиСвойств.ВладелецФормы.СправочникСписок));
		Исключение
			Попытка
				ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(глФормаПанелиСвойств.ВладелецФормы.ДокументСписок));	
			Исключение
				Попытка
					ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(глФормаПанелиСвойств.ВладелецФормы.ЖурналДокументовСписок));	
				Исключение
					Попытка
						ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(глФормаПанелиСвойств.ВладелецФормы.ПланВидовХарактеристикСписок));		
					Исключение
						Попытка
							ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(глФормаПанелиСвойств.ВладелецФормы.ПланВидовРасчетаСписок));		
						Исключение
							Попытка //Для АРМ
								ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(глФормаПанелиСвойств.ВладелецФормы.СписокНоменклатура));
							Исключение
								ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(глФормаПанелиСвойств.ВладелецФормы.ЭтотОбъект));
								СписокНайден = Ложь;
							КонецПопытки;
						КонецПопытки;
					КонецПопытки;	
				КонецПопытки;
			КонецПопытки;
		КонецПопытки;
		//проверяем, а не автоматически ли хочет обновиться панель свойств при открытии формы объекта при заполненных
		//свойствах для другого типа объекта, если да, то не даем
		Если ОбъектМетаданных<>Метаданные.НайтиПоТипу(ТипЗнч(Объект)) И НЕ ОткрытаИзФормыСписка И глФормаПанелиСвойств.ОткрытаИзФормыСписка И ОткрытиеФормыЭлемента И СписокНайден Тогда
			Попытка
				Если НЕ ОбъектМетаданных.РегистрируемыеДокументы.Содержит(Метаданные.НайтиПоТипу(ТипЗнч(Объект))) Тогда
					Возврат;
				КонецЕсли;
			Исключение
				Возврат;
			КонецПопытки;
		КонецЕсли; 
	КонецЕсли;
	
	глФормаПанелиСвойств.ОткрытаИзФормыСписка = ОткрытаИзФормыСписка;
	УстановитьВладельцаПанелиСвойств(ФормаВладелец, Объект, Кнопка);
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////////
// БЛОК ИНИЦИАЛИЗАЦИИ МОДУЛЯ

КлючУникальности = "Обработка_ПанельСвойствОбъекта";
#КонецЕсли
// Модуль обработки АРМЖурналЗаказНарядов

Перем Права Экспорт; // права пользователя

// Поиск клиента и автомобиля в источниках, заданных параметрами поиска
// Параметры:
//  СтрокаПоиска - Строка -  содержимое поискового запроса пользователя
// 	РасширенныйПоиск - Булево - Если истина - значит поиск осуществляется в соответствии со всеми настройками
//								с учетом флагов, ложь - поиск везде, флаги ограничений игнорируются
//  СтруктураНастройкиПоиска - Структура - Структура источников поиска
Функция ПоискДокументов(СтрокаПоиска,РасширенныйПоиск,СтруктураНастройкиПоиска=Неопределено) Экспорт
	
	//источники поиска
	НаименованиеКлиента = Истина;
	КонтактнаяИнформация = Истина; 
	ГосНомер = Истина; 
	VIN = Истина; 
	НомерКарты = Истина; 
	
	Если ТипЗнч(СтруктураНастройкиПоиска) = Тип("ТаблицаЗначений") И РасширенныйПоиск Тогда
		НаименованиеКлиента = СтруктураНастройкиПоиска.Найти("НаименованиеКлиента","Параметр").Значение;
		КонтактнаяИнформация = СтруктураНастройкиПоиска.Найти("КонтактнаяИнформация","Параметр").Значение; 
		ГосНомер = СтруктураНастройкиПоиска.Найти("ГосНомер","Параметр").Значение; 
		VIN = СтруктураНастройкиПоиска.Найти("VIN","Параметр").Значение; 
		НомерКарты = СтруктураНастройкиПоиска.Найти("НомерКарты","Параметр").Значение; 
		НомерДокумента = СтруктураНастройкиПоиска.Найти("НомерДокумента","Параметр").Значение; 
	КонецЕсли;
	
	ЗапросНаименованиеКлиента =	"
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Заказчик.Наименование ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Заказчик.Наименование ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|	ИЛИ ЗаявкаНаРемонт.Заказчик ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|";
								
	ЗапросКонтактнаяИнформация = "
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Заказчик В (ВЫБРАТЬ ОбъектыКИ.Объект ИЗ	ОбъектыКИ КАК ОбъектыКИ)
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Заказчик В (ВЫБРАТЬ ОбъектыКИ.Объект ИЗ	ОбъектыКИ КАК ОбъектыКИ)
		|	ИЛИ ВЫРАЗИТЬ(ЗаявкаНаРемонт.ПредставлениеТелефона КАК Строка(50)) ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|";
		
	ЗапросГосНомер = "
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Автомобиль В (
		|		ВЫБРАТЬ
		|			АвтомобилиСрезПоследних.Автомобиль
		|		ИЗ
		|			РегистрСведений.Автомобили.СрезПоследних(, Значение ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|				И ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)
		|			) КАК АвтомобилиСрезПоследних
		|	)
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Автомобиль В (
		|		ВЫБРАТЬ
		|			АвтомобилиСрезПоследних.Автомобиль
		|		ИЗ
		|			РегистрСведений.Автомобили.СрезПоследних(, Значение ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|				И ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)
		|			) КАК АвтомобилиСрезПоследних
		|	)
		|	ИЛИ ЗаявкаНаРемонт.Автомобиль ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ВЫРАЗИТЬ(ЗаявкаНаРемонт.ГосНомер КАК Строка(50)) ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|";
		
	//Внимание: поиск по карте осуществляется по ее владельцу
	ЗапросНомерКарты = "
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Заказчик В (
		|		ВЫБРАТЬ
		|			ШтрихКоды.Объект
		|		ИЗ
		|			РегистрСведений.ШтрихКоды КАК ШтрихКоды
		|		ГДЕ
		|			ШтрихКоды.ШтрихКод ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|	)
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Заказчик В (
		|		ВЫБРАТЬ
		|			ШтрихКоды.Объект
		|		ИЗ
		|			РегистрСведений.ШтрихКоды КАК ШтрихКоды
		|		ГДЕ
		|			ШтрихКоды.ШтрихКод ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|	)
		|";
				   
	ЗапросVIN = "
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Автомобиль.VIN ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Автомобиль.VIN ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|	ИЛИ ЗаявкаНаРемонт.Автомобиль ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|";

	ЗапросОбъединить = "
		|ОБЪЕДИНИТЬ
		|";  				   
				   
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			КонтактнаяИнформация.Объект КАК Объект
		|		ПОМЕСТИТЬ 
		|			ОбъектыКИ
		|		ИЗ
		|			РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|		ГДЕ
		|			КонтактнаяИнформация.Представление ПОДОБНО ""%"" + &СтрокаПоиска + ""%""
		|	;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|   НайденныеДокументы.ДокументСсылка КАК ДокументСсылка
		|ПОМЕСТИТЬ ВременнаяТаблицаДокументов
		|ИЗ
		|	(";
	
	ЭтоПервыйФрагмент = Истина;
	ЗапросПустой = Истина;
	
	Если НаименованиеКлиента Тогда
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросНаименованиеКлиента;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросНаименованиеКлиента;
		КонецЕсли;
	КонецЕсли;
	
	Если КонтактнаяИнформация Тогда 
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросКонтактнаяИнформация;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросКонтактнаяИнформация;
		КонецЕсли;
	КонецЕсли;
		
	Если ГосНомер Тогда 			  
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросГосНомер;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросГосНомер;
		КонецЕсли;
	КонецЕсли;
	
	Если VIN Тогда 			  
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросVIN;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросVIN;
		КонецЕсли;
	КонецЕсли;
		
	Если НомерКарты Тогда 			  
		ЗапросПустой = Ложь;
		Если ЭтоПервыйФрагмент Тогда
			ТекстЗапроса = ТекстЗапроса + ЗапросНомерКарты;
			ЭтоПервыйФрагмент = Ложь;
		Иначе
			ТекстЗапроса = ТекстЗапроса + ЗапросОбъединить; 
			ТекстЗапроса = ТекстЗапроса + ЗапросНомерКарты;
		КонецЕсли;
	КонецЕсли;
	
	// пустой текст запроса означает, что сняты все флажки в параметрах поиска
	Если ЗапросПустой И (НЕ НомерДокумента) Тогда
		Сообщить("Не заданы параметры поиска. Проверьте настройки поиска!");
		Возврат Неопределено;
	ИначеЕсли ЗапросПустой Тогда
		//Эмуляция того, что на первом этапе поиска ничего не найдено
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЗаказНаряд.Ссылка<>ЗаказНаряд.Ссылка
		|";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = ТекстЗапроса + ") КАК НайденныеДокументы
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|   НайденныеДокументыВсего.ДокументСсылка КАК ДокументСсылка
		|ИЗ
		|	(
		|
		|//Собственно сами ЗН и ЗнР, отобранные на первом этапе
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|ИЗ
		|	ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|
		|ОБЪЕДИНИТЬ
		|
		|//ЗН, у которых искомый номер
		|ВЫБРАТЬ
		|	ЗаказНаряд.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ГДЕ
		|	ЛОЖЬ
		|"+?(НомерДокумента,"ИЛИ ЗаказНаряд.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//ЗнР, у которых искомый номер
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЛОЖЬ
		|"+?(НомерДокумента,"ИЛИ ЗаявкаНаРемонт.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//ЗН, у которого документ-основание ЗнР с искомым номером
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаказНаряд КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ДокументОснование В (
		|		ВЫБРАТЬ
		|			ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|		ИЗ
		|			Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|		ГДЕ
		|			ЛОЖЬ
		|			"+?(НомерДокумента,"ИЛИ ЗаявкаНаРемонт.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|	)
		|
		|ОБЪЕДИНИТЬ
		|
		|//ЗнР, у которого есть подчиненный ЗН с искомым номером
		|ВЫБРАТЬ
		|	ЗаявкаНаРемонт.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		|ГДЕ
		|	ЗаявкаНаРемонт.Ссылка В (
		|		ВЫБРАТЬ
		|			ЗаказНаряд.ДокументОснование КАК ДокументСсылка
		|		ИЗ
		|			Документ.ЗаказНаряд КАК ЗаказНаряд
		|		ГДЕ
		|			ЛОЖЬ
		|			"+?(НомерДокумента,"ИЛИ ЗаказНаряд.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|	)
		|
		|ОБЪЕДИНИТЬ
		|
		|//Перемещение товаров в производство, у которых искомый номер или основание из уже отобранных ЗН на первом этапе
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ПеремещениеТоваровВПроизводство КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ДокументОснование В (
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|		ИЗ
		|			ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|	)
		|"+?(НомерДокумента,"ИЛИ ПодчиненныйДокумент.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%"" ИЛИ ПодчиненныйДокумент.ДокументОснование.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//Извлечение товаров из производства, у которых искомый номер или основание из уже отобранных ЗН на первом этапе
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ИзвлечениеТоваровИзПроизводства КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ДокументОснование В (
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|		ИЗ
		|			ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|	)
		|"+?(НомерДокумента,"ИЛИ ПодчиненныйДокумент.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%"" ИЛИ ПодчиненныйДокумент.ДокументОснование.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//Акт разногласий, у которых искомый номер или основание из уже отобранных ЗН на первом этапе
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.АктРазногласий КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ДокументОснование В (
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|		ИЗ
		|			ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|	)
		|"+?(НомерДокумента,"ИЛИ ПодчиненныйДокумент.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%"" ИЛИ ПодчиненныйДокумент.ДокументОснование.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//Ввод остатоков товаров в производстве, у которых искомый номер или основание из уже отобранных ЗН на первом этапе
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ВводОстатковТоваровВПроизводстве КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ДокументОснование В (
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|		ИЗ
		|			ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|	)
		|"+?(НомерДокумента,"ИЛИ ПодчиненныйДокумент.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%"" ИЛИ ПодчиненныйДокумент.ДокументОснование.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//Разукомплектация автомобилей, у которых искомый номер или основание из уже отобранных ЗН на первом этапе
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.РазукомплектацияАвтомобилей КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ДокументОснование В (
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|		ИЗ
		|			ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|	)
		|"+?(НомерДокумента,"ИЛИ ПодчиненныйДокумент.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%"" ИЛИ ПодчиненныйДокумент.ДокументОснование.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//Перемещение незавершенного производства, у которых искомый номер или основание из уже отобранных ЗН на первом этапе
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ПеремещениеНезавершенногоПроизводства КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ДокументОснование В (
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|		ИЗ
		|			ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|	)
		|"+?(НомерДокумента,"ИЛИ ПодчиненныйДокумент.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%"" ИЛИ ПодчиненныйДокумент.ДокументОснование.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|ОБЪЕДИНИТЬ
		|
		|//Перемещение незавершенного производства, у которых ЗН-приемник из уже отобранных ЗН на первом этапе
		|ВЫБРАТЬ
		|	ПодчиненныйДокумент.Ссылка КАК ДокументСсылка
		|ИЗ
		|	Документ.ПеремещениеНезавершенногоПроизводства КАК ПодчиненныйДокумент
		|ГДЕ
		|	ПодчиненныйДокумент.ЗаказНаряд В (
		|		ВЫБРАТЬ
		|			ВременнаяТаблицаДокументов.ДокументСсылка КАК ДокументСсылка
		|		ИЗ
		|			ВременнаяТаблицаДокументов КАК ВременнаяТаблицаДокументов
		|	)
		|"+?(НомерДокумента,"ИЛИ ПодчиненныйДокумент.ЗаказНаряд.Номер ПОДОБНО ""%"" + &СтрокаПоиска + ""%""","")+"
		|
		|) КАК НайденныеДокументыВсего";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("СтрокаПоиска",СтрокаПоиска);
	
	//ДокументыТЗ = Новый ТаблицаЗначений;
	ДокументыТЗ = Запрос.Выполнить().Выгрузить();
	СписокДокументов = Новый СписокЗначений;
	СписокДокументов.ЗагрузитьЗначения(ДокументыТЗ.ВыгрузитьКолонку("ДокументСсылка"));
	Возврат СписокДокументов;
КонецФункции

Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли


Перем ДоступностьХарактеристик Экспорт; // Доступность характеристик
Перем Права					   Экспорт; // Права пользователя
Перем СкладПоПодразделению Экспорт; //БИЗТЕХ 08.07.2025 КОВАЛЬ ++ //Кеш склада по подразделению

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//Добавление позиции в документ при подборе, вызывается из формы подбора 
//и при стандартной обработке ОбработкеВыбора, в случае подбора по справочнику.
//В документ добавляется элемент справочника Номенклатура, если элемент представляет
//собой Группу справочника или Набор - то добавляются соотв. состаявляющие.
//Добавление в документ производится в основной единице измерения, если не указана иная.
//Если строка с указанной Номенклатурой и Единицей измерения отсутствует, 
//то добавляется новая строка или изменяется количество в противном случае.
//
//Параметры:
//	ЭтаФорма - Форма, из которой произведен вызов.
//	СтруктураПараметровПодбора - Структура, содержащая параметры подбора.
//	Номенклатура - СправочникСсылка.Номенклатура, добавляемая номенклатура (Элемент,Группа,Набор).
//	СпроситьКоличество - Булево, флаг запроса количества при подборе.
//	СпроситьЦену - Булево, флаг запроса цены при подборе.
//	Количество - Число, значение количества по умолчанию.
//	УстановитьТекущей - Булево, флаг необходимости установить добавляемую строку текущей в табличной части документа
//
Функция ДобавитьНоменклатуру(ЭтаФорма, СтруктураПараметровПодбора, Номенклатура, СпроситьКоличество, СпроситьЦену, Количество=1, УстановитьТекущей=ИСТИНА, СпроситьХарактеристику=ЛОЖЬ, Цена=0) Экспорт
	// Если пользователь решил отказаться от продолжения добавления товара
	#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	Если НЕ обЗначениеНеЗаполнено(Номенклатура) Тогда
		Попытка
			РезПроверки = дкПроверитьВозможностьОперацийСНоменклатурой(Номенклатура, ЭтаФорма.ВладелецФормы, ЭтаФорма.ВладелецФормы.ЭтотОбъект);
		Исключение
			Попытка
				// Просто оповестим о выборе!
				ЭтаФорма.ОповеститьОВыборе(Номенклатура);
				Возврат Неопределено;
			Исключение
				//попадаем сюда при добавлении номенклатуры, связанной с автоработой
				ФормаДокумента="";
				СтруктураПараметровПодбора.Свойство("ФормаДокумента",ФормаДокумента);
				Если НЕ обЗначениеНеЗаполнено(ФормаДокумента) Тогда
					РезПроверки = дкПроверитьВозможностьОперацийСНоменклатурой(Номенклатура,ФормаДокумента, ФормаДокумента.ЭтотОбъект);
				Иначе
					Сообщить("Ошибка при добавлении номенклатуры! Не найдена форма-владелец!");
					Возврат Неопределено;
				КонецЕсли;
			КонецПопытки;
		КонецПопытки;
		
		Если (РезПроверки.ЕстьБлок) И НЕ ПустаяСтрока(РезПроверки.Сообщение) Тогда
			Сообщить(РезПроверки.Сообщение, СтатусСообщения.Внимание);
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;

	ОткрытаФормаКоличестваГруппы = Неопределено;
	СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваГруппы", ОткрытаФормаКоличестваГруппы);
	Если ОткрытаФормаКоличестваГруппы = Неопределено Тогда
		ОткрытаФормаКоличестваГруппы = Ложь;
	КонецЕсли;
	
	ОткрытаФормаКоличестваСписка = Неопределено;
	СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваСписка", ОткрытаФормаКоличестваСписка);
	Если ОткрытаФормаКоличестваСписка = Неопределено Тогда
		ОткрытаФормаКоличестваСписка = Ложь;
	КонецЕсли;
	
	ЗапретПодбораНаборов = Неопределено;
	СтруктураПараметровПодбора.Свойство("ЗапретПодбораНаборов", ЗапретПодбораНаборов);
	Если ЗапретПодбораНаборов = Неопределено Тогда
		ЗапретПодбораНаборов = Ложь;
	КонецЕсли;
	
	Попытка
		ПодразделениеКомпании = ЭтаФорма.ПодразделениеКомпании;
	Исключение
		Попытка
			ПодразделениеКомпании = СтруктураПараметровПодбора.ФормаДокумента.ПодразделениеКомпании;
		Исключение
			ПодразделениеКомпании = Неопределено;
		КонецПопытки;
	КонецПопытки;
	
	// Получим имя реквизита табличной части "количество"
	ИмяРеквизитаКоличества = Неопределено;
	СтруктураПараметровПодбора.Свойство("ИмяРеквизитаКоличества", ИмяРеквизитаКоличества);
	Если ИмяРеквизитаКоличества = Неопределено Тогда
		ИмяРеквизитаКоличества = "Количество";
		СтруктураПараметровПодбора.Вставить("ИмяРеквизитаКоличества", ИмяРеквизитаКоличества);
	КонецЕсли;
	
	ЭтоГруппа = Номенклатура.ЭтоГруппа;
	ЭтоНабор  = (НЕ ЭтоГруппа) И (Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор);
	СтруктураПараметровПодбора.Вставить("ЭтоГруппа", ЭтоГруппа);
	СтруктураПараметровПодбора.Вставить("ЭтоНабор",  ЭтоНабор);
	СтруктураПараметровПодбора.Вставить("УстановитьТекущей", УстановитьТекущей);
	
	ЗагрузкаИзВнешнихКаталогов = Неопределено;
	СтруктураПараметровПодбора.Свойство("ЗагрузкаИзВнешнихКатологов", ЗагрузкаИзВнешнихКаталогов);
	ЗагрузкаИзВнешнихКаталогов = ?(ЗагрузкаИзВнешнихКаталогов = Неопределено, Ложь, ЗагрузкаИзВнешнихКаталогов);
	
	//если группа или набор - нужно развернуть, спросим количество один раз для всех
	Если ЭтоГруппа ИЛИ ЭтоНабор Тогда
		ЭтаФорма.МногострочныйВыбор = ЭтоГруппа;
		
		ФормаКоличества = ПолучитьОбщуюФорму("ВводПараметровПодбора");
		ФормаКоличества.ИмяРеквизитаКоличества  = ИмяРеквизитаКоличества;
		ФормаКоличества.ВладелецФормы           = ЭтаФорма;
		ФормаКоличества.ЗакрыватьПриВыборе      = ЛОЖЬ;
		ФормаКоличества.Количество              = Количество;
		ФормаКоличества.Цена                    = 0;
		ФормаКоличества.Сумма                   = 0;
		ФормаКоличества.Заголовок               = "Ввод параметров подбора для "+?(ЭтоГруппа, "группы", "набора")+" """ + Номенклатура + """";
		ФормаКоличества.ЕдиницаИзмерения        = ?(ЭтоГруппа, Неопределено, Номенклатура.ОсновнаяЕдиницаИзмерения);
		ФормаКоличества.ВладелецЕдиницИзмерения = ?(Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения=1, Номенклатура.ТипНоменклатуры, Номенклатура);
		ФормаКоличества.Номенклатура            = Номенклатура;
		ФормаКоличества.СтруктураПараметровПодбора = СтруктураПараметровПодбора;
		ФормаКоличества.ЗапрашиватьХарактеристикуНоменклатуры = Ложь;
		ФормаКоличества.ВидПодбора = 0;
		Если СпроситьКоличество ИЛИ СпроситьЦену ИЛИ СпроситьХарактеристику Тогда
			//перечисляем случаи, когда открывать количество для группы не надо -
			//уже было введено для списка или должно вводиться отдельно для каждого
			Если СпроситьЦену ИЛИ СпроситьХарактеристику ИЛИ ОткрытаФормаКоличестваГруппы ИЛИ ОткрытаФормаКоличестваСписка Тогда
				//не спрашиваем количество для всей группы, 
				//вызываем ВыполнитьОбработкуВыбора, чтобы
				//открыть форму отдельно для каждой номенклатуры
				СтруктураВыбор = Новый Структура("Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры");
				СтруктураВыбор.Количество                 = 1;
				СтруктураВыбор.Цена                       = 0;
				СтруктураВыбор.ЕдиницаИзмерения           = ?(ЭтоГруппа, Неопределено, Номенклатура.ОсновнаяЕдиницаИзмерения);
				СтруктураВыбор.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
				СтруктураВозврата = Новый Структура("СтруктураПараметровПодбора, СтруктураВыбор", СтруктураПараметровПодбора, СтруктураВыбор);
				ВыполнитьОбработкуВыбора(ЭтаФорма, СтруктураВозврата, ФормаКоличества)
			Иначе 
				//спрашиваем только количество для всей группы
				ФормаКоличества.СтруктураПараметровПодбора.Вставить("ОткрытаФормаКоличестваГруппы", Истина);
				ФормаКоличества.Открыть(); // немодально
			КонецЕсли;
		Иначе
			//если не спрашиваем ни количество, ни цену, ни характеристику, 
			//а просто добавляем каждой номенклатуры из группы по 1 штуке
			// форму не открываем, но оповестить необходимо
			СтруктураВыбор = Новый Структура("Количество,Цена,ЕдиницаИзмерения,ХарактеристикаНоменклатуры");
			СтруктураВыбор.Количество                 = Количество;
			СтруктураВыбор.Цена                       = 0;
			СтруктураВыбор.ЕдиницаИзмерения           = ?(ЭтоГруппа, Неопределено, Номенклатура.ОсновнаяЕдиницаИзмерения);
			СтруктураВыбор.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			СтруктураВозврата = Новый Структура("СтруктураПараметровПодбора, СтруктураВыбор", СтруктураПараметровПодбора, СтруктураВыбор);
			ВыполнитьОбработкуВыбора(ЭтаФорма, СтруктураВозврата, ФормаКоличества);
		КонецЕсли;
		
	Иначе //добавляем одиночный элемент
		ОтображениеУслуг = Неопределено;
		СтруктураПараметровПодбора.Свойство("ОтображениеУслуг", ОтображениеУслуг);
		Если ОтображениеУслуг = Неопределено Тогда
			ОтображениеУслуг = Истина;
			СтруктураПараметровПодбора.Вставить("ОтображениеУслуг", ОтображениеУслуг);
		КонецЕсли;
		
		// получим табличную часть документа
		ФормаДокумента = "";
		СтруктураПараметровПодбора.Свойство("ФормаДокумента", ФормаДокумента);
		Если обЗначениеНеЗаполнено(ФормаДокумента) Тогда
			ФормаДокумента=ЭтаФорма.ВладелецФормы;
			СтруктураПараметровПодбора.Вставить("ФормаДокумента", ФормаДокумента);
		КонецЕсли;
		
		ИмяТабличнойЧасти = "";
		СтруктураПараметровПодбора.Свойство("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
		Если обЗначениеНеЗаполнено(ИмяТабличнойЧасти) Тогда
			Попытка
				ТабличнаяЧасть = ФормаДокумента.Товары;
				СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти", "Товары");
				ИмяТабличнойЧасти = "Товары";
			Исключение
				Попытка // Док. Ввод в эксплуатацию (Заполнить по МОЛ), подбор идет не в документ, а в таблицу значений 
						// на форму ФормаЗаполненияПоМОЛ, поэтому пытаемся получить таблицу значений (BazD)
					ТабличнаяЧасть = ФормаДокумента.СписокТоваров;
					СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти","СписокТоваров");
					ИмяТабличнойЧасти = "СписокТоваров";
				Исключение
					Возврат Неопределено;
				КонецПопытки;
			КонецПопытки;
		Иначе
			ТабличнаяЧасть = ФормаДокумента[ИмяТабличнойЧасти];
			СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
		КонецЕсли;
		
		ИмяТабличногоПоля = "";
		СтруктураПараметровПодбора.Свойство("ИмяТабличногоПоля", ИмяТабличногоПоля);
		Если обЗначениеНеЗаполнено(ИмяТабличногоПоля) Тогда
			ИмяТабличногоПоля = "Товары";
			СтруктураПараметровПодбора.Вставить("ИмяТабличногоПоля", ИмяТабличногоПоля);
		КонецЕсли;
		
		// получим виды номенклатуры которые нельзя выбирать
		Попытка БлокироватьВидыНоменклатуры = ФормаДокумента.ЭтотОбъект.ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры(ИмяТабличнойЧасти); Исключение БлокироватьВидыНоменклатуры = Неопределено; КонецПопытки;
		
		// Игнорируем запрещенную номенклатуру
		Если БлокироватьВидыНоменклатуры <> Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры) = Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество() > 0 Тогда
			Попытка ЕстьОшибка=(БлокироватьВидыНоменклатуры[Номенклатура.ВидНоменклатуры] <> Неопределено); Исключение ЕстьОшибка = Ложь; КонецПопытки;
			Если (НЕ ОтображениеУслуг) И (Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга) Тогда ЕстьОшибка = Истина; КонецЕсли;
			Если ЕстьОшибка Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		// Оповестим о выборе документ, инициировавший подбор.
		ОбработкаВДокументе = Ложь;
		СтруктураПараметровПодбора.Свойство("ОбработкаВДокументе", ОбработкаВДокументе);
		Если ОбработкаВДокументе = Неопределено Тогда
			ОбработкаВДокументе = Ложь;
		КонецЕсли;
		
		ОбработкаВКорзине = Ложь;
		Если СтруктураПараметровПодбора.Свойство("ОбработкаВКорзине") Тогда
			ОбработкаВКорзине = СтруктураПараметровПодбора.ОбработкаВКорзине;
		КонецЕсли;
		
		ВариантПоставки = Неопределено;
		СтруктураПараметровПодбора.Свойство("ВариантПоставки", ВариантПоставки);
		
		// если обработка заполнения товара находится в корзине, то передадим выбор туда
		Если ОбработкаВКорзине = Истина Тогда
			// Отправим на обработку в корзину, если нужно будет, то вернемся сюда уже оттуда
			// Ранее могли сформировать НомерДетали
			//НомерДетали = Неопределено;
			//СтруктураПараметровПодбора.Свойство("НомерДетали", НомерДетали);
			//Если НомерДетали = Неопределено Тогда
			//	НомерДетали = Новый Структура;
			//	НомерДетали.Вставить("Источник",             "Клиент");
			//	НомерДетали.Вставить("ИдентификаторЗапроса", "Подбор номенклатуры");
			//	НомерДетали.Вставить("ИмяНабора",            "");
			//	НомерДетали.Вставить("Артикул",              Номенклатура.Артикул);
			//	НомерДетали.Вставить("АртикулДляПоиска",     Номенклатура.АртикулДляПоиска);
			//	НомерДетали.Вставить("Производитель",        Номенклатура.Производитель);
			//	НомерДетали.Вставить("Наименование",         Номенклатура.Наименование);
			//КонецЕсли;
			СтруктураВозврата = Новый Структура;
			СтруктураВозврата.Вставить("СпроситьХарактеристику", СпроситьХарактеристику);
			СтруктураВозврата.Вставить("СпроситьКоличество",     СпроситьКоличество);
			СтруктураВозврата.Вставить("СпроситьЦену",           СпроситьЦену);
			СтруктураВозврата.Вставить("Количество",             Количество);
			//СтруктураВозврата.Вставить("НомерДетали",            НомерДетали);
			СтруктураВозврата.Вставить("Номенклатура",           Номенклатура);
			СтруктураВозврата.Вставить("ВариантПоставки",        ВариантПоставки);
			
			ЭтаФорма.ОповеститьОВыборе(СтруктураВозврата);
			Возврат Неопределено;
		ИначеЕсли ОбработкаВДокументе Тогда
			ЭтаФорма.ОповеститьОВыборе(Номенклатура);
			Возврат Неопределено;
		КонецЕсли;
		
		Если ВариантПоставки <> Неопределено И ВариантПоставки.Свойство("Цена") Тогда
			Цена = ВариантПоставки.Цена;
		КонецЕсли;
		
		ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения;
		
		// поищем выбранную номенклатуру в таблице товаров
		МассивСтрок = Новый Массив();
		Попытка 
			// Особенности АРМ Корзина
			МассивСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура, ВариантПоставки", Номенклатура, Ложь));
		Исключение 
			Попытка МассивСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура));
			Исключение Попытка МассивСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("НоменклатураРасход", Номенклатура)); Исключение; КонецПопытки; КонецПопытки;
		КонецПопытки;
		
		Если СпроситьЦену ИЛИ СпроситьХарактеристику ИЛИ (НЕ ЗагрузкаИзВнешнихКаталогов И СпроситьКоличество И НЕ (ОткрытаФормаКоличестваГруппы ИЛИ ОткрытаФормаКоличестваСписка)) Тогда
			//если что-то нашли нужно заполнить текущие параметры формы подбора
			ВалютаЦены = ЭтаФорма.ВалютаДокумента;
			Если Цена = 0 Тогда
				Попытка
					КонтрагентДляЦены = ЭтаФорма.КонтрагентДляЦены;
				Исключение
					КонтрагентДляЦены = Справочники.Контрагенты.ПустаяСсылка();
				КонецПопытки;
				Попытка
					Цена = ?(ЭтаФорма.ОстаткиИЦеныНа, обПолучитьЦену(ЭтаФорма.ТипЦен, Номенклатура, ЭтаФорма.ДатаРасчетов, КонтрагентДляЦены, ВалютаЦены,, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании), обПолучитьЦену(ЭтаФорма.ТипЦен, Номенклатура,,, ВалютаЦены,, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании));
				Исключение
					Цена = обПолучитьЦену(ЭтаФорма.ТипЦен, Номенклатура,,, ВалютаЦены,, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, ПодразделениеКомпании);
				КонецПопытки;
			КонецЕсли;
			Количество = 1;
			КоличествоСтарое = 0;
			ЦенаСтарая = 0;
			СуммаСтарая = 0;
			ЕдиницаИзмеренияСтарая = Неопределено;
			Если МассивСтрок.Количество() > 0 Тогда
				//если нашли возьмем первое вхождение, чтоб оттуда утянуть единицу
				Если МассивСтрок.Количество() = 1 Тогда
					ТекущаяСтрока = МассивСтрок[0];
					Попытка КоличествоСтарое = ТекущаяСтрока[ИмяРеквизитаКоличества]; Исключение КонецПопытки;
					Попытка ЦенаСтарая = ТекущаяСтрока["Цена"]; Исключение КонецПопытки;
					Попытка СуммаСтарая = ТекущаяСтрока["Сумма"]; Исключение КонецПопытки;
					Попытка ЕдиницаИзмеренияСтарая = ТекущаяСтрока["ЕдиницаИзмерения"]; Исключение КонецПопытки;
					Попытка ЕдиницаИзмерения = ТекущаяСтрока["ЕдиницаИзмерения"]; Исключение КонецПопытки;
				Иначе //заполним по всем строкам и приведем к основной единице
					КоэффициентОсновнойЕдиницы = ?(Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент = 0, 1, Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент);
					Для Каждого ТекущаяСтрока Из МассивСтрок Цикл
						Попытка КоличествоСтарое = КоличествоСтарое + ТекущаяСтрока[ИмяРеквизитаКоличества]*ТекущаяСтрока["Коэффициент"]/КоэффициентОсновнойЕдиницы; Исключение КонецПопытки;
						Попытка СуммаСтарая = СуммаСтарая + ТекущаяСтрока["Сумма"]; Исключение КонецПопытки;
					КонецЦикла;
					ЕдиницаИзмеренияСтарая = Номенклатура.ОсновнаяЕдиницаИзмерения;
					ЦенаСтарая = СуммаСтарая/?(КоличествоСтарое*ЕдиницаИзмеренияСтарая.Коэффициент=0,1,КоличествоСтарое*ЕдиницаИзмеренияСтарая.Коэффициент);
				КонецЕсли;
			КонецЕсли;
			//открываем форму для ввода значений
			ЭтаФорма.МногострочныйВыбор = Ложь;
			ФормаКоличества=ПолучитьОбщуюФорму("ВводПараметровПодбора");
			ФормаКоличества.ИмяРеквизитаКоличества = ИмяРеквизитаКоличества;
			ФормаКоличества.ВладелецФормы          = ЭтаФорма;
			ФормаКоличества.Заголовок              = "Ввод параметров подбора для "+ Номенклатура + """";
			ФормаКоличества.Количество             = Количество;
			ФормаКоличества.Цена                   = Цена;
			ФормаКоличества.Сумма                  = Цена;
			ФормаКоличества.ЕдиницаИзмерения       = ЕдиницаИзмерения;
			ФормаКоличества.ЕдиницаИзмеренияСтарая = ЕдиницаИзмеренияСтарая;
			ФормаКоличества.ЦенаСтарая             = ЦенаСтарая;
			ФормаКоличества.СуммаСтарая            = СуммаСтарая;
			ФормаКоличества.КоличествоСтарое       = КоличествоСтарое;
			ФормаКоличества.Номенклатура           = Номенклатура;
			ФормаКоличества.ВладелецЕдиницИзмерения = ?(Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения=1,Номенклатура.ТипНоменклатуры,Номенклатура);
			ФормаКоличества.ВидПодбора             = 0;
			ФормаКоличества.ЗапрашиватьХарактеристикуНоменклатуры = СпроситьХарактеристику;
			СтруктураПараметровПодбора.Вставить("ТекущаяСтрока", ТекущаяСтрока);
			ФормаКоличества.СтруктураПараметровПодбора = СтруктураПараметровПодбора;
			ФормаКоличества.Открыть(); // немодально
		Иначе
			Если МассивСтрок.Количество() > 0 Тогда
				ТекущаяСтрока = МассивСтрок[0];
			КонецЕсли;
			СтруктураВыбСтрока = Новый Структура("Номенклатура, Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры",
				Номенклатура, Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры);
			СтруктураПараметровПодбора.Вставить("СпроситьКоличество", СпроситьКоличество);
			СтруктураПараметровПодбора.Вставить("СпроситьЦену",       СпроситьЦену);
			ДобавитьСтрокуВДокумент(СтруктураВыбСтрока, СтруктураПараметровПодбора);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущаяСтрока;
	
КонецФункции // ДобавитьНоменклатуру()

// Добавление выбранной строки в документ
// Параметры:
//	СтруктураВыбСтрока		   - структура выбранной строки
//	СтруктураПараметровПодбора - структура параметров подбора
Процедура ДобавитьСтрокуВДокумент(СтруктураВыбСтрока, СтруктураПараметровПодбора) Экспорт
	
	Номенклатура 				= СтруктураВыбСтрока.Номенклатура;
	Количество	 				= Число(СтруктураВыбСтрока.Количество);
	Цена						= СтруктураВыбСтрока.Цена;
	ЕдиницаИзмерения			= СтруктураВыбСтрока.ЕдиницаИзмерения;
	ХарактеристикаНоменклатуры  = СтруктураВыбСтрока.ХарактеристикаНоменклатуры;
	// все необходимое уже использовалось ранее
	ФормаДокумента			= СтруктураПараметровПодбора.ФормаДокумента;
	ИмяТабличнойЧасти       = СтруктураПараметровПодбора.ИмяТабличнойЧасти;
	ИмяТабличногоПоля       = СтруктураПараметровПодбора.ИмяТабличногоПоля;
	ИмяРеквизитаКоличества  = СтруктураПараметровПодбора.ИмяРеквизитаКоличества;
	СпроситьКоличество      = СтруктураПараметровПодбора.СпроситьКоличество;
	СпроситьЦену       		= СтруктураПараметровПодбора.СпроситьЦену;
	УстановитьТекущей   	= СтруктураПараметровПодбора.УстановитьТекущей;
	ТабличнаяЧасть			= ФормаДокумента[ИмяТабличнойЧасти];
	
	//проверка права "РедактированиеДеталейЗаказНаряда"
	Попытка
		Если ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.ЗаказНаряд") ИЛИ
			ТипЗнч(ФормаДокумента.ЭтотОбъект) = Тип("ДокументОбъект.ЗаявкаНаРемонт") Тогда
			РедактированиеНоменклатуры = обПраво("РедактированиеДеталейЗаказНаряда", Права);
			Если НЕ РедактированиеНоменклатуры Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	#Если Клиент Тогда
		Состояние("Добавление элемента: " + Номенклатура);
	#КонецЕсли

	//теперь будем пробовать добавлять в документ
	//для начала поищем строку с выбранной единицей чтоб туда добавится
	Попытка
		// Особенности АРМ Корзина
		МассивСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура, ЕдиницаИзмерения, ХарактеристикаНоменклатуры, ВариантПоставки", Номенклатура, ЕдиницаИзмерения, ХарактеристикаНоменклатуры, Ложь));
	Исключение
		Попытка
			МассивСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура, ЕдиницаИзмерения, ХарактеристикаНоменклатуры", Номенклатура, ЕдиницаИзмерения, ХарактеристикаНоменклатуры));
		Исключение
			Попытка
				МассивСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура));
			Исключение
				МассивСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("НоменклатураРасход, ЕдиницаИзмеренияРасход", Номенклатура, ЕдиницаИзмерения));
			КонецПопытки;
		КонецПопытки;
	КонецПопытки;
	
	НоваяСтрока = (МассивСтрок.Количество() = 0);
	Если НоваяСтрока Тогда
		ТекущаяСтрока = ТабличнаяЧасть.Добавить();
		Попытка
			ТекущаяСтрока.НоменклатураРасход = Номенклатура;
			Попытка
				ДопПараметры = Новый Структура("ЕдиницаИзмеренияРасход", ЕдиницаИзмерения);
				ФормаДокумента.ОбработкаРеквизита("Товары.НоменклатураРасход", ТекущаяСтрока, ФормаДокумента, ДопПараметры);
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".НоменклатураРасход", ТекущаяСтрока, ФормаДокумента, ДопПараметры);
			Исключение КонецПопытки;
			Попытка
				ТекущаяСтрока.ХарактеристикаНоменклатурыРасход = ХарактеристикаНоменклатуры;
			Исключение КонецПопытки;
			Если СпроситьКоличество ИЛИ Количество > 0 Тогда
				Попытка
					ТекущаяСтрока.КоличествоРасход = Количество;
					ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Количество", ТекущаяСтрока, ФормаДокумента);
					//ФормаДокумента.ОбработкаРеквизита("Товары.Количество",ТекущаяСтрока,ФормаДокумента);
				Исключение
				КонецПопытки; 
			КонецЕсли; 
		Исключение
			ТекущаяСтрока.Номенклатура = Номенклатура;
			Попытка
				ТекущаяСтрока.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;
			Исключение КонецПопытки;
			Попытка
				ДопПараметры = Новый Структура("ЕдиницаИзмерения,Количество", ЕдиницаИзмерения, Количество);
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Номенклатура", ТекущаяСтрока, ФормаДокумента, ДопПараметры);
				//ФормаДокумента.ОбработкаРеквизита("Товары.Номенклатура",ТекущаяСтрока,ФормаДокумента, ДопПараметры);
			Исключение КонецПопытки;
			Если СпроситьКоличество ИЛИ Количество > 0 Тогда
				Попытка
					ТекущаяСтрока[ИмяРеквизитаКоличества] = Количество;
					ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Количество", ТекущаяСтрока, ФормаДокумента);
					//ФормаДокумента.ОбработкаРеквизита("Товары.Количество",ТекущаяСтрока,ФормаДокумента);
				Исключение
				КонецПопытки; 
			КонецЕсли; 
		КонецПопытки;
		// для акта и корректировки
		Попытка
			ТекущаяСтрока.ПоДокументуРеализации = Ложь;
			ТекущаяСтрока.Подтверждение         = Истина;
		Исключение
		КонецПопытки;
		Попытка
			// БИЗТЕХ КОВАЛЬ 08.07.2025 ++ 
			// Склад подставлять по складу по подразделению
			//ТекущаяСтрока.СкладКомпании = обПраво("ОсновнойСкладКомпании", Права);
			Попытка
				ТекущаяСтрока.СкладКомпании = ?(ПолучитьСкладПоПодразделению(ФормаДокумента.ПодразделениеКомпании)=Неопределено,обПраво("ОсновнойСкладКомпании",Права),ПолучитьСкладПоПодразделению(ФормаДокумента.ПодразделениеКомпании));
			Исключение
				ТекущаяСтрока.СкладКомпании = обПраво("ОсновнойСкладКомпании", Права);
			КонецПопытки;
			// БИЗТЕХ КОВАЛЬ 08.07.2025 --
		Исключение
		КонецПопытки;
		Попытка
			ТекущаяСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		Исключение
		КонецПопытки;
	Иначе
		ТекущаяСтрока = МассивСтрок[0];
		//количество у нас в той единице что искали, можем смело прибавить
		Попытка
			ТекущаяСтрока[ИмяРеквизитаКоличества] = ТекущаяСтрока[ИмяРеквизитаКоличества] + Количество;
			ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + "." + ИмяРеквизитаКоличества, ТекущаяСтрока, ФормаДокумента);
			//ФормаДокумента.ОбработкаРеквизита("Товары."+ИмяРеквизитаКоличества,ТекущаяСтрока,ФормаДокумента);
		Исключение
			Попытка
				ТекущаяСтрока.КоличествоРасход = ТекущаяСтрока.КоличествоРасход + Количество;
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".КоличествоРасход", ТекущаяСтрока, ФормаДокумента);
				//ФормаДокумента.ОбработкаРеквизита("Товары.КоличествоРасход",ТекущаяСтрока,ФормаДокумента);
			Исключение
			КонецПопытки; 
		КонецПопытки;
	КонецЕсли;
	
	Если СпроситьЦену ИЛИ ЗначениеЗаполнено(Цена) Тогда
		// пересчитаем цену
		Попытка 
			ТекущаяСтрока.Цена = Цена;
			ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Цена", ТекущаяСтрока, ФормаДокумента); 
			//ФормаДокумента.ОбработкаРеквизита("Товары.Цена",ТекущаяСтрока,ФормаДокумента); 
		Исключение 
			Попытка 
				ТекущаяСтрока.ЦенаРасход = Цена;
				ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".ЦенаРасход", ТекущаяСтрока, ФормаДокумента); 
				//ФормаДокумента.ОбработкаРеквизита("Товары.ЦенаРасход",ТекущаяСтрока,ФормаДокумента); 
			Исключение 
			КонецПопытки;
		КонецПопытки;
	КонецЕсли;
	
	ВариантПоставки = Неопределено;
	СтруктураПараметровПодбора.Свойство("ВариантПоставки", ВариантПоставки);
	Если ВариантПоставки <> Неопределено Тогда
		// Для АРМКорзина, ЗаказВнутренний, ЗаказПокупателя, ЗаказПоставщику, СчетНаОплату, ЗаказНаряд, ЗаявкаНаРемонт
		Попытка 
			ТекущаяСтрока.КлючСтрокиПоставщика = ВариантПоставки.КлючСтрокиПоставщика;
			Попытка 
				ТекущаяСтрока.Поставщик           = ВариантПоставки.Поставщик;
				ТекущаяСтрока.СрокПоставкиВСтроке = ВариантПоставки.СрокПоставки;
			Исключение
				// Заказ поставщику
				ТекущаяСтрока.ПрайсЛист           = ВариантПоставки.Поставщик;
				//ТекущаяСтрока.ПримечаниеНоменклатура = ВариантПоставки.СрокПоставки;
			КонецПопытки;
		Исключение КонецПопытки;
	КонецЕсли;
	
	Попытка
		ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".РасчетРазницы", ТекущаяСтрока, ФормаДокумента);
	Исключение
	КонецПопытки;
	
	// АРМ Корзина
	Если СтруктураПараметровПодбора.Свойство("ИдентификаторНомераДетали") Тогда
		Попытка 
			ТекущаяСтрока.ИдентификаторНомераДетали = СтруктураПараметровПодбора.ИдентификаторНомераДетали;
		Исключение КонецПопытки;
	КонецЕсли;
	Если СтруктураПараметровПодбора.Свойство("ИдентификаторГруппыАналогов") Тогда
		Попытка 
			ТекущаяСтрока.ИдентификаторГруппыАналогов = СтруктураПараметровПодбора.ИдентификаторГруппыАналогов;
		Исключение КонецПопытки;
	КонецЕсли;
	
	// Вызовем обработчики изменения в форме
	#Если Клиент Тогда
		// Проверим заполняется табличная часть документа или таблица значений
		// (если таблица значений, то обработчики изменения, нам не нужны)
		Если НЕ ТипЗнч(ФормаДокумента[ИмяТабличнойЧасти]) = Тип("ТаблицаЗначений") Тогда
			// Установим строку текущей
			Если УстановитьТекущей Тогда
				ФормаДокумента.ЭлементыФормы[ИмяТабличногоПоля].ТекущаяСтрока = ТекущаяСтрока;
			КонецЕсли;
	
			ФормаДокумента.Модифицированность = Истина;
			
			ОтменаРедактирования=ЛОЖЬ;
			дкТоварыПриОкончанииРедактирования(ФормаДокумента,ФормаДокумента.ЭлементыФормы[ИмяТабличногоПоля], НоваяСтрока, ОтменаРедактирования);
			дкВывестиЗаголовокСуммаДокумента(ФормаДокумента);
			//Состояние("");
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

//БИЗТЕХ КОВАЛЬ 08.07.2025 ++
Функция ПолучитьСкладПоПодразделению(ПодразделениеКомпании) Экспорт
	Если ЗначениеЗаполнено(СкладПоПодразделению) Тогда
		Возврат СкладПоПодразделению;
	Иначе
		СкладПоПодразделению = РегистрыСведений.СкладПоУмолчаниюПоПодразделению.ПолучитьСкладПоПодразделению(ПодразделениеКомпании);
		Возврат СкладПоПодразделению;
	КонецЕсли;
КонецФункции
//БИЗТЕХ КОВАЛЬ 08.07.2025 --

// выполняет обработку выбора
Процедура ВыполнитьОбработкуВыбора(ЭтаФорма, ЗначениеВыбора, Источник) Экспорт
	
	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		ЭлементыФормы = ЭтаФорма.ЭлементыФормы;
		ФормаКоличества = Источник;
		СтруктураПараметровПодбора = ЗначениеВыбора.СтруктураПараметровПодбора; // входящая структура
		СтруктураВыбор = ЗначениеВыбора.СтруктураВыбор; // выходящая структура
		
		Если СтруктураПараметровПодбора.Свойство("ИмяТабличногоПоляИсточника") Тогда
			НоменклатураДляПодбора = ЭлементыФормы[СтруктураПараметровПодбора.ИмяТабличногоПоляИсточника];
		Иначе
			НоменклатураДляПодбора = ЭлементыФормы.НоменклатураДляПодбора;
		КонецЕсли;
		
		КоличествоВыделенныхСтрок = НоменклатураДляПодбора.ВыделенныеСтроки.Количество();
		Количество = СтруктураВыбор.Количество;
		
		ОткрытаФормаКоличестваГруппы = Неопределено;
		ОткрытаФормаКоличестваСписка = Неопределено;
		ЗаголовокФормыВладельца      = Неопределено;
		СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваГруппы", ОткрытаФормаКоличестваГруппы);
		СтруктураПараметровПодбора.Свойство("ОткрытаФормаКоличестваСписка", ОткрытаФормаКоличестваСписка);
		СтруктураПараметровПодбора.Свойство("ЗаголовокФормыВладельца",      ЗаголовокФормыВладельца);
		
		Если ОткрытаФормаКоличестваГруппы = Неопределено Тогда
			ОткрытаФормаКоличестваГруппы = Ложь;
		КонецЕсли;
		
		Если ОткрытаФормаКоличестваСписка = Неопределено Тогда
			ОткрытаФормаКоличестваСписка = Ложь;
		КонецЕсли;
		
		Если ЗаголовокФормыВладельца = Неопределено Тогда
			ЗаголовокФормыВладельца = "";
		КонецЕсли;
		
		ВыделенныеСтроки = НоменклатураДляПодбора.ВыделенныеСтроки;
		
		Если (КоличествоВыделенныхСтрок > 1) И ОткрытаФормаКоличестваСписка И ЗначениеВыбора.СтруктураПараметровПодбора.Флажок Тогда
			//Добавляем по каждой строке
			//тут количество уже было введено для списка, значит не нужно спрашивать ничего больше,
			//формы тоже не будут открываться
			//флажок указывает, что при следующем вызове ВыполнитьОбработкуВыбора данный код не должен выполниться
			Цена = 0;
			Если СтруктураВыбор.Свойство("Цена") Тогда
				Цена = СтруктураВыбор.Цена;
			КонецЕсли;
			КоличествоДобавлено=0;
			Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				КоличествоДобавлено=КоличествоДобавлено+1;
				СтруктураПараметровПодбора.Вставить("Флажок",Ложь);
				ТекНоменклатура = ?(ТипЗнч(ВыделеннаяСтрока)=Тип("СправочникСсылка.Номенклатура"), ВыделеннаяСтрока, ВыделеннаяСтрока.Номенклатура);
				Если КоличествоДобавлено=КоличествоВыделенныхСтрок Тогда
					УстановитьТекущуюСтроку=Истина;
				Иначе
					УстановитьТекущуюСтроку=Ложь;
				КонецЕсли; 
				ТекущаяСтрока = ДобавитьНоменклатуру(ЭтаФорма,  СтруктураПараметровПодбора, ТекНоменклатура, ЛОЖЬ, ЛОЖЬ, Количество,УстановитьТекущуюСтроку,,Цена);
				СтандартнаяОбработка = ЛОЖЬ;
			КонецЦикла; 
			Флажок = Ложь;
		Иначе 
			Номенклатура = ФормаКоличества.Номенклатура;
			Попытка
				СпроситьКоличество = ЭтаФорма.ЗапрашиватьКоличество;
			Исключение
				СпроситьКоличество = Ложь;
			КонецПопытки;
			
			Если Номенклатура.ЭтоГруппа Тогда
				ДоступностьХарактеристик = Ложь;
				ДоступностьЦены          = Ложь;
			ИначеЕсли Номенклатура.ТипНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор Тогда
				ДоступностьХарактеристик = Ложь;
				ДоступностьЦены          = Ложь;
			Иначе
				ДоступностьХарактеристик = Истина;
				Попытка
					ДоступностьЦены = ЭтаФорма.ОбщаяДоступностьЦены;
				Исключение
					ДоступностьЦены = Ложь;
				КонецПопытки;
			КонецЕсли;
			
			Попытка
				// ++ Alexku 09.12.2016
				//СпроситьЦену = ЭтаФорма.ЗапрашиватьЦену И ДоступностьЦены;
				СпроситьЦену = ЭтаФорма.ЗапрашиватьЦену;
				// -- Alexku 
			Исключение
				СпроситьЦену = Ложь;
			КонецПопытки;
			
			Попытка
				СпроситьХарактеристику = ЭтаФорма.ЗапрашиватьХарактеристикуНоменклатуры И Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик <= 2 И ДоступностьХарактеристик;
			Исключение
				СпроситьХарактеристику = Ложь;
			КонецПопытки;
			// вызов из рекурсии функции ДобавитьНоменклатуру()
			//если группа или набор - нужно развернуть, спросим количество один раз для всех
			ЭтоГруппа = Номенклатура.ЭтоГруппа;
			ЭтоНабор  = СтруктураПараметровПодбора.ЭтоНабор;
			УстановитьТекущей  = СтруктураПараметровПодбора.УстановитьТекущей;
			Если ЭтоГруппа ИЛИ ЭтоНабор Тогда
				ЭтаФорма.МногострочныйВыбор = ЭтоГруппа;
				//разворачиваем наш список
				Если ЭтоГруппа Тогда
					Запрос = Новый Запрос();
					ТекстЗапроса = 
					"ВЫБРАТЬ
					|	ЕСТЬNULL(НоменклатураСоставНабора.Номенклатура, НоменклатураСписок.Ссылка) КАК Ссылка,
					|	СУММА(ВЫБОР
					|			КОГДА НоменклатураСоставНабора.Номенклатура ЕСТЬ NULL 
					|				ТОГДА &Количество
					|			ИНАЧЕ 
					|				ВЫБОР
					|					КОГДА ЕСТЬNULL(НоменклатураСоставНабора.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) = 0
					|						ТОГДА НоменклатураСоставНабора.Количество * &Количество
					|					ИНАЧЕ НоменклатураСоставНабора.Количество * &Количество / НоменклатураСоставНабора.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
					|				КОНЕЦ
					|		КОНЕЦ) КАК Количество
					|ИЗ
					|	Справочник.Номенклатура КАК НоменклатураСписок
					|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.СоставНабора КАК НоменклатураСоставНабора
					|		ПО (НоменклатураСписок.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Набор))
					|			И НоменклатураСоставНабора.Ссылка = НоменклатураСписок.Ссылка
					|ГДЕ
					|	(НЕ НоменклатураСписок.ЭтоГруппа)
					|	И НоменклатураСписок.Родитель В ИЕРАРХИИ(&Родитель)";
					
					Попытка
						ВидыНоменклатуры = ЭтаФорма.СтруктураПараметровФормы.ВидыНоменклатуры;
						ТекстЗапроса = ТекстЗапроса + " И НоменклатураСписок.ВидНоменклатуры.Ссылка В (&ВидыНоменклатуры)";
						Запрос.УстановитьПараметр("ВидыНоменклатуры", ?(ТипЗнч(ВидыНоменклатуры) = Тип("СписокЗначений"),ВидыНоменклатуры.ВыгрузитьЗначения(),ВидыНоменклатуры));
					Исключение
					КонецПопытки;
					
					ТекстЗапроса = ТекстЗапроса + "
					|СГРУППИРОВАТЬ ПО
					|	ЕСТЬNULL(НоменклатураСоставНабора.Номенклатура, НоменклатураСписок.Ссылка)";
					Запрос.Текст = ТекстЗапроса;
					Запрос.УстановитьПараметр("Родитель", Номенклатура);
					Запрос.УстановитьПараметр("Количество", Количество);
					ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
					Для Каждого ТекТовар Из ТаблицаТоваров Цикл
						ТекущаяСтрока = ДобавитьНоменклатуру(ЭтаФорма,  СтруктураПараметровПодбора, ТекТовар.Ссылка, ЛОЖЬ, СпроситьЦену И ДоступностьЦены, ТекТовар.Количество, УстановитьТекущей, Ложь);
					КонецЦикла;
					
				ИначеЕсли ЭтоНабор Тогда
					ЗапретПодбораНаборов = Неопределено;
					СтруктураПараметровПодбора.Свойство("ЗапретПодбораНаборов", ЗапретПодбораНаборов);
					Если ЗапретПодбораНаборов=Неопределено Тогда
						ЗапретПодбораНаборов = Ложь;
					КонецЕсли;
					Если ЗапретПодбораНаборов Тогда 
						#Если Клиент Тогда
						Предупреждение("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(Номенклатура.ВидНоменклатуры)+"""",5);
						#КонецЕсли
					Иначе
						Для Каждого НоменклатураИзНабора Из Номенклатура.СоставНабора Цикл
							ЕдиницаНоменклатурыИзНабора = НоменклатураИзНабора.Номенклатура.ОсновнаяЕдиницаИзмерения;
							ТекущаяСтрока = ДобавитьНоменклатуру(ЭтаФорма, СтруктураПараметровПодбора, НоменклатураИзНабора.Номенклатура, ЛОЖЬ, ЛОЖЬ, НоменклатураИзНабора.Количество*Количество/?(ЕдиницаНоменклатурыИзНабора.Коэффициент=0,1,ЕдиницаНоменклатурыИзНабора.Коэффициент),УстановитьТекущей);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				//добавляем одиночный элемент
			Иначе
				Если СпроситьКоличество ИЛИ СпроситьЦену ИЛИ СпроситьХарактеристику Тогда
					// гот. результат
					Количество 					= СтруктураВыбор.Количество;
					Цена 						= СтруктураВыбор.Цена;
					ЕдиницаИзмерения 			= СтруктураВыбор.ЕдиницаИзмерения;
					ХарактеристикаНоменклатуры 	= СтруктураВыбор.ХарактеристикаНоменклатуры;
				КонецЕсли;
			КонецЕсли;
			
			// поместим в структуру
			Если НЕ ЭтоНабор И НЕ ЭтоГруппа Тогда
				СтруктураВыбСтрока = Новый Структура("Номенклатура, Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры",
					Номенклатура, Количество, Цена, ЕдиницаИзмерения, ХарактеристикаНоменклатуры);
				СтруктураПараметровПодбора.Вставить("СпроситьКоличество", СпроситьКоличество);
				СтруктураПараметровПодбора.Вставить("СпроситьЦену",       СпроситьЦену И ДоступностьЦены);
				СтруктураПараметровПодбора.Вставить("УстановитьТекущей",  УстановитьТекущей);
				ДобавитьСтрокуВДокумент(СтруктураВыбСтрока, СтруктураПараметровПодбора);
			КонецЕсли; 
			
			// ЗАВЕРШЕНИЕ
			// Покажем текущую строку
			Если ТекущаяСтрока<>Неопределено Тогда
				// Получим табличную часть документа
				ФормаДокумента="";
				Если (НЕ СтруктураПараметровПодбора.Свойство("ФормаДокумента",ФормаДокумента))ИЛИ(обЗначениеНеЗаполнено(ФормаДокумента)) Тогда
					ФормаДокумента=ЭтаФорма.ВладелецФормы;
				КонецЕсли;
				ИмяТабличногоПоля="";
				Если (НЕ СтруктураПараметровПодбора.Свойство("ИмяТабличногоПоля",ИмяТабличногоПоля))ИЛИ(обЗначениеНеЗаполнено(ИмяТабличногоПоля)) Тогда
					ИмяТабличногоПоля="Товары";
				КонецЕсли;
				// Установим текущей новую строку
				ФормаДокумента.ЭлементыФормы[ИмяТабличногоПоля].ТекущаяСтрока=ТекущаяСтрока;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Права = Обработки.Защита.Создать().Права; // Кеш прав пользователя для передачи в модули общего назначения

&НаКлиенте
Перем ШрифтЖирный;
&НаКлиенте
Перем Шрифт;

&НаКлиенте
Функция ПолучитьДанныеКоманды(ИмяКоманды,ИмяСтраницы = "")
	
	Если ПустаяСтрока(ИмяСтраницы) Тогда
		ИмяСтраницы = ТекущаяСтраница;
	КонецЕсли;
	
	Результат = Неопределено;
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы,ИмяКнопки",ИмяСтраницы,ИмяКоманды));
	
	Если МассивСтрок.Количество() = 1 Тогда
		
		Результат = МассивСтрок[0];
				
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьНастройкиСтраницы(ИмяСтраницы = "")
	
	Если ИмяСтраницы = "" Тогда
		Возврат Объект.Страницы.НайтиСтроки(Новый Структура("Имя",ТекущаяСтраница));
	Иначе
		Возврат Объект.Страницы.НайтиСтроки(Новый Структура("Имя",ИмяСтраницы));
	КонецЕсли;	
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПараметрыСтраниц()
	
	Страница = Объект.Страницы.Добавить();
	Страница.Имя = "ОсновнаяСтраница";
	Страница.Наименование = "Ресепшен";
	Страница.Заголовок = "Укажите данные клиента:";
		
	Страница = Объект.Страницы.Добавить();
	Страница.Имя = "ВыборМодели";
	Страница.Наименование = "Выберите модель";
	Страница.ПредыдущаяСтраница = "ОсновнаяСтраница";
	Страница.Заголовок = "Выберите модель:";
	ЗаполнитьСтраницуМоделей("ВыборМодели");
	
	Страница = Объект.Страницы.Добавить();
	Страница.Имя = "ВыборИсточника";
	Страница.Наименование = "Выберите источник информации";
	Страница.ПредыдущаяСтраница = "ОсновнаяСтраница";
	Страница.Заголовок = "Укажите источник информации:";
	ЗаполнитьСтраницуИсточников("ВыборИсточника");
	
	Страница = Объект.Страницы.Добавить();
	Страница.Имя = "ВыборСотрудникаСервис";
	Страница.Наименование = "Выберите ответственного сотрудника";
	Страница.ПредыдущаяСтраница = "ОсновнаяСтраница";
	Страница.Заголовок = "Выберите ответственного:";
	ЗаполнитьСтраницыСотрудников("ВыборСотрудникаСервис");
	
	Страница = Объект.Страницы.Добавить();
	Страница.Имя = "ВыборСотрудникаСалон";
	Страница.Наименование = "Выберите ответственного сотрудника";
	Страница.ПредыдущаяСтраница = "ОсновнаяСтраница";
	Страница.Заголовок = "Выберите ответственного:";
	ЗаполнитьСтраницыСотрудников("ВыборСотрудникаСалон");
	
	Страница = Объект.Страницы.Добавить();
	Страница.Имя = "ВводИмени";
	Страница.Наименование = "Введите имя клиента";
	Страница.ПредыдущаяСтраница = "ОсновнаяСтраница";
	
	Страница = Объект.Страницы.Добавить();
	Страница.Имя = "ВыборВидаКонтакта";
	Страница.Наименование = "Выберите вид контакта";
	Страница.ПредыдущаяСтраница = "ОсновнаяСтраница"; 
	Страница.Заголовок = "Укажите вид контакта:";
	ЗаполнитьСтраницуВидовКонтакта("ВыборВидаКонтакта");	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраницуВидовКонтакта(ИмяСтраницы)
	
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ВидыСобытий.ТелефонныйЗвонок);
	Массив.Добавить(Перечисления.ВидыСобытий.ЛичнаяВстреча);
	
	Сч = 0;
	
	Для Каждого ЭлементМассива ИЗ Массив Цикл
		
		Сч = Сч + 1;
		
		НоваяСтрока = Объект.КнопкиСтраниц.Добавить();
		НоваяСтрока.ИмяСтраницы = ИмяСтраницы;
		НоваяСтрока.ИмяКнопки = "Команда" + Сч;
		НоваяСтрока.Действие = "ВыбратьВидКонтакта";
		НоваяСтрока.ДанныеКнопки = ЭлементМассива;
		НоваяСтрока.НаименованиеКнопки  = ОбработатьСтроку(Строка(ЭлементМассива));           
		
	КонецЦикла;
	
	ДобавитьКнопкуНазад(ИмяСтраницы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОбработатьСтроку(ЗНАЧ Строка)
	
	Строка = СокрЛП(Строка);
	Строка = СтрЗаменить(Строка," ", Символы.ПС);
	
	Возврат Строка;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСтраницуМоделей(ИмяСтраницы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
	|	МобильныйМодели.Модель КАК ДанныеКнопки,
	|	МобильныйМодели.Модель.Наименование КАК НаименованиеКнопки
	|ИЗ
	|	ПланОбмена.Ресепшен.Модели КАК МобильныйМодели
	|ГДЕ
	|	МобильныйМодели.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ПланыОбмена.Ресепшен.ЭтотУзел().Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Сч = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Сч = Сч + 1;
		
		НоваяСтрока = Объект.КнопкиСтраниц.Добавить();
		НоваяСтрока.ИмяСтраницы = ИмяСтраницы;
		НоваяСтрока.ИмяКнопки = "Команда" + Сч;
		НоваяСтрока.Действие = "ВыбратьМодель";
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
	КонецЦикла;
	
	ДобавитьКнопкуНазад(ИмяСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКнопкуНазад(ИмяСтраницы)
	
	НоваяСтрока = Объект.КнопкиСтраниц.Добавить();
	НоваяСтрока.ИмяСтраницы = ИмяСтраницы;
	НоваяСтрока.ИмяКнопки = "Команда12";
	НоваяСтрока.Действие = "ПерейтиНазад";
	НоваяСтрока.НаименованиеКнопки = "Назад";
	
КонецПроцедуры
	
&НаСервере
Процедура ЗаполнитьСтраницуИсточников(ИмяСтраницы)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
	|	МобильныйИсточникиИнформации.ИсточникИнформации.Ссылка КАК ДанныеКнопки,
	|	МобильныйИсточникиИнформации.ИсточникИнформации.Наименование КАК НаименованиеКнопки
	|ИЗ
	|	ПланОбмена.Ресепшен.ИсточникиИнформации КАК МобильныйИсточникиИнформации
	|ГДЕ
	|	МобильныйИсточникиИнформации.Ссылка = &Ссылка";				   
	Запрос.УстановитьПараметр("Ссылка",ПланыОбмена.Ресепшен.ЭтотУзел().Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Сч = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Сч = Сч + 1;
		
		НоваяСтрока = Объект.КнопкиСтраниц.Добавить();
		НоваяСтрока.ИмяСтраницы = ИмяСтраницы;
		НоваяСтрока.ИмяКнопки = "Команда" + Сч;
		НоваяСтрока.Действие = "ВыбратьИсточник";
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
	КонецЦикла;
	
	ДобавитьКнопкуНазад(ИмяСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраницыСотрудников(ИмяСтраницы)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
	|	Сотрудники.Ссылка КАК ДанныеКнопки,
	|	Сотрудники.Наименование КАК НаименованиеКнопки
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.Ресепшен.Должности КАК МобильныйДолжности
	|		ПО Сотрудники.Должность = МобильныйДолжности.Должность
	|			И (НЕ Сотрудники.ФлагУволен)
	|			И (НЕ Сотрудники.ПометкаУдаления)
	|			И (МобильныйДолжности.Ссылка = &Ссылка)
	|			И (МобильныйДолжности.ПредметОбращения = &ПредметОбращения)";
	Запрос.УстановитьПараметр("Ссылка",ПланыОбмена.Ресепшен.ЭтотУзел().Ссылка);
	Если ИмяСтраницы = "ВыборСотрудникаСалон" Тогда
		Запрос.УстановитьПараметр("ПредметОбращения",Справочники.ПредметыОбращения.Автосалон);
	ИначеЕсли ИмяСтраницы = "ВыборСотрудникаСервис" Тогда
		Запрос.УстановитьПараметр("ПредметОбращения",Справочники.ПредметыОбращения.Сервис);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Сч = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Сч = Сч + 1;
		
		НоваяСтрока = Объект.КнопкиСтраниц.Добавить();
		НоваяСтрока.ИмяСтраницы = ИмяСтраницы;
		НоваяСтрока.ИмяКнопки = "Команда" + Сч;
		НоваяСтрока.Действие = "ВыбратьСотрудника";
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
	КонецЦикла;
	
	ДобавитьКнопкуНазад(ИмяСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКнопкиИзМакета()
	
	МакетКнопок = Обработки.АРМТонкийРесепшен.ПолучитьМакет("КнопкиСтраниц");
	
	Сч = 1;
	
	Пока Истина Цикл 
		
		Область = МакетКнопок.Область(Сч,1);
		
		НазваниеСтраницы = СокрЛП(Область.Текст);
		
		Если ПустаяСтрока(НазваниеСтраницы) Тогда
			Прервать;
		КонецЕсли;
		
		НоваяСтрока = Объект.КнопкиСтраниц.Добавить();
		НоваяСтрока.ИмяСтраницы = СокрЛП(МакетКнопок.Область(Сч,1).Текст); 
		НоваяСтрока.ИмяКнопки = СокрЛП(МакетКнопок.Область(Сч,2).Текст);
		НоваяСтрока.НаименованиеКнопки = СокрЛП(МакетКнопок.Область(Сч,3).Текст);
		НоваяСтрока.Действие = СокрЛП(МакетКнопок.Область(Сч,4).Текст);
		
		Сч = Сч + 1;
		
	КонецЦикла;

	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПараметрыСтраниц();
	ЗаполнитьКнопкиИзМакета();
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Шрифт = Новый Шрифт(Элементы.Команда10.Шрифт,,,Ложь);
	ШрифтЖирный = Новый Шрифт(Элементы.Команда10.Шрифт,,,Истина);
	
	Если Объект.Страницы.Количество() > 0 Тогда
		ВывестиСтраницу(Объект.Страницы[0].Имя);
	Иначе
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНазад()
	
	ВывестиСтраницу(ПолучитьНастройкиСтраницы()[0].ПредыдущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтраницу(ИмяСтраницы)
		
	Для Сч = 1 По 12 Цикл
		
		ИмяЭлемента = "Команда"+Сч;
		Элементы[ИмяЭлемента].Доступность = Ложь;
		Элементы[ИмяЭлемента].Заголовок = " ";
		Элементы[ИмяЭлемента].Пометка = Ложь;
		Элементы[ИмяЭлемента].Шрифт = Шрифт;
		
	КонецЦикла;
		
	МассивКнопок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы",ИмяСтраницы));
	
	Для Каждого СтрокаКнопки ИЗ МассивКнопок Цикл
		
		Элементы[СтрокаКнопки.ИмяКнопки].Доступность = Истина;
		Элементы[СтрокаКнопки.ИмяКнопки].Заголовок = СтрокаКнопки.НаименованиеКнопки;
		
		Если СтрокаКнопки.Пометка Тогда
			Элементы[СтрокаКнопки.ИмяКнопки].Пометка = Истина;
			Элементы[СтрокаКнопки.ИмяКнопки].Шрифт = ШрифтЖирный;
		КонецЕсли;
		
		
	КонецЦикла;
	
	УстановитьДействияКоманд(ИмяСтраницы);

	ОбработатьВыводТекста(ИмяСтраницы); 
	
	ТекущаяСтраница = ИмяСтраницы;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьДействияКоманд(ИмяСтраницы)
	
	МассивКнопок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы",ИмяСтраницы));
	
	Для Каждого СтрокаКнопки ИЗ МассивКнопок Цикл
		
		Если НЕ ПустаяСтрока(СтрокаКнопки.Действие) Тогда
			ЭтаФорма.Команды[СтрокаКнопки.ИмяКнопки].Действие = СтрокаКнопки.Действие;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыводТекста(ИмяСтраницы)
		
	Если ИмяСтраницы = "ВводТелефона" Тогда
		Элементы.Текст.Доступность = Истина;
		Элементы.Текст.ТолькоПросмотр = Ложь;
		Элементы.Текст.Маска = "+7 (999) 999-99-99 9999";
		Текст = СтрокаТелефона;
		ЭтаФорма.ТекущийЭлемент = Элементы.Текст;
	ИначеЕсли ИмяСтраницы = "ВводИмени" Тогда
		Элементы.Текст.Доступность = Истина;
		Элементы.Текст.ТолькоПросмотр = Ложь;
		Элементы.Текст.Маска = "";
		Текст = Клиент;
		ЭтаФорма.ТекущийЭлемент = Элементы.Текст;
	Иначе
		Элементы.Текст.Доступность = Ложь;
		Элементы.Текст.ТолькоПросмотр = Истина;
		Элементы.Текст.Маска = "";
		Текст = ПолучитьНастройкиСтраницы(ИмяСтраницы)[0].Заголовок;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыделитьКнопку(ИмяКнопки)
	
	Элементы[ИмяКнопки].Шрифт = ШрифтЖирный;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделениеКнопки(ИмяКнопки)
	
	Элементы[ИмяКнопки].Шрифт = Шрифт;
	
КонецПроцедуры

Процедура СнятьВыделениеНаСтранице(ИмяСтраницы,КнопкаИсключение = "")
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы",ИмяСтраницы));
	
	Для Каждого СтрокаКнопки ИЗ МассивСтрок Цикл
		Если НЕ СтрокаКнопки.ИмяКнопки = КнопкаИсключение Тогда                          
			СтрокаКнопки.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
//////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ОчиститьДанные()
	
	ОчиститьДанныеФормы();	
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиЧислоТекстом(Элемент)
	
	ИмяКоманды = Элемент.Имя;
	
	ДанныеКоманды = ПолучитьДанныеКоманды(ИмяКоманды);
	
	Если ДанныеКоманды <> Неопределено Тогда
		
		Текст = Текст + ДанныеКоманды.НаименованиеКнопки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСимвол()
	
	ДлинаТекста = СтрДлина(Текст);
	
	Если ДлинаТекста > 0 Тогда
		Текст = Лев(Текст,ДлинаТекста-1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСалон()
	
	Элементы.Команда1.Пометка = НЕ Элементы.Команда1.Пометка;
	
	Если Элементы.Команда1.Пометка Тогда
		Элементы.Команда2.Пометка = Ложь;
		ВыделитьКнопку("Команда1");
		СнятьВыделениеКнопки("Команда2");
		ПредметОбращения = ПредопределенноеЗначение("Справочник.ПредметыОбращения.Автосалон");
	Иначе
		СнятьВыделениеКнопки("Команда1");
		ПредметОбращения = Неопределено;
	КонецЕсли;
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда1","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		ДанныеКоманды.Пометка = Элементы.Команда1.Пометка;		
	КонецЕсли;
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда2","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		ДанныеКоманды.Пометка = Элементы.Команда2.Пометка;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСервис()
	
	Элементы.Команда2.Пометка = НЕ Элементы.Команда2.Пометка;
	
	Если Элементы.Команда2.Пометка Тогда
		Элементы.Команда1.Пометка = Ложь;
		ВыделитьКнопку("Команда2");
		СнятьВыделениеКнопки("Команда1");
		ПредметОбращения = ПредопределенноеЗначение("Справочник.ПредметыОбращения.Сервис");
	Иначе
		СнятьВыделениеКнопки("Команда2");
		ПредметОбращения = Неопределено;
	КонецЕсли;
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда1","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		ДанныеКоманды.Пометка = Элементы.Команда1.Пометка;		
	КонецЕсли;
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда2","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		ДанныеКоманды.Пометка = Элементы.Команда2.Пометка;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКМоделям()
	
	ВывестиСтраницу("ВыборМодели");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКИсточникам()
	
	ВывестиСтраницу("ВыборИсточника");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЮ()
	
	Элементы.Команда9.Пометка = НЕ Элементы.Команда9.Пометка;
	
	Если Элементы.Команда9.Пометка Тогда
		ВыделитьКнопку("Команда9");
	Иначе
		СнятьВыделениеКнопки("Команда9");
	КонецЕсли;
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда9","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		ДанныеКоманды.Пометка = Элементы.Команда9.Пометка;		
	КонецЕсли;
	
	ЮридическоеЛицо = Элементы.Команда9.Пометка;

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКИМени()
		
	ВывестиСтраницу("ВводИмени");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКТелефону()
	
	ВывестиСтраницу("ВводТелефона");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВторичный()
	
	Элементы.Команда10.Пометка = НЕ Элементы.Команда10.Пометка;
	
	Если Элементы.Команда10.Пометка Тогда
		ВыделитьКнопку("Команда10");
	Иначе
		СнятьВыделениеКнопки("Команда10");
	КонецЕсли;
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда10","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		ДанныеКоманды.Пометка = Элементы.Команда10.Пометка;		
	КонецЕсли;
	
	ВторичныйКонтакт = Элементы.Команда10.Пометка;

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСотрудникам()
	
	Если Элементы.Команда1.Пометка Тогда
		ВывестиСтраницу("ВыборСотрудникаСалон"); 
	ИначеЕсли Элементы.Команда2.Пометка Тогда
		ВывестиСтраницу("ВыборСотрудникаСервис");
	Иначе
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ПерейтиКСотрудникамЗавершение", ЭтаФорма), "Выберите салон/сервис");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСотрудникамЗавершение(ДополнительныеПараметры) Экспорт
    
    

КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанные(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьДанныеЗавершение", ЭтаФорма, Новый Структура("Оповещение,Результат"));
	Оповещение.ДополнительныеПараметры.Оповещение = Оповещение;
	ПроверитьЗаполненность(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанныеЗавершение(Результат, ДополнительныеПараметры) Экспорт
        
    Если Результат Тогда
        
        СохранитьДанныеНаСервере();
        
        ОчиститьДанныеФормы();
        
    КонецЕсли;
    
КонецПроцедуры  

&НаКлиенте
Процедура ОчиститьДанныеФормы()
	
	СнятьВыделениеНаСтранице("ВыборМодели");
	СнятьВыделениеНаСтранице("ВыборИсточника");
	СнятьВыделениеНаСтранице("ВыборСотрудникаСервис");
	СнятьВыделениеНаСтранице("ВыборСотрудникаСалон");
	СнятьВыделениеНаСтранице("ОсновнаяСтраница");
	СнятьВыделениеНаСтранице("ВыборВидаКонтакта");
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы,ИмяКнопки","ОсновнаяСтраница","Команда3"));
	МассивСтрок[0].НаименованиеКнопки = "Модель";
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы,ИмяКнопки","ОсновнаяСтраница","Команда4"));
	МассивСтрок[0].НаименованиеКнопки = "Источник";
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы,ИмяКнопки","ОсновнаяСтраница","Команда11"));
	МассивСтрок[0].НаименованиеКнопки = "Сотрудник";
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы,ИмяКнопки","ОсновнаяСтраница","Команда5"));
	МассивСтрок[0].НаименованиеКнопки = "Имя";
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы,ИмяКнопки","ОсновнаяСтраница","Команда6"));
	МассивСтрок[0].НаименованиеКнопки = "Телефон";
	
	МассивСтрок = Объект.КнопкиСтраниц.НайтиСтроки(Новый Структура("ИмяСтраницы,ИмяКнопки","ОсновнаяСтраница","Команда7"));
	МассивСтрок[0].НаименованиеКнопки = "Вид контакта";

	ПредметОбращения = Неопределено;
	Клиент = "";
	Модель = Неопределено;
	Реклама = Неопределено;
	Телефон = "";
	ЮридическоеЛицо = Ложь;
	ВторичныйКонтакт = Ложь;
	Сотрудник = Неопределено;
	СтрокаТелефона = "";
	ВидКонтакта = Неопределено;
	
	ВывестиСтраницу("ОсновнаяСтраница");
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеНаСервере()
	
	МенеджерЗаписи = РегистрыСведений.РегистрацияЗвонковИКонтактов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ДатаВремя = ТекущаяДата();
	МенеджерЗаписи.ДатаДень = ТекущаяДата();
	МенеджерЗаписи.Автор = ПараметрыСеанса.Пользователь;
	МенеджерЗаписи.Сотрудник = ПараметрыСеанса.Пользователь.Сотрудник;
	МенеджерЗаписи.Клиент = Клиент;
	МенеджерЗаписи.ВидТелефона = Справочники.ВидыКонтактнойИнформации.ТелефонСотовый;
	МенеджерЗаписи.Модель = Модель;
	МенеджерЗаписи.ПредставлениеТелефона = ПредставлениеТелефона;
	МенеджерЗаписи.Реклама = Реклама;
	МенеджерЗаписи.Участник = Сотрудник;
	МенеджерЗаписи.ЮридическоеЛицо = ЮридическоеЛицо;
	МенеджерЗаписи.ВторичныйКонтакт = ВторичныйКонтакт;
	МенеджерЗаписи.ПредметОбращения = ПредметОбращения;
	МенеджерЗаписи.ВидКонтакта = ВидКонтакта;
	МенеджерЗаписи.Организация = ПланыОбмена.Ресепшен.ЭтотУзел().Организация;
	МенеджерЗаписи.Подразделение = ПланыОбмена.Ресепшен.ЭтотУзел().Подразделение;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполненность(Знач Оповещение)
	
	Результат = Истина;
	
	СообщениеОбОшибке = "";
	
	Если НЕ ЗначениеЗаполнено(Клиент) Тогда
		Результат = Ложь;
		СообщениеОбОшибке = "Не указан клиент!" + Символы.ПС;
	КонецЕсли;
	
	//Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
	//	Результат = Ложь;
	//	СообщениеОбОшибке = "Не указан ответственный сотрудник!" + Символы.ПС;
	//КонецЕсли;
	
	Если НЕ ПустаяСтрока(СообщениеОбОшибке) Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ПроверитьЗаполненностьЗавершение", ЭтаФорма, Новый Структура("Оповещение, Результат", Оповещение, Результат)), СообщениеОбОшибке);
        Возврат;
	КонецЕсли;
	
	ПроверитьЗаполненностьФрагмент(Оповещение, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполненностьЗавершение(ДополнительныеПараметры) Экспорт
    
    Оповещение = ДополнительныеПараметры.Оповещение;
    Результат = ДополнительныеПараметры.Результат;
    
    ПроверитьЗаполненностьФрагмент(Оповещение, Результат);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполненностьФрагмент(Знач Оповещение, Знач Результат)
    
    ВыполнитьОбработкуОповещения(Оповещение, Результат);
    Возврат;

КонецПроцедуры

&НаКлиенте
Процедура ВвестиТелефон()
	
	ПредставлениеТелефона = СОКРЛП(Элементы.Текст.ТекстРедактирования);
	СтрокаТелефона = Текст;
	
	Текст = "";
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда6","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		
		Если НЕ ПустаяСтрока(СтрокаТелефона) Тогда
			ДанныеКоманды.НаименованиеКнопки = ПредставлениеТелефона;
			ДанныеКоманды.Пометка = Истина;
		Иначе
			ДанныеКоманды.НаименованиеКнопки = "Телефон";
			ДанныеКоманды.Пометка = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ВывестиСтраницу("ОсновнаяСтраница");
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиИмя()
	
	Клиент = СОКРЛП(Текст);
	
	Текст = "";
	
	ДанныеКоманды = ПолучитьДанныеКоманды("Команда5","ОсновнаяСтраница");
	
	Если ДанныеКоманды <> Неопределено Тогда
		
		Если Не ПустаяСтрока(Клиент) Тогда
			ДанныеКоманды.НаименованиеКнопки = Клиент;
			ДанныеКоманды.Пометка = Истина;
		Иначе
			ДанныеКоманды.НаименованиеКнопки = "Имя";
			ДанныеКоманды.Пометка = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ВывестиСтраницу("ОсновнаяСтраница");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМодель(Элемент)
	
	ИмяКоманды = Элемент.Имя;
	
	ДанныеКоманды = ПолучитьДанныеКоманды(ИмяКоманды);
	
	Если ДанныеКоманды <> Неопределено Тогда
		
		ДанныеКоманды.Пометка = Не ДанныеКоманды.Пометка;
		
		ИмяМодели = "";
		
		Если ДанныеКоманды.Пометка Тогда 
			Модель = ДанныеКоманды.ДанныеКнопки;
			ИмяМодели = ДанныеКоманды.НаименованиеКнопки;
			СнятьВыделениеНаСтранице(ТекущаяСтраница,ДанныеКоманды.ИмяКнопки);
		Иначе                                    
			СнятьВыделениеКнопки(ИмяКоманды);
			Модель = Неопределено;
		КонецЕсли;
		
		ДанныеКоманды = ПолучитьДанныеКоманды("Команда3","ОсновнаяСтраница");
		
		Если ДанныеКоманды <> Неопределено Тогда
			
			Если ЗначениеЗаполнено(Модель) Тогда
				ДанныеКоманды.НаименованиеКнопки = ИмяМодели;
				ДанныеКоманды.Пометка = Истина;
			Иначе
				ДанныеКоманды.НаименованиеКнопки = "Модель";
				ДанныеКоманды.Пометка = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		ВывестиСтраницу("ОсновнаяСтраница");
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИсточник(Элемент)
	
	ИмяКоманды = Элемент.Имя;

	ДанныеКоманды = ПолучитьДанныеКоманды(ИмяКоманды);
	
	Если ДанныеКоманды <> Неопределено Тогда
		
		ДанныеКоманды.Пометка = Не ДанныеКоманды.Пометка;
		
		ИмяРекламы = "";
		
		Если ДанныеКоманды.Пометка Тогда 
			Реклама = ДанныеКоманды.ДанныеКнопки;
			ИмяРекламы = ДанныеКоманды.НаименованиеКнопки;
			СнятьВыделениеНаСтранице(ТекущаяСтраница,ДанныеКоманды.ИмяКнопки);
		Иначе                                   
			СнятьВыделениеКнопки(ИмяКоманды);
			Реклама = Неопределено;
		КонецЕсли;
		
		ДанныеКоманды = ПолучитьДанныеКоманды("Команда4","ОсновнаяСтраница");
		
		Если ДанныеКоманды <> Неопределено Тогда
			
			Если ЗначениеЗаполнено(Реклама) Тогда
				ДанныеКоманды.НаименованиеКнопки = ИмяРекламы;
				ДанныеКоманды.Пометка = Истина;
			Иначе
				ДанныеКоманды.НаименованиеКнопки = "Источник";
				ДанныеКоманды.Пометка = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		ВывестиСтраницу("ОсновнаяСтраница");
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСотрудника(Элемент)
	
	ИмяКоманды = Элемент.Имя;

	ДанныеКоманды = ПолучитьДанныеКоманды(ИмяКоманды);
	                                              
	Если ДанныеКоманды <> Неопределено Тогда
		
		ДанныеКоманды.Пометка = Не ДанныеКоманды.Пометка;
		
		ИмяСотрудника = "";
		
		Если ДанныеКоманды.Пометка Тогда 
			Сотрудник = ДанныеКоманды.ДанныеКнопки;
			ИмяСотрудника = ДанныеКоманды.НаименованиеКнопки;
			СнятьВыделениеНаСтранице(ТекущаяСтраница,ДанныеКоманды.ИмяКнопки);
		Иначе
			СнятьВыделениеКнопки(ИмяКоманды);
			Сотрудник = Неопределено;
		КонецЕсли;
		
		ДанныеКоманды = ПолучитьДанныеКоманды("Команда11","ОсновнаяСтраница");
		
		Если ДанныеКоманды <> Неопределено Тогда
			
			Если ЗначениеЗаполнено(Сотрудник) Тогда
				ДанныеКоманды.НаименованиеКнопки = ИмяСотрудника;
				ДанныеКоманды.Пометка = Истина;
			Иначе
				ДанныеКоманды.НаименованиеКнопки = "Сотрудник";
				ДанныеКоманды.Пометка = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		ВывестиСтраницу("ОсновнаяСтраница");
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекст()
	
	Текст = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиККонтактам(Элемент)
	
	ВывестиСтраницу("ВыборВидаКонтакта");	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидКонтакта(Элемент)
	
	ИмяКоманды = Элемент.Имя;
	
	ДанныеКоманды = ПолучитьДанныеКоманды(ИмяКоманды);
	
	Если ДанныеКоманды <> Неопределено Тогда
		
		ДанныеКоманды.Пометка = Не ДанныеКоманды.Пометка;
		
		ИмяВидаКонтакта = "";
		
		Если ДанныеКоманды.Пометка Тогда 
			ВидКонтакта = ДанныеКоманды.ДанныеКнопки;
			ИмяВидаКонтакта = ДанныеКоманды.НаименованиеКнопки;
			СнятьВыделениеНаСтранице(ТекущаяСтраница,ДанныеКоманды.ИмяКнопки);
		Иначе                                    
			СнятьВыделениеКнопки(ИмяКоманды);
			ВидКонтакта = Неопределено;
		КонецЕсли;
		
		ДанныеКоманды = ПолучитьДанныеКоманды("Команда7","ОсновнаяСтраница");
		
		Если ДанныеКоманды <> Неопределено Тогда
			
			Если ЗначениеЗаполнено(ВидКонтакта) Тогда
				ДанныеКоманды.НаименованиеКнопки = ОбработатьСтроку(ВидКонтакта);
				ДанныеКоманды.Пометка = Истина;
			Иначе
				ДанныеКоманды.НаименованиеКнопки = "Вид контакта";
				ДанныеКоманды.Пометка = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		ВывестиСтраницу("ОсновнаяСтраница");
	
	КонецЕсли;
	
КонецПроцедуры
//////////////////////////////////////////////////////////////////////

 //Описание основной структуры данных
 
 //1. Список страниц обработки  и порядок их следования
 
 //2. Список кнопок страницы
 
 //3. Параметры кнопки 
 
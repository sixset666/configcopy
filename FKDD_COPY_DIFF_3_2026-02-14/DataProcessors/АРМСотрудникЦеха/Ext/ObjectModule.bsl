
// определение статуса текущего сотрудника
Процедура УстановитьСтатусСотрудника() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегистрацияВремениРаботСрезПоследних.Период КАК Период,
	|	РегистрацияВремениРаботСрезПоследних.Сотрудник,
	|	РегистрацияВремениРаботСрезПоследних.ВидОтметки,
	|	РегистрацияВремениРаботСрезПоследних.ЗаказНаряд,
	|	РегистрацияВремениРаботСрезПоследних.КонецРаботы,
	|	РегистрацияВремениРаботСрезПоследних.Продолжительность,
	|	РегистрацияВремениРаботСрезПоследних.ПакетРабот,
	|	РегистрацияВремениРаботСрезПоследних.РабочееМесто
	|ИЗ
	|	РегистрСведений.РегистрацияВремениРабот.СрезПоследних(
	|			,
	|			Сотрудник = &Сотрудник
	|				И Продолжительность = 0) КАК РегистрацияВремениРаботСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	Запрос.УстановитьПараметр("Сотрудник", СотрудникАвторизирован);
	ТекущиеРаботы = Запрос.Выполнить().Выгрузить();
	
	
	Если ТекущиеРаботы.Количество() = 0 Тогда
		Состояние = Справочники.ВидыОтметокВремени.НерабочееВремя;
	Иначе
		Состояние = ТекущиеРаботы[0].ВидОтметки;
	КонецЕсли;
	
	Если Состояние <> Справочники.ВидыОтметокВремени.НерабочееВремя Тогда
		ЗаказНаряд   = ТекущиеРаботы[0].ЗаказНаряд;
		РабочееМесто = ТекущиеРаботы[0].РабочееМесто;
		Автомобиль   = ЗаказНаряд.Автомобиль;
		
		ТаблицаРабот = урвзфПолучитьРаботыПоПакету(ТекущиеРаботы[0].ЗаказНаряд,ТекущиеРаботы[0].ПакетРабот);
		Работы.Загрузить(ТаблицаРабот);
		ТекПакетРабот = ТекущиеРаботы[0].ПакетРабот;
	КонецЕсли;
КонецПроцедуры

// получение данных для ЗН
Функция ПолучитьСпсиокЗНПоФильтру(Фильтр)
	
	ЗапросПоШК =
	"ВЫБРАТЬ
	|	ШтрихКоды.Объект КАК ЗаказНаряд
	|ИЗ
	|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	|ГДЕ
	|	ШтрихКоды.ШтрихКод = &ФильтрШК";
	
	//И ЗаказНарядРаботы.Ссылка.Номер ПОДОБНО &Фильтр
	ЗапросПоНомеруЗН =
	"ВЫБРАТЬ
	|	ЗаказНаряд.Ссылка КАК ЗаказНаряд
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Номер ПОДОБНО &Фильтр";
	
	ЗапросПоКлиенту =
	"ВЫБРАТЬ
	|	ЗаказНаряд.Ссылка КАК ЗаказНаряд
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Контрагент.Наименование ПОДОБНО &Фильтр
	|	ИЛИ ЗаказНаряд.Контрагент.НаименованиеПолное ПОДОБНО &Фильтр";
	
	ЗапросПоГосНомеру =
	"ВЫБРАТЬ
	|	ЗаказНаряд.Ссылка  КАК ЗаказНаряд
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Автомобиль В
	|			(ВЫБРАТЬ
	|				АвтомобилиСрезПоследних.Автомобиль
	|			ИЗ
	|				РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК АвтомобилиСрезПоследних
	|			ГДЕ
	|				АвтомобилиСрезПоследних.Значение ПОДОБНО &Фильтр)";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	ЗапросПоШК +
	"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|"
	+ЗапросПоНомеруЗН +
	"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|"
	+ЗапросПоКлиенту +
	"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|"
	+ЗапросПоГосНомеру;
	
	Запрос.УстановитьПараметр("Фильтр"   , "%" + СокрЛП(Фильтр) + "%");
	Запрос.УстановитьПараметр("ФильтрШК" , СокрЛП(Фильтр));
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗаказНаряд");
КонецФункции

Функция ПолучитьЗапланированныйПостДляПакета(ЗаказНарядПлан, Пакет) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЗаказНаряд"  , ЗаказНарядПлан);
	Запрос.УстановитьПараметр("ПакетРабот"  , Пакет);
	Запрос.УстановитьПараметр("Заявка"      , ЗаказНарядПлан.ДокументОснование);
	Запрос.УстановитьПараметр("Исполнитель" , СотрудникАвторизирован);
	
	// получим список работ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗаказНарядРаботы.Работа,
	|	ЗаказНарядРаботы.ИдентификаторРаботы
	|ПОМЕСТИТЬ Работы
	|ИЗ
	|	Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
	|ГДЕ
	|	ЗаказНарядРаботы.Ссылка = &ЗаказНаряд
	|	И ЗаказНарядРаботы.ПакетРабот = &ПакетРабот
	|;";
	Если ЗначениеЗаполнено(ЗаказНарядПлан.ДокументОснование) И ЗаказНарядПлан.ДокументОснование.ХозОперация = Справочники.ХозОперации.ПланРемонта Тогда
		// получаем из планирования
		ТекстЗапроса = ТекстЗапроса +
		"
		|ВЫБРАТЬ
		|	ГрафикРаботыРесурсов.Ресурс1 КАК Цех
		|ИЗ
		|	Работы КАК Работы,
		|	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
		|ГДЕ
		|	ГрафикРаботыРесурсов.Объект = &Заявка
		|	И ГрафикРаботыРесурсов.Авторабота В
		|			(ВЫБРАТЬ
		|				Работы.Работа
		|			ИЗ
		|				Работы КАК Работы)
		|	И ГрафикРаботыРесурсов.Ресурс2 = &Исполнитель";
	Иначе
		// получим из зн
		ТекстЗапроса = ТекстЗапроса +
		"
		|ВЫБРАТЬ
		|	ЗаказНарядИсполнители.Цех
		|ИЗ
		|	Документ.ЗаказНаряд.Исполнители КАК ЗаказНарядИсполнители
		|ГДЕ
		|	ЗаказНарядИсполнители.Исполнитель = &Исполнитель
		|	И ЗаказНарядИсполнители.ИдентификаторРаботы В
		|			(ВЫБРАТЬ
		|				Работы.ИдентификаторРаботы
		|			ИЗ
		|				Работы КАК Работы)
		|	И ЗаказНарядИсполнители.Ссылка = &ЗаказНаряд";
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапрса = Запрос.Выполнить();
	Если РезультатЗапрса.Пустой() Тогда
		Возврат Справочники.Цеха.ПустаяСсылка();
	КонецЕсли;
	
	Возврат РезультатЗапрса.Выгрузить()[0].Цех;
КонецФункции

// поиск по ЗН
Процедура ЗаполнитьСписокЗН(Фильтр = Неопределено) Экспорт
	СписокЗН = Неопределено;
	ПакетыЗаказНаряда.Очистить();
	
	Если ВыводитьПакетыТолькоПриНаличииОтбора И НЕ ЗначениеЗаполнено(Фильтр) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Фильтр) Тогда
		СписокЗН = ПолучитьСпсиокЗНПоФильтру(Фильтр);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказНарядРаботы.Ссылка КАК ЗаказНаряд,
	|	ЗаказНарядРаботы.Ссылка.ВидРемонта,
	|	ЗаказНарядРаботы.Ссылка.Автомобиль,
	|	ЗаказНарядРаботы.ПакетРабот КАК ИдентификаторПакета,
	|	ЗаказНарядРаботы.НомерПакета КАК НомерПакета
	|ИЗ
	|	Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
	|ГДЕ
	|	ЗаказНарядРаботы.Ссылка.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
	|	И ЗаказНарядРаботы.Ссылка.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)
	|	И ЗаказНарядРаботы.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ЗаказНарядРаботы.ПакетЗакрыт = ЛОЖЬ
	|	И ЗаказНарядРаботы.Ссылка.ПодразделениеКомпании В ИЕРАРХИИ (&ПодразделениеСотрудник)
	|	//ФИЛЬТР
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНарядРаботы.Ссылка.ВидРемонта,
	|	ЗаказНарядРаботы.Ссылка.Автомобиль,
	|	ЗаказНарядРаботы.Ссылка,
	|	ЗаказНарядРаботы.ПакетРабот,
	|	ЗаказНарядРаботы.НомерПакета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказНарядРаботы.Ссылка.МоментВремени
	|ИТОГИ
	|	МАКСИМУМ(НомерПакета)
	|ПО
	|	ЗаказНаряд,
	|	ИдентификаторПакета";
	
	Если СписокЗН <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ФИЛЬТР", "И ЗаказНарядРаботы.Ссылка В (&СписокЗН)");
		Запрос.УстановитьПараметр("СписокЗН", СписокЗН);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПодразделениеСотрудник", СотрудникАвторизирован.Подразделение);
	ВыборкаЗаказы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока ВыборкаЗаказы.Следующий() Цикл
		ВыборкаПакеты =  ВыборкаЗаказы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока ВыборкаПакеты.Следующий() Цикл
			НоваяСтрока = ПакетыЗаказНаряда.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПакеты);
			Если ПустаяСтрока(ВыборкаПакеты.ИдентификаторПакета) Тогда
				НоваяСтрока.НомерПакета = "-";
			Иначе
				НоваяСтрока.НомерПакета = ВыборкаПакеты.НомерПакета;
			КонецЕсли;
			
			НоваяСтрока.ЗаказНарядПредставление     = дкПолучитьНомерДляПечати(НоваяСтрока.ЗаказНаряд);
			НоваяСтрока.ЗаказНарядПредставлениеДаты = Формат(НоваяСтрока.ЗаказНаряд.Дата, "ДФ=dd.MM.yyyy");
			
			НоваяСтрока.ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(НоваяСтрока.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
			НоваяСтрока.Модель   = НоваяСтрока.Автомобиль.Модель;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура НачатьНовыйИнтервал(НовоеСостояние) Экспорт
	
	урвзфНачатьНовыйИнтервал(НовоеСостояние, СотрудникАвторизирован, ЗаказНаряд, ТекПакетРабот, РабочееМесто);
	Состояние = НовоеСостояние;
	
КонецПроцедуры

Процедура ЗаполнитьСписокЗНДляРедактирования(Фильтр=Неопределено) Экспорт
	СписокЗН = Неопределено;
	
	ЗаказНаряды.Очистить();
	
	Если ВыводитьПакетыТолькоПриНаличииОтбора И НЕ ЗначениеЗаполнено(Фильтр) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Фильтр) Тогда
		СписокЗН = ПолучитьСпсиокЗНПоФильтру(Фильтр);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказНарядРаботы.Ссылка КАК ЗаказНаряд
	|ИЗ
	|	Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
	|ГДЕ
	|	ЗаказНарядРаботы.Ссылка.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
	|	И ЗаказНарядРаботы.Ссылка.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)
	|	И ЗаказНарядРаботы.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ЗаказНарядРаботы.ПакетЗакрыт = ЛОЖЬ
	|	И ЗаказНарядРаботы.Ссылка.ПодразделениеКомпании В ИЕРАРХИИ (&ПодразделениеСотрудник)
	|//ФИЛЬТР
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНарядРаботы.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказНарядРаботы.Ссылка.МоментВремени";
	
	Если СписокЗН <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ФИЛЬТР" , "И ЗаказНарядРаботы.Ссылка В(&СписокЗН)");
		Запрос.УстановитьПараметр("СписокЗН", СписокЗН);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПодразделениеСотрудник", СотрудникАвторизирован.Подразделение);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ЗаказНаряды.Добавить();
		НоваяСтрока.ЗаказНаряд = Выборка.ЗаказНаряд;
		НоваяСтрока.ЗаказНарядПредставление = дкПолучитьНомерДляПечати(НоваяСтрока.ЗаказНаряд);
		НоваяСтрока.ДатаПредставление = Формат(НоваяСтрока.ЗаказНаряд.Дата,"ДФ=dd.MM.yyyy");
		НоваяСтрока.ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(НоваяСтрока.ЗаказНаряд.Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
		НоваяСтрока.Модель = НоваяСтрока.ЗаказНаряд.Автомобиль.Модель;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьПланирование() Экспорт
	
	Планирование.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ГрафикРаботыРесурсов.Объект.Автомобиль ССЫЛКА Справочник.Модели
	|			ТОГДА ГрафикРаботыРесурсов.Объект.Автомобиль
	|		ИНАЧЕ ГрафикРаботыРесурсов.Объект.Автомобиль.Модель
	|	КОНЕЦ КАК Модель,
	|	ГрафикРаботыРесурсов.НачалоРабочегоВремени КАК Время,
	|	ГрафикРаботыРесурсов.Объект.ВидРемонта КАК ВидРемонта,
	|	ЕСТЬNULL(АвтомобилиСрезПоследних.Значение, ""Не указано"") КАК ГосНомер
	|ИЗ
	|	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(&Дата, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК АвтомобилиСрезПоследних
	|		ПО ГрафикРаботыРесурсов.Объект.Автомобиль = АвтомобилиСрезПоследних.Автомобиль
	|ГДЕ
	|	ГрафикРаботыРесурсов.Ресурс2 = &Сотрудник
	|	И ГрафикРаботыРесурсов.Объект ССЫЛКА Документ.ЗаявкаНаРемонт
	|	И ГрафикРаботыРесурсов.Дата = &Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Время";
	Запрос.УстановитьПараметр("Сотрудник" , СотрудникАвторизирован);
	запрос.УстановитьПараметр("Дата"      , ДатаОтчета);
	
	Планирование.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

// заполняшки
Процедура ЗаполнитьСписокСотрудников(Заголовок, Процент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияВремениРаботСрезПоследних.Сотрудник,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА РегистрацияВремениРаботСрезПоследних.ВидОтметки = ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.Работа)
	|					И РегистрацияВремениРаботСрезПоследних.Продолжительность = 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК Работает
	|ИЗ
	|	РегистрСведений.РегистрацияВремениРабот.СрезПоследних КАК РегистрацияВремениРаботСрезПоследних
	|ГДЕ
	|	РегистрацияВремениРаботСрезПоследних.ВидОтметки <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.НерабочееВремя)
	|	И РегистрацияВремениРаботСрезПоследних.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И РегистрацияВремениРаботСрезПоследних.Сотрудник.Подразделение В ИЕРАРХИИ(&Подразделение)
	|	И РегистрацияВремениРаботСрезПоследних.Сотрудник.ПометкаУдаления = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияВремениРаботСрезПоследних.Сотрудник";
	Запрос.УстановитьПараметр("ДатаНач"       , НачалоДня(ТекущаяДата()));
	запрос.УстановитьПараметр("ДатаКон"       , КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("Подразделение" , ПараметрыСеанса.ПодразделениеКомпании);
	
	ТабСотрудников = Запрос.Выполнить().Выгрузить();
	ВсегоСотрудников    = ТабСотрудников.Количество();
	РаботаетСотрудников = ТабСотрудников.НайтиСтроки(Новый Структура("Работает",Истина)).Количество();
	
	Если ВсегоСотрудников = 0 Тогда
		Заголовок = "0 из 0";
		Процент   = "0%";
	Иначе
		Заголовок = Строка(РаботаетСотрудников)+" из "+Строка(ВсегоСотрудников);
		Процент = Строка(Формат(РаботаетСотрудников/ВсегоСотрудников*100,"ЧДЦ=0; ЧН=0")+"%");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСписокРабочихМест(Заголовок, Процент) Экспорт
	
	// получим список рабочих мест
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦехаРодители.Ссылка КАК Цех,
	|	Цеха.Ссылка КАК Цех2
	|ПОМЕСТИТЬ Цеха
	|ИЗ
	|	Справочник.Цеха КАК ЦехаРодители
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Цеха КАК Цеха
	|		ПО ЦехаРодители.Ссылка = Цеха.Родитель
	|ГДЕ
	|	ЕСТЬNULL(Цеха.Ссылка, 1) = 1
	|;
	|ВЫБРАТЬ
	|	Цеха.Цех КАК Цех,
	|	Цеха.Цех.ГрафикРаботы КАК ГрафикРаботы
	|ИЗ
	|	Цеха КАК Цеха
	|ГДЕ
	|	Цеха.Цех.ИспользуетсяВУРВ = ИСТИНА
	|	И Цеха.Цех.ПометкаУдаления = ЛОЖЬ
	|	И Цеха.Цех.Подразделение В ИЕРАРХИИ(&Подразделение)";
	Запрос.УстановитьПараметр("Подразделение", ПараметрыСеанса.ПодразделениеКомпании);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	РабочиеГрафики = ЗаполнитьГрафикиРабот(Результат.Скопировать());
	СегодняРаботают = Новый Массив;
	
	Для Каждого Цех Из Результат Цикл
		Если РабочиеГрафики.Найти(Цех.ГрафикРаботы) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СегодняРаботают.Добавить(Цех.Цех);
	КонецЦикла;
	
	ВсегоРабочихМест = СегодняРаботают.Количество();
	
	// получим текущую ситуацию
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияВремениРаботСрезПоследних.РабочееМесто
	|ИЗ
	|	РегистрСведений.РегистрацияВремениРабот.СрезПоследних(, ) КАК РегистрацияВремениРаботСрезПоследних
	|ГДЕ
	|	РегистрацияВремениРаботСрезПоследних.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И РегистрацияВремениРаботСрезПоследних.ВидОтметки <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.НерабочееВремя)
	|	И РегистрацияВремениРаботСрезПоследних.Продолжительность = 0
	|	И РегистрацияВремениРаботСрезПоследних.ЗаказНаряд <> ЗНАЧЕНИЕ(Документ.ЗаказНаряд.ПустаяСсылка)
	|	И РегистрацияВремениРаботСрезПоследних.РабочееМесто.Подразделение В ИЕРАРХИИ(&Подразделение)
	|	И РегистрацияВремениРаботСрезПоследних.РабочееМесто.ПометкаУдаления = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияВремениРаботСрезПоследних.РабочееМесто";
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ТекущаяДата()));
	
	ВыборкаПостовВРаботе = Запрос.Выполнить().Выбрать();
	
	ЦеховРаботает = ВыборкаПостовВРаботе.Количество();
	
	Если ВсегоРабочихМест = 0 Тогда
		Заголовок = "0 из 0";
		Процент = "0%";
	Иначе
		Заголовок = Строка(ЦеховРаботает)+" из "+Строка(ВсегоРабочихМест);
		Процент = Строка(Формат(ЦеховРаботает/ВсегоРабочихМест*100,"ЧДЦ=0; ЧН=0")+"%");
	КонецЕсли;
	
	//заполним список свободных цехов
	СостояниеЦехов.Очистить();
	
	Для Каждого Цех Из СегодняРаботают Цикл
		Если ВыборкаПостовВРаботе.НайтиСледующий(Новый Структура("РабочееМесто",Цех)) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока               = СостояниеЦехов.Добавить();
		НоваяСТрока.Цех           = Цех;
		НоваяСтрока.Состояние     = "Свободен";
		НоваяСтрока.Представление = Цех;
	КонецЦикла;
КонецПроцедуры

Функция ЗаполнитьГрафикиРабот(Знач Графики)
	СегодняРаботают = Новый Массив;
	
	Графики.Свернуть("ГрафикРаботы");
	
	// создадим отбор
	ВидыИнтервалов = Новый Массив;
	ВыборкаИнтервалов = Справочники.ВидыИнтервалов.Выбрать();
	Пока ВыборкаИнтервалов.Следующий() Цикл
		Если ВыборкаИнтервалов.РабочийИнтервал Тогда
			ВидыИнтервалов.Добавить(ВыборкаИнтервалов.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ВидыДней = Новый Массив;
	ВидыДней.Добавить(Перечисления.ВидДня.Рабочий);
	ВидыДней.Добавить(Перечисления.ВидДня.Предпраздничный);
	
	Для Каждого График Из Графики Цикл
		ТаблицаГрафика = кпПолучитьГрафик(График.ГрафикРаботы, НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата()), ВидыИнтервалов, ВидыДней);
		Если ТаблицаГрафика.Количество() > 0 Тогда
			СегодняРаботают.Добавить(График.ГрафикРаботы);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СегодняРаботают;
КонецФункции

Процедура ЗаполнитьСостоянияСотрудников() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияВремениРаботСрезПоследних.Сотрудник,
	|	РегистрацияВремениРаботСрезПоследних.ВидОтметки КАК Состояние,
	|	РегистрацияВремениРаботСрезПоследних.Период КАК Начало,
	|	(ВЫБОР
	|		КОГДА РегистрацияВремениРаботСрезПоследних.ВидОтметки = ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.Работа)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ) КАК Работает
	|ИЗ
	|	РегистрСведений.РегистрацияВремениРабот.СрезПоследних(, ) КАК РегистрацияВремениРаботСрезПоследних
	|ГДЕ
	|	РегистрацияВремениРаботСрезПоследних.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И РегистрацияВремениРаботСрезПоследних.ВидОтметки <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.НерабочееВремя)
	|	И РегистрацияВремениРаботСрезПоследних.Продолжительность = 0
	|	И РегистрацияВремениРаботСрезПоследних.Сотрудник.Подразделение В ИЕРАРХИИ(&Подразделение)
	|	И РегистрацияВремениРаботСрезПоследних.Сотрудник.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начало";
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("Подразделение", ПараметрыСеанса.ПодразделениеКомпании);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	СтрокиНеРабочих = РезультатЗапроса.НайтиСтроки(Новый Структура("Работает", Ложь));
	СостояниеСотрудников.Очистить();
	
	Для Каждого Строка Из СтрокиНеРабочих Цикл
		
		НоваяСтрока = СостояниеСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
		НоваяСтрока.СостояниеПредставление =  спПолучитьНаименование(НоваяСтрока.Сотрудник) + " - " + спПолучитьНаименование(НоваяСтрока.Состояние) +" (с " + Формат(НоваяСтрока.Начало,"ДФ=H:mm") + ")";
		
	КонецЦикла;
	
	// дополним список опоздавшими
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА Сотрудники.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|		ТОГДА
	|			ВЫБОР
	|				КОГДА Сотрудники.Цех.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|				ТОГДА Сотрудники.Подразделение.ГрафикРаботы
	|				ИНАЧЕ Сотрудники.Цех.ГрафикРаботы
	|			КОНЕЦ
	|		ИНАЧЕ Сотрудники.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РегистрацияВремениРабот.Сотрудник КАК Сотрудник
	|		ИЗ
	|			РегистрСведений.РегистрацияВремениРабот КАК РегистрацияВремениРабот
	|		ГДЕ
	|			РегистрацияВремениРабот.Период МЕЖДУ &ДатаНач И &ДатаКон
	|			И РегистрацияВремениРабот.Сотрудник.Подразделение В ИЕРАРХИИ(&Подразделение)) КАК ВложенныйЗапрос
	|		ПО Сотрудники.Ссылка = ВложенныйЗапрос.Сотрудник
	|ГДЕ
	|	Сотрудники.Подразделение В ИЕРАРХИИ(&Подразделение)
	|	И ВложенныйЗапрос.Сотрудник ЕСТЬ NULL
	|	И Сотрудники.Исполнитель     = ИСТИНА
	|	И Сотрудники.ФлагУволен      = ЛОЖЬ
	|	И Сотрудники.ПометкаУдаления = ЛОЖЬ
	|	И Сотрудники.ЭтоГруппа       = ЛОЖЬ";
	
	СотрудникиНеНаРаботе = Запрос.Выполнить().Выгрузить();
	
	ГрафикиСотрудников = ЗаполнитьГрафикиРабот(СотрудникиНеНаРаботе.Скопировать());
	ГрафикиСОпозданием = Новый Массив;
	ТекущееВремя = Дата(Формат(ТекущаяДата(),"ДФ=00010101HHmmss"));
	Для Каждого График Из ГрафикиСотрудников Цикл
		Если График.НачалоРабочегоВремениПоУмолчанию < ТекущееВремя И ТекущееВремя < График.КонецРабочегоВремениПоУмолчанию Тогда
			ГрафикиСОпозданием.Добавить(График);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Сотрудник Из СотрудникиНеНаРаботе Цикл
		Если ГрафикиСОпозданием.Найти(Сотрудник.ГрафикРаботы) <> Неопределено Тогда
			Разница = ТекущееВремя - График.НачалоРабочегоВремениПоУмолчанию;
			ЧасовОпоздания = (Разница - Разница%(3600))/3600;
			МинутОпоздания = ((Разница - 3600*ЧасовОпоздания) - (Разница - 3600*ЧасовОпоздания)%60)/60;
			ВремяОпоздания = "" + ?(ЧасовОпоздания = 0,"",Строка(ЧасовОпоздания) + " ч ") + Строка(МинутОпоздания);
			
			НоваяСтрока = СостояниеСотрудников.Добавить();
			НоваяСтрока.СостояниеПредставление =  спПолучитьНаименование(Сотрудник.Сотрудник)
			+ " - опаздывает на " + ВремяОпоздания + " м.";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьПоказателиСтраницыБлокировок() Экспорт
	СтруктураРезультат = Новый Структура;
	
	// Записано на сегодня
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГрафикРаботыРесурсов.Объект КАК ЗаявкаНаРемонт
	|ИЗ
	|	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	|ГДЕ
	|	ГрафикРаботыРесурсов.Дата = &Дата
	|	И ГрафикРаботыРесурсов.Объект ССЫЛКА Документ.ЗаявкаНаРемонт
	|	И ГрафикРаботыРесурсов.Объект.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикРаботыРесурсов.Объект";
	Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПараметрыСеанса.ПодразделениеКомпании);
	
	ТаблицаЗаявок = Запрос.Выполнить().Выгрузить();
	СтруктураРезультат.Вставить("Записано", ТаблицаЗаявок.Количество());
	
	// приехало по записи
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказНаряд.Автомобиль
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.ДокументОснование В(&СписокОснований)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаряд.Автомобиль";
	Запрос.УстановитьПараметр("СписокОснований", ТаблицаЗаявок.ВыгрузитьКолонку("ЗаявкаНаРемонт"));
	
	ТаблицаПриехавшихАвтомобилей = Запрос.Выполнить().Выгрузить();
	СтруктураРезультат.Вставить("ПриехалоПоЗаписи", ТаблицаПриехавшихАвтомобилей.Количество());
	
	// составим таблицу заказ наряды по записи
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказНаряд.Ссылка КАК ЗаказНаряд,
	|	ЗаказНаряд.Автомобиль КАК Автомобиль
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	(ЗаказНаряд.ДокументОснование В (&СписокОснований)
	|			ИЛИ ЗаказНаряд.Автомобиль В (&СписокАвтомобилей)
	|				И (ЗаказНаряд.ДатаСоздания МЕЖДУ &Дата И &ДатаКон
	|					И ЗаказНаряд.ДокументОснование = НЕОПРЕДЕЛЕНО))
	|ИТОГИ ПО
	|	Автомобиль";
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("СписокАвтомобилей", ТаблицаПриехавшихАвтомобилей.ВыгрузитьКолонку("Автомобиль"));
	
	РезультатЗапросаЗаказНарядыПоЗаписи = Запрос.Выполнить();
	
	// Работы
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВложенныйЗапрос.ВидОтметки = ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.Работа)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК ЕстьПростой,
	|	ВложенныйЗапрос.ЗаказНаряд.Автомобиль КАК Автомобиль
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияВремениРабот.ЗаказНаряд КАК ЗаказНаряд,
	|		РегистрацияВремениРабот.ВидОтметки КАК ВидОтметки,
	|		РегистрацияВремениРабот.ПакетРабот КАК ПакетРабот
	|	ИЗ
	|		РегистрСведений.РегистрацияВремениРабот КАК РегистрацияВремениРабот
	|	ГДЕ
	|		РегистрацияВремениРабот.Продолжительность = 0
	|		И РегистрацияВремениРабот.ВидОтметки <> ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.НеРабочееВремя)
	|		И РегистрацияВремениРабот.ЗаказНаряд В(&СписокЗН)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РегистрацияВремениРабот.ЗаказНаряд,
	|		РегистрацияВремениРабот.ПакетРабот,
	|		РегистрацияВремениРабот.ВидОтметки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ЗаказНаряд.Автомобиль";
	Запрос.УстановитьПараметр("СписокЗН", РезультатЗапросаЗаказНарядыПоЗаписи.Выгрузить().ВыгрузитьКолонку("ЗаказНаряд"));
	Отметки = Запрос.Выполнить().Выгрузить();
	
	СтруктураРезультат.Вставить("ВРаботе", Отметки.НайтиСтроки(Новый Структура("ЕстьПростой",Ложь)).Количество());
	СтруктураРезультат.Вставить("ВПростое", Отметки.Количество() - СтруктураРезультат.ВРаботе);
	
	
	// Ожидают выполнения
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(РегистрацияВремениРаботСрезПоследних.Продолжительность, -1) = -1
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьНеСтартованные,
	|	ЗаказНарядРаботы.Ссылка.Автомобиль КАК Автомобиль
	|ИЗ
	|	Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияВремениРабот.СрезПоследних(, ЗаказНаряд В (&СписокЗН)) КАК РегистрацияВремениРаботСрезПоследних
	|		ПО ЗаказНарядРаботы.Ссылка = РегистрацияВремениРаботСрезПоследних.ЗаказНаряд
	|			И ЗаказНарядРаботы.ПакетРабот = РегистрацияВремениРаботСрезПоследних.ПакетРабот
	|ГДЕ
	|	ЗаказНарядРаботы.Ссылка В(&СписокЗН)
	|	И НЕ(ЗаказНарядРаботы.Ссылка.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
	|				ИЛИ ЗаказНарядРаботы.Ссылка.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт))
	|	И НЕ ЗаказНарядРаботы.Ссылка.Автомобиль В(&СписокАвтоСОтметками)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНарядРаботы.Ссылка.Автомобиль";

	Запрос.УстановитьПараметр("СписокАвтоСОтметками", Отметки.ВыгрузитьКолонку("Автомобиль"));
	
	Ожидают = Запрос.Выполнить().Выгрузить();
	СтруктураРезультат.Вставить("ОжидаютВыполнения", Ожидают.НайтиСтроки(Новый Структура("ЕстьНеСтартованные", Истина)).Количество());
	
	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказНаряд.Автомобиль,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаказНаряд.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьВыполненные
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Ссылка В(&СписокЗН)
	|	И (ЗаказНаряд.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен)
	|	ИЛИ ЗаказНаряд.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаряд.Автомобиль";
	
	ВыполненныИЗакрыты = Запрос.Выполнить().Выгрузить();
	СтруктураРезультат.Вставить("Выполнено" , ВыполненныИЗакрыты.НайтиСтроки(Новый Структура("ЕстьВыполненные", Истина)).Количество());
	СтруктураРезультат.Вставить("Выдано"    , ВыполненныИЗакрыты.Количество() - СтруктураРезультат.Выполнено);
	
	Возврат СтруктураРезультат;
КонецФункции
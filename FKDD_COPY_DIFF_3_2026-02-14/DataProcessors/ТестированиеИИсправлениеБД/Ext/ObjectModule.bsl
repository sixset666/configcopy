#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// Модуль обработки тестирования и исправления базы данных

Перем ОтчетБаланс;              //Объект отчет баланса
Перем КоллекцияКартинок Экспорт;//Коллекция картинок
Перем ФормаИндикатора Экспорт;  //Форма индикатора
Перем ТаблицаОшибок;			//таблица значений ошибок объекта
Перем СписокУровней;			//список уровней дерева для объекта
Перем ВидТекущегоОбъекта;       //вид текущего объекта
Перем ТипТекущегоОбъекта;       //тип текущего объекта
Перем МаксУровень;				//максимальный уровень вложенности дерева
Перем ПоПодсистемам Экспорт;	//вариант отображения дерева фильтра Истина - по подсистемам, Ложь - по видам объектов
Перем СписокИсключений Экспорт; //Список наименований подсистем, которые не требуется выводить в дерево


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// функция дополняет строку многострочного сообщения новым сообщением
//
// Параметры:
//  Ошибки    - Строка - содержит сообщение об ошибках,
//  ЕщеСтрока - Строка - содержит сообщение об ошибках.
//
// Возвращаемое значение:
//  Ошибки - Строка.
//
Функция ДобавитьОшибку(Ошибки="",ЕщеСтрока="") 
	Ошибки=?(Ошибки="",ЕщеСтрока,Ошибки+Символы.ПС+ЕщеСтрока);
	Возврат(Ошибки);
КонецФункции // ДобавитьОшибку()

// запись информации об ошибке в таблицу ошибок объекта
//
// Параметры:
//  ВидОбъекта  - вид проверяемого объекта,
//  Объект      - проверяемый объект, 
//  ВидПроверки - Строка - название текущего действия,
//  Ошибка      - Строка - список ошибок.
//
Функция ЗаписатьОшибку(ВидОбъекта,Объект,ВидПроверки,Ошибка)
	Если НЕ ПустаяСтрока(Ошибка) Тогда
		НоваяСтрока = ТаблицаОшибок.Добавить();
		НоваяСтрока.ВидПроверки = ВидПроверки;
		НоваяСтрока.ОписаниеОшибки = Ошибка;
		НоваяСтрока.ВидОбъекта = ВидОбъекта;
		НоваяСтрока.ТипОбъекта = ТипТекущегоОбъекта;
		НоваяСтрока.Ссылка = Объект.Ссылка;
	КонецЕсли;
КонецФункции // ЗаписатьОшибку()

// Если есть ошибки - записывает их в таблицу ошибок или в журнал регистрации
//
// Параметры:
//  Объект - проверяемый объект.
//
Процедура ЗаписатьОшибки(Объект)
	Если НЕ ТаблицаОшибок.Количество()=0 Тогда
		Для Каждого Ошибка Из ТаблицаОшибок Цикл
			Если ВыводитьВЖурнал Тогда
				//сформируем одну строку и выведем ее в журнал
				СтрокаОшибки = "";
				СтрокаОшибки = ДобавитьОшибку(СтрокаОшибки,Ошибка.ОписаниеОшибки);
				ЗаписьЖурналаРегистрации("Тестирование и исправление БД",УровеньЖурналаРегистрации.Ошибка,Объект,Ошибка.Ссылка,СтрокаОшибки);
			Иначе
				НоваяСтрока = ТаблицаОшибочныхОбъектов.Добавить();
				НоваяСтрока.ВидПроверки = Ошибка.ВидПроверки;
				НоваяСтрока.ОписаниеОшибки = Ошибка.ОписаниеОшибки;
				НоваяСтрока.ВидОбъекта = Ошибка.ВидОбъекта;
				НоваяСтрока.ТипОбъекта = Ошибка.ТипОбъекта;
				НоваяСтрока.Ссылка = Ошибка.Ссылка;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ЗаписатьОшибки()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Формирует печатную форму отчета
// 
// Параметры:
//  ТабДокумент - табличный документ.
//
Процедура ПечатьОтчета(ТабДокумент) Экспорт
	СформироватьТаблицуПечати();
	ТабДокумент.Очистить();
	Макет = ПолучитьМакет("Макет");
	МакетОформления = ПолучитьОбщийМакет("МакетОформлениеОтчета");
	ОформлениеШапкаТаблицы = МакетОформления.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	
	ОформлениеСтроки = Новый Массив;
	ОбластьОформленияИзмерений = МакетОформления.ПолучитьОбласть("ОформлениеИзмерений");
	Для Сч = 1 По ОбластьОформленияИзмерений.ВысотаТаблицы Цикл
		ОформлениеСтроки.Добавить(ОбластьОформленияИзмерений.Область(Сч, 1));
	КонецЦикла;
   
	ОформлениеПодвал       = МакетОформления.ПолучитьОбласть("ОформлениеИтоги").Область(1, 1);
	//выводим строку настроек
	ОбластьМакета = Макет.ПолучитьОбласть("Настройки");
	ОбластьМакета.Параметры.СтрокаНастроек = СформироватьСтрокуНастроек();
	ТабДокумент.Вывести(ОбластьМакета);
	
	//выводим шапку
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Область(1,2,1,4).Шрифт = ОформлениеШапкаТаблицы.Шрифт;
	ОбластьМакета.Область(1,2,1,4).ЦветФона = ОформлениеШапкаТаблицы.ЦветФона;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ФильтрУровней = Новый Структура;
	Для Уровень = 0 По МаксУровень Цикл
		ФильтрУровней.Вставить("Уровень"+Уровень,Неопределено);
	КонецЦикла;
	Если НЕ ПоПодсистемам Тогда
		ФильтрУровней.Вставить("ВидОбъекта",Неопределено);
		ФильтрУровней.Вставить("ТипОбъекта",Неопределено);
	КонецЕсли;
	ФильтрУровней.Вставить("Объект",Неопределено);
	
	ТабДокумент.НачатьАвтогруппировкуСтрок();
	Для Каждого Строка Из ТаблицаОшибочныхОбъектов Цикл
		ТекУровеньГруппировки = Неопределено;
		Если ПоПодсистемам Тогда
			Для Уровень = 0 По МаксУровень Цикл
				ФильтрУровней.Свойство("Уровень"+Уровень,ТекУровеньГруппировки);
				Если НЕ ТекУровеньГруппировки = Строка[Уровень] Тогда
					ФильтрУровней.Вставить("Уровень"+Уровень,Строка[Уровень]);
					ОбластьМакета = Макет.ПолучитьОбласть("Подсистема");
					ОбластьМакета.Параметры.Подсистема = Строка[Уровень].Имя;
					ОбластьМакета.Область(1,2,1,4).Шрифт = ОформлениеСтроки[Уровень+2].Шрифт;
					ОбластьМакета.Область(1,2,1,4).ЦветТекста = ОформлениеСтроки[Уровень+2].ЦветТекста;
					ОбластьМакета.Область(1,2,1,4).ЦветФона = ОформлениеСтроки[Уровень+2].ЦветФона;
					ОбластьМакета.Область(1,2).Отступ = Уровень;
					ТабДокумент.Вывести(ОбластьМакета,Уровень);
				КонецЕсли;
			КонецЦикла;
		Иначе
			ФильтрУровней.Свойство("ВидОбъекта",ТекУровеньГруппировки);
			Если НЕ ТекУровеньГруппировки=Строка.ВидОбъекта Тогда
				ФильтрУровней.Вставить("ВидОбъекта",Строка.ВидОбъекта);
				ОбластьМакета = Макет.ПолучитьОбласть("Подсистема");
				ОбластьМакета.Параметры.Подсистема = Строка.ВидОбъекта;
				ОбластьМакета.Область(1,2,1,4).Шрифт = ОформлениеСтроки[3].Шрифт;
				ОбластьМакета.Область(1,2,1,4).ЦветТекста = ОформлениеСтроки[3].ЦветТекста;
				ОбластьМакета.Область(1,2,1,4).ЦветФона = ОформлениеСтроки[3].ЦветФона;
				ОбластьМакета.Область(1,2).Отступ = 0;
				ТабДокумент.Вывести(ОбластьМакета,0);
			КонецЕсли;
			
			ФильтрУровней.Свойство("ТипОбъекта",ТекУровеньГруппировки);
			Если НЕ ТекУровеньГруппировки=Строка.ТипОбъекта Тогда
				ФильтрУровней.Вставить("ТипОбъекта",Строка.ТипОбъекта);
				ОбластьМакета = Макет.ПолучитьОбласть("Подсистема");
				ОбластьМакета.Параметры.Подсистема = Строка.ТипОбъекта;
				ОбластьМакета.Область(1,2,1,4).Шрифт = ОформлениеСтроки[4].Шрифт;
				ОбластьМакета.Область(1,2,1,4).ЦветТекста = ОформлениеСтроки[4].ЦветТекста;
				ОбластьМакета.Область(1,2,1,4).ЦветФона = ОформлениеСтроки[4].ЦветФона;
				ОбластьМакета.Область(1,2).Отступ = 1;
				ТабДокумент.Вывести(ОбластьМакета,1);	
			КонецЕсли;
			
		КонецЕсли;	
		ФильтрУровней.Свойство("Объект",ТекУровеньГруппировки);
		Если НЕ ТекУровеньГруппировки=Строка.Ссылка Тогда
			ФильтрУровней.Вставить("Объект",Строка.Ссылка);
			ОбластьМакета = Макет.ПолучитьОбласть("Объект");
			ОбластьМакета.Параметры.Ссылка = Строка.Ссылка;
			
			ОбластьМакета.Область(1,2,1,4).Шрифт = ОформлениеСтроки[МаксУровень+3].Шрифт;
			ОбластьМакета.Область(1,2,1,4).ЦветТекста = ОформлениеСтроки[МаксУровень+3].ЦветТекста;
			ОбластьМакета.Область(1,2,1,4).ЦветФона = ОформлениеСтроки[МаксУровень+3].ЦветФона;
			
			ОбластьМакета.Область(1,2).Отступ = МаксУровень+1;
			ТабДокумент.Вывести(ОбластьМакета,МаксУровень+1);
		КонецЕсли;
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.Заполнить(Строка);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();
КонецПроцедуры // ПечатьОтчета()

//Формирует строку настроек для вывода в отчет
//
Функция СформироватьСтрокуНастроек()
	СтрокаНастроек = "Настройки: ";
	СтрокаНастроек = ?(ПроверятьОбязательныеРеквизиты,СтрокаНастроек+"Проверка обязательных реквизитов; ",СтрокаНастроек);
	СтрокаНастроек = ?(ПроверятьНаНеиспользуемость,СтрокаНастроек+"Проверка на неиспользуемость; ",СтрокаНастроек);
	СтрокаНастроек = ?(ПроверятьПометкиНаУдаление,СтрокаНастроек+"Проверка пометок на удаление; ",СтрокаНастроек);
	СтрокаНастроек = ?(ПересчетВзаиморасчетовИКурсовДокументов,СтрокаНастроек+"Пересчет взаиморасчетов и курсов; ",СтрокаНастроек);
	
	СтрокаНастроек = ?(Перепроводить,СтрокаНастроек+"Перепроведение документов; ",СтрокаНастроек);
	СтрокаНастроек = ?(ПроверитьБалансныеПроводки,СтрокаНастроек+"Проверка балансных проводок; ",СтрокаНастроек);
	СтрокаНастроек = ?(Перепроводить,СтрокаНастроек+"Перепроведение при ошибках баланса",СтрокаНастроек);
	Возврат СтрокаНастроек;
КонецФункции // СформироватьСтрокуНастроек()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПОЛНЕНИЯ РЕКВИЗИТОВ ОБРАБОТКИ

// Заполнение таблицы значений "ВидыМетаданных" списком всех визуальных
// видов метаданных в том порядке, в котором они выводятся на закладках
//
Процедура ЗаполнитьВидыМетаданных()
	Состояние("Заполняю таблицу видов метаданных");	
	// Сформируем структуры таблицы видов документов
	ВидыМетаданных.Колонки.Очистить();
	ВидыМетаданных.Колонки.Добавить ("Идентификатор");
	ВидыМетаданных.Колонки.Добавить ("Представление");
	// Заполним таблицу видов документов
	Строка=ВидыМетаданных.Добавить();	Строка.Идентификатор="Справочники";				Строка.Представление="Справочники";
	Строка=ВидыМетаданных.Добавить();	Строка.Идентификатор="ПланыВидовХарактеристик";	Строка.Представление="Планы видов хар-к";
	Строка=ВидыМетаданных.Добавить();	Строка.Идентификатор="ПланыВидовРасчета";		Строка.Представление="Планы видов расчета";
	Строка=ВидыМетаданных.Добавить();	Строка.Идентификатор="Документы";				Строка.Представление="Документы";
	
	// Заполним коллекцию картинок
	КоллекцияКартинок = Новый Структура;
	// Не во всех конфигурациях есть картинки метаданных, да еще с такими именами
	Попытка 	КоллекцияКартинок.Вставить("Подсистема",БиблиотекаКартинок["МетаданныеПодсистема"]);
	Исключение	КоллекцияКартинок.Вставить("Подсистема",Новый Картинка());
	КонецПопытки;
	Для Каждого Строка Из ВидыМетаданных Цикл
		Попытка		КоллекцияКартинок.Вставить(Строка.Идентификатор,БиблиотекаКартинок["Метаданные"+Строка.Идентификатор]);
		Исключение	КоллекцияКартинок.Вставить(Строка.Идентификатор,Новый Картинка());
		КонецПопытки;
	КонецЦикла; 
КонецПроцедуры // ЗаполнитьВидыМетаданных()

// Формирует список всех подсистем, с указанием полного пути.
// рекурсивно вызывает сама себя, добавляя подсистемы от КорневойПодсистемы указанного уровня
//
Процедура СформироватьСписокПодсистем(КорневаяПодсистема, Уровень = 0, Знач ПолныйПуть = "")
	МаксУровень = ?(Уровень>МаксУровень,Уровень,МаксУровень);
	Для Каждого Подсистема Из КорневаяПодсистема.Подсистемы Цикл
		Строка = ТаблицаПодсистем.Добавить();
		Строка.ИмяПодсистемы 	= Подсистема.Имя;
		Строка.Подсистема 		= Подсистема;
		стрПуть = ПолныйПуть+"|"+ Подсистема.Имя;
		Строка.ПолныйПуть = стрПуть + "|*";
		СформироватьСписокПодсистем (Подсистема, Уровень+1, стрПуть);
	КонецЦикла; 
КонецПроцедуры // СформироватьСписокПодсистем()

// Формирует список всех подсистем, с указанием полного пути.
// рекурсивно вызывает сама себя, добавляя подсистемы от КорневойПодсистемы указанного уровня
//
Процедура СформироватьВеткуПодсистем(КорневаяПодсистема, КорневаяВетка)
	Для Каждого Подсистема Из КорневаяПодсистема.Подсистемы Цикл
		Строка = КорневаяВетка.Строки.Добавить();
		Строка.Подсистема = Подсистема;
		СформироватьВеткуПодсистем (Подсистема, Строка);
	КонецЦикла; 
КонецПроцедуры // СформироватьВеткуПодсистем()

// Подготовка списка подсистем (как визуального так и общего)
//
Процедура ЗаполнитьТаблицуПодсистем()
	Состояние("Заполняю таблицу подсистем");	
	// Создадим структуру для заливки дерева подсистем
	ТаблицаПодсистем.Колонки.Очистить();
	ТаблицаПодсистем.Колонки.Добавить ("Подсистема");
	ТаблицаПодсистем.Колонки.Добавить ("ИмяПодсистемы");
	ТаблицаПодсистем.Колонки.Добавить ("ПолныйПуть");
	// Заполним список подсистем
	Строка = ТаблицаПодсистем.Добавить();
	Строка.ИмяПодсистемы = Метаданные.Имя;
	Строка.Подсистема = Метаданные;
	Строка.ПолныйПуть = "|"+ Метаданные.Имя+"|*";
	СформироватьСписокПодсистем (Метаданные,0,"|"+ Метаданные.Имя);
	
	ДеревоПодсистем.Колонки.Очистить();
	ДеревоПодсистем.Колонки.Добавить ("Подсистема");
	// Заполним список подсистем
	Строка = ДеревоПодсистем.Строки.Добавить();
	Строка.Подсистема = "<ВСЕ ПОДСИСТЕМЫ>";
	СформироватьВеткуПодсистем (Метаданные,Строка);
КонецПроцедуры // ЗаполнитьТаблицуПодсистем()

// Возвращает полные пути к подсистемам в виде строки с разделителями
//
// Параметры:
//  Объект - ссылка на ОбъектМетаданных.
//
Функция ПодсистемыЧерезРазделитель(Объект)
	стрПодсистемы = "";
	ОбъектПодсистемы = Новый Массив;
	ПолучитьПодсистемыОбъекта(Объект,ОбъектПодсистемы,Метаданные.Подсистемы);
	Для Каждого Подсистема Из ОбъектПодсистемы Цикл
		стрПолныйПуть = "";
		Строка = ТаблицаПодсистем.Найти(Подсистема.Имя,"ИмяПодсистемы");
		стрПолныйПуть = стрПолныйПуть+?(Строка = Неопределено,"|"+Метаданные.Имя+"|*",Строка.ПолныйПуть);
		стрПодсистемы = стрПодсистемы +","+ стрПолныйПуть;
	КонецЦикла; 
	Возврат стрПодсистемы;
КонецФункции // ПодсистемыЧерезРазделитель()

// Получение подсистем объекта метаданных
//
// Параметры
//  Объект  - ОбъектМетаданных - Объект метаданных, для которого требуется получить его подсистемы
//  ПодсистемыОбъекта  - Массив - Массив, содержащий список подсистем обхекта метаданных
//  КорневаяПодсистема  - ОбъектМетаданных - Корневая подсистема, относительно которой перебираются вложенные 
//
Процедура ПолучитьПодсистемыОбъекта(Объект,ПодсистемыОбъекта,КорневаяПодсистема) Экспорт
	СисИнфо = Новый СистемнаяИнформация;
	ВерсияПриложения = СисИнфо.ВерсияПриложения;
	
	Если Лев(ВерсияПриложения,3)="8.1" Тогда
		ПодсистемыОбъекта=Объект.Подсистемы;
	Иначе
		Для каждого Подсистема Из КорневаяПодсистема Цикл
			Если Подсистема.Состав.Содержит(Объект) Тогда
				ПодсистемыОбъекта.Добавить(Подсистема);
			КонецЕсли;
			Если Подсистема.Подсистемы.Количество()>0 Тогда
				ПолучитьПодсистемыОбъекта(Объект,ПодсистемыОбъекта,Подсистема.Подсистемы);
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры // ПолучитьПодсистемыОбъекта() 

// Формирует список объектов конфигурации, с указанием принадлежности к подсистемам
//
Процедура ЗаполнитьТаблицуОбъектов()
	Состояние("Заполняю таблицу объектов");	
	// Создадим структуру таблицы для заливки всех метаданных
	ОбъектыМетаданных.Колонки.Очистить();
	ОбъектыМетаданных.Колонки.Добавить ("Пометка");
	ОбъектыМетаданных.Колонки.Добавить ("Подсистемы");
	ОбъектыМетаданных.Колонки.Добавить ("Объект");
	ОбъектыМетаданных.Колонки.Добавить ("ВидОбъекта");
	// Переберем все метаданные, заполняя таблицу
	Для Каждого Строка Из ВидыМетаданных Цикл
		Идентификатор = Строка.Идентификатор;
		Для Каждого Объект Из Метаданные[Идентификатор] Цикл
			Строка = ОбъектыМетаданных.Добавить();
			Строка.Пометка = 0;
			Строка.Подсистемы = ПодсистемыЧерезРазделитель(Объект);
			Строка.ВидОбъекта = Идентификатор;
			Строка.Объект = Объект;
		КонецЦикла; 
	КонецЦикла;
КонецПроцедуры // ЗаполнитьТаблицуОбъектов()

// Формирует список объектов для обработки
// флСпособФормирования - определяет, как будет формироваться таблица
//                    0 - из данных формы
//                    1 - из данных, переданных через процедуру вызова обработки
// СписокОбъектов       - объекты, из которых будет формироваться таблица при программном вызове обработки
Процедура СформироватьТаблицуОбработки(флСпособФормирования,СписокОбъектов="") Экспорт
	Если флСпособФормирования = 0 Тогда
		Для Каждого Строка Из ОбъектыМетаданных Цикл
			Если Строка.Пометка=1 Тогда
				НоваяСтрока = ТаблицаОбработки.Добавить();
				НоваяСтрока.Метаданные = Строка.Объект;
				НоваяСтрока.ВидОбъекта = Строка.ВидОбъекта;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого Элемент Из СписокОбъектов Цикл
			Объект = Элемент.Значение;
			
			Если Метаданные.Справочники.Найти(Объект) <> Неопределено Тогда
				//имеем справочник
				НоваяСтрока = ТаблицаОбработки.Добавить();
				НоваяСтрока.Метаданные = Метаданные.Справочники[Объект];
				НоваяСтрока.ВидОбъекта = "Справочники";
			ИначеЕсли Метаданные.Документы.Найти(Объект) <> Неопределено Тогда
				//имеем документ
				НоваяСтрока = ТаблицаОбработки.Добавить();
				НоваяСтрока.Метаданные = Метаданные.Документы[Объект];
				НоваяСтрока.ВидОбъекта = "Документы";
			ИначеЕсли Метаданные.ПланыВидовХарактеристик.Найти(Объект) <> Неопределено Тогда
				//имеем ПВХ
				НоваяСтрока = ТаблицаОбработки.Добавить();
				НоваяСтрока.Метаданные = Метаданные.ПланыВидовХарактеристик[Объект];
				НоваяСтрока.ВидОбъекта = "ПланыВидовХарактеристик";
			ИначеЕсли Метаданные.ПланыВидовРасчета.Найти(Объект) <> Неопределено Тогда
				//имеем ПВР
				НоваяСтрока = ТаблицаОбработки.Добавить();
				НоваяСтрока.Метаданные = Метаданные.ПланыВидовРасчета[Объект];
				НоваяСтрока.ВидОбъекта = "ПланыВидовРасчета";
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры  // СформироватьТаблицуОбработки()

// Заполняет все основные табличные реквизиты обработки по метаданным
// сохраняет их для ускорения последующего открытия
//
Процедура ЗаполнитьТабличныеРеквизиты() Экспорт
	// Заполним список видов объектов метаданных
	ЗаполнитьВидыМетаданных();
	// Заполним таблицу подсистем
	ЗаполнитьТаблицуПодсистем();
	// Заполним основную таблицу всех объектов конфигурации
	ЗаполнитьТаблицуОбъектов();
КонецПроцедуры // ЗаполнитьТабличныеРеквизиты()
  
//формирует таблицу объектов и подсистем для печати
//
Процедура СформироватьТаблицуПечати()
	ПараметрыСортировки="";
	Если ПоПодсистемам Тогда
		//если в дереве иерархия по подсистемам
		УровеньВложенности = МаксУровень;
		Для Уровень=0 По УровеньВложенности Цикл
			//ТаблицаОшибочныхОбъектов.Колонки.Добавить("Уровень"+Уровень);
			ТаблицаОшибочныхОбъектов.Колонки.Вставить(Уровень,"Уровень"+Уровень);
			ПараметрыСортировки = ПараметрыСортировки + "Уровень"+Строка(Уровень)+",";
		КонецЦикла;
		ПараметрыСортировки = ПараметрыСортировки+"ТипОбъекта,Ссылка";
		ТаблицаОшибочныхОбъектов.Колонки.Добавить("УровеньОбъекта");
		Для Каждого СтрокаОбъекта Из ТаблицаОшибочныхОбъектов Цикл
			СтрокаДерева = ДеревоМетаданных.Строки.Найти(СтрокаОбъекта.ТипОбъекта,"Метаданные",Истина);
			СписокУровней.Очистить();
			ТекУровень = МаксУровень;
			ПолучитьУровни(СтрокаДерева,ТекУровень);
			СписокУровней.Сортировать("УровеньИерархии");
			ТекУровень = 0;
			Для Каждого Уровень Из СписокУровней Цикл
				СтрокаОбъекта[ТекУровень]=Уровень.Объект;
				ТекУровень = ТекУровень+1;
			КонецЦикла;
			//глубина уровня текущего объекта
			СтрокаОбъекта.УровеньОбъекта = СписокУровней.Количество();
		КонецЦикла;
		ТаблицаОшибочныхОбъектов.Сортировать(ПараметрыСортировки);
	Иначе
		ТаблицаОшибочныхОбъектов.Сортировать("ВидОбъекта,ТипОбъекта,Ссылка");
	КонецЕсли;
КонецПроцедуры // СформироватьТаблицуПечати()

// получить уровни подсистем для строки дерева
//
// Параметры:
//  СтрокаДерева - Строка Дерева Метаданных.
//  ТекУровень   - Число - текущий уровень.
//
Процедура ПолучитьУровни(СтрокаДерева,ТекУровень)
	НовыйУровень = СписокУровней.Добавить();
	НовыйУровень.УровеньИерархии = ТекУровень;
	НовыйУровень.Объект = СтрокаДерева.Метаданные;
	ТекУровень = ТекУровень-1;
	Если СтрокаДерева.Родитель = Неопределено Тогда Возврат; КонецЕсли;
	ПолучитьУровни(СтрокаДерева.Родитель,ТекУровень);
КонецПроцедуры // ПолучитьУровни()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ТЕСТИРОВАНИЯ ИНФОРМАЦИОННОЙ БАЗЫ

// Проверяет обязательные реквизиты
//
// Параметры:
//  Объект - текущий объект проверки.
//
Процедура ПроверитьОбязательныеРеквизиты(Объект)
	НазваниеДействия = "Проверка обязательных реквизитов";
	СтрокаОшибки = "";
	Объект.ПроверитьКорректность(СтрокаОшибки);
	Если НЕ СтрокаОшибки = "" Тогда
		ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
	КонецЕсли;
КонецПроцедуры //ПроверитьОбязательныеРеквизиты()

// Проверяет пометки на удаление
// Объект  - проверяемый объект
// Вид     - вид проверяемого объекта
// Ошибки  - список ошибок
Процедура ПроверитьПометкиНаУдаление(Объект,Вид)
	НазваниеДействия = "Проверка пометки на удаление";
	Если Объект.ПометкаУдаления Тогда
		Массив = Новый Массив;
		Массив.Добавить(Объект.Ссылка);
		Ссылки = НайтиПоСсылкам(Массив);
		Если НЕ Ссылки.Количество()=0 Тогда
			СтрокаОшибки = ?(Вид="Документ","Документ помечен на удаление! Ссылки на документ присутствуют в:","Элемент помечен на удаление! Ссылки на элемент присутствуют в:");
			Для Каждого Ссылка Из Ссылки Цикл
				СтрокаОшибки = ДобавитьОшибку(СтрокаОшибки,Ссылка[2]);
			КонецЦикла;
			ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПроверитьПометкиНаУдаление()

// Производит перерасчет курсов документа (упр. валюты и валюты взаиморасчета)
// Объект  - обрабатываемый документ
Процедура ПровестиПересчетВзаиморасчетовИКурсовДокумента(Объект)
	НазваниеДействия = "Пересчет взаиморасчетов и курсов";
	ЕстьРеквизитКурсВалютыУпр = дкДокументЕстьРеквизитШапки("КурсВалютыУпр", Объект.Метаданные().Имя);
	ЕстьРеквизитКурсВалютыВзаиморасчетов = дкДокументЕстьРеквизитШапки("КурсВалютыВзаиморасчетов", Объект.Метаданные().Имя);
	ЕстьРеквизитДоговорВзаиморасчетов = дкДокументЕстьРеквизитШапки("ДоговорВзаиморасчетов", Объект.Метаданные().Имя);
	
	Если ЕстьРеквизитКурсВалютыУпр Тогда
		Если обЗначениеНеЗаполнено(Объект.КурсВалютыУпр) Тогда
			ВалютаУпр = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			СтруктураКурса = обКурсДляВалюты(ВалютаУпр, Объект.Дата);
			КурсУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			Если КурсУпр <> 0 Тогда
				Объект.КурсВалютыУпр = КурсУпр;
				Попытка
					Объект.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					СтрокаОшибки = "Ошибка записи документа!";
					ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
				КонецПопытки;
			Иначе
				СтрокаОшибки = "Ошибка получения курса валюты управленческого учета!";
				ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьРеквизитКурсВалютыВзаиморасчетов И ЕстьРеквизитДоговорВзаиморасчетов Тогда
		Если обЗначениеНеЗаполнено(Объект.КурсВалютыВзаиморасчетов) И (НЕ обЗначениеНеЗаполнено(Объект.ДоговорВзаиморасчетов))Тогда
			Попытка
				СтруктураКурса = обКурсДляВалюты(Объект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов, Объект.Дата);
				КурсРасч = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			Исключение
				КурсРасч = 0;
			КонецПопытки;
			Если КурсРасч <> 0 Тогда
				Объект.КурсВалютыВзаиморасчетов = КурсРасч;
				Если Объект.Проведен Тогда
					Попытка
						Объект.Записать(РежимЗаписиДокумента.Проведение);
					Исключение
						СтрокаОшибки = "Ошибка проведения документа!";
						ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
					КонецПопытки;
				Иначе
					Попытка
						Объект.Записать(РежимЗаписиДокумента.Запись);
					Исключение
						СтрокаОшибки = "Ошибка записи документа!";
						ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
					КонецПопытки;
				КонецЕсли;
			Иначе
				СтрокаОшибки = "Ошибка получения курса взаиморасчетов!";
				ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПровестиПересчетВзаиморасчетовИКурсовДокумента()

// Перепроводит документ
// Объект - перепроводимый документ
// Ошибки - список ошибок
Процедура Перепровести(Объект)
	НазваниеДействия = "Перепроведение";
	Если Объект.Проведен Тогда 
		Попытка
			Объект.Записать(РежимЗаписиДокумента.Проведение) 
		Исключение
			СтрокаОшибки = "Ошибка проведения документа!";
			ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры  // Перепровести()

// Проверяет балансные проводки документа
// Объект - проверяемый документ
// Ошибки - список ошибок
Процедура ПроверитьБалансныеПроводки(Объект)
	НазваниеДействия = "Проверка балансных проводок";
	Если Объект.Проведен Тогда
		РазницаБаланса = 0;
		Попытка
			ОбъектСсылка=Объект.Ссылка;
		Исключение
			Возврат;
		КонецПопытки; 
		БалансСходится = ОтчетБаланс.ПроверитьБалансноеУравнение(ОбъектСсылка,РазницаБаланса);
		Если НЕ БалансСходится Тогда
			СтрокаОшибки = "Баланс документа не сходится!" + Символы.ПС + "Разница баланса: " + РазницаБаланса;
			Если Перепроводить Тогда
				Попытка
					Объект.Записать(РежимЗаписиДокумента.Проведение);
					Попытка
						ОбъектСсылка=Объект.Ссылка;
					Исключение
						Возврат;
					КонецПопытки; 
					БалансСходится = ОтчетБаланс.ПроверитьБалансноеУравнение(ОбъектСсылка, РазницаБаланса);
					Если НЕ БалансСходится Тогда
						СтрокаОшибки = ДобавитьОшибку(СтрокаОшибки,"Документ перепроведен! Баланс документа не сходится." +Символы.ПС + "Разница баланса: " + РазницаБаланса)
					Иначе
						СтрокаОшибки = ДобавитьОшибку(СтрокаОшибки,"Документ перепроведен! Баланс документа сходится")
					КонецЕсли;
				Исключение
					СтрокаОшибки = ДобавитьОшибку(СтрокаОшибки,"Ошибка проведения документа!");
				КонецПопытки;
			КонецЕсли;
			ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПроверитьБалансныеПроводки()

//Проверяет элемент справочника на используемость
//Объект - элемент справочника
//Ошибки - список ошибок
Процедура ПроверитьНаНеиспользуемость(Объект)
	НазваниеДействия = "Проверка на неиспользуемость";
	Массив = Новый Массив;
	Массив.Добавить(Объект.Ссылка);
	Ссылки = НайтиПоСсылкам(Массив);
	Если Ссылки.Количество()=0 Тогда
		СтрокаОшибки = "Элемент не используется";
		ЗаписатьОшибку(ВидТекущегоОбъекта,Объект,НазваниеДействия,СтрокаОшибки);
	КонецЕсли;
КонецПроцедуры // ПроверитьНаНеиспользуемость()

// Процедура обработки документа
// Объект - обрабатываемый документ
// Ошибки - список ошибок
Процедура ОбработатьДокумент(Объект)
	Вид = "Документ";
	//проверка заполнения обязательных реквизитов
	Если ПроверятьОбязательныеРеквизиты Тогда
		ПроверитьОбязательныеРеквизиты(Объект);
	КонецЕсли;
	
	//проверка помеченных на удаление
	Если ПроверятьПометкиНаУдаление Тогда
		ПроверитьПометкиНаУдаление(Объект, Вид);
	КонецЕсли;
	
	//пересчет взаиморасчетов и курсов
	Если ПересчетВзаиморасчетовИКурсовДокументов Тогда
		ПровестиПересчетВзаиморасчетовИКурсовДокумента(Объект);
	КонецЕсли;
	
	//перепроведение
	Если Перепровести Тогда
		Перепровести(Объект);
	КонецЕсли;
	
	//проверка проводок
	Если ПроверитьБалансныеПроводки Тогда
		ПроверитьБалансныеПроводки(Объект);
	КонецЕсли;
	
	//запись ошибок
	ЗаписатьОшибки(Объект);
КонецПроцедуры // ОбработатьДокумент()

// Процедура обработки элемента справочника
// Объект - элемент справочника
// Ошибки - список ошибок
Процедура ОбработатьЭлементСправочника(Объект)
	Вид = "ЭлементСправочника";
	
	//проверка заполнения обязательных реквизитов
	Если ПроверятьОбязательныеРеквизиты Тогда
		ПроверитьОбязательныеРеквизиты(Объект);
	КонецЕсли;
	
	//проверка на используемость
	Если ПроверятьНаНеиспользуемость Тогда
		ПроверитьНаНеиспользуемость(Объект);
	КонецЕсли;
	
	//проверка помеченных на удаление
	Если ПроверятьПометкиНаУдаление Тогда
		ПроверитьПометкиНаУдаление(Объект, Вид);
	КонецЕсли;
	
	//запись ошибок
	ЗаписатьОшибки(Объект);
КонецПроцедуры // ОбработатьЭлементСправочника()

// Процедура обработки элемента ПВХ
Процедура ОбработатьЭлементПВХ(Объект)
	Вид = "ЭлементПВХ";
	
	//проверка заполнения обязательных реквизитов
	Если ПроверятьОбязательныеРеквизиты Тогда
		ПроверитьОбязательныеРеквизиты(Объект);
	КонецЕсли;
	
	//проверка помеченных на удаление
	Если ПроверятьПометкиНаУдаление Тогда
		ПроверитьПометкиНаУдаление(Объект, Вид);
	КонецЕсли;
	
	//запись ошибок
	ЗаписатьОшибки(Объект);
КонецПроцедуры // ОбработатьЭлементПВХ()

// Процедура обработки элемента ПВР
//
// Параметры:
//  Объект - текущий объект проверки.
//
Процедура ОбработатьЭлементПВР(Объект)
	Вид = "ЭлементПВХ";
	
	//проверка заполнения обязательных реквизитов
	Если ПроверятьОбязательныеРеквизиты Тогда
		ПроверитьОбязательныеРеквизиты(Объект);
	КонецЕсли;
	
	//проверка помеченных на удаление
	Если ПроверятьПометкиНаУдаление Тогда
		ПроверитьПометкиНаУдаление(Объект, Вид);
	КонецЕсли;
	
	//запись ошибок
	ЗаписатьОшибки(Объект);
КонецПроцедуры // ОбработатьЭлементПВХ()

// выполняет проверку всех выбранных объектов
// СписокОбъектов - список имен объектов при программном вызове обработки
Процедура ВыполнитьПроверку(СписокОбъектов="") Экспорт
	ТаблицаОбработки.Колонки.Очистить();
	ТаблицаОбработки.Колонки.Добавить("Метаданные");
	ТаблицаОбработки.Колонки.Добавить("ВидОбъекта");
	флПрограммныйВызов = ?(СписокОбъектов="",Ложь,Истина);
	Если СписокОбъектов="" Тогда
		//если список объектов не передан, то формируем таблицу из дерева метаданных
		СформироватьТаблицуОбработки(0);
	Иначе
		//формируем таблицу из списка
		СформироватьТаблицуОбработки(1,СписокОбъектов);
		ВыводитьВЖурнал = Истина;
	КонецЕсли;
	
	КолОбъектов = ТаблицаОбработки.Количество();
	
#Если Клиент Тогда
		Если НЕ флПрограммныйВызов Тогда
			ФормаИндикатора = ПолучитьОбщуюФорму("Индикация");
			ФормаИндикатора.СтрокаСостояния = "Идет обработка";
			ФормаИндикатора.Открыть();
			ФормаИндикатора.ЭлементыФормы.Индикатор.МинимальноеЗначение=0;
			ФормаИндикатора.ЭлементыФормы.Индикатор.МаксимальноеЗначение=КолОбъектов;
			ФормаИндикатора.ЭлементыФормы.Индикатор.Шаг=1;
			Сч = 0;
		КонецЕсли;
#КонецЕсли
	
	ТаблицаОшибочныхОбъектов.Колонки.Очистить();
	ТаблицаОшибочныхОбъектов.Колонки.Добавить("ВидОбъекта");
	ТаблицаОшибочныхОбъектов.Колонки.Добавить("ТипОбъекта");
	ТаблицаОшибочныхОбъектов.Колонки.Добавить("Ссылка");
	ТаблицаОшибочныхОбъектов.Колонки.Добавить("ВидПроверки");
	ТаблицаОшибочныхОбъектов.Колонки.Добавить("ОписаниеОшибки");
	
	Для Каждого Строка Из ТаблицаОбработки Цикл
		ОбработкаПрерыванияПользователя();
		
		#Если Клиент Тогда
			Если НЕ флПрограммныйВызов Тогда
				ФормаИндикатора.ЭлементыФормы.Индикатор.Значение=Сч;
			КонецЕсли;
		#КонецЕсли
		
		//Если объект - документ
		Если Строка.ВидОбъекта = "Документы" Тогда
			ВидТекущегоОбъекта = Строка.ВидОбъекта;
			ТипТекущегоОбъекта = Строка.Метаданные;
			КонПериода = ?(КонПериода='0001-01-01', КонПериода, КонецДня(КонПериода));
			Выборка = Документы[Строка.Метаданные.Имя].Выбрать(НачПериода,КонПериода);
			Пока Выборка.Следующий() Цикл
				ОбработкаПрерыванияПользователя();
				Объект = Выборка.ПолучитьОбъект();
				ФормаИндикатора.СтрокаСостояния = "Объект: "+Строка.Метаданные.Представление()+" - "+Объект;
				Ошибки = "";
				ТаблицаОшибок.Очистить();
				
				ОбработатьДокумент(Объект);
				Объект = Неопределено;
			КонецЦикла;
			
		ИначеЕсли Строка.ВидОбъекта = "Справочники" Тогда
			ВидТекущегоОбъекта = Строка.ВидОбъекта;
			ТипТекущегоОбъекта = Строка.Метаданные;
			Выборка = Справочники[Строка.Метаданные.Имя].Выбрать();
			Пока Выборка.Следующий() Цикл
				ОбработкаПрерыванияПользователя();
				Если Выборка.ЭтоГруппа Тогда
					Продолжить;
				Иначе
					Объект = Выборка.ПолучитьОбъект();
					ФормаИндикатора.СтрокаСостояния = "Объект: "+Строка.Метаданные.Представление()+" - "+Объект.Наименование;
					Ошибки = "";
					ТаблицаОшибок.Очистить();
					
					ОбработатьЭлементСправочника(Объект);
					Объект = Неопределено;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли Строка.ВидОбъекта = "ПланыВидовХарактеристик" Тогда
			ВидТекущегоОбъекта = Строка.ВидОбъекта;
			ТипТекущегоОбъекта = Строка.Метаданные;
			Выборка = ПланыВидовХарактеристик[Строка.Метаданные.Имя].Выбрать();
			Пока Выборка.Следующий() Цикл
				ОбработкаПрерыванияПользователя();
				Если Выборка.ЭтоГруппа Тогда
					Продолжить;
				Иначе
					Объект = Выборка.ПолучитьОбъект();
					ФормаИндикатора.СтрокаСостояния = "Объект: "+Строка.Метаданные.Представление()+" - "+Объект.Наименование;
					Ошибки = "";
					ТаблицаОшибок.Очистить();
					
					ОбработатьЭлементПВХ(Объект);
					Объект = Неопределено;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли Строка.ВидОбъекта = "ПланыВидовРасчета" Тогда
			ВидТекущегоОбъекта = Строка.ВидОбъекта;
			ТипТекущегоОбъекта = Строка.Метаданные;
			Выборка = ПланыВидовРасчета[Строка.Метаданные.Имя].Выбрать();
			Пока Выборка.Следующий() Цикл
				ОбработкаПрерыванияПользователя();
				Объект = Выборка.ПолучитьОбъект();
				ФормаИндикатора.СтрокаСостояния = "Объект: "+Строка.Метаданные.Представление()+" - "+Объект.Наименование;
				Ошибки = "";
				ТаблицаОшибок.Очистить();
					
				ОбработатьЭлементПВР(Объект);
				Объект = Неопределено;
			КонецЦикла;
		КонецЕсли;
		Сч = Сч + 1;	
	КонецЦикла;
	#Если Клиент Тогда	
	Если НЕ флПрограммныйВызов Тогда
		Попытка
			ФормаИндикатора.Закрыть();
		Исключение КонецПопытки; 
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры // ВыполнитьПроверку()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ
ОтчетБаланс = Отчеты.АктивыИПассивы.Создать();
ТаблицаОшибок = Новый ТаблицаЗначений;
ТаблицаОшибок.Колонки.Добавить("ВидОбъекта");
ТаблицаОшибок.Колонки.Добавить("ТипОбъекта");
ТаблицаОшибок.Колонки.Добавить("Ссылка");
ТаблицаОшибок.Колонки.Добавить("ВидПроверки");
ТаблицаОшибок.Колонки.Добавить("ОписаниеОшибки");
МаксУровень = 0;
СписокУровней = Новый ТаблицаЗначений;
СписокУровней.Колонки.Добавить("УровеньИерархии");
СписокУровней.Колонки.Добавить("Объект");
//Список наименований подсистем, которые не требуется выводить в дерево
СписокИсключений = Новый СписокЗначений;
//СписокИсключений.Добавить("Классификаторы");
СписокИсключений.Добавить("Отчеты");
СписокИсключений.Добавить("Обработки");
#КонецЕсли

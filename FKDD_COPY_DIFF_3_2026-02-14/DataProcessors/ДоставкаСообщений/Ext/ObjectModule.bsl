
Перем УдаленныеПодразделения_ЭтотУзел, 
	  УдаленныеПодразделения_Узел; 			//узлы с которыми работаем, запоминаем один раз
Перем КомментироватьХодОбмена Экспорт; 		// Флаг необходимости комментирования хода обмена (при ручном обмене)
Перем СтатусыСобытийПротокола; 				// Перечисление.СтатусыСобытийСистемногоПротокола
Перем КаталогТемпФайлов Экспорт; 			// каталог хранения темп-файлов
Перем КаталогФайловСообщений Экспорт; 		// каталог хранения файлов сообщений обмена
Перем КаталогОбработанныхСообщений Экспорт; // каталог для хранения копий обработанных файлов сообщений обмена
Перем КаталогОтправленныхСообщений;			// Здесь будут сохраняться сообщения, отправленные другим транспортом (FTP/e-mail) //DOOY+ 01.5.11.21
Перем ТемпФайлыДляУдаления Экспорт;			// Список имен файлов созданных как временные при работе и подлежащих удалению
Перем ФайлыДляПеремещения;					// Список имен файлов содержащих обработанные сообщения и подлежащих перемещению
Перем МодульИнициализирован; 				// Флаг инициализации данной обработки
Перем ТекДатаЛога; 							// Последняя дата лог-файла
Перем ПрефиксЛога; 							// для записи лога
Перем КаталогЛогФайлов Экспорт; 			// каталог хранения лог-файлов
Перем ЛогФайлОбъект Экспорт; 				// Текущий лог-файл
Перем ПолноеИмяТекущегоЛога Экспорт; 		// Полное имя текущего лог-файла
Перем ВестиЖурнал Экспорт; 					// Флаг ведения журнала регистрации
Перем ВестиЛоги Экспорт; 					// Флаг ведения лог-файла при обмене

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Проверяет корректность заполнения параметров для работы методов обработки
Функция ПроверитьКорректность(стрОшибки = "") Экспорт
	Если НЕ ЗначениеЗаполнено(НастройкаДоставки) Тогда
		стрОшибки = стрОшибки + "Не указана настройка доставки";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

// Получает лог-файл, если не найден, создает новый.
// Если не найдена папка, создает новую.
Функция ПолучитьЛогФайл(НуженНовый = Ложь)
	ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	Если НуженНовый ИЛИ НЕ ЗначениеЗаполнено(ПолноеИмяТекущегоЛога) ИЛИ (ТипЗнч(ЛогФайлОбъект)<>Тип("ЗаписьТекста")) Тогда
		// создаем новый лог-файл
		Если ПустаяСтрока(КаталогЛогФайлов) ИЛИ НЕ одОбменДанными.ЕстьКаталог(КаталогЛогФайлов) Тогда
			КаталогЛогФайлов = КаталогВременныхФайлов();
		КонецЕсли;
		Сч=0;
		Пока Сч<10 Цикл
			Попытка 
				ПолноеИмяТекущегоЛога = КаталогЛогФайлов + СокрЛП(ЭтотУзел.Код) + "_" + Формат(ТекущаяДата(), "ДФ = гггг-ММ-дд")+ ?(ЗначениеЗаполнено(Сч),"-"+Сч,"") + ".log";
				ЛогФайлОбъект = Новый ЗаписьТекста(ПолноеИмяТекущегоЛога, КодировкаТекста.ANSI,, Истина);
				Прервать;
			Исключение
				Сч=Сч+1;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	Возврат ЛогФайлОбъект; 
КонецФункции	//	ПолучитьЛогФайл()

// Пишет системный протокол
Функция ЗаписьЛога(ТипСобытия, СтрСообщения, ОбъектЛога="", Знач СтатусСобытия = Неопределено) Экспорт
	СтатусСобытия = ?(СтатусСобытия = Неопределено, Перечисления.СтатусыСобытийСистемногоПротокола.Информация, СтатусСобытия);
	Если ЛогФайлОбъект = Неопределено Тогда
	    ПолучитьЛогФайл();
	КонецЕсли; 
	МаскаЛога = ?(ВестиЖурнал, "1", "0") + ?(ВестиЛоги, "1", "0");
	одОбменДанными.ЗафиксироватьСобытие(ТипСобытия, СтрСообщения, ОбъектЛога, СтатусСобытия, МаскаЛога, ЛогФайлОбъект, ПолноеИмяТекущегоЛога);
	#Если Клиент Тогда
		Если КомментироватьХодОбмена Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			СтрИнтСообщения = ТекДата + СтрСообщения;
			Сообщить(СтрИнтСообщения, ?(СтатусСобытия = Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка, СтатусСообщения.Внимание, СтатусСообщения.Информация));
		КонецЕсли;
	#КонецЕсли
КонецФункции

// Функция подключения к почтовым серверам или почтовому
// клиенту для инициализации переменной "Почта"
// Возвращает: Соединение с почтой, если не установлено - Неопределено
Функция ПодключитьсяКПочте(СтрОшибки = "")
	Рез = Неопределено;
	
	Если НастройкаДоставки.СпособОбмена = 1 Тогда //MAPI
		#Если Клиент Тогда
			Почта = Новый Почта();
			Попытка
				Почта.Подключиться(); 
				Рез = Почта;
			Исключение
				СтрОшибки = СтрОшибки + "Ошибка при подключении к почтовому профилю пользователя: " + ИнформацияОбОшибке().Описание;
						 
				Возврат Рез;
			КонецПопытки;
		#Иначе
			СтрОшибки = СтрОшибки + "Почтовый профиль пользователя недоступен в режиме внешнего соединения или на сервере!";
			
			Возврат рез;
		#КонецЕсли
	
	Иначе //e-Mail
		ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
		ПочтовыйПрофиль.АдресСервераPOP3 = НастройкаДоставки.СерверPOP3;
		ПочтовыйПрофиль.ПортPOP3         = НастройкаДоставки.ПортPOP3;
		ПочтовыйПрофиль.Пользователь     = НастройкаДоставки.ПользовательPOP3;
		ПочтовыйПрофиль.Пароль           = НастройкаДоставки.ПарольPOP3;
		ПочтовыйПрофиль.АдресСервераSMTP = НастройкаДоставки.СерверSMTP;
		ПочтовыйПрофиль.ПортSMTP         = НастройкаДоставки.ПортSMTP;
		ПочтовыйПрофиль.ПользовательSMTP = НастройкаДоставки.ПользовательSMTP;
		ПочтовыйПрофиль.ПарольSMTP       = НастройкаДоставки.ПарольSMTP;
		Если НастройкаДоставки.Таймаут > 0 Тогда
			ПочтовыйПрофиль.ВремяОжидания = НастройкаДоставки.Таймаут;
		КонецЕсли; 
		   
		Почта = Новый ИнтернетПочта(); 
		Попытка
			Почта.Подключиться(ПочтовыйПрофиль);
			Рез = Почта;
		Исключение 
			СтрОшибки = СтрОшибки + "Ошибка при подключении к почтовому серверу SMTP: " + ИнформацияОбОшибке().Описание;
			
			Возврат Рез; 
		КонецПопытки;
		
	КонецЕсли;
		
	Возврат Рез;
КонецФункции	//	ПодключитьсяКПочте()

// Функция подключения к FTP
//Параметры:
//	ПараметрыСоединения - любая коллекция значений с именованными полями (структура, строка ТЗ и т.п.). Поля:
//		- СерверFTP
//		- ПортFTP
//		- ПользовательFTP
//		- ПарольFTP
//		- ПассивныйРежим
// Возвращает: объект FTPСоединение, если не установлено - Неопределено
Функция ПодключитьсяКFTP(СтрОшибки = "")
	FTP = Неопределено;
	
	//получаем параметры
	Попытка
		Сервер = НастройкаДоставки.СерверFTP;
		Порт = НастройкаДоставки.ПортFTP;
		Логин = НастройкаДоставки.ПользовательFTP;
		Пароль = НастройкаДоставки.ПарольFTP;
		ПассивныйРежим = НастройкаДоставки.ПассивныйРежим;
	Исключение
		СтрОшибки = СтрОшибки + "Некорректные параметры FTP: " + ИнформацияОбОшибке().Описание;
		Возврат неопределено;
	КонецПопытки;
	
	// Проверим реквизиты подключения к FTP
	Если FTP = НЕОПРЕДЕЛЕНО  Тогда 
		#Если Клиент Тогда
			Состояние("Попытка подключения к FTP...");
		#КонецЕсли
		Попытка 
			FTP = Новый FTPСоединение(Сервер, 
			Порт, 
			Логин, 
			Пароль,
			, 
			ПассивныйРежим);
			#Если Клиент Тогда
				Состояние("Попытка подключения к FTP - ОК");
			#КонецЕсли
		Исключение
			ТекстОшибки = ИнформацияОбОшибке().Описание;
			Если Найти(ВРег(ТекстОшибки),ВРег("couldn't connect to server")) Тогда
			    ТекстОшибки = " на указанном сервере не запущен FTP-сервер";
			ИначеЕсли Найти(ВРег(ТекстОшибки),ВРег("couldn't resolve host name")) Тогда 	
			    ТекстОшибки = " указанный сервер не доступен";
			КонецЕсли; 
			стрОшибки = стрОшибки + "Ошибка подключения к FTP серверу <ftp://" + НастройкаДоставки.СерверFTP + ">: " + ТекстОшибки;
			Возврат неопределено;
		КонецПопытки;
	КонецЕсли;
	
	// Проверим существование "нашего" каталога 		
	Попытка 
		#Если Клиент Тогда
			Состояние("Попытка установить каталог на FTP...");
		#КонецЕсли
		КаталогОбменаFTP = "";
		Если Прав(НастройкаДоставки.КаталогОбменаFTP,1) <> "/" Тогда
			КаталогОбменаFTP = НастройкаДоставки.КаталогОбменаFTP + "/";
		КонецЕсли;
		FTP.УстановитьТекущийКаталог(КаталогОбменаFTP);
	Исключение
		#Если Клиент Тогда
			Состояние("Попытка создать каталог на FTP...");
		#КонецЕсли
		КаталогОбменаFTP = НастройкаДоставки.КаталогОбменаFTP;
		Попытка // попробуем создать "нужный" каталог
			FTP.СоздатьКаталог(КаталогОбменаFTP);
			FTP.УстановитьТекущийКаталог(КаталогОбменаFTP);
		Исключение
			СтрОшибки = "Ошибка: не удалось создать на FTP заданный каталог обмена """ + КаталогОбменаFTP + """";
			Возврат неопределено;
		КонецПопытки;
	КонецПопытки;
	
	#Если Клиент Тогда
		Состояние("Проверяем текущий каталог на FTP...");
	#КонецЕсли
	СтрТекущийКаталог = FTP.ТекущийКаталог();
	Если Прав(СтрТекущийКаталог,1) <> "/" Тогда
		СтрТекущийКаталог = СтрТекущийКаталог + "/";
	КонецЕсли;
	Если Лев(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = "/" + КаталогОбменаFTP;
	КонецЕсли;
	Если ВРЕГ(СтрТекущийКаталог) = ВРЕГ(КаталогОбменаFTP) Тогда
		#Если Клиент Тогда
			Состояние("Проверяем текущий каталог на FTP - ОК");
		#КонецЕсли
		Возврат FTP;
	Иначе
		#Если Клиент Тогда
			Состояние("Проверяем текущий каталог на FTP - Ошибка!");
		#КонецЕсли
		СтрОшибки = "Ошибка: не удалось установить на FTP в качестве текущего заданный каталог обмена """ + КаталогОбменаFTP + """";
		Возврат неопределено;
	КонецЕсли;
	
КонецФункции

// Проверят наличие указанного в параметре каталога
//если отсутствует, то пытается создать новый
//Добавляет заключительный знак "\" к переданному пути
//Возвращает:
//-	Истина если каталог есть (или был создан) 
//-	Ложь в противном случае
//Во втором (необязательном) параметре возвращает текст сообщения об ошибке
Функция ЕстьКаталог(Путь,ТекстОшибки="")
	Если НЕ ЗначениеЗаполнено(Путь) ИЛИ (ТипЗнч(Путь) <> Тип("Строка")) Тогда
		ТекстОшибки = "Ошибка: Неверный параметр";
		Возврат Ложь;
	КонецЕсли;
	Если Прав(Путь,1) <> "\" Тогда
		Путь = Путь + "\";
	КонецЕсли;
    Попытка
		ПроверкаКаталога = Новый Файл(Путь);
		Если НЕ ПроверкаКаталога.Существует() Тогда
			СоздатьКаталог(Путь);
			ТекстОшибки = "Создан каталог: " + Путь;
			ПроверкаКаталога = Новый Файл(Путь);
		КонецЕсли;			
		Если ПроверкаКаталога.Существует() Тогда
			Если ПроверкаКаталога.ЭтоКаталог() Тогда
				Результат = Истина;
			Иначе
				ТекстОшибки = "Ошибка: заданный путь <"+Путь+"> не является каталогом (возможно это файл?)";
				Результат = Ложь;
			КонецЕсли;
		Иначе
			ТекстОшибки = "Ошибка: не удалось создать каталог: <"+Путь+">";
			Результат = Ложь;
		КонецЕсли;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Результат = Ложь;
	КонецПопытки;
	// Возвращаем что у нас получилось
	Возврат Результат;
КонецФункции // ЕстьКаталог

// Выполняет проверку отправки сообщения обмена через ftp-сервер
// 
Функция ПроверитьВозможностьОтправки(FTP=НЕОПРЕДЕЛЕНО,СтрОшибок="") Экспорт
	
	// Создадим XML-файл со случайным именем для проверки работы всех механизмов
	ВременноеИмя = Новый УникальныйИдентификатор();
	ВременноеИмя = "_TstWRAcc_" + СтрЗаменить(ВременноеИмя,"-","") + ".tmp";

	Если НЕ ЕстьКаталог(КаталогТемпФайлов) Тогда
		КаталогТемпФайлов = КаталогВременныхФайлов();
	КонецЕсли;
	ИмяТестовогоФайла = КаталогТемпФайлов + ВременноеИмя;
	
	Попытка
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.ОткрытьФайл(ИмяТестовогоФайла);
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписьXML.Закрыть();
		ЗаписьXML = НЕОПРЕДЕЛЕНО;
	Исключение
		СтрОшибок = ОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;
	// Проверим создался ли наш файл
	ФС = Новый Файл(ИмяТестовогоФайла);
	Если НЕ ФС.Существует() ИЛИ НЕ ФС.ЭтоФайл() Тогда
		ФС = НЕОПРЕДЕЛЕНО;
		СтрОшибок = "Нет прав на запись в папку """ + КаталогТемпФайлов + """";
		Возврат Ложь;
	КонецЕсли;
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяТестовогоФайла;
	ФС = НЕОПРЕДЕЛЕНО; // Не нужен более этот объект
	
	Результат = Ложь;
	// Проверка возможности отправки в зависимости от способа обмена (транспорта)
	Если (НастройкаДоставки.СпособОбмена = 0) ИЛИ (НастройкаДоставки.СпособОбмена = 3) Тогда
		// Сначала проверим правильно ли задан каталог обмена
		КаталогОбмена = "";
		Если НЕ ПроверитьСуществованиеКаталогаОбмена(КаталогОбмена,FTP,СтрОшибок) Тогда
			// Раз нет каталога куда отправить - то можно и не пытаться что-либо отправлять
			Результат = Ложь;
		ИначеЕсли (НастройкаДоставки.СпособОбмена = 0) Тогда
			НовоеМестоРасположениеФайла = КаталогФайловСообщений + ВременноеИмя;
			ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + НовоеМестоРасположениеФайла;
			Попытка
				ПереместитьФайл(ИмяТестовогоФайла,НовоеМестоРасположениеФайла);
				Результат = Истина;
			Исключение 
				СтрОшибок = "Ошибка перемещения тест-файл  в папку """+КаталогФайловСообщений+""" - " + ОписаниеОшибки() + ". ОПЕРАЦИЯ ОТПРАВКИ БУДЕТ ПРЕРВАНА!";
				Результат = Ложь;
			КонецПопытки;
		Иначе // ЗначенияРеквизитов.СпособОбмена = 3
			FTPИмяТестФайла = КаталогОбмена + ВременноеИмя;
			Попытка
				#Если Клиент Тогда
				Состояние("Проверяем доступность FTP на запись...");
				#КонецЕсли
				FTP.Записать(ИмяТестовогоФайла, FTPИмяТестФайла);
				#Если Клиент Тогда
				Состояние("Проверяем доступность FTP на запись - ОК");
				#КонецЕсли
			    Результат = Истина;
			Исключение
				СтрОшибок = ОписаниеОшибки();
				Результат = Ложь;
			КонецПопытки;
			#Если Клиент Тогда
			Состояние("Удаляем временные файлы с FTP...");
			#КонецЕсли
			Попытка FTP.Удалить(FTPИмяТестФайла);
			Исключение КонецПопытки;
		КонецЕсли;
	ИначеЕсли (НастройкаДоставки.СпособОбмена = 1) Тогда
		#Если Клиент Тогда
			Почта = Новый Почта(); 
			Попытка
				Почта.Подключиться();
				Почта.Отключиться();
				Результат = Истина;
			Исключение
				СтрОшибок = "Ошибка: " + ОписаниеОшибки();
				Результат = Ложь;
			КонецПопытки;
		#Иначе
			СтрОшибок = "Ошибка: Почтовая система недоступна на сервере 1С:Предприятие и в модуле внешнего соединения!";
			Результат = Ложь;
		#КонецЕсли
	ИначеЕсли (НастройкаДоставки.СпособОбмена = 2) Тогда
		ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
		ПочтовыйПрофиль.АдресСервераSMTP = НастройкаДоставки.СерверSMTP;
		ПочтовыйПрофиль.ПортSMTP         = НастройкаДоставки.ПортSMTP;
		ПочтовыйПрофиль.ПользовательSMTP = НастройкаДоставки.ПользовательSMTP;
		ПочтовыйПрофиль.ПарольSMTP       = НастройкаДоставки.ПарольSMTP;
		
		Почта = Новый ИнтернетПочта(); 
	  	Попытка
			Почта.Подключиться(ПочтовыйПрофиль);
			Почта.Отключиться();
			Результат = Истина;
		Исключение
			СтрОшибок = "Ошибка: " + ОписаниеОшибки();
			Результат = Ложь;
		КонецПопытки;
	КонецЕсли;
	#Если Клиент Тогда
	Состояние("");
	#КонецЕсли
	Возврат Результат;
	
КонецФункции	//	ПроверитьВозможностьОтправки()

// Производит проверку на существование каталога обмена - локального и на ftp-сервере
//
Функция ПроверитьСуществованиеКаталогаОбмена(СтрКаталогОбмена = "",FTP=НЕОПРЕДЕЛЕНО,СтрОшибки="") Экспорт
	
	// для почты не проверяем
	Если НастройкаДоставки.СпособОбмена = 1 или НастройкаДоставки.СпособОбмена = 2 Тогда Возврат Истина; КонецЕсли;
	
	#Если Клиент Тогда
	Состояние("Проверка каталога обмена...");
	#КонецЕсли

	Если НастройкаДоставки.СпособОбмена = 0 Тогда
		СтрКаталогОбмена = КаталогФайловСообщений;
	ИначеЕсли НастройкаДоставки.СпособОбмена = 3 Тогда	
		СтрКаталогОбмена = НастройкаДоставки.КаталогОбменаFTP;
	КонецЕсли; 
	
	Если ПустаяСтрока(СтрКаталогОбмена) Тогда
		СтрОшибки = "Ошибка: Не указан каталог обмена!";
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		Сообщить(ТекДата + СтрОшибки);
		Возврат Ложь;
	ИначеЕсли (НастройкаДоставки.СпособОбмена = 0) Тогда
		Если ЕстьКаталог(СтрКаталогОбмена) Тогда
			Возврат Истина;
		Иначе
			СтрОшибки = "Ошибка: указан неверный каталог обмена <" + СтрКаталогОбмена + ">";
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + СтрОшибки);
		КонецЕсли;
	ИначеЕсли (НастройкаДоставки.СпособОбмена = 3) Тогда
		Если (Прав(СтрКаталогОбмена,1) <> "/") Тогда
			СтрКаталогОбмена = СтрКаталогОбмена + "/";
		КонецЕсли;
		// Проверим реквизиты подключения к FTP
		Если FTP = НЕОПРЕДЕЛЕНО  Тогда 
			#Если Клиент Тогда
			Состояние("Попытка подключения к FTP...");
			#КонецЕсли
			Попытка 
				FTP = Новый FTPСоединение(НастройкаДоставки.СерверFTP, 
					НастройкаДоставки.ПортFTP, 
					НастройкаДоставки.ПользовательFTP, 
					НастройкаДоставки.ПарольFTP,, 
					НастройкаДоставки.ПассивныйРежим);
				#Если Клиент Тогда
					Состояние("Попытка подключения к FTP - ОК");
				#КонецЕсли
			Исключение
				ТекстОшибки = ИнформацияОбОшибке().Описание;
				Если Найти(ВРег(ТекстОшибки),ВРег("couldn't connect to server")) Тогда
					ТекстОшибки = " на указанном сервере не запущен FTP-сервер";
				ИначеЕсли Найти(ВРег(ТекстОшибки),ВРег("couldn't resolve host name")) Тогда 	
					ТекстОшибки = " указанный сервер не доступен";
				КонецЕсли; 
				стрОшибки = стрОшибки + "Ошибка подключения к FTP серверу <ftp://" + НастройкаДоставки.СерверFTP + ">: " + ТекстОшибки;
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;
		
		// Проверим существование "нашего" каталога 		
		Попытка 
			#Если Клиент Тогда
			Состояние("Попытка установить каталог на FTP...");
			#КонецЕсли
			FTP.УстановитьТекущийКаталог(СтрКаталогОбмена);
		Исключение
			#Если Клиент Тогда
			Состояние("Попытка создать каталог на FTP...");
			#КонецЕсли
			Попытка // попробуем создать "нужный" каталог
				FTP.СоздатьКаталог(СтрКаталогОбмена);
				FTP.УстановитьТекущийКаталог(СтрКаталогОбмена);
			Исключение
				СтрОшибки = "Ошибка: не удалось создать на FTP заданный каталог обмена """ + СтрКаталогОбмена + """";
				ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
				Сообщить(ТекДата + СтрОшибки, СтатусСообщения.ОченьВажное);
				Возврат Ложь;
			КонецПопытки;
		КонецПопытки;
		
		#Если Клиент Тогда
		Состояние("Проверяем текущий каталог на FTP...");
		#КонецЕсли
		СтрТекущийКаталог = FTP.ТекущийКаталог();
		Если ВРЕГ(СтрТекущийКаталог) = ВРЕГ(СтрКаталогОбмена) Тогда
			#Если Клиент Тогда
			Состояние("Проверяем текущий каталог на FTP - ОК");
			#КонецЕсли
			Возврат Истина;
		Иначе
			#Если Клиент Тогда
			Состояние("Проверяем текущий каталог на FTP - Ошибка!");
			#КонецЕсли
			СтрОшибки = "Ошибка: не удалось установить на FTP в качестве текущего заданный каталог обмена """ + СтрКаталогОбмена + """";
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			Сообщить(ТекДата + СтрОшибки, СтатусСообщения.ОченьВажное);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	СтрОшибки = "Ошибка: не удалось определить наличие каталога обмена - обратитесь к разработчику";
	Возврат Ложь; 
	
КонецФункции	//	ПроверитьСуществованиеКаталогаОбмена()


////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕРЕМЕЩЕНИЯ ФАЙЛОВ ДЛЯ РАЗЛИЧНЫХ СПОСОБОВ ДОСТАВКИ

// Функция публикации файла с сообщением обмена
//
// Параметры:
//  ИмяФайла 		- имя файла, содержащего сообщение обмена
//  НомерСообщения  - номер сообщения отправляемых данных
//
Функция ОтправитьФайл_ЛокСеть(ИмяОтправляемогоФайла = "")
	
	// ПРОВЕРКИ
	Рез = Ложь; // все плохо - пока :-)
			
	// Проверка на существование файла-источника в выбранном каталоге
	ФайлИсточник = Новый Файл(ИмяОтправляемогоФайла);
	Если НЕ ФайлИсточник.Существует() Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Файл сообщения не обнаружен <" + ИмяОтправляемогоФайла + ">",,
			СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	
	// в массив для удаления
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	
	// Проверка на существование каталога приемника (корневой каталог)
	Если НайтиФайлы(КаталогФайловСообщений).Количество() = 0 Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Каталог локальной сети не обнаружен <"
			+ КаталогФайловСообщений + ">", , СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;	
	КонецЕсли;
	
	// ОТПРАВКА
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Начало отправки файла <" + ИмяОтправляемогоФайла + ">");

	// Определим полное имя файла-приемника
	ИмяФайлаСообщения = ФайлИсточник.Имя;
	ПолноеИмяФайлаОбменаПриемник = КаталогФайловСообщений + ИмяФайлаСообщения;
	
	// Упакуем сообщение если оно запакованное во временную папку 
	Если НастройкаДоставки.АрхивироватьСообщенияОбмена Тогда
		ПолноеИмяФайлаОбменаПриемник = КаталогФайловСообщений + ФайлИсточник.ИмяБезРасширения + ".zip";
		Рез = ЗапаковатьОтправляемыйФайл(ФайлИсточник);
		Если Рез = Ложь Тогда
			Возврат Рез;
		КонецЕсли;
		ИмяОтправляемогоФайла = ФайлИсточник.ПолноеИмя; //теперь это zip вместо xml
	КонецЕсли;
		
	// Копируем файл в каталог, определенный для данного узла
	// если необходимо - делаем копию уже отправленного сообщения
	Попытка
		// копирование из темп-каталога в каталог-приемник
		ПереместитьФайл(ИмяОтправляемогоФайла, ПолноеИмяФайлаОбменаПриемник);  // файл отправлен
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Успешно отправлен файл <" + ИмяОтправляемогоФайла + "> в локальный каталог <" + КаталогФайловСообщений + ">");
		Рез = Истина;
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка отправки файла  <" + ИмяОтправляемогоФайла + "> в локальный каталог <" + КаталогФайловСообщений + ">: "
			+ ИнформацияОбОшибке().Описание, , СтатусыСобытийПротокола.Ошибка);
		Рез = Ложь;
	КонецПопытки;
	
	// Локальный темп каталог чистим всегда от ВСЕХ наших действий (в т.ч. и следы архивации)
	// для лок.сети копии не нужны - чистим сразу
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Конец отправки файла <" + ИмяОтправляемогоФайла + ">");
	
	Возврат Рез;
	
КонецФункции // ОтправитьФайл_ЛокСеть()

// Функция публикации файла с сообщением обмена
//
// Параметры:
//  ИмяФайла 		- имя файла, содержащего сообщение обмена
//  НомерСообщения  - номер сообщения отправляемых данных
//
Функция ОтправитьФайл_FTP(ИмяОтправляемогоФайла = "")
	
	// ПРОВЕРКИ

	//проверка на существование файла-источника
	ФайлИсточник = Новый Файл(ИмяОтправляемогоФайла);
	Если НЕ ФайлИсточник.Существует() Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Файл сообщения не обнаружен <" + ИмяОтправляемогоФайла + ">",,
			СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	ПолноеИмяИсходногоФайла = ФайлИсточник.ПолноеИмя;
	ИмяИсходногоФайла = ФайлИсточник.Имя;
	// в массив для удаления
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	
	// Упакуем сообщение 
	Если НастройкаДоставки.АрхивироватьСообщенияОбмена Тогда
		Рез = ЗапаковатьОтправляемыйФайл(ФайлИсточник); // теперь это zip
		Если Рез = Ложь Тогда
			Возврат Рез;
		КонецЕсли;
		ИмяОтправляемогоФайла = ФайлИсточник.ПолноеИмя;
		// в массив для удаления
		ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	КонецЕсли;
	
	// транслит...
	Транслит = НастройкаДоставки.ВыполнятьТранслитерацию;
	ИмяФайлаСообщения = ФайлИсточник.Имя;
	Если Транслит Тогда
		ИмяФайлаСообщения = одОбменДанными.ВыполнитьТранслит(ИмяФайлаСообщения);
	КонецЕсли;

	КаталогОбменаFTP = НастройкаДоставки.КаталогОбменаFTP;
	Если Прав(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = КаталогОбменаFTP + "/";
	КонецЕсли;
	Если Лев(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = "/" + КаталогОбменаFTP;
	КонецЕсли;
	ПолноеИмяФайлаОбменаПриемник = КаталогОбменаFTP + ИмяФайлаСообщения;
	
	// каталог-источник на FTP - еще раз проверили (с момента послед. проверки связь могла пропасть
	СтрОшибкиFTP = "";
	FTP = ПодключитьсяКFTP(СтрОшибкиFTP);
	Если FTP = Неопределено Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрОшибкиFTP,,	СтатусыСобытийПротокола.Ошибка);
		СтрОшибки = СтрОшибки + СтрОшибкиFTP + Символы.ПС;
		Возврат Ложь;
	КонецЕсли;
	
	// ОТПРАВКА
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Начало отправки файла <" + ИмяОтправляемогоФайла + ">");
	Рез = Ложь; // все плохо

	//пишем в каталог обмена файл
	Попытка
		FTP.Записать(ИмяОтправляемогоФайла, ПолноеИмяФайлаОбменаПриемник);
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Отправлен файл на <ftp://" + НастройкаДоставки.СерверFTP  + ПолноеИмяФайлаОбменаПриемник + ">",
			, СтатусыСобытийПротокола.Информация);
		Рез = Истина; // отправили
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка отправки файла на <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаПриемник + ">: "
			+ ИнформацияОбОшибке().Описание, , СтатусыСобытийПротокола.Ошибка);
		Рез = Ложь;
	КонецПопытки;
	
	Если Рез Тогда // Почистим файлы устаревших сообщений если есть там
		ПозРазделителя = Найти(ПолноеИмяФайлаОбменаПриемник,".");
		НомерОтправленного = Сред(ПолноеИмяФайлаОбменаПриемник,ПозРазделителя-10,10);
		МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Истина, "??????????", "*", , Транслит);
		FTPФайлыДляУдаления = FTP.НайтиФайлы(КаталогОбменаFTP, МаскаФайловУдаления);
		Для Каждого FTPФайл Из FTPФайлыДляУдаления Цикл
			ИмяFTPФайла = FTPФайл.Имя;
			ПозРазделителя = Найти(ИмяFTPФайла,".");
			Номер = Сред(ИмяFTPФайла,ПозРазделителя-10,10);
			Если Номер < НомерОтправленного Тогда
				Попытка // Пытаемся удалить файл со старыми сообщением
					FTP.Удалить(FTPФайл.ПолноеИмя, );
					СтрСообщения = "Удалено устаревшее сообщение <" + ИмяFTPФайла + ">";
					ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения);
				Исключение
					СтрОшибкиFTP = ОписаниеОшибки();
					СтрСообщения = "Ошибка удаления устаревшего сообщения <" + ИмяFTPФайла + "> - "+СтрОшибкиFTP;
					ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Предупреждение);
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// перемещение из темп-каталога в каталог-отправленных сообщений
	Если Рез И НастройкаДоставки.СохранятьОтправленные Тогда 
		Попытка
		    ПолноеИмяОтправленногоФайла = КаталогОтправленныхСообщений + ИмяИсходногоФайла;
			ПереместитьФайл(ПолноеИмяИсходногоФайла, ПолноеИмяОтправленногоФайла);
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Сохранена копия файла <" + ПолноеИмяОтправленногоФайла + ">");
		Исключение
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка при сохранении копии файла <" + ПолноеИмяОтправленногоФайла + ">: "
			+ ИнформацияОбОшибке().Описание, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		КонецПопытки;
	КонецЕсли;
	
	// Локальный темп каталог чистим всегда от ВСЕХ наших действий (в т.ч. и следы архивации)
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	FTP = Неопределено;
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Конец отправки файла <" + ИмяОтправляемогоФайла + ">");
	
	Возврат Рез;

КонецФункции // ОтправитьФайл_FTP()

// Отправка файла по электронной почте
Функция ОтправитьФайл_Почта(ИмяОтправляемогоФайла = "") Экспорт
	
	// ПРОВЕРКА
	//проверка на существование файла-источника
	ФайлИсточник = Новый Файл(ИмяОтправляемогоФайла);
	Если НЕ ФайлИсточник.Существует() Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Файл сообщения не обнаружен <" + ИмяОтправляемогоФайла + ">",,
			СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	ПолноеИмяИсходногоФайла = ФайлИсточник.ПолноеИмя;
	ИмяИсходногоФайла = ФайлИсточник.Имя;
	// в массив для удаления
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	
	// Упакуем сообщение 
	Если НастройкаДоставки.АрхивироватьСообщенияОбмена Тогда
		Рез = ЗапаковатьОтправляемыйФайл(ФайлИсточник); // теперь это zip
		Если Рез = Ложь Тогда
			Возврат Рез;
		КонецЕсли;
		ИмяОтправляемогоФайла = ФайлИсточник.ПолноеИмя;
		// в массив для удаления
		ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	КонецЕсли;
	
	// транслит...
	Транслит = НастройкаДоставки.ВыполнятьТранслитерацию;
	ИмяФайлаСообщения = ФайлИсточник.Имя;
	Если Транслит Тогда
		ИмяФайлаСообщения = одОбменДанными.ВыполнитьТранслит(ИмяФайлаСообщения);
	КонецЕсли;

    // проверили почту
	СтрОшибкиПочта = "";
	Почта = ПодключитьсяКПочте(СтрОшибкиПочта);
	Если Почта = Неопределено Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрОшибкиПочта, , СтатусыСобытийПротокола.Ошибка);
		СтрОшибки = СтрОшибки + СтрОшибкиПочта + Символы.ПС;
		Возврат Ложь;
	КонецЕсли;
	
	// ОТПРАВКА
	Рез = Ложь; // все плохо
	
	// MAPI
	Если НастройкаДоставки.СпособОбмена = 1 Тогда
		#Если Клиент Тогда
			ПочтовоеСообщение = Новый ПочтовоеСообщение;
		#Иначе
			ЗаписьЛога(ПрефиксЛога + "Отправка", 
				"Почтовый профиль пользователя недоступен в режиме внешнего соединения или на сервере!", , СтатусыСобытийПротокола.Ошибка);
			Возврат Ложь;
		#КонецЕсли
		// SMTP/POP3   
	Иначе	
		ПочтовоеСообщение = Новый ИнтернетПочтовоеСообщение;
		ПочтовоеСообщение.Кодировка = "windows-1251";
	КонецЕсли;
	
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Начало отправки файла <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">");
	Рез = Истина;
	
	// Формируем сообщение обмена
	ПочтовоеСообщение.Тема = "1С: СООБЩЕНИЕ ОБМЕНА: " + ИмяФайлаСообщения;
	ПочтовоеСообщение.Получатели.Добавить(НастройкаДоставки.АдресЭлектроннойПочты);
	ПочтовоеСообщение.Отправитель = НастройкаДоставки.АдресЭлектроннойПочты;
			
	// Создаем вложение с файлом обмена
	ПочтовоеСообщение.Вложения.Добавить(ИмяОтправляемогоФайла, ИмяФайлаСообщения);
	
	// Отправляем сообщение с файлом обмена
	Попытка
		Почта.Послать(ПочтовоеСообщение);
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Отправлен файл <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">.",
			,СтатусыСобытийПротокола.Информация);
		Рез = Истина; // отправили
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка при отправке файла <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">: "
			+ ИнформацияОбОшибке().Описание, , СтатусыСобытийПротокола.Ошибка);
		Рез = Ложь;
	КонецПопытки;
	
	// перемещение из темп-каталога в каталог-отправ.сообщений
	Если Рез И НастройкаДоставки.СохранятьОтправленные Тогда 
		Попытка
		    ПолноеИмяОтправленногоФайла = КаталогОтправленныхСообщений + ИмяИсходногоФайла;
			ПереместитьФайл(ПолноеИмяИсходногоФайла, ПолноеИмяОтправленногоФайла);
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Сохранена копия файла <" + ПолноеИмяОтправленногоФайла + ">");
		Исключение
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка при сохранении копии файла <" + ПолноеИмяОтправленногоФайла + ">: "
			+ ИнформацияОбОшибке().Описание, ,СтатусыСобытийПротокола.Ошибка);
		КонецПопытки;
	КонецЕсли;
	
	// Локальный темп каталог чистим всегда от ВСЕХ наших действий (в т.ч. и следы архивации)
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;

	Попытка
		Почта.Отключиться();
	Исключение
	КонецПопытки;
	
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Конец отправки файла <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">");
	
	Возврат Истина;
КонецФункции

// Выполняет сохранение обработанного сообщения
// Параметры:
//	ИмяФайлаСообщения - имя файла сообщения
// Возвращает истина если сохранение прошло без ошибок иначе ложь
//
Функция СохранитьОбработанноеСообщение(ИмяФайлаСообщения)  Экспорт
	
	Рез = Ложь;
	Если ИмяФайлаСообщения = "" Тогда
	    ЗаписьЛога(ПрефиксЛога + "Получение", "Файл для сохранения обработанного сообщения не обнаружен <"
			+ КаталогОбработанныхСообщений + ">", , СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли; 
	
	//проверка на существование файла-источника
	ФайлИсточник = Новый Файл(ИмяФайлаСообщения);
	Если НЕ ФайлИсточник.Существует() Тогда
		ЗаписьЛога(ПрефиксЛога + "Получение", "Файл для сохранения обработанного сообщения не обнаружен <"
			+ КаталогОбработанныхСообщений + ">", , СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	КраткоеИмяФайлаСообщения = ФайлИсточник.Имя;

	ПолноеИмяОтправленногоФайла = КаталогОбработанныхСообщений + КраткоеИмяФайлаСообщения;
	Попытка
	    // SUIG делаем именно копирование, если не требуется оставлять последнее принятое сообщение
		// в корне каталога обменов - тогда ПереместитьФайл
		КопироватьФайл(ИмяФайлаСообщения,ПолноеИмяОтправленногоФайла);
		ЗаписьЛога(ПрефиксЛога + "Получение", "Сохранена копия файла <" + ПолноеИмяОтправленногоФайла + ">");
		Рез = Истина;
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Получение", "Ошибка при сохранении копии файла <" + ПолноеИмяОтправленногоФайла + ">: "
		+ ИнформацияОбОшибке().Описание, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Рез = Ложь;
	КонецПопытки;
	
	Возврат Рез;
	
КонецФункции
 
// Получение файла из FTP-сервера
Функция ПолучитьФайл_FTP(ИмяПолученногоФайла = "") Экспорт
	// обработали каталоги
	КаталогОбменаFTP = НастройкаДоставки.КаталогОбменаFTP;
	Если Прав(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = КаталогОбменаFTP + "/";
	КонецЕсли;
	Если Лев(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = "/" + КаталогОбменаFTP;
	КонецЕсли;
	
	// Подключимся к фтп повторно
	СтрОшибкиFTP = "";
	FTP = ПодключитьсяКFTP(СтрОшибкиFTP);
	Если FTP = Неопределено Тогда
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрОшибкиFTP,, СтатусыСобытийПротокола.Ошибка); 
		Возврат Ложь;
	КонецЕсли;
	
	Транслит = НастройкаДоставки.ВыполнятьТранслитерацию;
	// Получаем маску для поиска файлов с сообщениями обмена
	МаскаФайловИсточник = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*",,Транслит);
	МаскаФайловПриемник = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*",,Ложь);
	Попытка
		Файлы = FTP.НайтиФайлы(КаталогОбменаFTP, МаскаФайловИсточник);
	Исключение
		СтрОшибкиFTP = ОписаниеОшибки();
		Если Найти(Врег(СтрОшибкиFTP),ВРег("access denied")) Тогда
			СтрОшибкиFTP = "Ошибка аутентификации при доступе к ресурсу <ftp://" + НастройкаДоставки.СерверFTP + НастройкаДоставки.КаталогОбменаFTP + ">";
		КонецЕсли; 
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрОшибкиFTP,, СтатусыСобытийПротокола.Ошибка);
		ВызватьИсключение СтрОшибкиFTP;
		Возврат Ложь;
	КонецПопытки;
	
	Если Файлы.Количество() = 0 Тогда
		// А вдруг несогласованны галочки Транслитераци у отправителя получателя
		Транслит = НЕ Транслит; // Попробуем с настройкой "наоборот" посмотреть
		МаскаФайловИсточник = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*",,Транслит);
		Файлы = FTP.НайтиФайлы(КаталогОбменаFTP, МаскаФайловИсточник);
	КонецЕсли;

	// получим ожидаемое имя файла для приема
	ОжидаемоеИмяФайла = одОбменДанными.ИмяФайлаДоставки(УзелОбмена,Ложь,Неопределено,"*", ,Транслит);
	ОжидаемоеИмяФайла = Лев(ОжидаемоеИмяФайла,Найти(ОжидаемоеИмяФайла,".")-1);
	#Если Клиент Тогда
		Состояние("Ждите. Идет поиск файла обмена на <ftp://" + НастройкаДоставки.СерверFTP + НастройкаДоставки.КаталогОбменаFTP);
	#КонецЕсли
	ПолноеИмяФайлаОбменаИсточник = ""; ИмяФайлаОбменаИсточник = "";
	ИмяСМаксНомером = "";
	
	// Отбираем файл с максимальным номером в имени
	ИмяФайлаСообщенияФТП = "";
	FTPФайлыДляУдаления = Новый Массив;
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ИмяБезРасширения < ОжидаемоеИмяФайла Тогда  // Это устаревшие сообщения, сразу пропускаем их обработку
			FTPФайлыДляУдаления.Добавить(Файл.ПолноеИмя);
			Продолжить;
		КонецЕсли;
		Если Файл.ИмяБезРасширения > ИмяСМаксНомером Тогда
			Если НЕ ПустаяСтрока(ПолноеИмяФайлаОбменаИсточник) Тогда
				FTPФайлыДляУдаления.Добавить(ПолноеИмяФайлаОбменаИсточник);	// Не успели его прочитать как оно уже устарело (прислали с еще бОльшим номером)
			КонецЕсли;
			ПолноеИмяФайлаОбменаИсточник = Файл.ПолноеИмя;
			ИмяСМаксНомером = Файл.ИмяБезРасширения;
			ИмяФайлаСообщенияФТП = Файл.Имя;
		КонецЕсли;	
	КонецЦикла;
	#Если Клиент Тогда
	Состояние("");
	#КонецЕсли
	// Преобразовали из транслита
	Если Транслит И НЕ ПустаяСтрока(ИмяФайлаСообщенияФТП) Тогда
		Номер = СтрЗаменить(ИмяФайлаСообщенияФТП,"MessageExchange_","");
		Номер = Прав(СтрЗаменить(Номер,Файл.Расширение,""),10);
		ИмяФайлаСообщения = СтрЗаменить(МаскаФайловПриемник,"??????????",Номер);
		ИмяФайлаСообщения = СтрЗаменить(ИмяФайлаСообщения,".*",Файл.Расширение);
	Иначе
		ИмяФайлаСообщения = ИмяФайлаСообщенияФТП;
	КонецЕсли;
	
	ПолноеИмяФайлаОбменаПриемник = КаталогТемпФайлов + ИмяФайлаСообщения;
	ПолноеИмяФайлаОбменаИсточник = КаталогОбменаFTP + ИмяФайлаСообщенияФТП;
	
	Рез = Ложь; // Начнем с утверждения что все плохо и своими действиями изменим ситуацию к лучшему :-)
		
	Если Не ПустаяСтрока(ИмяФайлаСообщенияФТП) Тогда
		СтрСообщения = "Начало получения файла <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + ">";
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения);
		
		// Получаем файл с сообщением обмена в указанный файл
		Попытка
		
			ГрузитьИзFTP = Истина; ФайлПолучен = Ложь;
			// Проверим в каталоге обменов готового файла с сообщением обмена
			Файлы = НайтиФайлы(КаталогФайловСообщений,Лев(ИмяФайлаСообщения, СтрДлина(ИмяФайлаСообщения)-3) + "*");
			Если Файлы.Количество() > 0 Тогда
				#Если Клиент Тогда
					ГрузитьИзFTP = (Вопрос("Обнаружен файл сообщения <" + Файлы[0].ПолноеИмя +  ">" + Символы.ПС + " аналогичный файлу обмена на FTP!
					| Загрузить изменения из данного файла?"
					,РежимДиалогаВопрос.ОКОтмена,20,КодВозвратаДиалога.ОК)=КодВозвратаДиалога.Отмена);
				#Иначе
					ГрузитьИзFTP = Ложь; 
				#КонецЕсли
			КонецЕсли;			
			
			Если ГрузитьИзFTP Тогда
				Попытка
					// SUIG - расскоментировать для удаления текущего обработанного файла
					// иначе будет всегда оставаться самый последний обработанный файл
					ТемпФайлыДляУдаления = ПолноеИмяФайлаОбменаПриемник + Символы.ПС + ПолноеИмяФайлаОбменаПриемник;
					FTP.Получить(ПолноеИмяФайлаОбменаИсточник, ПолноеИмяФайлаОбменаПриемник);
					ЗаписьЛога(ПрефиксЛога + "Получение", "Успешно получен файл <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + ">"
					 + " в локальный каталог <" + ПолноеИмяФайлаОбменаПриемник + ">");
					ФайлПолучен = Истина;
				Исключение
					СтрОшибка = "Ошибка получения файла с <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + ">" + ОписаниеОшибки();
					ЗаписьЛога(ПрефиксЛога + "Получение", СтрОшибка, , СтатусыСобытийПротокола.Ошибка);
					Возврат Ложь;	
				КонецПопытки;
			Иначе
				ФайлПолучен = Истина;
				ПолноеИмяФайлаОбменаПриемник = Файлы[0].ПолноеИмя;
			КонецЕсли;
			
			ФайлСообщения = Новый Файл(ПолноеИмяФайлаОбменаПриемник);
			Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
				ВызватьИсключение "Не найдено полученное с <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + ">" 
				+ " сообщение обмена <" + ПолноеИмяФайлаОбменаПриемник + ">";
				Возврат Ложь;
			КонецЕсли;
			
			// Распакуем сообщение если оно запакованное во временную папку и в случае успеха
			// переместим результат в каталог обменов
			Если НРег(Прав(ПолноеИмяФайлаОбменаПриемник, 3)) = "zip" Тогда
				Рез = РаспаковатьПолученныйФайл(ФайлСообщения);
				Если Рез = Ложь Тогда
				   Возврат Рез;
			   КонецЕсли;
			   ПолноеИмяФайлаОбменаПриемник = ФайлСообщения.ПолноеИмя;
			КонецЕсли;
			
			ПутьФайла=ВРЕГ(ФайлСообщения.Путь);
			Если Прав(ПутьФайла,1) <> "\" Тогда
				ПутьФайла = ПутьФайла + "\";
			КонецЕсли;
			Если ПутьФайла = ВРЕГ(КаталогФайловСообщений) Тогда
				Рез = Истина; // Файл уже лежит где нужно
				ИмяПолученногоФайла = ФайлСообщения.ПолноеИмя;
			Иначе
				// Перемещение готового сообщения в каталог обменов
				Попытка
					ПереместитьФайл(ФайлСообщения.ПолноеИмя,КаталогФайловСообщений+ФайлСообщения.Имя);
					ИмяПолученногоФайла = КаталогФайловСообщений+ФайлСообщения.Имя;;
					Рез = Истина;
				Исключение
					ВызватьИсключение "Не удалось доставить файл сообщения <" + ФайлСообщения.ПолноеИмя + "> в каталог <" + КаталогФайловСообщений + "> "+ОписаниеОшибки();
				КонецПопытки;
			КонецЕсли;			
			
			ФайлСообщения = НЕОПРЕДЕЛЕНО;
			
		Исключение
			ЗаписьЛога(ПрефиксЛога + "Получение", "Ошибка получения файла  <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + "> "
				+ ИнформацияОбОшибке().Описание,, СтатусыСобытийПротокола.Ошибка);
			Рез = Ложь;
		КонецПопытки;
		
	Иначе
		ЗаписьЛога(ПрефиксЛога + "Получение", "Файл сообщения не обнаружен на <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + "> ",, СтатусыСобытийПротокола.Информация);
		Рез = Истина; // ну нет его там
	КонецЕсли;
	
	// ЗАКЛЮЧИТЕЛЬНЫЕ ДЕЙСТВИЯ
	// Очистим фтп каталог от ненужных файлов
	Если FTPФайлыДляУдаления.Количество()>0 Тогда
		Попытка
			FTP.УстановитьТекущийКаталог(КаталогОбменаFTP);
		Исключение
			FTP = ПодключитьсяКFTP(СтрОшибкиFTP);
		КонецПопытки;
		Если FTP = НЕОПРЕДЕЛЕНО Тогда
			СтрСообщения = "Ошибка возобновления соединения с сервером <ftp://" + НастройкаДоставки.СерверFTP + "> - "+СтрОшибкиFTP + Символы.ПС + "Файлы устаревших сообщений не будут удалены";
			ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Предупреждение);
		Иначе
			Для Каждого Файл Из FTPФайлыДляУдаления Цикл
				Попытка // Пытаемся удалить файл со старыми сообщением
					FTP.Удалить(Файл, );
					СтрСообщения = "Удален файл устаревшего сообщения<ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + ">";
					ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения);
				Исключение
					СтрОшибкиFTP = ОписаниеОшибки();
					СтрСообщения = "Ошибка удаления устаревшего сообщения <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + "> - "+СтрОшибкиFTP;
					ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Предупреждение);
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;			
	КонецЕсли;
	
	// Локальный темп каталог чистим всегда от наших действий
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	СтрСообщения = "Конец получения файла <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаИсточник + ">";
	ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения);
    
	// Возвращаем результат 
	Возврат Рез;
КонецФункции

// Функция получения файла с новым сообщением обмена
// Ищет и копирует в указанный файл файл с последним сообщением обмена
//
// Параметры:
//  ИмяФайла	- файл, в который будет произведено копирование файла с сообщением обмена
//
// Возвращаемое значение:
//  Булево - Истина - если файл с сообщением обмена получен и Ложь - в противном случае
//
Функция ПолучитьФайл_ЛокСеть(ИмяПолученногоФайла = "")
	
	СтрСообщения = "Начало получения файла из локального каталога <" + КаталогФайловСообщений + ">";
	ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения);
	
	// Получаем маску для поиска файлов с сообщениями обмена
	МаскаФайловИсточник = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*"); 

	// получим ожидаемое имя файла для приема
	ОжидаемоеИмяФайла = одОбменДанными.ИмяФайлаДоставки(УзелОбмена,Ложь,Неопределено,"*", ,Ложь);
	ОжидаемоеИмяФайла = Лев(ОжидаемоеИмяФайла,Найти(ОжидаемоеИмяФайла,".")-1);
	#Если Клиент Тогда
		Состояние("Ждите. Идет получение файла обмена ...");
	#КонецЕсли
	ПолноеИмяФайлаОбменаИсточник = ""; ИмяФайлаОбменаИсточник = "";
	ИмяСМаксНомером = "";
	
	// Отбираем файл с максимальным номером в имени в каталоге-источнике
	Файлы = НайтиФайлы(КаталогФайловСообщений, МаскаФайловИсточник);
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ИмяБезРасширения < ОжидаемоеИмяФайла Тогда  // Это устаревшие сообщения, сразу пропускаем их обработку
			ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + Файл.ПолноеИмя;
			Продолжить;
		КонецЕсли;
		
		Если Файл.ИмяБезРасширения > ИмяСМаксНомером Тогда
			// Не успели его прочитать как оно уже устарело (прислали с еще бОльшим номером)
			Если НЕ ПустаяСтрока(ПолноеИмяФайлаОбменаИсточник) Тогда
				ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ПолноеИмяФайлаОбменаИсточник; // Предыдущего кандидата переносим в черный список
			КонецЕсли;
			ПолноеИмяФайлаОбменаИсточник = Файл.ПолноеИмя;	// Здесь теперь запоминаем нового кандидата
			мИмяСМаксНомером = Файл.ИмяБезРасширения;		// Номерок перещелкнем для следующих сравнений
		КонецЕсли;	
	КонецЦикла;
	
	Рез = Ложь;
	
	ПолноеИмяФайлаОбменаПриемник = ПолноеИмяФайлаОбменаИсточник; // это один и тот же файл
	Если ПустаяСтрока(ПолноеИмяФайлаОбменаИсточник) Тогда
		СтрСообщения = "Не найдено сообщение обмена в локальном каталоге <" + КаталогФайловСообщений + ">";
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Информация);
		Возврат Истина; // ну нет его там и все тут
	КонецЕсли;
	
	
	ФайлСообщения = Новый Файл(ПолноеИмяФайлаОбменаПриемник);
	Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
		СтрСообщения = "Не найдено сообщение обмена: " + ПолноеИмяФайлаОбменаПриемник + " в локальном каталоге <" + КаталогФайловСообщений + ">";
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Информация);
		Возврат Истина; // ну нет его там и все тут
	КонецЕсли;
	
	// Распакуем сообщение если оно запакованное во временную папку и в случае успеха переместим
	// результат в каталог обменов
	Если НРег(Прав(ПолноеИмяФайлаОбменаПриемник, 3)) = "zip" Тогда
		Рез = РаспаковатьПолученныйФайл(ФайлСообщения);
		Если Рез = Ложь Тогда
			Возврат Рез;
		КонецЕсли;
		ПолноеИмяФайлаОбменаПриемник = ФайлСообщения.ПолноеИмя; //теперь это xml вместо zip
	КонецЕсли;
	
	ПутьФайла=ВРЕГ(ФайлСообщения.Путь);
	Если Прав(ПутьФайла,1) <> "\" Тогда
		ПутьФайла = ПутьФайла + "\";
	КонецЕсли;
	Если ПутьФайла = ВРЕГ(КаталогФайловСообщений) Тогда
		Рез = Истина; // Файл уже лежит где нужно
		ИмяПолученногоФайла = ФайлСообщения.ПолноеИмя;
	Иначе
		// Перемещение готового сообщения в каталог обменов
		Попытка
			ПереместитьФайл(ФайлСообщения.ПолноеИмя,КаталогФайловСообщений+ФайлСообщения.Имя);
			ИмяПолученногоФайла = КаталогФайловСообщений + ФайлСообщения.Имя;;
			Рез = Истина;
			СтрСообщения = "Успешно получен файл сообщения <" + ФайлСообщения.ПолноеИмя + "> в каталог <" + КаталогФайловСообщений + "> "+ОписаниеОшибки();
			ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Информация);

		Исключение
			СтрСообщения = "Не удалось получить файл сообщения <" + ФайлСообщения.ПолноеИмя + "> в каталог <" + КаталогФайловСообщений + "> "+ОписаниеОшибки();
			ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Ошибка);
			Рез = Ложь;
		КонецПопытки;
	КонецЕсли;			
	
	// Локальный темп каталог чистим всегда от наших действий
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;

	СтрСообщения = "Конец получения файла сообщения <" + ФайлСообщения.ПолноеИмя + "> из локального каталога <" + КаталогФайловСообщений + ">";

	Возврат Рез;
	
КонецФункции // ПолучитьФайлОбмена(ИмяФайла)

// Получение файла по электронной почте
Функция ПолучитьФайл_Почта(ИмяПолученногоФайла = "") Экспорт
	
	ЗаписьЛога(ПрефиксЛога + "Получение", "Начало получения файла  из почтового сервера " + НастройкаДоставки.АдресЭлектроннойПочты);
	
	// попытка подключения
	СтрОшибкиПочта = "";
	Почта = ПодключитьсяКПочте(СтрОшибкиПочта);
	Если Почта = Неопределено Тогда
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрОшибкиПочта,, СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	
	ТемаСообщения = ""; ИмяФайлаСообщения = ""; АдресОтправителя = "";
	
	МаскаТемыСообщенияОбмена = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "", ""); // "MessageExchange_Ц_П1_..."
	МаскаТемыСообщенияОбмена = Лев(МаскаТемыСообщенияОбмена, СтрДлина(МаскаТемыСообщенияОбмена)-1); // "MessageExchange_Ц_П1_"
	МаскаТемыСообщенияОбмена = "1С: СООБЩЕНИЕ ОБМЕНА: " + МаскаТемыСообщенияОбмена;
	
	// ПРОВЕРКА ПИСЕМ - MAPI
	Если НастройкаДоставки.СпособОбмена = 1 Тогда
		МассивСообщенийОбмена = Почта.Выбрать(Истина, Ложь);
		Если МассивСообщенийОбмена.Количество() = 0 Тогда
			// Сообщений в почтовом ящике нет
			ЗаписьЛога(ПрефиксЛога + "Получение", "Писем на почтовом сервере <" + НастройкаДоставки.АдресЭлектроннойПочты + "> не обнаружено",, СтатусыСобытийПротокола.Информация);
			Возврат Истина;
		КонецЕсли;
		
		Для Индекс = 0 По МассивСообщенийОбмена.Количество() - 1 Цикл
			// Отсеиваем только те сообщения, которые содержат файлы обмена для участвующих в обмене узлов.
			Если Лев(МассивСообщенийОбмена[Индекс].Тема, СтрДлина(МаскаТемыСообщенияОбмена)) = МаскаТемыСообщенияОбмена Тогда
				// Выбираем сообщение с последним номером сообщения обмена
				Если МассивСообщенийОбмена[Индекс].Тема > ТемаСообщения Тогда
					ИмяФайлаСообщения = МассивСообщенийОбмена[Индекс].Вложения[0].Наименование;
					АдресОтправителя = МассивСообщенийОбмена[Индекс].Отправитель;
					Если ТипЗнч(АдресОтправителя) <> Тип("Строка") Тогда
						АдресОтправителя = АдресОтправителя.Адрес;
					КонецЕсли;
					ИндексСообщения = Индекс;
					ТемаСообщения = МассивСообщенийОбмена[Индекс].Тема;
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
		
		Если ПустаяСтрока(ТемаСообщения) Тогда
			// Сообщений в почтовом ящике нет
			ЗаписьЛога(ПрефиксЛога + "Получение", "Писем по маске <" + МаскаТемыСообщенияОбмена 
				+ "> не обнаружено на почтовом сервере <" + НастройкаДоставки.АдресЭлектроннойПочты + ">",, СтатусыСобытийПротокола.Информация);
			Возврат Истина; // нет их там
        КонецЕсли;
	
	Иначе
		// SMTP / POP3
		Заголовки = Почта.ПолучитьЗаголовки();
		Если Заголовки.Количество() = 0 Тогда
			// Сообщений в почтовом ящике нет
			ЗаписьЛога(ПрефиксЛога + "Получение", "Писем на почтовом сервере  <" + НастройкаДоставки.АдресЭлектроннойПочты + "> не обнаружено",, СтатусыСобытийПротокола.Информация);
			Возврат Истина;
		КонецЕсли;
		
		Для Индекс = 0 По Заголовки.Количество() - 1 Цикл
			// Отсеиваем только те сообщения, которые содержат файлы обмена для участвующих в обмене узлов.
			Если Лев(Заголовки[Индекс].Тема, СтрДлина(МаскаТемыСообщенияОбмена)) = МаскаТемыСообщенияОбмена Тогда
				// Выбираем сообщение с последним номером сообщения обмена
				Если Заголовки[Индекс].Тема > ТемаСообщения Тогда
					МассивЗаголовков = Новый Массив();
					МассивЗаголовков.Добавить(Заголовки[Индекс]);
		
					ТемаСообщения = Заголовки[Индекс].Тема;
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
		
		Если ПустаяСтрока(ТемаСообщения) Тогда
			// Сообщений в почтовом ящике нет
			ЗаписьЛога(ПрефиксЛога + "Получение", "Писем по маске <" + МаскаТемыСообщенияОбмена 
				+ " на почтовом сервере <" + НастройкаДоставки.АдресЭлектроннойПочты + "> не обнаружено",, СтатусыСобытийПротокола.Информация);
			Возврат Истина;
        КонецЕсли;
		
		МассивСообщенийОбмена = Почта.Выбрать(Ложь, МассивЗаголовков);
		Если МассивСообщенийОбмена.Количество() = 0 Тогда
			// Сообщений в почтовом ящике нет
			ЗаписьЛога(ПрефиксЛога + "Получение", "Писем по маске <" + МаскаТемыСообщенияОбмена 
				+ " на почтовом сервере <" + НастройкаДоставки.АдресЭлектроннойПочты + "> не обнаружено",, СтатусыСобытийПротокола.Информация);
			Возврат Истина;
		КонецЕсли;
		
		ИндексСообщения = 0;
		
		ИмяФайлаСообщения = МассивСообщенийОбмена[ИндексСообщения].Вложения[0].Имя;
		АдресОтправителя = МассивСообщенийОбмена[ИндексСообщения].Отправитель.Адрес;
	КонецЕсли;

	// Записываем файл обмена во временный файл
	ПолноеИмяФайлаОбменаПриемник = КаталогТемпФайлов + ИмяФайлаСообщения;
    
	Рез = Ложь; // пока все плохо
	
	Попытка
		Попытка
			МассивСообщенийОбмена[ИндексСообщения].Вложения[0].Данные.Записать(ПолноеИмяФайлаОбменаПриемник);
			ЗаписьЛога(ПрефиксЛога + "Получение", "Получен файл <" + ИмяФайлаСообщения + ">");
		Исключение
			ЗаписьЛога(ПрефиксЛога + "Получение", "Ошибка получения файла <"
			+ ИмяФайлаСообщения + ">: " + ИнформацияОбОшибке().Описание, , СтатусыСобытийПротокола.Ошибка);
			Рез = Ложь;
		КонецПопытки;
		
		ФайлСообщения = Новый Файл(ПолноеИмяФайлаОбменаПриемник);
		Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
			ВызватьИсключение "Не найдено сообщение обмена: " + ПолноеИмяФайлаОбменаПриемник;
			Возврат Ложь;
		КонецЕсли;
		
		// Распакуем сообщение если оно запакованное во временную папку
		// и в случае успеха переместим результат в каталог обменов
		Если НРег(Прав(ПолноеИмяФайлаОбменаПриемник, 3)) = "zip" Тогда
			Рез = РаспаковатьПолученныйФайл(ФайлСообщения);
			Если Рез = Ложь Тогда
				Возврат Ложь;
			КонецЕсли;
			ПолноеИмяФайлаОбменаПриемник = ФайлСообщения.ПолноеИмя;
		КонецЕсли;
		
		ПутьФайла=ВРЕГ(ФайлСообщения.Путь);
		Если Прав(ПутьФайла,1) <> "\" Тогда
			ПутьФайла = ПутьФайла + "\";
		КонецЕсли;
		Если ПутьФайла = ВРЕГ(КаталогФайловСообщений) Тогда
			Рез = Истина; // Файл уже лежит где нужно
			ИмяПолученногоФайла = ФайлСообщения.ПолноеИмя;
		Иначе
			// Перемещение готового сообщения в каталог обменов
			Попытка
				ПереместитьФайл(ФайлСообщения.ПолноеИмя,КаталогФайловСообщений+ФайлСообщения.Имя);
				ИмяПолученногоФайла = КаталогФайловСообщений+ФайлСообщения.Имя;;
				Рез = Истина;
			Исключение
				ВызватьИсключение "Не удалось доставить файл сообщения <" + ФайлСообщения.ПолноеИмя + "> в каталог <" + КаталогФайловСообщений + "> "+ОписаниеОшибки();
			КонецПопытки;
		КонецЕсли;			
		
		ФайлСообщения = НЕОПРЕДЕЛЕНО;
		
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Получение", "Ошибка получения файл сообщения <" + ФайлСообщения.ПолноеИмя + "> из почтового ящика " + НастройкаДоставки.АдресЭлектроннойПочты
			+ "| в каталог <" + КаталогФайловСообщений + "> " + ИнформацияОбОшибке().Описание,, СтатусыСобытийПротокола.Ошибка);
		Рез = Ложь;
	КонецПопытки;
	
	// ЗАКЛЮЧИТЕЛЬНЫЕ ДЕЙСТВИЯ
	// Очистим почтовый ящик от ненужных писем с сообщениями
    // доступно только для SMTP/POP3
	Если НастройкаДоставки.СпособОбмена = 2 Тогда
		Попытка	
			Почта.УдалитьСообщения(МассивСообщенийОбмена);
			ЗаписьЛога(ПрефиксЛога + "Получение", "Удалены неактуальные файлы сообщений  из почтового ящика " + НастройкаДоставки.АдресЭлектроннойПочты,
			,СтатусыСобытийПротокола.Информация);
		Исключение КонецПопытки;
	КонецЕсли;
	
	// Отключаемся ...
	Попытка	Почта.Отключиться();
	Исключение КонецПопытки;
	
	// Локальный темп каталог чистим всегда от наших действий
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;

	ЗаписьЛога(ПрефиксЛога + "Получение", "Конец получения файла <" + ИмяПолученногоФайла + ", из почтового ящика " + НастройкаДоставки.АдресЭлектроннойПочты + ">");
 	
	Возврат Рез;
КонецФункции

// Проверяет возможность доставки сообщения согласно текущей настройке
// Возвращает Истина, если доставка возможна
//
// Параметры:
//		стрОшибки - строка ошибок, параметр на выход
//		флСообщать - по ходу проверок выводить сообщения в строку сообщений пользователю
//
Функция ПроверитьДоставку(стрОшибки = "", флСообщать = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(МодульИнициализирован) Тогда
		СтрОшибки = "Ошибка: обработка доставки не была правильно инициализирована";
		ЗаписьЛога(ПрефиксЛога + "Доставка", стрОшибки, , СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	
	Результат = одОбменДанными.ПроверитьДоставкуСообщения(НастройкаДоставки, стрОшибки, флСообщать);
	Возврат Результат;
	
КонецФункции

// Выполняет распаковку файла сообщения
//
// Параметры
//  ФайлСообщения  – Файл – полученный файл сообщения обмена
//
// Возвращаемое значение:
//   Рез   – флаг успешного завершения распаковки
//
Функция РаспаковатьПолученныйФайл(ФайлСообщения)
    Рез = Ложь;
	// Сначала почистим временный каталог от прошлых попыток распаковки
	СтарыеФайлы = НайтиФайлы(КаталогТемпФайлов + ФайлСообщения.ИмяБезРасширения + ".xml");
	Для Каждого СтарыйФайл Из СтарыеФайлы Цикл
		УдалитьФайлы(СтарыйФайл.ПолноеИмя);
	КонецЦикла;
	Попытка // Теперь собственно пытаемся распаковать
		ФайлZIP = Новый ЧтениеZipФайла(ФайлСообщения.ПолноеИмя,НастройкаДоставки.ПарольАрхивации);
		#Если Клиент Тогда
			Состояние("Распаковка ZIP-архива...");
		#КонецЕсли
		ФайлZIP.ИзвлечьВсе(КаталогТемпФайлов);
		ФайлZIP.Закрыть();
		#Если Клиент Тогда
			Состояние("Распаковка ZIP-архива - ОК");
		#КонецЕсли
	Исключение
		СтрСообщения = "Не удалось распаковать файл <" + ФайлСообщения.ПолноеИмя + "> в каталог <" + КаталогТемпФайлов + "> " + ОписаниеОшибки();
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Ошибка);
        Возврат Ложь;
	КонецПопытки;
	
	// Попробуем найти результат
	ФайлСообщения = Новый Файл(КаталогТемпФайлов + ФайлСообщения.ИмяБезРасширения + ".xml");
	Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
		СтрСообщения = "Не удалось распаковать файл обмена  <" + ФайлСообщения.ПолноеИмя + ">";
		ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	Иначе
		Рез = Истина;
	КонецЕсли;
	
	Возврат Рез;

КонецФункции // РаспаковатьФайлСообщения()

// Выполняет архивацию файла обмена
//Параметры:
//	ПолноеИмяФайла - полное имя файла обмена
//	стрОшибки - строка ошибок, накопление ошибок при архивации
//	УдалитьИсточник - Булево - если Истина, то удалить после архивации исходный файл
Функция ЗапаковатьОтправляемыйФайл(ФайлСообщения)
	Рез = Ложь;
	// Сначала почистим временный каталог от прошлых архивов ???
	СтарыеФайлы = НайтиФайлы(КаталогТемпФайлов + ФайлСообщения.ИмяБезРасширения + ".zip");
	Для Каждого СтарыйФайл Из СтарыеФайлы Цикл
		УдалитьФайлы(СтарыйФайл.ПолноеИмя);
	КонецЦикла;

	Попытка
		ИмяФайлаZIP = ФайлСообщения.ИмяБезРасширения + ".zip";
		ПолноеИмяФайлаZIP = КаталогТемпФайлов + ИмяФайлаZIP;
		
		ФайлZIP = Новый ЗаписьZipФайла(ПолноеИмяФайлаZIP,НастройкаДоставки.ПарольАрхивации);
		ФайлZIP.Добавить(ФайлСообщения.ПолноеИмя);
		#Если Клиент Тогда
			Состояние("Упаковка файла сообщения в ZIP-архив...");
		#КонецЕсли
		ФайлZIP.Записать();
		#Если Клиент Тогда
			Состояние("Упаковка файла сообщения в ZIP-архив - Ок");
		#КонецЕсли
		ФайлZIP = Неопределено;

	Исключение
		СтрСообщения = "Не удалось запаковать файл <" + ФайлСообщения.ПолноеИмя + "> в каталог <" + КаталогТемпФайлов + "> " + ОписаниеОшибки();
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения,, СтатусыСобытийПротокола.Ошибка);
        Возврат Ложь;
	КонецПопытки;
	
	// Попробуем найти результат
	ФайлСообщения = Новый Файл(ПолноеИмяФайлаZIP);
	Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
		СтрСообщения = "Не удалось запаковать файл обмена  <" + ФайлСообщения.ПолноеИмя + ">";
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения,, СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	Иначе
		Рез = Истина;
	КонецЕсли;
	
	Возврат Рез;

КонецФункции


////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДОСТАВКИ СООБЩЕНИЙ ОБМЕНА

// Только загрузка сообщения обмена (полная загрузка - из конечного файлообменника в текущий узел)
// Функция возвращает результат Истина когда было успешно доставлено сообщение
// Через параметр возвращается ПолноеИмяФайлаДоставленногоСообщения (полностью готового к чтению!)
// Через другой параметр строка с описанием ошибки
//
Функция Доставка_ЗагрузкаСообщенияОбмена(ИмяФайлаСообщения="",СтрОшибки="") Экспорт
	
	Рез = Ложь; // Ничего полезного еще не успели сделать
	
	Если НЕ ЗначениеЗаполнено(МодульИнициализирован) Тогда
		СтрОшибки = "Ошибка: обработка доставки не была правильно инициализирована";
		ЗаписьЛога(ПрефиксЛога + "Получение", стрОшибки, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Возврат Рез;
	КонецЕсли;
	
	Если НЕ ПроверитьКорректность(стрОшибки) Тогда
		ЗаписьЛога(ПрефиксЛога + "Получение", стрОшибки, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	
	СпособОбмена = НастройкаДоставки.СпособОбмена;
	Если СпособОбмена = 0 Тогда // лок. сеть
		Рез = ПолучитьФайл_ЛокСеть(ИмяФайлаСообщения);
	ИначеЕсли СпособОбмена = 1 ИЛИ СпособОбмена = 2 Тогда // почта
		Рез = ПолучитьФайл_Почта(ИмяФайлаСообщения);
	ИначеЕсли СпособОбмена = 3 Тогда // FTP
		Рез = ПолучитьФайл_FTP(ИмяФайлаСообщения);
	КонецЕсли;
	
	// удалим темп-файлы, оставшиеся от проверок или неудачного получения
	#Если Клиент Тогда
		Состояние("Удаляем временные файлы...");
	#КонецЕсли
	Для ИндСтр=1 По СтрЧислоСтрок(ТемпФайлыДляУдаления) Цикл
        ИмяФайла = СтрПолучитьСтроку(ТемпФайлыДляУдаления,ИндСтр);
		Если НЕ ПустаяСтрока(ИмяФайла) Тогда
			Попытка	УдалитьФайлы(ИмяФайла);
			Исключение КонецПопытки;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Рез;
КонецФункции

// Только выгрузка сообщения обмена (полная выгрузка - из текущего узла в конечный файлообменник)
//
Функция Доставка_ВыгрузкаСообщенияОбмена(ИмяФайлаСообщения="",СтрОшибки="") Экспорт

	Рез = Ложь; // все плохо
	
	Если НЕ ЗначениеЗаполнено(МодульИнициализирован) Тогда
		СтрОшибки = "Ошибка: обработка доставки не была правильно инициализирована";
		ЗаписьЛога(ПрефиксЛога + "Отправка", стрОшибки, , СтатусыСобытийПротокола.Ошибка);
		Возврат Рез;
	КонецЕсли;
	
	Если НЕ ПроверитьКорректность(стрОшибки) Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", стрОшибки, , СтатусыСобытийПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	
	// Отправим выбранным способом
	СпособОбмена = НастройкаДоставки.СпособОбмена;
	ПараметрыОбмена = ПараметрыСеанса.ПараметрыОбменаДанными.Получить();
	ВыполнятьОбменНаКлиенте = ПараметрыОбмена.ВыполнятьОбменНаКлиенте;

	Если СпособОбмена = 0 Тогда // лок. сеть
		
		Если ВыполнятьОбменНаКлиенте Тогда
			Рез = ОтправитьФайл_ЛокСеть(ИмяФайлаСообщения);
		Иначе
			// отправка принудительно на стороне сервера (также как и запись сообщения)
			Рез = пвПривилегированныйМодуль.ОтправитьФайл_ЛокСеть(ИмяФайлаСообщения,УзелОбмена,
				НастройкаДоставки, КаталогФайловСообщений, КаталогТемпФайлов, ТемпФайлыДляУдаления);
		КонецЕсли; 

	ИначеЕсли СпособОбмена = 1 ИЛИ СпособОбмена = 2 Тогда // почта
		
		Если ВыполнятьОбменНаКлиенте Тогда
			Рез = ОтправитьФайл_Почта(ИмяФайлаСообщения);
		Иначе
			// отправка принудительно на стороне сервера (также как и запись сообщения)
			Рез = пвПривилегированныйМодуль.ОтправитьФайл_Почта(ИмяФайлаСообщения,УзелОбмена,
				НастройкаДоставки, КаталогФайловСообщений, КаталогТемпФайлов, ТемпФайлыДляУдаления);
		КонецЕсли; 
			
	ИначеЕсли СпособОбмена = 3 Тогда // FTP
		
		Если ВыполнятьОбменНаКлиенте Тогда
			Рез = ОтправитьФайл_FTP(ИмяФайлаСообщения);
		Иначе
			// отправка принудительно на стороне сервера (также как и запись сообщения)
			Рез = пвПривилегированныйМодуль.ОтправитьФайл_FTP(ИмяФайлаСообщения,УзелОбмена,
				НастройкаДоставки, КаталогФайловСообщений, КаталогТемпФайлов, ТемпФайлыДляУдаления);
		КонецЕсли; 
			
	КонецЕсли;
	
	// удалим темп-файлы, оставшиеся от проверок или неудачной отправки
	#Если Клиент Тогда
		Состояние("Удаляем временные файлы...");
	#КонецЕсли
	Для ИндСтр=1 По СтрЧислоСтрок(ТемпФайлыДляУдаления) Цикл
        ИмяФайла = СтрПолучитьСтроку(ТемпФайлыДляУдаления,ИндСтр);
		Если НЕ ПустаяСтрока(ИмяФайла) Тогда
			Попытка	УдалитьФайлы(ИмяФайла);
			Исключение КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	ПозРазделителя = Найти(ИмяФайлаСообщения,".");
	Если Рез И (ПозРазделителя > 10) Тогда
		Попытка
			НомерОтправленного = Сред(ИмяФайлаСообщения,ПозРазделителя-10,10);
			НомерОтправленного = XMLСтрока(Число(НомерОтправленного));
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Успешно отправлено сообщение № " + НомерОтправленного);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции

// Выполняем инициализацию параметров, необходимых для доставки сообщений
//
Процедура ИнициализацияПараметров(Параметры="") Экспорт
	
	МодульИнициализирован = НЕОПРЕДЕЛЕНО;
	Если НЕ ЗначениеЗаполнено(Параметры) ИЛИ НЕ Параметры.Количество()>0 Тогда 
		Возврат;
	КонецЕсли;
	
	Если  Параметры.Свойство("УзелОбмена",УзелОбмена)
		И Параметры.Свойство("НастройкаДоставки",НастройкаДоставки)
		И Параметры.Свойство("Прием",Прием)
		И Параметры.Свойство("Отправка",Отправка)
		И Параметры.Свойство("КомментироватьХодОбмена",КомментироватьХодОбмена)
		И Параметры.Свойство("ВестиЖурнал",ВестиЖурнал)
		И Параметры.Свойство("ВестиЛоги",ВестиЛоги)
		И Параметры.Свойство("ЛогФайлОбъект",ЛогФайлОбъект)
	Тогда
		// Нам передали все параметры, хорошо
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УзелОбмена) И ЗначениеЗаполнено(НастройкаДоставки) Тогда
		// И они даже заполнены, отлично
	Иначе
		Возврат;
	КонецЕсли;
		
	УдаленныеПодразделения_ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	СтатусыСобытийПротокола = Перечисления.СтатусыСобытийСистемногоПротокола;
	КомментироватьХодОбмена = ?(КомментироватьХодОбмена = неопределено,Истина,КомментироватьХодОбмена);
	
	ЛогФайлОбъект = НЕОПРЕДЕЛЕНО;
	
	КорневойКаталог="";
	КаталогТемпФайлов="";
	КаталогФайловСообщений="";
	КаталогЛогФайлов="";
	КаталогОбработанныхСообщений="";
	КаталогОтправленныхСообщений="";
	ПрефиксЛога = "Обмен.Узел_" + СокрЛП(УзелОбмена.Код) + ".";
	
	Если ТемпФайлыДляУдаления = Неопределено Тогда
	   ТемпФайлыДляУдаления = "";
    КонецЕсли; 
	
	Если ЛогФайлОбъект = неопределено Тогда
	   ПолучитьЛогФайл();
	КонецЕсли; 
    
	
	// Если заполнена константа Каталог регл.операций, берем путь из нее
	// иначе смотрим, что указано
	КорневойКаталог = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(КорневойКаталог) Тогда
	    КорневойКаталог = НастройкаДоставки.КаталогОбменов;
	КонецЕсли; 
	
	Если НЕ ЕстьКаталог(КорневойКаталог) Тогда
		Возврат;
	КонецЕсли;
		
	//КаталогОбменов = КорневойКаталог;
	КаталогФайловСообщений = КорневойКаталог;
	ПараметрыОбмена = одОбменДанными.ПолучитьПараметрыСеансаОбмена();

	Если НастройкаДоставки.ИспользоватьВременныйКаталог Тогда 
		КаталогТемпФайлов = КорневойКаталог + "TEMP\";
	Иначе
		КаталогТемпФайлов = одОбменДанными.ПолучитьКаталогВременныхФайлов(ПараметрыОбмена.ВыполнятьОбменНаКлиенте);
	КонецЕсли;
	Если НастройкаДоставки.СохранятьОтправленные Тогда 
		КаталогОтправленныхСообщений = КорневойКаталог + "SENT\";
	Иначе
		КаталогОтправленныхСообщений = "";
	КонецЕсли;
	Если НастройкаДоставки.СохранятьПолученные Тогда 
		КаталогОбработанныхСообщений = КорневойКаталог + "PROCESSED\";
	Иначе
		КаталогОбработанныхСообщений = "";
	КонецЕсли;
	Если  НЕ ЕстьКаталог(КаталогТемпФайлов) Тогда
		КаталогТемпФайлов = "";
	КонецЕсли;
	Если  НЕ ЕстьКаталог(КаталогОтправленныхСообщений) Тогда
		КаталогОтправленныхСообщений = "";
	КонецЕсли;
	Если  НЕ ЕстьКаталог(КаталогОбработанныхСообщений) Тогда
		КаталогОбработанныхСообщений = "";
	КонецЕсли;
	МодульИнициализирован = Истина;
	
КонецПроцедуры	//	ИнициализацияПараметров()

МодульИнициализирован = НЕОПРЕДЕЛЕНО;
ПрефиксЛога = "Обмен.";
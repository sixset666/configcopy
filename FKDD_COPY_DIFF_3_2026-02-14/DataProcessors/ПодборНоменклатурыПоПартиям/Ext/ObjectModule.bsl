#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// создает и перезаполняет дерево данных
Процедура ЗаполнитьДеревоДанных(ЭтоВыбор) Экспорт
	// чистим
	ДеревоДанных.Строки .Очистить();
	ДеревоДанных.Колонки.Очистить();
	
	// Описатели типов данных
	ОписаниеТиповКоличество	= Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3));
	ОписаниеТиповСумма		= Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2));
	
	// Создаем колонки дерева
	ДеревоДанных.Колонки.Добавить("Объект");
	ДеревоДанных.Колонки.Добавить("Контрагент");
	ДеревоДанных.Колонки.Добавить("ЕдиницаИзмерения");
	ДеревоДанных.Колонки.Добавить("Количество",		ОписаниеТиповКоличество);
	ДеревоДанных.Колонки.Добавить("Оборот",			ОписаниеТиповКоличество);
	ДеревоДанных.Колонки.Добавить("Остаток",		ОписаниеТиповКоличество);
	ДеревоДанных.Колонки.Добавить("Цена",			ОписаниеТиповСумма);
	ДеревоДанных.Колонки.Добавить("Сумма",			ОписаниеТиповСумма);
	ДеревоДанных.Колонки.Добавить("ЦенаРозничная",	ОписаниеТиповСумма);
	ДеревоДанных.Колонки.Добавить("СуммаРозничная",	ОписаниеТиповСумма);
	ДеревоДанных.Колонки.Добавить("ХарактеристикаНоменклатуры");
	
	// Проверка
	Если ПустаяСтрока(Построитель.Текст) Тогда
		Возврат;
	КонецЕсли;
	
	// Создадим запрос для определения цен товара
	ТекстЗапросаЦены = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТоварныйДокументТовары.Номенклатура КАК Номенклатура,
	|	ТоварныйДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТоварныйДокументТовары.Цена КАК Цена
	|ИЗ
	|	Документ.$ИмяДокумента$.Товары КАК ТоварныйДокументТовары
	|ГДЕ
	|	ТоварныйДокументТовары.Ссылка = &Ссылка
	|	И ТоварныйДокументТовары.Номенклатура = &Номенклатура
	|	И ТоварныйДокументТовары.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры";
	ТекстЗапросаЦеныПересортица = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПересортицаТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПересортицаТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПересортицаТоваровТовары.Цена КАК Цена
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
	|ГДЕ
	|	ПересортицаТоваровТовары.Ссылка = &Ссылка
	|	И ПересортицаТоваровТовары.Номенклатура = &Номенклатура
	|	И ПересортицаТоваровТовары.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры";
	ЗапросЦены = Новый Запрос();
	
	// Полезные переменные
	ВалютаУпр		= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	СтруктураКурса	= обКурсДляВалюты(ВалютаУпр,ДатаДокумента);
	КурсУпр			= ?(СтруктураКурса.Кратность = 0, СтруктураКурса.Курс, СтруктураКурса.Курс / СтруктураКурса.Кратность);
	КоэффициентПересчета = обПересчет(1,ВалютаУпр,КурсУпр,ВалютаДокумента,КурсДокумента);
	
	// Установим параметры построителя
	Построитель.Параметры.Вставить("ДатаНачала", НачалоДня(ДатаНачала));
	Если ЭтоВыбор И НЕ ВозвратНам Тогда
		ГраницаРасчета = ?(обЗначениеНеЗаполнено(Документ),КонецДня(РабочаяДата),Новый Граница(Документ.МоментВремени(),ВидГраницы.Исключая));
		Построитель.Параметры.Вставить("ДатаКонца", ГраницаРасчета);
	Иначе
		Построитель.Параметры.Вставить("ДатаКонца", ?(ДатаКонца='00010101',КонецДня(РабочаяДата),КонецДня(ДатаКонца)));
	КонецЕсли;
	
	// Выполняем запрос
	Построитель.Выполнить();
	
	// Идем по выборке и рисуем дерево
	ЕстьСтрокиДокумента = (ТоварыДокументаВозврата.Количество() > 0);
	ДеревоДанных.Строки.Очистить();
	
	СтрокаРодитель = Неопределено;
	СтруктураОтбора = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Партия");
	
	Выборка = Построитель.Результат.Выбрать();
	ЕстьКолонкаЦена = (Построитель.Результат.Колонки.Найти("Цена") <> Неопределено);
	Пока Выборка.Следующий() Цикл
		Если (СтрокаРодитель = Неопределено) ИЛИ (СтрокаРодитель.Объект <> Выборка.Номенклатура) ИЛИ (СтрокаРодитель.ХарактеристикаНоменклатуры <> Выборка.ХарактеристикаНоменклатуры) Тогда
			СтрокаРодитель = ДеревоДанных.Строки.Добавить();
			СтрокаРодитель.Объект						= Выборка.Номенклатура;
			СтрокаРодитель.ХарактеристикаНоменклатуры	= Выборка.ХарактеристикаНоменклатуры;
			СтрокаРодитель.ЕдиницаИзмерения				= Выборка.БазоваяЕдиница;
			СтруктураОтбора.Номенклатура				= СтрокаРодитель.Объект;
			СтруктураОтбора.ХарактеристикаНоменклатуры	= СтрокаРодитель.ХарактеристикаНоменклатуры;
		КонецЕсли;
		НоваяСтрока = СтрокаРодитель.Строки.Добавить();
		НоваяСтрока.Объект = Выборка.Документ;
		Попытка
			НоваяСтрока.Контрагент = Выборка.Контрагент;
		Исключение КонецПопытки;
		НоваяСтрока.Оборот	= Выборка.Оборот;
		НоваяСтрока.Остаток	= Выборка.Остаток;
		НоваяСтрока.Сумма	= Выборка.СуммаУпр * КоэффициентПересчета;
		
		// Определим цену товара
		Если ЕстьКолонкаЦена Тогда
			НоваяСтрока.Цена = Выборка.Цена * КоэффициентПересчета;
		ИначеЕсли НоваяСтрока.Объект.Метаданные().ТабличныеЧасти.Найти("Товары") = Неопределено Тогда
			НоваяСтрока.Цена = ?(НоваяСтрока.Остаток = 0, 0, НоваяСтрока.Сумма / НоваяСтрока.Остаток);
		Иначе
			// Попробуем получить цену из самого документа
			ИмяДокумента = НоваяСтрока.Объект.Метаданные().Имя;
			Если ИмяДокумента = "ПересортицаТоваров" Тогда
				ЗапросЦены.Текст = ТекстЗапросаЦеныПересортица;
			Иначе
				ЗапросЦены.Текст = СтрЗаменить(ТекстЗапросаЦены, "$ИмяДокумента$", ИмяДокумента);
			КонецЕсли;
			ЗапросЦены.УстановитьПараметр("Ссылка", НоваяСтрока.Объект);
			ЗапросЦены.УстановитьПараметр("Номенклатура", СтрокаРодитель.Объект);
			ЗапросЦены.УстановитьПараметр("ХарактеристикаНоменклатуры", СтрокаРодитель.ХарактеристикаНоменклатуры);
			
			РезультатЦены = ЗапросЦены.Выполнить();
			ВыборкаЦены	  = РезультатЦены.Выбрать();
			Если РезультатЦены.Пустой() ИЛИ(НЕ ВыборкаЦены.Следующий())Тогда
				НоваяСтрока.Цена = ?(НоваяСтрока.Остаток = 0, 0, НоваяСтрока.Сумма / НоваяСтрока.Остаток);
			Иначе
				НоваяСтрока.Цена = обПересчет(ВыборкаЦены.Цена, НоваяСтрока.Объект.ВалютаДокумента, НоваяСтрока.Объект.КурсДокумента, ВалютаДокумента, КурсДокумента);
			КонецЕсли;
		КонецЕсли;
		
		// Поищем в документе количество возвращенного товара
		Если ЕстьСтрокиДокумента Тогда
			СтруктураОтбора.Партия = НоваяСтрока.Объект;
			НайденныеСтроки = ТоварыДокументаВозврата.НайтиСтроки(СтруктураОтбора);
			Если НайденныеСтроки.Количество() > 0 Тогда
				НоваяСтрока.Количество = НайденныеСтроки[0].Количество;
			КонецЕсли;
		КонецЕсли;

		// Просуммируем данные строки родителя
		СтрокаРодитель.Оборот		= СтрокаРодитель.Оборот + НоваяСтрока.Оборот;
		СтрокаРодитель.Остаток		= СтрокаРодитель.Остаток + НоваяСтрока.Остаток;
		СтрокаРодитель.Сумма		= СтрокаРодитель.Сумма + НоваяСтрока.Сумма;
		СтрокаРодитель.Количество	= СтрокаРодитель.Количество + НоваяСтрока.Количество;
	КонецЦикла;
	
КонецПроцедуры
#КонецЕсли

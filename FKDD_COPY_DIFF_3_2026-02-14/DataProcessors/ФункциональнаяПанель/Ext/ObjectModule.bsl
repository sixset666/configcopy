
#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем СписокСтрокДляУдаления; // Cписок строк для удаления (управление закладками АРМ)
Перем ТаблицаОткрытыхАРМ Экспорт; // Таблица открытых АРМ-ов

//Переменные для хранения текущей истории поиска для соответствующих АРМ
//история хранится, пока открыта форма функциональной панели
Перем ИсторияПоискаКлиентовАвтомобилей Экспорт; // История поиска в строке поиска АРМ "Клиенты и автомобили"
Перем ИсторияПоискаРаботЗапчастей Экспорт; 		// История поиска в строке поиска АРМ "Калькуляция"
Перем ИсторияПоискаЗаявок Экспорт; 				// История поиска в строке поиска АРМ "Запись на ремонт"
Перем ИсторияПоискаАРМАвтосалон Экспорт;		// История поиска в строке поиска АРМ "Автосалон" 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Действия при открытии АРМ при контекстном переходе либо запуске из панели
Процедура ОткрытьАРМ(ИмяАРМПриемника, ФункциональнаяПанель, Основание = Неопределено, ФормаИсточник = Неопределено) Экспорт

	//проверка наличия Таблицы открытых АРМ
	Если НЕ ТипЗнч(ТаблицаОткрытыхАРМ) = Тип("ТаблицаЗначений") Тогда
		ТаблицаОткрытыхАРМ = Новый ТаблицаЗначений;
		ТаблицаОткрытыхАРМ.Колонки.Добавить("Имя");
		ТаблицаОткрытыхАРМ.Колонки.Добавить("Форма");
	КонецЕсли;
	
	//удаление "зависших" АРМ	
	Для Каждого ОткрытыйАРМ Из ТаблицаОткрытыхАРМ Цикл
		Если НЕ ОткрытыйАРМ.Форма.Открыта() Тогда
			ОткрытыйАРМ.Форма = Неопределено;	
			ТаблицаОткрытыхАРМ.Удалить(ОткрытыйАРМ);
		КонецЕсли;
	КонецЦикла;
	
	//если АРМ уже открыт, найдем его
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Имя",  ИмяАРМПриемника);
	НайденныеСтроки = ТаблицаОткрытыхАРМ.НайтиСтроки(СтруктураОтбора);
	ФормаСуществует = ?(НайденныеСтроки.Количество() = 0, Ложь, Истина);
	
	//получим форму
	Если ФормаСуществует Тогда
		ФормаАРМ = НайденныеСтроки[0].Форма;
	Иначе
		ФормаАРМ = Обработки[ИмяАРМПриемника].ПолучитьФорму(,ФункциональнаяПанель);
	КонецЕсли;	
	
	//заполним основание
	Попытка
		ЗаполненоОснование = Ложь;
		
		//для АРМ, использующих Заявку на ремонт, необходимо передавать объект для исключения блокировок
		Если ФормаИсточник <> Неопределено Тогда
			СтрокаТаблицыАРМ  = ТаблицаОткрытыхАРМ.Найти(ФормаИсточник, "Форма");
			Если СтрокаТаблицыАРМ <> Неопределено Тогда
				ИмяАРМИсточника = СтрокаТаблицыАРМ.Имя;	
			КонецЕсли;
			Если ИмяАРМИсточника = "АРМКлиентыИАвтомобили" ИЛИ ИмяАРМИсточника = "АРМКалькуляция" ИЛИ ИмяАРМИсточника = "АРМЗаписьНаРемонт" Тогда
				Если ИмяАРМПриемника = "АРМКлиентыИАвтомобили" ИЛИ ИмяАРМПриемника = "АРМКалькуляция" ИЛИ ИмяАРМПриемника = "АРМЗаписьНаРемонт" Тогда
					ФормаАРМ.Основание = ФормаИсточник.ДокументОбъект;
					ЗаполненоОснование = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
		//в остальных случаях передается ссылка на объект
		Если НЕ ЗаполненоОснование Тогда
			ФормаАРМ.Основание = Основание;
		КонецЕсли 
		
	Исключение
	КонецПопытки;
	
	//откроем или активизируем форму
	Если ФормаСуществует Тогда
		ФормаАРМ.Активизировать();
		//оповестим об открытии, чтобы сработали обработчики заполнения
		Оповестить("Активизировать" + ИмяАРМПриемника);
	Иначе
		ФормаАРМ.Открыть();
		//добавим запись в таблицу
		Если ИмяАРМПриемника <> "АРМРесепшен" Тогда
			НоваяСтрокаТаблицыАРМ = ТаблицаОткрытыхАРМ.Добавить();
			НоваяСтрокаТаблицыАРМ.Имя   = ИмяАРМПриемника;
			НоваяСтрокаТаблицыАРМ.Форма = ФормаАРМ;
		КонецЕсли;
	КонецЕсли;
		
	Попытка
		ДеревоНастройки = ФормаАРМ.ПолучитьДеревоФормы();
		Если ТипЗнч(ДеревоНастройки) = Тип("ДеревоЗначений") Тогда
			НастроитьПанелиФормы(ФормаАРМ, ДеревоНастройки.Строки);
		КонецЕсли;
	Исключение
	КонецПопытки; 
	
КонецПроцедуры

// Действия при закрытии АРМ - удаление АРМ из Таблицы открытых
Процедура ПриЗакрытииАРМ(ФормаАРМ) Экспорт
	
	СтрокаФормы = ТаблицаОткрытыхАРМ.Найти(ФормаАРМ, "Форма");
	Если СтрокаФормы <> Неопределено Тогда
		СтрокаФормы.Форма = Неопределено;	
		ТаблицаОткрытыхАРМ.Удалить(СтрокаФормы);
	КонецЕсли;
	
КонецПроцедуры

// возвращает полную таблицу кнопок и значения предопределённых настроек
Функция ПолучитьТаблицуКнопок() Экспорт
	// создаем таблицу кнопок
	ТаблицаКнопок = Новый ТаблицаЗначений;
	ТаблицаКнопок.Колонки.Добавить("Имя");
	ТаблицаКнопок.Колонки.Добавить("Представление");
	ТаблицаКнопок.Колонки.Добавить("Картинка");
	ТаблицаКнопок.Колонки.Добавить("Шрифт");
	ТаблицаКнопок.Колонки.Добавить("Цвет");

	// получим макет, описывающий функциональную панель
	МакетПараметров = ПолучитьМакет("Кнопки");

	// область, описывающая предопределённые настройки
	ОбластьНастроек = МакетПараметров.ПолучитьОбласть("Настройки");
	МассивНастроек = Новый Массив;
	КоличествоНастроек = ОбластьНастроек.ШиринаТаблицы;
	Для Сч=4 По КоличествоНастроек Цикл
		ОбластьИмениНастройки = ОбластьНастроек.Область(1,Сч,1,Сч);
		// добавляем в таблицу колонок имя предопределённой настройки
		ТаблицаКнопок.Колонки.Вставить(Сч, ОбластьИмениНастройки.Текст);
	КонецЦикла;

	// область, описывающая кнопки
	ОбластьКнопок = МакетПараметров.ПолучитьОбласть("Кнопки");
	КоличествоКнопок = ОбластьКнопок.ВысотаТаблицы;

	// рисуем кнопки
	ПредыдущаяКнопка = Неопределено;
	Для Сч=1 По КоличествоКнопок Цикл
		ОбластьИмя = ОбластьКнопок.Область(Сч,1,Сч,1);
		ОбластьПредставление = ОбластьКнопок.Область(Сч,2,Сч,2);
		ОбластьКартинка = ОбластьКнопок.Область(Сч,3,Сч,3);

		// получим картинку
		Картинка = Неопределено;
		Если ЗначениеЗаполнено(ОбластьКартинка.Текст) Тогда
			Попытка
				Картинка = БиблиотекаКартинок[ОбластьКартинка.Текст];
			Исключение
			КонецПопытки;
		Иначе
			Попытка
				Картинка = ОбластьКнопок.Рисунки["Картинка"+СокрЛП(ОбластьИмя.Текст)].Картинка;
			Исключение
			КонецПопытки;
		КонецЕсли;
		
		// добавляем строку в таблицу
		НоваяСтрока = ТаблицаКнопок.Добавить();
		НоваяСтрока.Имя					 = ОбластьИмя.Текст;
		НоваяСтрока.Представление		 = ОбластьПредставление.Текст;
		НоваяСтрока.Шрифт				 = ОбластьПредставление.Шрифт;
		НоваяСтрока.Цвет				 = ОбластьПредставление.ЦветТекста;
		НоваяСтрока.Картинка			 = Картинка;

		// добавляем значения всех предопределённых настроек
 		Для Счётчик=4 По КоличествоНастроек Цикл
			ОбластьНастройки = ОбластьКнопок.Область(Сч,Счётчик,Сч,Счётчик);
			ИмяКолонки = ТаблицаКнопок.Колонки.Получить(Счётчик).Имя;
			НоваяСтрока[ИмяКолонки] = ОбластьНастройки.Текст;
		КонецЦикла;

	КонецЦикла;

	Возврат ТаблицаКнопок;
КонецФункции

// возвращает полную таблицу сводной информации
Функция ПолучитьТаблицуИнформации() Экспорт
	// создаем таблицу кнопок
	ТаблицаИнформации = Новый ТаблицаЗначений;
	ТаблицаИнформации.Колонки.Добавить("Имя");
	ТаблицаИнформации.Колонки.Добавить("Представление");
	ТаблицаИнформации.Колонки.Добавить("Отчет"); // имя отчета расшифровки

	// получим макет, описывающий функциональную панель
	МакетПараметров = ПолучитьМакет("Информация");

	// область, описывающая предопределённые настройки
	ОбластьНастроек = МакетПараметров.ПолучитьОбласть("Настройки");
	МассивНастроек = Новый Массив;
	КоличествоНастроек = ОбластьНастроек.ШиринаТаблицы;
	//вставим временную колонку для получения необходимого количества индексов
	ТаблицаИнформации.Колонки.Вставить(3, "ПустаяКолонка");
	Для Сч=4 По КоличествоНастроек Цикл
		ОбластьИмениНастройки = ОбластьНастроек.Область(1,Сч,1,Сч);
		// добавляем в таблицу колонок имя предопределённой настройки
		ТаблицаИнформации.Колонки.Вставить(Сч, ОбластьИмениНастройки.Текст);
	КонецЦикла;

	// область, описывающая панель информации
	ОбластьИнформации = МакетПараметров.ПолучитьОбласть("Информация");

	// получаем информацию
	Для Сч=1 По ОбластьИнформации.ВысотаТаблицы Цикл
		ОбластьИмя = ОбластьИнформации.Область(Сч,1,Сч,1);
		ОбластьПредставление = ОбластьИнформации.Область(Сч,2,Сч,2);
		ОбластьОтчет = ОбластьИнформации.Область(Сч,3,Сч,3);

		// добавляем строку в таблицу
		НоваяСтрока = ТаблицаИнформации.Добавить();
		НоваяСтрока.Имя				= ОбластьИмя.Текст;
		НоваяСтрока.Представление	= ОбластьПредставление.Текст;
		НоваяСтрока.Отчет			= ОбластьОтчет.Текст;

		// добавляем значения всех предопределённых настроек
		Для Счётчик=4 По КоличествоНастроек Цикл
			ОбластьНастройки = ОбластьИнформации.Область(Сч,Счётчик,Сч,Счётчик);
			ИмяКолонки = ТаблицаИнформации.Колонки.Получить(Счётчик).Имя;
			НоваяСтрока[ИмяКолонки] = ОбластьНастройки.Текст;
		КонецЦикла;

	КонецЦикла;
	// удаляем временную колонку
	ТаблицаИнформации.Колонки.Удалить(3);

	Возврат ТаблицаИнформации;
КонецФункции

// процедура вызывается при попытке одного из параметров сводной информации, если стандартного отчета не было найдено
// ИмяПараметра - Строка. Имя расшифровываемого параметра
// ИмяОтчета - Строка. Имя стандартного отчета
Процедура РасшифровкаСводнойИнформации(ИмяПараметра,ИмяОтчета) Экспорт
	
КонецПроцедуры

// процедура обновляет сводную информацию
Процедура ОбновитьИнформацию(ИнформационноеПоле) Экспорт
	Если НЕ ЗначениеЗаполнено(ИнформационноеПоле) Тогда
		Возврат;    
	КонецЕсли;

	// переберём таблицу параметров
	Если ИнформационноеПоле.Количество()>0 Тогда
		Для Каждого СтрокаИнформации ИЗ ИнформационноеПоле Цикл
			СтрокаИнформации.Всех = ПолучитьЗначениеПараметра(СтрокаИнформации.Имя);
			СтрокаИнформации.Моих = ПолучитьЗначениеПараметра(СтрокаИнформации.Имя, ПараметрыСеанса.Пользователь);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// функция получает значение параметра сводной информации по его имени
// ИмяПараметра - Строка. Имя параметра
// Возвращает: Строка. Значение параметра
Функция ПолучитьЗначениеПараметра(ИмяПараметра,ТекПользователь=Неопределено) Экспорт
	Значение = "";
	
	//количество автомобилей, записанных на сегодня (по заявкам на ремонт)
	Если ИмяПараметра="ЗаписаноНаСегодняАМ" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ГрафикРаботыРесурсов.Объект.Автомобиль) КАК Записано
		               |ИЗ
		               |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
		               |ГДЕ
		               |	ГрафикРаботыРесурсов.Объект.ПометкаУдаления = ЛОЖЬ
		               |	И ГрафикРаботыРесурсов.Объект.Ссылка ССЫЛКА Документ.ЗаявкаНаРемонт
		               |	И ГрафикРаботыРесурсов.Дата = &Дата";
		Запрос.УстановитьПараметр("Дата",НачалоДня(ТекущаяДата()));
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = Запрос.Текст + " И ГрафикРаботыРесурсов.Объект.Автор = &Автор";
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		КонецЕсли;
		
		Значение = 0;
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Записано;
		КонецЕсли;
		
	//количество нормочасов, записанных на сегодня (по заявкам на ремонт)
	ИначеЕсли ИмяПараметра="ЗаписаноНаСегодняНЧ" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	СУММА(ЧАС(ГрафикРаботыРесурсов.Продолжительность) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.Продолжительность) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.Продолжительность)) / 3600 КАК Записано
		               |ИЗ
		               |	РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
		               |ГДЕ
		               |	ГрафикРаботыРесурсов.Объект.ПометкаУдаления = ЛОЖЬ
		               |	И ГрафикРаботыРесурсов.Объект.Ссылка ССЫЛКА Документ.ЗаявкаНаРемонт
		               |	И ГрафикРаботыРесурсов.Дата = &Дата";
		Запрос.УстановитьПараметр("Дата",НачалоДня(ТекущаяДата()));
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = Запрос.Текст + " И ГрафикРаботыРесурсов.Объект.Автор = &Автор";
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		КонецЕсли;
		
		Значение = 0;
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Записано;
		КонецЕсли;
		
	//количество автомобилей, принятых сегодня по предварительной записи
	ИначеЕсли ИмяПараметра="ПринятоПоЗаписи" Тогда
		
		НачалоДня = НачалоДня(ТекущаяДата());
		КонецДня =  КонецДня(ТекущаяДата());
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказНаряд.Автомобиль) КАК Принято
		               |ИЗ
		               |	Документ.ЗаказНаряд КАК ЗаказНаряд
		               |ГДЕ
		               |	ЗаказНаряд.ПометкаУдаления = ЛОЖЬ
					   |	И ЗаказНаряд.ДокументОснование ССЫЛКА Документ.ЗаявкаНаРемонт
		               |	И ЗаказНаряд.Дата МЕЖДУ &НачалоДня И &КонецДня
		               |	И (НЕ ЗаказНаряд.ДокументОснование = ЗНАЧЕНИЕ(Документ.ЗаявкаНаРемонт.ПустаяСсылка))
					   |	И (НЕ ЗаказНаряд.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Заявка))";
		Запрос.УстановитьПараметр("НачалоДня",НачалоДня);
		Запрос.УстановитьПараметр("КонецДня",КонецДня);
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = Запрос.Текст + " И ЗаказНаряд.Автор = &Автор";
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		КонецЕсли;
		
		Значение = 0;
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Принято;
		КонецЕсли;
		
	//количество автомобилей, принятых сегодня без предварительной записи
	ИначеЕсли ИмяПараметра="ПринятоБезЗаписи" Тогда
		
		НачалоДня = НачалоДня(ТекущаяДата());
		КонецДня =  КонецДня(ТекущаяДата());
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказНаряд.Автомобиль) КАК Принято
		               |ИЗ
		               |	Документ.ЗаказНаряд КАК ЗаказНаряд
		               |ГДЕ
		               |	ЗаказНаряд.ПометкаУдаления = ЛОЖЬ
		               |	И ЗаказНаряд.Дата МЕЖДУ &НачалоДня И &КонецДня
		               |	И (НЕ ЗаказНаряд.ДокументОснование ССЫЛКА Документ.ЗаявкаНаРемонт)
		               |	И ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Заявка)";
		Запрос.УстановитьПараметр("НачалоДня",НачалоДня);	
		Запрос.УстановитьПараметр("КонецДня",КонецДня);			   
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = Запрос.Текст + " И ЗаказНаряд.Автор = &Автор";
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		КонецЕсли;
		
		Значение = 0;
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Принято;
		КонецЕсли;
		
	ИначеЕсли ИмяПараметра="НевыполненныеЗаказы" Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(ЗаказыПокупателейОстатки.Заказ) КАК КоличествоЗаказов
		               |ИЗ
		               |	РегистрНакопления.ЗаказыПокупателей.Остатки КАК ЗаказыПокупателейОстатки
		               |ГДЕ
		               |	ЗаказыПокупателейОстатки.ЗаказаноОстаток > 0
		               |";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.КоличествоЗаказов;
		Иначе
			Значение = 0;
		КонецЕсли;
		
	//количество автомобилей в открытых заказ-нарядах
	ИначеЕсли ИмяПараметра="ВРаботе" Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказНаряд.Автомобиль) КАК Количество
		               |ИЗ
		               |	Документ.ЗаказНаряд КАК ЗаказНаряд
		               |ГДЕ
		               |	ЗаказНаряд.ДатаНачала < &ТекущаяДата
		               |	И (ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Заявка) И
					   |	   ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен) И
					   |	   ЗаказНаряд.Состояние <> ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт))
		               |	И ЗаказНаряд.ПометкаУдаления = Ложь";
		Запрос.УстановитьПараметр("ТекущаяДата",КонецДня(ТекущаяДата()));	
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = Запрос.Текст + " И ЗаказНаряд.Автор = &Автор";
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Количество;
		Иначе
			Значение = 0;
		КонецЕсли;
		
	//количество автомобилей в выполненных заказ-нарядах
	ИначеЕсли ИмяПараметра="РаботыВыполнены" Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказНаряд.Автомобиль) КАК Количество
		               |ИЗ
		               |	Документ.ЗаказНаряд КАК ЗаказНаряд
		               |ГДЕ
		               |	ЗаказНаряд.ДатаНачала < &ТекущаяДата
		               |	И ЗаказНаряд.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Выполнен) 
					   |	И ЗаказНаряд.ПометкаУдаления = Ложь";
		Запрос.УстановитьПараметр("ТекущаяДата",КонецДня(ТекущаяДата()));	
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = Запрос.Текст + " И ЗаказНаряд.Автор = &Автор";
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Количество;
		Иначе
			Значение = 0;
		КонецЕсли;
		
	//количество новых клиентов
	ИначеЕсли ИмяПараметра="НовыхКлиентов" Тогда

		НачалоДня = НачалоДня(ТекущаяДата());
		
		Запрос = Новый Запрос;
		
		ТекстОтбора = "";
		Если ТекПользователь <> Неопределено Тогда
			ТекстОтбора = "
			|		И Контрагенты.Автор = &Автор ";
			Запрос.УстановитьПараметр("Автор", ТекПользователь);
		КонецЕсли;
		
		Запрос.Текст = "
		               |ВЫБРАТЬ 
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаЗаказчики.Заказчик) КАК Количество
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		ЗаказНаряд.Заказчик
		               |	ИЗ
		               |		Документ.ЗаказНаряд КАК ЗаказНаряд
		               |	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		               |		Справочник.Контрагенты КАК Контрагенты
		               |	ПО
		               |		ЗаказНаряд.Заказчик = Контрагенты.Ссылка
		               |	ГДЕ
		               |		Контрагенты.ДатаРегистрации >= &НачалоДня"+ТекстОтбора+"
		               |	
		               |	ОБЪЕДИНИТЬ ВСЕ
		               |	
		               |	ВЫБРАТЬ
		               |		ЗаявкаНаРемонт.Заказчик
		               |	ИЗ
		               |		Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		               |	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		               |		Справочник.Контрагенты КАК Контрагенты
		               |	ПО
		               |		ЗаявкаНаРемонт.Заказчик ССЫЛКА Справочник.Контрагенты И 
		               |		ЗаявкаНаРемонт.Заказчик = Контрагенты.Ссылка
		               |	ГДЕ
		               |		Контрагенты.ДатаРегистрации >= &НачалоДня"+ТекстОтбора+") КАК ТаблицаЗаказчики
		               |";
		Запрос.УстановитьПараметр("НачалоДня",НачалоДня);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Количество;
		Иначе
			Значение = 0;
		КонецЕсли;

	//количество новых автомобилей
	ИначеЕсли ИмяПараметра="НовыхАвто" Тогда

		НачалоДня = НачалоДня(ТекущаяДата());
		
		Запрос = Новый Запрос;
		
		ТекстОтбора = "";
		Если ТекПользователь <> Неопределено Тогда
			ТекстОтбора = "
			|		И Автомобили.Автор = &Автор ";
			Запрос.УстановитьПараметр("Автор", ТекПользователь);
		КонецЕсли;
		
		Запрос.Текст = "
		               |ВЫБРАТЬ 
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаАвтомобили.Автомобиль) КАК Количество
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		ЗаказНаряд.Автомобиль
		               |	ИЗ
		               |		Документ.ЗаказНаряд КАК ЗаказНаряд
		               |	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		               |		Справочник.Автомобили КАК Автомобили
		               |	ПО
		               |		ЗаказНаряд.Автомобиль = Автомобили.Ссылка
		               |	ГДЕ
		               |		Автомобили.ДатаРегистрации >= &НачалоДня"+ТекстОтбора+"
		               |	
		               |	ОБЪЕДИНИТЬ ВСЕ
		               |	
		               |	ВЫБРАТЬ
		               |		ЗаявкаНаРемонт.Автомобиль
		               |	ИЗ
		               |		Документ.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт
		               |	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		               |		Справочник.Автомобили КАК Автомобили
		               |	ПО
		               |		ЗаявкаНаРемонт.Автомобиль ССЫЛКА Справочник.Автомобили И 
		               |		ЗаявкаНаРемонт.Автомобиль = Автомобили.Ссылка
		               |	ГДЕ
		               |		Автомобили.ДатаРегистрации >= &НачалоДня"+ТекстОтбора+") КАК ТаблицаАвтомобили
		               |";
		Запрос.УстановитьПараметр("НачалоДня",НачалоДня);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = Выборка.Количество;
		Иначе
			Значение = 0;
		КонецЕсли;
		
	//среднее количество нормочасов на заказ-наряд
	ИначеЕсли ИмяПараметра="НормоЧасовНаЗН" Тогда
		
		НачалоДня = НачалоДня(ТекущаяДата());
		КонецДня =  КонецДня(ТекущаяДата());

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА КОЛИЧЕСТВО(ДанныеПоДокументам.Ссылка) > 0
		               |			ТОГДА СУММА(ДанныеПоДокументам.Нормочасов) / КОЛИЧЕСТВО(ДанныеПоДокументам.Ссылка)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ КАК Среднее
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		ЗаказНаряд.Ссылка КАК Ссылка,
		               |		СУММА(ЗаказНарядРаботы.Количество * ЗаказНарядРаботы.Коэффициент) КАК Нормочасов
		               |	ИЗ
		               |		Документ.ЗаказНаряд КАК ЗаказНаряд
		               |			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
		               |			ПО (ЗаказНарядРаботы.Ссылка = ЗаказНаряд.Ссылка)
		               |	ГДЕ
		               |		ЗаказНаряд.Ссылка.ДатаЗакрытия МЕЖДУ &НачалоДня И &КонецДня
		               |		И ЗаказНаряд.Ссылка.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)
		               |		И &Автор
		               |	
		               |	СГРУППИРОВАТЬ ПО
		               |		ЗаказНаряд.Ссылка) КАК ДанныеПоДокументам";
		Запрос.УстановитьПараметр("НачалоДня",НачалоДня);	
		Запрос.УстановитьПараметр("КонецДня",КонецДня);			   
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"И &Автор"," И ЗаказНаряд.Ссылка.Автор = &Автор");
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"И &Автор","");
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = ?(ЗначениеЗаполнено(Выборка.Среднее),Формат(Выборка.Среднее,"ЧДЦ=1"),0);
		Иначе
			Значение = 0;
		КонецЕсли;
		
	//средняя стоимость запчастей на нормочас
	ИначеЕсли ИмяПараметра="ЗапчастейНаЗН" Тогда
		
		НачалоДня = НачалоДня(ТекущаяДата());
		КонецДня =  КонецДня(ТекущаяДата());

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА СУММА(ДанныеПоДокументам.Нормочасов) > 0
		               |			ТОГДА СУММА(ДанныеПоДокументам.СуммаНоменклатурыДокумента) / СУММА(ДанныеПоДокументам.Нормочасов)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ КАК Среднее
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		СУММА(ЗаказНарядРаботы.Количество * ЗаказНарядРаботы.Коэффициент) КАК Нормочасов,
		               |		ЗаказНаряд.СуммаНоменклатурыДокумента КАК СуммаНоменклатурыДокумента
		               |	ИЗ
		               |		Документ.ЗаказНаряд КАК ЗаказНаряд
		               |			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
		               |			ПО (ЗаказНарядРаботы.Ссылка = ЗаказНаряд.Ссылка)
		               |	ГДЕ
		               |		ЗаказНаряд.Ссылка.ДатаЗакрытия МЕЖДУ &НачалоДня И &КонецДня
		               |		И ЗаказНаряд.Ссылка.Состояние = ЗНАЧЕНИЕ(Справочник.ВидыСостоянийЗаказНарядов.Закрыт)
		               |		И &Автор
		               |	
		               |	СГРУППИРОВАТЬ ПО
		               |		ЗаказНаряд.СуммаНоменклатурыДокумента) КАК ДанныеПоДокументам";
		Запрос.УстановитьПараметр("НачалоДня",НачалоДня);	
		Запрос.УстановитьПараметр("КонецДня",КонецДня);			   
		
		Если ТекПользователь <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"И &Автор"," И ЗаказНаряд.Ссылка.Автор = &Автор");
			Запрос.УстановитьПараметр("Автор",ТекПользователь);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"И &Автор","");
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Значение = ?(ЗначениеЗаполнено(Выборка.Среднее),Формат(Выборка.Среднее,"ЧДЦ=1"),0);
		Иначе
			Значение = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	
	Возврат Значение;
КонецФункции

// процедура выполняет действие, которое определено для нажатой кнопки
// ЭтаФорма - Форма. Функциональная панель.
// ИмяКнопки - Строка. Имя нажатой кнопки функциональной панели
// СтруктураПараметров - Структура. Фиксированного формата. Через нее передаются параметры в
// открываемый АРМ при вызове из другого АРМ
// ИмяТочкиВызова - Строка. АРМ вызывающий открытие другого АРМ.
Процедура ОбработчикНажатияКнопки(ЭтаФорма,ИмяКнопки,СтруктураПараметров=Неопределено,ИмяТочкиВызова="ФункциональнаяПанель") Экспорт
	
	ТипКнопки = ВернутьТипКнопки(ИмяКнопки);	

	Если ТипКнопки = "АРМ" Тогда
		
		ОткрытьАРМ(ИмяКнопки, ЭтаФорма);
		
	ИначеЕсли ТипКнопки = "Обработка" Тогда
	ИначеЕсли ТипКнопки = "Справочник" Тогда
	ИначеЕсли ТипКнопки = "Документ" Тогда
		Форма = Неопределено;
		СтрокаКода = "Форма = Документы." + ИмяКнопки + ".ПолучитьФормуСписка()"; 
		Выполнить(СтрокаКода);
		Если НЕ Форма.Открыта() Тогда
			Форма.Открыть();
		КонецЕсли;
	ИначеЕсли ТипКнопки = "Отчет" Тогда
	ИначеЕсли ТипКнопки = "Группа" Тогда
	ИначеЕсли ТипКнопки = "Журнал" Тогда
 	КонецЕсли;
	
КонецПроцедуры

// функция определяет тип метаданных по имени
Функция ВернутьТипКнопки(ИмяКнопки = Неопределено)
	НайденныйОбъект = Неопределено;
	ТипМетаданных = Неопределено;
	
	Если ИмяКнопки = "" Тогда
		ТипМетаданных = Неопределено
	Иначе                                                                      
		// АРМ?
		Если Лев(ИмяКнопки,3) = "АРМ" Тогда
			Попытка
				НайденныйОбъект = Обработки[ИмяКнопки];
				Если НайденныйОбъект <> Неопределено Тогда
					ТипМетаданных = "АРМ";
				КонецЕсли;
			Исключение
				ТипМетаданных = Неопределено;
			КонецПопытки;
		КонецЕсли;
		
		// обработка?
		Если ТипМетаданных = Неопределено Тогда
			Попытка
				НайденныйОбъект = Обработки[ИмяКнопки];
				Если НайденныйОбъект <> Неопределено Тогда
					ТипМетаданных = "Обработка";
				КонецЕсли;
			Исключение
				ТипМетаданных = Неопределено;
			КонецПопытки;
		КонецЕсли;
		
		// справочник?
		Если ТипМетаданных = Неопределено Тогда
			Попытка
				НайденныйОбъект = Справочники[ИмяКнопки];
				Если НайденныйОбъект <> Неопределено Тогда
					ТипМетаданных = "Справочник";
				КонецЕсли;
			Исключение
				ТипМетаданных = Неопределено;
			КонецПопытки;
		КонецЕсли;
		
		// документ?
		Если ТипМетаданных = Неопределено Тогда
			Попытка
				НайденныйОбъект = Документы[ИмяКнопки];
				Если НайденныйОбъект <> Неопределено Тогда
					ТипМетаданных = "Документ";
				КонецЕсли;
			Исключение
				ТипМетаданных = Неопределено;
			КонецПопытки;
		КонецЕсли;
		
		// отчет?
		Если ТипМетаданных = Неопределено Тогда
			Попытка
				НайденныйОбъект = Отчеты[ИмяКнопки];
				Если НайденныйОбъект <> Неопределено Тогда
					ТипМетаданных = "Отчет";
				КонецЕсли;
			Исключение
				ТипМетаданных = Неопределено;
			КонецПопытки;
		КонецЕсли;
		
		// журнал документов?
		Если ТипМетаданных = Неопределено Тогда
			Попытка
				НайденныйОбъект = ЖурналыДокументов[ИмяКнопки];
				Если НайденныйОбъект <> Неопределено Тогда
					ТипМетаданных = "Журнал";
				КонецЕсли;
			Исключение
				ТипМетаданных = Неопределено;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	Если ТипМетаданных = Неопределено Тогда
		ТипМетаданных = "";
	КонецЕсли;
	
	Возврат ТипМетаданных;
КонецФункции

// Настройка панелей формы по дереву значений
Процедура НастроитьПанелиФормы(ФормаАРМ,ДеревоФормы)
	
	
	// Создадим таблицу в которую будем собирать свойства всех панелей страницы-родителя
	ПараметрыПанелей = Новый ТаблицаЗначений();
	ПараметрыПанелей.Колонки.Добавить("ИмяПанели",			Новый ОписаниеТипов("Строка"));
	ПараметрыПанелей.Колонки.Добавить("ВидимыхСтраниц",		Новый ОписаниеТипов("Число"));
	ПараметрыПанелей.Колонки.Добавить("ТекущаяСтраница",	Новый ОписаниеТипов("Строка"));
	ПараметрыПанелей.Колонки.Добавить("ГлавнаяПанель",		Новый ОписаниеТипов("Булево"));
	ПараметрыПанелей.Колонки.Добавить("ОтображениеЗакладок",Новый ОписаниеТипов("ОтображениеЗакладок"));
	
	Для Каждого ТекСтраница Из ДеревоФормы Цикл
		// Если на странице есть еще одна панель, то обработаем сначала ее страницы
		Если (ТекСтраница.Видимость) И (ТекСтраница.Строки.Количество() > 0) Тогда
			НастроитьПанелиФормы(ФормаАРМ,ТекСтраница.Строки);
		КонецЕсли;
		
		// Текущая обрабатываемая панель
		ТекПанель = ФормаАРМ.ЭлементыФормы[ТекСтраница.ИмяПанели];
		
		// Найдем или добавим новую строчку в таблицу параметров панелей
		ПараметрыПанели = ПараметрыПанелей.Найти(ТекСтраница.ИмяПанели,"ИмяПанели");
		Если ПараметрыПанели = Неопределено Тогда
			ПараметрыПанели = ПараметрыПанелей.Добавить();
			ПараметрыПанели.ИмяПанели			= ТекСтраница.ИмяПанели;
			ПараметрыПанели.ВидимыхСтраниц		= 0;
			ПараметрыПанели.ТекущаяСтраница		= ?(ТекПанель.ТекущаяСтраница.Видимость,ТекПанель.ТекущаяСтраница.Имя,"");
			ПараметрыПанели.ГлавнаяПанель		= (ТекСтраница.Родитель = Неопределено);
			ПараметрыПанели.ОтображениеЗакладок	= ТекСтраница.ОтображениеЗакладок;
		КонецЕсли;
		
		// Установим видимость страниц
		Если ТекПанель.Страницы[ТекСтраница.ИмяСтраницы].Видимость <> ТекСтраница.Видимость Тогда
			Попытка
				ТекПанель.Страницы[ТекСтраница.ИмяСтраницы].Видимость = ТекСтраница.Видимость;
			Исключение
				Если СписокСтрокДляУдаления.НайтиПоЗначению(ТекСтраница) = Неопределено Тогда
					СписокСтрокДляУдаления.Добавить(ТекСтраница);
				КонецЕсли;
			КонецПопытки;
		КонецЕсли;
		
		Если ТекСтраница.Видимость Тогда
			// Определим текущую страницу
			Если ТекСтраница.Текущая ИЛИ ПустаяСтрока(ПараметрыПанели.ТекущаяСтраница) Тогда
				ПараметрыПанели.ТекущаяСтраница = ТекСтраница.ИмяСтраницы;
			КонецЕсли;
			
			// Поменяем порядок следования страниц на панели
			Попытка
				Разница = ДеревоФормы.Индекс(ТекСтраница) - ТекПанель.Страницы.Индекс(ТекПанель.Страницы[ТекСтраница.ИмяСтраницы]);
				Если НЕ Разница = 0 Тогда
					ТекПанель.Страницы.Сдвинуть(ТекПанель.Страницы[ТекСтраница.ИмяСтраницы], Разница);
				КонецЕсли;
			Исключение
				Если СписокСтрокДляУдаления.НайтиПоЗначению(ТекСтраница) = Неопределено Тогда
					СписокСтрокДляУдаления.Добавить(ТекСтраница);
				КонецЕсли;	
			КонецПопытки;
		КонецЕсли;
		
		// Будем накапливать количество видимых страниц, для того чтобы в последствии судить
		// о том отображать ли закладки на главных панелях
		Если ПараметрыПанели.ГлавнаяПанель Тогда
			ПараметрыПанели.ВидимыхСтраниц = ПараметрыПанели.ВидимыхСтраниц + ?(ТекСтраница.Видимость,1,0)
		КонецЕсли;
	КонецЦикла;
		
	// Установим видимость закладок, а также установим текущие страницы всех найденных панелей
	Для Каждого ТекПараметрыПанели Из ПараметрыПанелей Цикл
		ТекПанель = ФормаАРМ.ЭлементыФормы[ТекПараметрыПанели.ИмяПанели];
		Если ТекПараметрыПанели.ГлавнаяПанель Тогда
			ТекПанель.ОтображениеЗакладок = ?(ТекПараметрыПанели.ВидимыхСтраниц = 1, ОтображениеЗакладок.НеИспользовать, ТекПараметрыПанели.ОтображениеЗакладок);
		КонецЕсли;
		ТекПанель.ТекущаяСтраница = ТекПанель.Страницы[ТекПараметрыПанели.ТекущаяСтраница];
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ИсторияПоискаКлиентовАвтомобилей = Новый СписокЗначений;
ИсторияПоискаРаботЗапчастей = Новый СписокЗначений;
ИсторияПоискаЗаявок = Новый СписокЗначений;
ИсторияПоискаАРМАвтосалон = Новый СписокЗначений;

#КонецЕсли
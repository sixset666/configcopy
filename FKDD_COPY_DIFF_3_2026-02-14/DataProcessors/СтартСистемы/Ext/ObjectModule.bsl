#Если Клиент Тогда
Перем ПервыйЗапуск;	// Идентификатор первого запуска
Перем ИмяКомпонентыОборудования;
Перем ВерсияКомпонентыОборудования;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция создания предопределенных пользователей информационной базы
Функция СоздатьПредопределенныхПользователейИнформационнойБазы(ТребуетсяПерезапуск) Экспорт
	Если НЕ ПравоДоступа("Администрирование", Метаданные) Тогда
		ТребуетсяПерезапуск=Ложь;
		Возврат Истина;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	КоличествоПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей().Количество();
	Если КоличествоПользователей=0 Тогда
		ТребуетсяПерезапуск=Истина;
	Иначе
		ТребуетсяПерезапуск=Ложь;
	КонецЕсли;
	
	// Проверим заданы ли пользователи ИБ
	// Кроме предопределенного нужно создать хотя бы еще одного а то не зайдут в базу :-) 
	Если КоличествоПользователей = 0 Тогда
		ТекРоль = Неопределено;
		Для Каждого ТекРоль Из Метаданные.Роли Цикл
			Если ПравоДоступа("Администрирование", Метаданные, ТекРоль) Тогда
				Прервать; // нашли
			КонецЕсли;
		КонецЦикла;
		Если ТекРоль = Неопределено Тогда
			Предупреждение("  ВНИМАНИЕ! Не определена роль с правами администрирования.
			|Необходимо создать данную роль (например, ""ПолныеПрава"") в режиме конфигуратора!", 15);
			Возврат Ложь;
		Иначе
			ИмяПользователя = "Администратор";
			ПолноеИмяПользователя = "Администратор информационной базы";
			ПользовательАдминистратор = ПользователиИнформационнойБазы.СоздатьПользователя();
			ПользовательАдминистратор.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях=Ложь;
			ПользовательАдминистратор.Имя = ИмяПользователя;
			ПользовательАдминистратор.ПолноеИмя = ПолноеИмяПользователя;
			ПользовательАдминистратор.ПоказыватьВСпискеВыбора = Истина;
			ПользовательАдминистратор.Роли.Добавить(ТекРоль);
			ПользовательАдминистратор.Язык = Метаданные.Языки.Русский;
			ПользовательАдминистратор.Записать();
			Предупреждение("  ВНИМАНИЕ! Не определены пользователи базы данных.
			|Необходимо задать список пользователей для этой базы данных. Это можно сделать либо в режиме предприятия при помощи интерфейса справочника ""Пользователи"", либо в режиме конфигуратора через меню ""Администрирование"", пункт ""Пользователи"".",15);
		КонецЕсли;				
	КонецЕсли;
			 
	// Необходимо проконтролировать наличие предопределенного пользователя ИБ по имени "Робот"
	НеобходимоЗаписать = Ложь;
	ПользовательРобот = ПользователиИнформационнойБазы.НайтиПоИмени("Робот");
	Если (ПользовательРобот = Неопределено) Тогда
		ТекРоль = Неопределено;
		Для Каждого ТекРоль Из Метаданные.Роли Цикл
			Если НЕ ПравоДоступа("Администрирование", Метаданные, ТекРоль)
				 И  ПравоДоступа("МонопольныйРежим", Метаданные, ТекРоль)
				 И ПравоДоступа("ОбновлениеКонфигурацииБазыДанных", Метаданные, ТекРоль) Тогда
				 Прервать; // нашли
			КонецЕсли;
		КонецЦикла;
		Если ТекРоль = Неопределено Тогда
			Предупреждение("  ВНИМАНИЕ! Не определена служебная роль с правами обновления ИБ, выполнения обмена и 
			|регламентных заданий. Необходимо создать данную роль (например, ""ОбновлениеОбменИФЗ"") в режиме конфигуратора!", 15);
			Возврат Ложь;
		Иначе				
			ПользовательРобот = ПользователиИнформационнойБазы.СоздатьПользователя();
			ПользовательРобот.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях=Ложь;
			ПользовательРобот.Имя = "Робот";
			ПользовательРобот.ПолноеИмя = "Робот регламентных операций";
			ПользовательРобот.ПоказыватьВСпискеВыбора = Ложь; // Этого лучше не светить
			ПользовательРобот.Язык = Метаданные.Языки.Русский;
			НеобходимоЗаписать = Истина;
		КонецЕсли;
	КонецЕсли;
		
	// Даем полные права на все, но только в неинтерактивном режиме
	// то есть Робот может выполнять обновление конфигурации, фон. задания, но не может
	// интерактивно открыть формы или заниматься администрированием ИБ
	Если НЕ ТекРоль = Неопределено Тогда
		Если НЕ ПользовательРобот.Роли.Содержит(ТекРоль) Тогда
			ПользовательРобот.Роли.Добавить(ТекРоль);
			НеобходимоЗаписать = Истина;
		КонецЕсли;
	КонецЕсли;
	Если (Найти(ВРЕГ(ИмяПользователя()),"РОБОТ")>0) Тогда
		// Если это робот зашел значит он знал пароль - ничего не будем трогать
	Иначе // А иначе проверим установлен ли пароль
		Пароль = Константы.ПарольАдминистратораИБ.Получить();
		Если ПустаяСтрока(Пароль) ИЛИ ПользовательРобот.ПарольУстановлен Тогда
			// Тогда лучше ничего не трогать
		Иначе
			ПользовательРобот.Пароль = Пароль;
			Сообщить("Служебному пользователю ""Робот"" установлен пароль из константы ""Пароль администратора ИБ""", СтатусСообщения.Внимание);
			НеобходимоЗаписать = Истина;
		КонецЕсли;		
	КонецЕсли;
	// Сохраним если необходимо
	Если НеобходимоЗаписать Тогда
		ПользовательРобот.Записать();
	КонецЕсли;

	Если ТребуетсяПерезапуск Тогда
		КоличествоПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей().Количество();
		Если КоличествоПользователей=0 Тогда
			ТекстСообщения="Не заданы пользователи информационной базы.
							|Создайте нового администратора в режиме конфигуратора или выполните запуск с правами администратора.";
		Иначе
			ТекстСообщения="Созданы новые пользователи информационной базы.
							|Требуется перезапуск.";
		КонецЕсли;
		Предупреждение(ТекстСообщения+"
					   |Работа программы будет завершена.",,"Внимание!");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Истина;
КонецФункции

// Определение текущего пользователя и параметров его работы
//
// Параметры возврата:
//						ЕстьПраваАдминистратора - булево
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, Ложь - что-то не в порядке.
// 
Функция ПервоначальнаяИнициализация(ЕстьПраваАдминистратора,ОбнаруженПервыйЗапуск,НеобходимоОбновлениеБазыДанных) Экспорт
	
	Пользователь = ПараметрыСеанса.Пользователь;
	ИмяПользователя = ИмяПользователя();
	ПолноеИмяПользователя = ПолноеИмяПользователя();
	ИмяКомпьютера = ИмяКомпьютера();
	
	// Переопределим текущий компьютер (в клиентской сессии пользователя он может
	// быть отличным от того что был определен в УстановкаПараметровСеанса)
	Состояние("Обновляем информацию о компьютере ...");
	ТЗ = "ВЫБРАТЬ
	     |	Компьютеры.ИмяКомпьютера,
	     |	Компьютеры.Пользователь,
	     |	Компьютеры.Ссылка,
	     |	Компьютеры.Наименование,
	     |	ВЫБОР
	     |		КОГДА Компьютеры.Пользователь = &ПустойПользователь
	     |			ТОГДА 1
	     |		ИНАЧЕ 0
	     |	КОНЕЦ КАК Порядок
	     |ИЗ
	     |	Справочник.Компьютеры КАК Компьютеры
	     |ГДЕ
	     |	(Компьютеры.ИмяКомпьютера = &ИмяКомпьютера ИЛИ Компьютеры.Наименование ПОДОБНО &ИмяКомпьютера)
	     |И (Компьютеры.Пользователь = &Пользователь ИЛИ  Компьютеры.Пользователь = &ПустойПользователь)
	     |УПОРЯДОЧИТЬ ПО
	     |	Порядок";
		 
	Запрос = Новый Запрос(ТЗ);
	Запрос.УстановитьПараметр("ИмяКомпьютера",		ИмяКомпьютера);
	Запрос.УстановитьПараметр("ПустойПользователь", Справочники.Пользователи.ПустаяСсылка());
	Запрос.УстановитьПараметр("Пользователь",		Пользователь);
	РезЗапроса = Запрос.Выполнить();
	Если НЕ РезЗапроса.Пустой() Тогда
		Выборка = РезЗапроса.Выбрать();
		Выборка.Следующий();
		Комп = Выборка.Ссылка;
	Иначе
		Комп = Справочники.Компьютеры.СоздатьЭлемент();
		Комп.УстановитьНовыйКод("");
		Комп.ИмяКомпьютера = ИмяКомпьютера;
		Комп.Наименование = Комп.ПолучитьНаименованиеРабочегоМеста();
		Комп.ОбменДанными.Загрузка = Истина;
		Комп.Записать();
	КонецЕсли;
	ПараметрыСеанса.Компьютер = Комп.Ссылка; // Вот теперь тут правильное значение для клиентской сессии
	
	Состояние("Инициализация системы прав и настроек...");
	Объект=ПланыВидовХарактеристик.ПраваИНастройки.СоздатьЭлемент();
	Объект.ИнициализироватьПраваИНастройки();
	
	// Получим права пользователя
	Состояние("Получение прав и настроек текущего пользователя...");
	глПрава = обПолучитьПраваИНастройкиПользователя(Пользователь);
	
	// Проверим, а можно ли пользователю работать с программой
	Если ЗначениеЗаполнено(Пользователь) Тогда
		// проверка на "временный бан"
		ТЗ="ВЫБРАТЬ ПЕРВЫЕ 1
		   |	РежимРаботы.Текст КАК Сообщение
		   |ИЗ
		   |	РегистрСведений.РежимыРаботыПользователей КАК РежимРаботы
		   |ГДЕ
		   |	РежимРаботы.Пользователь = &Пользователь
		   |	И РежимРаботы.Режим = 1";
		
		Запрос=Новый Запрос(ТЗ);
		Запрос.УстановитьПараметр("Пользователь",Пользователь);
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда 
			// Есть контакт - этого пользователя в базе видеть не хотят
			Выборка=РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			СообщениеАдминистратора=Выборка.Сообщение;
			Если ПустаяСтрока(СообщениеАдминистратора) Тогда
				СообщениеАдминистратора = "Сотруднику <"+СокрЛП(Пользователь.Сотрудник.Наименование)+"> запрещено работать с программой.
				|Обратитесь к администратору базы данных.";
			КонецЕсли;
			Если ПравоДоступа("Администрирование",Метаданные) Тогда		// а админа все-таки пустим
				СообщениеАдминистратора = СообщениеАдминистратора + Символы.ПС + " 
				|(Имеются привелегии администратора - ваш сеанс продолжается)";
			КонецЕсли;
			Предупреждение(СообщениеАдминистратора,15);
			Если НЕ ПравоДоступа("Администрирование",Метаданные) Тогда	// а админа все-таки пустим
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	// Вторую проверку смотрим в постоянном стоп-листе
	Если НЕ Пользователь.Сотрудник.Пустая() Тогда
		Если Пользователь.Сотрудник.ФлагУволен Тогда
			Предупреждение("Сотрудник <"+СокрЛП(Пользователь.Сотрудник.Наименование)+"> уволен. Запрещено работать с программой!
			|Обратитесь к администратору базы данных.",5);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
    
	// проверка необходимости обновления информационной базы
	Если ПустаяСтрока(Константы.НомерРелизаКонфигурации.Получить()) Тогда
		ОбнаруженПервыйЗапуск = Истина;
	ИначеЕсли Константы.НомерРелизаКонфигурации.Получить() <> Метаданные.Версия Тогда
		НеобходимоОбновлениеБазыДанных = Истина;
	КонецЕсли;
	
	Если ОбнаруженПервыйЗапуск Тогда

		ФормаЛицензионноеСоглашение = Обработки.ЛицензионноеСоглашение.ПолучитьФорму("ЛицензионноеСоглашение");
        ПользовательСогласен = ФормаЛицензионноеСоглашение.ОткрытьМодально();
		Если ПользовательСогласен = Ложь ИЛИ ПользовательСогласен = Неопределено Тогда
			ЗавершитьРаботуСистемы(Ложь);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
    
    Если ЕстьПраваАдминистратора Тогда
		// если это первый запуск - заполним реквизиты этого узла плана обмена,
		// так как в дальнейшем он нам очень пригодится
		Если ОбнаруженПервыйЗапуск Тогда
			ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
			НеобходимоЗаполнениеУзла = Ложь;
			Если ПустаяСтрока(ЭтотУзел.Код) 
				ИЛИ ПустаяСтрока(ЭтотУзел.Префикс) 
				ИЛИ ПустаяСтрока(ЭтотУзел.Наименование) Тогда
				НеобходимоЗаполнениеУзла = Истина;
			КонецЕсли;
			Если НеобходимоЗаполнениеУзла Тогда
				ЭтотУзелОбъект = ЭтотУзел.ПолучитьОбъект();
				Если ПустаяСтрока(ЭтотУзелОбъект.Код) Тогда
					ЭтотУзелОбъект.Код = "ЦБ";
				КонецЕсли;
				Если ПустаяСтрока(ЭтотУзелОбъект.Префикс) Тогда
					ЭтотУзелОбъект.Префикс = ЭтотУзелОбъект.Код;
				КонецЕсли;
				Если ПустаяСтрока(ЭтотУзелОбъект.Наименование) Тогда
					ЭтотУзелОбъект.Наименование = "Центральная база";
				КонецЕсли;
				ЭтотУзелОбъект.ДополнительныеСвойства.Вставить("Загрузка",Истина);
				Попытка
					ЭтотУзелОбъект.Записать();
				Исключение
					Сообщить("Ошибка при первоначальном заполнении реквизитов этого узла: " + ОписаниеОшибки(),СтатусСообщения.Важное);
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
			
		// SUIG - создаем сразу всю иерархию ИБ
		// если это центральный узел - сам центральный узел + все подчиненные на 1 уровень вниз
		// иначе (главная ИБ придет при чтении сообщения обмена) - только подчиненные на 1 уровень вниз
		РезПроверки = Истина;
		Если НЕ ЗначениеЗаполнено(ПланыОбмена.ГлавныйУзел()) Тогда
			// имеет смысл только для центрального, для остальных тек.ИБ сама появится по завершении
			// чтения сообщения от главного узла
			РезПроверки = одОбменДанными.ПроверитьТекущуюИБ();
			// для центрального узла настройки доставки сейчас пока нет
		КонецЕсли; 
		Если РезПроверки Тогда
		    РезПроверки = одОбменДанными.ПроверитьИерархиюИБ();
			// теперь все дочерние ИБ есть, присвоим для них всех настройки по умолчанию, взяв ее из старых настроек
			одОбменДанными.ПеренестиНастройкиДоставкиСообщений();
		КонецЕсли; 
		
	КонецЕсли;
	
	Возврат Истина;	
	
КонецФункции // ПервоначальнаяИнициализация()

// Проверяет, есть ли необходимость выполнять обновление информационной базы.
// Необходимость обновления определяется по соответствию константы и номеру версии в метаданных.
// Если обновление не удалось выполнить - предлагается завершить работу системы.
//
Функция ВыполнитьОбновлениеИнформационнойБазы(ПервыйЗапускВДочернемУзле=Ложь) Экспорт
	
	// Установка монопольного режима для обновления информационной базы.
	МонопольныйРежимУстановлен = Ложь;
	ТекстОшибки = ОписаниеОшибки();
	Пока НЕ МонопольныйРежимУстановлен Цикл
		Попытка 
			УстановитьМонопольныйРежим(Истина);
			МонопольныйРежимУстановлен = Истина;
		Исключение
			Если ПараметрыСеанса.Пользователь = Справочники.Пользователи.Робот Тогда
				ТекстОшибки = ИнформацияОбОшибке().Описание;
				Прервать;
			Иначе	
				Текст = "Не удалось установить монопольный режим."
				+ Символы.ПС + "Выполнить повторную попытку установки?";
				Ответ = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Нет);
				Если Ответ = КодВозвратаДиалога.Нет Тогда
					Прервать;
				КонецЕсли; 
			КонецЕсли; 
		КонецПопытки;
	КонецЦикла;
	Если НЕ МонопольныйРежимУстановлен Тогда
		ТекстСообщения = "Не удалось установить монопольный режим:" + ТекстОшибки + ". Работа системы будет завершена.";
		Сообщить(ТекстСообщения, СтатусСообщения.ОченьВажное);
		ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		Возврат Ложь;
	Иначе
		ТекстСообщения = "Монопольный режим для обновления релиза успешно установлен";
		Сообщить(ТекстСообщения,СтатусСообщения.Информация);
		ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , ТекстСообщения);
	КонецЕсли; 
	
	
	// Обновление информационной базы.
	Обработки.ОбновлениеИнформационнойБазы.Создать().ВыполнитьОбновление(ПервыйЗапускВДочернемУзле);

	// Отключение монопольного режима.
	УстановитьМонопольныйРежим(Ложь);
	ТекстСообщения = "Монопольный режим для обновления релиза снят";
	Сообщить(ТекстСообщения,СтатусСообщения.Информация);
	ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , ТекстСообщения);

	// Проверка выполнения обновления информационной базы.
	Если (Константы.НомерРелизаКонфигурации.Получить() <> Метаданные.Версия) Тогда
		Если НЕ ПервыйЗапускВДочернемУзле Тогда	// (для подчиненной базы это может быть и не ошибка, просто ждем обновления значения константы)
			Текст = "Не выполнено обновление информационной базы! Продолжить работу системы?";
			Ответ = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Да);
			Возврат (Ответ = КодВозвратаДиалога.Да);
		КонецЕсли;
	КонецЕсли;

	// После обновления базы права лучше пересчитать
	Состояние("Получение прав и настроек текущего пользователя...");
	глПрава = обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
	
	Возврат Истина;
КонецФункции // ВыполнитьОбновлениеИнформационнойБазы()

 //Процедура проверят заполнены ли все константы учета
//если нет, то открывается форма заполнения констант или завершается работа системы
//
Функция ВыполнитьПроверкуЗаполненияКонстант(ЕстьПраваАдминистратора,Знач ПервыйЗапуск)  Экспорт
	СтрокаОшибки = "";
	
	//проверим заполненность по всем константам, исключая следующие
	Для Каждого Константа Из Константы Цикл
		Если Константа = Константы.ПартияТоваровОтрицательныхОстатков Тогда Продолжить;
		ИначеЕсли Константа = Константы.НомерРелизаКонфигурации Тогда Продолжить;
		ИначеЕсли Константа = Константы.ИнтервалПроверкиНапоминаний Тогда Продолжить; // если  равно "0" - механизм напоминаний отключен
		ИначеЕсли Константа = Константы.СерверЛицензирования Тогда Продолжить; // для "локальных" установок СерверЛицензирования пустой
		ИначеЕсли Константа = Константы.КодДоступаКлючаЗащиты Тогда Продолжить;
		ИначеЕсли Константа = Константы.SMSНомерОтправителя Тогда Продолжить;
		//+СофтФон	
		ИначеЕсли Константа = Константы.CRM_ИспользуемыйСофтФон Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ИмяСервера Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_Порт Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_КодГорода Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_КодСтраны Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_КоличествоХранимыхЦифрТелефона Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_МаксимальнаяДлинаВнутреннегоНомера Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ПрефиксВыходаВГород Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ПрефиксВыходаНаМежгород Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ПрефиксВыходаНаМеждународную Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerIP Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerLogin Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerPassword Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerPort Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONType Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьCLON Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьСпрут7 Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпКоличествоВзаимодействийДляМаршрутизации Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпСтрокаПодключенияИстории Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпДатаИсторииЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИдентификаторМаршрутизации Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьSMS Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьЗаписьПереговоров Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьИсториюЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьМаршрутизацию Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьРегламентноеЗаданиеИсторииЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьРегламентноеЗаданиеТелефонныхКниг Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьСофтФон Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпМинимальнаяДлинаВнутреннихНомеров Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпОповещатьОПропущенныхЗвонках Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпПользовательДляОповещений Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьВнутреннийНомерИзКИПользователя Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпДополнительнаяИнформацияКонтактов Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпДействиеПриОшибкеЗагрузкиИсторииЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпОграничениеНаПросмотрТелефонныхЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьCoMagic Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпЛогинCoMagic Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпПарольCoMagic Тогда Продолжить;
		//-СофтФон	
		ИначеЕсли Константа = Константы.ИмяАдминистратораСервера Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистратораСервера Тогда Продолжить;
		ИначеЕсли Константа = Константы.НомерПортаПодключенияКАгенту Тогда Продолжить;	
		ИначеЕсли Константа = Константы.ИмяАдминистратораКластера Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистратораКластера Тогда Продолжить;
		ИначеЕсли Константа = Константы.ИмяАдминистратораИБ Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистратораИБ Тогда Продолжить;
		ИначеЕсли Константа = Константы.СлужебныйКаталогРегламентныхОпераций Тогда Продолжить;
		ИначеЕсли Константа = Константы.СообщениеОБлокировкеБазы Тогда Продолжить;
		ИначеЕсли Константа = Константы.КодРазрешенияВходаПриБлокировке Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистрированияСервера Тогда Продолжить; // пустой вполне может быть
		ИначеЕсли Константа = Константы.смсИмяПользователя Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсИмяПользователяПоУмолчанию Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсКонецПериодаЗапрета Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсНачалоПериодаЗапрета Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсНеОтправлятьSMS Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПарольПользователя Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПолучатьТолькоПолныеСообщения Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиПароль Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиПользователь Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиПорт Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиСервер Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсСрокЖизниSMS Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсРоботРегламентныхЗаданий Тогда Продолжить; 
		ИначеЕсли Константа = Константы.ДнейДоОкончанияДействияКлюча Тогда Продолжить; 
		КонецЕсли;
		
		ЗначениеКонстанты = Константа.Получить();
		Если обЗначениеНеЗаполнено(ЗначениеКонстанты) Тогда
			СтрокаОшибки = СтрокаОшибки + "
				|"+Метаданные.Константы[СтрЗаменить(Строка(Константа),"КонстантаМенеджер.","")].Синоним;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(СтрокаОшибки) Тогда
		Сообщить("
		|Производится начальное заполнение констант...");
	КонецЕсли;
	
	// попытаемся заполнить пустые константы
	Если обЗначениеНеЗаполнено(Константы.СпособВеденияБаланса.Получить()) Тогда
		// считаем, что по-умолчанию способ ведения баланса по компании в целом
		Константы.СпособВеденияБаланса.Установить(Перечисления.СпособВеденияБаланса.ПоКомпании);
		Сообщить("Константа ""Способ ведения баланса"" установлена в ""По компании""");
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ВалютаУправленческогоУчетаКомпании.Получить()) Тогда
		// ищем рубли
		Рубли = Справочники.Валюты.НайтиПоКоду("643");
		Если НЕ Рубли.Пустая() Тогда
			Константы.ВалютаУправленческогоУчетаКомпании.Установить(Рубли);
			Сообщить("Константа ""Валюта управленческого учета компании"" установлена в "+СокрЛП(Рубли));
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()) Тогда
		// ищем валюту
		Рубли = Справочники.Валюты.НайтиПоКоду("643");
		Если НЕ Рубли.Пустая() Тогда
			Константы.ВалютаРегламентированногоУчетаОрганизаций.Установить(Рубли);
			Сообщить("Константа ""Валюта регламентированного учета компании"" установлена в "+СокрЛП(Рубли));
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.СтратегияСписанияПартийТоваровПоДатам.Получить()) Тогда
		СтратегияСписанияПартийТоваровПоДатам = Перечисления.СтратегияСписанияПартийТоваровПоДатам.ФИФО; 
		Константы.СтратегияСписанияПартийТоваровПоДатам.Установить(СтратегияСписанияПартийТоваровПоДатам);
		Сообщить("Константа ""Стратегия списания партий товаров по датам"" установлена в "+СокрЛП(СтратегияСписанияПартийТоваровПоДатам));
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.СтратегияСписанияПартийТоваровПоСтатусам.Получить()) Тогда
		СтратегияСписанияПартийТоваровПоСтатусам = Перечисления.СтратегияСписанияПартийТоваровПоСтатусам.СначалаКупленныеПотомПринятые; 
		Константы.СтратегияСписанияПартийТоваровПоСтатусам.Установить(СтратегияСписанияПартийТоваровПоСтатусам);
		Сообщить("Константа ""Стратегия списания партий товаров по статусам"" установлена в "+СокрЛП(СтратегияСписанияПартийТоваровПоСтатусам));
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ОсновнаяЕдиницаИзмеренияМассы.Получить()) Тогда
		// ищем килограммы
		Кило = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("166");
		Если НЕ Кило.Пустая() Тогда
			Константы.ОсновнаяЕдиницаИзмеренияМассы.Установить(Кило);
			Сообщить("Константа ""Основная единица измерения массы"" установлена в "+СокрЛП(Кило));
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ОсновнаяЕдиницаИзмеренияКоличества.Получить()) Тогда
		// ищем штуки
		Штука = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("796");
		Если НЕ Штука.Пустая() Тогда
			Константы.ОсновнаяЕдиницаИзмеренияКоличества.Установить(Штука);
			Сообщить("Константа ""Основная единица измерения количества"" установлена в "+СокрЛП(Штука));
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ОсновнаяЕдиницаРазмерности.Получить()) Тогда
		// ищем метры
		Метр = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("006");
		Если НЕ Метр.Пустая() Тогда
			Константы.ОсновнаяЕдиницаРазмерности.Установить(Метр);
			Сообщить("Константа ""Основная единица размерности ордерного склада"" установлена в "+СокрЛП(Метр));
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ПрефиксШтучногоШК.Получить()) Тогда
		Константы.ПрефиксШтучногоШК.Установить(20);
		Сообщить("Константа ""Префикс штучного штрихкода"" установлена в "+СокрЛП(20));
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ПрефиксВесовогоШК.Получить()) Тогда
		Константы.ПрефиксВесовогоШК.Установить(22);
		Сообщить("Константа ""Префикс весового штрихкода"" установлена в "+СокрЛП(22));
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ВалютаНакопительныхСумм.Получить()) Тогда
		// ищем рубли
		Рубли = Справочники.Валюты.НайтиПоКоду("643");
		Если НЕ Рубли.Пустая() Тогда
			Константы.ВалютаНакопительныхСумм.Установить(Рубли);
			Сообщить("Константа ""Валюта накопительных сумм"" установлена в "+СокрЛП(Рубли));
		КонецЕсли;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ПериодНакопительныхСкидок.Получить()) Тогда	
		Константы.ПериодНакопительныхСкидок.Установить(Перечисления.ПериодичностьАнализаНакопительныхСкидок.НеИспользуется);
		Сообщить("Константа ""Период накопительных скидок"" установлена в ""Не используется""");
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Константы.ПартияТоваровОтрицательныхОстатков.Получить()) Тогда	
		Дата = НачалоГода(ТекущаяДата())-1;
		// проверим какой у нас первый документ поступления
		Запрос = Новый Запрос();
		Запрос.Текст =  "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПоступлениеТоваров.Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|УПОРЯДОЧИТЬ ПО
		|	ПоступлениеТоваров.МоментВремени";
		РезультатЗапроса = Запрос.Выполнить().Выбрать();
		флСоздатьДокумент = Ложь;
		
		Если РезультатЗапроса.Следующий() Тогда //есть первая запись
			Если РезультатЗапроса.Ссылка.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровСреднееОтрицательное Тогда
				//позиционируемся на существующем
				Константы.ПартияТоваровОтрицательныхОстатков.Установить(РезультатЗапроса.Ссылка);
			Иначе
				//будем создавать новый
				флСоздатьДокумент = Истина;
				Дата = Мин(РезультатЗапроса.Ссылка.Дата-1,Дата);
			КонецЕсли;
		Иначе //нет ни одной записи
			флСоздатьДокумент = Истина;
		КонецЕсли;
		
		Если флСоздатьДокумент Тогда
			ДокументПоступление = Документы.ПоступлениеТоваров.СоздатьДокумент();
			ДокументПоступление.Заполнить(Неопределено);
			ДокументПоступление.Дата = Дата;
			ДокументПоступление.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровСреднееОтрицательное;
			ДокументПоступление.ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
			СтруктураКурса = обКурсДляВалюты(ДокументПоступление.ВалютаДокумента,ДокументПоступление.Дата);
			ДокументПоступление.КурсДокумента = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
			ДокументПоступление.Контрагент = Справочники.Контрагенты.ОсновнойПоставщик;
			ДокументПоступление.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Справочники.Контрагенты.ОсновнойПоставщик,Перечисления.ВидыДоговоров.Покупка,ДокументПоступление);
			ДокументПоступление.УстановитьНовыйНомер();
			ДокументПоступление.ОбменДанными.Загрузка=Истина;
			Попытка
				ДокументПоступление.Записать();
				Константы.ПартияТоваровОтрицательныхОстатков.Установить(ДокументПоступление.Ссылка);
			Исключение
			КонецПопытки;
		КонецЕсли;
		ЗначениеКонстанты =	ДокументПоступление;
		Сообщить("Константа ""Партия товаров отрицательных остатков"" установлена в "+ЗначениеКонстанты);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Константы.ИнтервалПроверкиЗаданий.Получить()) Тогда
		// по умолчанию установим 5 минут
		Константы.ИнтервалПроверкиЗаданий.Установить(300);
		Сообщить("Константа ""Интервал обработки фоновых заданий"" установлена в 5 мин");
	КонецЕсли;
	
    Если Константы.ТаймаутЗавершенияРаботыПользователей.Получить() < 120 Тогда
		// по умолчанию установим 5 минут
		Константы.ТаймаутЗавершенияРаботыПользователей.Установить(300);
		Сообщить("Константа ""Таймаут завершения работы пользователей"" установлена в 5 мин");
	КонецЕсли;
	
	Если ПустаяСтрока(Константы.ИмяАдминистратораИБ.Получить()) Тогда
		// по умолчанию установим "Робот"
		Константы.ИмяАдминистратораИБ.Установить("Робот");
		Сообщить("Константа ""Имя робота ИБ"" установлена в ""Робот""");
	КонецЕсли;
	
	Если ПустаяСтрока(Константы.ИсполняемыйФайлКлиентаНаСервере.Получить()) Тогда
		// по умолчанию установим стандартный каталог
		ИсполняемыйФайлКлиентаНаСервере=КаталогПрограммы();
		Если Прав(ИсполняемыйФайлКлиентаНаСервере,1)<>"\" Тогда
			ИсполняемыйФайлКлиентаНаСервере=ИсполняемыйФайлКлиентаНаСервере+"\";
		КонецЕсли;
		ИсполняемыйФайлКлиентаНаСервере=ИсполняемыйФайлКлиентаНаСервере+"1cv8.exe";
		Константы.ИсполняемыйФайлКлиентаНаСервере.Установить(ИсполняемыйФайлКлиентаНаСервере);
		Сообщить("Константа ""Исполняемый файл клиента на сервере"" установлена в " + ИсполняемыйФайлКлиентаНаСервере);
	КонецЕсли;
	
	Если ПустаяСтрока(Константы.СообщениеОБлокировкеБазы.Получить()) Тогда
		Константы.СообщениеОБлокировкеБазы.Установить("
			| База данных ЗАКРЫТА на регламентное обслуживание.
			|
			|ВНИМАНИЮ тех, кто еще пока продолжает свою работу:
			|Администратор просит выйти из базы самостоятельно.
			|Через несколько минут ваш сеанс будет завершен
			|автоматически в принудительном режиме!
			|");
	КонецЕсли;
	
	Если ПустаяСтрока(Константы.КодРазрешенияВходаПриБлокировке.Получить()) Тогда
		Константы.КодРазрешенияВходаПриБлокировке.Установить("AllowRobotLogon");
	КонецЕсли;
	
	// SMS4B +
	Если НЕ ЗначениеЗаполнено(Константы.смсСрокЖизниSMS.Получить()) Тогда
		// по умолчанию установим 24 часа
		Константы.смсСрокЖизниSMS.Установить(24);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Константы.смсНачалоПериодаЗапрета.Получить()) Тогда
		Константы.смсНачалоПериодаЗапрета.Установить(Дата('00010101210000'));
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Константы.смсКонецПериодаЗапрета.Получить()) Тогда
		Константы.смсКонецПериодаЗапрета.Установить(Дата('00010101090000'));
	КонецЕсли;
	// SMS4B -
	
	
	Если НЕ ПустаяСтрока(СтрокаОшибки) Тогда
		Сообщить("Установка констант завершена.");
		Сообщить("--------------------------------------------------------");
	КонецЕсли;
	
	//проверим заполненность по всем константам, исключая исключения
	СтрокаОшибки ="";
	Для Каждого Константа Из Константы Цикл
		Если Константа = Константы.ПартияТоваровОтрицательныхОстатков Тогда Продолжить;
		ИначеЕсли Константа = Константы.НомерРелизаКонфигурации Тогда Продолжить;
		ИначеЕсли Константа = Константы.СерверЛицензирования Тогда Продолжить; // для "локальных" установок СерверЛицензирования пустой
		ИначеЕсли Константа = Константы.КодДоступаКлючаЗащиты Тогда Продолжить;
		ИначеЕсли Константа = Константы.ИнтервалПроверкиНапоминаний Тогда Продолжить;
		ИначеЕсли Константа = Константы.SMSНомерОтправителя Тогда Продолжить;
		//+СофтФон	
		ИначеЕсли Константа = Константы.CRM_ИспользуемыйСофтФон Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ИмяСервера Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_Порт Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_КодГорода Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_КодСтраны Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_КоличествоХранимыхЦифрТелефона Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_МаксимальнаяДлинаВнутреннегоНомера Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ПрефиксВыходаВГород Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ПрефиксВыходаНаМежгород Тогда Продолжить;
		ИначеЕсли Константа = Константы.CRM_ПрефиксВыходаНаМеждународную Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerIP Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerLogin Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerPassword Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONServerPort Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпCLONType Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьCLON Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьСпрут7 Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпКоличествоВзаимодействийДляМаршрутизации Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпСтрокаПодключенияИстории Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпДатаИсторииЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИдентификаторМаршрутизации Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьSMS Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьЗаписьПереговоров Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьИсториюЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьМаршрутизацию Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьРегламентноеЗаданиеИсторииЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьРегламентноеЗаданиеТелефонныхКниг Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьСофтФон Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпМинимальнаяДлинаВнутреннихНомеров Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпОповещатьОПропущенныхЗвонках Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпПользовательДляОповещений Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьВнутреннийНомерИзКИПользователя Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпДополнительнаяИнформацияКонтактов Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпДействиеПриОшибкеЗагрузкиИсторииЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпОграничениеНаПросмотрТелефонныхЗвонков Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпИспользоватьCoMagic Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпЛогинCoMagic Тогда Продолжить;
		ИначеЕсли Константа = Константы.сфпПарольCoMagic Тогда Продолжить;
		//-СофтФон
		ИначеЕсли Константа = Константы.ИмяАдминистратораСервера Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистратораСервера Тогда Продолжить;
		ИначеЕсли Константа = Константы.НомерПортаПодключенияКАгенту Тогда Продолжить;	
		ИначеЕсли Константа = Константы.ИмяАдминистратораКластера Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистратораКластера Тогда Продолжить;
		ИначеЕсли Константа = Константы.ИмяАдминистратораИБ Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистратораИБ Тогда Продолжить;
		ИначеЕсли Константа = Константы.СлужебныйКаталогРегламентныхОпераций Тогда Продолжить;
		ИначеЕсли Константа = Константы.СообщениеОБлокировкеБазы Тогда Продолжить;
		ИначеЕсли Константа = Константы.КодРазрешенияВходаПриБлокировке Тогда Продолжить;
		ИначеЕсли Константа = Константы.ПарольАдминистрированияСервера Тогда Продолжить; // пустой вполне может быть
		ИначеЕсли Константа = Константы.смсИмяПользователя Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсИмяПользователяПоУмолчанию Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсКонецПериодаЗапрета Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсНачалоПериодаЗапрета Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсНеОтправлятьSMS Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПарольПользователя Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПолучатьТолькоПолныеСообщения Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиПароль Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиПользователь Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиПорт Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсПроксиСервер Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсСрокЖизниSMS Тогда Продолжить; 
		ИначеЕсли Константа = Константы.смсРоботРегламентныхЗаданий Тогда Продолжить; 
		ИначеЕсли Константа = Константы.ДнейДоОкончанияДействияКлюча Тогда Продолжить; 
		КонецЕсли;
		ЗначениеКонстанты = Константа.Получить();
		Если обЗначениеНеЗаполнено(ЗначениеКонстанты) Тогда
			СтрокаОшибки = СтрокаОшибки + "
				|"+Метаданные.Константы[СтрЗаменить(Строка(Константа),"КонстантаМенеджер.","")].Синоним;
		КонецЕсли;
	КонецЦикла;
	
	// если есть незаполненные
	Если ПустаяСтрока(СтрокаОшибки) Тогда
		Результат = Истина; // проверка успешна завершена, все Ок
	Иначе
		Сообщить("Не заполнены константы: "+Символы.ПС+СтрокаОшибки);
		Если ЕстьПраваАдминистратора И (ПараметрыСеанса.Пользователь<>Справочники.Пользователи.Робот) 
		 И (ПервыйЗапуск ИЛИ Вопрос("Выполнить настройку констант?
			|При отрицательном ответе работа системы будет ЗАВЕРШЕНА!",РежимДиалогаВопрос.ДаНет,15) = КодВозвратаДиалога.Да) Тогда
			ФормаКонстант = ПолучитьОбщуюФорму("НастройкаПараметров");
			ФормаКонстант.ОткрытьМодально();
			Результат = ВыполнитьПроверкуЗаполненияКонстант(ЕстьПраваАдминистратора,ПервыйЗапуск);
		Иначе // если не заполнять, то долой
			Предупреждение( "  ВНИМАНИЕ! Не заполнены константы информационной базы. Дальнейшая работа с системой невозможна! Текущая сессия работы сейчас будет завершена.
							|  Для настройки констант необходимо зайти в программу пользователю с правами администратора базы данных.",15);
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
КонецФункции // ВыполнитьПроверкуЗаполненияКонстант()

// Заполнение рабочих структур данных обновление прав
//заполнение служебных элементов и значений
//
// Возвращаемое значение:
//  Возвращает Истина - все ОК, Ложь - что-то не в порядке.
// 
Функция ПроверкаЗаполненияРабочихСтруктурДанных(ПервыйЗапуск,Обновление) Экспорт
	Результат = Ложь;
    Попытка
		
		// Проверим, а можно ли пользователю работать с программой 	
		Если НЕ обПраво("РазрешитьРаботуСПрограммой",глПрава) Тогда
			Предупреждение("Текущему пользователю <"+СокрЛП(ПараметрыСеанса.Пользователь.Код)+"> администратор не установил право, разрешающее работать с программой!
			|Обратитесь к администратору базы данных.",5);
			Возврат Ложь;
		КонецЕсли;
		
		// Включим индикацию пользователя в заголовке системы
		Если обПраво("ИндикацияПользователяВЗаголовкеСистемы",глПрава) Тогда
			Если обПраво("ПредставлениеАвторовПоПолномуНаименованию",глПрава) Тогда
				Если обЗначениеНеЗаполнено(ПараметрыСеанса.Пользователь.Сотрудник) Тогда
					ИмяПользователя = СокрЛП(ПараметрыСеанса.Пользователь.Наименование);	
				Иначе
					ИмяПользователя = СокрЛП(ПараметрыСеанса.Пользователь.Сотрудник.Наименование);
				КонецЕсли;
			Иначе
					ИмяПользователя = СокрЛП(ПараметрыСеанса.Пользователь.Код);
			КонецЕсли;
			УстановитьЗаголовокСистемы(ПолучитьЗаголовокСистемы()+"    Пользователь: <"+ИмяПользователя+">");
		КонецЕсли;
		
		// Включим индикацию подразделения в заголовке системы
		Если обПраво("ИндикацияПодразделенияВЗаголовкеСистемы",глПрава) Тогда
			УстановитьЗаголовокСистемы(ПолучитьЗаголовокСистемы()+"    Подразделение: <"+ПараметрыСеанса.Пользователь.Подразделение+">");
		КонецЕсли;
		
		Если ПервыйЗапуск Тогда
			// При первом запуске предложим заполнить справочник станций метро
			Ответ = Вопрос("Произвести автозаполнение справочника станций метрополитена?", РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Справочники.СтанцииМетро.СоздатьЭлемент().ЗагрузитьСтанцииМетроИзМакета();
			КонецЕсли;
			// При первом запуске предложим заполнить справочник районов
			Ответ = Вопрос("Произвести автозаполнение справочника городских районов?", РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Справочники.Районы.СоздатьЭлемент().ЗагрузитьРайоныИзМакета();
			КонецЕсли;
			Если зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаТехосмотр",Ложь) Тогда
				// При первом запуске предложим заполнить справочник требований к ТС
				Ответ = Вопрос("Произвести автозаполнение справочника требований к транспортным средствам?", РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Да);
				Если Ответ = КодВозвратаДиалога.Да Тогда
					Справочники.ТребованияКТранспортнымСредствам.ЗагрузитьТребованияИзМакета();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Ну если досюда дошли то точно все в порядке
		Результат = Истина;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	Возврат Результат;
КонецФункции // ПроверкаЗаполненияРабочихСтруктурДанных	

Функция ВерсияВВидеЧисла(ВерсияСтрокой)
	Результат = 0;
	ДлинаСтроки = СтрДлина(ВерсияСтрокой);
	ВыделеннаяСтрока = "0";
	ВыделениеНачалось = Ложь;
	МассивРазделов = Новый Массив;
	// выделим последовательность цифр в имени
	Для Сч=1 По ДлинаСтроки Цикл
		Символ = Сред(ВерсияСтрокой,Сч,1);
		Если Найти("0123456789",Символ) > 0 Тогда
			ВыделеннаяСтрока = ВыделеннаяСтрока + Символ;
			ВыделениеНачалось = Истина;
		ИначеЕсли ВыделениеНачалось Тогда
			// Вот уже и закончилось
			ВыделениеНачалось = Ложь;
			Попытка
				ТекущийРаздел = Число(ВыделеннаяСтрока);
			Исключение
				ТекущийРаздел = 0;
			КонецПопытки;
			МассивРазделов.Добавить(ТекущийРаздел);
			ВыделеннаяСтрока = "0";
		Иначе
			// Просто пропускаем пока не начнутся цифры
		КонецЕсли;
	КонецЦикла;
	Если СтрДлина(ВыделеннаяСтрока) > 1 Тогда
		Попытка
			ТекущийРаздел = Число(ВыделеннаяСтрока);
		Исключение
			ТекущийРаздел = 0;
		КонецПопытки;
		МассивРазделов.Добавить(ТекущийРаздел);
	КонецЕсли;
	// Получим общий результат
	Множитель = 1;
	Основание = 256 * 256;
	МаксИндекс = МассивРазделов.ВГраница();
	Для Инд=0 По МаксИндекс Цикл
		ТекущийРаздел = МассивРазделов.Получить(МаксИндекс - Инд);
		Результат = Результат + ТекущийРаздел * Множитель;
		Множитель = Основание * Множитель;
	КонецЦикла;
	// Вернем результат
	Возврат Результат;
КонецФункции 

// Выгружает компоненту управления оборудованием в указанный каталог
Процедура ВыгрузитьКомпонентуОборудования(КаталогНазначения, Обновление = Ложь, ТекстОшибки = "")
	
	Если Обновление Тогда 
		ИмяФайлаВыгрузки = КаталогНазначения + ИмяКомпонентыОборудования + ".new";
	Иначе
		ИмяФайлаВыгрузки = КаталогНазначения + ИмяКомпонентыОборудования + ".dll";
	КонецЕсли; 
	Попытка 
		МакетКомпоненты = ПолучитьМакет("КомпонентаОборудования");
		МакетКомпоненты.Записать(ИмяФайлаВыгрузки);
	Исключение
		ТекстОшибки = "Ошибка выгрузки компоненты управления оборудованием в """ + ИмяФайлаВыгрузки + """  - " + ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("СтартСистемы.ОбновлениеКомпонент",
								 УровеньЖурналаРегистрации.Ошибка, Неопределено, , ТекстОшибки);
	КонецПопытки;

КонецПроцедуры // ВыгрузитьКомпонентуПродукта


// Проверяет наличие и версию компоненты управления оборудованием в указанном каталоге
Процедура ПроверитьОбновитьКомпонентуОборудования(КаталогНазначения, ТекстОшибки = "") Экспорт
	
	ИмяФайлаКомпоненты=КаталогНазначения+ИмяКомпонентыОборудования+".dll";
	ИмяФайлаКомпонентыНовой=КаталогНазначения+ИмяКомпонентыОборудования+".new";
	
	//Проверим наличие файла обновления компоненты оборудования
	//И если такой существует, то обновим старую компоненту на новую
	ФСНовый = Новый Файл(ИмяФайлаКомпонентыНовой);
	Если ФСНовый.Существует() Тогда
		Попытка
			Состояние("Обновление компоненты управления оборудованием ...");
			ПереместитьФайл(ИмяФайлаКомпонентыНовой,ИмяФайлаКомпоненты);
			Состояние("Обновление компоненты управления оборудованием - ОК");
		Исключение
			Состояние("Ошибка обновления компоненты управления оборудованием.");
			ТекстОшибки="Ошибка обновления компоненты управления оборудованием: "+ОписаниеОшибки();
			ЗаписьЖурналаРегистрации("СтартСистемы.ОбновлениеКомпонент",
									 УровеньЖурналаРегистрации.Ошибка, Неопределено, , ТекстОшибки);
		КонецПопытки; 
		Возврат;
	КонецЕсли;
	
	//Проверим наличие компоненты оборудования в локальном каталоге
	ФС=Новый Файл(ИмяФайлаКомпоненты);
	Если НЕ ФС.Существует() Тогда
		//Если файла компоненты не существует, то выгрузим его из макета
		ВыгрузитьКомпонентуОборудования(КаталогНазначения, Ложь, ТекстОшибки);
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
			ЗаписьЖурналаРегистрации("СтартСистемы.ОбновлениеКомпонент",
									 УровеньЖурналаРегистрации.Ошибка,Неопределено,,ТекстОшибки);
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	
	//Проверим версию компоненты оборудования, находящуюся в локальном каталоге
	
	ОбработкаЗащиты=Обработки.Защита.Создать();
	РезультатИнициализацииОборудования = ОбработкаЗащиты.ЗагрузитьКомпонентуОборудования(КаталогНазначения);
	Если РезультатИнициализацииОборудования <> "OK" Тогда
		ТекстОшибки = РезультатИнициализацииОборудования;
		Возврат;
	КонецЕсли;
	
	Попытка
		ТекущаяВерсияСтрокой=глРарус_Компонента.ПолучитьВерсиюФайла(ИмяФайлаКомпоненты);
	Исключение
		//У компоненты нет метода получения версии файла
		//Значит компонента старая и будем ее менять
		ТекущаяВерсияСтрокой="0.0.0.00";
	КонецПопытки;
	
	ТекущаяВерсияЧислом=ВерсияВВидеЧисла(ТекущаяВерсияСтрокой);
	АктуальнаяВерсияЧислом=ВерсияВВидеЧисла(ВерсияКомпонентыОборудования);
	Если АктуальнаяВерсияЧислом>ТекущаяВерсияЧислом Тогда
		Состояние("Выгрузка новой версии компоненты оборудования...");
		ВыгрузитьКомпонентуОборудования(КаталогНазначения, Истина, ТекстОшибки);
		Если ПустаяСтрока(ТекстОшибки) Тогда
			Состояние("Выгрузка новой версии компоненты оборудования - ОК");
		Иначе
			ЗаписьЖурналаРегистрации("СтартСистемы.ОбновлениеКомпонент",
									 УровеньЖурналаРегистрации.Ошибка,Неопределено,,ТекстОшибки);
		КонецЕсли;		
		
		Если Вопрос("Обнаружена новая компонента управления оборудованием (версия "+ВерсияКомпонентыОборудования+").
					|Рекомендуется перезапустить 1С:Предприятие для обновления компоненты управления оборудованием.
					|Перед этим закройте все запущенные сессии 1С:Предприятия на данном компьютере.
					|
					|ОК - завершить работу 1С:Предприятия
					|Отмена - продолжить работу", РежимДиалогаВопрос.ОКОтмена) = КодВозвратаДиалога.ОК Тогда
			глЗавершениеРаботыСистемы=Истина;
			ЗавершитьРаботуСистемы(Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПроверитьОбновитьКомпонентуОборудования

// Проверяет наличие и содержание локального каталога системы управления оборудованием
// Параметры
//	ПроверяемыйКаталог		- Строка,	проверяемый локальный путь размещения файлов системы управления оборудованием
//	ТекстОшибки				- Строка,	возвратный параметр, текст описания ошибки, который может быть выведен пользователю
//	Режим					- Строка,	задаваемый режим проверки, доступные значения:
//										"Все" проверить и каталог и наличие компонент
//										"ТолькоКаталог" проверить только каталог и возможность его использования (право записи и т.п.)
//										"ТолькоФайлы" проверить только наличие необходимых файлов для запуска типового решения
//										"ТолькоФайлСхемы" проверить только наличие конфигурационного файла системы управления оборудованием
// Возвращаемое значение:
//   Булево   – Результат проверки каталога системы управления оборудованием, если Ложь то еще заполнен параметр ТекстОшибки
Функция ПроверкаКаталогаОборудования(Знач ПроверяемыйКаталог="", ТекстОшибки="", Знач Режим="Все") Экспорт
	ТекстОшибки = ""; // В этой переменной "накопим" все ошибки, если в конце процедуры она будет пустой, то значит все Ок
	// Проверка входных параметров
	Если ТипЗнч(ПроверяемыйКаталог)<>Тип("Строка") ИЛИ ПустаяСтрока(ПроверяемыйКаталог) Тогда
		ТекстОшибки = "Не задан каталог (путь к файлам) для проверки";
		Возврат Ложь;
	ИначеЕсли	(Найти(ПроверяемыйКаталог,"/")>0)
		ИЛИ 	(Найти(ПроверяемыйКаталог,"\\")>0) 
		ИЛИ		(Найти(ПроверяемыйКаталог,":")<>2) Тогда
		ТекстОшибки = 	"Ошибка: Неправильно задан каталог (путь к файлам) системы оборудования
						|Для размещения данных файлов можно использовать только локальный путь
						|(каталог должен располагаться на диске данной рабочей станции).";
		Возврат Ложь;
	КонецЕсли;
	ПроверяемыйКаталог=СокрЛП(ПроверяемыйКаталог)+?(Прав(СокрЛП(ПроверяемыйКаталог),1)="\","","\");
	Если ТипЗнч(Режим)<>Тип("Строка") ИЛИ ПустаяСтрока(Режим) Тогда
		Режим="Все";
	КонецЕсли; 
	// Проверяем наличие каталога	
	Если (Режим="Все") ИЛИ (Режим="ТолькоКаталог") Тогда
		ФС = Новый Файл(ПроверяемыйКаталог);
		Если НЕ ФС.Существует() Тогда
			Попытка
				СоздатьКаталог(ПроверяемыйКаталог);
			Исключение
				ТекстОшибки = "Указанный каталог """+ПроверяемыйКаталог+""" не существует и не может быть создан 
							  |из-за ошибки: " + ОписаниеОшибки();
				Возврат Ложь;
			КонецПопытки;
		ИначеЕсли НЕ ФС.ЭтоКаталог() Тогда
			ТекстОшибки = "Указанный путь """+ПроверяемыйКаталог+""" не является папкой!";
			Возврат Ложь;
		КонецЕсли;
		// проверить нужно на запись
		Попытка
			// Создадим случайное имя и запишем такой файл
			ВременноеИмя = Новый УникальныйИдентификатор();	// Чтобы не "пересекались" ни при каких вариантах 
			ВременноеИмя = "_TMP_" + СокрЛП(ИмяПользователя()) + "_" + ВременноеИмя; // Чистим по префиксу _TMP_
			ВременноеИмя = СтрЗаменить(ВременноеИмя," ","_"); // В именах пользователей бывают пробелы
			ВременноеИмя = СтрЗаменить(ВременноеИмя,"-","") + ".txt"; // Сократим немного имя
			ТекстДок = Новый ТекстовыйДокумент();
			ТекстДок.Вывод = ИспользованиеВывода.Разрешить; // на случай, если у пользователя нет права Вывод
			ТекстДок.УстановитьТекст("Проверка доступности каталога на запись - ОК");
			ТекстДок.Записать(ПроверяемыйКаталог+ВременноеИмя,КодировкаТекста.ANSI);
			ТекстДок = НЕОПРЕДЕЛЕНО;
			// Проверим создался ли наш файл
			ФС = Новый Файл(ПроверяемыйКаталог+ВременноеИмя);
			Если НЕ ФС.Существует() ИЛИ НЕ ФС.ЭтоФайл() Тогда
				ТекстОшибки = ?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС)
							+ "Не удалось создать временный файл, вероятно отсутствуют права на запись в каталог";
			КонецЕсли;
			ФС = НЕОПРЕДЕЛЕНО;
			Попытка // Теперь почистим
				УдалитьФайлы(ПроверяемыйКаталог+ВременноеИмя);
			Исключение
				// Есть права на создание и запись файлов, но нет на удаление?
				//или быть может ошибка совместного доступа (антивирус проверяет?)
				//так что вылетать с ошибкой не будем, но если кому хочется "для
				//строгости", то может раскомментировать нижеследующие строки кода
				//ТекстОшибки = ?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС)
				//			  + "Ошибка при удалении временного файла """+ПроверяемыйКаталог+ВременноеИмя+
				//""":"+Символы.ПС+ОписаниеОшибки();;
			КонецПопытки;
		Исключение
			ТекстОшибки = ?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС)
						+ "При проверке прав доступа к каталогу и работу с файлами получили ошибку:" + Символы.ПС
						+ ОписаниеОшибки()+ "
						|Возможно отсутствуют права на запись/создание/удаление файлов в этом каталоге.";
		КонецПопытки;
		// Проверка что это не каталог сервиса синхронизации
		ФС = Новый Файл(ПроверяемыйКаталог+"root.ini");
		Если ФС.Существует() Тогда
			ФС = НЕОПРЕДЕЛЕНО;
			Попытка
				УдалитьФайлы(ПроверяемыйКаталог,"root.ini");
				// Если файл удалился то мы можем тут работать
				ФС = Новый Файл(ПроверяемыйКаталог+"root.ini");
				Если ФС.Существует() Тогда
					ТекстОшибки = ?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС)
								+ "  Внимание! Указанный путь является рабочим каталогом сервиса синхронизации.
								  |Следует указать другой путь для размещения файлов системы управления оборудованием";
				КонецЕсли;
			Исключение
					ТекстОшибки = ?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС)
								+ "  Внимание! Указанный путь является рабочим каталогом сервиса синхронизации.
								  |Следует указать другой путь для размещения файлов системы управления оборудованием";
			КонецПопытки;	
		КонецЕсли;
	КонецЕсли;
	// Проверка наличия необходимых файлов
	Если (Режим="Все") ИЛИ (Режим="ТолькоФайлСхемы") Тогда
		ФС = Новый Файл(ПроверяемыйКаталог+"config.xml");
		Если НЕ ФС.Существует() ИЛИ НЕ ФС.ЭтоФайл() Тогда 
			ТекстОшибки = ?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС)
						+ "Не обнаружен конфигурационный файл системы управления оборудованием (config.xml)
						|Возможно в указанный каталог не была установлена система управления оборудованием.";
		КонецЕсли;
	КонецЕсли;
	Если (Режим="Все") ИЛИ (Режим="ТолькоКомпоненту") Тогда
		ОшибкаВыгрузки = "";
		ПроверитьОбновитьКомпонентуОборудования(ПроверяемыйКаталог, ОшибкаВыгрузки);
		ФС = Новый Файл(ПроверяемыйКаталог+ИмяКомпонентыОборудования+".dll");
		Если НЕ ФС.Существует() Тогда
			ТекстОшибки = 	?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС) 
							+ "Не удалось выгрузить компоненту управления оборудованием ("+ИмяКомпонентыОборудования+".dll)"
							+ Символы.ПС + ?(ПустаяСтрока(ОшибкаВыгрузки),"Возможно нет прав записи в каталог",ОшибкаВыгрузки)
				            + "   Без данного файла нет возможности работать оборудованием";
		КонецЕсли;
	КонецЕсли;
	Если (Режим="Все") ИЛИ (Режим="ТолькоФайлы") Тогда
		МассивФайлов = НайтиФайлы(ПроверяемыйКаталог,"*.plu",Ложь);
		Если МассивФайлов.Количество() = 0 Тогда 
			ТекстОшибки = ?(ПустаяСтрока(ТекстОшибки),"",ТекстОшибки+Символы.ПС)
						+ "Не обнаружены файлы драйверов системы управления оборудованием";
		КонецЕсли;
	КонецЕсли;
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		ТекстОшибки = "При проверке каталога системы управления оборудованием """+ПроверяемыйКаталог+""" были обнаружены ошибки:" + Символы.ПС + ТекстОшибки;
	КонецЕсли;
	// Возвращаем результат проверки
	Возврат ПустаяСтрока(ТекстОшибки);
КонецФункции // ПроверкаКаталогаОборудования

// Подключает внешнюю компоненту (или вызывает мастер настройки) и запускает систему оборудования
//Возвращает Истина - все ОК, Ложь - что-то не в порядке.
Функция ИнициализацияФункцийВнешнихКомпонент() Экспорт
	// Начальная инициализация 
	Состояние("Проверка настроек работы внешних компонент...");
	
	Попытка // на случай если параметр еще не был инициализирован
		СтрИсхРежимРаботы = СокрЛП(ПараметрыСеанса.РежимРаботы);
	Исключение		
		СтрИсхРежимРаботы = "";
	КонецПопытки;
	РежимРаботыСимвл1 = Сред(СтрИсхРежимРаботы, 1, 1);
	РежимРаботыСимвл2 = Сред(СтрИсхРежимРаботы, 2, 1);
	КодСимвлола1 = КодСимвола(РежимРаботыСимвл1);
	КодСимвлола2 = КодСимвола(РежимРаботыСимвл2);
	Если (КодСимвлола1 >= 48) И (КодСимвлола1 <= 49) И (КодСимвлола2 >= 48) И (КодСимвлола2 <= 50) Тогда
		// Режим работы с оборудованием уже был предустановлен, но мы его сейчас все равно заменим на свой
		СтрИсхРежимРаботы = Сред(СтрИсхРежимРаботы, 3); // только сохраним остальной "хвост" строки режимов
	КонецЕсли;
	
	// Получим флаги режима работы с оборудованием для текущей сессии
	КомпьютерНеВключатьОборудование = XMLСтрока(ПараметрыСеанса.Компьютер.НеВключатьОборудованиеПриВходе);
	ПользовательРежимИспОборудования = XMLСтрока(ПараметрыСеанса.Пользователь.РежимИспользованияОборудования);
	// Установим новый режим оборудования
	СтрокаНовогоРежимаОборудования = КомпьютерНеВключатьОборудование + ПользовательРежимИспОборудования;
	ПараметрыСеанса.РежимРаботы = СтрокаНовогоРежимаОборудования + "0" + СтрИсхРежимРаботы;
	
	// Проверим каталог системы управления оборудованием
	ТекстСообщения = "";
	ПроверкаКаталогаОборудованияВыполнена=Ложь;
	КаталогОборудования = ПараметрыСеанса.Компьютер.ЛокальныйКаталог;
	Если ПустаяСтрока(КаталогОборудования) Тогда
		КаталогОборудованияПоУмолчанию=уоПолучитьПутьККаталогуОборудования();
		Если ПроверкаКаталогаОборудования(КаталогОборудованияПоУмолчанию,ТекстСообщения) Тогда
			ПроверкаКаталогаОборудованияВыполнена=Истина;
			КаталогОборудования=КаталогОборудованияПоУмолчанию;
			ТекКомпьютер=ПараметрыСеанса.Компьютер.ПолучитьОбъект();
			ТекКомпьютер.ЛокальныйКаталог=КаталогОборудования;
			ТекКомпьютер.ОбменДанными.Загрузка=Истина;
			Попытка
				ТекКомпьютер.Записать();
			Исключение
			КонецПопытки; 
		КонецЕсли; 
	КонецЕсли;
	РаботаСОборудованиемЗапрещена = "[Работа с оборудованием запрещена]";
	
	ФормаМастер = ПолучитьФорму();
	СистемаУправленияОборудованиемПроверена = Ложь;
	
	// Цикл попыток загрузить внешнюю компоненту типового решения
	Пока НЕ СистемаУправленияОборудованиемПроверена Цикл
		Если КаталогОборудования=РаботаСОборудованиемЗапрещена Тогда
			ПараметрыСеанса.РежимРаботы = "120" + СтрИсхРежимРаботы; // не включать автоматически, оборудование запрещено, вторичная сессия
			Прервать;
		КонецЕсли; 
		// "Бесконечный" цикл. Выход успешная загрузка или отказ пользователя продолжать
		Если НЕ ПроверкаКаталогаОборудованияВыполнена Тогда
			Если ПустаяСтрока(КаталогОборудования) ИЛИ НЕ ПроверкаКаталогаОборудования(КаталогОборудования, ТекстСообщения) Тогда
				
				Если ПустаяСтрока(КаталогОборудования) И ПустаяСтрока(ТекстСообщения) Тогда
					
					ТекстСообщения 	= 	"  Внимание! Обнаружен первый запуск программы на данном рабочем месте.
										|Необходимо запустить мастер настройки и указать каталог куда была установлена система управления оборудованием из дистрибутива поставки.";
				КонецЕсли;			
									
				// Сначала спросим пользователя хочет ли он попробовать все исправить, а то может устал уже?
				ОтветПользователя=Вопрос(ТекстСообщения+"
										|
										|   ЗАПУСТИТЬ МАСТЕР НАСТРОЙКИ СИСТЕМЫ УПРАВЛЕНИЯ ОБОРУДОВАНИЕМ?
										|
										|Нажатие на кнопку ""Да"" откроет форму мастера настройки.
										|Нажатие на кнопку ""Нет"" приведет к НЕМЕДЛЕННОМУ ЗАВЕРШЕНИЮ РАБОТЫ
										|Нажатие на кнопку ""Отмена"" позволит продолжить запуск программы,
										|но заблокирует использование любых обращений к оборудованию",
										РежимДиалогаВопрос.ДаНетОтмена,60,КодВозвратаДиалога.Нет);
										
				Если ОтветПользователя=КодВозвратаДиалога.Отмена Тогда
					// Выбрал плохой, не рекомендуемый вариант, ну пусть сам плачет
					ПараметрыСеанса.РежимРаботы = "120" + СтрИсхРежимРаботы; // не включать автоматически, оборудование запрещено, вторичная сессия
					ТекКомпьютер=ПараметрыСеанса.Компьютер.ПолучитьОбъект();
					ТекКомпьютер.ЛокальныйКаталог=РаботаСОборудованиемЗапрещена;
					ТекКомпьютер.ОбменДанными.Загрузка=Истина;
					Попытка
						ТекКомпьютер.Записать();
					Исключение
					КонецПопытки; 
					Прервать; // прервем внутренний цикл
				КонецЕсли;
				
				Если (ОтветПользователя=КодВозвратаДиалога.Нет) ИЛИ (ОтветПользователя=КодВозвратаДиалога.Таймаут) Тогда
					// Будем завершать работу программы
					ЗавершитьРаботуСистемы(Ложь);
					Возврат Ложь;
				КонецЕсли;
				
				// Здесь если пользователь выбрал запускать форму мастера настройки
				Если НЕ ПустаяСтрока(КаталогОборудования) Тогда
					ФормаМастер.ПутьДоКомпоненты = КаталогОборудования;
				КонецЕсли;
				РезультатРаботыМастера = ФормаМастер.ОткрытьМодально(); // если пользователь указал каталог, то вернет Истина
				Если ТипЗнч(РезультатРаботыМастера) = Тип("Булево") Тогда
					Если РезультатРаботыМастера Тогда
						КаталогОборудования = ФормаМастер.ПутьДоКомпоненты;
					КонецЕсли;
				Иначе
					// Работает инсталлятор из дистрибутива поставки, не должно быть запущенных копий 1С Предприятия в памяти системы
					ЗавершитьРаботуСистемы(Ложь);
					Возврат Ложь;
				КонецЕсли;
				Продолжить; 
			КонецЕсли;	
		КонецЕсли;
		ПроверкаКаталогаОборудованияВыполнена=Ложь;

		// Создаем обработку управляющую всем оборудованием
		ОбработкаЗащиты=Обработки.Защита.Создать();
		Если глРарус_Компонента=Неопределено Тогда
			РезультатИнициализацииОборудования = ОбработкаЗащиты.ЗагрузитьКомпонентуОборудования(КаталогОборудования);
			Если РезультатИнициализацииОборудования <> "OK" Тогда
				ТекстСообщения = РезультатИнициализацииОборудования;
				ОтветПользователя=Вопрос("Ошибка при загрузке компоненты управления оборудованием: "+ТекстСообщения+"
										|
										|   ПРОДОЛЖИТЬ БЕЗ ВОЗМОЖНОСТИ ИСПОЛЬЗОВАНИЯ ОБОРУДОВАНИЯ?
										|
										|Нажатие на кнопку ""Да"" продолжение работы без использования оборудования.
										|Нажатие на кнопку ""Нет"" приведет к немедленному завершению работы.",
										РежимДиалогаВопрос.ДаНет,60,КодВозвратаДиалога.Нет);
				Если ОтветПользователя=КодВозвратаДиалога.Да Тогда
					ПараметрыСеанса.РежимРаботы = "120" + СтрИсхРежимРаботы; // не включать автоматически, оборудование запрещено, вторичная сессия
					Прервать;
					//Продолжить; // Идем на следующий цикл проверки попытки загрузки
				Иначе
					ЗавершитьРаботуСистемы(Ложь);
					Возврат Ложь;
				КонецЕсли; 						
			КонецЕсли;
		КонецЕсли; 
		
		ЗапросЛицензииОборудования=глРарус_Компонента.ПолучитьЗапросНаЛицензиюУправленияОборудованием();
		ЛицензияОборудования=зфЗащищенныеФункцииСервер.ПолучитьЛицензиюНаОборудование(ЗапросЛицензииОборудования);
		ИмяКомпоненты=ОбработкаЗащиты.ИмяКомпоненты + ".dll";
		НаименованиеПродукта=Метаданные.Синоним;
		КодОшибкиИнициализацииОборудования=0;
		Попытка
			КодОшибкиИнициализацииОборудования=глРарус_Компонента.ЗапуститьУправлениеОборудованием(ИмяКомпоненты, НаименованиеПродукта, ЛицензияОборудования, КаталогОборудования);
		Исключение
			КодОшибкиИнициализацииОборудования=20000; // код ошибки не определен
			ТекстОшибки="Ошибка инициализации системы уравления оборудованием (Код=20000) ";
			Попытка
				ТекстОшибки=ТекстОшибки + глРарус_Компонента.ОписаниеОшибки;
			Исключение
			КонецПопытки;
		КонецПопытки;
		
		Если (КодОшибкиИнициализацииОборудования = 0) Тогда
			
			ФлагОсновнойСессии = "0";
			Попытка
				Если (глРарус_Компонента.ЭтоОсновнаяСессия() = 1) Тогда
					ФлагОсновнойСессии = "1";
				КонецЕсли;
			Исключение
			КонецПопытки;
			
			// Установим третий флаг режима работы с оборудованием (в главной сессии можно создавать оборудование)
			ПараметрыСеанса.РежимРаботы = СтрокаНовогоРежимаОборудования + ФлагОсновнойСессии + СтрИсхРежимРаботы;
			
			СистемаУправленияОборудованиемПроверена = Истина;
			// Проверим сохранен ли этот каталог в настройках рабочей стенции
			Если ВРЕГ(СокрЛП(ПараметрыСеанса.Компьютер.ЛокальныйКаталог)) <> ВРЕГ(КаталогОборудования) Тогда
				Попытка // Проверка прошла можно сразу попытаться запомнить этот путь для следующей попытки
					КомпьютерОбъект=ПараметрыСеанса.Компьютер.ПолучитьОбъект();
					КомпьютерОбъект.ЛокальныйКаталог=КаталогОборудования;
					КомпьютерОбъект.Записать();
				Исключение
				КонецПопытки;
			КонецЕсли;
		Иначе
			// Запретим работу с оборудованием в текущем сеансе пользователя
			ПараметрыСеанса.РежимРаботы = "120" + СтрИсхРежимРаботы; // не включать автоматически, оборудование запрещено, вторичная сессия
			ТекстОшибки="Ошибка при инициализации системы уравления оборудованием Код=" + КодОшибкиИнициализацииОборудования + " - ";
			Попытка
				ТекстОшибки=ТекстОшибки + глРарус_Компонента.ОписаниеОшибки;
			Исключение
			КонецПопытки;
			ТекстСообщения=	ТекстОшибки +Символы.ПС +
							"	Из-за возникших ошибок система управления оборудованием не была подключена." + Символы.ПС +
							"Рекомендуется исправить возникшие проблемы перед следующей попыткой запуска программы." + Символы.ПС +
							"Возможно Вам следует переустановить данную подсистему из дистрибутива поставки продукта.";
			Сообщить(ТекстОшибки, СтатусСообщения.Внимание);
		КонецЕсли;
		
	КонецЦикла; // попыток загрузить внешнюю компоненту типового решения

	глТорговоеОборудование = Обработки.ТорговоеОборудование.Создать();
	Если НЕ глТорговоеОборудование.РаботаСОборудованиемЗапрещена() Тогда
		// Если работа с оборудованием не запрещена, то нужно его инициализировать
		Состояние("Инициализация торгового оборудования...");
		Если НЕ глТорговоеОборудование.Инициализация() Тогда
			Возврат Ложь; // не получилось
		КонецЕсли;
		// Если не стоит запрет автоподключения оборудования, то нужно включить все что нам может потребоваться
		Если глТорговоеОборудование.РежимАвтоподключенияОборудования() Тогда
			Состояние("Запуск используемого оборудования...");
			Попытка
				глТорговоеОборудование.ВключитьТорговоеОборудованиеИспользуемое();
			Исключение
				Предупреждение("Ошибка при включении оборудования, используемого данным рабочим местом <"+СокрЛП(ПараметрыСеанса.Компьютер)+">: "+Символы.ПС+ОписаниеОшибки(),30,"Инициализация оборудования:");
			КонецПопытки; 
		КонецЕсли;
		// Сканер прописанный на нашем рабочем месте включаем и блокируем на себя всегда
		// (кроме режима полного запрета работы с оборудованием)
		Сканер=ПараметрыСеанса.Компьютер.Сканер;
		Если Не обЗначениеНеЗаполнено(Сканер) Тогда
			Состояние("Подключение сканера...");
			Статус=глТорговоеОборудование.СтатусОборудования(Сканер);
			Если Статус<>Перечисления.СтатусыОборудования.Готово Тогда
				// Тогда его нужно обязательно включить и заблокировать на себя
				ТекстОшибки="";
				глТорговоеОборудование.ВключитьОборудование(Сканер,ТекстОшибки);
				Если ТекстОшибки<>"ОК" Тогда
					Сообщить(Сканер.Наименование + " - " + ТекстОшибки, СтатусСообщения.Важное);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; // указан сканер в настройках рабочего места
	КонецЕсли; // разрешено использование оборудования
	
	// Сюда дойдем если инициализация прошла успешно
	Состояние("Инициализация системы завершена успешно.");
	Возврат Истина;
	
КонецФункции // ИнициализацияФункцийВнешнихКомпонент

// Выполняет проверки обновления базы, связанные с переходом на новую систему обменов
//
Процедура ЗавершитьОбновлениеБазыДочернегоУзла(Знач ПараметрЗапуска) Экспорт
	
	// Отключим проверки и сообщения на время обновления
	Если НЕ ЗначениеЗаполнено(глПрава) Тогда // Если еще не инициализированы
		Состояние("Получение прав и настроек текущего пользователя...");
		глПрава = обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
	КонецЕсли;
	// Отключим на время обновления
	ПВХ = ПланыВидовХарактеристик.ПраваИНастройки;
	глПрава.Вставить(ПВХ.ВыводитьСообщенияПроверкиПравДоступа,Ложь);
	глПрава.Вставить(ПВХ.ПроверкаДоступаКСправочникамИДокументам,Ложь);
	глПрава.Вставить(ПВХ.ПроверкаЗаполненияСправочниковИДокументов,Ложь);
	глПрава.Вставить(ПВХ.ВыводитьСообщениеОбОшибкахВИнформационноеПоле,Ложь);
	
	НастройкаДочернегоУзла = Неопределено;
	ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	// в дочерней базе на этот момент пока нет ни самой ИБ, ни ее настроек
	Если ЭтотУзел.ИнформационнаяБаза.Пустая() Тогда
		// поэтому временно подставим созданную при переносе настроек - одну единственную
		// настройку главного узла (на всякий случай именно пытаемся сделать перенос)
		одОбменДанными.ПеренестиНастройкиДоставкиСообщений(НастройкаДочернегоУзла);
	КонецЕсли; 

	// поищем недочитанное сообщение обмена
	ЕстьФайл = Ложь; // изначально считаем что его нет
	ОтправкаОтветногоСообщения = Ложь;
	
	ИмяФайлаСообщения = "";
	ПозицияПараметра = Найти(ВРЕГ(ПараметрЗапуска), Врег("UpdateIB"));
	Если ПозицияПараметра > 0  Тогда //Запуск процедуры обмена
		
		ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Получена команда на проведение обмена: "+ПараметрЗапуска);
		ЕстьФайл = ВыделитьИмяФайлаСообщения(ПараметрЗапуска,ИмяФайлаСообщения);
		Если ЕстьФайл И НЕ ЗначениеЗаполнено(НастройкаДочернегоУзла) Тогда
			
			ИмяКаталога = ""; ИмяФайла = "";
			одОбменДанными.ПолучитьКаталогИИмяФайла(ИмяФайлаСообщения, ИмяКаталога, ИмяФайла);
			Если НЕ ПустаяСтрока(ИмяКаталога) Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	НастройкиОбмена.Ссылка
				|ИЗ
				|	Справочник.НастройкиДоставкиСообщений КАК НастройкиОбмена
				|ГДЕ
				|	ПОДСТРОКА(НастройкиОбмена.КаталогОбменов, 1, 1000) = &ИмяКаталога";
				
				Запрос.УстановитьПараметр("ИмяКаталога", ИмяКаталога + "\");
				РезультатЗапроса = Запрос.Выполнить();
				Если НЕ РезультатЗапроса.Пустой() Тогда
					Выборка = РезультатЗапроса.Выбрать();
					Выборка.Следующий();
					НастройкаДочернегоУзла = Выборка.Ссылка;
				КонецЕсли; 
				
			КонецЕсли; 
			
		КонецЕсли; 
		
	Иначе
		
		ПозицияПараметра = Найти(ВРЕГ(ПараметрЗапуска), Врег("SendExchangeMessage"));
		Если ПозицияПараметра > 0 Тогда
			
			НастройкаДочернегоУзла = ВыделитьНастройкуОбмена(ПараметрЗапуска);
			ЕстьФайл = Истина;
			ОтправкаОтветногоСообщения = Истина;

		КонецЕсли; 
		
	КонецЕсли;	
		
		
	Если НЕ ЕстьФайл ИЛИ НЕ ОтправкаОтветногоСообщения Тогда
		
		ГлавныйУзел = ПланыОбмена.ГлавныйУзел();
		
		Если ЗначениеЗаполнено(ГлавныйУзел) Тогда
			
			// Вариант 1: явно имя файла не передано, однако его можно получить из ПараметровОбмена
			// если и там будет пусто тогда попробуем получить косвенным путем
			НайденаНастройкаДоставки = Ложь;
			ДанныеОбмена = одОбменДанными.ПолучитьДанныеПоследнегоОбмена(ГлавныйУзел);			
			ИмяФайлаСообщения = ДанныеОбмена.ИмяФайлаПоследнейЗагрузки;
			Если НЕ ПустаяСтрока(ИмяФайлаСообщения) Тогда
				// проверим существование файла - может обмен давно уже не производили
				// и данные устарели
				Если НЕ ПустаяСтрока(ИмяФайлаСообщения) Тогда
					ФайлСообщения = Новый Файл(ИмяФайлаСообщения);
					Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
						ЕстьФайл = Ложь;
					Иначе
						ЕстьФайл = Истина;
						НайденаНастройкаДоставки = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;	
			
			Если НЕ ЕстьФайл Тогда
				// Вариант 2: попробуем взять настройку доставки из реглам. задания
				ЗаданиеОбмен = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ОбменСУдаленнымиПодразделениями);
				Если ЗаданиеОбмен <> Неопределено Тогда
				   Если ЗаданиеОбмен.Параметры.Количество() > 0 Тогда
				        Параметр1 = ЗаданиеОбмен.Параметры[0];
						Если ТипЗнч(Параметр1) = Тип("Структура") Тогда
							СписокУзлыОбмена = неопределено;
							Если Параметр1.Свойство("ТаблицаУзлов",СписокУзлыОбмена) Тогда
								Если ТипЗнч(Параметр1.ТаблицаУзлов) = Тип("ТаблицаЗначений") Тогда
									СписокУзлыОбмена = Параметр1.ТаблицаУзлов;
									СтруктураПоиска = Новый Структура("Узел,Прием",ГлавныйУзел,Истина);
									НайденыСтроки = СписокУзлыОбмена.НайтиСтроки(СтруктураПоиска);
									Если НайденыСтроки.Количество() > 0 Тогда
										ТекСтрока = НайденыСтроки[0];
										Если ЗначениеЗаполнено(ТекСтрока.НастройкаДоставки) Тогда
											НастройкаДочернегоУзла = ТекСтрока.НастройкаДоставки;
											НайденаНастройкаДоставки = Истина;
										КонецЕсли; 
									КонецЕсли; 
								КонецЕсли; 
							КонецЕсли;
						КонецЕсли;	
				   КонецЕсли; 
				КонецЕсли; 
								
				// Вариант 3: если не удалось из регламентного - пробуем из сохраненных настроек ручного обмена
				Если НЕ НайденаНастройкаДоставки Тогда
					СтруктураУзелИНастройка = ВосстановитьЗначение("ОбменУзелИНастройка_" + ГлавныйУзел.Префикс);
					Если СтруктураУзелИНастройка <> Неопределено Тогда
						Если ТипЗнч(СтруктураУзелИНастройка) = Тип("Структура") Тогда
							ТекУзелОбмена  = Неопределено;
							СтруктураУзелИНастройка.Свойство("УзелОбмена",ТекУзелОбмена);
							Если ТекУзелОбмена <> Неопределено Тогда
								УзелОбмена =  ТекУзелОбмена;
							КонецЕсли;
							Если ТекУзелОбмена = ГлавныйУзел Тогда
								ТекНастройкаОбмена  = Неопределено;
								СтруктураУзелИНастройка.Свойство("НастройкаОбмена",ТекНастройкаОбмена);
								Если ТекНастройкаОбмена <> Неопределено Тогда
									НастройкаДочернегоУзла =  ТекНастройкаОбмена;
									НайденаНастройкаДоставки = Истина;
								КонецЕсли;
							КонецЕсли; 
						КонецЕсли;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли; // Если НЕ ЕстьФайл
			
		КонецЕсли; 
	КонецЕсли;

	Если ОтправкаОтветногоСообщения Тогда
		ОтветноеСообщениеОтправлено = ОтправитьОтветноеСообщение(НастройкаДочернегоУзла);
	Иначе
		СообщениеДочитано = ДочитатьСообщениеОбмена(?(ЕстьФайл,ИмяФайлаСообщения,""),НастройкаДочернегоУзла);
	КонецЕсли; 
	
КонецПроцедуры // ЗавершитьОбновлениеБазыДочернегоУзла()
 
// Производит выделение полного имени файла сообщения обмена
// в переданном параметре запуска
// 
// Параметры
// 	ПараметрЗапуска - Строка. Текущий параметр запуска
//	ИмяФайлаСообщения - Строка. Полученное имя файла сообщения
//
// Возвращаемое значение:
//  ЕстьФайл - Булево. Флаг существования файла сообщения
//
Функция ВыделитьИмяФайлаСообщения(ПараметрЗапуска,ИмяФайлаСообщения = "") Экспорт
	
	// Если это обмен подчиненного узла с главным, через параметр сразу передаем имя готового файла для чтения
	// (при возобновлении обмена после обновления конфигурации)
	МассивПодстрок = обРазложитьСтрокуВМассивПодстрок(ПараметрЗапуска, "$");
	Если МассивПодстрок.Количество() >= 2 Тогда
		Если (Врег(Прав(МассивПодстрок[0],9)) = Врег("UpdateIB=")) Тогда
			ИмяФайлаСообщения = ВРЕГ(МассивПодстрок[1]);
		КонецЕсли;
	КонецЕсли;
	
	// А теперь убедимся что нам не подсунули чего попало
	ТестФайл = Новый Файл(ИмяФайлаСообщения);
	Если ТестФайл.Существует() И ТестФайл.ЭтоФайл() И (ВРЕГ(ТестФайл.Расширение) = ".XML") И (ТестФайл.Размер() > 600) Тогда
		СтрОшибок = "Получен файл сообщения <" + ИмяФайлаСообщения + ">";
		ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , СтрОшибок);
		ЕстьФайл = Истина;
	Иначе
		СтрОшибок = "Не найден файл сообщения <" + ИмяФайлаСообщения + ">";
		ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Предупреждение, , , СтрОшибок);
		ЕстьФайл = Ложь;
	КонецЕсли;
	
	Возврат ЕстьФайл;
	
КонецФункции

// Производит выделение настройки обмена по ее коду
// в переданном параметре запуска
// 
// Параметры
// 	ПараметрЗапуска - Строка. Текущий параметр запуска
//
// Возвращаемое значение:
//  Справочник.НастройкиДоставкиСообщений. 
//
Функция ВыделитьНастройкуОбмена(ПараметрЗапуска) Экспорт
	
	// определим настройку доставки (если указана)
	КодНастройкиДоставки = фзФоновыеЗадания.ВыделитьКодНастройкиДоставки(ПараметрЗапуска);
	Если КодНастройкиДоставки = "" Тогда
		ТекНастройкаДоставки = Неопределено;
	Иначе
		// исходим из того, что это код элемента справочника НастройкиДоставки
		ТекНастройкаДоставки = Справочники.НастройкиДоставкиСообщений.НайтиПоКоду(КодНастройкиДоставки);
		// не нашли - выдадим ошибку, не берем настройку по умолчанию, пользователь должен знать
	КонецЕсли;
	
	Возврат ТекНастройкаДоставки;
	
КонецФункции

// Производит дочитывание файла сообщения обмена после выполнения обновления ИБ
// подчиненного узла (считали изменения конфигурации - завершение работы 
// - обновление БД - запуск с продолжением чтения того же файла
//
// Параметры
//  ИмяФайлаСообщения - Строка. Полное имя файла сообщения
//  НастройкаОбмена - СправочникСсылка.НастройкиДоставкиСообщений. Полученная настройка доставки
//
Функция ДочитатьСообщениеОбмена(ИмяФайлаСообщения,НастройкаОбмена = Неопределено) Экспорт
	
	ОбработкаОбмен = Обработки.ОбменСУдаленнымиПодразделениями.Создать();
	ОбработкаОбмен.УзелОбмена = ПланыОбмена.ГлавныйУзел().Ссылка;
	
	Если НЕ ЗначениеЗаполнено(НастройкаОбмена) Тогда
		НастройкаОбмена = Справочники.НастройкиДоставкиСообщений.ЛокальнаяСеть;
		ЭтотУзелИБ = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Ссылка.ИнформационнаяБаза;
		Если НЕ ЭтотУзелИБ.НастройкаДоставкиПоУмолчанию.Пустая() Тогда
			НастройкаОбмена = ЭтотУзелИБ.НастройкаДоставкиПоУмолчанию;
		КонецЕсли;
	КонецЕсли; 
	
	ОбработкаОбмен.НастройкаОбмена 			= НастройкаОбмена; // для главного узла настройки может и не быть еще
	ОбработкаОбмен.Комментировать 			= Истина;
	ОбработкаОбмен.КомментироватьОбъекты 	= Истина;
	ОбработкаОбмен.ДопроводитьПоПартиям 	= Истина;
	ОбработкаОбмен.ВестиЖурналРегистрации 	= Истина;
	ОбработкаОбмен.ВестиЛогФайл 			= Истина;
		
	КорневойКаталог = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(КорневойКаталог) Тогда
		ТекКаталогОбменов = НастройкаОбмена.КаталогОбменов;
	Иначе
		ТекКаталогОбменов = КорневойКаталог;
	КонецЕсли;
	Если ПустаяСтрока(ТекКаталогОбменов) Тогда
		ОбработкаОбмен.КаталогЛогов = КаталогВременныхФайлов();
	Иначе
		ОбработкаОбмен.КаталогЛогов = ТекКаталогОбменов + "LOGs\";
	КонецЕсли;
	ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Для дочитывания сообщения обмена "+ИмяФайлаСообщения 
		+ " использована настройка доставки " + СокрЛП(НастройкаОбмена.Наименование) + "(" + СокрЛП(НастройкаОбмена.Код) + ")"
        + ", каталог лог-файла = " + ОбработкаОбмен.КаталогЛогов);
	
	СтрОшибки = "";  ДочитываниеФайлаОбмена = истина;
	ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Запуск повторной загрузки сообщения обмена"+ИмяФайлаСообщения);
	#Если Клиент Тогда
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		Сообщить(ТекДата + "Запуск повторной загрузки сообщения обмена " + ИмяФайлаСообщения, СтатусСообщения.Информация);
	#КонецЕсли
	Результат = ОбработкаОбмен.ВыполнитьЗагрузку(,, СтрОшибки,ИмяФайлаСообщения,ДочитываниеФайлаОбмена);
	
	Возврат  Результат;
	
КонецФункции

// Функция отпраляет
Функция ОтправитьОтветноеСообщение(НастройкаОбмена = Неопределено)
	
	ОбработкаОбмен = Обработки.ОбменСУдаленнымиПодразделениями.Создать();
	ОбработкаОбмен.УзелОбмена = ПланыОбмена.ГлавныйУзел().Ссылка;
	
	Если НЕ ЗначениеЗаполнено(НастройкаОбмена) Тогда
		НастройкаОбмена = Справочники.НастройкиДоставкиСообщений.ЛокальнаяСеть;
		ЭтотУзелИБ = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел().Ссылка.ИнформационнаяБаза;
		Если НЕ ЭтотУзелИБ.НастройкаДоставкиПоУмолчанию.Пустая() Тогда
			НастройкаОбмена = ЭтотУзелИБ.НастройкаДоставкиПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	ОбработкаОбмен.НастройкаОбмена = НастройкаОбмена; // для главного узла настройки может и не быть еще
	ОбработкаОбмен.Комментировать = Истина;
	ОбработкаОбмен.КомментироватьОбъекты = Истина;
	ОбработкаОбмен.ДопроводитьПоПартиям = Истина;
	ОбработкаОбмен.ВестиЖурналРегистрации 	= Истина;
	ОбработкаОбмен.ВестиЛогФайл 			= Истина;
		
	КорневойКаталог = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(КорневойКаталог) Тогда
		ТекКаталогОбменов = НастройкаОбмена.КаталогОбменов;
	Иначе
		ТекКаталогОбменов = КорневойКаталог;
	КонецЕсли;
	Если ПустаяСтрока(ТекКаталогОбменов) Тогда
		ОбработкаОбмен.КаталогЛогов = КаталогВременныхФайлов();
	Иначе
		ОбработкаОбмен.КаталогЛогов = ТекКаталогОбменов + "LOGs\";
	КонецЕсли;

	ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Для отправка ответного сообщения обмена " 
		+ " использована настройка доставки " + СокрЛП(НастройкаОбмена.Наименование) + "(" + СокрЛП(НастройкаОбмена.Код) + ")"
		+ ", каталог лог-файла = " + ОбработкаОбмен.КаталогЛогов);
	
	ЗаписьЖурналаРегистрации("Обновление", УровеньЖурналаРегистрации.Информация, , , "Запуск ответной выгрузки сообщения обмена");
	#Если Клиент Тогда
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		Сообщить(ТекДата + "Запуск ответной выгрузки сообщения обмена",СтатусСообщения.Информация);
	#КонецЕсли
	Результат = ОбработкаОбмен.ВыполнитьВыгрузку(,НастройкаОбмена);

	Возврат Результат;
	
КонецФункции

ИмяКомпонентыОборудования=Метаданные.Обработки.СтартСистемы.Макеты.КомпонентаОборудования.Синоним;
ВерсияКомпонентыОборудования=Метаданные.Обработки.СтартСистемы.Макеты.КомпонентаОборудования.Комментарий;

#КонецЕсли
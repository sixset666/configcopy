#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Функция ПолучитьТекстЗапросаОсновныхТаблиц(ШаблонЦена, ТекстСоединенийПроцентыСкидкиНаценки)
	РезультатТекстЗапроса = "";
	
	ДатаКонцаАнализа = ?(НЕ ЗначениеЗаполнено(ДатаКонцаАнализа), ТекущаяДата(), ДатаКонцаАнализа);
	Если УчитыватьОтсутствиеНаСкладе Тогда
		РезультатТекстЗапроса = "
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			НАЧАЛОПЕРИОДА(ОстаткиТоваровКомпании.Период, ДЕНЬ)
		|		ИНАЧЕ
		|			КОНЕЦПЕРИОДА(ОстаткиТоваровКомпании.Период, ДЕНЬ)
		|	КОНЕЦ КАК Период,
		|	СУММА(ВЫБОР
		|			КОГДА ОстаткиТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ОстаткиТоваровКомпании.Количество
		|			ИНАЧЕ -ОстаткиТоваровКомпании.Количество
		|		КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ
		|	ТаблицаОборотовТовара
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
		|ГДЕ
		|	ОстаткиТоваровКомпании.Период МЕЖДУ &ДатаНачалаАнализа И &ДатаКонцаАнализа
		|{ГДЕ
		|	ОстаткиТоваровКомпании.СкладКомпании.Подразделение.Организация.* КАК Организация,
		|	ОстаткиТоваровКомпании.СкладКомпании.Подразделение.* КАК ПодразделениеКомпании,
		|	ОстаткиТоваровКомпании.СкладКомпании.* КАК СкладКомпании}
		|	
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиТоваровКомпании.Номенклатура,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			НАЧАЛОПЕРИОДА(ОстаткиТоваровКомпании.Период, ДЕНЬ)
		|		ИНАЧЕ
		|			КОНЕЦПЕРИОДА(ОстаткиТоваровКомпании.Период, ДЕНЬ)
		|	КОНЕЦ
		|;
		|
		|ВЫБРАТЬ
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПродажиОбороты.КоличествоОборот КАК КоличествоПродажи,
		|	ПродажиОбороты.СуммаОборот КАК СуммаПродажи,
		|	ТаблицаОборотовТовара.Период КАК Период,
		|	ТаблицаОборотовТовара.Количество КАК ОборотЗаДень,
		|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК Остаток
		|ПОМЕСТИТЬ
		|	ТаблицаОборотов
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&ДатаНачалаАнализа, &ДатаКонцаАнализа, , 
		|													Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)
		|										{
		|										ПодразделениеКомпании.Организация.* КАК Организация,
		|										ПодразделениеКомпании.* КАК ПодразделениеКомпании,
		|										СкладКомпании.* КАК СкладКомпании}
		|										) КАК ПродажиОбороты
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаФормированияЗаказа,
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)
		|						{СкладКомпании.* КАК СкладКомпании}) КАК ОстаткиТоваровКомпанииОстатки
		|ПО
		|	ПродажиОбороты.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаОборотовТовара КАК ТаблицаОборотовТовара
		|ПО
		|	ПродажиОбороты.Номенклатура = ТаблицаОборотовТовара.Номенклатура
		|ГДЕ
		|	ПродажиОбороты.КоличествоОборот > 0
		|;
		|
		|ВЫБРАТЬ
		|	ИтоговаяТаблица.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ИтоговаяТаблица.КоличествоПродажи) КАК КоличествоПродажи,
		|	МАКСИМУМ(ИтоговаяТаблица.СуммаПродажи) КАК СуммаПродажи,
		|	СУММА(ИтоговаяТаблица.ДлинаПериодаАнализа)+1 КАК ДлинаПериодаАнализа,
		|	МАКСИМУМ(ИтоговаяТаблица.Остаток) КАК Остаток,
		|	МАКСИМУМ(ИтоговаяТаблица.ЦенаПоставщика) КАК ЦенаПоставщика,
		|	МАКСИМУМ(ИтоговаяТаблица.МинимальнаяЦена) КАК МинимальнаяЦена,
		|	МАКСИМУМ(ИтоговаяТаблица.ДатаПоступления) КАК ДатаПоступления,
		|	СУММА(ИтоговаяТаблица.Заказано) КАК Заказано
		|ПОМЕСТИТЬ
		|	ИтоговаяТаблица
		|ИЗ (
		|ВЫБРАТЬ
		|	ОбъединенныйЗапрос.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ОбъединенныйЗапрос.КоличествоПродажи) КАК КоличествоПродажи,
		|	МАКСИМУМ(ОбъединенныйЗапрос.СуммаПродажи) КАК СуммаПродажи,
		|	РАЗНОСТЬДАТ(ОбъединенныйЗапрос.Период, МИНИМУМ(ОбъединенныйЗапрос.БлижайшаяДата), ДЕНЬ) КАК ДлинаПериодаАнализа,
		|	МАКСИМУМ(ОбъединенныйЗапрос.Остаток) КАК Остаток,
		|	0 КАК ЦенаПоставщика,
		|	0 КАК МинимальнаяЦена,
		|	ДатаВремя(1,1,1) КАК ДатаПоступления,
		|	0 КАК Заказано
		|ИЗ (
		|	ВЫБРАТЬ
		|		ТаблицаПродажи.Номенклатура КАК Номенклатура,
		|		ТаблицаПродажи.КоличествоПродажи КАК КоличествоПродажи,
		|		ТаблицаПродажи.СуммаПродажи КАК СуммаПродажи,
		|		ТаблицаПродажи.Период КАК Период,
		|		НАЧАЛОПЕРИОДА(&ДатаКонцаАнализа, ДЕНЬ) КАК БлижайшаяДата,
		|		ТаблицаПродажи.Остаток КАК ОстатокНаДень,
		|		ТаблицаПродажи.Остаток КАК Остаток
		|	ИЗ
		|		ТаблицаОборотов КАК ТаблицаПродажи
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПродажи.Номенклатура,
		|		0,
		|		0,
		|		ТаблицаПродажи.Период,
		|		ТаблицаОборотов.Период,
		|		- ТаблицаОборотов.ОборотЗаДень,
		|		0
		|	ИЗ
		|		ТаблицаОборотов КАК ТаблицаПродажи
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаОборотов КАК ТаблицаОборотов
		|	ПО
		|		ТаблицаПродажи.Номенклатура = ТаблицаОборотов.Номенклатура И 
		|		ТаблицаПродажи.Период < ТаблицаОборотов.Период
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаПродажи.Номенклатура,
		|		МАКСИМУМ(ТаблицаПродажи.КоличествоПродажи),
		|		МАКСИМУМ(ТаблицаПродажи.СуммаПродажи),
		|		НАЧАЛОПЕРИОДА(&ДатаНачалаАнализа, ДЕНЬ),
		|		МИНИМУМ(ТаблицаПродажи.Период),
		|		МАКСИМУМ(ТаблицаПродажи.Остаток) - СУММА(ТаблицаПродажи.ОборотЗаДень),
		|		МАКСИМУМ(ТаблицаПродажи.Остаток)
		|	ИЗ
		|		ТаблицаОборотов КАК ТаблицаПродажи
		|	СГРУППИРОВАТЬ ПО
		|		ТаблицаПродажи.Номенклатура
		|	ИМЕЮЩИЕ
		|		МИНИМУМ(ТаблицаПродажи.Период) > &ДатаНачалаАнализа) КАК ОбъединенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ОбъединенныйЗапрос.Номенклатура,
		|	ОбъединенныйЗапрос.Период
		|ИМЕЮЩИЕ
		|	СУММА(ОбъединенныйЗапрос.ОстатокНаДень) > 0
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЦен.Номенклатура КАК Номенклатура,
		|	0 КАК КоличествоПродажи,
		|	0 КАК СуммаПродажи,
		|	0 КАК ДлинаПериодаАнализа,
		|	0 КАК Остаток,
		|	("+ШаблонЦена+")*ТаблицаПересчета.Коэффициент,
		|	0,
		|	ДатаВремя(1,1,1),
		|	0
		|ИЗ
		|	СрезПоследних КАК СрезПоследних
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ТаблицаЦенПодразделений КАК ТаблицаЦен
		|ПО 
		|	СрезПоследних.Контрагент   = ТаблицаЦен.Контрагент И 
		|	СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура И 
		|	СрезПоследних.Уровень      = ТаблицаЦен.Уровень
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаПересчета КАК ТаблицаПересчета
		|ПО"+?(ТипЦен.ВВалютеУчета, "
		|	ТаблицаЦен.Номенклатура.ВалютаУчета = ТаблицаПересчета.Валюта","
		|	ИСТИНА") + " 
		|" + ТекстСоединенийПроцентыСкидкиНаценки + "
		|ГДЕ
		|	ТаблицаЦен.Контрагент = &Контрагент
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|	ТаблицаЦен.Номенклатура КАК Номенклатура,
		|	0 КАК КоличествоПродажи,
		|	0 КАК СуммаПродажи,
		|	0 КАК ДлинаПериодаАнализа,
		|	0 КАК Остаток,
		|	0,
		|	МИНИМУМ(("+ШаблонЦена+")*ТаблицаПересчета.Коэффициент),
		|	ДатаВремя(1,1,1),
		|	0
		|ИЗ
		|	СрезПоследних КАК СрезПоследних
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ТаблицаЦенПодразделений КАК ТаблицаЦен
		|ПО
		|	СрезПоследних.Контрагент   = ТаблицаЦен.Контрагент И 
		|	СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура И 
		|	СрезПоследних.Уровень      = ТаблицаЦен.Уровень
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаПересчета КАК ТаблицаПересчета
		|ПО"+?(ТипЦен.ВВалютеУчета, "
		|	ТаблицаЦен.Номенклатура.ВалютаУчета = ТаблицаПересчета.Валюта","
		|	ИСТИНА") + " 
		|" + ТекстСоединенийПроцентыСкидкиНаценки + "
		//|ГДЕ
		//|	НЕ ТаблицаЦен.Контрагент = &Контрагент
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЦен.Номенклатура
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.СрокПоставки,
		|	ЗаказыПоставщикамОстатки.ЗаказаноОстаток
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаФормированияЗаказа, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК ЗаказыПоставщикамОстатки
		|ГДЕ
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.СрокПоставки <= &ДатаСледующегоЗаказа) КАК ИтоговаяТаблица
		|СГРУППИРОВАТЬ ПО
		|	ИтоговаяТаблица.Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаОборотовТовара
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаОборотов
		|;
		|
		|УНИЧТОЖИТЬ
		|	СрезПоследних
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаЦенПодразделений
		|;
		|
		|ВЫБРАТЬ
		|	ИтоговаяТаблица.Номенклатура КАК Номенклатура,
		|	ИтоговаяТаблица.Номенклатура.Производитель КАК Производитель,
		|	ИтоговаяТаблица.КоличествоПродажи КАК Оборот,
		|	ВЫБОР
		|		КОГДА ИтоговаяТаблица.КоличествоПродажи=0 ТОГДА
		|			ИтоговаяТаблица.СуммаПродажи
		|		ИНАЧЕ
		|			ИтоговаяТаблица.СуммаПродажи / ИтоговаяТаблица.КоличествоПродажи
		|	КОНЕЦ КАК ЦенаПродажи,
		|	ИтоговаяТаблица.ДлинаПериодаАнализа КАК ДлинаПериодаАнализа,
		|	ИтоговаяТаблица.Остаток КАК ОстатокЗаказ,
		|	ИтоговаяТаблица.ЦенаПоставщика КАК ЦенаПоставщика,
		|	ИтоговаяТаблица.МинимальнаяЦена КАК МинимальнаяЦена,
		|	ИтоговаяТаблица.ДатаПоступления КАК ДатаПоступления,
		|	ИтоговаяТаблица.Заказано КАК Заказано,
		|	ЕСТЬNULL(МинимальныйОстатокСрезПоследних.ЗначениеРеквизита, 0) КАК МинимальныйОстаток,
		|	ЕСТЬNULL(МаксимальныйЗапасСрезПоследних.ЗначениеРеквизита, 0) КАК МаксимальныйЗапас,
		|	ЕСТЬNULL(КвантПоставкиСрезПоследних.ЗначениеРеквизита, 1) КАК КвантПоставки,
		|	ИтоговаяТаблица.Номенклатура.Артикул КАК Артикул,
		|	ИтоговаяТаблица.Номенклатура.Код КАК Код
		|ИЗ
		|	ИтоговаяТаблица КАК ИтоговаяТаблица
		|////////////////////// МИНИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МинимальныйОстатокСрезПоследних
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МинимальныйОстатокСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МинимальныйОстатокСрезПоследнихОбщ
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МинимальныйОстатокСрезПоследнихОбщ.Номенклатура
		|////////////////////// МАКСИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МаксимальныйЗапасСрезПоследних
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МаксимальныйЗапасСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МаксимальныйЗапасСрезПоследнихОбщ
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МаксимальныйЗапасСрезПоследнихОбщ.Номенклатура
		|////////////////////// КВАНТ ПОСТАВКИ //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК КвантПоставкиСрезПоследних
		|ПО
		|	ИтоговаяТаблица.Номенклатура = КвантПоставкиСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК КвантПоставкиСрезПоследнихОбщ
		|ПО
		|	ИтоговаяТаблица.Номенклатура = КвантПоставкиСрезПоследнихОбщ.Номенклатура
		|";
	Иначе
		РезультатТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаЦен.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ТаблицаЦен.Цена) КАК Цена,
		|	МАКСИМУМ(ТаблицаЦен.МинимальнаяЦена) КАК МинимальнаяЦена
		|ПОМЕСТИТЬ
		|	ТаблицаЦен
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаЦен.Номенклатура КАК Номенклатура,
		|		("+ШаблонЦена+")*ТаблицаПересчета.Коэффициент КАК Цена,
		|		0 КАК МинимальнаяЦена
		|	ИЗ
		|		СрезПоследних КАК СрезПоследних
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаЦенПодразделений КАК ТаблицаЦен
		|	ПО
		|		СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура И 
		|		СрезПоследних.Уровень = ТаблицаЦен.Уровень
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаПересчета КАК ТаблицаПересчета
		|	ПО"+?(ТипЦен.ВВалютеУчета, "
		|		ТаблицаЦен.Номенклатура.ВалютаУчета = ТаблицаПересчета.Валюта","
		|		ИСТИНА") + " 
		|	" + ТекстСоединенийПроцентыСкидкиНаценки + "
		|	ГДЕ
		|		ТаблицаЦен.Контрагент = &Контрагент
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаЦен.Номенклатура КАК Номенклатура,
		|		0,
		|		МИНИМУМ(("+ШаблонЦена+")*ТаблицаПересчета.Коэффициент)
		|	ИЗ
		|		СрезПоследних КАК СрезПоследних
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаЦенПодразделений КАК ТаблицаЦен
		|	ПО
		|		СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура И 
		|		СрезПоследних.Уровень = ТаблицаЦен.Уровень
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ТаблицаПересчета КАК ТаблицаПересчета
		|	ПО"+?(ТипЦен.ВВалютеУчета, "
		|		ТаблицаЦен.Номенклатура.ВалютаУчета = ТаблицаПересчета.Валюта","
		|		ИСТИНА") + " 
		|	" + ТекстСоединенийПроцентыСкидкиНаценки + "
		|	ГДЕ
		|		НЕ ТаблицаЦен.Контрагент = &Контрагент
		|	СГРУППИРОВАТЬ ПО
		|		ТаблицаЦен.Номенклатура) КАК ТаблицаЦен
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаЦен.Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаЦенПодразделений
		|;
		|
		|УНИЧТОЖИТЬ
		|	СрезПоследних
		|;
		|
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ЗаказыПоставщикамОстатки.ЗаказПоставщику.СрокПоставки) КАК ДатаПоступления,
		|	СУММА(ЗаказыПоставщикамОстатки.ЗаказаноОстаток) КАК Заказано
		|ПОМЕСТИТЬ
		|	ТаблицаЗаказов
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаФормированияЗаказа, Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК ЗаказыПоставщикамОстатки
		|ГДЕ
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику.СрокПоставки <= &ДатаСледующегоЗаказа
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыПоставщикамОстатки.Номенклатура
		|;
		|
		|ВЫБРАТЬ
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПродажиОбороты.Номенклатура.Производитель КАК Производитель,
		|	ПродажиОбороты.КоличествоОборот КАК Оборот,
		|	ПродажиОбороты.СуммаОборот / ПродажиОбороты.КоличествоОборот КАК ЦенаПродажи,
		|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК ОстатокЗаказ,
		|	ЕСТЬNULL(ТаблицаЦен.Цена, 0) КАК ЦенаПоставщика,
		|	ЕСТЬNULL(ТаблицаЗаказов.ДатаПоступления, ДатаВремя(1,1,1)) КАК ДатаПоступления,
		|	ЕСТЬNULL(ТаблицаЗаказов.Заказано, 0) КАК Заказано,
		|	ЕСТЬNULL(ТаблицаЦен.МинимальнаяЦена, 0) КАК МинимальнаяЦена,
		|	ЕСТЬNULL(МинимальныйОстатокСрезПоследних.ЗначениеРеквизита, 0) КАК МинимальныйОстаток,
		|	ЕСТЬNULL(МаксимальныйЗапасСрезПоследних.ЗначениеРеквизита, 0) КАК МаксимальныйЗапас,
		|	ЕСТЬNULL(КвантПоставкиСрезПоследних.ЗначениеРеквизита, 1) КАК КвантПоставки,
		|	ПродажиОбороты.Номенклатура.Артикул КАК Артикул,
		|	ПродажиОбороты.Номенклатура.Код КАК Код
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&ДатаНачалаАнализа, &ДатаКонцаАнализа, ,
		|													Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)
		|										{
		|										ПодразделениеКомпании.Организация.* КАК Организация,
		|										ПодразделениеКомпании.* КАК ПодразделениеКомпании,
		|										СкладКомпании.* КАК СкладКомпании}
		|										) КАК ПродажиОбороты
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаФормированияЗаказа,
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)
		|						{
		|							СкладКомпании.Подразделение.Организация.* КАК Организация,
		|							СкладКомпании.Подразделение.* КАК ПодразделениеКомпании,
		|							СкладКомпании.* КАК СкладКомпании}) КАК ОстаткиТоваровКомпанииОстатки
		|ПО
		|	ПродажиОбороты.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаЗаказов КАК ТаблицаЗаказов
		|ПО
		|	ПродажиОбороты.Номенклатура = ТаблицаЗаказов.Номенклатура
		|
		|////////////////////// МИНИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МинимальныйОстатокСрезПоследних
		|ПО
		|	ПродажиОбороты.Номенклатура = МинимальныйОстатокСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МинимальныйОстатокСрезПоследнихОбщ
		|ПО
		|	ПродажиОбороты.Номенклатура = МинимальныйОстатокСрезПоследнихОбщ.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаЦен КАК ТаблицаЦен
		|ПО
		|	ПродажиОбороты.Номенклатура = ТаблицаЦен.Номенклатура
		|////////////////////// МАКСИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МаксимальныйЗапасСрезПоследних
		|ПО
		|	ПродажиОбороты.Номенклатура = МаксимальныйЗапасСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК МаксимальныйЗапасСрезПоследнихОбщ
		|ПО
		|	ПродажиОбороты.Номенклатура = МаксимальныйЗапасСрезПоследнихОбщ.Номенклатура
		|////////////////////// КВАНТ ПОСТАВКИ //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК КвантПоставкиСрезПоследних
		|ПО
		|	ПродажиОбороты.Номенклатура = КвантПоставкиСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) И 
		|						Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)) КАК КвантПоставкиСрезПоследнихОбщ
		|ПО
		|	ПродажиОбороты.Номенклатура = КвантПоставкиСрезПоследнихОбщ.Номенклатура
		|ГДЕ
		|	ПродажиОбороты.КоличествоОборот > 0
		|";
	КонецЕсли;
	
	Возврат РезультатТекстЗапроса;
	
КонецФункции

//Процедура формирования результатов заказа поставщику
Процедура СформироватьРезультат() Экспорт
	
	ТекПостроитель = Новый ПостроительОтчета;
	ТекРодитель = Подразделение;
	ТекстОбъединенияУровней = "";
	Уровень = 0;
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		ТекПостроитель.Параметры.Вставить("Подразделение"+Уровень, ТекРодитель);
		ТекстОбъединенияУровней = ТекстОбъединенияУровней+"
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	&Подразделение"+Уровень+",
		|	"+Уровень;
		ТекРодитель = ТекРодитель.Родитель;
		Уровень=Уровень+1;
	КонецЦикла;
	
	ТекстОбъединенияУровней = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПодразделений.Родитель КАК Подразделение,
	|	ТаблицаПодразделений.Уровень КАК Уровень
	|ПОМЕСТИТЬ
	|	ТаблицаПодразделений
	|ИЗ (
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) КАК Родитель,
	|		"+Уровень+" КАК Уровень
	|	"+ТекстОбъединенияУровней+") КАК ТаблицаПодразделений
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;";
	
	РабочийТипЦен = ТипЦен;
	ШаблонЦена = "ТаблицаЦен.Цена";
		
	ТекстСоединенийПроцентыСкидкиНаценки = "";
		
	// Если расчетная цена получим базовую цену
	Счетчик = 1;
	Пока РабочийТипЦен.Рассчитывается Цикл
		
		Если РабочийТипЦен.ПроцентыСкидкиНаценки.Количество()>0 Тогда
			
			ИмяТаблицы = "ПроцентыСкидкиНаценки" + Строка(Счетчик);
			ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК " + ИмяТаблицы + "
			|ПО
			|	" + ИмяТаблицы + ".Ссылка = &ТипЦены" + Строка(Счетчик) + " И 
			|	ТаблицаЦен.Номенклатура.ТипНоменклатуры = " + ИмяТаблицы + ".ТипНоменклатуры";
			
			ШаблонЦена = "("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
			
			ТекПостроитель.Параметры.Вставить("ТипЦены" + Строка(Счетчик), РабочийТипЦен);
			
			Счетчик = Счетчик + 1;
			
		ИначеЕсли РабочийТипЦен.ПроцентСкидкиНаценки <> 0 Тогда
			
			ШаблонЦена = "("+ШаблонЦена+"+"+ШаблонЦена+"*"+Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100, "ЧРД=.")+")";
			
		КонецЕсли;
		
		РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
		
	КонецЦикла;
	
	ТекстЗапроса = "
	| " + ТекстОбъединенияУровней + "
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	(КурсыВалютСрезПоследних.Курс/ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1)/&КурсПрайсЛиста) КАК Коэффициент
	|ПОМЕСТИТЬ
	|	ТаблицаПересчета
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаТекущегоЗаказа, " + ?(ТипЦен.ВВалютеУчета, "", "Валюта=&ВалютаТипаЦен") + ") КАК КурсыВалютСрезПоследних
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ
	|	ТаблицаТоваров
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|ГДЕ
	|	ПартииТоваровКомпании.Период <= &ДатаКонцаАнализа И 
	|	ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И 
	|	ПартииТоваровКомпании.Количество > 0 И
	|	ПартииТоваровКомпании.Партия.Контрагент = &Контрагент
	|{ГДЕ
	|	ПартииТоваровКомпании.СкладКомпании КАК СкладКомпании,
	|	ПартииТоваровКомпании.СкладКомпании.Организация КАК Организация,
	|	ПартииТоваровКомпании.СкладКомпании.Подразделение КАК ПодразделениеКомпании,
	|	ПартииТоваровКомпании.Номенклатура.* КАК Номенклатура
	|}
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровКомпании.Номенклатура
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.Контрагент КАК Контрагент,
	|	ТаблицаЦен.Период КАК Период,
	|	ТаблицаПодразделений.Уровень КАК Уровень,
	|	ТаблицаЦен.Цена КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦен
	|ИЗ
	|	РегистрСведений.Цены КАК ТаблицаЦен
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаПодразделений КАК ТаблицаПодразделений
	|ПО 
	|	ТаблицаЦен.ПодразделениеКомпании = ТаблицаПодразделений.Подразделение
	|ГДЕ
	|	ТаблицаЦен.Период <= &ДатаТекущегоЗаказа И 
	|	ТаблицаЦен.ТипЦен = &ВыбТипЦен И 
	|	(НЕ ТаблицаЦен.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) И 
	|	ТаблицаЦен.ХарактеристикаНоменклатуры=ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) И 
	|	ТаблицаЦен.ЕдиницаИзмерения=ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) И 
	|	ТаблицаЦен.Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров)
	|ИНДЕКСИРОВАТЬ ПО
	|	Уровень,
	|	Контрагент,
	|	Номенклатура
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаПодразделений
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦен.Уровень,
	|	ТаблицаЦен.Контрагент КАК Контрагент,
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ТаблицаЦен.Период) КАК Период
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаЦен КАК ТаблицаЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Уровень,
	|	ТаблицаЦен.Контрагент,
	|	ТаблицаЦен.Номенклатура
	|ИНДЕКСИРОВАТЬ ПО
	|	Уровень,
	|	Контрагент,
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦен.Контрагент КАК Контрагент,
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.Уровень КАК Уровень,
	|	МИНИМУМ(ТаблицаЦен.Цена) КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦенПодразделений
	|ИЗ
	|	СрезПоследних КАК СрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаЦен КАК ТаблицаЦен
	|ПО
	|	СрезПоследних.Уровень      = ТаблицаЦен.Уровень И 
	|	СрезПоследних.Контрагент   = ТаблицаЦен.Контрагент И 
	|	СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура И 
	|	СрезПоследних.Период       = ТаблицаЦен.Период
	|ГДЕ
	|	НЕ ТаблицаЦен.Цена = 0
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Контрагент,
	|	ТаблицаЦен.Номенклатура,
	|	ТаблицаЦен.Уровень
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	Номенклатура
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаЦен
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследних
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦен.Контрагент КАК Контрагент,
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаЦенПодразделений КАК ТаблицаЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Контрагент,
	|	ТаблицаЦен.Номенклатура
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	Номенклатура
	|;
	|" + ПолучитьТекстЗапросаОсновныхТаблиц(ШаблонЦена, ТекстСоединенийПроцентыСкидкиНаценки);
	
	ТекПостроитель.Текст = ТекстЗапроса;
	ТекПостроитель.УстановитьНастройки(ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Ложь), Истина, Ложь, Ложь, Ложь, Ложь);
	ТекПостроитель.Параметры.Вставить("ВыбТипЦен", РабочийТипЦен);
	ТекПостроитель.Параметры.Вставить("ДатаНачалаАнализа",      НачалоДня(ДатаНачалаАнализа));
	ТекПостроитель.Параметры.Вставить("ДатаКонцаАнализа",       КонецДня(?(обЗначениеНеЗаполнено(ДатаКонцаАнализа), РабочаяДата,ДатаКонцаАнализа)));
	ТекПостроитель.Параметры.Вставить("ДатаТекущегоЗаказа",     КонецДня(ДатаТекущегоЗаказа));
	ТекПостроитель.Параметры.Вставить("ДатаСледующегоЗаказа",   КонецДня(ДатаСледующегоЗаказа));
	ТекПостроитель.Параметры.Вставить("ДатаФормированияЗаказа", КонецДня(ДатаФормированияЗаказа));
	ТекПостроитель.Параметры.Вставить("Контрагент",             Контрагент);
	ТекПостроитель.Параметры.Вставить("ПодразделениеКомпании",  Подразделение);
	ТекПостроитель.Параметры.Вставить("ВалютаТипаЦен",          ТипЦен.ВалютаЦены);
	СтруктураКурса = обКурсДляВалюты(ТипЦен.ВалютаЦены, ТекущаяДата());
	КурсПрайсЛиста = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	ТекПостроитель.Параметры.Вставить("КурсПрайсЛиста", ?(КурсПрайсЛиста=0,1,КурсПрайсЛиста));

	// чистим таблицу
	
	Товары.Очистить();
	// выполняем запрос
	ТекПостроитель.Выполнить();
	
	ДлинаПериодаАнализа=Цел((НачалоДня(ДатаКонцаАнализа)-НачалоДня(ДатаНачалаАнализа))/86400)+1;
	
	// открываем выборку
	Выборка = ТекПостроитель.Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// подсчитаем количество дней в периоде
		Если УчитыватьОтсутствиеНаСкладе Тогда
			ДлинаПериодаАнализа = Выборка.ДлинаПериодаАнализа;
			Если ДлинаПериодаАнализа = Null Тогда
				ДлинаПериодаАнализа = 0;
			КонецЕсли;
		КонецЕсли;
		
		// Cчитаем сколько рекомендуется заказать
		МинимальныйОстаток = Выборка.МинимальныйОстаток;
		МаксимальныйЗапас  = Выборка.МаксимальныйЗапас;
		КвантПоставки      = Выборка.КвантПоставки;
		
		// СредняяПродажа - количество проданное в день за период анализа
		Оборот = ?(Выборка.Оборот = Null, 0, Выборка.Оборот);
		СредняяПродажа = ?(ДлинаПериодаАнализа=0, 0, Оборот/ДлинаПериодаАнализа);
		
		РасходЗапасаДоСледующейПоставки = Окр(СредняяПродажа*(Цел((ДатаСледующегоЗаказа - НачалоДня(ДатаФормированияЗаказа))/86400)+1), 2);
		Рекомендуем = Макс((РасходЗапасаДоСледующейПоставки - Выборка.ОстатокЗаказ) + МинимальныйОстаток, 0);
		
		// не больше максимального
		Если МаксимальныйЗапас>0 Тогда
			Рекомендуем = ?(Рекомендуем > МаксимальныйЗапас, МаксимальныйЗапас, Рекомендуем);
		КонецЕсли;
		
		// CASE по округлениям
		Если Режим_Округления="ДробноеКоличествоВМеньшуюСторону" Тогда
			Рекомендуем=ЦЕЛ(Рекомендуем);
		ИначеЕсли Режим_Округления="ДробноеКоличествоВБольшуюСторону" Тогда
			Рекомендуем=?(ЦЕЛ(Рекомендуем)=Рекомендуем,Рекомендуем,ЦЕЛ(Рекомендуем)+1);
		ИначеЕсли Режим_Округления="КвантПоставкиВМеньшуюСторону" Тогда
			Если КвантПоставки<>0 Тогда
				Рекомендуем=ЦЕЛ(Рекомендуем/КвантПоставки)*КвантПоставки;
			КонецЕсли;
		ИначеЕсли Режим_Округления="КвантПоставкиВБольшуюСторону" Тогда
			Если КвантПоставки<>0 Тогда
				ЦелРекомендуем=ЦЕЛ(Рекомендуем/КвантПоставки);
				Рекомендуем=?(Рекомендуем/КвантПоставки<>ЦелРекомендуем,(ЦелРекомендуем+1)*КвантПоставки,ЦелРекомендуем*КвантПоставки);
			КонецЕсли;
		КонецЕсли;
		
		// добавляем строку, если не 0
		//Если Рекомендуем = 0 Тогда
		//	Продолжить;
		//КонецЕсли;
		
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура               = Выборка.Номенклатура;
		НоваяСтрока.Количество                 = Окр(Рекомендуем,0);
		НоваяСтрока.ЦенаПродажи                = Выборка.ЦенаПродажи;
		НоваяСтрока.КоличествоПродажВДень      = СредняяПродажа;
		НоваяСтрока.КоличествоПродажЗаПериод   = Оборот;
		НоваяСтрока.МаксимальныйЗапас          = Выборка.МаксимальныйЗапас;
		НоваяСтрока.МинимальнаяЦена            = Выборка.МинимальнаяЦена;
		НоваяСтрока.МинимальныйЗапас           = Выборка.МинимальныйОстаток;
		НоваяСтрока.ТекущийОстаток             = Выборка.ОстатокЗаказ;
		НоваяСтрока.УжеЗаказано                = Выборка.Заказано;
		НоваяСтрока.ЦенаПоставщика             = Выборка.ЦенаПоставщика;
		НоваяСтрока.ДатаПоступленияЗаказанного = Выборка.ДатаПоступления;
		НоваяСтрока.Использование              = (НоваяСтрока.Количество > 0);
		НоваяСтрока.Код                        = Выборка.Код;
		НоваяСтрока.Артикул                    = Выборка.Артикул;
		НоваяСтрока.Производитель              = Выборка.Производитель;
		
	КонецЦикла;
	
КонецПроцедуры

// устанавливает текст запроса построителя
//
Процедура УстановитьТекстЗапроса() Экспорт
	
	Если УчитыватьОтсутствиеНаСкладе Тогда
		
		ТекстЗапроса="
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпании.Номенклатура КАК Номенклатура,
		|	НАЧАЛОПЕРИОДА(ОстаткиТоваровКомпании.Период, ДЕНЬ) КАК Период,
		|	СУММА(ВЫБОР
		|		КОГДА ОстаткиТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ОстаткиТоваровКомпании.Количество
		|		ИНАЧЕ
		|			- ОстаткиТоваровКомпании.Количество
		|	КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ
		|	ТаблицаОборотовТовара
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
		|ГДЕ
		|	ОстаткиТоваровКомпании.Период МЕЖДУ &ДатаНачалаАнализа И &ДатаКонцаАнализа
		|{ГДЕ
		|	СкладКомпании.* КАК СкладКомпании,
		|	Номенклатура.* КАК Номенклатура}
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиТоваровКомпании.Номенклатура,
		|	НАЧАЛОПЕРИОДА(ОстаткиТоваровКомпании.Период, ДЕНЬ)
		|;
		|
		|ВЫБРАТЬ
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПродажиОбороты.КоличествоОборот КАК КоличествоПродажи,
		|	ТаблицаОборотовТовара.Период КАК Период,
		|	ТаблицаОборотовТовара.Количество КАК ОборотЗаДень,
		|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК Остаток
		|ПОМЕСТИТЬ
		|	ТаблицаОборотов
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&ДатаНачалаАнализа, &ДатаКонцаАнализа, , Поставщик=&Контрагент 
		|																							{
		|																							ПодразделениеКомпании.Организация.* КАК Организация,
		|																							ПодразделениеКомпании.* КАК ПодразделениеКомпании,
		|																							СкладКомпании.* КАК СкладКомпании,
		|																							Номенклатура.* КАК Номенклатура}
		|																							) КАК ПродажиОбороты
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаФормированияЗаказа,
		|						{СкладКомпании.* КАК СкладКомпании, Номенклатура.* КАК Номенклатура}) КАК ОстаткиТоваровКомпанииОстатки
		|ПО
		|	ПродажиОбороты.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаОборотовТовара КАК ТаблицаОборотовТовара
		|ПО
		|	ПродажиОбороты.Номенклатура = ТаблицаОборотовТовара.Номенклатура
		|ГДЕ
		|	ПродажиОбороты.КоличествоОборот > 0
		|;
		|
		|ВЫБРАТЬ
		|	ИтоговаяТаблица.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ИтоговаяТаблица.КоличествоПродажи) КАК КоличествоПродажи,
		|	СУММА(ИтоговаяТаблица.ДлинаПериодаАнализа)+1 КАК ДлинаПериодаАнализа,
		|	МАКСИМУМ(ИтоговаяТаблица.Остаток) КАК Остаток
		|ПОМЕСТИТЬ
		|	ИтоговаяТаблица
		|ИЗ (
		|ВЫБРАТЬ
		|	ОбъединенныйЗапрос.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ОбъединенныйЗапрос.КоличествоПродажи) КАК КоличествоПродажи,
		|	РАЗНОСТЬДАТ(ОбъединенныйЗапрос.Период, МИНИМУМ(ОбъединенныйЗапрос.БлижайшаяДата), ДЕНЬ) КАК ДлинаПериодаАнализа,
		|	МАКСИМУМ(ОбъединенныйЗапрос.Остаток) КАК Остаток
		|ИЗ (
		|	ВЫБРАТЬ
		|		ТаблицаПродажи.Номенклатура КАК Номенклатура,
		|		ТаблицаПродажи.КоличествоПродажи КАК КоличествоПродажи,
		|		ТаблицаПродажи.Период КАК Период,
		|		НАЧАЛОПЕРИОДА(&ДатаКонцаАнализа, ДЕНЬ) КАК БлижайшаяДата,
		|		ТаблицаПродажи.Остаток КАК ОстатокНаДень,
		|		ТаблицаПродажи.Остаток КАК Остаток
		|	ИЗ
		|		ТаблицаОборотов КАК ТаблицаПродажи
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПродажи.Номенклатура,
		|		0,
		|		ТаблицаПродажи.Период,
		|		ТаблицаОборотов.Период,
		|		- ТаблицаОборотов.ОборотЗаДень,
		|		0
		|	ИЗ
		|		ТаблицаОборотов КАК ТаблицаПродажи
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаОборотов КАК ТаблицаОборотов
		|	ПО
		|		ТаблицаПродажи.Номенклатура = ТаблицаОборотов.Номенклатура И 
		|		ТаблицаПродажи.Период < ТаблицаОборотов.Период
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаПродажи.Номенклатура,
		|		МАКСИМУМ(ТаблицаПродажи.КоличествоПродажи),
		|		НАЧАЛОПЕРИОДА(&ДатаНачалаАнализа, ДЕНЬ),
		|		МИНИМУМ(ТаблицаПродажи.Период),
		|		МАКСИМУМ(ТаблицаПродажи.Остаток) - СУММА(ТаблицаПродажи.ОборотЗаДень),
		|		МАКСИМУМ(ТаблицаПродажи.Остаток)
		|	ИЗ
		|		ТаблицаОборотов КАК ТаблицаПродажи
		|	СГРУППИРОВАТЬ ПО
		|		ТаблицаПродажи.Номенклатура
		|	ИМЕЮЩИЕ
		|		МИНИМУМ(ТаблицаПродажи.Период) > &ДатаНачалаАнализа) КАК ОбъединенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ОбъединенныйЗапрос.Номенклатура,
		|	ОбъединенныйЗапрос.Период
		|ИМЕЮЩИЕ
		|	СУММА(ОбъединенныйЗапрос.ОстатокНаДень) > 0) КАК ИтоговаяТаблица
		|СГРУППИРОВАТЬ ПО
		|	ИтоговаяТаблица.Номенклатура
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаОборотовТовара
		|;
		|
		|УНИЧТОЖИТЬ
		|	ТаблицаОборотов
		|;
		|
		|ВЫБРАТЬ
		|	ИтоговаяТаблица.Номенклатура КАК Номенклатура,
		|	ИтоговаяТаблица.Номенклатура.Производитель КАК Производитель,
		|	ИтоговаяТаблица.КоличествоПродажи КАК Оборот,
		|	ИтоговаяТаблица.ДлинаПериодаАнализа КАК ДлинаПериодаАнализа,
		|	ИтоговаяТаблица.Остаток КАК ОстатокЗаказ,
		|	ЕСТЬNULL(МинимальныйОстатокСрезПоследних.ЗначениеРеквизита, 0) КАК МинимальныйОстаток,
		|	ЕСТЬNULL(МаксимальныйЗапасСрезПоследних.ЗначениеРеквизита, 0) КАК МаксимальныйЗапас,
		|	ЕСТЬNULL(КвантПоставкиСрезПоследних.ЗначениеРеквизита, 1) КАК КвантПоставки,
		|	ИтоговаяТаблица.Номенклатура.Артикул КАК Артикул,
		|	ИтоговаяТаблица.Номенклатура.Код КАК Код
		|ИЗ
		|	ИтоговаяТаблица КАК ИтоговаяТаблица
		|////////////////////// МИНИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) {Номенклатура.* КАК Номенклатура}) КАК МинимальныйОстатокСрезПоследних
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МинимальныйОстатокСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) {Номенклатура.* КАК Номенклатура}) КАК МинимальныйОстатокСрезПоследнихОбщ
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МинимальныйОстатокСрезПоследнихОбщ.Номенклатура
		|////////////////////// МАКСИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) {Номенклатура.* КАК Номенклатура}) КАК МаксимальныйЗапасСрезПоследних
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МаксимальныйЗапасСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) {Номенклатура.* КАК Номенклатура}) КАК МаксимальныйЗапасСрезПоследнихОбщ
		|ПО
		|	ИтоговаяТаблица.Номенклатура = МаксимальныйЗапасСрезПоследнихОбщ.Номенклатура
		|////////////////////// КВАНТ ПОСТАВКИ //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) {Номенклатура.* КАК Номенклатура}) КАК КвантПоставкиСрезПоследних
		|ПО
		|	ИтоговаяТаблица.Номенклатура = КвантПоставкиСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) {Номенклатура.* КАК Номенклатура}) КАК КвантПоставкиСрезПоследнихОбщ
		|ПО
		|	ИтоговаяТаблица.Номенклатура = КвантПоставкиСрезПоследнихОбщ.Номенклатура
		|";
		
	Иначе
		
		ТекстЗапроса="
		|ВЫБРАТЬ
		|	ПродажиОбороты.Номенклатура КАК Номенклатура,
		|	ПродажиОбороты.Номенклатура.Производитель КАК Производитель,
		|	ПродажиОбороты.КоличествоОборот КАК Оборот,
		|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстаткиЗаказ.КоличествоОстаток, 0) КАК ОстатокЗаказ,
		|	ЕСТЬNULL(МинимальныйОстатокСрезПоследних.ЗначениеРеквизита, 0) КАК МинимальныйОстаток,
		|	ЕСТЬNULL(МаксимальныйЗапасСрезПоследних.ЗначениеРеквизита, 0) КАК МаксимальныйЗапас,
		|	ЕСТЬNULL(КвантПоставкиСрезПоследних.ЗначениеРеквизита, 1) КАК КвантПоставки,
		|	ПродажиОбороты.Номенклатура.Артикул КАК Артикул,
		|	ПродажиОбороты.Номенклатура.Код КАК Код
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&ДатаНачалаАнализа, &ДатаКонцаАнализа,, Поставщик=&Контрагент 
		|																							{
		|																							ПодразделениеКомпании.Организация.* КАК Организация,
		|																							ПодразделениеКомпании.* КАК ПодразделениеКомпании,
		|																							СкладКомпании.* КАК СкладКомпании,
		|																							Номенклатура.* КАК Номенклатура}
		|																							) КАК ПродажиОбороты
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаФормированияЗаказа,
		|						{СкладКомпании.* КАК СкладКомпании, Номенклатура.* КАК Номенклатура}) КАК ОстаткиТоваровКомпанииОстаткиЗаказ
		|ПО
		|	ПродажиОбороты.Номенклатура = ОстаткиТоваровКомпанииОстаткиЗаказ.Номенклатура
		|////////////////////// МИНИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) {Номенклатура.* КАК Номенклатура}) КАК МинимальныйОстатокСрезПоследних
		|ПО
		|	ПродажиОбороты.Номенклатура = МинимальныйОстатокСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток) {Номенклатура.* КАК Номенклатура}) КАК МинимальныйОстатокСрезПоследнихОбщ
		|ПО
		|	ПродажиОбороты.Номенклатура = МинимальныйОстатокСрезПоследнихОбщ.Номенклатура
		|////////////////////// МАКСИМАЛЬНЫЙ ЗАПАС //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) {Номенклатура.* КАК Номенклатура}) КАК МаксимальныйЗапасСрезПоследних
		|ПО
		|	ПродажиОбороты.Номенклатура = МаксимальныйЗапасСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.МаксимальныйЗапас) {Номенклатура.* КАК Номенклатура}) КАК МаксимальныйЗапасСрезПоследнихОбщ
		|ПО
		|	ПродажиОбороты.Номенклатура = МаксимальныйЗапасСрезПоследнихОбщ.Номенклатура
		|////////////////////// КВАНТ ПОСТАВКИ //////////////////
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании = &ПодразделениеКомпании И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) {Номенклатура.* КАК Номенклатура}) КАК КвантПоставкиСрезПоследних
		|ПО
		|	ПродажиОбороты.Номенклатура = КвантПоставкиСрезПоследних.Номенклатура
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(&ДатаТекущегоЗаказа, 
		|			ПодразделениеКомпании  = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) И
		|			ДополнительныйРеквизит = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРеквизитыНоменклатуры.КвантПоставки) {Номенклатура.* КАК Номенклатура}) КАК КвантПоставкиСрезПоследнихОбщ
		|ПО
		|	ПродажиОбороты.Номенклатура = КвантПоставкиСрезПоследнихОбщ.Номенклатура
		|";
	КонецЕсли;
	
	ПостроительОтчета.Текст = ТекстЗапроса;
	
КонецПроцедуры // УстановитьТекстЗапроса()

// добавляет элементы отбора для построителя
//
Процедура УстановитьФильтры() Экспорт
	
	// Добавим фильтры
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Организация",,"Организация");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("ПодразделениеКомпании",,"Подразделение компании");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("СкладКомпании",,"Склад компании");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура",,"Номенклатура");
	ПолеОтбора.ВидСравнения=ВидСравнения.ВСпискеПоИерархии;
	
КонецПроцедуры // УстановитьФильтры()

// расчет и заполнение таблицы рекомендуемых для заказа товаров
//
Процедура РассчитатьИЗаполнитьРекомендации() Экспорт
	
	ПостроительОтчета.Параметры.Вставить("ДатаНачалаАнализа",      НачалоДня(ДатаНачалаАнализа));
	ПостроительОтчета.Параметры.Вставить("ДатаКонцаАнализа",       КонецДня(?(обЗначениеНеЗаполнено(ДатаКонцаАнализа), РабочаяДата,ДатаКонцаАнализа)));
	ПостроительОтчета.Параметры.Вставить("ДатаТекущегоЗаказа",     КонецДня(ДатаТекущегоЗаказа));
	ПостроительОтчета.Параметры.Вставить("ДатаСледующегоЗаказа",     КонецДня(ДатаСледующегоЗаказа));
	
	ПостроительОтчета.Параметры.Вставить("ДатаФормированияЗаказа", КонецДня(ДатаФормированияЗаказа));
	ПостроительОтчета.Параметры.Вставить("Контрагент",             Контрагент);
	ПостроительОтчета.Параметры.Вставить("ПодразделениеКомпании",  ПараметрыСеанса.ПодразделениеКомпании);
	
	// чистим таблицу
	
	Товары.Очистить();
	// выполняем запрос
	ПостроительОтчета.Выполнить();
	
	ДлинаПериодаАнализа=Цел((НачалоДня(ДатаКонцаАнализа)-НачалоДня(ДатаНачалаАнализа))/86400)+1;
	
	// открываем выборку
	Выборка = ПостроительОтчета.Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// подсчитаем количество дней в периоде
		Если УчитыватьОтсутствиеНаСкладе Тогда
			ДлинаПериодаАнализа = Выборка.ДлинаПериодаАнализа;
			Если ДлинаПериодаАнализа = Null Тогда
				ДлинаПериодаАнализа = 0;
			КонецЕсли;
		КонецЕсли;
		
		// Cчитаем сколько рекомендуется заказать
		МинимальныйОстаток = Выборка.МинимальныйОстаток;
		МаксимальныйЗапас  = Выборка.МаксимальныйЗапас;
		КвантПоставки      = Выборка.КвантПоставки;
		
		// СредняяПродажа - количество проданное в день за период анализа
		Оборот = ?(Выборка.Оборот = Null, 0, Выборка.Оборот);
		СредняяПродажа = ?(ДлинаПериодаАнализа=0, 0, Оборот/ДлинаПериодаАнализа);
		
		РасходЗапасаДоСледующейПоставки = Окр(СредняяПродажа*(Цел((ДатаСледующегоЗаказа - НачалоДня(ДатаФормированияЗаказа))/86400)+1), 2);
		Рекомендуем = Макс((РасходЗапасаДоСледующейПоставки - Выборка.ОстатокЗаказ) + МинимальныйОстаток, 0);
		
		// не больше максимального
		Если МаксимальныйЗапас>0 Тогда
			Рекомендуем = ?(Рекомендуем > МаксимальныйЗапас, МаксимальныйЗапас, Рекомендуем);
		КонецЕсли;
		
		// CASE по округлениям
		Если Режим_Округления="ДробноеКоличествоВМеньшуюСторону" Тогда
			Рекомендуем=ЦЕЛ(Рекомендуем);
		ИначеЕсли Режим_Округления="ДробноеКоличествоВБольшуюСторону" Тогда
			Рекомендуем=?(ЦЕЛ(Рекомендуем)=Рекомендуем,Рекомендуем,ЦЕЛ(Рекомендуем)+1);
		ИначеЕсли Режим_Округления="КвантПоставкиВМеньшуюСторону" Тогда
			Если КвантПоставки<>0 Тогда
				Рекомендуем=ЦЕЛ(Рекомендуем/КвантПоставки)*КвантПоставки;
			КонецЕсли;
		ИначеЕсли Режим_Округления="КвантПоставкиВБольшуюСторону" Тогда
			Если КвантПоставки<>0 Тогда
				ЦелРекомендуем=ЦЕЛ(Рекомендуем/КвантПоставки);
				Рекомендуем=?(Рекомендуем/КвантПоставки<>ЦелРекомендуем,(ЦелРекомендуем+1)*КвантПоставки,ЦелРекомендуем*КвантПоставки);
			КонецЕсли;
		КонецЕсли;
		
		// добавляем строку, если не 0
		Если Рекомендуем = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;
		НоваяСтрока.Количество   = Окр(Рекомендуем,0);
		
	КонецЦикла;
	
КонецПроцедуры // РассчитатьИЗаполнитьРекомендации()

#КонецЕсли
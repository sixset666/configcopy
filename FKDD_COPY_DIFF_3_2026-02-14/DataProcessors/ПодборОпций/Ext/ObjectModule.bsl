////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//Добавление позиции в документ при подборе, вызывается из формы подбора 
//и при стандартной обработке ОбработкеВыбора, в случае подбора по справочнику.
//В документ добавляется элемент справочника Номенклатура, если элемент представляет
//собой Группу справочника или Набор - то добавляются соотв. составляющие.
//Добавление в документ производится в основной единице измерения, если не указана иная.
//Если строка с указанной Номенклатурой и Единицей измерения отсутствует, 
//то добавляется новая строка или изменяется количество в противном случае.
//
//Параметры
//	ЭтаФорма - Форма, из которой произведен вызов.
//	СтруктураПараметровПодбора - Структура, содержащая параметры подбора.
//	Номенклатура - СправочникСсылка.Номенклатура, добавляемая номенклатура (Элемент,Группа,Набор).
//	СпроситьКоличество - Булево, флаг запроса количества при подборе.
//	СпроситьЦену - Булево, флаг запроса цены при подборе.
//	Количество - Число, значение количества по умолчанию.
//	УстановитьТекущей - Булево, флаг необходимости установить добавляемую строку текущей в табличной части документа
//
Функция ДобавитьНоменклатуру(ЭтаФорма, СтруктураПараметровПодбора, Опция, СпроситьКоличество, Количество=1, Цена = 0) Экспорт
    // Если пользователь решил отказаться от продолжения добавления товара
    #Если Клиент Тогда
        ОбработкаПрерыванияПользователя();
    #КонецЕсли
    
    // Получим имя реквизита табличной части "количество"
    ИмяРеквизитаКоличества = Неопределено;
    СтруктураПараметровПодбора.Свойство("ИмяРеквизитаКоличества",ИмяРеквизитаКоличества);
    Если ИмяРеквизитаКоличества = Неопределено Тогда
        ИмяРеквизитаКоличества = "Количество";
        СтруктураПараметровПодбора.Вставить("ИмяРеквизитаКоличества",ИмяРеквизитаКоличества);
    КонецЕсли;
    
    // получим табличную часть документа
    ФормаДокумента="";
    СтруктураПараметровПодбора.Свойство("ФормаДокумента",ФормаДокумента);
    Если обЗначениеНеЗаполнено(ФормаДокумента) Тогда
        ФормаДокумента=ЭтаФорма.ВладелецФормы;
        СтруктураПараметровПодбора.Вставить("ФормаДокумента",ФормаДокумента);
    КонецЕсли;
    
    Попытка
        ИдентификаторАвтомобиля = "";
        СтруктураПараметровПодбора.Свойство("ИдентификаторАвтомобиля", ИдентификаторАвтомобиля);
        Если обЗначениеНеЗаполнено(ИдентификаторАвтомобиля) Тогда
            ИдентификаторАвтомобиля=ЭтаФорма.СтруктураПараметровФормы.ИдентификаторАвтомобиля;
            СтруктураПараметровПодбора.Вставить("ИдентификаторАвтомобиля",ИдентификаторАвтомобиля);
        КонецЕсли;
    Исключение
    КонецПопытки;
    
    ИмяТабличнойЧасти = "";
    СтруктураПараметровПодбора.Свойство("ИмяТабличнойЧасти",ИмяТабличнойЧасти);
    Если обЗначениеНеЗаполнено(ИмяТабличнойЧасти) Тогда
        Попытка
            ТабличнаяЧасть=ФормаДокумента[ЭтаФорма.СтруктураПараметровФормы.ИмяТабличнойЧасти];
            ИмяТабличнойЧасти = ЭтаФорма.СтруктураПараметровФормы.ИмяТабличнойЧасти;
            СтруктураПараметровПодбора.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
        Исключение
            Возврат Неопределено;
        КонецПопытки;
    Иначе
        ТабличнаяЧасть=ФормаДокумента[ЭтаФорма.СтруктураПараметровФормы.ИмяТабличнойЧасти];     
    КонецЕсли;
    
    Если ТипЗнч(ТабличнаяЧасть) = Тип("РегистрСведенийСписок.ОпцииВариантовКомплектации") Тогда
        ВариантКомплектации="";
        СтруктураПараметровПодбора.Свойство("ВариантКомплектации",ВариантКомплектации);
        Если обЗначениеНеЗаполнено(ВариантКомплектации) Тогда
            ВариантКомплектации=ЭтаФорма.СтруктураПараметровФормы.ВариантКомплектации;
            СтруктураПараметровПодбора.Вставить("ВариантКомплектации",ВариантКомплектации);
        КонецЕсли;
        
        ВидВключения="";
        СтруктураПараметровПодбора.Свойство("ВидВключения",ВидВключения);
        Если обЗначениеНеЗаполнено(ВидВключения) Тогда
            ВидВключения=ЭтаФорма.СтруктураПараметровФормы.ВидВключения;
            СтруктураПараметровПодбора.Вставить("ВидВключения",ВидВключения);
        КонецЕсли;
    КонецЕсли;
    
    // Оповестим о выборе документ, инициировавший подбор.
    ОбработкаВДокументе=Ложь;
    СтруктураПараметровПодбора.Свойство("ОбработкаВДокументе",ОбработкаВДокументе);
    Если ОбработкаВДокументе = Неопределено Тогда
        ОбработкаВДокументе = Ложь;
    КонецЕсли; 
    // если обработка заполнения товара находится в документе, то передадим выбор туда
    Если ОбработкаВДокументе Тогда 
        ЭтаФорма.ОповеститьОВыборе(Опция);
        Возврат Неопределено;
    КонецЕсли;
    
    // поищем выбранную номенклатуру в таблице товаров
    МассивСтрок=Новый Массив();
    Если НЕ ТипЗнч(ТабличнаяЧасть) = Тип("РегистрСведенийСписок.ОпцииВариантовКомплектации") Тогда
        СтруктураОтбора = Новый Структура("Опция", Опция);
        Если ЗначениеЗаполнено(ИдентификаторАвтомобиля) Тогда
            СтруктураОтбора.Вставить("ИдентификаторАвтомобиля", ИдентификаторАвтомобиля);
        КонецЕсли;
        МассивСтрок=ТабличнаяЧасть.НайтиСтроки(СтруктураОтбора);
    Иначе
        
        ЗапросДОКомплектации = Новый Запрос;
        ЗапросДОКомплектации.Текст = "ВЫБРАТЬ
        |	ОпцииВариантовКомплектации.Опция
        |ИЗ
        |	РегистрСведений.ОпцииВариантовКомплектации КАК ОпцииВариантовКомплектации
        |ГДЕ
        |	ОпцииВариантовКомплектации.ВариантКомплектации = &ВариантКомплектации";
        ЗапросДОКомплектации.УстановитьПараметр("ВариантКомплектации",ВариантКомплектации);
        ВыборкаДОКомплектации = ЗапросДОКомплектации.Выполнить().Выбрать();
        
        Если НЕ ВыборкаДОКомплектации.НайтиСледующий(Новый Структура("Опция",Опция)) Тогда
            НоваяОпция=РегистрыСведений.ОпцииВариантовКомплектации.СоздатьМенеджерЗаписи();
            НоваяОпция.ВариантКомплектации=ВариантКомплектации;
            НоваяОпция.Опция=Опция;
            НоваяОпция.Количество=1;
            НоваяОпция.ВидВключения=ВидВключения;
            НоваяОпция.Записать(Истина);
        КонецЕсли; 
        //КонецЦикла; 
        Возврат 0;
    КонецЕсли;
    
    Если МассивСтрок.Количество()>0 Тогда
        ТекущаяСтрока=МассивСтрок[0];
    КонецЕсли;
    СтруктураВыбСтрока = Новый Структура("Опция,Количество,Цена", Опция,Количество,Цена);
    ДобавитьСтрокуВДокумент(СтруктураВыбСтрока,СтруктураПараметровПодбора);
    
    Возврат ТекущаяСтрока;
КонецФункции // ДобавитьНоменклатуру()

// Процедура добавления строки в документ
//
// Параметры:
//	СтруктураВыбСтрока - Структура с данными выбранной строки
//	СтруктураПараметровПодбора - Структура с параметрами подбора
//
Процедура ДобавитьСтрокуВДокумент(СтруктураВыбСтрока,СтруктураПараметровПодбора) Экспорт
    
    Опция 				= СтруктураВыбСтрока.Опция;
    Количество	 		= СтруктураВыбСтрока.Количество;
    Цена                = СтруктураВыбСтрока.Цена;
    
    // все необходимое уже использовалось ранее
    ФормаДокумента			 = СтруктураПараметровПодбора.ФормаДокумента;
    ИмяТабличнойЧасти        = СтруктураПараметровПодбора.ИмяТабличнойЧасти;
    ИмяРеквизитаКоличества   = СтруктураПараметровПодбора.ИмяРеквизитаКоличества;
    ИдентификаторАвтомобиля  = СтруктураПараметровПодбора.ИдентификаторАвтомобиля;
    ЭтоДокумент              = СтруктураПараметровПодбора.ЭтоДокумент;
    ТабличнаяЧасть			 = ФормаДокумента[ИмяТабличнойЧасти];
    
    #Если Клиент Тогда
        Состояние("Добавление элемента: " + Опция);
    #КонецЕсли
    
    СтруктураОтбора = Новый Структура("Опция", Опция);
    Если ЗначениеЗаполнено(ИдентификаторАвтомобиля) Тогда
        СтруктураОтбора.Вставить("ИдентификаторАвтомобиля", ИдентификаторАвтомобиля);
    КонецЕсли;
    МассивСтрок=ТабличнаяЧасть.НайтиСтроки(СтруктураОтбора);
    
    НоваяСтрока = (МассивСтрок.Количество() = 0);
    Если НоваяСтрока Тогда
        ТекущаяСтрока = ТабличнаяЧасть.Добавить();
        ТекущаяСтрока.Опция = Опция;
        Если ЭтоДокумент Тогда
            Если ЗначениеЗаполнено(ИдентификаторАвтомобиля) Тогда
                ТекущаяСтрока.ИдентификаторАвтомобиля = ИдентификаторАвтомобиля;
            КонецЕсли;
            Попытка
                ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Опция",ТекущаяСтрока,ФормаДокумента);
            Исключение
            КонецПопытки;
            ТекущаяСтрока[ИмяРеквизитаКоличества] = Количество;
            Если ЗначениеЗаполнено(Цена) Тогда
                ТекущаяСтрока.Цена = Цена;
            КонецЕсли;
            Если ТекущаяСтрока.Опция = Справочники.Опции.ПустаяСсылка() Тогда
                #Если Клиент Тогда
                    Сообщить("Опция <"+Опция+"> недопустима для данного варианта комплектации.");
                #КонецЕсли
            КонецЕсли;
            ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + ".Количество",ТекущаяСтрока,ФормаДокумента);
        КонецЕсли;
    ИначеЕсли ЭтоДокумент Тогда
        ТекущаяСтрока = МассивСтрок[0];
        ТекущаяСтрока[ИмяРеквизитаКоличества] = ТекущаяСтрока[ИмяРеквизитаКоличества] + Количество;
        Если ТекущаяСтрока.Цена=0 Тогда
            ТекущаяСтрока.Цена = Цена;
        КонецЕсли;
        ФормаДокумента.ОбработкаРеквизита(ИмяТабличнойЧасти + "." + ИмяРеквизитаКоличества,ТекущаяСтрока,ФормаДокумента);
    КонецЕсли;
    
    // Вызовем обработчики изменения в форме
    #Если Клиент Тогда
        // Проверим заполняется табличная часть документа или таблица значений
        // (если таблица значений, то обработчики изменения, нам не нужны)
        Если ЭтоДокумент Тогда
            дкВывестиЗаголовокСуммаДокумента(ФормаДокумента);
        КонецЕсли;
    #КонецЕсли
    
КонецПроцедуры

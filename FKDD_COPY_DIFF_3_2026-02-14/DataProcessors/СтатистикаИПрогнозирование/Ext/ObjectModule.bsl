
///////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Рассчет системы линейных уравнений методом Гаусса
//
// Параметры:
// 	Матрица - сама матрица, 
//  Вектор - массив значений после "="
//
// Возвращаемое значение:
// 	Возвращает массив - решение системы.
//
Функция Гаусс(Матрица,Вектор) Экспорт
	Н=Матрица.ВГраница();
	Для К=0 По Н-1 Цикл
		// Ищем строку l с максимальным элементом в k-ом столбце
		Л=0; М=0;
		Для Й=К По Н Цикл
			А=Матрица[Й][К];
			Если ?(А<0,-А,А)>М Тогда
				М=?(А<0,-А,А); Л=Й;
			КонецЕсли;        
		КонецЦикла;

	 	// Меняем местом Л-ую строку с К-ой 
		Если Л<>К Тогда
			Для Ж=0 По Н Цикл
				Т=Матрица[К][Ж];
				Матрица[К][Ж]=Матрица[Л][Ж];
				Матрица[Л][Ж]=Т;
			КонецЦикла;
			Т=Вектор[К];
			Вектор[К]=Вектор[Л];
			Вектор[Л]=Т;
		КонецЕсли;

	 	//Преобразуем матрицу
		Для Й=К+1 По Н Цикл
			КУ=Матрица[Й][К]/Матрица[К][К];
			Для Ж=0 По Н Цикл
				Если Ж=К Тогда
					Матрица[Й][Ж]=0;
				Иначе                                
					Матрица[Й][Ж]=Матрица[Й][Ж]-(КУ*Матрица[К][Ж]);
				КонецЕсли;
			КонецЦикла;
			Вектор[Й]=Вектор[Й]-КУ*Вектор[К];
		КонецЦикла;                           
	КонецЦикла;
	
	// Вычисляем решение
	Решение=Новый Массив(Н+1);
	Для Сч=0 По Н Цикл
		Решение[Сч]=0;
	КонецЦикла;
	Решение[Н]=Вектор[Н]/Матрица[Н][Н];
	Й=Н-1;
	Пока Й>=0 Цикл
		Т=0;
		Для Ж=0 По Н-Й Цикл
			Т=Т+Матрица[Й][Й+Ж]*Решение[Й+Ж];
		КонецЦикла;
		Решение[Й]=(1/Матрица[Й][Й])*(Вектор[Й]-Т);
		Й=Й-1;
	КонецЦикла;
	
	Возврат Решение;
КонецФункции // Гаусс()

// заполняем дерево возможных действий
//
// Параметры:
//  ЕстьСглаживание - Булево - определяет добавлять ли строки сглаживания,
//  ЕстьТренды      - Булево - определяет добавлять ли строки линий тренда, 
//  ЕстьПрогнозы    - Булево - определяет добавлять ли строки моделей прогноза.
//
Процедура ИнициализироватьДеревоДействий(ЕстьСглаживание=Истина,ЕстьТренды=Истина,ЕстьПрогнозы=Истина) Экспорт
	// чистим деревце
	ДеревоДействий.Строки.Очистить();
	ДеревоДействий.Колонки.Очистить();
	
	// создаем колонки
	ДеревоДействий.Колонки.Добавить("ИмяДействия");
	ДеревоДействий.Колонки.Добавить("ПредставлениеДействия");
	ДеревоДействий.Колонки.Добавить("ИмяФормыНастройки");
	
	// Добавляем строки первого уровня
	Если ЕстьСглаживание Тогда
		Сглаживание=ДеревоДействий.Строки.Добавить(); Сглаживание.ПредставлениеДействия="Методы сглаживания рядов"; Сглаживание.ИмяДействия="#Сглаживание";
	КонецЕсли;
	Если ЕстьТренды Тогда
		Тренды=ДеревоДействий.Строки.Добавить(); Тренды.ПредставлениеДействия="Линии тренда"; Тренды.ИмяДействия="#Тренды";
	КонецЕсли;
	Если ЕстьПрогнозы Тогда
		Прогнозы=ДеревоДействий.Строки.Добавить(); Прогнозы.ПредставлениеДействия="Модели прогнозов"; Прогнозы.ИмяДействия="#Прогнозы";
		ПростыеМодели=Прогнозы.Строки.Добавить(); ПростыеМодели.ПредставлениеДействия="Простые модели"; ПростыеМодели.ИмяДействия="#ПростыеМодели";
	КонецЕсли;
	
	// СТРОКИ СГЛАЖИВАНИЯ
	
	Если ЕстьСглаживание Тогда
		// скользящее среднее
		НоваяСтрокаСглаживание=Сглаживание.Строки.Добавить();
		НоваяСтрокаСглаживание.ПредставлениеДействия="Сглаживание скользящим средним";
		НоваяСтрокаСглаживание.ИмяДействия="СглаживаниеСкользящееСреднее";
		НоваяСтрокаСглаживание.ИмяФормыНастройки="НастройкиСкользящееСреднее";
		// экспоненциальное
		НоваяСтрокаСглаживание=Сглаживание.Строки.Добавить();
		НоваяСтрокаСглаживание.ПредставлениеДействия="Сглаживание экспоненциальное";
		НоваяСтрокаСглаживание.ИмяДействия="СглаживаниеЭкспоненциальное";
		НоваяСтрокаСглаживание.ИмяФормыНастройки="НастройкиЭкспоненциальное";
		// Хольта - Винтерса
		НоваяСтрокаСглаживание=Сглаживание.Строки.Добавить();
		НоваяСтрокаСглаживание.ПредставлениеДействия="Сглаживание методом Хольта-Винтерса";
		НоваяСтрокаСглаживание.ИмяДействия="СглаживаниеХольтаВинтерса";
		НоваяСтрокаСглаживание.ИмяФормыНастройки="НастройкиХольтаВинтерса";
	КонецЕсли;
	
	// СТРОКИ ТРЕНДОВ
	
	Если ЕстьТренды Тогда
		// Линейный
		НоваяСтрокаТренд=Тренды.Строки.Добавить();
		НоваяСтрокаТренд.ПредставлениеДействия="Тренд линейный";
		НоваяСтрокаТренд.ИмяДействия="ТрендЛинейный";
		// Логарифмический
		НоваяСтрокаТренд=Тренды.Строки.Добавить();
		НоваяСтрокаТренд.ПредставлениеДействия="Тренд логарифмический";
		НоваяСтрокаТренд.ИмяДействия="ТрендЛогарифмический";
		// Полиномиальный
		НоваяСтрокаТренд=Тренды.Строки.Добавить();
		НоваяСтрокаТренд.ПредставлениеДействия="Тренд полиномиальный";
		НоваяСтрокаТренд.ИмяДействия="ТрендПолиномиальный";
		НоваяСтрокаТренд.ИмяФормыНастройки="НастройкиТрендПолиномиальный";
		// Экспоненциальный
		//НоваяСтрокаТренд=Тренды.Строки.Добавить();
		//НоваяСтрокаТренд.ПредставлениеДействия="Тренд экспоненциальный";
		//НоваяСтрокаТренд.ИмяДействия="ТрендЭкспоненциальный";
	КонецЕсли;
	
	// СТРОКИ МОДЕЛЕЙ ПРОГНОЗОВ
	
	Если ЕстьПрогнозы Тогда
		// прогноз средним значением
		НоваяСтрокаПростыеМодели=ПростыеМодели.Строки.Добавить();
		НоваяСтрокаПростыеМодели.ПредставлениеДействия="Прогноз средним значением";
		НоваяСтрокаПростыеМодели.ИмяДействия="ПрогнозСреднимЗначением";
		НоваяСтрокаПростыеМодели.ИмяФормыНастройки="";
		// прогноз экспоненциальным сглаживанием
		НоваяСтрокаПростыеМодели=ПростыеМодели.Строки.Добавить();
		НоваяСтрокаПростыеМодели.ПредставлениеДействия="Прогноз экспоненциальным сглаживанием";
		НоваяСтрокаПростыеМодели.ИмяДействия="ПрогнозСглаживаниеЭкспоненциальное";
		НоваяСтрокаПростыеМодели.ИмяФормыНастройки="НастройкиЭкспоненциальное";
		// прогноз сглаживанием Хольта-Винтерса
		НоваяСтрокаПростыеМодели=ПростыеМодели.Строки.Добавить();
		НоваяСтрокаПростыеМодели.ПредставлениеДействия="Прогноз сглаживанием Хольта-Винтерса";
		НоваяСтрокаПростыеМодели.ИмяДействия="ПрогнозСглаживаниеХольтаВинтерса";
		НоваяСтрокаПростыеМодели.ИмяФормыНастройки="НастройкиХольтаВинтерса";
		// прогноз линейным трендом
		НоваяСтрокаПростыеМодели=ПростыеМодели.Строки.Добавить();
		НоваяСтрокаПростыеМодели.ПредставлениеДействия="Прогноз линейным трендом";
		НоваяСтрокаПростыеМодели.ИмяДействия="ПрогнозТрендЛинейный";
		// прогноз логарифмическим трендом
		НоваяСтрокаПростыеМодели=ПростыеМодели.Строки.Добавить();
		НоваяСтрокаПростыеМодели.ПредставлениеДействия="Прогноз логарифмическим трендом";
		НоваяСтрокаПростыеМодели.ИмяДействия="ПрогнозТрендЛогарифмический";
		// прогноз полиномиальным трендом
		НоваяСтрокаПростыеМодели=ПростыеМодели.Строки.Добавить();
		НоваяСтрокаПростыеМодели.ПредставлениеДействия="Прогноз полиномиальным трендом";
		НоваяСтрокаПростыеМодели.ИмяДействия="ПрогнозТрендПолиномиальный";
		НоваяСтрокаПростыеМодели.ИмяФормыНастройки="НастройкиТрендПолиномиальный";
		// прогноз экспоненциальным трендом
		//НоваяСтрокаПростыеМодели=ПростыеМодели.Строки.Добавить();
		//НоваяСтрокаПростыеМодели.ПредставлениеДействия="Прогноз экспоненциальным трендом";
		//НоваяСтрокаПростыеМодели.ИмяДействия="ПрогнозТрендЭкспоненциальный";
	КонецЕсли;
КонецПроцедуры // ИнициализироватьДеревоДействий()

// исполняет выбранные действия
//
// Параметры:
//   ИсходныйРяд - Массив, исходный ряд
//
// Возвращаемое значение:
//  возвращает полученный ряд (Массив)
// 
Функция ВыполнитьДействия(ИсходныйРяд) Экспорт
	// что бы не подпортить исходный ряд, скопируем его
	ПолученныйРяд=Новый Массив();
	Для Сч=0 По ИсходныйРяд.ВГраница() Цикл
		ПолученныйРяд.Добавить(ИсходныйРяд[Сч]);
	КонецЦикла;
	
	// идем по таблице действий
	Для Каждого Действие Из ВыбранныеДействия Цикл
		Если НЕ Действие.Выбран Тогда Продолжить; КонецЕсли;
		
		// получим настройки
		Если НЕ ПустаяСтрока(Действие.ИмяФормыНастройки) Тогда
			Попытка
				СтруктураНастроек=ЗначениеИзСтрокиВнутр(Действие.СтруктураНастроек);
				к=1/Число(ТипЗнч(СтруктураНастроек)=Тип("Структура"));
			Исключение
				Сообщить("Ошибка получения настроек для метода: "+СокрЛП(Действие.ПредставлениеДействия),СтатусСообщения.Важное);
				Продолжить;
			КонецПопытки;
		КонецЕсли;
		
		// CASE по именам действий
		Если Действие.Действие="СглаживаниеСкользящееСреднее" Тогда
			ПолученныйРяд=СглаживаниеСкользящееСреднее(ПолученныйРяд,СтруктураНастроек);
			
		ИначеЕсли Действие.Действие="СглаживаниеЭкспоненциальное" Тогда
			ПолученныйРяд=СглаживаниеЭкспоненциальное(ПолученныйРяд,СтруктураНастроек);
			
		ИначеЕсли Действие.Действие="СглаживаниеХольтаВинтерса" Тогда
			ПолученныйРяд=СглаживаниеХольтаВинтерса(ПолученныйРяд,СтруктураНастроек);
			
		ИначеЕсли Действие.Действие="ТрендЛинейный" Тогда
			ПолученныйРяд=ТрендЛинейный(ПолученныйРяд);
			
		ИначеЕсли Действие.Действие="ТрендЛогарифмический" Тогда
			ПолученныйРяд=ТрендЛогарифмический(ПолученныйРяд);
			
		ИначеЕсли Действие.Действие="ТрендПолиномиальный" Тогда
			ПолученныйРяд=ТрендПолиномиальный(ПолученныйРяд,СтруктураНастроек);
			
		//ИначеЕсли Действие.Действие="ТрендЭкспоненциальный" Тогда
		
		ИначеЕсли Действие.Действие="ПрогнозСреднимЗначением" Тогда
			ПолученныйРяд=ПрогнозСреднимЗначением(ПолученныйРяд,Истина);
		
		ИначеЕсли Действие.Действие="ПрогнозСглаживаниеЭкспоненциальное" Тогда
			ПолученныйРяд=СглаживаниеЭкспоненциальное(ПолученныйРяд,СтруктураНастроек,Истина);
		
		ИначеЕсли Действие.Действие="ПрогнозСглаживаниеХольтаВинтерса" Тогда
			ПолученныйРяд=СглаживаниеХольтаВинтерса(ПолученныйРяд,СтруктураНастроек,Истина);
			
		ИначеЕсли Действие.Действие="ПрогнозТрендЛинейный" Тогда
			ПолученныйРяд=ТрендЛинейный(ПолученныйРяд,Истина);
			
		ИначеЕсли Действие.Действие="ПрогнозТрендЛогарифмический" Тогда
			ПолученныйРяд=ТрендЛогарифмический(ПолученныйРяд,Истина);
			
		ИначеЕсли Действие.Действие="ПрогнозТрендПолиномиальный" Тогда
			ПолученныйРяд=ТрендПолиномиальный(ПолученныйРяд,СтруктураНастроек,Истина);
			
		//ИначеЕсли Действие.Действие="ПрогнозТрендЭкспоненциальный" Тогда
		
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПолученныйРяд;
КонецФункции // ВыполнитьДействия()
	
#Если Клиент Тогда
	
// возвращает список значений с названиями рядов-примеров
//
Функция ПолучитьПримерыРядов() Экспорт
	Макет=ПолучитьМакет("ПримерыЧисловыхРядов");
	Сч=1; СписокВыбора=Новый СписокЗначений();
	Пока Истина Цикл
		Стр=Макет.Область(1,Сч,1,Сч).Текст;
		Если ПустаяСтрока(Стр) Тогда Прервать; КонецЕсли;
		СписокВыбора.Добавить(Сч,СокрЛП(Стр));
		Сч=Сч+1;
	КонецЦикла;
	
	Возврат СписокВыбора;
КонецФункции // ПолучитьПримерыРядов()

// возвращает массив-ряд примера
//
// Параметры:
//  НомерПримера - число. Порядковый номер примера
//
// Возвращаемое значение:
//  МассивИсходныеДанные - массив - ряд примера/
// 
Функция ПолучитьИсходныйРядПримера(НомерПримера=1) Экспорт
	МассивИсходныеДанные=Новый Массив(100);
	Макет=ПолучитьМакет("ПримерыЧисловыхРядов");
	Для Сч=1 По 100 Цикл
		МассивИсходныеДанные[Сч-1]=Число(Макет.Область(Сч+1,НомерПримера,Сч+1,НомерПримера).Текст);
	КонецЦикла;
    Возврат МассивИсходныеДанные;
КонецФункции // ПолучитьИсходныйРядПримера()

// Отображает результаты работы на графике
//
// Параметры:
//  ИсходныйРяд   - Массив,
//  ПолученныйРяд - Массив.
//
Функция ПоказатьРезультат(ИсходныйРяд, ПолученныйРяд) Экспорт
	ФормаРезультат=ПолучитьФорму("Результат");
	ГрафикРезультат=ФормаРезультат.ЭлементыФормы.ГрафикРезультат;
	ГрафикРезультат.АвтоМаксимальноеЗначение=Истина;
	ГрафикРезультат.АвтоМинимальноеЗначение=Истина;
	ГрафикРезультат.АвтоТранспонирование=Ложь;
	СерияИсходныйРяд=ГрафикРезультат.Серии[0];
	СерияПолученныйРяд=ГрафикРезультат.Серии[1];
	// рисуем исходные данные
	Для Сч=0 По ИсходныйРяд.ВГраница() Цикл
		ГрафикРезультат.УстановитьЗначение(ГрафикРезультат.УстановитьТочку(Сч),СерияИсходныйРяд,ИсходныйРяд[Сч]);
	КонецЦикла;
	// рисуем полученные данные
	Для Сч=0 По ПолученныйРяд.ВГраница() Цикл
		ГрафикРезультат.УстановитьЗначение(ГрафикРезультат.УстановитьТочку(Сч),СерияПолученныйРяд,ПолученныйРяд[Сч]);
	КонецЦикла;
	// покажем диаграмму
	ФормаРезультат.Открыть();
КонецФункции // ПоказатьРезультат()

#КонецЕсли

// заполняет значения для строки таблицы действий
//
// Параметры:
//  ТекСтрока - текущая строка таблицы
//  ВыбранноеДействие - строка дерева значений. Если не определена, то открывается дерево с выбором
//
Функция УстановитьДействиеСтроки(ТекСтрока,ВыбранноеДействие=Неопределено) Экспорт
	// откроем форму выбора действия
	Если ВыбранноеДействие=Неопределено Тогда
		#Если Клиент Тогда
		ФормаДействий=ПолучитьФорму("ДеревоДействий");
		ФормаДействий.РежимВыбора=Истина;
		ФормаДействий.ЗакрыватьПриВыборе=Истина;
		ВыбранноеДействие=ФормаДействий.ОткрытьМодально();
		#КонецЕсли
	КонецЕсли;
	// проверим что выбрали
	Если ВыбранноеДействие=Неопределено Тогда Возврат Ложь; КонецЕсли;
	// формируем строку
	ТекСтрока.Выбран=Истина;
	ТекСтрока.Действие=ВыбранноеДействие.ИмяДействия;
	ТекСтрока.ПредставлениеДействия=ВыбранноеДействие.ПредставлениеДействия;
	ТекСтрока.ИмяФормыНастройки=ВыбранноеДействие.ИмяФормыНастройки;
	// установим настройки
	СтруктураНастроек=Неопределено;
	#Если Клиент Тогда
	Попытка СтруктураНастроек=ПолучитьФорму(ТекСтрока.ИмяФормыНастройки).НастройкиПоУмолчанию; Исключение КонецПопытки;
	#КонецЕсли
	УстановитьНастройки(ТекСтрока,СтруктураНастроек);
	
	Возврат Истина;
КонецФункции // УстановитьДействиеСтроки()

// устанавливает настройки для строки таблицы действий
//
// Параметры:
//  ТекСтрока - текущая строка таблицы
//  СтруктураНастроек - Структура. Если не определена, то открывается окно настроек
//
Процедура УстановитьНастройки(ТекСтрока,СтруктураНастроек=Неопределено) Экспорт
	// выберем настройки
	Если СтруктураНастроек=Неопределено И НЕ ПустаяСтрока(ТекСтрока.ИмяФормыНастройки) Тогда
		#Если Клиент Тогда
		ФормаНастроек=ПолучитьФорму(ТекСтрока.ИмяФормыНастройки);
		ФормаНастроек.РежимВыбора=Истина;
		ФормаНастроек.ЗакрыватьПриВыборе=Истина;
		// попытаемся установить те настройки, которые есть в строке
		СтруктураНастроекИзСтроки=Новый Структура();
		Попытка 
			СтруктураНастроекИзСтроки=ЗначениеИзСтрокиВнутр(ТекСтрока.СтруктураНастроек); 
			Для Каждого Настройка Из СтруктураНастроекИзСтроки Цикл
				Попытка ФормаНастроек[Настройка.Ключ]=Настройка.Значение; Исключение КонецПопытки;
			КонецЦикла;
		Исключение 
		КонецПопытки;
		СтруктураНастроек=ФормаНастроек.ОткрытьМодально();
		#КонецЕсли
	КонецЕсли;
	// проверим что выбрали
	Если ТипЗнч(СтруктураНастроек)<>Тип("Структура") ИЛИ СтруктураНастроек.Количество()=0 Тогда
		СтруктураНастроек=Неопределено
	КонецЕсли;
	
	// получим представление настроек
	ПредставлениеНастроек=Неопределено;
	#Если Клиент Тогда
	Попытка ПредставлениеНастроек=ПолучитьФорму(ТекСтрока.ИмяФормыНастройки).ПредставлениеНастроек; Исключение КонецПопытки;
	#КонецЕсли
	
	Если СтруктураНастроек<>Неопределено Тогда
		ТекСтрока.СтруктураНастроек=ЗначениеВСтрокуВнутр(СтруктураНастроек);
	КонецЕсли;
	// представление настроек
	Если ПредставлениеНастроек<>Неопределено И СтруктураНастроек<>Неопределено Тогда
		ТекСтрока.ПредставлениеНастроек="";
		Для Каждого Настройка Из СтруктураНастроек Цикл
			ТекСтрока.ПредставлениеНастроек=ТекСтрока.ПредставлениеНастроек+ПредставлениеНастроек[Настройка.Ключ]+" = "+СокрЛП(Настройка.Значение)+"; ";
		КонецЦикла;
	Иначе
		ТекСтрока.ПредставлениеНастроек="<настройки не определены>";
	КонецЕсли;
КонецПроцедуры // УстановитьНастройки()


///////////////////////////////////////////////////////////////////
// СГЛАЖИВАНИЕ РЯДОВ

// сглаживание методом скользящего среднего
//
// Параметры:
//  МассивРяд - Массив. Сглаживающий, числовой ряд. СтуктураПараметров - Структура. Параметры сглаживания.
//  КоличествоТочекПрогноза - Число. Количество точек на которое надо спрогнозировать результат
//
Функция СглаживаниеСкользящееСреднее(МассивРяд,СтуктураПараметров) Экспорт
	// начальные значения
	КоличествоЭлементов=МассивРяд.Количество();
	Если КоличествоЭлементов=0 Тогда Возврат Новый Массив(); КонецЕсли;
	МассивРезультат=Новый Массив(КоличествоЭлементов);
	Для Сч=0 По МассивРезультат.ВГраница() Цикл МассивРезультат[Сч]=0; КонецЦикла;
	Попытка
		Интервал=СтуктураПараметров.Интервал; к=1/Число(Интервал);
		Если (Интервал/2)=Цел(Интервал/2) Тогда к=1/0; КонецЕсли;
	Исключение
		Интервал=5;
	КонецПопытки;
	// сглаживаем
	Среднее=0;
	Для Сч=Цел(Интервал/2) По КоличествоЭлементов-(Цел(Интервал/2)+1) Цикл
		Сумма=МассивРяд[Сч]; Среднее=Среднее+МассивРяд[Сч];
		Для Сч2=1 По Цел(Интервал/2) Цикл
			Сумма=Сумма+МассивРяд[Сч+Сч2]+МассивРяд[Сч-Сч2];
		КонецЦикла;
		МассивРезультат[Сч]=Сумма/Интервал;
	КонецЦикла;
	// установим начальные и конечные значения в среднее
	Среднее=?((КоличествоЭлементов-(Цел(Интервал/2)+1))=0,0,Среднее/(КоличествоЭлементов-(Цел(Интервал/2)+1)));
	Для Сч=0 По Цел(Интервал/2)-1 Цикл
		МассивРезультат[0+Сч]=МассивРезультат[Цел(Интервал/2)];
		МассивРезультат[КоличествоЭлементов-1-Сч]=МассивРезультат[КоличествоЭлементов-Цел(Интервал/2)-1];
	КонецЦикла;
	
	// возвращаем полученный результат
	Возврат МассивРезультат;
КонецФункции // СглаживаниеСкользящееСреднее()

// экспоненциальное сглаживание
//
// Параметры:
//  МассивРяд - Массив. Сглаживающий, числовой ряд. СтуктураПараметров - Структура. Параметры сглаживания.
//  Прогноз - Булево. Если Истна, то будет спрогнозирован ряд на "Количество точек прогноза"
//
Функция СглаживаниеЭкспоненциальное(МассивРяд,СтуктураПараметров,Прогноз=Ложь) Экспорт
	// начальные значения
	КоличествоЭлементов=МассивРяд.Количество();
	МассивРезультат=Новый Массив(КоличествоЭлементов);
	Для Сч=0 По МассивРезультат.ВГраница() Цикл МассивРезультат[Сч]=0; КонецЦикла;
	Попытка
		Коэффициент=СтуктураПараметров.Коэффициент; к=1/Число(Коэффициент);
		Если Коэффициент<0.2 ИЛИ Коэффициент>0.5 Тогда к=1/0; КонецЕсли;
	Исключение
		Коэффициент=0.33;
	КонецПопытки;
	// сглаживаем
	МассивРезультат[0]=МассивРяд[0];
	Для Сч=1 По МассивРезультат.ВГраница() Цикл
		МассивРезультат[Сч]=Коэффициент*МассивРяд[Сч]+(1-Коэффициент)*МассивРезультат[Сч-1];
	КонецЦикла;
	
	Если Прогноз Тогда
		ТочекПрогноза=?(Прогноз,КоличествоТочекПрогноза,0);
		МассивРезультат.Добавить(МассивРезультат[МассивРезультат.ВГраница()]);
		Для Сч=2 По ТочекПрогноза Цикл
			МассивРезультат=СглаживаниеЭкспоненциальное(МассивРезультат,СтуктураПараметров);
			МассивРезультат.Добавить(МассивРезультат[МассивРезультат.ВГраница()]);
		КонецЦикла;
	КонецЕсли;
	
	// возвращаем полученный результат
	Возврат МассивРезультат;
КонецФункции // СглаживаниеЭкспоненциальное()

// сглаживание методом Хольта-Винтерса
//
// Параметры:
//  МассивРяд - Массив. Сглаживающий, числовой ряд. СтуктураПараметров - Структура. Параметры сглаживания.
//  Прогноз - Булево. Если Истна, то будет спрогнозирован ряд на "Количество точек прогноза"
//
Функция СглаживаниеХольтаВинтерса(МассивРяд,СтуктураПараметров,Прогноз=Ложь) Экспорт
	// начальные значения
	ТочекПрогноза=?(Прогноз,КоличествоТочекПрогноза,0);
	КоличествоЭлементов=МассивРяд.Количество();
	// проверим
	Если КоличествоЭлементов=0 Тогда Возврат Новый Массив(); КонецЕсли;
	
	МассивРезультат=Новый Массив(КоличествоЭлементов+ТочекПрогноза);
	Для Сч=0 По МассивРезультат.ВГраница() Цикл МассивРезультат[Сч]=0; КонецЦикла;
	МассивТренд=Новый Массив(КоличествоЭлементов);
	Попытка
		КоэффициентУровня=СтуктураПараметров.КоэффициентУровня; к=1/Число(КоэффициентУровня);
		Если КоэффициентУровня<0.25 ИЛИ КоэффициентУровня>=1 Тогда к=1/0; КонецЕсли;
	Исключение
		КоэффициентУровня=0.3;
	КонецПопытки;
	Попытка
		КоэффициентТренда=СтуктураПараметров.КоэффициентТренда; к=1/Число(КоэффициентТренда);
		Если КоэффициентТренда<0 ИЛИ КоэффициентУровня>0.5 Тогда к=1/0; КонецЕсли;
	Исключение
		КоэффициентТренда=0.3;
	КонецПопытки;
	
	// начальные значения
	МассивРезультат[0]=МассивРяд[0];
	МассивТренд[0]=МассивРяд[0];
	МассивРезультат[1]=МассивРяд[1];
	МассивТренд[1]=МассивРяд[1]-МассивРяд[0];
	
	// сглаживание
	ОпорнаяТочка = 0; ПоследнееЗначениеТренда = 0;
	Для Сч=2 По МассивРяд.ВГраница() Цикл
		МассивРезультат[Сч]=КоэффициентУровня*(МассивРезультат[Сч-1]+МассивТренд[Сч-1])+(1-КоэффициентУровня)*МассивРяд[Сч];
		МассивТренд[Сч]=КоэффициентТренда*МассивТренд[Сч-1]+(1-КоэффициентТренда)*(МассивРяд[Сч]-МассивРяд[Сч-1]);
		// для прогноза
		ОпорнаяТочка = МассивРезультат[Сч]; ПоследнееЗначениеТренда = МассивТренд[Сч];
	КонецЦикла;
	
	Если Прогноз Тогда
		Для Сч=1 По ТочекПрогноза Цикл
			МассивРезультат[КоличествоЭлементов+Сч-1] = ОпорнаяТочка+Сч*ПоследнееЗначениеТренда;
		КонецЦикла;
	КонецЕсли;
	
	// возвращаем полученный результат
	Возврат МассивРезультат;
КонецФункции // СглаживаниеХольтаВинтерса()


///////////////////////////////////////////////////////////////////
// РАССЧЕТ ОШИБКИ


///////////////////////////////////////////////////////////////////
// РАССЧЕТ ТРЕНДОВ

// построение линейного тренда
//
// Параметры:
//  МассивРяд - Массив. Числовой ряд для которого строим тренд.
//  Прогноз - Булево. Если Истна, то будет спрогнозирован ряд на "Количество точек прогноза"
//
Функция ТрендЛинейный(МассивРяд,Прогноз=Ложь) Экспорт
	ТочекПрогноза=?(Прогноз,КоличествоТочекПрогноза,0);
	// необходимые переменные
	Х_Среднее=0; У_Среднее=0; Б1=0; Б0=0; СуммаХУ=0; СуммаХ2=0; Н=МассивРяд.Количество();
	// проверим
	Если Н=0 Тогда Возврат Новый Массив(); КонецЕсли;
	// считаем необходимые данные
	Для Сч=0 По МассивРяд.ВГраница() Цикл
		Х=Сч; У=МассивРяд[Сч];
		СуммаХУ=СуммаХУ+(Х*У);
		Х_Среднее=Х_Среднее+Х;
		У_Среднее=У_Среднее+У;
		СуммаХ2=СуммаХ2+(Х*Х);
	КонецЦикла;
	Х_Среднее=Х_Среднее/Н;
	У_Среднее=У_Среднее/Н;
	
	// получаем коэффициенты уравнения прямой
	Б1=(СуммаХУ-Н*Х_Среднее*У_Среднее)/(СуммаХ2-Н*(Х_Среднее*Х_Среднее));
	Б0=У_Среднее-Б1*Х_Среднее;
	
	МассивРезультат=Новый Массив(Н+ТочекПрогноза);
	Для Сч=0 По МассивРезультат.ВГраница() Цикл МассивРезультат[Сч]=0; КонецЦикла;
	
	// строим прямую
	Для Х=0 По МассивРяд.ВГраница()+ТочекПрогноза Цикл
		МассивРезультат[Х]=Б0+Б1*Х;
	КонецЦикла;
	
	Возврат МассивРезультат;
КонецФункции // ТрендЛинейный()

// построение логарифмического тренда
//
// Параметры:
//  МассивРяд - Массив. Числовой ряд для которого строим тренд.
//  Прогноз - Булево. Если Истна, то будет спрогнозирован ряд на "Количество точек прогноза"
//
Функция ТрендЛогарифмический(МассивРяд,Прогноз=Ложь) Экспорт
	// необходимые переменные
	ТочекПрогноза=?(Прогноз,КоличествоТочекПрогноза,0);
	Х_Среднее=0; У_Среднее=0; Б1=0; Б0=0; СуммаХУ=0; СуммаХ2=0; Н=МассивРяд.Количество();
	// проверим
	Если Н=0 Тогда Возврат Новый Массив(); КонецЕсли;
	МассивРезультат=Новый Массив(Н+ТочекПрогноза);
	Для Сч=0 По МассивРезультат.ВГраница() Цикл МассивРезультат[Сч]=0; КонецЦикла;
	// считаем необходимые данные
	Для Сч=0 По МассивРяд.ВГраница() Цикл
		Х=Сч; У=МассивРяд[Сч];
		Если У=0 Тогда 
			Сообщить("При построении логарифмического тренда обнаружены точки с значениями = 0. Тренд не построен!",СтатусСообщения.Важное);
			Возврат МассивРезультат; 
		КонецЕсли; // при экспоненциальной зависимости нолей быть не должно
		У=Log(У);
		СуммаХУ=СуммаХУ+(Х*У);
		Х_Среднее=Х_Среднее+Х;
		У_Среднее=У_Среднее+У;
		СуммаХ2=СуммаХ2+(Х*Х);
	КонецЦикла;
	Х_Среднее=Х_Среднее/Н;
	У_Среднее=У_Среднее/Н;
	
	// получаем коэффициенты уравнения прямой
	Б1=(СуммаХУ-Н*Х_Среднее*У_Среднее)/(СуммаХ2-Н*(Х_Среднее*Х_Среднее));
	Б0=У_Среднее-Б1*Х_Среднее;
	
	// строим прямую
	Для Х=0 По МассивРяд.ВГраница()+ТочекПрогноза Цикл
		МассивРезультат[Х]=Exp(Б0+Б1*Х);
	КонецЦикла;
	
	Возврат МассивРезультат;
КонецФункции // ТрендЛогарифмический()

// построение полиномиального тренда степени от 2 до 6
//
// Параметры:
//  МассивРяд - Массив. Числовой ряд для которого строим тренд.
//  СтуктураПараметров - Структура. Параметры тренда.
//  Прогноз - Булево. Если Истна, то будет спрогнозирован ряд на "Количество точек прогноза"
//
Функция ТрендПолиномиальный(МассивРяд,СтуктураПараметров,Прогноз=Ложь) Экспорт
	// проверим
	Если МассивРяд.Количество()=0 Тогда Возврат Новый Массив(); КонецЕсли;
	
	Попытка
		Степень=СтуктураПараметров.Степень; к=1/Число(Степень);
		Если Степень<2 ИЛИ Степень>6 Тогда к=1/0; КонецЕсли;
	Исключение
		Степень=3;
	КонецПопытки;
	
	// начальные значения
	ТочекПрогноза=?(Прогноз,КоличествоТочекПрогноза,0);
	КоличествоЭлементов=МассивРяд.Количество();
	// проверим
	Если КоличествоЭлементов<Степень*Степень Тогда
		Сообщить("Для ряда, содержащего только "+СокрЛП(КоличествоЭлементов)+" элементов, полином степени "+СокрЛП(Степень)+" не может быть построен",СтатусСообщения.Важное);
		Возврат Новый Массив();
	КонецЕсли;

	МассивРезультат=Новый Массив(КоличествоЭлементов+ТочекПрогноза);
	Для Сч=0 По МассивРезультат.ВГраница() Цикл МассивРезультат[Сч]=0; КонецЦикла;
	ВекторХ=Новый Массив(КоличествоЭлементов);
	ВекторУ=Новый Массив(Степень+1);
	
	// рассчитаем вектора
	ВекторХ[0]=КоличествоЭлементов;
	Для Сч=1 По МассивРяд.ВГраница() Цикл
		ВекторХ[Сч]=0;
		Для Сч2=0 По КоличествоЭлементов Цикл
			Х=Сч2;
			ВекторХ[Сч]=ВекторХ[Сч]+Pow(Х,Сч);
		КонецЦикла;
	КонецЦикла;
	Для Сч=0 По ВекторУ.ВГраница() Цикл
		ВекторУ[Сч]=0;
		Для Сч2=0 По МассивРяд.ВГраница() Цикл
			Х=Сч2;
			ВекторУ[Сч]=ВекторУ[Сч]+(МассивРяд[Сч2]*Pow(Х,Сч));
		КонецЦикла;
	КонецЦикла;
	
	// формируем матрицу
	Матрица=Новый Массив(Степень+1,Степень+1);
	Для Сч=0 По Степень Цикл
		Для Сч2=0 По Степень Цикл
			Матрица[Сч][Сч2]=ВекторХ[Сч+Сч2];
		КонецЦикла;
	КонецЦикла;
	// решаем систему линейных уравнений
	Решение=Гаусс(Матрица,ВекторУ);
	
	// тренд
	Для Сч=0 По МассивРяд.ВГраница()+ТочекПрогноза Цикл
		МассивРезультат[Сч]=Решение[0];
		Х=Сч;
		Для Сч2=1 По Степень Цикл
			МассивРезультат[Сч]=МассивРезультат[Сч]+Решение[Сч2]*Pow(Сч,Сч2);
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивРезультат;
КонецФункции // ТрендПолиномиальный()
            
///////////////////////////////////////////////////////////////////
// МОДЕЛИ ПРОГНОЗОВ

// построение линии по среднему значению ряда
//
// Параметры:
//  МассивРяд - Массив. Числовой ряд для которого строим тренд.
//  Прогноз - Булево. Если Истна, то будет спрогнозирован ряд на "Количество точек прогноза"
//
Функция ПрогнозСреднимЗначением(МассивРяд,Прогноз=Ложь) Экспорт
	ТочекПрогноза=?(Прогноз,КоличествоТочекПрогноза,0);
	// необходимые переменные
	Среднее=0; Н=МассивРяд.Количество();
	// проверим
	Если Н=0 Тогда Возврат Новый Массив(); КонецЕсли;
	МассивРезультат=Новый Массив(Н+ТочекПрогноза);
	// считаем необходимые данные
	Для Сч=0 По МассивРяд.ВГраница() Цикл
		Среднее=Среднее+МассивРяд[Сч];
	КонецЦикла;
	Среднее=Среднее/Н;
	// строим прямую
	Для Х=0 По МассивРяд.ВГраница()+ТочекПрогноза Цикл
		МассивРезультат[Х]=Среднее;
	КонецЦикла;
	
	Возврат МассивРезультат;
КонецФункции // ПрогнозСреднимЗначением()

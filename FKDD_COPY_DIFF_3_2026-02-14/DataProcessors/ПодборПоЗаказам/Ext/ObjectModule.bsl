Перем ТекстЗапросаПоЗаказам;                        //Текст запроса по заказам, на основании которого строится список номенклатуры
Перем СтруктураПараметровЗапроса Экспорт;           //Структура с именами реквизитов для параметров запроса или самими значениями параметров
Перем СтруктураПараметровВиртуальныхТаблиц Экспорт; //Структура с отборами виртуальных таблиц (текст, вставляемый в основной запрос)
Перем ИмяГлавнойКолонки Экспорт;                    //Имя колонки в списке номенклатур, по которой осуществляется подбор
Перем ТаблицаТоваров Экспорт;                       //Имя табличного поля, в которое будет производиться добавление номенклатуры
Перем ВариантВызова Экспорт;                        //Строка, идентифицирующая вариант запроса номенклатуры (зависит от вида документа или др. места вызова)
Перем СтруктураСвойств Экспорт;                        //Структура свойств объекта для отбора

Процедура ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, ИмяСекцииОтбора, ТекстОтбора)
	
	Если СтруктураПараметровВиртуальныхТаблиц.Свойство(ИмяСекцииОтбора) Тогда
		СтруктураПараметровВиртуальныхТаблиц.Вставить(ИмяСекцииОтбора, СтруктураПараметровВиртуальныхТаблиц[ИмяСекцииОтбора]+Символы.ПС+"	И "+ТекстОтбора);
	Иначе
		СтруктураПараметровВиртуальныхТаблиц.Вставить(ИмяСекцииОтбора, ТекстОтбора);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСписокПредставленийТипов(ПередаваемыйТип)
	
	Результат = Неопределено;
	
	ТекущийТип = ?(Тип("ОписаниеТипов") = ТипЗнч(ПередаваемыйТип), ПередаваемыйТип.Типы()[0], ПередаваемыйТип);
	
	Если ТекущийТип=Тип("Строка") Тогда
		Результат = "Строка";
	ИначеЕсли ТекущийТип=Тип("Число") Тогда
		Результат = "Число";
	ИначеЕсли ТекущийТип=Тип("Дата") Тогда
		Результат = "Дата";
	ИначеЕсли ТекущийТип=Тип("Булево") Тогда
		Результат = "Булево";	
	Иначе
		Результат = Метаданные.НайтиПоТипу(ТекущийТип).ПолноеИмя();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Инициализация. Заполняется текст запроса и условия
//
// Параметры:
//  Объект            - неопределено - владелецформы.
//  ПостроительОтчета - ПостроительОтчета.
//
Процедура ЗаполнитьНачальныеНастройки(Объект, ПостроительОтчета) Экспорт
	//инициализация параметров
	Попытка
		ТипЦен = Объект.ТипЦен;
		ЗапрашиватьЦену = Истина;
	Исключение
		ЗапрашиватьЦену = Ложь;
	КонецПопытки;
			
	//получаем макет
	Макет = ПолучитьМакет("ТекстЗапроса");
	
	//получаем текст запроса
	Попытка
		ОбластьТекстаЗапроса = Макет.ПолучитьОбласть("Документ_" + Строка(ВариантВызова));
	Исключение
		ОбластьТекстаЗапроса = Макет.ПолучитьОбласть("ПоУмолчанию");
	КонецПопытки;
	Сч = ОбластьТекстаЗапроса.КоличествоСтрок();
	Пока Сч > 0 Цикл //удаляем служебные строки границ области
		ТекСтрока = ОбластьТекстаЗапроса.ПолучитьСтроку(Сч);
		Если Лев(СокрЛ(ТекСтрока), 1) = "#" Тогда
			ОбластьТекстаЗапроса.УдалитьСтроку(Сч);
		КонецЕсли;
		Сч = Сч - 1;
	КонецЦикла;
	ТекстЗапросаПоЗаказам = ОбластьТекстаЗапроса.ПолучитьТекст();
	
	//вставляем условие ИмяГлавнойКолонки > 0
	ТекстЗапросаПоЗаказам = СтрЗаменить(ТекстЗапросаПоЗаказам, "//ИмяГлавнойКолонки", ИмяГлавнойКолонки);
	ТекстСоединения = "";
	ТекстОтборов = "";
	
	Для Каждого ТекСвойство Из СтруктураСвойств Цикл
		ПредставлениеСвойства = ПолучитьСписокПредставленийТипов(ТекСвойство.Значение.ЗначениеОбъекта.ТипЗначения);
		ПустоеЗначение = """""";
		Если ПредставлениеСвойства="Строка" Тогда
		ИначеЕсли ПредставлениеСвойства="Число" Тогда
			ПустоеЗначение = "0";
		ИначеЕсли ПредставлениеСвойства="Дата" Тогда
			ПустоеЗначение = "ДАТАВРЕМЯ(1,1,1)";
		ИначеЕсли ПредставлениеСвойства="Булево" Тогда
			ПустоеЗначение = "ЛОЖЬ";	
		Иначе
			ПустоеЗначение = "ЗНАЧЕНИЕ("+ПредставлениеСвойства+".ПустаяСсылка)";
		КонецЕсли;
		ИмяСвойства = ПланыВидовХарактеристик.СвойстваОбъектов.ПолучитьИмяПредопределенного(ТекСвойство.Значение.ЗначениеОбъекта);
		
		ТекстСоединения = ТекстСоединения+Символы.ПС+"{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов"+ТекСвойство.Ключ+"
		|			ПО тбл.Номенклатура = ВЫРАЗИТЬ(ЗначенияСвойствОбъектов"+ТекСвойство.Ключ+".Объект КАК Справочник.Номенклатура)
		|			 И ЗначенияСвойствОбъектов"+ТекСвойство.Ключ+".Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов."+ИмяСвойства+")}";
		ТекстОтборов = ТекстОтборов + Символы.ПС + ", ВЫРАЗИТЬ(ЕСТЬNULL(ЗначенияСвойствОбъектов"+ТекСвойство.Ключ+".Значение, "+ПустоеЗначение+") КАК "+ПредставлениеСвойства+") КАК "+ТекСвойство.Ключ;
	КонецЦикла;
	
	ТекстЗапросаПоЗаказам = СтрЗаменить(ТекстЗапросаПоЗаказам, "//СОЕДИНЕНИЯ",ТекстСоединения);
	ТекстЗапросаПоЗаказам = СтрЗаменить(ТекстЗапросаПоЗаказам, "//СВОЙСТВА", ТекстОтборов);
	
	ПостроительОтчета.Текст = ТекстЗапросаПоЗаказам;
	
	Для Каждого ТекСвойство Из СтруктураСвойств Цикл
		ПостроительОтчета.Параметры.Вставить("Свойство"+ТекСвойство.Ключ, ТекСвойство.Значение.ЗначениеОбъекта);
	КонецЦикла;
	
	Если СтруктураСвойств.Количество()>1 Тогда
		ПостроительОтчета.Параметры.Вставить("СвойствоПустое",Справочники.ЗначенияСвойств.ПустаяСсылка());
	КонецЕсли;
	
	ЗаполнитьЭлементыОтбора(ПостроительОтчета);
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// Заполняется отбор построителя по умолчанию
//
Процедура ЗаполнитьЭлементыОтбора(ПостроительОтчета)
	Если ВариантВызова = "ЗаказПоставщику" Или ВариантВызова = "ПоступлениеТоваров" Или ВариантВызова = "РеализацияТоваров" Тогда
		ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Подразделение", "Подразделение", "Подразделение");
		ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
		Если ВариантВызова = "ЗаказПоставщику" Тогда
			ПолеОтбора=ПостроительОтчета.Отбор.Добавить("ТипЗаказа", "ТипЗаказа", "Тип заказа");
			ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
			ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Поставщик","Поставщик","Поставщик");
			ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
		КонецЕсли;
	КонецЕсли;

	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура","Номенклатура","Номенклатура");
	ПолеОтбора.ВидСравнения=ВидСравнения.ВСписке;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.Код","НоменклатураКод","Код");
	ПолеОтбора.ВидСравнения=ВидСравнения.Содержит;
	//ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.Артикул","НоменклатураАртикул","Номер по каталогу");
	//ПолеОтбора.ВидСравнения=ВидСравнения.Содержит;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.АртикулДляПоиска","НоменклатураАртикулДляПоиска","Номер по каталогу");
	ПолеОтбора.ВидСравнения=ВидСравнения.Содержит;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.Наименование","НоменклатураНаименование","Наименование");
	ПолеОтбора.ВидСравнения=ВидСравнения.Содержит;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.НаименованиеПолное","НоменклатураНаименованиеПолное","Наименование полное");
	ПолеОтбора.ВидСравнения=ВидСравнения.Содержит;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.Родитель","НоменклатураРодитель","В группе");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.СтранаПроисхождения","СтранаПроисхождения","Страна происхождения");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Номенклатура.Производитель","НоменклатураПроизводитель","Производители");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("НоменклатураПрименяемостьМодель","НоменклатураПрименяемостьМодель","Применяемость");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Остаток");
	ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	Попытка
		ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Заказано");
		ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	Исключение
	КонецПопытки;
	Попытка
		ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Резерв");
		ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	Исключение
	КонецПопытки;
	Попытка
		ПолеОтбора=ПостроительОтчета.Отбор.Добавить("Распределено");
		ПолеОтбора.ВидСравнения=ВидСравнения.Равно;
	Исключение
	КонецПопытки;
КонецПроцедуры // ЗаполнитьЭлементыОтбора()
	
// Формирует структуру фильтров запроса в зависимости от выбранных реквизитов документа и вида документа
//
// Параметры:
//  ЭтаФорма - ссылка на форму.
//
Процедура СформироватьУсловияОтбора(ЭтаФорма, ИерархическийПросмотр)
	Объект  = ЭтаФорма.ВладелецФормы;
	Момент_ = ЭтаФорма.Момент;
	флИспользоватьМомент = ЭтаФорма.ФлажокРассчитыватьНаМомент;
	
	СтруктураПараметровЗапроса.Очистить();
	СтруктураПараметровВиртуальныхТаблиц.Очистить();
	
	Если флИспользоватьМомент Тогда
		СтруктураПараметровЗапроса.Вставить("Момент", КонецДня(Момент_));
		СтруктураПараметровВиртуальныхТаблиц.Вставить("Момент", "&Момент");
	КонецЕсли;
	
	Если ИерархическийПросмотр Тогда
		//Устанавливаем условие родитель номенклатуры = текущий родитель
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Номенклатура.Родитель = &Родитель");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРаспределения", "Номенклатура.Родитель = &Родитель");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРезерв", "Номенклатура.Родитель = &Родитель");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораЗаказыПокупателей", "Номенклатура.Родитель = &Родитель");
	КонецЕсли;
	
	//условия выбираются case-ом по виду документа
	Если ВариантВызова = "ЗаказВнутренний" Тогда
		СтруктураПараметровЗапроса.Вставить("ПодразделениеКомпании", "ПодразделениеПолучатель");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Заказ.ПодразделениеКомпании = &ПодразделениеКомпании");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРаспределения", "ЗаказПокупателя.ПодразделениеКомпании = &ПодразделениеКомпании");
	ИначеЕсли ВариантВызова = "КорректировкаЗаказаПокупателя" Тогда
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		СтруктураПараметровЗапроса.Вставить("Заказ", "ДокументОснование");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент И Заказ = &Заказ");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРаспределения", "ЗаказПокупателя = &Заказ");
	ИначеЕсли ВариантВызова = "КорректировкаЗаказаПоставщику" Тогда
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		СтруктураПараметровЗапроса.Вставить("ЗаказПоставщику", "ДокументОснование");
		
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент И ЗаказПоставщику = &ЗаказПоставщику");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРаспределения", "ЗаказПоставщика = &ЗаказПоставщику");
		
	ИначеЕсли ВариантВызова = "РезервированиеЗаказовПокупателя" Тогда
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		Если Объект.Контрагент <> Неопределено И НЕ Объект.Контрагент.Пустая() Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент");
		КонецЕсли; 
		
	ИначеЕсли ВариантВызова = "СнятиеРезервовЗаказовПокупателя" Тогда
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		Если Объект.Контрагент <> Неопределено И НЕ Объект.Контрагент.Пустая() Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент");
		КонецЕсли; 
		
	ИначеЕсли ВариантВызова = "ПоступлениеТоваров" Тогда
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		Если Объект.Контрагент <> Неопределено И НЕ Объект.Контрагент.Пустая() Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент");
		КонецЕсли;
		
	ИначеЕсли ВариантВызова = "ПеремещениеТоваров" Тогда
		Если ТипЗнч(Объект.СкладПолучатель) = Тип("СправочникСсылка.СкладыКомпании") Тогда
			СтруктураПараметровЗапроса.Вставить("Контрагент", Объект.СкладПолучатель.Подразделение);
		Иначе
			СтруктураПараметровЗапроса.Вставить("Контрагент", Объект.СкладПолучатель);
		КонецЕсли;
		СтруктураПараметровЗапроса.Вставить("СкладКомпании", "СкладКомпании");
		Если ЗначениеЗаполнено(Объект.СкладПолучатель) Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент");
		КонецЕсли; 
		Если ЗначениеЗаполнено(Объект.СкладКомпании) Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРезерв", "СкладКомпании = &СкладКомпании");
		КонецЕсли; 
	ИначеЕсли ВариантВызова = "РеализацияТоваров" Тогда
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		СтруктураПараметровЗапроса.Вставить("СкладКомпании", "СкладКомпании");
		Если НЕ Объект.Контрагент.Пустая() Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент");
		КонецЕсли; 
		Если НЕ Объект.СкладКомпании.Пустая() Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРезерв", "СкладКомпании = &СкладКомпании");
		КонецЕсли; 
	ИначеЕсли ВариантВызова = "ЗакрытиеСмены" Тогда
		СтруктураПараметровЗапроса.Вставить("СкладКомпании", "СкладКомпании");
		Если НЕ Объект.СкладКомпании.Пустая() Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРезерв", "СкладКомпании = &СкладКомпании");
		КонецЕсли;
		
	ИначеЕсли ВариантВызова = "РаспределениеЗаказаПокупателя" Тогда
		
		СтруктураПараметровЗапроса.Вставить("Заказ", "ДокументОснование");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Заказ = &Заказ");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораЗаказыПокупателей", "Заказ ССЫЛКА Документ.ЗаказВнутренний И Заказ<>&Заказ");
		
	ИначеЕсли ВариантВызова = "СнятиеРаспределенияЗаказовПокупателя" Тогда
		
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		Если Объект.ХозОперация = Справочники.ХозОперации.СнятиеРаспределенияПоставщика Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРаспределения", "ЗаказПоставщика ССЫЛКА Документ.ЗаказПоставщику");
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияГДЕОтборРаспределения", "ГДЕ
			|	ВЫРАЗИТЬ(ЗаказыРаспределениеОстатки.ЗаказПоставщика КАК Документ.ЗаказПоставщику).Контрагент = &Контрагент");
		Иначе
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРаспределения", "ЗаказПоставщика ССЫЛКА Документ.ЗаказВнутренний");
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияГДЕОтборРаспределения", "ГДЕ
			|	ВЫРАЗИТЬ(ЗаказыРаспределениеОстатки.ЗаказПоставщика КАК Документ.ЗаказВнутренний).ПодразделениеПолучатель = &Контрагент");
		КонецЕсли;
		
	ИначеЕсли ВариантВызова = "ЗаявкаНаРемонт" ИЛИ ВариантВызова = "ЗаказНаряд" Тогда
		СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Заказ ССЫЛКА Документ.ЗаказПокупателя");
		Контрагент = Объект.Контрагент;
		Если ЗначениеЗаполнено(Контрагент) Тогда
			ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент");
		КонецЕсли;
	ИначеЕсли ВариантВызова = "ПеремещениеРезервов" Тогда
		//СтруктураПараметровЗапроса.Вставить("Контрагент", "Контрагент");
		СтруктураПараметровЗапроса.Вставить("Заказ",               "ДокументОснование");
		СтруктураПараметровЗапроса.Вставить("ДокументРегистратор", "Ссылка");
		
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораЗаказыПокупателей", "Заказ = &Заказ");
		ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРезерв",            "(НЕ Заказ = &Заказ) И 
		|			Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаТоваров КАК ТаблицаТоваров)");
		
		
		//ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораОстатки", "Контрагент = &Контрагент И Заказ = &Заказ");
		//ДобавитьТекстОтбораПараметров(СтруктураПараметровВиртуальныхТаблиц, "УсловияОтбораРаспределения", "ЗаказПокупателя = &Заказ");
	КонецЕсли;
	
КонецПроцедуры // СформироватьУсловияОтбора()
	
// Перезаполнение списка номенклатуры (если иерархический просмотр, то для текущего родителя)
//
// Параметры:
//  ЭтаФорма - ссылка на форму.
//  Родитель - Родитель.
//
Процедура ПерезаполнитьТаблицуНоменклатуры(ЭтаФорма, Родитель = Неопределено) Экспорт

	ПостроительОтчета = ЭтаФорма.Фильтр;
	ВладелецФормы_ = ЭтаФорма.ВладелецФормы;
	ИерархическийПросмотр = ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатуры.Кнопки.ИерархическийПросмотр.Пометка;
	УстановитьОтбор = ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатуры.Кнопки.УстановитьОтбор.Пометка;
	ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатуры.Кнопки.ОтключитьОтбор.Пометка = НЕ УстановитьОтбор;
	
	СписокНоменклатуры.Очистить();
	Если Родитель=Неопределено Тогда Родитель=Справочники.Номенклатура.ПустаяСсылка(); КонецЕсли; 
	Если ИерархическийПросмотр Тогда
		// Заполним группы для перехода на предыдущий уровень.
		ТекГруппа=Родитель;
		Пока НЕ ТекГруппа.Пустая() Цикл
			НоваяСтрока=СписокНоменклатуры.Вставить(0);
			НоваяСтрока.Код=ТекГруппа.Код;
			НоваяСтрока.Артикул=ТекГруппа.Артикул;
			НоваяСтрока.Номенклатура=ТекГруппа;
			НоваяСтрока.ПереходитьВВерх=Истина;
			ТекГруппа=ТекГруппа.Родитель;
		КонецЦикла;
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапросаПоЗаказам;
	
	//Формируется структура отбора вирт. таблиц и параметры запроса
	СформироватьУсловияОтбора(ЭтаФорма, ИерархическийПросмотр);
	
	//Установим параметры запроса
	Для каждого Параметр Из СтруктураПараметровЗапроса Цикл
		Если ТипЗнч(Параметр.Значение) = Тип("Строка") Тогда
			ПостроительОтчета.Параметры.Вставить(Параметр.Ключ, ВладелецФормы_[Параметр.Значение]);
		Иначе
			ПостроительОтчета.Параметры.Вставить(Параметр.Ключ, Параметр.Значение);
		КонецЕсли; 
	КонецЦикла; 
	Если НЕ ИерархическийПросмотр Тогда
		//убираем объединение с выборкой групп справочника номенклатуры
		СтрНачалаОбъединения = Найти(ТекстЗапроса, "//НачалоВыбораГрупп") + 19;
		СтрКонцаОбъединения  = Найти(ТекстЗапроса, "//КонецВыбораГрупп");
		Если СтрНачалаОбъединения > 20 Тогда
			ТекстЗапроса = Лев(ТекстЗапроса, СтрНачалаОбъединения - 1)
			+ Сред(ТекстЗапроса, СтрКонцаОбъединения - 1);
		КонецЕсли;
	КонецЕсли;
	
	//корректируем условия виртуальных таблиц
	Для каждого ТекОтбор Из СтруктураПараметровВиртуальныхТаблиц Цикл
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//" + ТекОтбор.Ключ, ТекОтбор.Значение);
	КонецЦикла;
	
	// Если фильтр не установлен
	Настройки = ПостроительОтчета.ПолучитьНастройки(ИСТИНА,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ);
	
	//работаем с построителем
	ПостроительОтчета.Текст = ТекстЗапроса;
	
	ПостроительОтчета.УстановитьНастройки(Настройки,ИСТИНА,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ);
	Если НЕ УстановитьОтбор Тогда
		Для каждого ТекПараметр Из ПостроительОтчета.Отбор Цикл
			ТекПараметр.Использование=ЛОЖЬ;
		КонецЦикла;
	КонецЕсли;
	
	ПостроительОтчета.Параметры.Вставить("Родитель", Родитель);
	ПостроительОтчета.Выполнить();
	Выборка = ПостроительОтчета.Результат.Выбрать();
	
	// Возвращаем параметры отбора
	Если НЕ УстановитьОтбор Тогда
		ПостроительОтчета.УстановитьНастройки(Настройки,ИСТИНА,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ);
	КонецЕсли;
	
	//зададим структуру отбора строк ТЧ документа
	СтруктураОтбораСтрок = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры");
	тчТовары = ВладелецФормы_[ТаблицаТоваров];
	флЕстьЗаказПокупателяВСтроках = (ВладелецФормы_.Метаданные().ТабличныеЧасти[ТаблицаТоваров].Реквизиты.Найти("ЗаказПокупателя") <> Неопределено);
	Если флЕстьЗаказПокупателяВСтроках Тогда
		СтруктураОтбораСтрок.Вставить("ЗаказПокупателя");
	КонецЕсли;
	флЕстьЗаказПоставщикуВСтроках = (ВладелецФормы_.Метаданные().ТабличныеЧасти[ТаблицаТоваров].Реквизиты.Найти("ЗаказПоставщику") <> Неопределено);
	Если флЕстьЗаказПоставщикуВСтроках Тогда
		СтруктураОтбораСтрок.Вставить("ЗаказПоставщику");
	КонецЕсли;
	
	Если ВариантВызова = "ЗаказНаряд" Тогда
		СтруктураОтбораСтрок.Вставить("СкладКомпании");
	КонецЕсли;
	
	//перебираем результат запроса
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа = Истина Тогда
			НоваяСтрока = СписокНоменклатуры.Добавить();
			НоваяСтрока.Код = Выборка.НоменклатураКод;
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			НоваяСтрока.ПереходитьВВерх = Ложь;
		Иначе
			ГлавнаяКолонка = Выборка[ИмяГлавнойКолонки];
			
			//смотрим есть ли уже подобранная ном. в ТЧ документа... 
			СтруктураОтбораСтрок.Номенклатура = Выборка.Номенклатура;
			СтруктураОтбораСтрок.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
			Если флЕстьЗаказПокупателяВСтроках Тогда
				СтруктураОтбораСтрок.ЗаказПокупателя = Выборка.Заказ;
			КонецЕсли;
			
			Если флЕстьЗаказПоставщикуВСтроках Тогда
				СтруктураОтбораСтрок.ЗаказПоставщику = Выборка.Заказ;
			КонецЕсли;
			
			Если ВариантВызова = "ЗаказНаряд" Тогда
				СтруктураОтбораСтрок.СкладКомпании = Выборка.СкладКомпании;
			КонецЕсли;
			
			МассивСтрок = тчТовары.НайтиСтроки(СтруктураОтбораСтрок);
			//...если есть, то уменьшаем выводимый остаток
			Для каждого СтрокаТабличнойЧасти Из МассивСтрок Цикл
				ГлавнаяКолонка = ГлавнаяКолонка - СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Коэффициент;
			КонецЦикла;
			
			//если в главной колонке количество ноль, то пропустим
			Если ГлавнаяКолонка <= 0 Тогда Продолжить; КонецЕсли; 
						
			НоваяСтрока = СписокНоменклатуры.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			НоваяСтрока.Код = Выборка.НоменклатураКод;
			НоваяСтрока.Артикул = Выборка.НоменклатураАртикул;
			НоваяСтрока.АртикулДляПоиска = Выборка.НоменклатураАртикулДляПоиска;
			НоваяСтрока.Производитель    = Выборка.НоменклатураПроизводитель;
			НоваяСтрока[ИмяГлавнойКолонки] = ГлавнаяКолонка;
			
			Если ВариантВызова = "СнятиеРаспределенияЗаказовПокупателя" Тогда
				Если Выборка.ЗаказПоставщику = NULL Тогда
					НоваяСтрока.ЗаказПоставщикуПредставление = "";
				Иначе
					НоваяСтрока.ЗаказПоставщикуПредставление = "№" + Строка(Выборка.ЗаказПоставщикуНомер) + " от "
					+ Формат(Выборка.ЗаказПоставщикуДата," дд.ММ.гггг");
				КонецЕсли;
			КонецЕсли;
			
			Если Выборка.Заказ = NULL Тогда
				НоваяСтрока.ЗаказПредставление = "";
			Иначе
				НоваяСтрока.ЗаказПредставление = "№" + Строка(Выборка.ЗаказНомер) + " от "
				+ Формат(Выборка.ЗаказДата," дд.ММ.гггг") + " " + Выборка.ЗаказПредставлениеПолучателя;
			КонецЕсли; 
			
			НоваяСтрока.ПереходитьВВерх = Ложь;
		КонецЕсли; 
	КонецЦикла;
	
	//обновим надпись внизу
	ОбновитьНадписьОтбор(ЭтаФорма);
КонецПроцедуры // ПерезаполнитьТаблицуНоменклатуры()

// Обновляет надпись внизу формы
//
// Параметры:
//  ЭтаФорма - ссылка на форму.
//
Процедура ОбновитьНадписьОтбор(ЭтаФорма) Экспорт
	КоличествоНоменклатуры = СписокНоменклатуры.Количество();
		
	флХотяБыОдинЭлементИспользуется = Ложь;
	Для Каждого ЭлементОтбора Из ЭтаФорма.Фильтр.Отбор Цикл
		Если ЭлементОтбора.Использование Тогда
			флХотяБыОдинЭлементИспользуется = Истина;
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	Если флХотяБыОдинЭлементИспользуется Тогда
		ЭтаФорма.ТекстСостояниеОтбора = "Отбор установлен. Отобрано: " + Формат(КоличествоНоменклатуры, "ЧЦ=15; ЧДЦ=0; ЧН=0") + " позиций";
	ИначеЕсли НЕ ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатуры.Кнопки.ИерархическийПросмотр.Пометка Тогда
		ЭтаФорма.ТекстСостояниеОтбора = "Отбор не установлен. Всего: " + Формат(КоличествоНоменклатуры, "ЧЦ=15; ЧДЦ=0; ЧН=0") + " позиций";
	Иначе
		ЭтаФорма.ТекстСостояниеОтбора = "Отбор не установлен.";
	КонецЕсли;
КонецПроцедуры // ОбновитьНадписьОтбор()

Функция ПолучитьПараметрыСтруктурыСвойств(Представление, ЗначениеОбъекта, ВидСравнения)
	
	Возврат Новый Структура("Представление, ЗначениеОбъекта, ВидСравнения", Представление, ЗначениеОбъекта, ВидСравнения);
	
КонецФункции


ВариантВызова = "";
СтруктураПараметровЗапроса = Новый Структура;
СтруктураПараметровВиртуальныхТаблиц = Новый Структура;

СтруктураСвойств = Новый Структура;
СтруктураСвойств.Вставить("ШиныВысота", ПолучитьПараметрыСтруктурыСвойств("Высота шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныВысота, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныШирина", ПолучитьПараметрыСтруктурыСвойств("Ширина шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныШирина, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныРадиус", ПолучитьПараметрыСтруктурыСвойств("Диаметр шины/диска", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныРадиус, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныПрофиль", ПолучитьПараметрыСтруктурыСвойств("Модель шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныПрофиль, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныНазначение", ПолучитьПараметрыСтруктурыСвойств("Назначение шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныНазначение, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныКонструкция", ПолучитьПараметрыСтруктурыСвойств("Конструкция шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныКонструкция, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныСезонность", ПолучитьПараметрыСтруктурыСвойств("Сезонность шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСезонность, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныИндексСкорости", ПолучитьПараметрыСтруктурыСвойств("Индекс скорости шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексСкорости, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныИндексНагрузки", ПолучитьПараметрыСтруктурыСвойств("Индекс нагрузки шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексНагрузки, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ШиныСпособГерметизации", ПолучитьПараметрыСтруктурыСвойств("Способ герметизации шины", ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСпособГерметизации, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ДискиШирина", ПолучитьПараметрыСтруктурыСвойств("Ширина диска", ПланыВидовХарактеристик.СвойстваОбъектов.ДискиШирина, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ДискиМодель", ПолучитьПараметрыСтруктурыСвойств("Модель диска", ПланыВидовХарактеристик.СвойстваОбъектов.ДискиМодель, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ДискиКоличествоОтверстий", ПолучитьПараметрыСтруктурыСвойств("Количество отверстий диска", ПланыВидовХарактеристик.СвойстваОбъектов.ДискиКоличествоОтверстий, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ДискиРасстояниеМеждуОтверстиями", ПолучитьПараметрыСтруктурыСвойств("Расстояние между отверстиями", ПланыВидовХарактеристик.СвойстваОбъектов.ДискиРасстояниеМеждуОтверстиями, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ДискиВылет", ПолучитьПараметрыСтруктурыСвойств("Вылет диска", ПланыВидовХарактеристик.СвойстваОбъектов.ДискиВылет, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ДискиДиаметрСтупицы", ПолучитьПараметрыСтруктурыСвойств("Диаметр ступицы", ПланыВидовХарактеристик.СвойстваОбъектов.ДискиДиаметрСтупицы, ВидСравнения.Равно));
СтруктураСвойств.Вставить("ДискиТип", ПолучитьПараметрыСтруктурыСвойств("Тип диска", ПланыВидовХарактеристик.СвойстваОбъектов.ДискиШирина, ВидСравнения.Равно));

#Если Клиент Тогда
// процедура заполнения таблицы дополнительных реквизитов
//
// Параметр:
//	Номенклатура	- СправочникСсылка	- Номенклатура
//
Процедура ЗаполнитьТаблицуДополнительныхРеквизитов(Номенклатура=Неопределено) Экспорт
	Если Номенклатура<>Неопределено Тогда
		Объект=Номенклатура;
	КонецЕсли;
	Состав.Очистить();
	Если Объект<>Неопределено Тогда
		Запрос=Новый Запрос();
		Запрос.Текст="ВЫБРАТЬ
		             |	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ДополнительныйРеквизит,
		             |	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ЗначениеРеквизита,
		             |	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ПодразделениеКомпании,
		             |	ДополнительныеРеквизитыНоменклатурыСрезПоследних.Период
		             |ИЗ
		             |	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних(, Номенклатура = &Номенклатура) КАК ДополнительныеРеквизитыНоменклатурыСрезПоследних";
		Запрос.УстановитьПараметр("Номенклатура",Объект);
		Состав.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли; 
КонецПроцедуры // ЗаполнитьТаблицуДополнительныхРеквизитов()

// функция сохранение таблицы дополнительных реквизитов
//
// Возвращаемое значение:
//	Булево	- Результат сохранения
//
Функция СохранитьТаблицуДополнительныхРеквизитов() Экспорт
	Если Объект=Неопределено Тогда
		Возврат Ложь;
	Иначе
		//Получим список дополнительных реквизитов для данной номенклатуры
		Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	ДополнительныеРеквизитыНоменклатуры.ДополнительныйРеквизит,
			|	ДополнительныеРеквизитыНоменклатуры.ПодразделениеКомпании,
			|	ДополнительныеРеквизитыНоменклатуры.Период
			|ИЗ
			|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры КАК ДополнительныеРеквизитыНоменклатуры
			|ГДЕ
			|	ДополнительныеРеквизитыНоменклатуры.Номенклатура = &Номенклатура
			|");
		Запрос.УстановитьПараметр("Номенклатура",Объект);
		Выборка = Запрос.Выполнить().Выбрать();
		//Структура для поиска дополнительных реквизитов
		СтруктураОтбораСостава = Новый Структура;
		Пока Выборка.Следующий() Цикл
			СтруктураОтбораСостава.Вставить("ДополнительныйРеквизит",Выборка.ДополнительныйРеквизит);
			СтруктураОтбораСостава.Вставить("ПодразделениеКомпании",Выборка.ПодразделениеКомпании);
			СтруктураОтбораСостава.Вставить("Период",Выборка.Период);
			Если Состав.НайтиСтроки(СтруктураОтбораСостава).Количество()=0 Тогда
				//Если записи из регистра нет в таблице значений, значит она была удалена
				МенеджерЗаписиРегистра = РегистрыСведений.ДополнительныеРеквизитыНоменклатуры.СоздатьМенеджерЗаписи();
				МенеджерЗаписиРегистра.Номенклатура = Объект;
				МенеджерЗаписиРегистра.ПодразделениеКомпании = Выборка.ПодразделениеКомпании;
				МенеджерЗаписиРегистра.ДополнительныйРеквизит = Выборка.ДополнительныйРеквизит;
				МенеджерЗаписиРегистра.Период = Выборка.Период;
				МенеджерЗаписиРегистра.Удалить();
			КонецЕсли; 
		КонецЦикла; 
		//Записываем новые значения дополнительных параметров
		Для каждого ДополнительныйРеквизит Из Состав Цикл
			Если обЗначениеНеЗаполнено(ДополнительныйРеквизит.ДополнительныйРеквизит) Тогда
				Сообщить("Значение поля Дополнительный реквизит не может быть пустым!");
				Возврат Ложь;
			КонецЕсли;
			МенеджерЗаписиРегистра = РегистрыСведений.ДополнительныеРеквизитыНоменклатуры.СоздатьМенеджерЗаписи();
			МенеджерЗаписиРегистра.Номенклатура = Объект;
			МенеджерЗаписиРегистра.ПодразделениеКомпании = ДополнительныйРеквизит.ПодразделениеКомпании;
			МенеджерЗаписиРегистра.ДополнительныйРеквизит = ДополнительныйРеквизит.ДополнительныйРеквизит;
			МенеджерЗаписиРегистра.ЗначениеРеквизита = ДополнительныйРеквизит.ЗначениеРеквизита;
			Если НЕ обЗначениеНеЗаполнено(ДополнительныйРеквизит.Период) Тогда
				МенеджерЗаписиРегистра.Период = ДополнительныйРеквизит.Период;
			Иначе
				МенеджерЗаписиРегистра.Период = РабочаяДата;
			КонецЕсли; 
			МенеджерЗаписиРегистра.Записать(Истина);
		КонецЦикла; 
	КонецЕсли;
	
	Возврат Истина;
КонецФункции // СохранитьТаблицуДополнительныхРеквизитов()

#КонецЕсли
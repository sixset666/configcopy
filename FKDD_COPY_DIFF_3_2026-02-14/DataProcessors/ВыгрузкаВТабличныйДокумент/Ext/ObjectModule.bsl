#Если Клиент Тогда

// процедура выгрузки в табличный документ
//
// Параметр:
//	ТабличныйДокумент	-	ТабличныйДокумент	- Табличный документ
//
Процедура ВыгрузитьВТабличныйДокумент(ТабличныйДокумент=Неопределено, Показать=Истина) Экспорт
	
	Если ТабличныйДокумент = Неопределено Тогда
		ТабличныйДокумент = Новый ТабличныйДокумент;
	КонецЕсли;
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.НачатьАвтоГруппировкуСтрок();
	
	// Получим соответствующий объект метаданных.
	МетаданныеОбъекта = ОбъектСсылка.Метаданные();
	// Получим класс объекта.
	КлассОбъекта = сбСобытия.ПолучитьКлассОбъектаМетаданных(ОбъектСсылка.Метаданные());
	
	// ЗАГОЛОВОК
	Макет = ПолучитьМакет("Макет");		
	Если КлассОбъекта="Справочники" Тогда
		ОбластьЗаголовок = Макет.ПолучитьОбласть("ДляСправочников");
		ОбластьЗаголовок.Параметры.Код          = Строка(ОбъектСсылка.Код);
		ОбластьЗаголовок.Параметры.Наименование = Строка(ОбъектСсылка.Наименование); 
	Иначе		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.Объект = Строка(ОбъектСсылка);		
	КонецЕсли;	
	ТабличныйДокумент.Вывести(ОбластьЗаголовок,1);
	
	// РЕКВИЗИТЫ ОБЪЕКТА
	РеквизитыОбъекта     = МетаданныеОбъекта.Реквизиты;
	КоличествоРеквизитов = РеквизитыОбъекта.Количество();
	
	Если КоличествоРеквизитов>0 Тогда
		ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокРеквизитов");
		ТабличныйДокумент.Вывести(ОбластьЗаголовок,1);
		
		Область = Макет.ПолучитьОбласть("Шапка|ОтступСлева");
		ТабличныйДокумент.Вывести(Область,2);
		
		Область = Макет.ПолучитьОбласть("Шапка|Реквизит");
		Область.Параметры.Значение = "Имя реквизита";

		ТабличныйДокумент.Присоединить(Область,2);
		
		Область.Параметры.Значение = "Значение реквизита";
		ТабличныйДокумент.Присоединить(Область,2);
		
		// Выводим значения реквизитов объекта
		ОбластьОтступСлева    = Макет.ПолучитьОбласть("Строка|ОтступСлева"); 		
		ОбластьСтрокаРеквизит = Макет.ПолучитьОбласть("Строка|Реквизит");
		Область = ОбластьСтрокаРеквизит.Область();
		Область.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
		Для Каждого Реквизит Из РеквизитыОбъекта Цикл
			
			ТабличныйДокумент.Вывести(ОбластьОтступСлева,2); 				
					
			ОбластьСтрокаРеквизит.Параметры.Значение = Реквизит.Синоним;
			ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит,2);
									
			ОбластьСтрокаРеквизит.Параметры.Значение = ОбъектСсылка[Реквизит.Имя];
			ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит,2);
		КонецЦикла;	
	КонецЕсли;	
	
	// ТАБЛИЧНЫЕ ЧАСТИ ОБЪЕКТА
	ТабличныеЧастиОбъекта     = МетаданныеОбъекта.ТабличныеЧасти;
	КоличествоТабличныхЧастей = ТабличныеЧастиОбъекта.Количество();
	
	Если КоличествоТабличныхЧастей>0 Тогда
		ОбластьЗаголовокТабЧасти    = Макет.ПолучитьОбласть("ЗаголовокТабЧасть");
		ОбластьШапкаОтступСлева     = Макет.ПолучитьОбласть("Шапка|ОтступСлева");
		ОбластьШапкаРеквизит        = Макет.ПолучитьОбласть("Шапка|Реквизит");
		ОбластьСтрокаОтступСлева    = Макет.ПолучитьОбласть("Строка|ОтступСлева");
		ОбластьСтрокаРеквизит       = Макет.ПолучитьОбласть("Строка|Реквизит");
		ОбластьРазделительТабЧастей = Макет.ПолучитьОбласть("РазделительТабЧастей");
		Для Каждого ТабличнаяЧасть Из ТабличныеЧастиОбъекта Цикл
			ОбластьЗаголовокТабЧасти.Параметры.ИмяТабличнойЧасти = ТабличнаяЧасть.Синоним;
			ТабличныйДокумент.Вывести(ОбластьЗаголовокТабЧасти,1);
			
			ТабличнаяЧастьОбъекта = ОбъектСсылка[ТабличнаяЧасть.Имя]; 			
			РеквизитыТабЧасти     = ТабличнаяЧасть.Реквизиты;
			
			ТабличныйДокумент.Вывести(ОбластьШапкаОтступСлева,2);
			
			// Выводим шапку табличной части 			
			Для Каждого Реквизит Из РеквизитыТабЧасти Цикл 				
				ОбластьШапкаРеквизит.Параметры.Значение = Реквизит.Синоним;
				ТабличныйДокумент.Присоединить(ОбластьШапкаРеквизит,2);
			КонецЦикла;
			
			// Выводим строки табличной части.
			Для Каждого ТекСтрокаТабличнойЧасти Из ТабличнаяЧастьОбъекта Цикл 																		
				ТабличныйДокумент.Вывести(ОбластьСтрокаОтступСлева,2);								
				Для Каждого Реквизит Из РеквизитыТабЧасти Цикл
					ОбластьСтрокаРеквизит.Параметры.Значение = ТекСтрокаТабличнойЧасти[Реквизит.Имя];
					ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит,2);
				КонецЦикла;
			КонецЦикла;				
			
			 // Пропуск между табличными частями.
			ТабличныйДокумент.Вывести(ОбластьРазделительТабЧастей);	
		КонецЦикла;
	КонецЕсли;
	
	// ДВИЖЕНИЯ ДОКУМЕНТА
	Если КлассОбъекта="Документы" Тогда
		ОбластьЗаголовокНабораЗаписей = Макет.ПолучитьОбласть("ЗаголовокНаборЗаписей");
		ОбластьШапкаОтступСлева       = Макет.ПолучитьОбласть("Шапка|ОтступСлева");
		ОбластьШапкаИзмерение         = Макет.ПолучитьОбласть("ШапкаИзмерение|Реквизит");
		ОбластьШапкаРесурс            = Макет.ПолучитьОбласть("ШапкаРесурс|Реквизит");
		ОбластьШапкаРеквизит          = Макет.ПолучитьОбласть("ШапкаРеквизит|Реквизит");  		
		ОбластьСтрокаОтступСлева      = Макет.ПолучитьОбласть("Строка|ОтступСлева");
		ОбластьСтрокаРеквизит         = Макет.ПолучитьОбласть("Строка|Реквизит");
		ОбластьРазделительТабЧастей   = Макет.ПолучитьОбласть("РазделительТабЧастей");
		
		ОбластьЯчеекРесурса = ОбластьСтрокаРеквизит.Область();

		
		// Получим объект.
		Попытка
			ОбъектДокумент = ОбъектСсылка.ПолучитьОбъект();
			
			// Проходимся по движения документа.
			Для Каждого ТекНаборЗаписей Из ОбъектДокумент.Движения Цикл  	
				МетаданныеНаборЗаписей = ТекНаборЗаписей.Метаданные();			
				ОбластьЗаголовокНабораЗаписей.Параметры.ИмяРегистра = МетаданныеНаборЗаписей.Синоним;
				ТабличныйДокумент.Вывести(ОбластьЗаголовокНабораЗаписей, 1);  				
				ТабличныйДокумент.Вывести(ОбластьШапкаОтступСлева , 1);		
				
				КлассНабораЗаписей = сбСобытия.ПолучитьКлассОбъектаМетаданных(МетаданныеНаборЗаписей);
				
				Если (КлассНабораЗаписей="РегистрыНакопления") И МетаданныеНаборЗаписей.ВидРегистра=Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
					ЕстьВидДвижения = Истина;
				Иначе
					ЕстьВидДвижения = Ложь;
				КонецЕсли;
				
				// Выводим доп. поля.
				Если ЕстьВидДвижения Тогда
					ОбластьШапкаИзмерение.Параметры.Значение = "Вид движения";
					ТабличныйДокумент.Присоединить(ОбластьШапкаИзмерение);		
				КонецЕсли;
				
				ОбластьШапкаИзмерение.Параметры.Значение = "Активность";
				ТабличныйДокумент.Присоединить(ОбластьШапкаИзмерение);
				
				ОбластьШапкаИзмерение.Параметры.Значение = "Номер строки";				
				ТабличныйДокумент.Присоединить(ОбластьШапкаИзмерение);
				
				ОбластьШапкаИзмерение.Параметры.Значение = "Период";				
				ТабличныйДокумент.Присоединить(ОбластьШапкаИзмерение); 
								
				Измерения = МетаданныеНаборЗаписей.Измерения;
				Ресурсы   = МетаданныеНаборЗаписей.Ресурсы;
				Реквизиты = МетаданныеНаборЗаписей.Реквизиты;
				
				// Формируем шапку набора записей.
				Для Каждого ТекИзмерение Из Измерения Цикл  	
					ОбластьШапкаИзмерение.Параметры.Значение = ТекИзмерение.Синоним;
					ТабличныйДокумент.Присоединить(ОбластьШапкаИзмерение);
				КонецЦикла;
				Для Каждого ТекРесурс Из Ресурсы Цикл  	
					ОбластьШапкаРесурс.Параметры.Значение = ТекРесурс.Синоним;
					ТабличныйДокумент.Присоединить(ОбластьШапкаРесурс);					
				КонецЦикла;
				Для Каждого ТекРеквизит Из Реквизиты Цикл  	
					ОбластьШапкаРеквизит.Параметры.Значение = ТекРеквизит.Синоним;
					ТабличныйДокумент.Присоединить(ОбластьШапкаРеквизит);  					
				КонецЦикла;
				
				// Теперь проходимся по записям набора записей.
				ТекНаборЗаписей.Прочитать();
				Если ТекНаборЗаписей.Количество()=0 Тогда
					Продолжить;
				КонецЕсли;
								
				Для Каждого ТекЗапись Из ТекНаборЗаписей Цикл  	
					ТабличныйДокумент.Вывести(ОбластьСтрокаОтступСлева, 1);
					
					Если ЕстьВидДвижения Тогда
						ОбластьСтрокаРеквизит.Параметры.Значение = ТекЗапись["ВидДвижения"];
						ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит);						
					КонецЕсли;
					
					ОбластьСтрокаРеквизит.Параметры.Значение = ТекЗапись["Активность"];
					ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит);
					ОбластьСтрокаРеквизит.Параметры.Значение = ТекЗапись["НомерСтроки"];
                    ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит);
					ОбластьСтрокаРеквизит.Параметры.Значение = ТекЗапись["Период"];
                    ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит);    
					
					Для Каждого ТекИзмерение Из Измерения Цикл  	
						ОбластьСтрокаРеквизит.Параметры.Значение = ТекЗапись[ТекИзмерение.Имя];
						ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит);
					КонецЦикла;
					
					ОбластьЯчеекРесурса.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
					Для Каждого ТекРесурс Из Ресурсы Цикл  	
						ОбластьСтрокаРеквизит.Параметры.Значение = ТекЗапись[ТекРесурс.Имя];
						ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит);					
					КонецЦикла;
					
					ОбластьЯчеекРесурса.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
					Для Каждого ТекРеквизит Из Реквизиты Цикл  	
						ОбластьСтрокаРеквизит.Параметры.Значение = ТекЗапись[ТекРеквизит.Имя];
						ТабличныйДокумент.Присоединить(ОбластьСтрокаРеквизит);  					
					КонецЦикла;	
				КонецЦикла;
				
				// Пропуск.
				ТабличныйДокумент.Вывести(ОбластьРазделительТабЧастей);	 				
			КонецЦикла;
		Исключение
		КонецПопытки
	КонецЕсли;
	
	ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
	ТабличныйДокумент.ОтображатьГруппировки = Истина;
	ТабличныйДокумент.ОтображатьЗаголовки   = Ложь;
	ТабличныйДокумент.ОтображатьСетку       = Ложь;
	ТабличныйДокумент.ТолькоПросмотр        = Истина;
	ТабличныйДокумент.Автомасштаб           = Истина;
	Если Показать Тогда
		ТабличныйДокумент.Показать("Выгрузка в табличный документ: "+ Строка(ОбъектСсылка)+"("+ОбъектСсылка.Метаданные().Имя+")");
	КонецЕсли;
	
КонецПроцедуры // СформироватьЖурналРегистрации() Экспорт
#КонецЕсли
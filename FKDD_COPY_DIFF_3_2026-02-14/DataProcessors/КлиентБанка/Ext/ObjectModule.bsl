#Если Клиент Тогда
Перем Текст; 							// Текстовый документ обмена
Перем ДатаДокумента;					// Дата создания файла загрузки
Перем ОбязательныеАтрибутыЗАГРУЗКИ;     // Обязательные атрибуты загрузки
Перем ОбязательныеВрегЗАГРУЗКИ;         // Обязательные реквизиты в верхнем регистре
Перем ОбязательныеАтрибутыВЫГРУЗКИ;		// Обязательные реквизиты в файле обмена
Перем ОбязательныеАтрибутыБЮДЖЕТ;		// Обязательные реквизиты в файле обмена
Перем ОбязательныеВрегВЫГРУЗКИ;			// Обязательные реквизиты в файле обмена в верхнем регистре
Перем ОбязательныеВрегБЮДЖЕТ;			// Обязательные реквизиты в файле обмена в верхнем регистре
Перем ПорядокРеквизитов,РеквизитыВрег;  // Порядок реквизитов и реквизиты в верхнем регистре
Перем РасчетныеСчета;                   // Расчетные счета
Перем НеНайденныеКонтрагенты Экспорт;   // Не найденные контрагенты
Перем НачДатаЗагрузки;					// Начальная дата загрузки
Перем КонДатаЗагрузки;      			// Конечная дата загрузки
Перем ТаблицаРеквизитов;                // Таблица регистра
Перем СписокДокументов;                 // Список документов
Перем ПустаяДата;						// Пустая дата ("00010101")
Перем ЕстьНезаполненныеПоля;            // Флаг наличия незаполненных полей
Перем ЗагрДокументы;                    // Загруженные документы
Перем ПустыеПоляДокумента Экспорт;      // Незаполненные обязательные поля для выгрузки
Перем МассивДокументов Экспорт;         // Выбранные для экспорта документы
Перем ТаблицаСоответствий Экспорт;      // Таблица соответствий заменяемых контрагентов
Перем СоздаватьДокументБезКонтрагентов; // Разрешить создание документа без контрагентов

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Загружает секцию документа
Функция ЗагрузитьСекциюДокумента(Ном);
	Стр=Текст.ПолучитьСтроку(Ном);
	Если Врег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ПЛАТЕЖНОЕ ПОРУЧЕНИЕ" И
		Врег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ПЛАТЕЖНОЕ ТРЕБОВАНИЕ" И
		Врег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ВАЛЮТНЫЙ ПЕРЕВОД" И
		Врег(Стр) <> "СЕКЦИЯДОКУМЕНТ=МЕМОРИАЛЬНЫЙ ОРДЕР" И
		Врег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ДЕНЕЖНЫЙ ЧЕК" И
		Врег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ОБЪЯВЛЕНИЕ НА ВЗНОС НАЛИЧНЫМИ" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=КАССОВЫЙ ДОКУМЕНТ" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ВНУТРЕННИЙ ПЕРЕВОД" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=БАНКОВСКИЙ ОРДЕР" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ПЛАТЕЖНЫЙ ОРДЕР" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ИНКАССОВОЕ ПОРУЧЕНИЕ" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ДОКУМЕНТ ПО БАНКОВСКОЙ КАРТЕ" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=РАСХОДНЫЙ КАССОВЫЙ ЧЕК" И
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ПРОЧЕЕ" И // для непонятных ситуаций
		ВРег(Стр) <> "СЕКЦИЯДОКУМЕНТ=ПРОЧИЕ" Тогда
		Для Ном=Ном+1 По Текст.КоличествоСтрок() Цикл
			Стр=Текст.ПолучитьСтроку(Ном);
			Если Лев(Врег(СокрЛП(Стр)), 14)="КОНЕЦДОКУМЕНТА" Тогда
				Возврат Ном;
			КонецЕсли;
		КонецЦикла
	КонецЕсли;
		
	ТаблицаРеквизитов = Новый Соответствие;
	Для Ном=Ном+1 По Текст.КоличествоСтрок() Цикл
		Стр=Текст.ПолучитьСтроку(Ном);
		Если Лев(Врег(СокрЛП(Стр)), 14)="КОНЕЦДОКУМЕНТА" Тогда
			Прервать;
		Иначе
			ЕстьЗнакРавно=Найти(Стр, "=");
			Имя=Врег(СокрЛП(Лев(Стр, ЕстьЗнакРавно-1)));
			Значение=Сред(Стр, ЕстьЗнакРавно+1);
			Если ЕстьЗнакРавно=0 Тогда
				Сообщить("Ошибка: строка " + Ном + " """+Стр+"""," + "не найден символ ""=""");
			Иначе
				ТаблицаРеквизитов.Вставить(Имя,СокрЛП(Значение));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//Проверим обязательные реквизиты
	Для Каждого ОбязательныйРеквизит Из ОбязательныеВрегЗАГРУЗКИ Цикл
		Если ТаблицаРеквизитов[ОбязательныйРеквизит.Значение]=Неопределено Тогда
			//может быть или "ДАТАПОСТУПИЛО" или "ДАТАСПИСАНО", но оба обязательны
		    //так что проверяем их отдельно, после цикла
			Если (ОбязательныйРеквизит.Значение = "ДАТАПОСТУПИЛО") ИЛИ (ОбязательныйРеквизит.Значение = "ДАТАСПИСАНО") ИЛИ
				(ОбязательныйРеквизит.Значение = "ПОЛУЧАТЕЛЬИНН") ИЛИ (ОбязательныйРеквизит.Значение = "ПЛАТЕЛЬЩИКИНН")
			Тогда Продолжить; КонецЕсли;
			
			Сообщить("В файле загрузки отсутствует обязательное поле: "+ОбязательныйРеквизит.Значение);
			ЕстьНезаполненныеПоля=Истина;
		КонецЕсли;
	КонецЦикла;
	
	//Проверим счет получателя = счет организации
	ВидДвижения="";
	
	СчетОрганизации = Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета",ТаблицаРеквизитов["ПОЛУЧАТЕЛЬСЧЕТ"],,Организация);
	Если НЕ СчетОрганизации=Справочники.БанковскиеСчета.ПустаяСсылка() Тогда
		
		// Проверим счет организации получателя
		СчетОрганизацииПолучатель=Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета",ТаблицаРеквизитов["ПЛАТЕЛЬЩИКСЧЕТ"],,Организация);
		
		Если НЕ СчетОрганизацииПолучатель=Справочники.БанковскиеСчета.ПустаяСсылка() Тогда
			ВидДвижения = "ПеремещениеМеждуРасчетнымиСчетами";
			ТипКонтрагента = "НЕОПРЕДЕЛЕН";
			Если ТаблицаРеквизитов["ПОЛУЧАТЕЛЬИНН"]=Неопределено Тогда
				Сообщить("В файле загрузки отсутствует обязательное поле: ПОЛУЧАТЕЛЬИНН");
				ЕстьНезаполненныеПоля=Истина;
			КонецЕсли;
		Иначе
			ВидДвижения = "Поступление";
			ТипКонтрагента = "ПЛАТЕЛЬЩИК";
			Если ТаблицаРеквизитов["ПЛАТЕЛЬЩИКИНН"]=Неопределено Тогда
				Сообщить("В файле загрузки отсутствует обязательное поле: ПЛАТЕЛЬЩИКИНН");
				ЕстьНезаполненныеПоля=Истина;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		СчетОрганизации=Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета",ТаблицаРеквизитов["ПЛАТЕЛЬЩИКСЧЕТ"],,Организация);
		
		Если НЕ СчетОрганизации=Справочники.БанковскиеСчета.ПустаяСсылка() Тогда
			
			ВидДвижения = "Платеж";
			ТипКонтрагента = "ПОЛУЧАТЕЛЬ";
			Если ТаблицаРеквизитов["ПОЛУЧАТЕЛЬИНН"]=Неопределено Тогда
				Сообщить("В файле загрузки отсутствует обязательное поле: ПОЛУЧАТЕЛЬИНН");
				ЕстьНезаполненныеПоля=Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
    //
	//Если НЕ РасчетныеСчета.НайтиПоЗначению(ТаблицаРеквизитов["ПОЛУЧАТЕЛЬСЧЕТ"])=Неопределено Тогда
	//	ВидДвижения = "Поступление";
	//	ТипКонтрагента = "ПЛАТЕЛЬЩИК";
	//Иначе
	//	Если НЕ РасчетныеСчета.НайтиПоЗначению(ТаблицаРеквизитов["ПЛАТЕЛЬЩИКСЧЕТ"])=Неопределено Тогда
	//		ВидДвижения = "Платеж";
	//		ТипКонтрагента = "ПОЛУЧАТЕЛЬ";
	//	КонецЕсли;
	//КонецЕсли;
	
	Если ВидДвижения = "" Тогда
		Сообщить("В файле загрузки в документе № "+ТаблицаРеквизитов["НОМЕР"]+" ни счет плательщика, ни счет получателя не принадлежат выбранной организации. Документ не может быть записан");
		ЕстьНезаполненныеПоля = Истина;
	Иначе
		ТаблицаРеквизитов.Вставить("ВИДДВИЖЕНИЯ",ВидДвижения);
		ТаблицаРеквизитов.Вставить("ТИПКОНТРАГЕНТА",ТипКонтрагента);
	КонецЕсли;
	
	// Произведем проверку заполненности одного из реквизитов даты
	ИмяРеквизитаДаты = ?(ВидДвижения = "Поступление","ДАТАПОСТУПИЛО", "ДАТАСПИСАНО");
	Если (НЕ ПустаяСтрока(ВидДвижения))И(ТаблицаРеквизитов[ИмяРеквизитаДаты] = Неопределено) Тогда
		ТаблицаРеквизитов[ИмяРеквизитаДаты] = ПустаяДата;
		//Сообщить("В файле загрузки отсутствует обязательное поле: "+ИмяРеквизитаДаты);
		//ЕстьНезаполненныеПоля = ИСТИНА;
	КонецЕсли;
	
	СписокДокументов.Добавить(ТаблицаРеквизитов);
	Возврат Ном;
КонецФункции

// Перевод даты из формата ДД.ММ.ГГГГ -> Дата(ГГГГММДД)
Функция ФорматДаты(Стр)
	Если (Стр = Неопределено) ИЛИ (ПустаяСтрока(Стр)) Тогда
		Возврат ПустаяДата;
	КонецЕсли;	
	День    = Лев(Стр,2);
	Месяц   = Сред(Стр,4,2);
	Год     = Сред(Стр,7,4);
	Попытка 
		ВыходДата = Дата(Год+Месяц+День);
	Исключение
		Сообщить("Неверный формат даты [ДД.ММ.ГГГГ]: "+Стр);
		ВыходДата = ПустаяДата;
	КонецПопытки;	
	Возврат ВыходДата;
КонецФункции

// Загрузка заголовка файла обмена
Процедура ЗагрузитьСтрокуЗаголовка(Стр,Ном)
	ЕстьЗнакРавно=Найти(Стр, "=");
	Имя=Врег(СокрЛП(Лев(Стр, ЕстьЗнакРавно-1)));
	Значение=Сред(Стр, ЕстьЗнакРавно+1);
	Если ЕстьЗнакРавно=0 Тогда
		Сообщить("Ошибка: строка " + Ном + """"+Стр+"""," + "не найден символ ""=""");
	 ИначеЕсли Имя="ДАТАНАЧАЛА" Тогда
		НачДатаЗагрузки=ФорматДаты(СокрЛП(Значение));
	ИначеЕсли Имя="ДАТАКОНЦА" Тогда
		КонДатаЗагрузки=ФорматДаты(СокрЛП(Значение));
	ИначеЕсли Имя="РАСЧСЧЕТ" Тогда
		Значение=СокрЛП(Значение);
		Если Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета",Значение,,Организация).Пустая() Тогда
			Сообщить("В файле загрузки счет организации: "+Значение+". Не принадлежит организации: "+Организация);
			ЕстьНезаполненныеПоля=Истина;
			Возврат
		КонецЕсли;
		РасчетныеСчета.Добавить(Значение);
	КонецЕсли;
КонецПроцедуры

// Инициализация значениями глобальных списков
Процедура Инициализация()
	ОбязательныеАтрибутыЗАГРУЗКИ = Новый СписокЗначений;
	//для секции шапки платежного документа
	//ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("СекцияДокумент");
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("Номер");
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("Дата");
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("Сумма");
	//реквизиты плательщика
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("ПлательщикСчет");
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("ДатаСписано");
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("ПлательщикИНН");
	//реквизиты банка получателя (поставщика)
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("ПолучательСчет");
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("ДатаПоступило");
	ОбязательныеАтрибутыЗАГРУЗКИ.Добавить("ПолучательИНН");
	
	ОбязательныеВрегЗАГРУЗКИ = ОбязательныеАтрибутыЗАГРУЗКИ.Скопировать();
	Для Каждого Рекв Из ОбязательныеВрегЗАГРУЗКИ Цикл
		Рекв.Значение = ВРег(Рекв.Значение);
	КонецЦикла;
	
	//для секции шапки платежного документа
	ОбязательныеАтрибутыВЫГРУЗКИ = Новый СписокЗначений;
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("СекцияДокумент");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("Номер");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("Дата");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("Сумма");
	//реквизиты плательщика
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("ПлательщикСчет");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("ПлательщикИНН");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("Плательщик");
	//реквизиты банка получателя (поставщика)
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("ПолучательСчет");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("ПолучательИНН");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("Получатель");
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("ВидПлатежа");	
	ОбязательныеАтрибутыВЫГРУЗКИ.Добавить("ВидОплаты");
	
	ОбязательныеВрегВЫГРУЗКИ = ОбязательныеАтрибутыВЫГРУЗКИ.Скопировать();
	Для Каждого Рекв Из ОбязательныеВрегВЫГРУЗКИ Цикл
		Рекв.Значение = ВРег(Рекв.Значение);
	КонецЦикла;
	
	//дополнительные реквизиты для платежей в бюджет РФ
	ОбязательныеАтрибутыБЮДЖЕТ = Новый СписокЗначений;
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("СтатусСоставителя");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПлательщикКПП");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПолучательКПП");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПоказательКБК");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ОКАТО");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПоказательОснования");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПоказательПериода");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПоказательНомера");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПоказательДаты");
	ОбязательныеАтрибутыБЮДЖЕТ.Добавить("ПоказательТипа");
	
	ОбязательныеВрегБЮДЖЕТ = ОбязательныеАтрибутыБЮДЖЕТ.Скопировать();
	Для Каждого Рекв Из ОбязательныеВрегБЮДЖЕТ Цикл
		Рекв.Значение = ВРег(Рекв.Значение);
	КонецЦикла;
	
	//Порядок реквизитов секции документ в файле выгрузки
	ПорядокРеквизитов = Новый СписокЗначений;
	ПорядокРеквизитов.Добавить("СекцияДокумент");
	ПорядокРеквизитов.Добавить("Номер");
	ПорядокРеквизитов.Добавить("Дата");
	ПорядокРеквизитов.Добавить("Сумма");
	ПорядокРеквизитов.Добавить("КвитанцияДата");
	ПорядокРеквизитов.Добавить("КвитанцияВремя");
	ПорядокРеквизитов.Добавить("КвитанцияСодержание");
	ПорядокРеквизитов.Добавить("ПлательщикСчет");
	ПорядокРеквизитов.Добавить("ПлательщикИНН");
	ПорядокРеквизитов.Добавить("ПлательщикКПП");
	ПорядокРеквизитов.Добавить("Плательщик");
	ПорядокРеквизитов.Добавить("Плательщик1");
	ПорядокРеквизитов.Добавить("Плательщик2");
	ПорядокРеквизитов.Добавить("Плательщик3");
	ПорядокРеквизитов.Добавить("Плательщик4");
	ПорядокРеквизитов.Добавить("ПлательщикРасчСчет");
	ПорядокРеквизитов.Добавить("ПлательщикБанк1");
	ПорядокРеквизитов.Добавить("ПлательщикБанк2");
	ПорядокРеквизитов.Добавить("ПлательщикБИК");
	ПорядокРеквизитов.Добавить("ПлательщикКорсчет");
	ПорядокРеквизитов.Добавить("ПолучательСчет");
	ПорядокРеквизитов.Добавить("ПолучательИНН");
	ПорядокРеквизитов.Добавить("ПолучательКПП");
	ПорядокРеквизитов.Добавить("Получатель");
	ПорядокРеквизитов.Добавить("Получатель1");
	ПорядокРеквизитов.Добавить("Получатель2");
	ПорядокРеквизитов.Добавить("Получатель3");
	ПорядокРеквизитов.Добавить("Получатель4");
	ПорядокРеквизитов.Добавить("ПолучательРасчСчет");
	ПорядокРеквизитов.Добавить("ПолучательБанк1");
	ПорядокРеквизитов.Добавить("ПолучательБанк2");
	ПорядокРеквизитов.Добавить("ПолучательБИК");
	ПорядокРеквизитов.Добавить("ПолучательКорсчет");
	ПорядокРеквизитов.Добавить("ВидПлатежа");
	ПорядокРеквизитов.Добавить("СрокПлатежа");
	ПорядокРеквизитов.Добавить("Очередность");
	ПорядокРеквизитов.Добавить("ВидОплаты");
	ПорядокРеквизитов.Добавить("СтатусСоставителя");
	ПорядокРеквизитов.Добавить("ПоказательКБК");
	ПорядокРеквизитов.Добавить("ОКАТО");
	ПорядокРеквизитов.Добавить("ПоказательОснования");
	ПорядокРеквизитов.Добавить("ПоказательПериода");
	ПорядокРеквизитов.Добавить("ПоказательНомера");
	ПорядокРеквизитов.Добавить("ПоказательДаты");
	ПорядокРеквизитов.Добавить("ПоказательТипа");
	ПорядокРеквизитов.Добавить("НазначениеПлатежа");
	ПорядокРеквизитов.Добавить("НазначениеПлатежа1");
	ПорядокРеквизитов.Добавить("НазначениеПлатежа2");
	ПорядокРеквизитов.Добавить("НазначениеПлатежа3");
	ПорядокРеквизитов.Добавить("НазначениеПлатежа4");
	ПорядокРеквизитов.Добавить("НазначениеПлатежа5");
	ПорядокРеквизитов.Добавить("НазначениеПлатежа6");
	ПорядокРеквизитов.Добавить("СрокАкцепта");
	ПорядокРеквизитов.Добавить("ВидАккредитива");
	ПорядокРеквизитов.Добавить("УсловиеОплаты1");
	ПорядокРеквизитов.Добавить("УсловиеОплаты2");
	ПорядокРеквизитов.Добавить("УсловиеОплаты3");
	ПорядокРеквизитов.Добавить("ПлатежПоПредст");
	ПорядокРеквизитов.Добавить("ДопУсловия");
	ПорядокРеквизитов.Добавить("НомерСчетаПоставщика");
	ПорядокРеквизитов.Добавить("ДатаОтсылкиДок");
	ПорядокРеквизитов.Добавить("ДокументОснование");
	ПорядокРеквизитов.Добавить("Код");
	ПорядокРеквизитов.Добавить("КодНазПлатежа");
	
	РеквизитыВрег = ПорядокРеквизитов.Скопировать();
	Для Каждого Рекв Из РеквизитыВрег Цикл
		Рекв.Значение = ВРег(Рекв.Значение);
	КонецЦикла;
КонецПроцедуры

//проверяем кого нет в справочнике
Процедура ОтобратьКонтрагентов()
	Инициализация();
	Если НЕ ПрочитатьФайл(ФайлЗагрузки) Тогда
		Возврат;
	КонецЕсли;
	Для Ном=2 По Текст.КоличествоСтрок() Цикл
		Стр = Текст.ПолучитьСтроку(Ном);
		Если (ПустаяСтрока(Стр)) ИЛИ (Сред(СокрЛП(Стр), 1, 2) = "//") Тогда
			Продолжить;
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 10)="КОНЕЦФАЙЛА" Тогда
			Прервать;
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 14)="СЕКЦИЯДОКУМЕНТ" Тогда
			Ном=ЗагрузитьСекциюДокумента(Ном);
		ИначеЕсли ((Лев(Врег(СокрЛП(Стр)), 14)="СЕКЦИЯРАСЧСЧЕТ") ИЛИ (Лев(Врег(СокрЛП(Стр)), 13)="КОНЕЦРАСЧСЧЕТ")) Тогда
			Продолжить;
		Иначе
			ЗагрузитьСтрокуЗаголовка(Стр, Ном);
		КонецЕсли;	
	КонецЦикла;
	
	// Проверим все документы на принадлежность запрашиваемому периоду
	Исключаемые = Новый Массив();
	Для Каждого ТекДокумент Из СписокДокументов Цикл
		ИмяРеквизитаДаты = ?(ТекДокумент.Значение["ВИДДВИЖЕНИЯ"] = "Платеж" ИЛИ ТекДокумент.Значение["ВИДДВИЖЕНИЯ"] = "ПеремещениеМеждуРасчетнымиСчетами","ДАТАСПИСАНО","ДАТАПОСТУПИЛО");
		Если НЕ ЗначениеЗаполнено(ТекДокумент.Значение[ИмяРеквизитаДаты]) И ЗагрузкаПлатежейБезДаты Тогда
			Если Не (ДатаЗагрузкиОперации = Дата("00010101")) Тогда
				ТекДокумент.Значение.Вставить(ИмяРеквизитаДаты, ДатаЗагрузкиОперации);
			КонецЕсли;
		КонецЕсли;
		ДатаОперации = ФорматДаты(ТекДокумент.Значение[ИмяРеквизитаДаты]);
		Если (ДатаОперации < НачПериода)ИЛИ((ДатаОперации > КонПериода)И(КонПериода <> '00010101')) ИЛИ ДатаОперации = Дата("00010101") Тогда
			Исключаемые.Добавить(ТекДокумент);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекДокумент Из Исключаемые Цикл
		СписокДокументов.Удалить(ТекДокумент);
	КонецЦикла;
КонецПроцедуры

// Процедура читает заголовок файла обмена
Функция ПрочитатьФайл(Файл)
	Флаг =Истина;
	ДатаДокумента = Дата("00000000000000");
	ДатаДокументаСтрока = "00000000";
	ВремяДокументаСтрока = "000000";
	ФайлЗагр = Новый Файл(Файл);
	Если ФайлЗагр.Существует() = Ложь Тогда
		Сообщить("Файла "+Файл+" не существует!");
		Возврат Ложь;
	КонецЕсли;
	Текст = Новый ТекстовыйДокумент();
	Попытка
		Текст.Прочитать(Файл);
	Исключение
		Сообщить("Файл не прочитан.", СтатусСообщения.Внимание);
		Возврат Ложь;
	КонецПопытки;
	//Кодировка =ПолучитьКодировку(Текст);
	Если Кодировка = "DOS" Тогда
		Кодир = КодировкаТекста.OEM;
	Иначе
		Кодир = КодировкаТекста.ANSI;
	Конецесли;
	Текст.Прочитать(Файл,Кодир);
	Если Текст.КоличествоСтрок()<1 Тогда
		Сообщить("В файл нет данных!");
		Флаг =Ложь;
		Возврат Ложь;
	КонецЕсли;
	Если СокрЛП(Текст.ПолучитьСтроку(1))<>"1CClientBankExchange" Тогда
		Сообщить("Указанный файл не является файлом обмена или неверно указана кодировка!");
		Флаг =Ложь;
		Возврат Ложь;
	КонецЕсли;
	Для Ном = 2 По Текст.КоличествоСтрок() Цикл
		Стр = Текст.ПолучитьСтроку(Ном);
		Флаг =Ложь;
		Если Лев(Врег(СокрЛП(Стр)), 12)="ДАТАСОЗДАНИЯ" Тогда 
			ДатаДокументаСтрока=Сред(Врег(СокрЛП(Стр)),14);
			ДатаДокументаСтрока=Сред(ДатаДокументаСтрока,7,4)+Сред(ДатаДокументаСтрока,4,2)+Сред(ДатаДокументаСтрока,1,2);
			ДатаДокумента=Дата(ДатаДокументаСтрока+ВремяДокументаСтрока);
		КонецЕсли;
		Если Лев(Врег(СокрЛП(Стр)), 13)="ВРЕМЯСОЗДАНИЯ" Тогда 
			ВремяДокументаСтрока=Сред(Врег(СокрЛП(Стр)),15);
			ВремяДокументаСтрока=СтрЗаменить(ВремяДокументаСтрока,":","");
			Пока СтрДлина(ВремяДокументаСтрока)<6 Цикл
            	ВремяДокументаСтрока="0"+ВремяДокументаСтрока;
			КонецЦикла; 
			ДатаДокумента=Дата(ДатаДокументаСтрока+ВремяДокументаСтрока);
		КонецЕсли;
		Если Лев(Врег(СокрЛП(Стр)), 14)="СЕКЦИЯДОКУМЕНТ" Тогда 
			Флаг =Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Флаг;
КонецФункции

// Ищет контрагента по ИНН
//
Функция НайтиКонтрагента(ИННКраткий, КППКонтрагента, СчетКонтрагента, Представление)
	НашлиКонтрагента=Ложь;
	//поищем по ИНН и КПП если они не пустые
	Если НЕ ПустаяСтрока(ИННКраткий) И НЕ ПустаяСтрока(КППКонтрагента) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.КПП = &КПП
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("ИНН", СокрЛП(ИННКраткий));
		Запрос.УстановитьПараметр("КПП", СокрЛП(КППКонтрагента));
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Ссылка = Выборка.Ссылка;
		Иначе
			Ссылка = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
	Иначе 
		Ссылка = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	Если Ссылка.Пустая() И НЕ ПустаяСтрока(ИННКраткий) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("ИНН", СокрЛП(ИННКраткий));
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Ссылка = Выборка.Ссылка;
		Иначе
			Ссылка = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	Если Ссылка.Пустая() И ИскатьПоНомеруСчета Тогда
		//поищем по счёту
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	БанковскиеСчета.НомерСчета,
		|	БанковскиеСчета.Владелец
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.НомерСчета = &НомерСчета
		|	И БанковскиеСчета.Владелец ССЫЛКА Справочник.Контрагенты
		|	И БанковскиеСчета.Владелец.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("НомерСчета",СчетКонтрагента);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Владелец;
		КонецЕсли;
		
		// если не нашли посмотрим в таблице соответствий
		Если ИспользоватьЗамены И ТаблицаСоответствий.Количество() > 0 Тогда
			МассивСтрок=ТаблицаСоответствий.НайтиСтроки(Новый Структура("ТекущийКонтрагент",Представление));
			Если МассивСтрок.Количество() <> 0 Тогда
				КонтрагентЗамены = МассивСтрок[0].Замена;
				Если НЕ КонтрагентЗамены.Пустая() Тогда
					Возврат КонтрагентЗамены;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Ссылка;
КонецФункции

//Добавляет счет контрагента, если не находит его в базе
// Параметры:
//  ТекущийДокумент  Соответствие Реквизиты текущего загружаемого документа
//  Контрагент       Справочник.Контрагенты   Контрагент 
Процедура ДобавитьСчетКонтрагента(ТекущийДокумент, Контрагент)
	ТипКонтрагента = ТекущийДокумент.Значение["ТИПКОНТРАГЕНТА"];
	НомерСчета = ТекущийДокумент.Значение[ТипКонтрагента+"СЧЕТ"];
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	БанковскиеСчета.НомерСчета,
	|	БанковскиеСчета.Владелец
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.НомерСчета = &НомерСчета
	|	И БанковскиеСчета.Владелец = &Владелец";
	Запрос.УстановитьПараметр("НомерСчета",НомерСчета);
	Запрос.УстановитьПараметр("Владелец",Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	//Если счет не найден
	Если Выборка.Количество() = 0 Тогда
				
		//Создадим банковский счет
		КодВалюты=Сред(НомерСчета,6,3);
		ВалютаСчета=Справочники.Валюты.НайтиПоКоду(КодВалюты);
		Если ВалютаСчета=Справочники.Валюты.ПустаяСсылка() Тогда
			ВалютаСчета=Организация.ОсновнойБанковскийСчет.ВалютаДенежныхСредств.Ссылка;
		КонецЕсли;
		Если ВалютаСчета=Справочники.Валюты.ПустаяСсылка() Тогда
			Сообщить("Неизвестная валюта с кодом """+КодВалюты+""" в счете """+НомерСчета+""" контрагента """+Контрагент+"""");
			Возврат;
		КонецЕсли;
		НовыйСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
		НовыйСчет.УстановитьНовыйКод();
		НовыйСчет.НомерСчета = НомерСчета;
		НовыйСчет.СформироватьНаименование();
		НовыйСчет.Владелец = Контрагент;
		НовыйСчет.ВидСчета = "Расчетный";
		НовыйСчет.ВалютаДенежныхСредств = ВалютаСчета;
		
		БИКБанка = ТекущийДокумент.Значение[ТипКонтрагента+"БИК"];
		Банк = Справочники.Банки.НайтиПоКоду(?(СтрДлина(БИКБанка)=8,"0"+БИКБанка,БИКБанка));
		Если Банк.Пустая() Тогда
			НовыйБанк = Справочники.Банки.СоздатьЭлемент();
			НовыйБанк.Код = БИКБанка;
			Если Не ТекущийДокумент.Значение[ТипКонтрагента+"БАНК1"]=Неопределено Тогда
				НовыйБанк.Наименование = ТекущийДокумент.Значение[ТипКонтрагента+"БАНК1"];
				Если Не ТекущийДокумент.Значение[ТипКонтрагента+"БАНК2"]=Неопределено Тогда
					НовыйБанк.Город = ТекущийДокумент.Значение[ТипКонтрагента+"БАНК2"];
				Иначе
					НовыйБанк.Город = "г. Москва";
				КонецЕсли;
				Если НЕ ТекущийДокумент.Значение[ТипКонтрагента+"КОРСЧЕТ"]=Неопределено Тогда	
					НовыйБанк.КоррСчет = ТекущийДокумент.Значение[ТипКонтрагента+"КОРСЧЕТ"];
				Иначе
					НовыйБанк.КоррСчет = 11111111111;
				КонецЕсли;
			Иначе
				НовыйБанк.Наименование = КлиентБанка;
				НовыйБанк.Наименование = "Новый банк";
				НовыйБанк.Город="г. Москва";
				НовыйБанк.КоррСчет=11111111111;
			КонецЕсли;
			Попытка
				НовыйБанк.Записать();
			Исключение
				Сообщить("Ошибка при записи банка контрагента <"+Контрагент+">: "+ОписаниеОшибки());
			КонецПопытки;
			НовыйСчет.Банк = НовыйБанк.Ссылка;
		Иначе
			НовыйСчет.Банк = Банк;
		КонецЕсли;
			НовыйСчет.СформироватьНаименование();
		Попытка
			НовыйСчет.Записать();
		Исключение
			Сообщить("Ошибка при записи счета контрагента <"+Контрагент+">: "+ОписаниеОшибки());
		КонецПопытки;
		//НовыйЭлемент.ОсновнойБанковскийСчет=НовыйСчет.Ссылка;
	КонецЕсли;
КонецПроцедуры

//Сверка указанных реквизитов документа с файлом загрузки
Процедура ПроверитьРеквизитыКонтрагента(ТекущийДокумент, Контрагент)
	//Проверка КПП
	ТипКонтрагента = ТекущийДокумент.Значение["ТИПКОНТРАГЕНТА"];
	ЗагрКПП = ТекущийДокумент.Значение[ТипКонтрагента + "КПП"];
	
	Если ПустаяСтрока(Контрагент.КПП) И НЕ обЗначениеНеЗаполнено(ЗагрКПП) И СтрДлина(Контрагент.ИНН) <> 12 Тогда
		КонтрагентОбъект = Контрагент.ПолучитьОбъект();
		КонтрагентОбъект.КПП = ЗагрКПП;
		КонтрагентОбъект.Записать();
		Сообщить("Контрагенту "+Контрагент+ " заполнен КПП", СтатусСообщения.Информация);
	КонецЕсли;
КонецПроцедуры

Функция НенайденныйКонтрагентПолучитьНаименование(НенайденныйКонтрагентЗначение)
	
	ТипКонтрагента = НенайденныйКонтрагентЗначение["ТИПКОНТРАГЕНТА"];
	
	Если НенайденныйКонтрагентЗначение[ТипКонтрагента+"1"] <> Неопределено Тогда
		
		НаименованиеКонтрагента = НенайденныйКонтрагентЗначение[ТипКонтрагента+"1"];
		
	Иначе
	
		НаименованиеКонтрагента = НенайденныйКонтрагентЗначение[ТипКонтрагента];
			
	КонецЕсли;
	
	Возврат НаименованиеКонтрагента;
	
КонецФункции

Функция НенайденныйКонтрагентПолучитьПредставление(НенайденныйКонтрагентЗначение)	
	
	ТипКонтрагента = НенайденныйКонтрагентЗначение["ТИПКОНТРАГЕНТА"];
	
	Представление = НенайденныйКонтрагентПолучитьНаименование(НенайденныйКонтрагентЗначение);
	
	Если НенайденныйКонтрагентЗначение[ТипКонтрагента+"1"] = Неопределено Тогда
	
		ИНН = НенайденныйКонтрагентЗначение[ТипКонтрагента+"ИНН"];
		Если ЗначениеЗаполнено(ИНН) Тогда
			Представление = Представление + ", ИНН " + ИНН;				
		КонецЕсли;
				
	КонецЕсли; 
		
	Возврат Представление;	
	
КонецФункции

// Разбирает контрагентов
Процедура ОбработатьКонтрагентов()
	НеНайденныеКонтрагенты.Очистить();
	Для Каждого ТекущийДокумент Из СписокДокументов Цикл
		ТипКонтрагента = ТекущийДокумент.Значение["ТИПКОНТРАГЕНТА"];
		
		// Проверять контрагента для данной записи не имеет смысла.
		Если ТипКонтрагента = "НЕОПРЕДЕЛЕН" Тогда
			Продолжить;
		КонецЕсли;
		
		Представление = НенайденныйКонтрагентПолучитьПредставление(ТекущийДокумент.Значение);		
		
		Контрагент = НайтиКонтрагента(ТекущийДокумент.Значение[ТипКонтрагента+"ИНН"], ТекущийДокумент.Значение[ТипКонтрагента+"КПП"],ТекущийДокумент.Значение[ТипКонтрагента+"СЧЕТ"],Представление);
		Если Контрагент.Пустая() Тогда
			НеНайденныеКонтрагенты.Добавить(ТекущийДокумент.Значение);
		Иначе
			//проверка счета контрагента
			ДобавитьСчетКонтрагента(ТекущийДокумент,Контрагент);
			//проверка реквизитов контрагента
			ПроверитьРеквизитыКонтрагента(ТекущийДокумент,Контрагент);
		КонецЕсли;
	КонецЦикла;
	
	Если НеНайденныеКонтрагенты.Количество()>0 Тогда
		ТаблицаКонтрагентов.Строки.Очистить();
		ТаблицаКонтрагентов.Колонки.Очистить();
		ТаблицаКонтрагентов.Колонки.Добавить("Представление");
		ТаблицаКонтрагентов.Колонки.Добавить("Значение");
		ТаблицаКонтрагентов.Колонки.Добавить("Реквизит");
		ТаблицаКонтрагентов.Колонки.Добавить("Пометка", Новый ОписаниеТипов("Булево"));
		Индекс=0;
		Для Каждого НенайденныйКонтрагент Из НеНайденныеКонтрагенты Цикл
			//Для Каждого контрагента получим его тип (Плательщик или Получатель)
			ТипКонтрагента 				= НенайденныйКонтрагент.Значение["ТИПКОНТРАГЕНТА"];
			НаименованиеКонтрагента  	= НенайденныйКонтрагентПолучитьНаименование(НенайденныйКонтрагент.Значение);
			ПредставлениеКонтрагента 	= НенайденныйКонтрагентПолучитьПредставление(НенайденныйКонтрагент.Значение);			
				
			Если НЕ ТаблицаКонтрагентов.Строки.Найти(ПредставлениеКонтрагента,"Представление")=Неопределено Тогда
				Продолжить; 
			КонецЕсли;
			ТаблицаКонтрагентов.Строки.Добавить().Установить(0, ПредставлениеКонтрагента);

			ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();			
			ТаблицаКонтрагентов.Строки[Индекс].Строки[0].Установить(0, "Наименование контрагента");			
			ТаблицаКонтрагентов.Строки[Индекс].Строки[0].Установить(1, НаименованиеКонтрагента);
			ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
			ТаблицаКонтрагентов.Строки[Индекс].Строки[1].Установить(0, "ИНН контрагента");
			ТаблицаКонтрагентов.Строки[Индекс].Строки[1].Установить(1, НенайденныйКонтрагент.Значение[ТипКонтрагента+"ИНН"]);
			ТаблицаКонтрагентов.Строки[Индекс].Строки[1].Установить(2, ТипКонтрагента+"ИНН");
			
			ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
			ТаблицаКонтрагентов.Строки[Индекс].Строки[2].Установить(0, "Р/счет контрагента");
			ТаблицаКонтрагентов.Строки[Индекс].Строки[2].Установить(1, НенайденныйКонтрагент.Значение[ТипКонтрагента+"СЧЕТ"]);
			ТаблицаКонтрагентов.Строки[Индекс].Строки[2].Установить(2, ТипКонтрагента+"СЧЕТ");
			
			ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
			ТаблицаКонтрагентов.Строки[Индекс].Строки[3].Установить(0, "БИК плательщика");
			ТаблицаКонтрагентов.Строки[Индекс].Строки[3].Установить(1, НенайденныйКонтрагент.Значение[ТипКонтрагента+"БИК"]);
			ТаблицаКонтрагентов.Строки[Индекс].Строки[3].Установить(2, ТипКонтрагента+"БИК");
			
			ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
			ТаблицаКонтрагентов.Строки[Индекс].Строки[4].Установить(0, "Наименование банка контрагента");
			ТаблицаКонтрагентов.Строки[Индекс].Строки[4].Установить(1, "Новый банк");
			ТаблицаКонтрагентов.Строки[Индекс].Строки[4].Установить(2, "НАИМЕНОВАНИЕБАНКА");
			
			Индекс=Индекс+1;
		КонецЦикла;
	КонецЕсли;
	ПросмотрКонтрагентов(НеНайденныеКонтрагенты.Количество());
КонецПроцедуры

//вызываем форму для просмотра тех, кого не нашли
Процедура ПросмотрКонтрагентов(ТипПросмотра) Экспорт	

	ФормаКонтрагентов=ПолучитьФорму("ФормаКонтрагентов");
	Если (ТипПросмотра = 0) И (ФормаКонтрагентов.Открыта()) Тогда
		ФормаКонтрагентов.Закрыть();
	ИначеЕсли (ТипПросмотра > 0) И (ФормаКонтрагентов.Открыта()) Тогда
		ФормаКонтрагентов.Обновить();
	ИначеЕсли (ТипПросмотра > 0) И (НЕ(ФормаКонтрагентов.Открыта())) Тогда
		СоздаватьДокументБезКонтрагентов = ФормаКонтрагентов.ОткрытьМодально();
	ИначеЕсли (ТипПросмотра = 0) И (НЕ(ФормаКонтрагентов.Открыта())) Тогда
		Сообщить("Все контрагенты существуют в справочнике.", СтатусСообщения.Информация);
	КонецЕсли;
	
	//Пересмотрим может создали всех контрагентов,
	//тогда можно продолжить загрузку
	Если ТипПросмотра > 0 Тогда
		
		Для Каждого ТекущийДокумент Из СписокДокументов Цикл
			ТипКонтрагента = ТекущийДокумент.Значение["ТИПКОНТРАГЕНТА"];
			Представление = НенайденныйКонтрагентПолучитьПредставление(ТекущийДокумент.Значение);
			
			Контрагент = НайтиКонтрагента(ТекущийДокумент.Значение[ТипКонтрагента+"ИНН"],ТекущийДокумент.Значение[ТипКонтрагента+"КПП"],ТекущийДокумент.Значение[ТипКонтрагента+"СЧЕТ"],Представление);
			
			Если Контрагент.Пустая() Тогда
				НеНайденныеКонтрагенты.Добавить(ТекущийДокумент.Значение);
			Иначе
				//проверка счета контрагента
				ДобавитьСчетКонтрагента(ТекущийДокумент,Контрагент);
				//проверка реквизитов контрагента
				ПроверитьРеквизитыКонтрагента(ТекущийДокумент,Контрагент);
			КонецЕсли;
		КонецЦикла;
		
		Если НеНайденныеКонтрагенты.Количество()>0 Тогда
			ТаблицаКонтрагентов.Строки.Очистить();
			ТаблицаКонтрагентов.Колонки.Очистить();
			ТаблицаКонтрагентов.Колонки.Добавить("Представление");
			ТаблицаКонтрагентов.Колонки.Добавить("Значение");
			ТаблицаКонтрагентов.Колонки.Добавить("Реквизит");
			ТаблицаКонтрагентов.Колонки.Добавить("Пометка", Новый ОписаниеТипов("Булево"));
			Индекс=0;
			Для Каждого НенайденныйКонтрагент Из НеНайденныеКонтрагенты Цикл
				//Для Каждого контрагента получим его тип (Плательщик или Получатель)
				ТипКонтрагента 				= НенайденныйКонтрагент.Значение["ТИПКОНТРАГЕНТА"];
				НаименованиеКонтрагента  	= НенайденныйКонтрагентПолучитьНаименование(НенайденныйКонтрагент.Значение);
				ПредставлениеКонтрагента 	= НенайденныйКонтрагентПолучитьПредставление(НенайденныйКонтрагент.Значение);
				
				Если НЕ ТаблицаКонтрагентов.Строки.Найти(ПредставлениеКонтрагента,"Представление")=Неопределено Тогда
					Продолжить; 
				КонецЕсли;
				ТаблицаКонтрагентов.Строки.Добавить().Установить(0, ПредставлениеКонтрагента);
				
				ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
				
				ТаблицаКонтрагентов.Строки[Индекс].Строки[0].Установить(0, "Наименование контрагента");
				ТаблицаКонтрагентов.Строки[Индекс].Строки[0].Установить(1, НаименованиеКонтрагента);
			
				ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
				ТаблицаКонтрагентов.Строки[Индекс].Строки[1].Установить(0, "ИНН контрагента");
				ТаблицаКонтрагентов.Строки[Индекс].Строки[1].Установить(1, НенайденныйКонтрагент.Значение[ТипКонтрагента+"ИНН"]);
				ТаблицаКонтрагентов.Строки[Индекс].Строки[1].Установить(2, ТипКонтрагента+"ИНН");
				
				ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
				ТаблицаКонтрагентов.Строки[Индекс].Строки[2].Установить(0, "Р/счет контрагента");
				ТаблицаКонтрагентов.Строки[Индекс].Строки[2].Установить(1, НенайденныйКонтрагент.Значение[ТипКонтрагента+"СЧЕТ"]);
				ТаблицаКонтрагентов.Строки[Индекс].Строки[2].Установить(2, ТипКонтрагента+"СЧЕТ");
				
				ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
				ТаблицаКонтрагентов.Строки[Индекс].Строки[3].Установить(0, "БИК плательщика");
				ТаблицаКонтрагентов.Строки[Индекс].Строки[3].Установить(1, НенайденныйКонтрагент.Значение[ТипКонтрагента+"БИК"]);
				ТаблицаКонтрагентов.Строки[Индекс].Строки[3].Установить(2, ТипКонтрагента+"БИК");
				
				ТаблицаКонтрагентов.Строки[Индекс].Строки.Добавить();
				ТаблицаКонтрагентов.Строки[Индекс].Строки[4].Установить(0, "Наименование банка контрагента");
				ТаблицаКонтрагентов.Строки[Индекс].Строки[4].Установить(1, "Новый банк");
				ТаблицаКонтрагентов.Строки[Индекс].Строки[4].Установить(2, "НАИМЕНОВАНИЕБАНКА");				
				
				Индекс=Индекс+1;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Функция загружает информацию из файла обмена
Функция Загрузить() Экспорт
	РасчетныеСчета.Очистить();
	СписокДокументов.Очистить();
	ЕстьНезаполненныеПоля = ЛОЖЬ;
	
	// Получим список платежных документов
	ОтобратьКонтрагентов();
	Если НЕ СоздаватьПустуюВыпискуПриЗагрузке Тогда
		Если СписокДокументов.Количество()=0 Тогда
			Сообщить("В файле загрузки нет документов за указанный период");
			ЕстьНезаполненныеПоля = ИСТИНА;
		КонецЕсли;
	КонецЕсли; 
	
	Если ЕстьНезаполненныеПоля Тогда
		Сообщить("Загрузка остановлена.");
		Возврат Ложь;
	КонецЕсли;
	
	ОбработатьКонтрагентов();
	
	//ФормаКонтрагентов = ПолучитьФорму("ФормаКонтрагентов");
	Если НеНайденныеКонтрагенты.Количество() <> 0
		И НЕ РазрешитьСозданиеДокументаБезКонтрагентов() Тогда
		Сообщить("Есть не найденные контрагенты");
		Сообщить("Загрузка остановлена.");
		Возврат Ложь;
	КонецЕсли;
	
	// Все данные подготовлены. Осталось только создать сами выписки.
	СоздатьДокументы();
	Возврат Истина
КонецФункции

//Удаляет документы не присутствующие в клиент банка
Процедура УдалитьДокументы() Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Выписка.Ссылка
	|ИЗ
	|	Документ.Выписка КАК Выписка
	|ГДЕ
	|	Выписка.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И НЕ Выписка.Ссылка В (&МассивДокументов)
	|	И Выписка.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеремещениеДенежныхСредств.Ссылка
	|ИЗ
	|	Документ.ПеремещениеДенежныхСредств КАК ПеремещениеДенежныхСредств
	|ГДЕ
	|	ПеремещениеДенежныхСредств.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И ПеремещениеДенежныхСредств.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеДенежныхСредствМеждуРасчетнымиСчетамиОрганизации)
	|	И НЕ ПеремещениеДенежныхСредств.Ссылка В (&МассивДокументов)
	|	И НЕ ПеремещениеДенежныхСредств.ПометкаУдаления");
	Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(НачДатаЗагрузки));
	Запрос.УстановитьПараметр("ДатаКонца",КонецДня(КонДатаЗагрузки));
	Запрос.УстановитьПараметр("МассивДокументов",ЗагрДокументы);
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		ОбъектВыписка = РезультатЗапроса.Ссылка.ПолучитьОбъект();
		ОбъектВыписка.УстановитьПометкуУдаления(Истина);
		Сообщить("Документ: "+ОбъектВыписка.Ссылка+" помечен на удаление");
	КонецЦикла; 
КонецПроцедуры

//Проводит созданные банковские выписки
Процедура ПровестиДокументы() Экспорт
	Для Каждого Документ Из ЗагрДокументы Цикл
		Попытка
			ВыпискаОбъект = Документ.Значение.ПолучитьОбъект();
		Исключение
		КонецПопытки;
		Попытка
			ВыпискаОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Сообщить("Проведен документ :"+ Документ.Значение);
		Исключение
			Сообщить("Не удалось провести документ :"+ Документ.Значение + ". " + ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

//Создает контрагента
Функция СоздатьКонтрагента(СтрокаКонтрагент = Неопределено, ОткрыватьФорму=Истина) Экспорт //или СтрокаКонтрагент - строка, или ТаблицаКонтрагентов - ТЗ
	НовыйЭлемент =Справочники.Контрагенты.СоздатьЭлемент();
	Индекс=0;
	Если ТаблицаСоответствий.Количество() > 0 И ИспользоватьЗамены Тогда
		МассивКонтрагетов = ТаблицаСоответствий.НайтиСтроки(Новый Структура("ТекущийКонтрагент", СтрокаКонтрагент));
		Если НЕ МассивКонтрагетов.Количество() = 0 Тогда
			СтрокаМассива=МассивКонтрагетов[0];
			Если НЕ СтрокаМассива.Замена.Пустая() Тогда
				Возврат СтрокаМассива.Замена;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Пока Индекс<>НеНайденныеКонтрагенты.Количество() Цикл
		ТипКонтрагента 				= НеНайденныеКонтрагенты[Индекс].Значение["ТИПКОНТРАГЕНТА"];
		ПредставлениеКонтрагента 	= НенайденныйКонтрагентПолучитьПредставление(НеНайденныеКонтрагенты[Индекс].Значение);
		НаименованиеКонтрагента  	= НенайденныйКонтрагентПолучитьНаименование(НеНайденныеКонтрагенты[Индекс].Значение);
		Если ПредставлениеКонтрагента = СтрокаКонтрагент Тогда
			//Создадим контрагента
			НовыйЭлемент.УстановитьНовыйКод();
			НовыйЭлемент.Наименование		=НаименованиеКонтрагента;
			НовыйЭлемент.НаименованиеПолное	=НаименованиеКонтрагента;
			НовыйЭлемент.ИНН=НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"ИНН"];
			Если СтрДлина(НовыйЭлемент.ИНН) = 12 Тогда
				НовыйЭлемент.КПП = "";
			Иначе
				НовыйЭлемент.КПП = НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"КПП"];
			КонецЕсли;
			Если ТипКонтрагента="ПЛАТЕЛЬЩИК" Тогда
				НовыйЭлемент.ВидКонтрагента=Перечисления.ВидыКонтрагентов.Покупатель;
			ИначеЕсли ТипКонтрагента="ПОЛУЧАТЕЛЬ" Тогда
				НовыйЭлемент.ВидКонтрагента=Перечисления.ВидыКонтрагентов.Поставщик;
			Иначе
				НовыйЭлемент.ВидКонтрагента=Перечисления.ВидыКонтрагентов.Прочее;
			КонецЕсли; 
			НовыйЭлемент.ФормаСобственности=Перечисления.ФормыСобственности.Прочее;
			Попытка
				НовыйЭлемент.Записать();
			Исключение
				Сообщить("Ошибка при записи контрагента <"+НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"1"]+">: "+ОписаниеОшибки());
				//Возврат Справочники.Контрагенты.ПустаяСсылка();
			КонецПопытки;
			
			//Создадим банковский счет
			НомерСчета=НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"СЧЕТ"];
			КодВалюты=Сред(НомерСчета,6,3);
			ВалютаСчета=Справочники.Валюты.НайтиПоКоду(КодВалюты);
			Если ВалютаСчета=Справочники.Валюты.ПустаяСсылка() Тогда
				ВалютаСчета=Организация.ОсновнойБанковскийСчет.ВалютаДенежныхСредств.Ссылка;
			КонецЕсли;
			НовыйСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
			НовыйСчет.УстановитьНовыйКод();
			НовыйСчет.НомерСчета = НомерСчета;
			НовыйСчет.Владелец = НовыйЭлемент.Ссылка;
			НовыйСчет.ВидСчета = "Расчетный";
			НовыйСчет.ВалютаДенежныхСредств = ВалютаСчета;
			БИКБанка = НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"БИК"];
			Банк = Справочники.Банки.НайтиПоКоду(?(СтрДлина(БИКБанка)=8,"0"+БИКБанка,БИКБанка));
			Если Банк.Пустая() Тогда
				НовыйБанк = Справочники.Банки.СоздатьЭлемент();
				НовыйБанк.Код = БИКБанка;
				Если Не НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"БАНК1"]=Неопределено Тогда
					НовыйБанк.Наименование = НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"БАНК1"];
					Если Не НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"БАНК2"]=Неопределено Тогда
						НовыйБанк.Город = НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"БАНК2"];
					Иначе
						НовыйБанк.Город = "г. Москва";
					КонецЕсли;
					Если НЕ НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"КОРСЧЕТ"]=Неопределено Тогда	
						НовыйБанк.КоррСчет = НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"КОРСЧЕТ"];
					Иначе
						НовыйБанк.КоррСчет = 11111111111;
					КонецЕсли;
				Иначе
					НовыйБанк.Наименование = КлиентБанка;
					НовыйБанк.Наименование = "Новый банк";
					НовыйБанк.Город="г. Москва";
					НовыйБанк.КоррСчет=11111111111;
				КонецЕсли;
				Если обЗначениеНеЗаполнено(НовыйБанк.Код) И ТипЗнч(ТаблицаКонтрагентов)=Тип("ДеревоЗначений") Тогда
					Для Каждого СтрокаДерева Из ТаблицаКонтрагентов.Строки Цикл
						НайденнаяСтрока	= СтрокаДерева.Строки.Найти("ПЛАТЕЛЬЩИКБИК", "Реквизит");
						Если НайденнаяСтрока<>Неопределено Тогда
							ПлательщикБик	= НайденнаяСтрока.Значение;
						КонецЕсли;
						Если обЗначениеНеЗаполнено(ПлательщикБик) Тогда
							НовыйБанк.УстановитьНовыйКод();
						Иначе
							НовыйБанк.Код	= ПлательщикБик;
						КонецЕсли;
						Прервать;
					КонецЦикла;
				КонецЕсли;
				Если обЗначениеНеЗаполнено(НовыйБанк.Наименование) И ТипЗнч(ТаблицаКонтрагентов)=Тип("ДеревоЗначений") Тогда
					Для Каждого СтрокаДерева Из ТаблицаКонтрагентов.Строки Цикл
						НайденнаяСтрока	= СтрокаДерева.Строки.Найти("НАИМЕНОВАНИЕБАНКА", "Реквизит");
						Если НайденнаяСтрока<>Неопределено Тогда
							НаименованиеБанка	= НайденнаяСтрока.Значение;
						КонецЕсли;
						Если обЗначениеНеЗаполнено(НаименованиеБанка) Тогда
							НовыйБанк.Наименование	= "Новый банк";;
						Иначе
							НовыйБанк.Наименование	= НаименованиеБанка;
						КонецЕсли;
						Прервать;
					КонецЦикла;
				КонецЕсли;				
				Попытка
					НовыйБанк.Записать();
				Исключение
					Сообщить("Ошибка при записи банка контрагента <"+НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"1"]+">: "+ОписаниеОшибки());
				КонецПопытки;
				НовыйСчет.Банк = НовыйБанк.Ссылка;
			Иначе
				НовыйСчет.Банк = Банк;
			КонецЕсли;
			НовыйСчет.СформироватьНаименование();
			Попытка
				НовыйСчет.Записать();
			Исключение
				Сообщить("Ошибка при записи счета контрагента <"+НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"1"]+">: "+ОписаниеОшибки());
			КонецПопытки;
			НовыйЭлемент.ОсновнойБанковскийСчет=НовыйСчет.Ссылка;
			
			//Создадим договор
			//НовыйДоговор = Справочники.ДоговорыВзаиморасчетов.СоздатьЭлемент();
			//НовыйДоговор.УстановитьНовыйКод();
			//НовыйДоговор.Владелец = НовыйЭлемент.Ссылка;
			//Если ТипКонтрагента="ПЛАТЕЛЬЩИК" Тогда
			//	НовыйДоговор.ВидДоговора = Перечисления.ВидыДоговоров.Продажа;
			//ИначеЕсли ТипКонтрагента="ПОЛУЧАТЕЛЬ" Тогда
			//	НовыйДоговор.ВидДоговора = Перечисления.ВидыДоговоров.Покупка;
			//Иначе
			//	НовыйДоговор.ВидДоговора = Перечисления.ВидыДоговоров.Прочее;
			//КонецЕсли; 
			//НовыйДоговор.ДатаНачала = НачДатаЗагрузки;
			//Если обЗначениеНеЗаполнено(НовыйДоговор.ДатаНачала) Тогда
			//	НовыйДоговор.ДатаНачала=ТекущаяДата();
			//КонецЕсли; 
			//НовыйДоговор.ВалютаВзаиморасчетов = ВалютаСчета;
			//НаименованиеДоговора = СокрЛП(Строка(НовыйДоговор.ВидДоговора))
			// 	+ " № " + СокрЛП(НовыйДоговор.Код)
			// 	+ " в " + ВалютаСчета.Наименование
			// 	+ " от "+Формат(НачДатаЗагрузки,"ДФ=дд.ММ.гг");
			//НовыйДоговор.Наименование=НаименованиеДоговора;
			//НовыйДоговор.Организация=Организация;
			//НовыйДоговор.Подразделение=ПараметрыСеанса.ПодразделениеКомпании;
			//НовыйДоговор.ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет;
			//Попытка
			//	НовыйДоговор.Записать();
			//Исключение
			//	Сообщить("Ошибка при записи договора контрагента: "+ОписаниеОшибки());
			//КонецПопытки;
			//Попытка
			//	НовыйЭлемент.Записать();
			//Исключение
			//	Сообщить("Ошибка при записи контрагента: "+ОписаниеОшибки());
			//КонецПопытки;
			НеНайденныеКонтрагенты.Удалить(НеНайденныеКонтрагенты[Индекс]);
			Сообщить("Элемент создан");
			Если ОткрыватьФорму Тогда
				ОткрытьЗначение(НовыйЭлемент);
			КонецЕсли;
			Контрагент = НовыйЭлемент.Ссылка;
    		Возврат Контрагент;
		КонецЕсли;
		Индекс=Индекс+1;
	КонецЦикла;	
КонецФункции

Функция РазрешитьСозданиеДокументаБезКонтрагентов()
	
	Возврат (СоздаватьДокументБезКонтрагентов <> Неопределено
		И СоздаватьДокументБезКонтрагентов = Истина);	

КонецФункции // РазрешитьСоздаватьДокументБезКонтрагентов()


//Ищет контрагента в таблице замен
Функция ПроверитьВТаблицеЗамен(Представление) Экспорт
	Если ТаблицаСоответствий.Количество() > 0 И ИспользоватьЗамены Тогда
		МассивКонтрагетов = ТаблицаСоответствий.НайтиСтроки(Новый Структура("ТекущийКонтрагент", Представление));
		Если НЕ МассивКонтрагетов.Количество() = 0 Тогда
			СтрокаМассива=МассивКонтрагетов[0];
			Если НЕ СтрокаМассива.Замена.Пустая() Тогда
				Возврат Истина;
			Иначе
				Возврат Ложь;
			КонецЕсли;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

//Создает документы
Процедура СоздатьДокументы()
	// Очистим очередь загруженных документов
	ЗагрДокументы.Очистить();
	
	// Создадим и заполним временную таблицу загружаемых документов
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("Дата");
	ТаблицаДокументов.Колонки.Добавить("БанковскийСчет");
	ТаблицаДокументов.Колонки.Добавить("БанковскийСчетПолучатель");
	ТаблицаДокументов.Колонки.Добавить("Данные");
	
	Для Каждого ТекДокумент Из СписокДокументов Цикл
		НоваяСтрока = ТаблицаДокументов.Добавить();
		НоваяСтрока.Данные = ТекДокумент.Значение;
		Если ТекДокумент.Значение["ВИДДВИЖЕНИЯ"] = "Платеж" Тогда
			НоваяСтрока.Дата			= ФорматДаты(ТекДокумент.Значение["ДАТАСПИСАНО"]);
			НоваяСтрока.БанковскийСчет	= ТекДокумент.Значение["ПЛАТЕЛЬЩИКСЧЕТ"];
		ИначеЕсли ТекДокумент.Значение["ВИДДВИЖЕНИЯ"] = "ПеремещениеМеждуРасчетнымиСчетами" Тогда
			НоваяСтрока.Дата			= ФорматДаты(ТекДокумент.Значение["ДАТАСПИСАНО"]);
			НоваяСтрока.БанковскийСчет	= ТекДокумент.Значение["ПЛАТЕЛЬЩИКСЧЕТ"];
			НоваяСтрока.БанковскийСчетПолучатель = ТекДокумент.Значение["ПОЛУЧАТЕЛЬСЧЕТ"];
		Иначе
			НоваяСтрока.Дата			= ФорматДаты(ТекДокумент.Значение["ДАТАПОСТУПИЛО"]);
			НоваяСтрока.БанковскийСчет	= ТекДокумент.Значение["ПОЛУЧАТЕЛЬСЧЕТ"];
		КонецЕсли;
	КонецЦикла;
	ТаблицаДокументов.Сортировать("Дата,БанковскийСчет,БанковскийСчетПолучатель");
	
	МассивДокументов = Новый Массив;
	Если ТаблицаДокументов.Количество()=0 Тогда
		// Нет документов для создания. Создаем пустые выписки на дату создания файла
		Для каждого СтрокаТаблицыСчетов Из ТаблицаСчетов Цикл
        	Если СтрокаТаблицыСчетов.Пометка Тогда
				СоздатьВыписку(ДатаДокумента, СтрокаТаблицыСчетов.Счет.НомерСчета, МассивДокументов);	
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		// Создание документов
		ТекущаяДата = ТаблицаДокументов[0].Дата;
		ТекущийСчет = ТаблицаДокументов[0].БанковскийСчет;
		Для Каждого ТекДокумент Из ТаблицаДокументов Цикл
			
			Если ЗначениеЗаполнено(ТекДокумент.БанковскийСчетПолучатель) Тогда
				СоздатьПеремещениеДенежныхСредств(ТекущаяДата, ТекДокумент.БанковскийСчет,ТекДокумент.БанковскийСчетПолучатель, ТекДокумент.Данные);
				Продолжить;
			КонецЕсли;
			
			Если ((ТекущаяДата <> ТекДокумент.Дата) ИЛИ (ТекущийСчет <> ТекДокумент.БанковскийСчет)) Тогда
				// Очередной документ отличается по дате и/или счету от предыдущего. прошлые нужно записать
				СоздатьВыписку(ТекущаяДата, ТекущийСчет, МассивДокументов);
				МассивДокументов.Очистить();
				ТекущаяДата = ТекДокумент.Дата;
				ТекущийСчет = ТекДокумент.БанковскийСчет;
			КонецЕсли;
			МассивДокументов.Добавить(ТекДокумент.Данные);
		КонецЦикла;
		
		Если МассивДокументов.Количество()<>0 Тогда
			СоздатьВыписку(ТекущаяДата, ТекущийСчет, МассивДокументов);
			МассивДокументов.Очистить();
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

// Формирует и записывает документ выписка
Процедура СоздатьВыписку(Дата, БанковскийСчет, МассивДокументов)
	ВыпискаОбъект = Документы.Выписка.СоздатьДокумент();
	
	// Заполним шапку документа
	ВыпискаОбъект.Дата				= Дата;
	ВыпискаОбъект.БанковскийСчет	= Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета", БанковскийСчет,, Организация);
	ВыпискаОбъект.ВалютаДокумента	= ВыпискаОбъект.БанковскийСчет.ВалютаДенежныхСредств;
	ВыпискаОбъект.ПодразделениеКомпании = ВыпискаОбъект.БанковскийСчет.Подразделение;
	ВыпискаОбъект.Организация		= ВыпискаОбъект.БанковскийСчет.Подразделение.Организация;
	ВыпискаОбъект.Заполнить(Неопределено);
	
	// Приступим к заполнению табличной части
	Для Каждого ТекДокумент Из МассивДокументов Цикл
		НоваяСтрока = ВыпискаОбъект.Состав.Добавить();
		
		СуммаСделки = Число(ТекДокумент["СУММА"]);
		СуммаСделки = ?(СуммаСделки < 0, -СуммаСделки, СуммаСделки);
		
		ТипКонтрагента = ТекДокумент["ТИПКОНТРАГЕНТА"];		
		Представление  = НенайденныйКонтрагентПолучитьПредставление(ТекДокумент);		
		
		// Обратим внимание на то что мы получаем только платежные поручения (см. функцию "ЗагрузитьСекциюДокумента")
		Если ТекДокумент["ВИДДВИЖЕНИЯ"]="Платеж" Тогда
			НоваяСтрока.СтатьяДДС	= Справочники.СтатьиДДС.ОплатаПоставщику;
			НоваяСтрока.Контрагент	= НайтиКонтрагента(ТекДокумент["ПОЛУЧАТЕЛЬИНН"],ТекДокумент["ПОЛУЧАТЕЛЬКПП"],ТекДокумент["ПОЛУЧАТЕЛЬСЧЕТ"],Представление);
			ВидДоговора = Перечисления.ВидыДоговоров.Покупка;
			НоваяСтрока.СуммаРасход	= СуммаСделки;
			ВидДоговораКомиссия	= Перечисления.ВидыДоговоров.СКомитентом;
		Иначе
			НоваяСтрока.СтатьяДДС	= Справочники.СтатьиДДС.ОплатаОтПокупателя;
			НоваяСтрока.Контрагент	= НайтиКонтрагента(ТекДокумент["ПЛАТЕЛЬЩИКИНН"],ТекДокумент["ПЛАТЕЛЬЩИККПП"],ТекДокумент["ПЛАТЕЛЬЩИКСЧЕТ"],Представление);
			ВидДоговора = Перечисления.ВидыДоговоров.Продажа;
			НоваяСтрока.СуммаПриход	= СуммаСделки;
			ВидДоговораКомиссия	= Перечисления.ВидыДоговоров.СКомиссионером;
		КонецЕсли;
		
		// заполним НДС
		СтруктураНДС = ВыделитьНДСИзНазначенияПлатежа(ТекДокумент["НАЗНАЧЕНИЕПЛАТЕЖА"], СуммаСделки);
		Если НЕ обЗначениеНеЗаполнено(СтруктураНДС.СтавкаНДС) Тогда
			НоваяСтрока.СтавкаНДС = СтруктураНДС.СтавкаНДС;
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(СтруктураНДС.СуммаНДС) Тогда
			НоваяСтрока.СуммаНДС = СтруктураНДС.СуммаНДС;
		КонецЕсли;

		// Заполним остальные реквизиты строки
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ВидДоговора",ВидДоговора);
		НоваяСтрока.ДоговорВзаиморасчетов	= Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(НоваяСтрока.Контрагент,ВидДоговора,ВыпискаОбъект,ДопПараметры);
		Если обЗначениеНеЗаполнено(НоваяСтрока.ДоговорВзаиморасчетов) Тогда
			НоваяСтрока.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(НоваяСтрока.Контрагент,ВидДоговораКомиссия,ВыпискаОбъект,Новый Структура("ВидДоговора", ВидДоговораКомиссия));
		КонецЕсли;
		Если НЕ обЗначениеНеЗаполнено(НоваяСтрока.ДоговорВзаиморасчетов)
			И НЕ обЗначениеНеЗаполнено(НоваяСтрока.ДоговорВзаиморасчетов.СтатьяДДС) Тогда
			НоваяСтрока.СтатьяДДС = НоваяСтрока.ДоговорВзаиморасчетов.СтатьяДДС;
		КонецЕсли;
		НоваяСтрока.АвтоЗакрытиеСделок		= НоваяСтрока.ДоговорВзаиморасчетов.АвтоЗакрытиеСделок;
		НоваяСтрока.КурсВалютыВзаиморасчетов= дкПолучитьКурсВалютыВзаиморасчетов(ВыпискаОбъект, НоваяСтрока.ДоговорВзаиморасчетов);
		НоваяСтрока.НазначениеПлатежа		= ТекДокумент["НАЗНАЧЕНИЕПЛАТЕЖА"];
		НоваяСтрока.ПлатежноеПоручениеОснование = "№" + ТекДокумент["НОМЕР"]+?(ЗначениеЗаполнено(ТекДокумент["ДАТА"])," от " + ТекДокумент["ДАТА"],"");
		Если обЗначениеНеЗаполнено(НоваяСтрока.НазначениеПлатежа) Тогда
			Для Сч = 1 По 6 Цикл
				Если ЗначениеЗаполнено(НоваяСтрока.НазначениеПлатежа) Тогда
					Прервать;
				КонецЕсли;
				Попытка
					НоваяСтрока.НазначениеПлатежа = ТекДокумент["НАЗНАЧЕНИЕПЛАТЕЖА"+Сч];
				Исключение
					НоваяСтрока.НазначениеПлатежа = "";
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;
		
	// КодНазПлатежа
	НоваяСтрока.КодВидаДохода = ТекДокумент["КОДНАЗПЛАТЕЖА" + Сч];
	КонецЦикла;	
	
	// Запишем документ
	Попытка
		Если НеНайденныеКонтрагенты.Количество() > 0 И РазрешитьСозданиеДокументаБезКонтрагентов() Тогда
			ВыпискаОбъект.ОбменДанными.Загрузка = Истина;		
		КонецЕсли;
		ВыпискаОбъект.Записать(РежимЗаписиДокумента.Запись,РежимПроведенияДокумента.Неоперативный);
	Исключение
		Для Каждого СтрокаТЧ Из ВыпискаОбъект.Состав Цикл
			Если ЗначениеЗаполнено(СтрокаТЧ.Контрагент) И НЕ ЗначениеЗаполнено(СтрокаТЧ.ДоговорВзаиморасчетов) Тогда 
				Сообщить("Не задан договор взаиморасчетов. Для контрагента <"+СтрокаТЧ.Контрагент+"> необходим договор в валюте <"+ВыпискаОбъект.ВалютаДокумента+">.");
			КонецЕсли;
		КонецЦикла;
	КонецПопытки;
	
	// Добавим документ в очередь загруженных
	ЗагрДокументы.Добавить(ВыпискаОбъект.Ссылка);
КонецПроцедуры

// Формирует и записывает документ "Перемещение денежных средств"
Процедура СоздатьПеремещениеДенежныхСредств(Дата, БанковскийСчет, БанковскийСчетПолучатель, ТекДокумент)
	
	ПеремещениеДС = Документы.ПеремещениеДенежныхСредств.СоздатьДокумент();
	ПеремещениеДС.Заполнить(Неопределено);
	
	ПеремещениеДС.ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредствМеждуРасчетнымиСчетамиОрганизации;
	ПеремещениеДС.Дата = Дата;
	ПеремещениеДС.КассаКомпании = Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета", БанковскийСчет,, Организация);
	ПеремещениеДС.ВалютаДокумента	= ПеремещениеДС.КассаКомпании.ВалютаДенежныхСредств;
	ПеремещениеДС.ПодразделениеКомпании = ПеремещениеДС.КассаКомпании.Подразделение;
	ПеремещениеДС.Организация		= ПеремещениеДС.КассаКомпании.Подразделение.Организация;
	ПеремещениеДС.КассаКомпанииПолучатель = Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета", БанковскийСчетПолучатель,, Организация);
	
	ПеремещениеДС.СтатьяДДС = Справочники.СтатьиДДС.ПеремещениеДС;
	
	ПеремещениеДС.СуммаДокумента = Число(ТекДокумент["СУММА"]);
	
	Если НЕ обЗначениеНеЗаполнено(ТекДокумент["НАЗНАЧЕНИЕПЛАТЕЖА"]) Тогда
		ПеремещениеДС.Комментарий = "Назначение платежа: " + ТекДокумент["НАЗНАЧЕНИЕПЛАТЕЖА"];
	Иначе
		Для Сч = 1 По 6 Цикл
			Если ЗначениеЗаполнено(ПеремещениеДС.Комментарий) Тогда
				Прервать;
			КонецЕсли;
			Попытка
				ПеремещениеДС.Комментарий = ТекДокумент["НАЗНАЧЕНИЕПЛАТЕЖА"+Сч];
			Исключение
				ПеремещениеДС.Комментарий = "";
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	// Запишем документ
	Попытка
		ПеремещениеДС.Записать(РежимЗаписиДокумента.Запись,РежимПроведенияДокумента.Неоперативный);
	Исключение
		Сообщить("Не удалось сформировать документ перемещения денежных средств между расчетными счетами организации по причине: "+ ОписаниеОшибки());
	КонецПопытки;
	
	// Добавим документ в очередь загруженных
	ЗагрДокументы.Добавить(ПеремещениеДС.Ссылка);
	
КонецПроцедуры // СоздатьПеремещениеДенежныхСредств()

//Печать отчета
//
Процедура Печать( ПолеОтчета, ПустыеРеквизиты =Неопределено) Экспорт
	МакетОтчета = ПолучитьМакет("Отчет");
	
	Если ПустыеРеквизиты = Неопределено Тогда
		Шапка    = МакетОтчета.ПолучитьОбласть("Шапка");
		Строка   = МакетОтчета.ПолучитьОбласть("Строка");
		Подвал   = МакетОтчета.ПолучитьОбласть("Подвал");
		ПолеОтчета.Вывести(Шапка);
		Индекс=0;
		Для Каждого Элем Из ЗагрДокументы Цикл
			Док = Элем.Значение;
			Если Док.ХозОперация = Справочники.ХозОперации.ПлатежноеПоручение Тогда
				Индекс = Индекс+1;
				Строка.Параметры.Индекс = Индекс;
				Строка.Параметры.Документ = Док;
				Строка.Параметры.Плательщик = Док.Организация;
				Строка.Параметры.ПлательщикСчет = Док.СчетОрганизации;
				Строка.Параметры.Получатель = Док.Контрагент;
				Строка.Параметры.ПолучательСчет = Док.СчетКонтрагента;
				Строка.Параметры.Сумма = Док.СуммаДокумента;
				ПолеОтчета.Вывести(Строка);
			ИначеЕсли Док.ХозОперация = Справочники.ХозОперации.ПеремещениеДенежныхСредствМеждуРасчетнымиСчетамиОрганизации Тогда
				Индекс = Индекс+1;
				Строка.Параметры.Индекс = Индекс;
				Строка.Параметры.Документ = Док;
				Строка.Параметры.Плательщик = Док.Организация;
				Строка.Параметры.ПлательщикСчет = Док.КассаКомпании;
				Строка.Параметры.Получатель = Док.Организация;
				Строка.Параметры.ПолучательСчет = Док.КассаКомпанииПолучатель;
				Строка.Параметры.Сумма = Док.СуммаДокумента;
				ПолеОтчета.Вывести(Строка);
			Иначе
				
				Для Каждого СтрокаСостав Из Док.Состав Цикл
					Индекс=Индекс+1;
					Строка.Параметры.Индекс = Индекс;
					Строка.Параметры.Документ = Док;
					Если СтрокаСостав.СтатьяДДС = Справочники.СтатьиДДС.ОплатаОтПокупателя Тогда
						//оплата от покупателя
						Строка.Параметры.Плательщик = СтрокаСостав.Контрагент;
						Строка.Параметры.ПлательщикСчет = СтрокаСостав.Контрагент.ОсновнойБанковскийСчет;
						Строка.Параметры.Получатель = Док.Организация;
						Строка.Параметры.ПолучательСчет = Док.БанковскийСчет;
						Строка.Параметры.Сумма = СтрокаСостав.СуммаПриход;
					Иначе
						//Оплата поставщику
						Строка.Параметры.Плательщик = Док.Организация;
						Строка.Параметры.ПлательщикСчет = Док.БанковскийСчет;
						Строка.Параметры.Получатель = СтрокаСостав.Контрагент;
						Строка.Параметры.ПолучательСчет = СтрокаСостав.Контрагент.ОсновнойБанковскийСчет;
						Строка.Параметры.Сумма = СтрокаСостав.СуммаРасход;
					КонецЕсли;
					ПолеОтчета.Вывести(Строка);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		ПолеОтчета.Вывести(Подвал);
	Иначе
		Шапка     =МакетОтчета.ПолучитьОбласть("ШапкаПусто");
		Документ  =МакетОтчета.ПолучитьОбласть("Документ");
		Сообщение =МакетОтчета.ПолучитьОбласть("Сообщение");
		Подвал    =МакетОтчета.ПолучитьОбласть("Подвал");
		
		ПолеОтчета.Вывести(Шапка);
		
		Для Каждого Эл Из ПустыеРеквизиты Цикл
			
			Поле =Эл["Поля"];
			Док  =Эл["Документ"];
			
			Документ.Параметры.Наименование =Док;
			ПолеОтчета.Вывести(Документ);
						
			Если
				Поле.Свойство("НЕТ_ДАННЫХ_ДЛЯ_ВЫГРУЗКИ")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Нет документов для выгрузки";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
			
			Если
				Поле.Свойство("Номер") ИЛИ
				Поле.Свойство("Дата")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Проверьте реквизиты документа.";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
		
			Если
				Поле.Свойство("Сумма") ИЛИ
				Поле.Свойство("ВидПлатежа") ИЛИ
				Поле.Свойство("СрокПлатежа") ИЛИ
				Поле.Свойство("Очередность")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Проверьте реквизиты платежа.";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
		
			Если
				Поле.Свойство("ПлательщикСчет") ИЛИ
				Поле.Свойство("ПлательщикИНН") ИЛИ
				Поле.Свойство("Плательщик") ИЛИ
				Поле.Свойство("Плательщик1")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Проверьте реквизиты плательщика.";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
			
			Если
				Поле.Свойство("ПлательщикРасчСчет") ИЛИ
				Поле.Свойство("ПлательщикБанк1") ИЛИ
				Поле.Свойство("ПлательщикБанк2") ИЛИ
				Поле.Свойство("ПлательщикБИК") ИЛИ
				Поле.Свойство("ПлательщикКорсчет")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Проверьте реквизиты банка плательщика.";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
			
			Если
				Поле.Свойство("ПолучательСчет") ИЛИ
				Поле.Свойство("ПолучательИНН") ИЛИ
				Поле.Свойство("Получатель") ИЛИ
				Поле.Свойство("Получатель1")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Проверьте реквизиты получателя.";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
			
			Если
				Поле.Свойство("ПолучательРасчСчет") ИЛИ
				Поле.Свойство("ПолучательБанк1") ИЛИ
				Поле.Свойство("ПолучательБанк2") ИЛИ
				Поле.Свойство("ПолучательБИК") ИЛИ
				Поле.Свойство("ПолучательКорcчет")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Проверьте реквизиты банка получателя.";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
						
			Если
				Поле.Свойство("СтатусСоставителя") ИЛИ
				Поле.Свойство("ПлательщикКПП") ИЛИ
				Поле.Свойство("ПолучательКПП") ИЛИ
				Поле.Свойство("ПоказательКБК") ИЛИ
				Поле.Свойство("ОКАТО") ИЛИ
				Поле.Свойство("ПоказательОснования") ИЛИ
				Поле.Свойство("ПоказательПериода") ИЛИ
				Поле.Свойство("ПоказательНомера") ИЛИ
				Поле.Свойство("ПоказательДаты") ИЛИ
				Поле.Свойство("ПоказательТипа")
			Тогда
				Сообщение.Параметры.ТекстСообщения ="Проверьте реквизиты для платежей в бюджет РФ.";
				ПолеОтчета.Вывести(Сообщение);
			КонецЕсли;
			ПолеОтчета.Вывести(Подвал);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры	

// Основная процедура выгрузки данных
Процедура Выгрузить()  Экспорт
	Инициализация();
	Текст = Новый ТекстовыйДокумент();
	Текст.ДобавитьСтроку("1CClientBankExchange");
	Текст.ДобавитьСтроку("ВерсияФормата=1.01");
	Текст.ДобавитьСтроку("Кодировка="+Кодировка);
	Текст.ДобавитьСтроку("Отправитель="+Метаданные.Синоним);
	Текст.ДобавитьСтроку("Получатель="+КлиентБанка);
	Текст.ДобавитьСтроку("ДатаСоздания="+Формат(ТекущаяДата(),"ДЛФ=Д"));
	Текст.ДобавитьСтроку("ВремяСоздания="+Формат(ТекущаяДата(),"ДЛФ=В"));
	Текст.ДобавитьСтроку("ДатаНачала="+Формат(НачПериода,"ДЛФ=Д"));
	Текст.ДобавитьСтроку("ДатаКонца="+Формат(КонПериода,"ДЛФ=Д"));
	//Счета, по которым будут проходить операции
	СписокСчетов = Новый СписокЗначений;
	Для Каждого Строка Из ТаблицаСчетов Цикл
		Если Строка.Пометка = Истина Тогда
		 	Текст.ДобавитьСтроку("РасчСчет="+Строка.Счет.НомерСчета);
			СписокСчетов.Добавить(Строка.Счет);
		КонецЕсли;	
	КонецЦикла;
	Текст.ДобавитьСтроку("Документ=Платежное поручение");
	//ТЗ для полей с пустыми значениями
	ПустыеПоляДокумента =Новый ТаблицаЗначений();
	ПустыеПоляДокумента.Колонки.Добавить("Поля");
	ПустыеПоляДокумента.Колонки.Добавить("Документ");
	Для Каждого ТекущийСчёт Из ТаблицаСчетов Цикл
		Если ТекущийСчёт.Пометка = Истина Тогда
			ВыгрузитьДокументы(ТекущийСчёт.Счет);
		КонецЕсли;
	КонецЦикла;
	
	Текст.ДобавитьСтроку("КонецФайла");
	Если Кодировка = "DOS" Тогда
		Кодир = КодировкаТекста.OEM;
	Иначе
		Кодир = КодировкаТекста.ANSI;
	КонецЕсли;
	
	Если ПустыеПоляДокумента.Количество() = 0 Тогда 
		Текст.Записать(ФайлВыгрузки,Кодир);
		Если ПрочитатьФайл(ФайлВыгрузки) Тогда
			Сообщить("Выгрузка завершена!");
			ЗаписатьВРегистр();
		Иначе
			Текст.Очистить();
			Сообщить("В файле нет данных.");
			Сообщить("Файл не сформирован!");
		КонецЕсли;
	Иначе
		//печать в модуле формы
		Текст.Очистить();
		Сообщить("Файл не сформирован!");
	КонецЕсли; 
КонецПроцедуры

// Форматирует сумму  документа
//
// Параметры:
//  СуммаДок - число - реквизит, который надо отформатировать
//  СуммаБезКопеек - булево - флаг представления суммы без копеек
//
// Возвращаемое значение
//  Отформатированную строку
//
Функция ФорматироватьСумму(СуммаДок,СуммаБезКопеек)
	Результат  = СуммаДок;
	ЦелаяЧасть = Цел(СуммаДок);
	Если (Результат - ЦелаяЧасть) = 0 Тогда
		Если СуммаБезКопеек Тогда
			Результат = Формат(Результат,"ЧДЦ=0; ЧГ=0");
		Иначе
			Результат = Формат(Результат,"ЧДЦ=2; ЧРД=.; ЧГ=0");
		КонецЕсли;
	Иначе
		Результат = Формат(Результат,"ЧДЦ=2; ЧРД=.; ЧГ=0");
	КонецЕсли;
	Возврат Результат;
КонецФункции

// Выгружает платежные реквизиты счета
Процедура ВыгрузитьПлатежныеРеквизиты(БанковскийСчет, Слово, ПрямыеРасчетыОрганизация)
	
	Если ПрямыеРасчетыОрганизация Тогда
		ТаблицаРеквизитов.Вставить(Слово+"РасчСчет" ,БанковскийСчет.НомерСчета);
		ТаблицаРеквизитов.Вставить(Слово+"Банк1"    ,БанковскийСчет.Банк.Наименование);
		ТаблицаРеквизитов.Вставить(Слово+"Банк2"    ,БанковскийСчет.Банк.Город);
		ТаблицаРеквизитов.Вставить(Слово+"БИК"      ,БанковскийСчет.Банк.Код);
		ТаблицаРеквизитов.Вставить(Слово+"Корсчет"  ,БанковскийСчет.Банк.КоррСчет);		
	Иначе
		ТаблицаРеквизитов.Вставить(Слово+"2"        ,БанковскийСчет.НомерСчета);
		ТаблицаРеквизитов.Вставить(Слово+"3"        ,БанковскийСчет.Банк.Наименование);
		ТаблицаРеквизитов.Вставить(Слово+"4"        ,БанковскийСчет.Банк.Город);
		ТаблицаРеквизитов.Вставить(Слово+"РасчСчет" ,БанковскийСчет.Банк.КоррСчет);
		ТаблицаРеквизитов.Вставить(Слово+"Банк1"    ,БанковскийСчет.БанкДляРасчетов.Наименование);
		ТаблицаРеквизитов.Вставить(Слово+"Банк2"    ,БанковскийСчет.БанкДляРасчетов.Город);
		ТаблицаРеквизитов.Вставить(Слово+"БИК"      ,БанковскийСчет.БанкДляРасчетов.Код);
		ТаблицаРеквизитов.Вставить(Слово+"Корсчет"  ,БанковскийСчет.БанкДляРасчетов.КоррСчет);		
	КонецЕсли;
	
КонецПроцедуры

// Выгружает налоговые реквизиты из документа
Процедура ВыгрузитьНалоговыеДанные(Докум)
	ТаблицаРеквизитов.Вставить("СтатусСоставителя"  ,Докум.СтатусСоставителя);
	ТаблицаРеквизитов.Вставить("ПлательщикКПП"      ,?(ПустаяСтрока(Докум.КПППлательщика),"0",Докум.КПППлательщика));
	ТаблицаРеквизитов.Вставить("ПолучательКПП"      ,?(ПустаяСтрока(Докум.КПППолучателя),"0",Докум.КПППолучателя));
	ТаблицаРеквизитов.Вставить("ПоказательКБК"      ,Докум.КодБК);  	//104
	ТаблицаРеквизитов.Вставить("ОКАТО"              ,Докум.КодОКТМО); 	//105
	Если ПустаяСтрока(СокрЛП(Докум.КодУИН)) Тогда
		ТаблицаРеквизитов.Вставить("Код"			,"0");
	Иначе
		ТаблицаРеквизитов.Вставить("Код"			,СокрЛП(Докум.КодУИН));
	КонецЕсли; 
	Если Докум.СтатусСоставителя="15" Тогда
		ТаблицаРеквизитов.Вставить("ПоказательОснования","0");   //106
		ТаблицаРеквизитов.Вставить("ПоказательПериода"  ,"0");   //107
		ТаблицаРеквизитов.Вставить("ПоказательНомера"   ,"0");   //108
		ТаблицаРеквизитов.Вставить("ПоказательДаты"     ,"0");   //109
		ТаблицаРеквизитов.Вставить("ПоказательТипа"     ,"0");   //110
	Иначе
		ТаблицаРеквизитов.Вставить("ПоказательОснования",Докум.ПоказательОснования);
		ТаблицаРеквизитов.Вставить("ПоказательПериода"  ,?(ПустаяСтрока(Докум.ПоказательПериода),"0",Докум.ПоказательПериода));
		ТаблицаРеквизитов.Вставить("ПоказательНомера"   ,?(ПустаяСтрока(Докум.ПоказательНомера),"0",Докум.ПоказательНомера));
		ТаблицаРеквизитов.Вставить("ПоказательДаты"     ,Формат(Докум.ПоказательДаты,"ДФ=dd.MM.yyyy; ДП=0"));
		ТаблицаРеквизитов.Вставить("ПоказательТипа"     ,?(ПустаяСтрока(Докум.ПоказательТипа),"0",Докум.ПоказательТипа));
	КонецЕсли;
КонецПроцедуры	

// Выгружает реквизиты документов
//
Процедура ВыгрузитьДокументы(ТекущийСчёт)
	//отбираем подходящие под параметры документы
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачПериода", НачалоДня(НачПериода));
	Запрос.УстановитьПараметр("КонПериода", КонецДня(КонПериода));
	Запрос.УстановитьПараметр("СчетОтбора", ТекущийСчёт);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Номер КАК Номер,
	|	Док.Дата КАК Дата,
	|	Док.СуммаДокумента КАК СуммаДокумента,
	|	Док.Организация.НаименованиеПолное КАК Организация,
	|	Док.Организация.ИНН КАК ОрганизацияИНН,
	|	Док.Организация.КПП КАК ОрганизацияКПП,
	|	Док.СчетОрганизации КАК СчетОрганизации,
	|	Док.СчетОрганизации.НомерСчета КАК НомерСчетаОрганизации,
	|	Док.СчетОрганизации.БанкДляРасчетов КАК БанкДляРасчетовОрганизации,
	|	Док.СчетОрганизации.Банк.Наименование КАК БанкОрганизации,
	|	Док.СчетОрганизации.СуммаБезКопеек КАК СуммаБезКопеек,
	|	Док.Контрагент.НаименованиеПолное КАК Контрагент,
	|	ВЫБОР
	|		КОГДА Док.Контрагент.ФормаСобственности = ЗНАЧЕНИЕ(Перечисление.ФормыСобственности.ОбособленноеПодразделение)
	|			ТОГДА Док.Контрагент.ГоловнойКонтрагент.ИНН
	|		ИНАЧЕ Док.Контрагент.ИНН
	|	КОНЕЦ КАК КонтрагентИНН,
	|	Док.Контрагент.КПП КАК КонтрагентКПП,
	|	Док.СчетКонтрагента КАК СчетКонтрагента,
	|	Док.СчетКонтрагента.НомерСчета КАК НомерСчетаКонтрагента,
	|	Док.СчетКонтрагента.БанкДляРасчетов КАК БанкДляРасчетовКонтрагента,
	|	Док.СчетКонтрагента.Банк.Наименование КАК БанкКонтрагента,
	|	Док.ИННПлательщика КАК ИННПлательщика,
	|	Док.ИННПолучателя КАК ИННПолучателя,
	|	Док.КПППлательщика КАК КПППлательщика,
	|	Док.КПППолучателя КАК КПППолучателя,
	|	Док.ТекстПлательщика КАК ТекстПлательщика,
	|	Док.ТекстПолучателя КАК ТекстПолучателя,
	|	Док.ВидПлатежа КАК ВидПлатежа,
	|	Док.КодУИН КАК КодУИН,
	|	Док.КодВидаДохода КАК КодВидаДохода
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК Док
	|ГДЕ
	|	Док.Дата >= &НачПериода
	|	И Док.Дата <= &КонПериода
	|	И Док.СчетОрганизации = &СчетОтбора
	|	И Док.Организация = &Организация
	|	И Док.ПометкаУдаления = ЛОЖЬ
	|	И НЕ Док.СуммаДокумента = 0
	|	И Док.Ссылка В(&МассивДокументов)"; 
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	ПустыеПоля =Новый Структура();
	ПустыеПоля.Очистить();
	Пока Выборка.Следующий() Цикл
		ТаблицаРеквизитов = Новый Соответствие();
		ТаблицаРеквизитов.Вставить("СекцияДокумент" ,"Платежное поручение");
		ТаблицаРеквизитов.Вставить("Номер"          ,дкПолучитьНомерДляПечати(Выборка.Ссылка,Истина));
		ТаблицаРеквизитов.Вставить("Дата"           ,Формат(Выборка.Дата,"ДЛФ=Д"));
		ТаблицаРеквизитов.Вставить("Сумма"          ,ФорматироватьСумму(Выборка.СуммаДокумента,Выборка.СуммаБезКопеек));
		
		ПрямыеРасчетыОрганизация = Выборка.БанкДляРасчетовОрганизации.Пустая();
		ПрямыеРасчетыКонтрагент = Выборка.СчетКонтрагента.БанкДляРасчетов.Пустая();
		ТаблицаРеквизитов.Вставить("ПлательщикСчет",Выборка.НомерСчетаОрганизации);
		
		ПлательщикИНН           = ?(ПустаяСтрока(Выборка.ИННПлательщика), Выборка.ОрганизацияИНН, СокрЛП(Выборка.ИННПлательщика));
		ПлательщикКПП			= ?(ПустаяСтрока(Выборка.КПППлательщика), Выборка.ОрганизацияКПП, СокрЛП(Выборка.КПППлательщика));
		Плательщик              = "ИНН " + ПлательщикИНН+" "+?(ПустаяСтрока(Выборка.ТекстПлательщика),Выборка.Организация,СокрЛП(Выборка.ТекстПлательщика));
		Плательщик =?(ПрямыеРасчетыОрганизация, Плательщик, Плательщик+" "+Выборка.БанкОрганизации);
		ТаблицаРеквизитов.Вставить("Плательщик",Плательщик);
		ТаблицаРеквизитов.Вставить("ПлательщикИНН" ,ПлательщикИНН);
		ТаблицаРеквизитов.Вставить("ПлательщикКПП" ,ПлательщикКПП);
		ТаблицаРеквизитов.Вставить("Плательщик1"   ,?(ПустаяСтрока(Выборка.ТекстПлательщика),Выборка.Организация,СокрЛП(Выборка.ТекстПлательщика)));
		ВыгрузитьПлатежныеРеквизиты(Выборка.СчетОрганизации,"Плательщик", ПрямыеРасчетыОрганизация);
		ТаблицаРеквизитов.Вставить("ПолучательСчет" ,Выборка.НомерСчетаКонтрагента);
		ПолучательИНН           = ?(ПустаяСтрока(Выборка.ИННПолучателя), Выборка.КонтрагентИНН, СокрЛП(Выборка.ИННПолучателя));
		ПолучательКПП			= ?(ПустаяСтрока(Выборка.КПППолучателя), Выборка.КонтрагентКПП, СокрЛП(Выборка.КПППолучателя));
		Получатель              = "ИНН " + ПолучательИНН+" "+?(ПустаяСтрока(Выборка.ТекстПолучателя),Выборка.Контрагент,СокрЛП(Выборка.ТекстПолучателя));
		Получатель =?(ПрямыеРасчетыКонтрагент, Получатель, Получатель+" "+Выборка.БанкКонтрагента);
		ТаблицаРеквизитов.Вставить("Получатель",Получатель);
		ТаблицаРеквизитов.Вставить("ПолучательИНН" ,ПолучательИНН);
		ТаблицаРеквизитов.Вставить("ПолучательКПП" ,ПолучательКПП);
		ТаблицаРеквизитов.Вставить("Получатель1"    ,?(ПустаяСтрока(Выборка.ТекстПолучателя),Выборка.Контрагент,СокрЛП(Выборка.ТекстПолучателя)));
		ВыгрузитьПлатежныеРеквизиты(Выборка.СчетКонтрагента,"Получатель", ПрямыеРасчетыКонтрагент);
		ТаблицаРеквизитов.Вставить("ВидПлатежа"	,Выборка.Ссылка.ВидПлатежа);
		ТаблицаРеквизитов.Вставить("Очередность"	,Выборка.Ссылка.ОчередностьПлатежа);
		ТаблицаРеквизитов.Вставить("ВидОплаты"	,"01");
		
		НазначениеПлатежа = Выборка.Ссылка["НазначениеПлатежа"];
		Если НЕ ПустаяСтрока(НазначениеПлатежа) Тогда
			НазначениеПлатежаСтрока = СтрЗаменить(НазначениеПлатежа,Символы.ПС,", ");
			ТаблицаРеквизитов.Вставить("НазначениеПлатежа"	,НазначениеПлатежаСтрока);
			Для ц=1 По СтрЧислоСтрок(НазначениеПлатежа) Цикл
				НазначениеПлатежаСтрока = СтрПолучитьСтроку(НазначениеПлатежа,ц);
				ТаблицаРеквизитов.Вставить("НазначениеПлатежа"+ц	,НазначениеПлатежаСтрока);
			КонецЦикла;
		КонецЕсли;
		Если Выборка.Ссылка.ПеречислениеНалога=Истина Тогда
			ВыгрузитьНалоговыеДанные(Выборка.Ссылка);	
		Иначе	
			Если ПустаяСтрока(СокрЛП(Выборка.КодУИН)) Тогда
				//ТаблицаРеквизитов.Вставить("Код","0");
			Иначе
				ТаблицаРеквизитов.Вставить("Код",СокрЛП(Выборка.КодУИН));
			КонецЕсли; 
		КонецЕсли;
		
			// КодНазПлатежа
		Если ДействуетУказаниеБанкаРоссии5286У(Выборка.Дата) Тогда
			ТаблицаРеквизитов.Вставить("КодНазПлатежа",СокрЛП(Выборка.КодВидаДохода));
		КонецЕсли;
		
		//проверка наличия всех полей и их значений
		//в основной таблице
		Для ц=0 По ОбязательныеАтрибутыВЫГРУЗКИ.Количество()-1 Цикл
			Значение = ТаблицаРеквизитов[Строка(ОбязательныеАтрибутыВЫГРУЗКИ.Получить(ц))];
			Атрибут  = ОбязательныеАтрибутыВЫГРУЗКИ.Получить(ц).Значение;
			Если Атрибут = "Очередность" Тогда
				Продолжить;
			КонецЕсли;
			Если (Значение = Неопределено) ИЛИ (СокрЛП(Значение)="") Тогда
				ПустыеПоля.Вставить(ОбязательныеАтрибутыВЫГРУЗКИ.Получить(ц));
				Сообщить("В документе "+Выборка.Ссылка+": не указано поле выгрузки "+ОбязательныеАтрибутыВЫГРУЗКИ.Получить(ц));
			КонецЕсли;
		КонецЦикла;
		//если расчеты с бюджетом (по налогам)
		Если Выборка.Ссылка.ПеречислениеНалога=Истина Тогда
			Для ц=0 По ОбязательныеАтрибутыБЮДЖЕТ.Количество()-1 Цикл
				Значение = ТаблицаРеквизитов[Строка(ОбязательныеАтрибутыБЮДЖЕТ.Получить(ц))];
				Если (Значение = Неопределено) ИЛИ (СокрЛП(Значение)="") Тогда
					ПустыеПоля.Вставить(ОбязательныеАтрибутыБЮДЖЕТ.Получить(ц));
					Сообщить("В документе "+Выборка.Ссылка+": не указано поле выгрузки "+ОбязательныеАтрибутыБЮДЖЕТ.Получить(ц));
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
		//добавляем все прописанные выше реквизиты только те у которых есть значеня
		Для ц=0 По ПорядокРеквизитов.Количество()-1 Цикл
			Значение = ТаблицаРеквизитов[Строка(ПорядокРеквизитов.Получить(ц))];
			Если (Значение <> Неопределено) Тогда
				Текст.ДобавитьСтроку(Строка(ПорядокРеквизитов.Получить(ц))+"="+Значение);
			КонецЕсли;	
		КонецЦикла;
		Текст.ДобавитьСтроку("КонецДокумента");
		
		Если ПустыеПоля.Количество() <> 0 Тогда
			Стр =ПустыеПоляДокумента.Добавить();
			Стр["Поля"]     =ПустыеПоля;
			Стр["Документ"] =Выборка.Ссылка;
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

// Функция добавления колонки в таблицы
//
// Параметры:
//	РабочаяТаблица - таблича для создания колонки
//	ТабПоле - Табличное поле для создания колонки
//	Имя - имя колонки
//	ТипКолонки - Тип создоваемой колонки(необязательный)
//	Заголовок - Заголовок в шапке табличного поля
//	СоответсвиеХарактеристик
//	Подсказка - Всплывающая подсказка
//
Функция СоздатьКолонкуВТаблице(РабочаяТаблица, ТабПоле, Имя, ТипКолонки = Неопределено, Заголовок = "", СоответсвиеХарактеристик,Подсказка = "")
	
	Если ПустаяСтрока(Имя) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ШиринаКолонки = (СоответсвиеХарактеристик["Ширина"]);
	Если ТипЗнч(ШиринаКолонки) <> Тип("Число") Тогда
		ШиринаКолонки =  0;
	КонецЕсли;
	
	Если ТипЗнч(ТипКолонки) = Тип("ОписаниеТипов") Тогда
		РабочаяТаблица.Колонки.Добавить(Имя, ТипКолонки, Заголовок, ШиринаКолонки);
		
	Иначе
		РабочаяТаблица.Колонки.Добавить(Имя, , Заголовок, ШиринаКолонки);
		
	КонецЕсли;
	
	Если ТабПоле <> Неопределено Тогда
		// Создаем колонку в табличном поле
		КолонкаТабПоля = ТабПоле.Колонки.Добавить(Имя, Заголовок);
		
		Если ТипКолонки = Новый ОписаниеТипов("Булево") Тогда
			// связываем её с данными флажка
			КолонкаТабПоля.ДанныеФлажка = Имя;
			КолонкаТабПоля.УстановитьЭлементУправления(Тип("Флажок"));
			
		Иначе
			// связываем её с данными
			КолонкаТабПоля.Данные = Имя;
			КолонкаТабПоля.УстановитьЭлементУправления(Тип("ПолеВвода"));
			
		КонецЕсли;
		
		Если ТипЗнч(ШиринаКолонки) = Тип("Число") Тогда
			Если ШиринаКолонки > 0 Тогда
				КолонкаТабПоля.Ширина  = ШиринаКолонки;
			КонецЕсли;
		КонецЕсли;
		
		// устанавливаем характеристики
		КолонкаТабПоля.ИзменятьНастройку = (СоответсвиеХарактеристик["ИзменятьНастройку"] <> Ложь);
		
		КолонкаТабПоля.ИзменятьПозицию   = (СоответсвиеХарактеристик["ИзменятьПозицию"] <> Ложь);
		КолонкаТабПоля.Видимость         = (СоответсвиеХарактеристик["Видимость"] <> Ложь);
		КолонкаТабПоля.Доступность       = (СоответсвиеХарактеристик["Доступность"] <> Ложь);
		КолонкаТабПоля.ТолькоПросмотр    = (СоответсвиеХарактеристик["ТолькоПросмотр"] = Истина);
		
		КолонкаТабПоля.ИзменениеРазмера  = ?((СоответсвиеХарактеристик["ИзменениеРазмера"] <> Ложь), ИзменениеРазмераКолонки.Изменять, ИзменениеРазмераКолонки.НеИзменять);
		
		Если ТипЗнч(Подсказка) = Тип("Строка") Тогда
			КолонкаТабПоля.ПодсказкаВШапке = Подсказка;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// формирование структуры таблицы экспорта
Процедура СформироватьСтруктуруТаблицыЭкспорта(Таб, ТабПоле) Экспорт
	
	КлючиСтруктуры = "ИзменятьНастройку,ИзменятьПозицию,Видимость,Доступность,ТолькоПросмотр,ИзменениеРазмера,Ширина";
	
	Видимая      = Новый Структура(КлючиСтруктуры, Истина, Ложь,   Истина, Истина, Истина, Истина,  0);
	Видимая020   = Новый Структура(КлючиСтруктуры, Истина, Ложь,   Истина, Истина, Истина, Ложь,    3);
	Видимая080   = Новый Структура(КлючиСтруктуры, Истина, Ложь,   Истина, Истина, Истина, Ложь,   10);
	Видимая120   = Новый Структура(КлючиСтруктуры, Истина, Ложь,   Истина, Истина, Истина, Ложь,   15);
	Видимая160   = Новый Структура(КлючиСтруктуры, Истина, Ложь,   Истина, Истина, Истина, Ложь,   20);
	
	НеВидимая    = Новый Структура(КлючиСтруктуры, Истина, Ложь,   Ложь,   Истина, Истина, Истина,  0);
	
	Т_Булево    = Новый ОписаниеТипов("Булево");
	
	Т_Число15_2 = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2));
	Т_Число1    = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(1));
	Т_Число10   = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10));
	
	Т_Строка    = Новый ОписаниеТипов("Строка");
	Т_Строка02  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(2));
	Т_Строка09  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(9));
	Т_Строка10  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(10));
	Т_Строка11  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(11));
	Т_Строка12  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(12));
	Т_Строка20  = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20));
	Т_Строка100 = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100));
	
	Т_Дата      = Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата));
		
	Т_Документ  = Новый ОписаниеТипов("ДокументСсылка.ПлатежноеПоручение");
	Т_БанкСчет  = Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчета");
	Т_Контрагент  = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	Т_ВидСтатуса = Новый ОписаниеТипов("ПеречислениеСсылка.ВидыСтатусовЗагрузкиВыгрузки");
	
	Таб.Очистить();
	Таб.Колонки.Очистить();
	
	//  Имя, заголовок, выгружать, не пустая, ширина, изменять размер
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Выгружать",            Т_Булево,    "_",                          Видимая020, "Выгружать");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Готовность",           Т_Число1,    "Готовность",                 НеВидимая, "Готовность");
	
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Номер",                Т_Строка,    "Номер",                      Видимая080, "Номер документа");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Дата",                 Т_Дата,      "Дата",                       Видимая080, "Дата документа");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "СчетОрганизации",      Т_БанкСчет,  "Эл. Р/с организации",            НеВидимая, "Р/с организации");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "НомерСчетаОрганизации",Т_Строка,    "Р/с организации",            Видимая160, "Номер р/с организации");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Сумма",                Т_Строка20,  "Сумма",                      Видимая120, "Сумма");
	
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Контрагент",           Т_Строка,    "Контрагент",                 Видимая, "Контрагент");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "СчетКонтрагента",      Т_БанкСчет,  "Р/с контрагента",            НеВидимая ,"Р/с контрагента");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "НомерСчетаКонтрагента",Т_Строка,    "Эл. Р/с контрагента",            Видимая160, "Номер Р/с контрагента");
	
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "НазначениеПлатежа",    Т_Строка,    "Назначение платежа",         Видимая, "Назначение платежа");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Документ",             Т_Документ,  "Источник",                   НеВидимая, "Источник");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "ИсточникПриемник",     Т_Строка100, "Источник/Приемник",          Видимая160, "Источник/приемник");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "ВидСтатуса",           Т_ВидСтатуса,"Вид статуса",                Видимая120, "Вид статуса");
	СоздатьКолонкуВТаблице(Таб, ТабПоле, "Статус",               Т_Дата,      "Статус",                     Видимая080, "Статус");
КонецПроцедуры

// Заполняет документы на экспорт
Процедура ЗаполнитьДокументыНаЭкспорт(ДокументыНаЭкспорт) Экспорт
	ДокументыНаЭкспорт.Очистить();
	
	ЗапросПоДокументам = Новый Запрос;  
	//БизТех 27.10.2022г. в запросе прописала ПРЕДСТАВЛЕНИЕ, чтобы номер выгружался как строка без разделителей групп.
	ЗапросПоДокументам.Текст = "ВЫБРАТЬ
	                           |	ПлатежноеПоручение.Ссылка КАК Документ,
	                           |	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.Номер) КАК Номер,  //БизТех++
	                           |	ПлатежноеПоручение.Дата КАК Дата,
	                           |	ПлатежноеПоручение.Контрагент КАК Контрагент,
	                           |	ПлатежноеПоручение.СуммаДокумента КАК Сумма,
	                           |	ПлатежноеПоручение.СчетОрганизации КАК СчетОрганизации,
	                           |	ПлатежноеПоручение.СчетКонтрагента КАК СчетКонтрагента,
	                           |	ПлатежноеПоручение.СчетОрганизации.НомерСчета КАК НомерСчетаОрганизации,
	                           |	ПлатежноеПоручение.СчетКонтрагента.НомерСчета КАК НомерСчетаКонтрагента,
	                           |	ПлатежноеПоручение.НазначениеПлатежа КАК НазначениеПлатежа,
	                           |	ПлатежноеПоручение.КодВидаДохода КАК КодВидаДохода,
	                           |	СтатусыЗагрузкиВыгрузкиОбъектов.ВидСтатуса.Ссылка КАК ВидСтатуса,
	                           |	СтатусыЗагрузкиВыгрузкиОбъектов.Статус КАК Статус,
	                           |	СтатусыЗагрузкиВыгрузкиОбъектов.ИсточникПриемник КАК ИсточникПриемник,
	                           |	ВЫБОР
	                           |		КОГДА СтатусыЗагрузкиВыгрузкиОбъектов.Статус ЕСТЬ NULL
	                           |			ТОГДА ИСТИНА
	                           |		ИНАЧЕ ЛОЖЬ
	                           |	КОНЕЦ КАК Выгружать
	                           |ИЗ
	                           |	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	                           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗагрузкиВыгрузкиОбъектов КАК СтатусыЗагрузкиВыгрузкиОбъектов
	                           |		ПО ПлатежноеПоручение.Ссылка = СтатусыЗагрузкиВыгрузкиОбъектов.Объект
	                           |ГДЕ
	                           |	ПлатежноеПоручение.Дата МЕЖДУ &ДатаНачало И &ДатаОкончание
	                           |	И ПлатежноеПоручение.СчетОрганизации В(&МассивСчетов)
	                           |	И ПлатежноеПоручение.ПометкаУдаления = ЛОЖЬ
	                           |	И ПлатежноеПоручение.Организация = &Организация
	                           |	И ПлатежноеПоручение.СуммаДокумента <> 0";	
	МассивСчетов = Новый Массив;
	
	Для Каждого СтрокаСчетов из ТаблицаСчетов Цикл
		Если СтрокаСчетов.Пометка Тогда
			МассивСчетов.Добавить(СтрокаСчетов.Счет);
		КонецЕсли;
	КонецЦикла;
	
	КонецПериода = КонПериода;
	Если КонПериода = Дата("00010101") Тогда
		КонецПериода = Дата("29990101");
	КонецЕсли;
	
	ЗапросПоДокументам.УстановитьПараметр("ДатаНачало",НачалоДня(НачПериода));
	ЗапросПоДокументам.УстановитьПараметр("ДатаОкончание",КонецДня(КонецПериода));
	ЗапросПоДокументам.УстановитьПараметр("МассивСчетов",МассивСчетов);
	ЗапросПоДокументам.УстановитьПараметр("Организация", Организация);
	
	ДокументыНаЭкспорт = ЗапросПоДокументам.Выполнить().Выгрузить();
КонецПроцедуры

// Возвращает документы на экспорт по списку
Функция ПолучитьМассивДокументов(ДокументыНаЭкспорт) Экспорт
	МассивДокументов = Новый Массив;
	Для Каждого ДокументЭкспорт Из ДокументыНаЭкспорт Цикл
		Если ДокументЭкспорт.Выгружать Тогда
			МассивДокументов.Добавить(ДокументЭкспорт.Документ);
		КонецЕсли;
	КонецЦикла;
	Возврат  МассивДокументов;
КонецФункции

// Запись в регистр "СтатусыЗагрузкиВыгрузкиОбъектов"
Процедура ЗаписатьВРегистр()
	Для Каждого ДокументЭкспорта Из МассивДокументов Цикл
		Менеджер = РегистрыСведений.СтатусыЗагрузкиВыгрузкиОбъектов.СоздатьМенеджерЗаписи();
		Менеджер.Объект = ДокументЭкспорта;
		Менеджер.ВидСтатуса = Перечисления.ВидыСтатусовЗагрузкиВыгрузки.ВыгруженВКлиентБанк;
		Менеджер.ИсточникПриемник = КлиентБанка;
		Менеджер.Статус = ТекущаяДата();
		Менеджер.Записать();
	КонецЦикла;
КонецПроцедуры


// Функция поиска документа в базе
//Функция НайтиДокумент(Дата, БанковскийСчет, МассивДокументов)
//	// Создадим массив контрагентов задействованных в текущем документа
//	МассивКонтрагентов = Новый Массив;
//	Для Каждого ТекДокумент Из МассивДокументов Цикл
//		Если ТекДокумент["ВИДДВИЖЕНИЯ"] = "Платеж" Тогда
//			МассивКонтрагентов.Добавить(НайтиКонтрагента(ТекДокумент["ПОЛУЧАТЕЛЬИНН"], ТекДокумент["ПОЛУЧАТЕЛЬСЧЕТ"]));
//		Иначе
//			МассивКонтрагентов.Добавить(НайтиКонтрагента(ТекДокумент["ПЛАТЕЛЬЩИКИНН"], ТекДокумент["ПЛАТЕЛЬЩИКСЧЕТ"]));
//		КонецЕсли;
//	КонецЦикла;
//	
//	// Составим и выполним запрос
//	ТекстЗапроса = "ВЫБРАТЬ
//	|	КОЛИЧЕСТВО(ВыпискаСостав.Контрагент) КАК КоличествоКонтрВМассиве,
//	|	ВыпискаСостав.Ссылка
//	|ИЗ
//	|	Документ.Выписка.Состав КАК ВыпискаСостав
//	|ГДЕ
//	|	ВыпискаСостав.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
//	|	И ВыпискаСостав.Ссылка.БанковскийСчет.НомерСчета = &БанковскийСчет
//	|	И ВыпискаСостав.Контрагент В(&Контрагенты)
//	|	И ВыпискаСостав.Ссылка.ПометкаУдаления = ЛОЖЬ
//	|
//	|СГРУППИРОВАТЬ ПО
//	|	ВыпискаСостав.Ссылка";
//	Запрос = Новый Запрос(ТекстЗапроса);
//	Запрос.УстановитьПараметр("ДатаНачала",		НачалоДня(Дата));
//	Запрос.УстановитьПараметр("ДатаКонца",		КонецДня(Дата));
//	Запрос.УстановитьПараметр("БанковскийСчет",	БанковскийСчет);
//	Запрос.УстановитьПараметр("Контрагенты",	МассивКонтрагентов);
//	
//	РезультатЗапроса = Запрос.Выполнить().Выбрать();
//	Пока РезультатЗапроса.Следующий() Цикл
//		Если ((РезультатЗапроса.КоличествоКонтрВМассиве = РезультатЗапроса.Ссылка.Состав.Количество())
//		И (РезультатЗапроса.КоличествоКонтрВМассиве = МассивКонтрагентов.Количество())) Тогда
//			//Нашли документ
//			Индекс=0;
//			НайденныйДокумент = РезультатЗапроса.Ссылка.ПолучитьОбъект();
//			Сообщить("Найден документ загрузки в базе: "+РезультатЗапроса.Ссылка);
//			Для Каждого Контрагент Из МассивКонтрагентов Цикл
//				СтрокаНайденногоДокумента = НайденныйДокумент.Состав.Найти(Контрагент,"Контрагент");
//				
//				ВидДвижения = СписокДокументов[Индекс].Значение["ВИДДВИЖЕНИЯ"];
//				Если ВидДвижения = "Платеж" Тогда
//					НазначениеПлатежа = Справочники.СтатьиДДС.ОплатаПоставщику
//				Иначе
//					НазначениеПлатежа = Справочники.СтатьиДДС.ОплатаОтПокупателя
//				КонецЕсли;
//				
//				Если СтрокаНайденногоДокумента.СтатьяДДС<>НазначениеПлатежа Тогда
//					СтрокаНайденногоДокумента.СтатьяДДС=НазначениеПлатежа;
//					Сообщить("У документа: "+РезультатЗапроса.Ссылка+" изменено поле Статьи ДДС.
//					Строка "+СтрокаНайденногоДокумента.НомерСтроки);
//				КонецЕсли;
//				Если ВидДвижения = "Платеж" Тогда
//					Если СтрокаНайденногоДокумента.СуммаРасход <> Число(СписокДокументов[Индекс].Значение["СУММА"]) Тогда
//						СтрокаНайденногоДокумента.СуммаРасход = Число(СписокДокументов[Индекс].Значение["СУММА"]);
//						Сообщить("У документа: "+РезультатЗапроса.Ссылка+" изменено поле Сумма расхода.
//						Строка "+СтрокаНайденногоДокумента.НомерСтроки);
//					КонецЕсли;
//					Если СтрокаНайденногоДокумента.СуммаПриход <> 0 Тогда
//						СтрокаНайденногоДокумента.СуммаПриход = 0;
//					КонецЕсли;
//				Иначе
//					Если СтрокаНайденногоДокумента.СуммаРасход <> 0 Тогда
//						СтрокаНайденногоДокумента.СуммаРасход = 0;
//					КонецЕсли;
//					Если СтрокаНайденногоДокумента.СуммаПриход <> Число(СписокДокументов[Индекс].Значение["СУММА"]) Тогда
//						СтрокаНайденногоДокумента.СуммаПриход = Число(СписокДокументов[Индекс].Значение["СУММА"]);
//						Сообщить("У документа: "+РезультатЗапроса.Ссылка+" изменено поле Сумма прихода.
//						Строка "+СтрокаНайденногоДокумента.НомерСтроки);
//					КонецЕсли;
//				КонецЕсли;
//				Индекс=Индекс+1;
//			КонецЦикла;
//			Если НайденныйДокумент.Модифицированность() Тогда
//				Сообщить("Документ: "+РезультатЗапроса.Ссылка+" изменён");
//				Попытка
//					НайденныйДокумент.Записать(РежимЗаписиДокумента.Запись,РежимПроведенияДокумента.Неоперативный);
//				Исключение
//				КонецПопытки;
//			КонецЕсли;
//			ЗагрДокументы.Добавить(НайденныйДокумент.Ссылка);
//			Возврат НайденныйДокумент.Ссылка;
//		КонецЕсли;
//	КонецЦикла;
//	
//	Возврат Документы.Выписка.ПустаяСсылка();
//КонецФункции

#Область ВыделениеНДСИзНазначенияПлатежа

Функция ВыделитьНДСИзНазначенияПлатежа(ИсходноеНазначениеПлатежа, СуммаПлатежа)
	
	Перем СтавкаНДС, ЗначениеСтавкиНДС, ЗначениеСуммыНДС;
	
	Результат = Новый Структура("СтавкаНДС, СуммаНДС", Справочники.СтавкиНДС.БезНДС, 0);
	
	НазначениеПлатежа = ПривестиНазначениеПлатежа(ИсходноеНазначениеПлатежа);
	
	СпецСимволыЧисла = ":(%),.-= ";
	ИнформацияОНДС = НайтиИнформациюОНДС(НазначениеПлатежа, ЗначениеСтавкиНДС, СпецСимволыЧисла);
	
	Если ЗначениеСтавкиНДС = "БезНДС" Тогда
		СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		ЗначениеСуммыНДС = 0;
	ИначеЕсли ИнформацияОНДС = Неопределено Тогда
		ЗначениеСтавкиНДС = Неопределено;
	Иначе
		ЗначениеСтавкиНДС = Неопределено;
		РезультатПоиска = ВыделитьИскомыйТекст(ИнформацияОНДС, "%");
		Если РезультатПоиска <> Неопределено Тогда
			ИнформацияОПроцентеНДС = РезультатПоиска.ТекстДо;
			ИнформацияОСумме = РезультатПоиска.ТекстПосле;
		Иначе
			ИнформацияОПроцентеНДС = "";
			ИнформацияОСумме = ИнформацияОНДС;
		КонецЕсли;
		
		ЧисловоеЗначениеСтавкиНДС = СтрокаВЧисло(ИнформацияОПроцентеНДС, СпецСимволыЧисла);
		ЗначениеСуммыНДС = СтрокаВЧисло(ИнформацияОСумме, СпецСимволыЧисла);
		
		Если ЗначениеСуммыНДС <> Неопределено Тогда
			СуммаБезНДС = СуммаПлатежа - ЗначениеСуммыНДС;
			Если СуммаБезНДС > 0 Тогда
				РасчетноеЗначениеСтавкиНДС = Окр(100 * ЗначениеСуммыНДС / СуммаБезНДС, 0);
				Если ЧисловоеЗначениеСтавкиНДС = Неопределено Или РасчетноеЗначениеСтавкиНДС = ЧисловоеЗначениеСтавкиНДС Тогда
					ПроцентНДС = XMLСтрока(РасчетноеЗначениеСтавкиНДС);
					СтавкаНДС = СтавкаНДС(ПроцентНДС);
					Если СтавкаНДС = Неопределено Тогда
						// Расчетная ставка не найдена, подставим в документа пустую ставку и сумму НДС.
						// Если оставить Неопределено, то в документ подставится ставка "по умолчанию".
						СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
					КонецЕсли;
				Иначе
					СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЧисловоеЗначениеСтавкиНДС <> Неопределено И СтавкаНДС = Неопределено Тогда
			ПроцентНДС = XMLСтрока(ЧисловоеЗначениеСтавкиНДС);
			СтавкаНДС = СтавкаНДС(ПроцентНДС);
			ЗначениеСуммыНДС = СуммаНДС(СтавкаНДС, СуммаПлатежа);
		КонецЕсли;
	КонецЕсли;
	
	Если СтавкаНДС <> Неопределено Тогда
		Результат.СтавкаНДС = СтавкаНДС;
	КонецЕсли;
	
	Если ЗначениеСуммыНДС <> Неопределено Тогда
		Результат.Вставить("СуммаНДС", ЗначениеСуммыНДС);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НовыйШаблоныТекстаНДС()
	
	Шаблоны = Новый ТаблицаЗначений;
	Шаблоны.Колонки.Добавить("ТекстШаблона", Новый ОписаниеТипов("Строка"));
	Шаблоны.Колонки.Добавить("ЕстьНДС", Новый ОписаниеТипов("Булево"));
	// Шаблоны "Без НДС".
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'БЕЗ НАЛОГА'"); // "Без налога (НДС)"
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'БЕЗ НДС'");
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НЕТ НДС'");
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС НЕТ'");
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС НЕ ОБЛ'"); // "НДС не облагается"
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС НЕОБЛ'"); // "НДС не облагается" (могут быть некорректные сокращения).
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС НЕ УПЛ'"); // "НДС не уплачивается"
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС НЕ ВЗИМ'"); // "НДС не взимается"
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС НЕ ПРЕД'"); // "НДС не предусмотрен"
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НЕ ОБЛАГАЕТСЯ'"); // // "Не облагается НДС"
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НЕ ВЗИМАЕТСЯ'"); // "Не взимается НДС"
	
	// Шаблоны, с выделенным НДС.
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС В ТОМ ЧИСЛЕ'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС В Т.Ч.'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС В Т Ч'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС В ТЧ'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'В ТОМ ЧИСЛЕ НДС'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'В Т.Ч. НДС'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'В Т Ч НДС'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'В ТЧ НДС'");
	НоваяСтрока.ЕстьНДС = Истина;
	НоваяСтрока = Шаблоны.Добавить();
	НоваяСтрока.ТекстШаблона = НСтр("ru = 'НДС'");
	НоваяСтрока.ЕстьНДС = Истина;
	
	Возврат Шаблоны;
	
КонецФункции

Функция НайтиИнформациюОНДС(ТекстНазначениеПлатежа, ЗначениеСтавкиНДС, СпецСимволыЧисла)
	
	Перем ИнформацияОНДС;
	
	ДопустимыеСимволыЧисла = "0123456789" + СпецСимволыЧисла;
	НаборШаблонов = НовыйШаблоныТекстаНДС();
	
	Пока Не ПустаяСтрока(ТекстНазначениеПлатежа) Цикл
		РезультатПоиска = ПоискШаблонаВТекстеНазначения(ТекстНазначениеПлатежа, НаборШаблонов);
		Если РезультатПоиска = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ТекстНазначениеПлатежаПосле = СокрЛ(РезультатПоиска.ТекстПосле);
		ТекстНазначениеПлатежа = ТекстНазначениеПлатежаПосле;
		
		Шаблон = НаборШаблонов.Найти(РезультатПоиска.ИскомыйТекст, "ТекстШаблона");
		Если Не Шаблон.ЕстьНДС Тогда
			ЗначениеСтавкиНДС = "БезНДС";
			Прервать;
		ИначеЕсли ИнформацияОНДС <> Неопределено Тогда
			// В тексте назначения нашли несколько ставок НДС,
			// либо в одной из найденных подстрок есть паттерны похожие на НДС, но таковыми не являющиеся.
			// Такие ситуации не обслуживаем.
			ЗначениеСтавкиНДС = Неопределено;
			ИнформацияОНДС    = Неопределено;
			Прервать;
		КонецЕсли;
		
		Если Шаблон.ЕстьНДС И Не ПустаяСтрока(ТекстНазначениеПлатежаПосле) Тогда
			КонечнаяПозицияЧисла = 0;
			Для Индекс = 1 По СтрДлина(ТекстНазначениеПлатежаПосле) Цикл
				Символ = Сред(ТекстНазначениеПлатежаПосле, Индекс, 1);
				Если ДопустимыйСимвол(Символ, ДопустимыеСимволыЧисла) Тогда
					КонечнаяПозицияЧисла = Индекс;
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если КонечнаяПозицияЧисла > 0 Тогда
				ИнформацияОНДС = Лев(ТекстНазначениеПлатежаПосле, КонечнаяПозицияЧисла);
				ТекстНазначениеПлатежа = СокрЛ(Сред(ТекстНазначениеПлатежаПосле, КонечнаяПозицияЧисла + 1));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИнформацияОНДС;
	
КонецФункции

Функция ПривестиНазначениеПлатежа(ИсходноеНазначениеПлатежа)
	
	ЗаменяемыеСимволы = "()"
		+ Символы.НПП
		+ Символы.ПС
		+ Символы.ВК
		+ Символы.ПФ
		+ Символы.Таб
		+ Символы.ВТаб;
	
	НазначениеПлатежа = ЗаменитьОдниСимволыДругими(
		ЗаменяемыеСимволы, ИсходноеНазначениеПлатежа,
		СформироватьСтрокуСимволов(" ", СтрДлина(ЗаменяемыеСимволы)));
	
	Пока СтрНайти(НазначениеПлатежа, "  ") > 0 Цикл
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "  ", " ");
	КонецЦикла;

	НазначениеПлатежа = СокрЛП(НазначениеПлатежа);
	
	Если СтрЗаканчиваетсяНа(НазначениеПлатежа, ".") Тогда
		НазначениеПлатежа = Лев(НазначениеПлатежа, СтрДлина(НазначениеПлатежа) - 1);
	КонецЕсли;
	
	Возврат ВРег(СокрП(НазначениеПлатежа));
	
КонецФункции

Функция СтавкаНДС(ЗначениеСтавкиНДС)
	
	Если Не ЗначениеЗаполнено(ЗначениеСтавкиНДС) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	ЧисловоеЗначениеСтавки = ОписаниеТипаЧисло.ПривестиЗначение(ЗначениеСтавкиНДС);
	
	РасчетныеСтавки = Новый Массив;
	РасчетныеСтавки.Добавить(Справочники.СтавкиНДС.ОсновнаяСтавкаНДСРасчетная);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РасчетныеСтавки", РасчетныеСтавки);
	Запрос.УстановитьПараметр("СтавкаНДС", ЧисловоеЗначениеСтавки);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтавкиНДС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|ГДЕ
	|	СтавкиНДС.Ставка = &СтавкаНДС
	|	И НЕ СтавкиНДС.Ссылка В (&РасчетныеСтавки)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].Ссылка;
	
КонецФункции

Функция СуммаНДС(СтавкаНДС, СуммаДокумента)
	
	Если СтавкаНДС = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Окр((СуммаДокумента * СтавкаНДС.Ставка) / (100 + СтавкаНДС.Ставка), 2);
	
КонецФункции

Функция СтрокаВЧисло(ТекстСЧислом, ДопустимыеСимволыЧисла)
	
	Если ПустаяСтрока(ТекстСЧислом) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Текст = СтрЗаменить(ТекстСЧислом, " ", "");
	
	Для Индекс = 1 По СтрДлина(ДопустимыеСимволыЧисла) Цикл
		Символ = Сред(ДопустимыеСимволыЧисла, Индекс, 1);
		Текст = СтрЗаменить(Текст, Символ, ".");
	КонецЦикла;
	
	Пока СтрНайти(Текст, "  ") > 0 Цикл
		Текст = СтрЗаменить(Текст, "  ", " ");
	КонецЦикла;
	
	Пока СтрНачинаетсяС(Текст, ".") Цикл
		Текст = Сред(Текст, 2);
	КонецЦикла;
	
	Пока СтрЗаканчиваетсяНа(Текст, ".") Цикл
		Текст = Лев(Текст, СтрДлина(Текст) - 1);
	КонецЦикла;
	
	Если СтрЧислоВхождений(Текст, ".") > 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПозицияТочки = СтрНайти(Текст, ".");
	Точность = ?(ПозицияТочки = 0, 0, СтрДлина(Текст) - ПозицияТочки);
	ФорматнаяСтрока = "ЧДЦ=" + Точность + "; ЧН=0; ЧРД=.; ЧГ=0";
	
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	ЧисловоеЗначениеСтавки = ОписаниеТипаЧисло.ПривестиЗначение(Текст);
	
	Если Формат(ЧисловоеЗначениеСтавки, ФорматнаяСтрока) = Текст Тогда
		Возврат ЧисловоеЗначениеСтавки;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ВыделитьИскомыйТекст(ОбрабатываемыйТекст, ИскомыйТекст)
	
	ПозицияНачала = СтрНайти(НРег(ОбрабатываемыйТекст), НРег(ИскомыйТекст));
	Если ПозицияНачала = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстДо = СокрЛ(Лев(ОбрабатываемыйТекст, ПозицияНачала - 1));
	ТекстПосле = СокрЛП(Сред(ОбрабатываемыйТекст, ПозицияНачала + СтрДлина(ИскомыйТекст)));
	
	Возврат Новый Структура("ТекстДо, ИскомыйТекст, ТекстПосле, Позиция",
		ТекстДо, ИскомыйТекст, ТекстПосле, ПозицияНачала - 1);
	
КонецФункции

Функция ПоискШаблонаВТекстеНазначения(Текст, Шаблоны)
	Перем Результат;
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ДопустимыеСимволы = "0123456789" + ".,;:$№#@&_-–+*^=?!'/|\""%()[]{}<> «»“”";
	Для Каждого Шаблон Из Шаблоны Цикл
		РезультатПоиска = ВыделитьИскомыйТекст(Текст, Шаблон.ТекстШаблона);
		Если РезультатПоиска = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СимволДо = СокрЛП(Прав(РезультатПоиска.ТекстДо, 1));
		Если Шаблон.ЕстьНДС Тогда
			СимволПосле = СокрЛП(Лев(РезультатПоиска.ТекстПосле, 1));
		Иначе
			РезультатПоиска.ТекстПосле = "";
			СимволПосле = "";
		КонецЕсли;
		
		Если Не ПустаяСтрока(СимволДо) И Не ДопустимыйСимвол(СимволДо, ДопустимыеСимволы)
			Или Не ПустаяСтрока(СимволПосле) И Не ДопустимыйСимвол(СимволПосле, ДопустимыеСимволы) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Результат = Неопределено Или Результат.Позиция > РезультатПоиска.Позиция Тогда
			Результат = СкопироватьСтруктуру(РезультатПоиска);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДопустимыйСимвол(Символ, ДопустимыеСимволы)
	
	Если СтрДлина(Символ) = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат СтрНайти(ДопустимыеСимволы, Символ) > 0;
	
КонецФункции

Функция ЗаменитьОдниСимволыДругими(ЗаменяемыеСимволы, Строка, СимволыЗамены)
	
	Результат = Строка;
	
	Для НомерСимвола = 1 По СтрДлина(ЗаменяемыеСимволы) Цикл
		Результат = СтрЗаменить(Результат, Сред(ЗаменяемыеСимволы, НомерСимвола, 1), Сред(СимволыЗамены, НомерСимвола, 1));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьСтрокуСимволов(Знач Символ, Знач ДлинаСтроки)
	
	Результат = "";
	Для Счетчик = 1 По ДлинаСтроки Цикл
		Результат = Результат + Символ;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СкопироватьСтруктуру(СтруктураИсточник)
	
	СтруктураРезультат = Новый Структура;
	
	Для Каждого КлючИЗначение Из СтруктураИсточник Цикл
		СтруктураРезультат.Вставить(КлючИЗначение.Ключ, СкопироватьРекурсивно(КлючИЗначение.Значение));
	КонецЦикла;
	
	Возврат СтруктураРезультат;
	
КонецФункции

Функция СкопироватьРекурсивно(Источник)
	
	Перем Приемник;
	
	ТипИсточника = ТипЗнч(Источник);
	Если ТипИсточника = Тип("Структура") Тогда
		Приемник = СкопироватьСтруктуру(Источник);
	Иначе
		Приемник = Источник;
	КонецЕсли;
	
	Возврат Приемник;
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

РасчетныеСчета = Новый СписокЗначений;
НеНайденныеКонтрагенты = Новый СписокЗначений;
СписокДокументов = Новый СписокЗначений;
ПустаяДата = Дата("00010101");
ЗагрДокументы = Новый СписокЗначений;
ТаблицаСоответствий = Новый ТаблицаЗначений;
ТаблицаСоответствий.Колонки.Добавить("ТекущийКонтрагент", Новый ОписаниеТипов("Строка"), "Контрагент");
ТаблицаСоответствий.Колонки.Добавить("Замена", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
#КонецЕсли
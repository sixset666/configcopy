#Если Клиент Тогда

Перем Рарус_Компонента Экспорт;     // Объект компаненты рарус
Перем Права Экспорт;                // Кеш прав и настроек текущего пользователя системы
Перем GUID_Дисплей Экспорт;         // GUID-идентификатор текущего дисплея или пустая строка если нет
Перем ДлинаСтрокиДисплея Экспорт;   // Считанное значение свойства из оборудования
Перем ТекстЗаставкиДисплея Экспорт; // Считанное значение свойства из оборудования
Перем ТаймаутДисплея Экспорт;       // Таймаут ожидания выполнения команд устройством
Перем ДисплейИнформацияОСуммеЧека Экспорт; 
Перем GUID_ФР Экспорт;              // GUID-идентификатор текущего ФР или пустая строка если нет
Перем GUID_Весы Экспорт;            // GUID-идентификатор текущего ФР или пустая строка если нет
Перем ТаймаутФР Экспорт;            // Таймаут ожидания выполнения команд устройством
Перем ДлинаСтрокиФР Экспорт;        // Считанное значение свойства из оборудования
Перем GUID_ТСД	Экспорт;            // GUID-идентификатор текущего ТСД или пустая строка если нет
Перем ТаймаутТСД Экспорт;           // Таймаут ожидания выполнения команд устройством
Перем Сканер Экспорт;               // Ссылка на элемент оборудования
Перем GUID_Сканер Экспорт;          // GUID-идентификатор текущего сканера штрих-кодов рабочего места
Перем GUID_Авторизатор Экспорт;     // GUID-идентификатор текущего авторизатора платежей
Перем GUID_АвторизаторПлатежей Экспорт; // GUID-идентификатор текущего авторизатора платежей за услуги
Перем GUID_ПЧ Экспорт;              // GUID-идентификатор текущего принтера чеков
Перем ТаймаутПЧ Экспорт;            // Таймаут ожидания выполнения команд устройством
Перем ДлинаСтрокиПЧ Экспорт;        // Считанное значение свойства из оборудования
Перем ТаймаутВесы Экспорт;          // Таймаут ожидания выполнения команд устройством
Перем КартыАвторизатрора Экспорт;   // Строка с типами карт, поддерживаемых данной моделью авторизатора
Перем ТаймаутАвторизатора Экспорт;  // Таймаут ожидания выполнения команд устройством
Перем ПечатьНаПинПадеАвторизатора Экспорт; // Булево, признак того, что квитанция будет печатать сама на пин паде
Перем СчитанныйНомерКарты Экспорт;  // считанное значение с дорожки платежной карты (сохранять в базе запрещено!)
Перем ИнкассаторПоУмолчанию Экспорт; // Элемент справочника контрагенты, подставляемый в документы инкассации
Перем ПараметрыСкидкиШапки Экспорт; // Переменная для хранения параметров скидки шапки
Перем тзДоступныеВидыОплат Экспорт; // Тз доступных видов оплат ФР
Перем флДоступностьНал Экспорт;     // Флаг доступности кнопки НАЛ
Перем флДоступностьБН Экспорт;      // Флаг доступности кнопки БЕЗНАЛ
Перем РежимДиагностики	Экспорт;    // Флаг работы в режиме диагностики (запись ошибок в журнал регистрации)
Перем тзСвойства Экспорт;           // Таблица свойств чека

Перем Режим Экспорт;                // переменная для хранения текущего режима
Перем ВременныйРежим;               // временная переменная для сохранения/восстановления режима
Перем ТекстОшибки Экспорт;          // текст ошибки
Перем РезультатыФронта Экспорт;     // результаты фронта
Перем ТекСуммаПлатежа;              // текущая сумма платежа
Перем тзПлатежиЗаУслуги;            // таблица платежей
Перем ОсновнаяКассаКомпании;        // основная касса компании
Перем ПрефиксДокументаЧек;          // префикс документа чек
Перем ТаблицаБП;                    // таблица бп

Перем флВестиЛогДействийПользователяВоФронте Экспорт; // флаг ведения лога

Перем ТоварныйЧекДляПечати Экспорт; // Тут будет хранится ссылка на чек для печати товарного чека 
                                    // (если печатаем на основании ранее пробитого).

Перем НомерВнешнегоСобытия Экспорт; // номер последнего внешнего события

Перем флУстанавливатьХарактеристику; // ситуация:
									 // при добавлении товара(по нажатию Энтера) запрашивается характеристика
									 // она обрабатывается стандартным обработчиком для текущей строки, а надо
									 // просто ее получить и добавить 

Перем РедактированиеСтроки Экспорт; // Специальная переменная указывающая что ведется редактирование строки
									// Необходима, чтобы отключать лишние перерасчеты в процессе редактирования

// Объекты для работы с архивом чеков
Перем АрхивЧековПодключен Экспорт;  // Флаг определяет использование архива чеков
Перем АрхивЧековКаталог;            // Путь к каталогу архива чеков
Перем АрхивЧековДокументы;          // Архив чеков по дкументам
Перем АрхивЧековТовары;             // Архив чеков по товарам
Перем АрхивЧековВерсияАрхиваЧеков;  // Номер версии архива чеков

// Кэш настроек пользователя
Перем фкСпособВыбораСкидки Экспорт;                             // Способ выбора скидки
Перем фкИспользованиеФормыПодбора Экспорт;                      // Признак использования формы подбора
Перем фкРучнойВводВозврата Экспорт;                             // Разрешение ручного ввода возврата
Перем фкАвтоматическиСкрыватьКолонкуХарактеристик Экспорт;      // Признак автоматического скрытия колонки
																// характеристик
Перем фкТаймаутДиалогов Экспорт;                                // Время таймауа диалогов
Перем фкПечатьНепробитыхЧеков Экспорт;                          // Разрешение печати не пробитых чеков
Перем фкПоказыватьЗапросНаЗавершениеРаботы Экспорт;             // Спрашивать о завершении работы
Перем фкРежимВыводаКода Экспорт;                                // Режим вывода кода
Перем фкЗаписыватьСобытияВЖурналРегистрации Экспорт;            // Признак записи событий в журнал регистрации
Перем фкРазрешитьУменьшатьКолво Экспорт;                        // Разрешение уменьшать количество
Перем фкРаботаСФронтомКассира Экспорт;                          // Разрешение работы с фронтом кассира
Перем фкРазрешитьРаботуССистемойПослеВыхода Экспорт;            // Разрешение работы системы после выхода
Перем фкЗапретитьПодарки Экспорт;                               // Признак запрета подарков
Перем фкЗащитаПечатныхФорм Экспорт;                             // Защита печатных форм
Перем фкОткрытиеПечатныхФормДокументовВРежимеПросмотра Экспорт; // Флаг открытия печатных форм в режиме просмотра.
Перем фкРазрешитьРучнуюАвторизациюБезналичныхПлатежей Экспорт;  // Флаг разрешения ручной авторизации безналичных
																// платежей.
Перем фкСмотретьФискальныеСчетчики Экспорт;                     // Признак просмотра фискальных счетчиков.
Перем фкРазрешитьГашениеФискСчетчиков Экспорт;                  // Флаг разрешения гашения фискальных счетчиков.
Перем фкРазрешитьГашениеАвторизаторов Экспорт;                  // Флаг разрешения гашения авторизаторов.
Перем фкУстройствоПечатиНефискальныхДокументов Экспорт;         // Ссылка на устройство печати нефискальных документов
Перем фкОформлятьРКОПриВозврате Экспорт;                        // Признак оформления РКО при возврате.
Перем фкОформлятьВозвратОтПокупателяВнеКассовойСмены Экспорт;   // Признак оформления возврата от покупателя вне
																// коссовой смены.
Перем фкПечатьПречекаПлатежа Экспорт;                           // Признак пречека платежа
Перем фкПроводитьАсинхронныеПлатежи Экспорт;                    // Признак проверки асинхронных платежей
Перем фкУдалятьПлатежи Экспорт;                                 // Признак разрешения удаления платежей
Перем фкГашенияКассыБезОбработки Экспорт;                       // Признак гашения кассы без обработки
Перем флПогашатьЧекЦеликом Экспорт;                             // Признак погашения чеков целиком
Перем флИнтеллектуальныйРежимВводаОплаты Экспорт;               // Признак Интеллектуального режима ввода оплаты
Перем флРазрешитьПроводитьИнкассацию Экспорт;                   // Признак разрешения проводить инкассацию
Перем флРазрешитьРучнойВыборКарточки Экспорт;                   // Признак разрешения ручного выбора карточки
Перем флРазрешитьВводКодаКартыПользователяВручную Экспорт;      // Признак разрешения ввода кода карты пользователя
																// вручную

Перем фкХарактеристика   Экспорт;   // использования характеристик при выборе товара
Перем фкИмяРеквизитаКода Экспорт;   // имя реквизита кода
Перем фкИерархия         Экспорт;   // использования иерархии при выборе товар

// Переменные для работы с подарками
Перем ПодаркиМаксимальнаяСумма;     // Максимальная сумма подарка, используется для отбора характеристик текущего подарка
Перем флНепрерывныйВыборПодарков;   // Флаг непрерывного выбора подарков, если флаг сброшен чек возвращается в исходное состояние 

// Кеш значения шапочных скидок
Перем ЗначениеСкидкиНаценкиКЕШ;
Перем СуммаСкидкиНаценкиКЕШ;

// Вспомогательные переменные для автоматического теста АРМ
Перем РежимТестирования Экспорт;                          // Признак режима тестирования
Перем ОтветДиалогаПоумолчаниюВРежимеТестирования Экспорт; // Ответ диалого по умолчанию

Перем ВыделениеМежценовойРазницыОтдельнойСтрокой Экспорт; // Настройка подразделения Выделение межценовой разницы отдельной строкой

Перем НеПечататьФискальныйЧекПриОтправкеЭлектронногоЧекаПокупателю Экспорт; // Кеш константы печати бумажного чека на ФР.

// Флаг оплаты через QR
Перем О2_QR_ОплатаЧерезQRКод Экспорт;

Перем ОшибкаПробития Экспорт; // Переменная в которую записывается текст ошибки. Данная переменная проверяется при автопробитии

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ДЛЯ РАБОТЫ С ОПЛАТАМИ

// Функция есть оплаты
//
Функция ЕстьОплаты(Объект=Неопределено)
	
	Объект = ?(Объект=Неопределено, ЭтотОбъект, Объект);
	Возврат Объект.Оплаты.Количество()>0;
	
КонецФункции

// Процедура оплаты очистить
//
Процедура ОплатыОчистить(ЭтаФорма)
	
	Оплаты.Очистить();
	ОплатыПриОкончанииРедактирования(ЭтаФорма);
	
КонецПроцедуры

// Процедура оплаты при окончании редактирования
//
Процедура ОплатыПриОкончанииРедактирования(ЭтаФорма)
	СуммаНал = 0;
	Для Каждого СтрокаОплаты Из Оплаты Цикл
		Если СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
			СуммаНал = СуммаНал + СтрокаОплаты.Сумма;
		КонецЕсли;
	КонецЦикла;
	ПолученоНал = СуммаНал;
	ОбработкаРеквизита("ПолученоНал",,ЭтаФорма,);
	ПолученоБезНал = Оплаты.Итог("Сумма") - ПолученоНал;
	ОбработкаРеквизита("ПолученоБезнал",,ЭтаФорма,);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция СформироватьСводнуюТаблицу()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Строка");
	Таблица.Колонки.Добавить("Таблица");
	Таблица.Колонки.Добавить("Сумма");
	Таблица.Колонки.Добавить("РаспределенноеЗначение");
	
	Для Каждого Строка Из Товары Цикл
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Строка                 = Строка;
		НоваяСтрока.Таблица                = "Товары";
		НоваяСтрока.Сумма                  = Строка.СуммаВсего + Строка.СуммаСкидкиБонусами;
		НоваяСтрока.РаспределенноеЗначение = 0;
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

// Процедура оплатить документ
//
//Параметры:
//  Документ	 - 
//
Процедура ОплатитьДокумент(Документ) Экспорт
	// Проверим тип переданного объекта
	Если НЕ (Метаданные.Документы.ЧекНаОплату.ВводитсяНаОсновании.Содержит(Документ.Метаданные()) ИЛИ
		ТипЗнч(Документ)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер") ИЛИ
		ТипЗнч(Документ)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") ИЛИ
		ТипЗнч(Документ)=Тип("ДокументСсылка.ЧекКоррекции") ИЛИ
		ТипЗнч(Документ)=Тип("ДокументСсылка.Выписка")) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДоступностьПробитияЧекаКонтрагенту(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ обЗначениеНеЗаполнено(Документ) Тогда
		Если Документ.Метаданные().Проведение=Метаданные.СвойстваОбъектов.Проведение.Разрешить Тогда
			Если обПраво("ВводНаОснованииПроведенныхДокументов",глПрава,,Документ) Тогда
				Если НЕ Документ.Проведен Тогда
					Сообщить("Ввод оплаты на основании непроведенного документа запрещен.", СтатусСообщения.Важное); 
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
	// Вызываем обработчик выбора чека на оплату
	Если ТипЗнч(Документ)=Тип("ДокументСсылка.ЧекНаОплату") Тогда
		Если Документ.ХозОперация=Справочники.ХозОперации.ЧекНаОплату
			ИЛИ Документ.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки Тогда
			Если Документ.Проведен Тогда
				РежимВыбора = "ВыборДокументаНаОплатуВозврат";
				ЗначениеВыбора = Документ;
				ПолнаяСуммаПоСделке = Документ.СуммаДокумента;
			Иначе
				РежимВыбора = "ВыборДокументаНаОплату";
				ЗначениеВыбора = Документ;
				ПолнаяСуммаПоСделке = Документ.СуммаДокумента;
			КонецЕсли;
		ИначеЕсли Документ.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат Тогда
			Предупреждение("На основании возврата по документу <Чек на оплату> нельзя вводить чеки на оплату."+Символы.ПС+"Выполнение операции прервано.");
			Возврат;
		ИначеЕсли Документ.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
			Предупреждение("На основании возврата по документу <Чек на оплату покупки> нельзя вводить чеки на оплату."+Символы.ПС+"Выполнение операции прервано.");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Документ)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер") ИЛИ ТипЗнч(Документ)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		Если НЕ Документ.ДляПробитияНаФР Тогда
			Предупреждение("Данный документ не предназначен для пробития на фискальном регистраторе. Установите признак документа ""Для пробития на фискальном регистраторе"" для возможности продолжения операции."+Символы.ПС+"Выполнение операции прервано.");
			Возврат;
		ИначеЕсли обЗначениеНеЗаполнено(Документ.ДатаФР) Тогда
			РежимВыбора = "ВыборДокументаНаОплату";
			ЗначениеВыбора = Документ;
			ПолнаяСуммаПоСделке = Документ.СуммаДокумента;
		Иначе
			Предупреждение("Данный документ уже был пробит на фискальном регистраторе. Повторное выполнение операции запрещено."+Символы.ПС+"Выполнение операции прервано.");
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Документ.ДокументОснование) И дкДокументЕстьРеквизитШапки("ТипЦен", Документ.ДокументОснование.Метаданные().Имя)
			И НЕ Документ.ДокументОснование.ТипЦен.ЦенаВключаетНДС Тогда
			Сообщить("Тип цен документа основания не включает НДС!");
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Документ)=Тип("ДокументСсылка.Выписка") Тогда
		
		Если НЕ Документ.ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
			Предупреждение("Пробитие на фискальном регистраторе доступно только для документа с хоз. операцией ""Строка банковской выписка"". "+Символы.ПС+"Выполнение операции прервано.");
			Возврат;
		ИначеЕсли НЕ Документ.ДляПробитияНаФР Тогда
			Предупреждение("Данный документ не предназначен для пробития на фискальном регистраторе. Установите признак документа ""Для пробития на фискальном регистраторе"" для возможности продолжения операции."+Символы.ПС+"Выполнение операции прервано.");
			Возврат;
		ИначеЕсли обЗначениеНеЗаполнено(Документ.ДатаФР) Тогда
			РежимВыбора = "ВыборДокументаНаОплату";
			ЗначениеВыбора = Документ;
			ЭтоОплатаДокумента = (Документ.СтатьяДДС.ВидДвижения = Перечисления.ВидыДвижений.Приход ИЛИ НЕ Документ.СуммаДокументаПриход = 0);
			ПолнаяСуммаПоСделке = ?(ЭтоОплатаДокумента, Документ.СуммаДокументаПриход, Документ.СуммаДокументаРасход);
		Иначе
			Предупреждение("Данный документ уже был пробит на фискальном регистраторе. Повторное выполнение операции запрещено."+Символы.ПС+"Выполнение операции прервано.");
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Документ.ДокументОснование) И дкДокументЕстьРеквизитШапки("ТипЦен", Документ.ДокументОснование.Метаданные().Имя)
			И НЕ Документ.ДокументОснование.ТипЦен.ЦенаВключаетНДС Тогда
			Сообщить("Тип цен документа основания не включает НДС!");
		КонецЕсли;
		
	Иначе
		// Произведем формирование чека на оплату по указанному документу
		ЧекНаОплату = Документы.ЧекНаОплату.СоздатьДокумент();
		
		//Проверка на право ввода на основании заказ-наряда
		Если НЕ обПраво("ВводОплатыПоЗаказНаряду",ЧекНаОплату.Права,,ЧекНаОплату) Тогда
			Если НЕ обЗначениеНеЗаполнено(Документ) Тогда
				ОтказНаВводДокумента=Ложь;
				Если ТипЗнч(Документ)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
					ОтказНаВводДокумента=Истина;
				Иначе
					Попытка
						Если ТипЗнч(Документ.ДокументОснование)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
							ОтказНаВводДокумента=Истина;
						КонецЕсли;
					Исключение КонецПопытки; 
				КонецЕсли; 
				Если ОтказНаВводДокумента Тогда
					Сообщить("Ввод чека на оплату на основании заказ-наряда запрещено!",СтатусСообщения.Важное);
					Отказ=Истина; Возврат;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
		Если ТипЗнч(Документ)=Тип("ДокументСсылка.ЗаказНаряд") Тогда
			Если Документ.ВидРемонта.ТипРемонта=Перечисления.ТипыРемонта.Бесплатный Тогда
				Если Вопрос("В заказ-наряде используется бесплатный ремонт. Уверенны что хотите создать """+ЧекНаОплату.Метаданные().Синоним+"""?"
					, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Нет Тогда
					Отказ=Истина;
					Возврат;
				КонецЕсли;
			ИначеЕсли Документ.СуммаДокумента=0 Тогда
				Если Вопрос("Сумма документа равна нулю. Уверенны что хотите создать """+ЧекНаОплату.Метаданные().Синоним+"""?"
					, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Нет Тогда
					Отказ=Истина;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Документ)=Тип("ДокументСсылка.ПоступлениеДопРасходов")
			И НЕ (Документ.ХозОперация = Справочники.ХозОперации.ПоступлениеДопРасходов
			ИЛИ Документ.ХозОперация = Справочники.ХозОперации.ПоступлениеДопРасходовНаАвтомобили) Тогда
			Сообщить("Ввод чека на оплату возможен только для хоз. операций ""Поступление дополнительных расходов"" и ""Поступление дополнительных расходов на автомобили""!",СтатусСообщения.Внимание);
			Отказ=Истина;
			Возврат;
		КонецЕсли;
		
		Если дкДокументЕстьРеквизитШапки("ТипЦен", Документ.Метаданные().Имя)
			И НЕ Документ.ТипЦен.ЦенаВключаетНДС Тогда
			Сообщить("Тип цен документа не включает НДС!");
		КонецЕсли;
		
		ЧекНаОплату.Заполнить(Документ);
		
		РежимВыбора = "ВыборДокументаНаОплату";
		ЗначениеВыбора = ЧекНаОплату;
		
		ПолнаяСуммаПоСделке = обПересчет(Документ.СуммаДокумента, Документ.ВалютаДокумента, Документ.КурсДокумента, ЧекНаОплату.ВалютаДокумента, ЧекНаОплату.Дата, РежимОкругления.Окр15как20);
		
	КонецЕсли;
	
	// Произведем инициализацию формы фронта
	ФормаФронта = Обработки.ФронтКассира.ПолучитьФорму("ФормаМенеджера");
	НовыйЧекНаОплату=Неопределено;
	
	// Попросим пользователя подтвердить сумму оплаты
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.Выписка") Тогда
		СуммаДляОплаты = МАКС(ПолнаяСуммаПоСделке, 0);
	Иначе
		СуммаДляОплаты = МАКС(ЗначениеВыбора.СуммаДокумента, 0);
	КонецЕсли;
	СтавкаНДСЧекаНаОплату = ЗначениеВыбора.СтавкаНДС;
	СуммаНДСЧекаНаОплату  = МАКС(ЗначениеВыбора.СуммаНДС, 0);
	ПечататьТабличнуюЧастьТоваров = Истина;
	//Если ТипЗнч(Документ)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер") ИЛИ
	//	 ТипЗнч(Документ)=Тип("ДокументСсылка.ЧекНаОплату") Тогда
	ОплатыПоДокументу = Неопределено;
	
	МассивДокументовВозврат = Новый Массив;
	МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.ВозвратПоставщику"));
	МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.ВозвратПоставщикуАвтомобилей"));
	МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.ВозвратОтПокупателя"));
	МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.ВозвратОтПокупателяАвтомобилей"));
	
	// БИЗТЕХ КОВАЛЬ ++
	// Переопределяем используемую форму для определенных документов для поэтапного внедрения
	СписокПереопрДокументов = новый СписокЗначений;
	СписокПереопрДокументов.Добавить(Тип("ДокументСсылка.РеализацияАвтомобилей"));
	СписокПереопрДокументов.Добавить(Тип("ДокументСсылка.ЗаказНаряд"));
	СписокПереопрДокументов.Добавить(Тип("ДокументСсылка.ЗаказНаАвтомобиль"));
	// БИЗТЕХ КОВАЛЬ --

	Если ТипЗнч(Документ)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		ИЛИ ТипЗнч(Документ)=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		// Проверим сумму к оплате
		Если СуммаДляОплаты=0 Тогда
			Если ТипЗнч(Документ)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
				Предупреждение("Сумма к оплате по данной сделке нулевая. Выполнение операции прервано.");
			Иначе
				Предупреждение("Сумма к возврату по данной сделке нулевая. Выполнение операции прервано.");
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		//МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
		//МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		//
		//Если Документ.ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача
		//	 И МассивДокументовВозврат.Найти(ТипЗнч(Документ.ДокументОснование)) = Неопределено Тогда
		//	
		//	ОплатыПоДокументу = Оплаты.Выгрузить();
		//	ОплатыПоДокументу.Очистить();
		//	
		//	СуммаПоСделке = ?(НЕ ЗначениеЗаполнено(Документ.ДокументОснование),
		//		0,
		//		обПересчет(Документ.ДокументОснование.СуммаДокумента, Документ.ДокументОснование.ВалютаДокумента, Документ.ДокументОснование.КурсДокумента, Документ.ВалютаДокумента, Документ.Дата, РежимОкругления.Окр15как20));
		//	
		//	Если СуммаДляОплаты < СуммаПоСделке Тогда
		//		ОтветНаВопрос = Вопрос(НСтр("ru = 'Добавить зачет аванса в оплаты?'"), РежимДиалогаВопрос.ДаНет);
		//		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		//			СуммаАванса = СуммаПоСделке - СуммаДляОплаты;
		//			СтрокаОплат = ОплатыПоДокументу.Найти(Справочники.ТипыОплат.ЗачетАванса,"ТипОплаты");
		//			Если СтрокаОплат = Неопределено Тогда
		//				СтрокаОплат = ОплатыПоДокументу.Добавить();
		//				СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.ЗачетАванса;
		//				СтрокаОплат.Сумма = СуммаАванса;
		//			Иначе
		//				СтрокаОплат.Сумма = СтрокаОплат.Сумма + СуммаАванса;
		//			КонецЕсли;
		//		КонецЕсли;
		//		СуммаДляОплаты = СуммаПоСделке;
		//	КонецЕсли;
		//	
		//КонецЕсли;   
		
		МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
		МассивДокументовВозврат.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		
		Если Документ.ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача
			И МассивДокументовВозврат.Найти(ТипЗнч(Документ.ДокументОснование)) = Неопределено Тогда
			
			ОплатыПоДокументу = Оплаты.Выгрузить();
			ОплатыПоДокументу.Очистить();
			
			//СуммаПоСделке = ?(НЕ ЗначениеЗаполнено(Документ.ДокументОснование),
			//	0,
			//	обПересчет(Документ.ДокументОснование.СуммаДокумента, Документ.ДокументОснование.ВалютаДокумента, Документ.ДокументОснование.КурсДокумента, Документ.ВалютаДокумента, Документ.Дата, РежимОкругления.Окр15как20));
			
			//bizteh++
			Если ЗначениеЗаполнено(Документ.ДокументОснование) И ТипЗнч(Документ.ДокументОснование) = Тип("ДокументСсылка.РабочийЛистОтделаСтрахования") Тогда 
				СуммаПоСделке=0;
			Иначе	
				СуммаПоСделке = ?(НЕ ЗначениеЗаполнено(Документ.ДокументОснование),
				0,
				обПересчет(Документ.ДокументОснование.СуммаДокумента, Документ.ДокументОснование.ВалютаДокумента, Документ.ДокументОснование.КурсДокумента, Документ.ВалютаДокумента, Документ.Дата, РежимОкругления.Окр15как20));
			КонецЕсли;
			//bizteh--
			
			Если СуммаДляОплаты < СуммаПоСделке Тогда
				ОтветНаВопрос = Вопрос(НСтр("ru = 'Добавить зачет аванса в оплаты?'"), РежимДиалогаВопрос.ДаНет);
				Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
					СуммаАванса = СуммаПоСделке - СуммаДляОплаты;
					СтрокаОплат = ОплатыПоДокументу.Найти(Справочники.ТипыОплат.ЗачетАванса,"ТипОплаты");
					Если СтрокаОплат = Неопределено Тогда
						СтрокаОплат = ОплатыПоДокументу.Добавить();
						СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.ЗачетАванса;
						СтрокаОплат.Сумма = СуммаАванса;
					Иначе
						СтрокаОплат.Сумма = СтрокаОплат.Сумма + СуммаАванса;
					КонецЕсли;
				КонецЕсли;
				СуммаДляОплаты = СуммаПоСделке;
			КонецЕсли;
			
		КонецЕсли;		
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЧекНаОплату") Тогда
		// Проверим сумму к оплате
		Если СуммаДляОплаты=0 Тогда
			Предупреждение("Сумма к оплате по данной сделке нулевая. Выполнение операции прервано.");
			Возврат;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.Выписка") Тогда
		// Проверим сумму к оплате
		Если СуммаДляОплаты=0 Тогда
			Если ЭтоОплатаДокумента Тогда
				Предупреждение("Сумма к оплате по данной сделке нулевая. Выполнение операции прервано.");
			Иначе
				Предупреждение("Сумма к возврату по данной сделке нулевая. Выполнение операции прервано.");
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		Если Документ.ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
			
			ОплатыПоДокументу = Оплаты.Выгрузить();
			ОплатыПоДокументу.Очистить();
			
			ОбщаяСуммаПоСделке = 0;
			
			Для Каждого ТекСтрока Из Документ.Состав Цикл
				
				Если ЗначениеЗаполнено(ТекСтрока.Сделка)
					И МассивДокументовВозврат.Найти(ТипЗнч(ТекСтрока.Сделка)) = Неопределено Тогда
					СуммаПоСделке = обПересчет(ТекСтрока.Сделка.СуммаДокумента, ТекСтрока.Сделка.ВалютаДокумента, ТекСтрока.Сделка.КурсДокумента, Документ.ВалютаДокумента, Документ.Дата, РежимОкругления.Окр15как20);
				Иначе
					СуммаПоСделке = ?(ЭтоОплатаДокумента, ТекСтрока.СуммаПриход, ТекСтрока.СуммаРасход);
				КонецЕсли;
				
				ОбщаяСуммаПоСделке = ОбщаяСуммаПоСделке + СуммаПоСделке;
				
			КонецЦикла;
			
			Если СуммаДляОплаты < ОбщаяСуммаПоСделке Тогда
				ОтветНаВопрос = Вопрос(НСтр("ru = 'Добавить зачет аванса в оплаты?'"), РежимДиалогаВопрос.ДаНет);
				Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
					СуммаАванса = ОбщаяСуммаПоСделке - СуммаДляОплаты;
					СтрокаОплат = ОплатыПоДокументу.Найти(Справочники.ТипыОплат.ЗачетАванса,"ТипОплаты");
					Если СтрокаОплат = Неопределено Тогда
						СтрокаОплат = ОплатыПоДокументу.Добавить();
						СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.ЗачетАванса;
						СтрокаОплат.Сумма = СуммаАванса;
					Иначе
						СтрокаОплат.Сумма = СтрокаОплат.Сумма + СуммаАванса;
					КонецЕсли;
				КонецЕсли;
				СуммаДляОплаты = ОбщаяСуммаПоСделке;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		// Создаем и заполняем форму корректировки оплаты
		// БИЗТЕХ КОВАЛЬ 14.07.2025 ++ АВТОПРОБИТИЕ
		//ФормаКорректировки = ПолучитьФорму("КорректировкаСуммыОплаты", ФормаФронта);
		Если НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено Тогда
			ФормаКорректировки = ПолучитьФорму("Обработка.ФронтКассира.Форма.КорректировкаСуммыОплатыУправляемая");
			ФормаКорректировки.ТекущийФР           = ЧекНаОплату.ФР;
		Иначе
			ФормаКорректировки = ПолучитьФорму("КорректировкаСуммыОплаты", ФормаФронта);
		КонецЕсли;
		// БИЗТЕХ КОВАЛЬ 14.07.2025 --
		ФормаКорректировки.СуммаПоСделке       = ПолнаяСуммаПоСделке;
		ФормаКорректировки.ОстатокПоСделке     = СуммаДляОплаты;
		ФормаКорректировки.СуммаДляОплаты      = ?(СуммаДляОплаты=0, Документ.СуммаДокумента, СуммаДляОплаты);
		ФормаКорректировки.ПолнаяСуммаПоСделке = ПолнаяСуммаПоСделке;
		ФормаКорректировки.СтавкаНДС           = СтавкаНДСЧекаНаОплату;
		ФормаКорректировки.СуммаНДС            = СуммаНДСЧекаНаОплату;
		Если СуммаДляОплаты = 0 Тогда
			// если предполагается пробитие полной суммы, то и подставим суммуНДС документа-основания.
			ДокументМетаданые = Документ.Метаданные();
			СуммаНДСДокумента = 0;
			Для Каждого ТабличнаяЧасть Из Документ.Метаданные().ТабличныеЧасти Цикл
				Если ТабличнаяЧасть.Реквизиты.Найти("СуммаНДС") <> Неопределено Тогда
					СуммаНДСДокумента = СуммаНДСДокумента + Документ[ТабличнаяЧасть.Имя].Итог("СуммаНДС");
				КонецЕсли;
			КонецЦикла;
			ФормаКорректировки.СуммаНДС = СуммаНДСДокумента;
		КонецЕсли;
		ФормаКорректировки.Сделка = Документ;
		
		// Открываем форму и обрабатываем результат
		Если обЗначениеНеЗаполнено(ФормаКорректировки.ОткрытьМодально()) Тогда
			Возврат;
		Иначе
			
			НовыйЧекНаОплату=Документы.ЧекНаОплату.СоздатьДокумент();
			НовыйЧекНаОплату.Заполнить(Документ);
			ДобавитьАвансВТаблицуОплат = Ложь;
			
			Если ФормаКорректировки.ТипСпособаРасчета = Перечисления.ТипыСпособаРасчетаККТ.Передача
				И (ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаряд")
				ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваров")) Тогда
				ТекстыОшибок = "";
				дкПроверитьКорректностьМаркировки(НовыйЧекНаОплату,,, ТекстыОшибок);
				Если НЕ ПустаяСтрока(ТекстыОшибок) Тогда
					Предупреждение("Проверьте корректность ввода кодов маркировки в документе оплаты. Операция отменена.");
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если НовыйЧекНаОплату.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат
				ИЛИ НовыйЧекНаОплату.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
				НовыйЧекНаОплату.Оплаты.Очистить();
			Иначе
				Если ФормаКорректировки.ОстатокПоСделке < ПолнаяСуммаПоСделке И ФормаКорректировки.ТипСпособаРасчета = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
					
					// БИЗТЕХ КОВАЛЬ 04.07.2025 ++ АВТОПРОБИТИЕ
					//Всегда переопределяем на Да
					Если НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено Тогда
						ОтветНаВопрос = КодВозвратаДиалога.Да;
					Иначе
						ОтветНаВопрос = Вопрос(НСтр("ru = 'Добавить зачет аванса в оплаты?'"), РежимДиалогаВопрос.ДаНет);
					КонецЕсли;
					//ОтветНаВопрос = Вопрос(НСтр("ru = 'Добавить зачет аванса в оплаты?'"), РежимДиалогаВопрос.ДаНет);
					Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
						СуммаАванса = ПолнаяСуммаПоСделке - ФормаКорректировки.ОстатокПоСделке;
						СтрокаОплат = НовыйЧекНаОплату.Оплаты.Найти(Справочники.ТипыОплат.ЗачетАванса,"ТипОплаты");
						Если СтрокаОплат = Неопределено Тогда
							СтрокаОплат = НовыйЧекНаОплату.Оплаты.Добавить();
							СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.ЗачетАванса;
							СтрокаОплат.Сумма = СуммаАванса;
						Иначе
							СтрокаОплат.Сумма = СтрокаОплат.Сумма + СуммаАванса;
						КонецЕсли;
					КонецЕсли;
					
				//БИЗТЕХ КОВАЛЬ 12.05.2025 ++
				//Всегда по умолчанию предоплата и вносить введенную сумму в таблицу оплат
				ИначеЕсли ФормаКорректировки.ТипСпособаРасчета = Перечисления.ТипыСпособаРасчетаККТ.Предоплата тогда
					Если НовыйЧекНаОплату.Оплаты.Количество() Тогда
						НовыйЧекНаОплату.Оплаты[0].Сумма = ФормаКорректировки.СуммаДляОплаты; 
					КонецЕсли;
				//БИЗТЕХ КОВАЛЬ 12.05.2025 --
				КонецЕсли;
				
				//БИЗТЕХ КОВАЛЬ 08.09.2025 ++ Сдвинул условие
				//Переопределяем типы оплат
				Если НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено Тогда
					
					НовыйЧекНаОплату.Оплаты.Очистить();
					
					//Наличные
					Если ФормаКорректировки.ВыборНаличныеБезналичные = 0 И ЗначениеЗаполнено(ФормаКорректировки.ТипОплатыНаличныеБезналичные) Тогда
						СтрокаОплат = НовыйЧекНаОплату.Оплаты.Найти(Справочники.ТипыОплат.Наличные,"ТипОплаты");
						Если СтрокаОплат = Неопределено Тогда
							СтрокаОплат = НовыйЧекНаОплату.Оплаты.Добавить();
							СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.Наличные;
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыНаличныеБезналичные;
						Иначе
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыНаличныеБезналичные;
						КонецЕсли;
					КонецЕсли;
					
					//Безналичные
					Если ФормаКорректировки.ВыборНаличныеБезналичные = 1 И ЗначениеЗаполнено(ФормаКорректировки.ТипОплатыНаличныеБезналичные) Тогда
						СтрокаОплат = НовыйЧекНаОплату.Оплаты.Найти(Справочники.ТипыОплат.Безналичные,"ТипОплаты");
						Если СтрокаОплат = Неопределено Тогда
							СтрокаОплат = НовыйЧекНаОплату.Оплаты.Добавить();
							СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.Безналичные;
							СтрокаОплат.ТипПлатежнойКарты = Справочники.ТипыПлатежныхКарт.Мир;
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыНаличныеБезналичные;
						Иначе
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыНаличныеБезналичные;
						КонецЕсли;
					КонецЕсли;
					
					//Предоплата
					Если ЗначениеЗаполнено(ФормаКорректировки.ТипОплатыПредоплата) Тогда
						СтрокаОплат = НовыйЧекНаОплату.Оплаты.Найти(Справочники.ТипыОплат.ЗачетАванса,"ТипОплаты");
						Если СтрокаОплат = Неопределено Тогда
							СтрокаОплат = НовыйЧекНаОплату.Оплаты.Добавить();
							СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.ЗачетАванса;
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыПредоплата;
						Иначе
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыПредоплата;
						КонецЕсли;
					КонецЕсли;
					
					//Кредит 
					Если ЗначениеЗаполнено(ФормаКорректировки.ТипОплатыКредит) Тогда
						СтрокаОплат = НовыйЧекНаОплату.Оплаты.Найти(Справочники.ТипыОплат.Кредит,"ТипОплаты");
						Если СтрокаОплат = Неопределено Тогда
							СтрокаОплат = НовыйЧекНаОплату.Оплаты.Добавить();
							СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.ПостоплатаКредит;
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыКредит;
						Иначе
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыКредит;
						КонецЕсли;
					КонецЕсли;
					
					//ВстречноеПредложение
					Если ЗначениеЗаполнено(ФормаКорректировки.ТипОплатыВстречноеПредоставление) Тогда
						СтрокаОплат = НовыйЧекНаОплату.Оплаты.Найти(Справочники.ТипыОплат.Кредит,"ТипОплаты");
						Если СтрокаОплат = Неопределено Тогда
							СтрокаОплат = НовыйЧекНаОплату.Оплаты.Добавить();
							СтрокаОплат.ТипОплаты = Справочники.ТипыОплат.ЗаСчетЗаведения; //))
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыВстречноеПредоставление;
						Иначе
							СтрокаОплат.Сумма = ФормаКорректировки.ТипОплатыВстречноеПредоставление;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				// БИЗТЕХ КОВАЛЬ 04.07.2025 --
				
				
			КонецЕсли; 
			НовыйЧекНаОплату.СуммаДокумента = ФормаКорректировки.СуммаДляОплаты;
			Документы.ЧекНаОплату.ПроизвестиРаспределениеСуммыОплаты(НовыйЧекНаОплату.СуммаДокумента, НовыйЧекНаОплату.Товары);
			НовыйЧекНаОплату.СтавкаНДС = ФормаКорректировки.СтавкаНДС;
			НовыйЧекНаОплату.СуммаНДС = ФормаКорректировки.СуммаНДС;
			НовыйЧекНаОплату.ТипСпособаРасчетаККТ = ФормаКорректировки.ТипСпособаРасчета;
			ПечататьТабличнуюЧастьТоваров = ФормаКорректировки.ПечататьТабличнуюЧастьТоваров;
			ЗаполнитьЗначенияСвойств(ЭтотОбъект,НовыйЧекНаОплату);
			СуммаДляОплаты			= ФормаКорректировки.СуммаДляОплаты;
			СтавкаНДСЧекаНаОплату	= ФормаКорректировки.СтавкаНДС;
			СуммаНДСЧекаНаОплату	= ФормаКорректировки.СуммаНДС;
			// БИЗТЕХ КОВАЛЬ 28.05.2025 ++
			Если О2_QR_ОплатаЧерезQRКод = Истина Тогда
				О2_QR_ОбщийМодуль.ОплатаЧерезQR(НовыйЧекНаОплату,Документ);
				Возврат;
			ИначеЕсли НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено И ФормаКорректировки.О2_QR_ОплатаЧерезQRКод = Истина Тогда
				О2_QR_ОбщийМодуль.ОплатаЧерезQR(НовыйЧекНаОплату,Документ);
				Возврат;
			КонецЕсли;
			// БИЗТЕХ КОВАЛЬ 28.05.2025 --
		КонецЕсли;
	КонецЕсли;
	
	// Производим открытие формы фронта
	Если ФормаФронта.Открыта() Тогда
		ФормаФронта.Активизировать();
		
		// Необходимо отменить текущий чек
		Если ФормаФронта.УстановленРежим("Блокировка") Тогда
			ОткрытьДиалог("Система заблокирована! Для продолжения работы необходимо разблокировать систему.", СтатусСообщения.Важное);
			Возврат;
		ИначеЕсли НЕ ФормаФронта.ОтменитьЧек(ФормаФронта) Тогда
			Возврат;
		КонецЕсли;
	Иначе
		ФормаФронта.Открыть();
	КонецЕсли;
	Если НовыйЧекНаОплату<>Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ФормаФронта.ОбработкаОбъект,НовыйЧекНаОплату);
	КонецЕсли; 
	
	// Проверим произошло ли открытие формы
	Если НЕ ФормаФронта.Открыта() Тогда
		Возврат;
	КонецЕсли;
	
	// Переводим фронт в режим "Оплаты"
	ФормаФронта.ИзменитьРежим("ЧекНаОплату");
	ФормаФронта.УправлениеИнтерфейсом();
	
	// Производим установку переданного документа
	ФормаФронта.ПерейтиВРежим(РежимВыбора);

	// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ ++ 07.07.2025
	Если НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено Тогда
		// Подмена оплачиваемого документа если выбрали ПКО (наличные)
		Если ЗначениеЗаполнено(ФормаКорректировки.ПробитиеПКО) И ТипЗнч(ФормаКорректировки.ПробитиеПКО) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
			НовыйЧекНаОплату = Неопределено;
			ЗначениеВыбора = ФормаКорректировки.ПробитиеПКО; 
			ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Подмена документа оплаты на " + ФормаКорректировки.ПробитиеПКО);
		КонецЕсли;
	КонецЕсли;
	// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ -- 07.07.2025

	Если НовыйЧекНаОплату=Неопределено Тогда
		ФормаФронта.ОбработкаВыбораФронта(ЗначениеВыбора, Неопределено, ФормаФронта);
		Если ЗначениеВыбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
	Иначе
		ФормаФронта.ОбработкаВыбораФронта(НовыйЧекНаОплату, Неопределено, ФормаФронта);
	КонецЕсли;
	// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ ++ 07.07.2025
	Если НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено Тогда
		//ОЧИСТКА ОТДЕЛА
		СкладКомпании = Неопределено;
		ФормаФронта.СкладКомпании = Неопределено;
	КонецЕсли;
	// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ -- 07.07.2025
	ФормаФронта.ВыйтиИзРежима(РежимВыбора);
    
	// Заполним оплаты
	Если НЕ ОплатыПоДокументу = Неопределено И ОплатыПоДокументу.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из ОплатыПоДокументу Цикл
			ЗаполнитьЗначенияСвойств(ФормаФронта.Оплаты.Добавить(), ТекущаяСтрока);
			ЗаполнитьЗначенияСвойств(Оплаты.Добавить(), ТекущаяСтрока);
		КонецЦикла;
		ОплатыПриОкончанииРедактирования(ФормаФронта);
		ФормаФронта.ПолученоБезНал = ПолученоБезНал;
		ФормаФронта.ПолученоНал = ПолученоНал;
	КонецЕсли;
	
	// Произведем установку нового режима
	Если ФормаФронта.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат
		ИЛИ ФормаФронта.ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
		ФормаФронта.УстановитьРежим("ЧекНаОплатуВозврат");
	ИначеЕсли ФормаФронта.ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер Тогда
		ФормаФронта.УстановитьРежим("ПриходныйКассовыйОрдер");
	ИначеЕсли ФормаФронта.ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер Тогда
		ФормаФронта.УстановитьРежим("РасходныйКассовыйОрдер");
	ИначеЕсли ФормаФронта.ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
		ФормаФронта.УстановитьРежим("БанковскаяВыписка");
	Иначе
		ФормаФронта.УстановитьРежим("ЧекНаОплату");	
	КонецЕсли;	
	
	// Произведем корректировку суммы оплаты
	ФормаФронта.СуммаДокумента = СуммаДляОплаты;
	ФормаФронта.ОбработкаРеквизита("СуммаДокумента",, ФормаФронта);
	ФормаФронта.СуммаНДСЧекаНаОплату = СуммаНДСЧекаНаОплату;
	ФормаФронта.СтавкаНДСЧекаНаОплату = СтавкаНДСЧекаНаОплату;
	Если НЕ ПечататьТабличнуюЧастьТоваров Тогда
		ФормаФронта.ПечатьТабличнойЧастиТовары = ПечататьТабличнуюЧастьТоваров;
	КонецЕсли;
	
	// БИЗТЕХ КОВАЛЬ 01/04/2025 ++
	// Включаем оборудование при каждом чеке
	фкУстройствоПечатиНефискальныхДокументов = обПраво("УстройствоПечатиНефискальныхДокументов",Права);
	ВключитьОборудование();
	// БИЗТЕХ КОВАЛЬ --
	
	// Обновим индикаторы формы
	ФормаФронта.УправлениеИнтерфейсом();
	
	// БИЗТЕХ КОВАЛЬ ЭЛЕКТРОННЫЕ ЧЕКИ ++ 15.10.2025
	// Произведем установку данных клиента если установлена галочка "Отправить чек по почте"
	Если НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено Тогда
				
		Если ФормаКорректировки.ОтправитьЧекПоПочте = Истина И ЗначениеЗаполнено(ФормаКорректировки.ПочтаКонтрагентаДляОтправки) Тогда
			ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Указана галочка для отправки чека по почте. Чек будет отправлен на почту: " + ФормаКорректировки.ПочтаКонтрагентаДляОтправки);
			
			ДанныеКлиента = ФормаКорректировки.ПочтаКонтрагентаДляОтправки;
			ФормаФронта.ДанныеКлиента = ФормаКорректировки.ПочтаКонтрагентаДляОтправки;
		КонецЕсли;

	КонецЕсли;
	// БИЗТЕХ КОВАЛЬ ЭЛЕКТРОННЫЕ ЧЕКИ -- 15.10.2025
	
	// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ ++ 07.07.2025
	// Произведем автопробитие и закрытие фронта менеджера
	АвтопробитиеУспешно=Неопределено;
	Если НЕ СписокПереопрДокументов.НайтиПоЗначению(ТипЗнч(Документ)) = Неопределено Тогда
		
		//Переопределяем ФР
		Если 1=2 Тогда //НовыйЧекНаОплату.ФР <>ФормаКорректировки.ТекущийФР
			
			// перейдем в режим нужной нам форме
			ПерейтиВРежим("ВЫБОРКАССЫ");
			
			ОбработкаВыбораФронта(ФормаКорректировки.ТекущийФР, ФормаКорректировки, ФормаФронта, Неопределено);
			ФормаФронта.ФР = ФР;
			ФормаФронта.GUID_ФР = GUID_ФР;
			// Выходим из режима
			ВыйтиИзРежима("ВЫБОРКАССЫ");
			
			Если ЧекОткрыт() Тогда
				Если ХозОперация=Справочники.ХозОперации.Чек Тогда
					УстановитьРежим("Чек");	
				ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаВозврат Тогда	
					УстановитьРежим("ЧекНаВозврат");
				ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат
					ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда	
					УстановитьРежим("ЧекНаОплатуВозврат");
				ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаОплату
					ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки Тогда	
					УстановитьРежим("ЧекНаОплату");
				ИначеЕсли ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер Тогда	
					УстановитьРежим("ПриходныйКассовыйОрдер");
				ИначеЕсли ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер Тогда	
					УстановитьРежим("РасходныйКассовыйОрдер");
				ИначеЕсли ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя Тогда
					УстановитьРежим("ВозвратТоваровОтПокупателя");
				ИначеЕсли ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
					УстановитьРежим("БанковскаяВыписка");
				КонецЕсли;	
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаВозврат И фкРучнойВводВозврата Тогда
				УстановитьРежим("ЧекНаВозврат");	
			КонецЕсли;
			ФормаФронта.УправлениеИнтерфейсом();
		КонецЕсли;
		
		Если ФормаКорректировки.АвтопробитиеЧека = Истина Тогда
			ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Указано АвтопробитиеЧека. Пробиваем чек.");
			// Перед автопробитием проведем некоторые проверки и запишем в лог если что
			Если ПустаяСтрока(GUID_ФР) Тогда
				ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Нет ФР для Автопробития Чека. Операция отменена.",,1);
				Возврат;
			КонецЕсли;
			Если НЕ ПроверитьОплаты() Тогда 
				ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Ошибка корректности оплат. Операция отменена.",,1);
				Возврат; 
			КонецЕсли;
			//БИЗТЕХ МАМОНОВ 19.12.2025 ++
			//Возводим флаг автопробитие
			ЭтотОбъект.Автопробитие = Истина;
			//БИЗТЕХ МАМОНОВ 19.12.2025 --
			ФормаФронта.ОшибкаПробития = Ложь;
			ФормаФронта.кнПробитьНажатие(ФормаФронта.ЭлементыФормы.кнПробить);
		КонецЕсли;
		// Проверим что при пробитии нет ошибок
		Если ФормаФронта.ОшибкаПробития <> Истина Тогда
			АвтопробитиеУспешно = Истина;
			ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Документ оплаты пробит успешно!");
			Сообщить("Документ оплаты пробит успешно!");
		Иначе
			АвтопробитиеУспешно = Ложь;
		КонецЕсли;
		// Если ошибок нет то закроем фронт
		Если ФормаКорректировки.АвтопробитиеЧека = Истина И ФормаКорректировки.ЗакрытьФронтМенеджера = Истина И АвтопробитиеУспешно = Истина Тогда
			ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Закрываем фронт менеджера");
			ФормаФронта.фкПоказыватьЗапросНаЗавершениеРаботы = Ложь;
			ФормаФронта.Закрыть();
		КонецЕсли;
		// Если Оплата проходила по заказ-наряду то закроем его если функционал активен и чек пробит корректно
		Если ФормаКорректировки.ЗакрытьЗаказНарядПослеОплаты = Истина И 
			ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаряд") И 
			Документ.состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен И 
			АвтопробитиеУспешно = Истина Тогда
			
			ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Закрываем заказ-наряд " + Документ);
			ОбДокумент = Документ.ПолучитьОбъект();
			ОбДокумент.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт;
			Попытка
				ОбДокумент.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);
				ТекстОшибки = "Переведен статус заказ-наряда <"+Документ.Ссылка+"> В состояние <Закрыт>";
				ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,ТекстОшибки);
				Сообщить(ТекстОшибки);
			Исключение
				ТекстОшибки1с = ОписаниеОшибки();
				ТекстОшибки = "Ошибка изменения статуса заказ-наряда <"+Документ.Ссылка+"> В состояние <Закрыт> - "+ ТекстОшибки1с;
				ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,ТекстОшибки,,1);
				Сообщить(ТекстОшибки);
			КонецПопытки;
		КонецЕсли;
		// Если по реализации был TRADEIN то создадим взаимозачеты
		
		// Функционал был перенесен в ОбработкуПроведения реализации автомобиля 25.07.2025 ++ КОВАЛЬ
		//Если ФормаКорректировки.СоздатьВзаимозачетыПоТрейдИн = Истина И 
		//	ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияАвтомобилей") И 
		//	АвтопробитиеУспешно = Истина Тогда
		//	ЗаписатьВФайлЛога("АВТОПРОБИТИЕ",,"Попытка создать взаимозачеты по Trade IN на сумму: " + ФормаКорректировки.СуммаВзаимозачетаТрейдИн);
		//	//ФормаКорректировки.СоздатьВзаимозачетыTradeIn(Документ,ФормаКорректировки.СуммаВзаимозачетаТрейдИн);
		//Иначе
		//	ЗаписьЖурналаРегистрации("Создание взаимозачетов Trade In",УровеньЖурналаРегистрации.Информация,"Не соблюдены условия для запуска функционала!");
		//КонецЕсли;
	КонецЕсли;
	// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ -- 07.07.2025
	
	
КонецПроцедуры

// Процедура оплатить чек коррекции
//
//Параметры:
//  Документ	 - 
//
Процедура ОплатитьЧекКоррекции(Документ, ПервоеПробитие=ИСТИНА) Экспорт
	
	Если Документ.ПробитЧек Тогда
		Сообщить("Чек уже пробит на фискальном устройстве.", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Ссылка = Документ;
	
	ФормаЧекаКоррекции = ПолучитьФорму("ФормаЧекаКоррекции");
	ФормаЧекаКоррекции.ЧекКоррекции = Документ;
	ФормаЧекаКоррекции.ПервоеПробитие = ПервоеПробитие;
	
	ЗаполнитьЗначенияСвойств(ФормаЧекаКоррекции.ОбработкаОбъект, Документ);
	СтавкаНДСЧекаНаОплату= Документ.СтавкаНДС;
	СуммаНДСЧекаНаОплату =Документ.СуммаНДС;
	
	УстановитьРежим("ЧекКоррекции");
	ПечатьТабличнойЧастиТовары = Истина;
	Товары.Очистить();
	Для Каждого Строка Из Документ.Товары Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	КодыМаркировки.Очистить();
	Для Каждого Строка Из Документ.КодыМаркировки Цикл
		НоваяСтрока = КодыМаркировки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	ОплатаККТ.Очистить();
	Оплаты.Очистить();
	Для Каждого Строка Из Документ.Оплаты Цикл
		НоваяСтрока = ОплатаККТ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ТипОплаты = Строка.ТипОплаты.ТипОплатыККТ;
		
		НоваяСтрока = Оплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	ТипСпособаРасчетаККТ = Документ.ПризнакСпособаРасчета;
	ФормаЧекаКоррекции.ОткрытьМодально();
	
КонецПроцедуры

// Предназначена для привлечения внимания кассира к предупреждению
// Играет сигнал сирены на PC-спикере. Введена буферизация команд
// до появления на экране сообщения (см. функцию компоненты <Диалог>)
Процедура Сигнализация(ТекРежим = 0)
	Если ТекРежим = 0 Тогда
		Рарус_Компонента.Гудок(800,500,0);
		Рарус_Компонента.Гудок(1000,500,0);
		Рарус_Компонента.Гудок(800,500,0);
		Рарус_Компонента.Гудок(1000,500,0);
		Рарус_Компонента.Гудок(800,500,0);
		Рарус_Компонента.Гудок(1000,500,0);
	Иначе
		Сигнал();// Более простой сигнал
	КонецЕсли;
КонецПроцедуры // Сигнализация()

// Возвращает номер пришедшего события и отсекает его из значения Событие
Функция ПолучитьНомерСобытия(Событие)
// формат значения Событие - "<ИмяСобытия>*<НомерСобытия>"
// например: "WM_SIZE*0"
	
	Результат = 0;
	
	Поз = Найти(Событие,"*");
	Если Поз > 0 Тогда
		Номер = Прав(Событие,СтрДлина(Событие)-Поз);
		Событие = Лев(Событие,Поз-1);
		Попытка
			Результат = Число(Номер);
		Исключение
			Результат = 0;
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// Проверяет обрабатывалось ли уже это внешнее событие
Функция ЭтоНовоеСобытие(Событие) Экспорт
	
	Результат = Ложь;
	
	// получим номер пришедшего события
	НСобытия = ПолучитьНомерСобытия(Событие);
	
	// проверим не приходило ли уже такое событие
	Если НСобытия > НомерВнешнегоСобытия Тогда
		// запомним его номер
		НомерВнешнегоСобытия = НСобытия;
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет не открыт ли чек
Функция ЧекОткрыт(Объект=Неопределено) Экспорт
// Условия, что чек открыт:
// 1 - не пустой товарный состав
// 2 - произведены какие либо оплаты
// 3 - выбрана ДК покупателя
// 4 - назначена скидка
// 5 - изменен склад продажи
	
	Объект = ?(Объект=Неопределено, ЭтотОбъект, Объект);
	
	Результат = Ложь;
	
	Если (Объект.Товары.Количество()>0)
			ИЛИ ЕстьОплаты(Объект)
			ИЛИ (Объект.СуммаДокумента>0)
			ИЛИ (НЕ Объект.Карточка.Пустая())
			ИЛИ (НЕ Объект.СкидкаНаценка.Пустая())
			ИЛИ (НЕ Объект.СкладКомпании=ПараметрыСеанса.Компьютер.Отдел) Тогда
		Результат = Истина
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет надо ли просить пользователя завешать работу с открытым чеком
Функция НадоЗавершитьРаботуСЧеком(флТребоватьЗакрытьКопию=ИСТИНА) Экспорт
// условия для завершения работы с чеком(то есть при этих условиях мы сами не сможем корректно завершить работу):
// 1 - не пустой товарный состав
// 2 - оплаты по безналу
// 3 - открыт чек копия - по флагу
	Результат = Ложь;
	
	Если ( УстановленРежим("Копия") И флТребоватьЗакрытьКопию) ИЛИ
		 (Товары.Количество()>0) ИЛИ (ПолученоБезнал>0) Тогда
			 
		ОткрытьДиалог("СНАЧАЛА ЗАВЕРШИТЕ РАБОТУ С ТЕКУЩИМ ЧЕКОМ!", СтатусСообщения.Важное);
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает текущую строку табличной части Товары у переданной формы
Функция ПолучитьТекущуюСтрокуФронта(ЭтаФорма) Экспорт
// если ни одна из строк по какой то причине не активна, то возвращается последняя строчка
	
	ТекущаяСтрока = ЭтаФорма.ЭлементыФормы.Товары.ТекущаяСтрока;
	
	Если (ТекущаяСтрока = Неопределено) И (Товары.Количество()<> 0) Тогда
		// по умолчанию текущей строкой будет последняя
		ТекущаяСтрока = Товары.Получить(Товары.Количество()-1);		
	КонецЕсли;
	
	Возврат ТекущаяСтрока;
КонецФункции

// Открывает специальный диалог с произвольным текстовым сообщением
Процедура ОткрытьДиалог(Текст, Статус, Время=Неопределено) Экспорт
	//---------------------------------------------------------+
	//        Статус               |Время|Режим|Комментарий ...
	//СтатусСообщения.БезСтатуса	- П		2*	
	//СтатусСообщения.Обычное		- П		2*	Сообщения, предупреждения
	//СтатусСообщения.Внимание		- П		2	Диалог предупреждающий о невнимательной работе
	//пользователя (что-то забыли заполнить или в данный момент такое действие не возможно),
	//отмена начала действия пользователя
	//СтатусСообщения.Информация	- Б		2*	Информационные сообщения, которые пользователь обязательно
	//должен увидеть, в.ч. "отчеты"
	//СтатусСообщения.Важное		- П		1	Невозможность выполнить действие по причине не корректной
	//работы пользователя, ведущей к ошибке, а также запреты на действие
	//СтатусСообщения.ОченьВажное	- Б		1	Невозможность выполнить действие по причине ошибки оборудования,
	//ошибок при записи и т.п.
	//
	// Необходимо расширить виды диалога на "обычный" и "информация"
	
	// В режиме тестирования открытие диалога производить не нужно
	Если РежимТестирования Тогда
		Возврат;
	КонецЕсли;
	
	// Определим основные параметры открываемого диалога
	РежимДиалога = ?(Статус=СтатусСообщения.Важное ИЛИ Статус=СтатусСообщения.ОченьВажное, 1, 2);
	ВремяДиалога = ?(Статус=СтатусСообщения.Информация ИЛИ Статус=СтатусСообщения.ОченьВажное, 0, фкТаймаутДиалогов);
	ВремяДиалога = ?(ТипЗнч(Время)=Тип("Число"), Время, ВремяДиалога);
	ВремяДиалога = ?(ВремяДиалога=Неопределено, 0, ВремяДиалога);
	
	Если ВремяДиалога <= 0 Тогда
		ВремяДиалога = 1000000;
	КонецЕсли;
	
	// Если сообщение классифицируется как "ошибка" или "внимание", то необходимо дополнительно выдать звуковое оповещение
	Если РежимДиалога=1 Тогда
		Сигнализация(0);
	ИначеЕсли Статус=СтатусСообщения.Внимание Тогда
		Сигнализация(1);
	КонецЕсли;
	
	// Открываем окно диалога
	Рарус_Компонента.Диалог(Текст, РежимДиалога, ВремяДиалога);
	
КонецПроцедуры	// ОткрытьДиалог()

// Открывает специальный диалог вопроса с произвольным текстовым сообщением
Функция ОткрытьДиалогВопроса(Текст, ОтветПоУмолчанию=Неопределено) Экспорт
	// возвращает результат вопроса
	
	// В режиме тестирования открытие диалога производить не нужно
	Если РежимТестирования Тогда
		ОтветПоУмолчанию = ?(ОтветПоУмолчанию = Неопределено, Истина, ОтветПоУмолчанию);
		Возврат ?(ОтветДиалогаПоумолчаниюВРежимеТестирования=Неопределено, ОтветПоУмолчанию, ОтветДиалогаПоумолчаниюВРежимеТестирования);
	КонецЕсли;
	
	// Определим основные параметры открываемого диалога
	Если (ТипЗнч(ОтветПоУмолчанию)=Тип("Булево")) И ОтветПоУмолчанию Тогда
		ВремяДиалога = фкТаймаутДиалогов;
	Иначе
		ВремяДиалога = 0;	
	КонецЕсли;
	//ВремяДиалога = ?(ТипЗнч(ОтветПоУмолчанию)=Тип("Булево"), фкТаймаутДиалогов, 0);
	
	Если ВремяДиалога <= 0 Тогда
		ВремяДиалога = 1000000;
	КонецЕсли;
	
	// Открываем окно диалога вопроса
	Результат = Рарус_Компонента.Диалог(Текст, 3, ВремяДиалога);
	
	// Обработаем результат и вернем значение
	Результат = ?(Результат=-1, ОтветПоУмолчанию, (Результат=1));
	
	Возврат Результат;
	
КонецФункции	// ОткрытьДиалогВопроса()

// Открыть расширенный диалог вопроса
Функция ОткрытьДиалогВопросаРасш(Текст,ВариантыОтвета) Экспорт
	ВремяДиалога = 0;	
	
	Если ВремяДиалога <= 0 Тогда ВремяДиалога = 1000000; КонецЕсли;

	// Открываем окно диалога вопроса
	Результат = Рарус_Компонента.Вопрос(Текст, ВариантыОтвета, ВремяДиалога);
	
	Возврат Результат;
	
КонецФункции

// обработка результата
Функция ОбработкаРезультата(Результат) Экспорт
	
	Если Результат = РезультатыФронта.Ок Тогда
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда 
			
			ОткрытьДиалог(ТекстОшибки, СтатусСообщения.Обычное); 
			Возврат Истина; 
		КонецЕсли;

	ИначеЕсли Результат = РезультатыФронта.Предупреждение Тогда
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда 
			ОткрытьДиалог(ТекстОшибки, СтатусСообщения.Внимание);	
			Возврат Ложь; 
		КонецЕсли;
		
	ИначеЕсли Результат = РезультатыФронта.Ошибка Тогда
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда 
			ОткрытьДиалог(ТекстОшибки, СтатусСообщения.Важное);
			Возврат Ложь; 
		КонецЕсли;
		
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	ТекстОшибки ="";
	Возврат Результат;
	
КонецФункции

// Функция предназначена для получения строкового представления переданного 
//в параметрах числа для его последующего отображения в полях вывода сумм
//
// Параметры:
//  Сумма - число - число, представление которого хотим получить.
//
// Возвращаемое значение:
//  Возвращает строковое представление переданного числа.
//
Функция ФРМсумма(Сумма) Экспорт
   Возврат Формат(Сумма,"ЧЦ=15; ЧДЦ=2; ЧРГ=' '; ЧН=0.00");	
КонецФункции // ФРМсумма()

// Функция предназначена для получения строкового представления переданного 
//в параметрах числа для его последующего отображения в полях вывода сумм
//
// Параметры:
//  Сумма - число - число, представление которого хотим получить.
//
// Возвращаемое значение:
//  Возвращает строковое представление переданного числа.
//
Функция ФРМколичество(Количество,Точность) Экспорт
	ФорматнаяСтрокаКоличества="ЧЦ=15; ЧДЦ="+СокрЛП(Точность)+"; ЧН=0";
	Если Точность>0 Тогда
		ФорматнаяСтрокаКоличества=ФорматнаяСтрокаКоличества+".";
		Для Сч=1 По Точность Цикл
			ФорматнаяСтрокаКоличества=ФорматнаяСтрокаКоличества+"0";
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат Формат(Количество,ФорматнаяСтрокаКоличества);
КонецФункции // ФРМсумма()

// Функция поиск товара по штрихкоду
//
// Параметры:
//  ШтрихКод - штрихкод товара, по которому ведется поиск
//
// Возвращаемое значение:
//  структура
//
Функция НайтиСтруктуруПоШтрихКоду(ШтрихКод, флРугатьсяНаЗапрещенныйШтрихКод=Ложь, СообщениеОбОшибке="") Экспорт
	ПрефиксВесовогоШК = Константы.ПрефиксВесовогоШК.Получить();
	ПрефиксШтучногоШК = Константы.ПрефиксШтучногоШК.Получить();
	
	ПолныйШК = ШтрихКод;
	ОбработкаОбъектШтрихКоды = Обработки.ШтрихКоды.Создать();
	СтруктураШК = ОбработкаОбъектШтрихКоды.РазобратьСтрокуШтрихкодаGS1(ШтрихКод, Истина);
	КодМаркировки = "";
	
	Если СтруктураШК.Разобран Тогда
		
		КодМаркировки = ШтрихКод;
		
		// Проверим, что данная маркировка ранее не считывалась
		СтруктураПоиска = Новый Структура("КодМаркировки", ШтрихКод);
		
		НайденныеШК = КодыМаркировки.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеШК.Количество() > 0 Тогда
			СообщениеОбОшибке = "Считанный код маркировки ранее был добавлен";
			Возврат Неопределено;
		КонецЕсли;
		
		Идентификатор01 = СтруктураШК.ДанныеШтрихкода["01"];
		Если НЕ Идентификатор01 = Неопределено Тогда
			ШтрихКод = ОбработкаОбъектШтрихКоды.EANПоКодуМаркировки(Идентификатор01.Значение);
		КонецЕсли;
		
	КонецЕсли;
	
	ШК=СокрЛП(Формат(ШтрихКод,"ЧГ=0")); Вес=0;
	Если ПустаяСтрока(ШК) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// проверим, может это ШК для весового товара
	Попытка ПрефиксШтрихКода=Число(Лев(ШК,2));
	Исключение ПрефиксШтрихКода= 0;
	КонецПопытки;
	Если ПрефиксШтрихКода=ПрефиксВесовогоШК Тогда
		// Вытащим вес
		Вес=Сред(ШК,7+1,СтрДлина(ШК)-8);
		Если НЕ ПустаяСтрока(Вес) Тогда
			Вес=Число(Вес)/1000;
		Иначе
			Вес=0;
		КонецЕсли;
		//если товар весовой, то ищем по первым семи символам
		ШК=Формат(Лев(ШК,7),"ЧГ=0");
	КонецЕсли; 
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ШтрихКод", ШК);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ШтрихКоды.Объект,
	|	ШтрихКоды.ШтрихКод,
	|	ШтрихКоды.ЕдиницаИзмерения,
	|	ШтрихКоды.ХарактеристикаНоменклатуры,
	|	ШтрихКоды.Запрет
	|ИЗ
	|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
	|ГДЕ
	|	ШтрихКоды.ШтрихКод = &ШтрихКод
	|";

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	СтуктураОбъект = Новый Структура();

	Если Выборка.Следующий() Тогда
		Если флРугатьсяНаЗапрещенныйШтрихКод И Выборка.Запрет Тогда
			ОткрытьДиалог("Запрещенный штрихкод", СтатусСообщения.Важное);
			Возврат Неопределено;
		КонецЕсли;
		ТекОбъект = Выборка.Объект;
		Если ТипЗнч(ТекОбъект)=Тип("СправочникСсылка.Номенклатура") Тогда
			//В системе с данным штрихкодом зарегистрирована номенклатура
			ИспользованиеШтрихКодов=ТекОбъект.ТипНоменклатуры.ИспользованиеШтрихКодов;
			СтуктураОбъект.Вставить("Объект",ТекОбъект);
			Если ИспользованиеШтрихКодов = 2 Тогда
				Если НЕ обЗначениеНеЗаполнено(Выборка.ЕдиницаИзмерения) Тогда
					СтуктураОбъект.Вставить("ЕдиницаИзмерения",Выборка.ЕдиницаИзмерения);	
				КонецЕсли;
			ИначеЕсли ИспользованиеШтрихКодов = 3 Тогда
				Если НЕ обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
					СтуктураОбъект.Вставить("ХарактеристикаНоменклатуры",Выборка.ХарактеристикаНоменклатуры);
				КонецЕсли;
			ИначеЕсли ИспользованиеШтрихКодов = 4 Тогда
				Если НЕ обЗначениеНеЗаполнено(Выборка.ЕдиницаИзмерения) Тогда
					СтуктураОбъект.Вставить("ЕдиницаИзмерения",Выборка.ЕдиницаИзмерения);	
				КонецЕсли;
				Если НЕ обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
					СтуктураОбъект.Вставить("ХарактеристикаНоменклатуры",Выборка.ХарактеристикаНоменклатуры);
				КонецЕсли;				
			КонецЕсли; 
			СтуктураОбъект.Вставить("Вес",Вес);
			
			Если СтруктураШК.Разобран Тогда
				СтуктураОбъект.Вставить("КодМаркировки", КодМаркировки);
				ДвоичныеДанныеКМ = ПолучитьДвоичныеДанныеИзСтроки(ПолныйШК);
				СтуктураОбъект.Вставить("КодМаркировкиBase64",  Base64Строка(ДвоичныеДанныеКМ));
			КонецЕсли;
			
			Возврат СтуктураОбъект;
		ИначеЕсли ТипЗнч(ТекОбъект)=Тип("СправочникСсылка.Карточки") Тогда
			//В системе с данным штрихкодом зарегистрирована карточка
			СтуктураОбъект.Вставить("Объект",ТекОбъект);
			Возврат СтуктураОбъект;
		КонецЕсли; 
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // НайтиСтруктуруПоШтрихКоду()

// Возвращает текущий режим вывода кода в табличных частях документа - код или артикул
//
// Параметры:
//  РежимыВыводаКодаВДокументах - ПеречислениеСсылка.РежимыВыводаКодаВДокументах, значение режима вывода
//  ПраваПользователя 			- Структура, кеш прав и настроек пользователя
//                               
// Возвращаемое значение:
//   ОбъектМетаданных - значения перечисления, полученное через метаданные.
//
Функция ПолучитьПараметрыРежимаВыводаКода() Экспорт
	
	Индекс=Перечисления.РежимыВыводаКодаВДокументах.Индекс(фкРежимВыводаКода);
	МД=Метаданные.Перечисления.РежимыВыводаКодаВДокументах;
	Возврат МД.ЗначенияПеречисления[Индекс];
КонецФункции // дкПолучитьПараметрыРежимаВыводаКодаВДокументах()

//Получение кода номенклатуры в зависимости от права режима вывода кода
//
//Параметры:
//  Номенклатура      - СправочникСсылка.Номенклатура - Номенклатура, код которой требуется получить
//  РежимыВыводаКодаВДокументах	- Строка - Имя реквизита номенклатуры, выступающее в качестве кода
//								- ПеречислениеСсылка.РежимыВыводаКодаВДокументах - Настройка режим отображения кода
//  ПраваПользователя - Права пользователя
//Возвращаемое значение:
//    Строка - значение кода номенклатуры в зависимости от настройки режима вывода
//
Функция ПолучитьЗначениеКолонкиКода(Номенклатура,ИмяРеквизитаКода) Экспорт

	//Закомментированное условие потребуется, 
	//когда кроме реквизитов справочника номенклатуры 
	//будем использовать что нибудь еще (например ШК)
	Если ИмяРеквизитаКода="КодИАртикул" Тогда
		КодНоменклатуры = Номенклатура["Код"]+" / "+Номенклатура["Артикул"];
	Иначе
		Попытка
			КодНоменклатуры=Номенклатура[ИмяРеквизитаКода];
		Исключение
			Попытка
				КодНоменклатуры=Номенклатура.Код;
			Исключение
				КодНоменклатуры="";
			КонецПопытки; 
		КонецПопытки; 
	КонецЕсли;
	Возврат КодНоменклатуры;
КонецФункции // ПолучитьЗначениеКолонкиКода()

// ИЗМЕНЕНО Проверяем корректность выбора оплаты
Функция ПроверитьОплаты()
	// Проверим корректность заполнения доп. реквизитов каждого вида оплат
	Для Каждого ТекОплата Из Оплаты Цикл
		Если обЗначениеНеЗаполнено(ТекОплата.Сумма) Тогда
			Продолжить;
		КонецЕсли;
		
		// Производим корректировку заполнения таблицы оплат
		Если ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Кредит Тогда
			ТекОплата.Контрагент = ТекОплата.ДоговорВзаиморасчетов.Владелец;
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Талон ИЛИ ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.КлубнаяКарта Тогда
			ТекОплата.ДоговорВзаиморасчетов	= ТекОплата.Карточка.Объект;
			ТекОплата.Контрагент			= ТекОплата.ДоговорВзаиморасчетов.Владелец;
		КонецЕсли;
			
		Если ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Безналичные И обЗначениеНеЗаполнено(ТекОплата.ТипПлатежнойКарты) Тогда
			ОткрытьДиалог("Не верно заполнены оплаты: не указана карта безналичной оплаты!", СтатусСообщения.Внимание);
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Кредит И обЗначениеНеЗаполнено(ТекОплата.ДоговорВзаиморасчетов) Тогда
			ОткрытьДиалог("Не верно заполнены оплаты: не указан договор кредита!", СтатусСообщения.Внимание);		
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Талон И обЗначениеНеЗаполнено(ТекОплата.Карточка) Тогда
			ОткрытьДиалог("Не верно заполнены оплаты: не указан талон!", СтатусСообщения.Внимание);
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.Талон Тогда
			// Получим сумму по данному талону
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК СуммаОстаток
			|ИЗ
			|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
			|			,
			|			Контрагент = &Контрагент
			|				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК ВзаиморасчетыКомпанииОстатки";
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Контрагент",ТекОплата.Контрагент);
			Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ТекОплата.ДоговорВзаиморасчетов);
			
			// Выполним запрос и определим доступную сумму
			Выборка = Запрос.Выполнить().Выбрать();
			ДоступнаяСумма = ?(Выборка.Следующий(),-Выборка.СуммаОстаток,0);
			
			// Проверяем...
			Если (ДоступнаяСумма-ТекОплата.Сумма) < 0 Тогда
				ОткрытьДиалог("Не верно заполнены оплаты: превышено доступное количество талонов!"+Символы.ПС+"Доступно "+?(ТекОплата.Карточка.Номинал=0,0,ДоступнаяСумма/ТекОплата.Карточка.Номинал)+" талонов", СтатусСообщения.Важное);
			Иначе
				Продолжить;
			КонецЕсли;
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.КлубнаяКарта И обЗначениеНеЗаполнено(ТекОплата.Карточка) Тогда
			ОткрытьДиалог("Не верно заполнены оплаты: не указанна клубная карта!", СтатусСообщения.Внимание);
		ИначеЕсли ТекОплата.ТипОплаты.Объект=Перечисления.ТипыОплатыВРознице.КлубнаяКарта И обПраво("ЗапретитьОплатуКлубнымиКартамиСОтрицательнымБалансом") Тогда
			// Получим сумму по данному талону
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЕСТЬNULL(ВзаиморасчетыКомпанииОстатки.СуммаОстаток, 0) КАК СуммаОстаток 
			|ИЗ
			|	РегистрНакопления.ВзаиморасчетыКомпании.Остатки(
			|			,
			|			Контрагент = &Контрагент
			|				И ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК ВзаиморасчетыКомпанииОстатки";
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Контрагент",ТекОплата.Контрагент);
			Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ТекОплата.ДоговорВзаиморасчетов);
			
			// Выполним запрос и определим доступную сумму
			Выборка = Запрос.Выполнить().Выбрать();
			ДоступнаяСумма = ?(Выборка.Следующий(),-Выборка.СуммаОстаток,0);
			
			// Проверяем...
			Если (ДоступнаяСумма-ТекОплата.Сумма) < 0 Тогда
				ОткрытьДиалог("На клубной карте недостаточно денежных средств!"+Символы.ПС+"Доступно "+Формат(ДоступнаяСумма,"ЧЦ=15; ЧДЦ=2; ЧН=0.00") + " " + СокрЛП(ВалютаДокумента), СтатусСообщения.Важное);
			Иначе
				Продолжить;
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		
		// Если дошли до этой точки, значит были ошибки
		Возврат ЛОЖЬ;
	КонецЦикла;
	
	// ANDV проверим что там навыбирали в оплатах		
	Если  ПолученоНал+ПолученоБезнал > СуммаДокумента Тогда
		Если ПолученоБезнал > СуммаДокумента Тогда // Где то перебрались безналом, проверим где
			ТипОплаты = Перечисления.ТипыОплатыВРознице.Талон;
			СтрокаОплатыТалон = ПолучитьСтрокуТипаОплаты(ТипОплаты);
			СуммаТалон = 0;
			Если СтрокаОплатыТалон <> Неопределено Тогда СуммаТалон = СтрокаОплатыТалон.Сумма; КонецЕсли;
			// Разница только в талонах
			Если СуммаТалон = ПолученоБезнал Тогда 
				Если (СуммаТалон-СуммаДокумента) >= СтрокаОплатыТалон.Карточка.Номинал Тогда  // Акелла промахнулся ...
					ОткрытьДиалог("Сумма талонов превышает необходимую!", СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				// Сумму приравняем Сумме документа, чтобы не было сдачи
				СтрокаОплатыТалон.Сумма = СуммаДокумента;
			Иначе // Пусть разбирается кассир
				ОткрытьДиалог("Сумма безналичной оплаты не может превышать сумму документа!", СтатусСообщения.Важное);
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат Истина;
	
КонецФункции

// Процедура устанавливает флИспользованиеХарактеристик в зависимости
// от Настроек пользователя и использования характеристик в базе
Процедура УстановитьфлИспользованиеХарактеристик() Экспорт		
	
	Если фкАвтоматическиСкрыватьКолонкуХарактеристик Тогда
		Если Товары.Количество() = 0 Тогда
			фкХарактеристика = Ложь;
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	Номенклатура.Ссылка
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|ГДЕ
			|	Номенклатура.Ссылка В(&Номенклатура) И
			|	(Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ
			|	Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2)";
			Запрос.УстановитьПараметр("Номенклатура",Товары.ВыгрузитьКолонку("Номенклатура"));
			
			фкХарактеристика = НЕ Запрос.Выполнить().Пустой();
			
			// Проверим наличие авторабот с характеристикой для вывода.
			Если НЕ фкХарактеристика Тогда
				Для Каждого ТекущаяСтрока Из Товары Цикл
					Если НЕ обЗначениеНеЗаполнено(ТекущаяСтрока.ХарактеристикаНоменклатуры) 
						И (ТипЗнч(ТекущаяСтрока.ХарактеристикаНоменклатуры) = Тип("Строка")
						ИЛИ ТекущаяСтрока.ХарактеристикаНоменклатуры.Владелец = Справочники.Номенклатура.Авторабота.ТипНоменклатуры) Тогда
						фкХарактеристика = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		фкХарактеристика = Истина;
	КонецЕсли;
	
	//Если НЕ фкХарактеристика Тогда
	//	фкХарактеристика = фкАвтоматическиСкрыватьКолонкуХарактеристик;
	//КонецЕсли;

КонецПроцедуры

// Функция загрузить настройки пользователя
//
Функция ЗагрузитьНастройкиПользователя()
	
	фкСпособВыбораСкидки=обПраво("СпособВыбораСкидки",Права);
	фкИспользованиеФормыПодбора=обПраво("ИспользованиеФормыПодбора",Права);
	фкРучнойВводВозврата=обПраво("РучнойВводВозврата",Права);
	фкАвтоматическиСкрыватьКолонкуХарактеристик = обПраво("АвтоматическиСкрыватьКолонкуХарактеристик",Права);
	фкТаймаутДиалогов = обПраво("ТаймаутДиалогов",Права);
	фкПечатьНепробитыхЧеков = обПраво("ПечатьНепробитыхЧеков",Права);
	фкПоказыватьЗапросНаЗавершениеРаботы = обПраво("ПоказыватьЗапросНаЗавершениеРаботы",Права);
	фкРежимВыводаКода = обПраво("РежимВыводаКодаВДокументах",Права);
	фкЗаписыватьСобытияВЖурналРегистрации = обПраво("ЗаписыватьСобытияВЖурналРегистрации",Права);
	РежимДиагностики = фкЗаписыватьСобытияВЖурналРегистрации;
	фкРазрешитьУменьшатьКолво = обПраво("РазрешитьУменьшатьКолво",Права);
	фкРаботаСФронтомКассира = обПраво("РаботаСФронтомКассира",Права);
	фкРазрешитьРаботуССистемойПослеВыхода = обПраво("РазрешитьРаботуССистемойПослеВыхода",Права);
	фкЗапретитьПодарки = обПраво("ЗапретитьПодарки",Права);
	фкЗащитаПечатныхФорм = обПраво("ЗащитаПечатныхФорм",Права);
	фкОткрытиеПечатныхФормДокументовВРежимеПросмотра = обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	фкРазрешитьРучнуюАвторизациюБезналичныхПлатежей = обПраво("РазрешитьРучнуюАвторизациюБезналичныхПлатежей",Права);
	фкСмотретьФискальныеСчетчики = обПраво("СмотретьФискальныеСчетчики",Права);
	фкРазрешитьГашениеФискСчетчиков = обПраво("РазрешитьГашениеФискСчетчиков",Права);
	фкРазрешитьГашениеАвторизаторов = обПраво("РазрешитьГашениеАвторизаторов",Права);
	фкУстройствоПечатиНефискальныхДокументов = обПраво("УстройствоПечатиНефискальныхДокументов",Права);	
	фкОформлятьРКОПриВозврате = обПраво("ОформлятьРКОПриВозвратеВнеКассовойСмены",Права);
	фкОформлятьВозвратОтПокупателяВнеКассовойСмены = обПраво("ОформлятьВозвратОтПокупателяВнеКассовойСмены",Права);
	фкПечатьПречекаПлатежа = обПраво("ПечатьПречекаПлатежа",Права);
	фкПроводитьАсинхронныеПлатежи = обПраво("РазрешитьОтложенныйПлатежПоЧеку",Права);
	фкУдалятьПлатежи =  обПраво("РазрешитьАннулированиеЧековБезВозвратаОплаты",Права);
	ОсновнаяКассаКомпании = обПраво("ОсновнаяКассаКомпании",Права);
	фкГашенияКассыБезОбработки = обПраво("РазрешитьСниматьZотчетБезРегламентнойОбработки",Права);
	флПогашатьЧекЦеликом                             = обПраво("ПогашатьЧекЦеликом",Права);
	флИнтеллектуальныйРежимВводаОплаты               = обПраво("ИнтеллектуальныйРежимВводаОплаты",Права);
	флРазрешитьПроводитьИнкассацию                   = обПраво("РазрешитьПроводитьИнкассацию",Права);
	флРазрешитьРучнойВыборКарточки                   = обПраво("РазрешитьРучнойВыборКарточки",Права);
	флРазрешитьВводКодаКартыПользователяВручную      = обПраво("РазрешитьВводКодаКартыПользователяВручную", Права);
	флВестиЛогДействийПользователяВоФронте           = обПраво("ВестиЛогДействийПользователяВоФронте", Права);

	фкИерархия = Истина;
	УстановитьфлИспользованиеХарактеристик();
	
КонецФункции

// Загружает настройки нового пользователя или проверяет настройки текущего
Функция ЗагрузитьПараметрыПользователя(Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		
		Результат = Истина;
		
		фкРаботаСФронтомКассира = обПраво("РаботаСФронтомКассира",Права);
		
		РежимДиагностики=фкЗаписыватьСобытияВЖурналРегистрации;
		// Проверка права на запуск фронта
		Если фкРаботаСФронтомКассира=Перечисления.РаботаСФронтомКассира.Запрещено Тогда
			ОткрытьДиалог("Работа с фронтом кассира запрещена",СтатусСообщения.Важное);
			Результат = Ложь;
		КонецЕсли;
		// проверим тип цен фронта
		ТипЦен=обПраво("ТипЦенФронта",Права);
		Если обЗначениеНеЗаполнено(ТипЦен) Тогда
			ОткрытьДиалог("Для АРМ кассира не задан тип цен (см. настройки для подразделения """+СокрЛП(ПараметрыСеанса.ПодразделениеКомпании)+""")",СтатусСообщения.Важное);
			Результат = Ложь;
		КонецЕсли;
		// проверим инкассатора по умолчанию
		ИнкассаторПоУмолчанию=обПраво("ИнкассаторПоУмолчанию",Права);
		Если обЗначениеНеЗаполнено(ИнкассаторПоУмолчанию) Тогда
			ОткрытьДиалог("Для АРМ кассира не задан Инкассатор по умолчанию (см. настройки для подразделения """+СокрЛП(ПараметрыСеанса.ПодразделениеКомпании)+""")",СтатусСообщения.Важное);
			Результат = Ложь;
		КонецЕсли;

		// проверим отдел
		СкладКомпании=ПараметрыСеанса.Компьютер.Отдел;

		Если СкладКомпании=Справочники.СкладыКомпании.ПустаяСсылка() Тогда
			ОткрытьДиалог("Заполните реквизит ""Основной отдел"" в справочнике ""Компьютеры""",СтатусСообщения.Важное);
			Результат = Ложь;
		КонецЕсли;
		
		// кэшируем нужные нам права и настройки
		ЗагрузитьНастройкиПользователя();
		
	Иначе
		ПараметрыСеанса.Пользователь          = Пользователь;
		ПараметрыСеанса.Организация           = Пользователь.Организация;
		ПараметрыСеанса.ПодразделениеКомпании = Пользователь.Подразделение;
		Автор = Пользователь;
		Права = обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
		
		Если ЗагрузитьПараметрыПользователя() Тогда
			// Пользователь сеанса сменен, обновляем кеш прав
			глПрава = Права;
			
			// Обновляем заголовок системы
			УстановитьЗаголовокСистемы(Метаданные.Синоним);
			
			// Включим индикацию пользователя в заголовке системы
			Если обПраво("ИндикацияПользователяВЗаголовкеСистемы",глПрава) Тогда
				Если обПраво("ПредставлениеАвторовПоПолномуНаименованию",глПрава) Тогда
					Если обЗначениеНеЗаполнено(Пользователь.Сотрудник) Тогда
						ИмяПользователя = СокрЛП(Пользователь.Наименование);	
					Иначе
						ИмяПользователя = СокрЛП(Пользователь.Сотрудник.Наименование);
					КонецЕсли;
				Иначе
					ИмяПользователя = СокрЛП(Пользователь.Код);
				КонецЕсли;
				УстановитьЗаголовокСистемы(ПолучитьЗаголовокСистемы()+"    Пользователь: <"+ИмяПользователя+">");
			КонецЕсли;
			
			// Включим индикацию подразделения в заголовке системы
			Если обПраво("ИндикацияПодразделенияВЗаголовкеСистемы",глПрава) Тогда
				УстановитьЗаголовокСистемы(ПолучитьЗаголовокСистемы()+"    Подразделение: <"+Пользователь.Подразделение+">");
			КонецЕсли;
			
			Результат = Истина;
		Иначе
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
    Возврат Результат;
	
КонецФункции

// Проверяет используются ли характеристики у переданного товара
// или нет и, если используются, то каким образом списываются
Функция ПроверитьИспользованиеХарактеристикиУТовара(Товар) Экспорт
// Товар - "СправочникСсылка.Номенклатура", у этого товара проверяется
// использование характеристик
// Возврат:
// 0 - Характеристики не используются или выбран не тот товар 
// 1 - Характеристики используются и используется ручное списание
// 2 - Характеристики используются и используется авто списание

	Если ТипЗнч(Товар) <> Тип("СправочникСсылка.Номенклатура") Тогда Возврат 0; КонецЕсли;

	ТипНоменклатуры = Товар.ТипНоменклатуры;
	Если (ТипНоменклатуры.ИспользованиеХарактеристик = 1) ИЛИ    // подчинение типу номенклатуры
		 (ТипНоменклатуры.ИспользованиеХарактеристик = 2) Тогда  // подчинение номенклатурной позиции
		 
		РежимСписанияПоХарактеристикам = ТипНоменклатуры.АвтоСписаниеХарактеристик; 
		Если (НЕ РежимСписанияПоХарактеристикам.Пустая()) И (НЕ РежимСписанияПоХарактеристикам = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание) Тогда
			Возврат 2;  // авто списание
		Иначе
			Возврат 1;  // ручное списание
		КонецЕсли;
	Иначе
		Возврат 0;		// характеристики не ведуться
	КонецЕсли;

КонецФункции

// Функция получить строку типа оплаты
//
//Параметры:
//  ТипОплаты	 - 
//
// Возвращаемое значение:
//
Функция ПолучитьСтрокуТипаОплаты(ТипОплаты)
	
	Для Каждого стрТипОплаты Из Оплаты Цикл
		Если стрТипОплаты.ТипОплаты.Объект = ТипОплаты Тогда
			Возврат стрТипОплаты;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Функция проверить тип оплаты
//
//Параметры:
//  ТипОплаты	 - 
//
// Возвращаемое значение:
//
Функция ПроверитьТипОплаты(ТипОплаты) Экспорт
	
	Для Каждого стрТипОплаты Из тзДоступныеВидыОплат Цикл
		Если стрТипОплаты.ТипОплаты.Объект = ТипОплаты Тогда
			Возврат стрТипОплаты;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Получает форму выбора у основной формы фронта кассира
Функция ПолучитьФормуФронта(ИмяФормы, ФормаВладелец, ОсновнаяФорма=Неопределено, ПараметрыОткрытия=Неопределено)
// получить форму фронта мы можем только из основной формы фронта
// в связи с этими необходимо определить
// 1) текущую форму - собственно ЭтаФорма
// 2) ОсновнаяФорма - форму у которой запрашивать форму фронта

    
	Если ОсновнаяФорма = Неопределено Тогда  // основная форма не передана, первый вызов
		// если у этой формы нету владельца то она и является основной
		Результат = ПолучитьФормуФронта(ИмяФормы, ФормаВладелец, 
					?(ФормаВладелец.ВладелецФормы=Неопределено, ФормаВладелец, ФормаВладелец.ВладелецФормы), ПараметрыОткрытия);
	ИначеЕсли ОсновнаяФорма.ВладелецФормы <> Неопределено Тогда // у "ОсновнаяФорма" тоже есть владелец
		Результат = ПолучитьФормуФронта(ИмяФормы, ФормаВладелец, ОсновнаяФорма.ВладелецФормы, ПараметрыОткрытия);
		
	ИначеЕсли ОсновнаяФорма.ВладелецФормы = Неопределено Тогда // нашли основную форму
		// спросим у основной формы какая форма выбора нам нужна
		Результат = ОсновнаяФорма.ПолучитьФормуФронта(ИмяФормы,ФормаВладелец, ПараметрыОткрытия);
	Иначе
		Результат = Неопределено;	
	КонецЕсли;
		
	Возврат Результат;
КонецФункции

// Процедура обработка выбора фронта
//
//Параметры:
//  ЗначениеВыбора	 - 
//  Источник	 - 
//  ЭтаФорма	 - 
//  ДопПараметры	 - 
//
Процедура ОбработкаВыбораФронта(ЗначениеВыбора, Источник, ЭтаФорма, ДопПараметры=Неопределено) Экспорт
	
	// получаем дополнительные параметры
	Попытка ТекущаяСтрока = ДопПараметры.Получить(0).Значение;	Исключение	ТекущаяСтрока = Неопределено;	КонецПопытки;
	Попытка ВведеннаяСуммаБезнала = ДопПараметры.Получить(1).Значение;	Исключение	ВведеннаяСуммаБезнала = Неопределено; КонецПопытки;

	Если УстановленРежим("Блокировка") Тогда Возврат; КонецЕсли;
	ТипЗначенияВыбора=ТипЗнч(ЗначениеВыбора);
	Если ТипЗначенияВыбора=Тип("СправочникСсылка.СкладыКомпании") Тогда
		// проверяем режим
		Если НЕ УстановленРежим("ВыборСклада") Тогда Возврат; КонецЕсли;
		
		Если Товары.Количество()=0 Тогда
			Если НЕ СкладКомпании=ЗначениеВыбора Тогда
				СкладКомпании = ЗначениеВыбора;
				ОбработкаРеквизита("СкладКомпании", ТекущаяСтрока, ЭтаФорма);
			КонецЕсли;
			
		ИначеЕсли НЕ ТекущаяСтрока=Неопределено Тогда
			Если ТекущаяСтрока.МестоРазмещения<>ЗначениеВыбора Тогда
				СостояниеОплата = (Найти(Режим, ВРЕГ("ЧекНаОплату")) > 0) ИЛИ (Найти(Режим, ВРЕГ("ПриходныйКассовыйОрдер")) > 0) ИЛИ 
				                  (Найти(Режим, ВРЕГ("ЧекНаОплатуВозврат")) > 0) ИЛИ (Найти(Режим, ВРЕГ("РасходныйКассовыйОрдер")) > 0) ИЛИ
				                  (Найти(Режим, ВРЕГ("БанковскаяВыписка")) > 0);
				Если НЕ СостояниеОплата Тогда
					КодНалогообложенияСкладКомпании=ПолучитьКодНалогообложенияСкладыКомпании(СкладКомпании.Организация,СкладКомпании.Подразделение,СкладКомпании);
					КодНалогообложенияЗначенияВыбора=ПолучитьКодНалогообложенияСкладыКомпании(ЗначениеВыбора.Организация,ЗначениеВыбора.Подразделение,ЗначениеВыбора);
					Если КодНалогообложенияСкладКомпании<>КодНалогообложенияЗначенияВыбора Тогда
						ОписаниеОшибки = "СНО выбранного отдела отлична от СНО документа!
						|Выбор отдела отменен.";
						ОткрытьДиалог(ОписаниеОшибки, СтатусСообщения.Важное);
						Возврат;
					КонецЕсли; 
					ТекущаяСтрока.МестоРазмещения=ЗначениеВыбора;
					ОбработкаРеквизита("Товары.МестоРазмещения",ТекущаяСтрока,ЭтаФорма);
				Иначе
					СкладКомпании = ЗначениеВыбора;
				КонецЕсли;
				
				ЗаписатьВФайлЛога("Выбран склад",,ЗначениеВыбора);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияВыбора=Тип("СправочникСсылка.Карточки") Тогда
		
		Если ТипЗнч(ЗначениеВыбора.Объект) = Тип("СправочникСсылка.Контрагенты")
			И НЕ ЗначениеВыбора.Объект.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
			ОткрытьДиалог("<"+Строка(ЗначениеВыбора)+"> не является дисконтной картой физ лица!", СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;
		
		// проверяем режим
		Если НЕ УстановленРежим("ВыборКарточки") Тогда Возврат; КонецЕсли;
		
		Если ЗначениеВыбора.ВидКарточки=Перечисления.ВидыКарточек.ДисконтнаяКарта Тогда
			Если фкСпособВыбораСкидки <> Перечисления.СкидкиСпособыВыбора.СкидкиЗапрещены Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = "
				|ВЫБРАТЬ
				|	ШтрихКоды.Запрет
				|ИЗ
				|	РегистрСведений.ШтрихКоды КАК ШтрихКоды
				|ГДЕ
				|	ШтрихКоды.Объект = &Карточка
				|";
				Запрос.УстановитьПараметр("Карточка",ЗначениеВыбора);
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					Если НЕ Выборка.Запрет Тогда
						Карточка = ЗначениеВыбора;
						ОбработкаРеквизита("Карточка",,ЭтаФорма);
						ЗаписатьВФайлЛога("Выбрана дисконтная карточка",,ЗначениеВыбора);
						ЭтаФорма.Индикация("Скидка");
					Иначе
						ОткрытьДиалог("Выбранная карточка заблокирована!", СтатусСообщения.Важное);
					КонецЕсли;
				КонецЕсли;
					
			КонецЕсли;
		Иначе 
			 ОткрытьДиалог(Строка(ЗначениеВыбора)+" не является дисконтной картой!",СтатусСообщения.Важное);
		КонецЕсли; 
		
	ИначеЕсли ТипЗначенияВыбора=Тип("СправочникСсылка.ТипыПлатежныхКарт") Тогда

		Если ВведеннаяСуммаБезнала=Неопределено ИЛИ ВведеннаяСуммаБезнала=0 ИЛИ ЗначениеВыбора=Справочники.ТипыПлатежныхКарт.ПустаяСсылка() Тогда
			ПлатежнаяКарта=Справочники.ТипыПлатежныхКарт.ПустаяСсылка();
			ПолученоБезнал=0;
		Иначе
			ПлатежнаяКарта=ЗначениеВыбора;
			ПолученоБезнал=ВведеннаяСуммаБезнала;
		КонецЕсли; 
		ОбработкаРеквизита("ПолученоБезнал",,ЭтаФорма,);
		ЗаписатьВФайлЛога("Выбрана карточка безнал оплаты",,ЗначениеВыбора);
		Поле="";
								
	ИначеЕсли ТипЗначенияВыбора=Тип("СправочникСсылка.КассыККМ") Тогда
		// проверяем режим
		Если НЕ УстановленРежим("ВыборКассы") Тогда Возврат; КонецЕсли;
		
		Если ЗначениеВыбора.ЭтоГруппа Тогда Возврат; КонецЕсли;
		
		Если КассаККМ<>ЗначениеВыбора Тогда
			КассаККМ=ЗначениеВыбора;
			ОбработкаРеквизита("КассаККМ",,ЭтаФорма);
			ЗаписатьВФайлЛога("Выбрана касса ККМ",,ЗначениеВыбора);
		КонецЕсли; 
		
	ИначеЕсли ТипЗначенияВыбора=Тип("ДокументСсылка.Чек") Тогда
		
		Если НЕ ДоступностьПробитияЧекаКонтрагенту(ЗначениеВыбора) Тогда
			Возврат;
		КонецЕсли;
		
		// Выбор отложенного чека
		Если УстановленРежим("ВыборЧека") Тогда
			// проверим отложенный ли чек к нам пришел
			Если ЗначениеВыбора.ХозОперация<>Справочники.ХозОперации.ЧекОтложенный Тогда
				//Разрешим загрузку только отложенного чека
				ОткрытьДиалог("Выбранный чек не является отложенным. Выберите отложенный чек.", СтатусСообщения.Важное);
				Возврат;
			КонецЕсли; 
			//Теперь определимся с тем, это чек или чек на возврат
			Если обЗначениеНеЗаполнено(ЗначениеВыбора.ДокументОснование) Тогда
				ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма); 
				ХозОперация=Справочники.ХозОперации.Чек;
				ЗаписатьВФайлЛога("Выбран чек",,ЗначениеВыбора);
			Иначе
				//Иначе отложенный чек является чеком на возврат
				Если ЗначениеВыбора.ДокументОснование.ХозОперация<>Справочники.ХозОперации.Чек ИЛИ
					// (НЕ ЗначениеВыбора.ДокументОснование.Проведен) ИЛИ
					обЗначениеНеЗаполнено(ЗначениеВыбора.ДокументОснование.НомерЧека) Тогда
					//Если у этого чека основание не является чеком
					ОткрытьДиалог("Основание отложенного чека на возврат должно быть чеком и должно быть проведено!", СтатусСообщения.Важное);
					Возврат;
				КонецЕсли;
				ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма); 
				ХозОперация=Справочники.ХозОперации.ЧекНаВозврат;
				ЗаписатьВФайлЛога("Выбран чек на возврат",,ЗначениеВыбора);
			КонецЕсли;
		ИначеЕсли УстановленРежим("КопияЧека") Тогда
			//Загрузка копии чека
			Если
				(обЗначениеНеЗаполнено(ЗначениеВыбора.НомерЧека)) Тогда
				//Можно загружать только проведенные чеки
				ОткрытьДиалог("Чек не пробит!",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли; 
			ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма); 
			ЗаписатьВФайлЛога("Выбрана копия чека",,ЗначениеВыбора);
		ИначеЕсли УстановленРежим("ВыборЧекаВозврат") Тогда	
			
			Если НЕ ДоступностьПробитияЧекаКонтрагенту(ЗначениеВыбора) Тогда
				Возврат;
			КонецЕсли;
			
			//Выбор чека на возврат
			Если (НЕ ЗначениеВыбора.Пустая()) И ((ЗначениеВыбора.ХозОперация<>Справочники.ХозОперации.Чек) ИЛИ
				 обЗначениеНеЗаполнено(ЗначениеВыбора.НомерЧека)) Тогда

				ОткрытьДиалог("Чек не пробит! Чек на возврат возможен только по пробитому чеку.",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли; 
			
			Если ЗначениеВыбора.Пустая() Тогда
				// ручной возврат, нужен новый чек с заполненными основными реквизитами
				ЗагрузитьЧек(Неопределено,ЭтаФорма);
				ЗаписатьВФайлЛога("Выбран ручной возврат",,ЗначениеВыбора);
			Иначе
				ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма); 
				//Этот чек будет являться основанием для чека возврата
				ДокументОснование=ЗначениеВыбора;
			КонецЕсли;
			
			// попробуем получить информацию откуда был получен чек
			флАрхив = Ложь;
			Если ТипЗнч(Источник.НачальноеЗначениеВыбора) = Тип("Булево") Тогда
				флАрхив = Источник.НачальноеЗначениеВыбора;
			КонецЕсли;
			
			Если флАрхив И фкОформлятьВозвратОтПокупателяВнеКассовойСмены Тогда
				ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя;
				Ссылка=Документы.ВозвратОтПокупателя.ПустаяСсылка();
			Иначе
				ХозОперация=Справочники.ХозОперации.ЧекНаВозврат;
				//А теперь удалим ссылку на него для создания нового чека
				Ссылка=Документы.Чек.ПустаяСсылка();
			КонецЕсли;			
			
		ИначеЕсли УстановленРежим("ВыборТоварногоЧека") Тогда
			// Выполняем проверку прав пользователя. Можно ли ему печатать непробитый чек?
			Если НЕ фкПечатьНепробитыхЧеков Тогда
				ОткрытьДиалог("Нет прав на печать товарного чека!", СтатусСообщения.Важное);
				Возврат;
			КонецЕсли;
			ТоварныйЧекДляПечати = ЗначениеВыбора;
			ЗаписатьВФайлЛога("Выбран товарный чек",,ЗначениеВыбора);
			
		Иначе // неизвестный режим
			Возврат;
		КонецЕсли; 
		
		ЭтаФорма.УправлениеИнтерфейсом();
		ЭтаФорма.Индикация("Сдача");
		
	ИначеЕсли ТипЗначенияВыбора=Тип("ДокументСсылка.ЧекНаОплату") ИЛИ ТипЗначенияВыбора=Тип("ДокументОбъект.ЧекНаОплату") Тогда
		
		Если НЕ ДоступностьПробитияЧекаКонтрагенту(ЗначениеВыбора) Тогда
			Возврат;
		КонецЕсли;
		
		// В режиме приема оплаты был выбран чек на оплату
		Если УстановленРежим("ВыборДокументаНаОплатуВозврат") Тогда
			// Если уже передался сформированный чек на возврат.
			Если ЗначениеВыбора.ХозОперация = Справочники.ХозОперации.ЧекНаОплатуВозврат
				ИЛИ ЗначениеВыбора.ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
				ЧекНаОплатуВозврат = ЗначениеВыбора;
			Иначе
				ЧекНаОплатуВозврат=Документы.ЧекНаОплату.СоздатьДокумент();
				ЧекНаОплатуВозврат.Заполнить(ЗначениеВыбора);
			КонецЕсли;
			ЗагрузитьЧек(ЧекНаОплатуВозврат,ЭтаФорма);
			ХозОперация=ЧекНаОплатуВозврат.ХозОперация;
			ЗаписатьВФайлЛога("Выбран документ на оплату на возврат",,ЗначениеВыбора);
		ИначеЕсли УстановленРежим("ВыборДокументаНаОплату") Тогда
			ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма);
			ХозОперация=ЗначениеВыбора.ХозОперация;
			ЗаписатьВФайлЛога("Выбран документ на оплату",,ЗначениеВыбора);
		ИначеЕсли УстановленРежим("КопияЧека") Тогда
			Если
				(обЗначениеНеЗаполнено(ЗначениеВыбора.НомерЧека)) Тогда
				//Можно загружать только проведенные чеки
				ОткрытьДиалог("Чек не пробит!",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли; 
			ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияВыбора=Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		
		Если НЕ ДоступностьПробитияЧекаКонтрагенту(ЗначениеВыбора) Тогда
			Возврат;
		КонецЕсли;
		
		Если УстановленРежим("ВыборДокументаНаОплату") Тогда
			ЗагрузитьЧек(ЗначениеВыбора, ЭтаФорма);
			Если ЗначениеВыбора = Неопределено Тогда
				Возврат;
			КонецЕсли;
			ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер;
			ЗаписатьВФайлЛога("Выбран документ на оплату",,ЗначениеВыбора);
		ИначеЕсли УстановленРежим("КопияЧека") Тогда
			Если
				(обЗначениеНеЗаполнено(ЗначениеВыбора.НомерЧека)) Тогда
				//Можно загружать только проведенные чеки
				ОткрытьДиалог("Чек не пробит!",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли; 
			ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма);
			ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер;
			ЗаписатьВФайлЛога("Выбрана копия чека",,ЗначениеВыбора);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияВыбора=Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		
		Если НЕ ДоступностьПробитияЧекаКонтрагенту(ЗначениеВыбора) Тогда
			Возврат;
		КонецЕсли;
		
		Если УстановленРежим("ВыборДокументаНаОплату") Тогда
			ЗагрузитьЧек(ЗначениеВыбора, ЭтаФорма);
			ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер;
			ЗаписатьВФайлЛога("Выбран документ на оплату",,ЗначениеВыбора);
		ИначеЕсли УстановленРежим("КопияЧека") Тогда
			Если
				(обЗначениеНеЗаполнено(ЗначениеВыбора.НомерЧека)) Тогда
				//Можно загружать только проведенные чеки
				ОткрытьДиалог("Чек не пробит!",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли; 
			ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма);
			ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер;
			ЗаписатьВФайлЛога("Выбрана копия чека",,ЗначениеВыбора);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияВыбора=Тип("ДокументСсылка.Выписка") Тогда
		
		Если НЕ ДоступностьПробитияЧекаКонтрагенту(ЗначениеВыбора) Тогда
			Возврат;
		КонецЕсли;
		
		Если УстановленРежим("ВыборДокументаНаОплату") Тогда
			ЗагрузитьЧек(ЗначениеВыбора, ЭтаФорма);
			Если ЗначениеВыбора = Неопределено Тогда
				Возврат;
			КонецЕсли;
			ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки;
			ЗаписатьВФайлЛога("Выбран документ на оплату",,ЗначениеВыбора);
		ИначеЕсли УстановленРежим("КопияЧека") Тогда
			Если
				(обЗначениеНеЗаполнено(ЗначениеВыбора.НомерЧека)) Тогда
				//Можно загружать только проведенные чеки
				ОткрытьДиалог("Чек не пробит!",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли; 
			ЗагрузитьЧек(ЗначениеВыбора,ЭтаФорма);
			ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки;
			ЗаписатьВФайлЛога("Выбрана копия чека",,ЗначениеВыбора);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияВыбора=Тип("СправочникСсылка.ТипыСкидок") Тогда
		Если  НЕ УстановленРежим("ВыборСкидки") ИЛИ ЗначениеВыбора.ЭтоГруппа Тогда
			Возврат;
		КонецЕсли;
		
		// Установим скидку
		НетСкидкиНаЧек = СкидкаНаценка.Пустая();
		НетСкидкиНаСтроку = ?(ТекущаяСтрока = Неопределено,Истина, ТекущаяСтрока.СкидкаНаТовар.Пустая());
		
		Если ЗначениеВыбора.ВидСкидки = Перечисления.ВидыСкидок.НаДокумент Тогда
			Если (НЕ НетСкидкиНаЧек) И (НЕ ОткрытьДиалогВопроса("Уже используется скидка <"+СкидкаНаценка+">"+Символы.ПС+"Отменить текущую скидку?", ИСТИНА)) Тогда
				Возврат;
			КонецЕсли;
			Если ОткрытьДиалогВопроса("Установить новую скидку <"+ЗначениеВыбора+">?", ИСТИНА) Тогда
				СкидкаНаценка = ЗначениеВыбора;
			Иначе
				СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецЕсли;
			
			ОбработкаРеквизита("СкидкаНаценка",, ЭтаФорма);
			ЗаписатьВФайлЛога("Выбрана скидка на документ",,ЗначениеВыбора);
		Иначе
			Если ТекущаяСтрока = Неопределено Тогда
				Возврат;
			КонецЕсли;
			Если (НЕ НетСкидкиНаСтроку) И (НЕ ОткрытьДиалогВопроса("Уже используется скидка <"+ТекущаяСтрока.СкидкаНаТовар+">"+Символы.ПС+"Отменить текущую скидку?", ИСТИНА)) Тогда
				Возврат;
			КонецЕсли; 
			Если ОткрытьДиалогВопроса("Установить новую скидку <"+ЗначениеВыбора+">?", ИСТИНА) Тогда
				ТекущаяСтрока.СкидкаНаТовар = ЗначениеВыбора;
			Иначе
				ТекущаяСтрока.СкидкаНаТовар = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецЕсли;
			
			ОбработкаРеквизита("Товары.СкидкаНаТовар", ТекущаяСтрока, ЭтаФорма);
			ЗаписатьВФайлЛога("Выбрана скидка на товар",,ЗначениеВыбора);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияВыбора=Тип("СправочникСсылка.Номенклатура") Тогда
		Если НЕ (УстановленРежим("ВыборТовара") ИЛИ 
			  	 УстановленРежим("ВыборПодарка")) Тогда
			 Возврат;
		КонецЕсли;
		Товар = ЗначениеВыбора;
		
		// В зависимости от текущего режима произведем добавление номенклатуры как подарка, либо как товара
		Если УстановленРежим("ВыборПодарка") Тогда
			// Определим правила использования хар-к выбранного товара
			ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			Если ПроверитьИспользованиеХарактеристикиУТовара(Товар) = 1 Тогда // ручное списание характеристик
				ХарактеристикаНоменклатуры = ВыбратьХарактеристикуПодарка(ЭтаФорма, Товар);
				Если обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры) Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если фкАвтоматическиСкрыватьКолонкуХарактеристик И ЭтаФорма<> Неопределено Тогда
				Попытка
					Если НЕ ЭтаФорма.ЭлементыФормы.Товары.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
						ЭтаФорма.ЭлементыФормы.Товары.Колонки.ХарактеристикаНоменклатуры.Видимость = (Товар.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ Товар.ТипНоменклатуры.ИспользованиеХарактеристик = 2);
					КонецЕсли; 
				Исключение
				КонецПопытки; 
			КонецЕсли;
			
			Если ЭтаФорма<> Неопределено Тогда
				Попытка
					Если НЕ ЭтаФорма.ЭлементыФормы.Товары.Колонки.КодыМаркировки.Видимость
						И НЕ (ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки
						ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат) Тогда
						ЭтаФорма.ЭлементыФормы.Товары.Колонки.КодыМаркировки.Видимость = (Товар.ТипНоменклатуры.ВедетсяМаркировка);
					КонецЕсли; 
				Исключение
				КонецПопытки; 
			КонецЕсли;
			
			// Сформируем структуру поиска товара в таблице
			ПараметрыОтбора = Новый Структура("Номенклатура,ЕдиницаИзмерения,ХарактеристикаНоменклатуры,Набор", Товар, Товар.ОсновнаяЕдиницаИзмерения, ХарактеристикаНоменклатуры, Справочники.Номенклатура.ПустаяСсылка());
			МассивСтрок = Товары.НайтиСтроки(ПараметрыОтбора);
			Если МассивСтрок.Количество()=0 Тогда
				ТекСтрока = Товары.Добавить();
				ТекСтрока.Номенклатура = Товар;
				ТекСтрока.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;
				ОбработкаРеквизита("Товары.Номенклатура", ТекСтрока, ЭтаФорма);
			Иначе
				ТекСтрока = МассивСтрок[0];
				ТекСтрока.Количество = ТекСтрока.Количество + 1;
			КонецЕсли;
			
			// Определим процент скидки и наложим скидку на итоговую сумму
			ТекСтрока.СуммаСкидкиСтроки = ТекСтрока.СуммаСкидкиСтроки + ТекСтрока.Цена;
			ТекСтрока.СуммаСкидкиСтроки = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидкиСтроки,ТекСтрока.СкидкаНаТовар);
			ОбработкаРеквизита("Товары.Количество", ТекСтрока, ЭтаФорма);
			ТекСтрока.ПроцентСкидкиСтроки = ?(ТекСтрока.Сумма = 0, 100, ТекСтрока.СуммаСкидкиСтроки / ТекСтрока.Сумма) * 100;
			
			ЗаписатьВФайлЛога("Выбран подарок",,ЗначениеВыбора);
		Иначе
			ДобавитьНоменклатуру(ЭтаФорма,Товар);
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияВыбора=Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда
		Если (НЕ УстановленРежим("ВыборХарактеристики")) 
			ИЛИ (УстановленРежим("ВыборТовара.ВыборХарактеристики"))Тогда   // ручное списание, значение обработано в обработчике товара
			Возврат;
		КонецЕсли;
				
		Если ТекущаяСтрока = Неопределено Тогда Возврат; КонецЕсли;
		
		Если НЕ флУстанавливатьХарактеристику Тогда Возврат; КонецЕсли;
		
		// меняем текущую характеристику
		ТекущаяСтрока.ХарактеристикаНоменклатуры = ЗначениеВыбора;
		ОбработкаРеквизита("Товары.ХарактеристикаНоменклатуры", ТекущаяСтрока, ЭтаФорма);
	ИначеЕсли ТипЗначенияВыбора = Тип("СправочникСсылка.Оборудование") Тогда
		Если НЕ УстановленРежим("ВыборКассы") Тогда Возврат; КонецЕсли;
		
		Если GUID_ФР = ЗначениеВыбора.ИдентификаторОборудования Тогда Возврат; КонецЕсли;
		
		тмпGUID = GUID_ФР;
		
		GUID_ФР=ЗначениеВыбора.ИдентификаторОборудования;
		Если НЕ ПустаяСтрока(GUID_ФР) Тогда
			// Есть устройство, включаем его
			Если глТорговоеОборудование.СтатусОборудования(GUID_ФР)=Перечисления.СтатусыОборудования.Включено Тогда
				ОписаниеОшибки="";
				// Выключим оборудование
				КодОшибки=глТорговоеОборудование.ВыключитьОборудование(GUID_ФР,ОписаниеОшибки);
				Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,ОписаниеОшибки) Тогда
					Результат=Ложь;
					GUID_ФР = тмпGUID;
				Иначе
					ФР=ЗначениеВыбора;
					ТаймаутФР=ФР.Таймаут;	
				КонецЕсли;
			КонецЕсли;
			ОписаниеОшибки="";
			КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_ФР,ОписаниеОшибки);
			// Включаем оборудование
			Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,ОписаниеОшибки) Тогда
				Результат=Ложь;
				GUID_ФР = тмпGUID;
			Иначе
				ФР=ЗначениеВыбора;
				ЗаписатьВФайлЛога("Выбрано оборудование",,ЗначениеВыбора);
			КонецЕсли;
			Если НЕ ПустаяСтрока(GUID_ФР) Тогда
				ТмпЗнач=0;
				Если глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_ФР,"ДлинаСтроки",ТмпЗнач) И (ТмпЗнач>0) Тогда
					ДлинаСтрокиФР=ТмпЗнач;
				КонецЕсли;
                // Получим доступные виды оплат
				ПолучитьДоступныеВидыОплат();
				ПолучитьВерсиюФФД();
			КонецЕсли;
		Иначе
			GUID_ФР = тмпGUID;	
		КонецЕсли;
			
	КонецЕсли; 
		
КонецПроцедуры // ОбработкаВыбора()

// Процедура распознать код
//
//Параметры:
//  ЭтаФорма	 - 
//  Код	 - 
//  РучнойВводКода	 - 
//
Процедура РаспознатьКод(ЭтаФорма, Код, РучнойВводКода = Ложь)
	
	//обработка события от сканера ШК
	Если Лев(Код,2)=Клавиатура.Префикс Тогда// Пришло событие от клавиатуры
		Клавиша = ПолучитьКлавишуПоКоду(Клавиатура,Код);
		Операция=СокрЛП(Клавиша.Действие);
		ОбъектДействия=Клавиша.Объект;
			
		Если ПустаяСтрока(Операция)=1 Тогда
			Предупреждение("Нажатая клавиша не определена", 5);
			Возврат;
		КонецЕсли;
		
		ВыполнитьОперацию(Операция, ЭтаФорма, ОбъектДействия);
		Возврат;
	КонецЕсли; 		
	
	СообщениеОшибки = "";
	СтруктураПоиска = НайтиСтруктуруПоШтрихКоду(Код,Истина,СообщениеОшибки);
	Если  обЗначениеНеЗаполнено(СтруктураПоиска) Тогда
		Если НЕ ПустаяСтрока(СообщениеОшибки) Тогда
			ОткрытьДиалог(СообщениеОшибки, СтатусСообщения.Важное);
		ИначеЕсли УстановленРежим("Блокировка") Тогда
			ОткрытьДиалог("Код не найден!", СтатусСообщения.Важное);
		Иначе
			ОткрытьДиалог("Код <" + Код + "> не найден!", СтатусСообщения.Важное);
		КонецЕсли;
		Возврат;
	ИначеЕсли ТипЗнч(СтруктураПоиска.Объект)=Тип("СправочникСсылка.Номенклатура") Тогда
		Если НЕ УстановленРежим("Чек") И
			НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
			Возврат ; 
		КонецЕсли; 
		
		Товар = СтруктураПоиска.Объект;
		ХарактеристикаНоменклатуры = Неопределено; ЕдиницаИзмерения = Неопределено;
		
		Попытка ХарактеристикаНоменклатуры = СтруктураПоиска.ХарактеристикаНоменклатуры; Исключение КонецПопытки;
		Попытка ЕдиницаИзмерения = СтруктураПоиска.ЕдиницаИзмерения; Исключение КонецПопытки;
		
		СтруктураМаркировки = Новый Структура("КодМаркировки,КодМаркировкиBase64");
		Если СтруктураПоиска.Свойство("КодМаркировки") Тогда
			ЗаполнитьЗначенияСвойств(СтруктураМаркировки, СтруктураПоиска);
		КонецЕсли;
		
		ДобавитьНоменклатуру(ЭтаФорма,Товар,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,,,СтруктураМаркировки);
		
	ИначеЕсли ТипЗнч(СтруктураПоиска.Объект)=Тип("СправочникСсылка.Карточки") Тогда
		Если (НЕ (УстановленРежим("Чек") ИЛИ УстановленРежим("Блокировка"))) И
			(НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата)) Тогда
			Возврат ; 
		КонецЕсли; 

		//В системе с данным штрихкодом зарегистрирована карточка
		Если СтруктураПоиска.Объект.ВидКарточки=Перечисления.ВидыКарточек.КарточкаПользователя Тогда
			Если УстановленРежим("Блокировка") Тогда
				Результат = ДействиеБлокировка(ЭтаФорма,СтруктураПоиска.Объект);
				ОбработкаРезультата(Результат);
			ИначеЕсли (НЕ РучнойВводКода) ИЛИ флРазрешитьВводКодаКартыПользователяВручную Тогда
				НовыйКассир=СтруктураПоиска.Объект.Объект;
				Если фкРаботаСФронтомКассира=Перечисления.РаботаСФронтомКассира.Запрещено Тогда
					ОткрытьДиалог("Вам запрещено работать с фронтом кассира", СтатусСообщения.Внимание);
				Иначе
					ТекущийКассир = Автор;
					// авторизуем нового пользователя
					Если Автор <> НовыйКассир Тогда
						Состояние("Считываются права нового пользователя");
						// попытаемся установить нового кассира
						Если ЗагрузитьПараметрыПользователя(НовыйКассир) Тогда
							// все параметры загружены, новый пользователь установлен
							ОткрытьДиалог("Установлен новый пользователь: "+НовыйКассир.Наименование, СтатусСообщения.Обычное);
						Иначе
							// вернем старого кассира
							ЗагрузитьПараметрыПользователя(ТекущийКассир);
						КонецЕсли;
						
						// Производим обновление отображения интерфейса пользователя
						Если ТипЗнч(ЭтаФорма)=Тип("Форма") Тогда
							ЭтаФорма.УправлениеИнтерфейсом();
						КонецЕсли;
					Иначе
						ОткрытьДиалог("Пользователь: "+НовыйКассир.Наименование+" уже установлен", СтатусСообщения.Внимание);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли СтруктураПоиска.Объект.ВидКарточки=Перечисления.ВидыКарточек.СлужебнаяОперация Тогда
			// Это карточка служебной операции.
			ВыполнитьОперацию(СтруктураПоиска.Объект.Объект, ЭтаФорма);
			
		Иначе
			//Для всех других карточек
			Если НЕ УстановленРежим("Чек") И
				НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
				Возврат ; 
			КонецЕсли; 
			
			Если СтруктураПоиска.Объект.ВидКарточки=Перечисления.ВидыКарточек.ДисконтнаяКарта Тогда
				//Если фкСпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидки ИЛИ
				//	 фкСпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.АвтоматическиеСкидки ИЛИ
				//	 фкСпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.АвтоматическиеИРучныеСкидки ИЛИ
				//	 фкСпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов Тогда
					 Если НЕ РучнойВводКода ИЛИ (РучнойВводКода И флРазрешитьРучнойВыборКарточки) Тогда
						 Карточка=СтруктураПоиска.Объект;
						 ОбработкаРеквизита("Карточка",,ЭтаФорма);
					 КонецЕсли;
				 //КонецЕсли;
			КонецЕсли;
			
		КонецЕсли; 
	
	Иначе
		// не умеем обрабатывать объекты других типов
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

// Внешние событие фронта
Процедура ВнешнееСобытиеФронта(Источник, Событие, Данные, ЭтаФорма, ДопПараметры=Неопределено ) Экспорт
	
	// проверяем номер события
	Если НЕ ЭтоНовоеСобытие(Событие) Тогда
		Возврат;
	КонецЕсли;
	
	// получаем дополнительные параметры
	
	Попытка ТекущаяСтрока = ДопПараметры.Получить(0).Значение;	Исключение	ТекущаяСтрока = Неопределено;	КонецПопытки;
	
	Если Источник = "Сканер" Тогда
		//защита от срабатывания в неактивных формах
		Если НЕ ЭтаФорма.ВводДоступен() Тогда 
			Возврат; 
		КонецЕсли;
		
		РаспознатьКод(ЭтаФорма, Данные);
	ИначеЕсли Источник="Hook" И Событие = "KeyPress" Тогда
		Данные = СтрЗаменить(Данные,"|",Символы.ПС);
		Если СтрЧислоСтрок(Данные) = 3 Тогда
			СкКод = Число(СтрПолучитьСтроку(Данные,1));
			СкСимвол = СтрПолучитьСтроку(Данные,2);
		    Стат = СокрЛП(СтрПолучитьСтроку(Данные,3));
		ИначеЕсли СтрЧислоСтрок(Данные) = 4 Тогда     // если сам символ "|"
			СкКод = Число(СтрПолучитьСтроку(Данные,1));
			СкСимвол = СтрПолучитьСтроку(Данные,3);
		    Стат = СокрЛП(СтрПолучитьСтроку(Данные,4));
		Иначе
			Возврат;
		КонецЕсли;
		
		ДопПараметры = Новый СписокЗначений();
		ДопПараметры.Добавить(ТекущаяСтрока, "ТекущаяСтрока");
		ОбработкаНажатойКлавиши(СкКод, Стат, СкСимвол, ЭтаФорма, ДопПараметры);
		
		Возврат;
	ИначеЕсли Источник="Hook" И Событие = "WM_SIZE" Тогда
		// выводится не будет, чтобы выводилось, надо сюда перенести из "ПриОткрытии" Формы, УстановитьРежим("Чек")
		Если УстановленРежим("НачалоРаботыФронта") Тогда
			// не наше окно
			Если Данные <> "Кассир" Тогда Возврат КонецЕсли;
			//Изменен размер окна
			ОткрытьДиалог("Фронт готов к работе", СтатусСообщения.Обычное, 1);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ВнешнееСобытие()

// Обработка нажатой клавиши
Процедура ОбработкаНажатойКлавиши(КодКлавиши, Состояние, СимволКлавиши, ЭтаФорма, ДопПараметры=Неопределено) Экспорт
			
	// получаем дополнительные параметры
	Попытка ТекущаяСтрока = ДопПараметры.Получить(0).Значение;	Исключение	ТекущаяСтрока = Неопределено;	КонецПопытки;
		
	Если (КодКлавиши >=65 И КодКлавиши <=90) ИЛИ                                 // А-Z
		 (КодКлавиши >=192 И КодКлавиши <=223) ИЛИ КодКлавиши = 184 ИЛИ  		 // А-Я
		 (КодКлавиши >=96 И КодКлавиши <=105) ИЛИ                          		 // <NumPad 0>-<NumPad 9>
		 (КодКлавиши >=48 И КодКлавиши <=57) ИЛИ                                 // 0 - 9
		  КодКлавиши = 32 ИЛИ                                                    // Space
		  КодКлавиши = 188 ИЛИ                                                   // "б" / ","
		  КодКлавиши = 190 ИЛИ                                                   // "ю" / "."
		  КодКлавиши = 191 ИЛИ                                                   // "." / "/"
		  КодКлавиши = 219 ИЛИ                                                   // "х" / "["
		  КодКлавиши = 221 ИЛИ                                                   // "ъ" / "]"
		  КодКлавиши = 186 ИЛИ                                                   // "ж" / ";"
		  КодКлавиши = 222 Тогда                                                 // "э" / "'"
				  
 
		Если Состояние = "000" Тогда
			Поле = Поле + СимволКлавиши;
		ИначеЕсли Состояние="010" Тогда												// CTRL    
			Если  КодКлавиши = 48 ИЛИ КодКлавиши = 96 Тогда                         // 0, <NumPad 0>
				Поле = Поле + "00"; 
			ИначеЕсли КодКлавиши = 70 Тогда
				
				// Временно снимем и установим перехват окна
				// Для поиска по ТЧ
				
				Рарус_Компонента.УдалитьКлавишу("Кассир",102); // f
				Рарус_Компонента.УдалитьКлавишу("Кассир",70);  // F
				Рарус_Компонента.УдалитьКлавишу("Кассир",192); // А
				Рарус_Компонента.УдалитьКлавишу("Кассир",224); // а
				
				ОбъектShell = Новый COMОбъект("Wscript.Shell");
				//Удерживая Ctrl нажать F
				ОбъектShell.SendKeys("^F");
				
				ДопПараметры.Добавить("ДобавитьКлавишиПерехвата");
				
			КонецЕсли;
		ИначеЕсли Состояние="100" Тогда												// SHIFT 
			Поле = Поле + СимволКлавиши;
		КонецЕсли;
	ИначеЕсли КодКлавиши = 110 Тогда 												// <NumPad .>
    	Если Состояние = "000" Тогда Поле = Поле + ","; КонецЕсли;
	ИначеЕсли  КодКлавиши = 107 ИЛИ КодКлавиши = 187 Тогда 							// <NumPad +>, +
		Если Состояние = "000" Тогда	ДействиеПлюс1(ЭтаФорма); КонецЕсли;
		Если Состояние = "100" Тогда	ДействиеПлюс1(ЭтаФорма); КонецЕсли;
	ИначеЕсли  КодКлавиши = 109 ИЛИ КодКлавиши = 189 Тогда 							// <NumPad ->, -	
		Если Состояние = "000" Тогда	ДействиеМинус1(ЭтаФорма); КонецЕсли; 
	ИначеЕсли  КодКлавиши = 8 Тогда 												// <Забой> 		
		Если Состояние = "000" Тогда	ДействиеЗабой(ЭтаФорма); КонецЕсли;	
	ИначеЕсли  КодКлавиши = 27 Тогда 												// <Esc>
		Если Состояние = "000" Тогда	ДействиеОчистить(ЭтаФорма); КонецЕсли;	
	ИначеЕсли  КодКлавиши = 36 Тогда 												// <HOME>
		// Не присвоено	
	ИначеЕсли  КодКлавиши = 45 Тогда 												// <Insert>
		Если Состояние = "000" Тогда	ДействиеВыбор(ЭтаФорма,ТекущаяСтрока); КонецЕсли;		
	ИначеЕсли  КодКлавиши = 46 Тогда 												// <Delete>
		Если Состояние = "000" Тогда	ДействиеСторно(ЭтаФорма); КонецЕсли;
	ИначеЕсли  КодКлавиши = 37 Тогда												// <LEFT Arrow>
		Если Состояние = "000" Тогда	
			Если УстановленРежим("Блокировка") Тогда
				ДействиеОчистить(ЭтаФорма);	
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли  КодКлавиши = 38 Тогда  												// <UP Arrow>
		Если Состояние = "000" Тогда	ДействиеВверх(ЭтаФорма); КонецЕсли;	
	ИначеЕсли  КодКлавиши = 39 Тогда 	 											// <RIGHT Arrow>
		// Не присвоено	
	ИначеЕсли  КодКлавиши = 40 Тогда 												// <DOWN Arrow>
		Если Состояние = "000" Тогда	ДействиеВниз(ЭтаФорма);	КонецЕсли;
	ИначеЕсли  КодКлавиши = 113 Тогда 												// <F2> 		
		Если Состояние = "000" Тогда	ДействиеКод(ЭтаФорма );	
		ИначеЕсли Состояние="100" Тогда	ДействиеВыбор(ЭтаФорма,ТекущаяСтрока); 		//ALT
		ИначеЕсли Состояние="101" Тогда ДействиеАртикул(ЭтаФорма );             	// SHIFT + ALT
		КонецЕсли;	
	ИначеЕсли  КодКлавиши = 116 Тогда 												// <F5>
		Если Состояние = "000" Тогда	ДействиеОплатаНаличными(ЭтаФорма);      
		ИначеЕсли Состояние="001" Тогда Действие0Чек(ЭтаФорма);         	     	//ALT
		ИначеЕсли Состояние="010" Тогда ДействиеБезнал(ЭтаФорма);       	     	// CTRL
		ИначеЕсли Состояние="011" Тогда ДействиеХОтчет(ЭтаФорма);        	 	 	// CTRL + ALT
		ИначеЕсли Состояние="100" Тогда ДействиеВнесение(ЭтаФорма);    				// SHIFT
		ИначеЕсли Состояние="101" Тогда ДействиеЯщик(ЭтаФорма);                  	// SHIFT + ALT
		ИначеЕсли Состояние="110" Тогда ДействиеИзъятие(ЭтаФорма);    			 	// SHIFT + CTRL
		ИначеЕсли Состояние="111" Тогда ДействиеZОтчет(ЭтаФорма);               	// SHIFT + CTRL + ALT
		КонецЕсли;	

	ИначеЕсли  КодКлавиши = 117 Тогда 												// <F6>
		Если Состояние = "000" Тогда	ДействиеКоличество(ЭтаФорма, ТекущаяСтрока);	
		ИначеЕсли Состояние="001" Тогда	                                        	 //ALT

			Если НадоЗавершитьРаботуСЧеком() Тогда
				Возврат;
			КонецЕсли;
			
			Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") Тогда
				ИзменитьРежим("ЧекНаОплату");	
				ЭтаФорма.УправлениеИнтерфейсом();
			ИначеЕсли УстановленРежим("ЧекНаОплату") Тогда
				ЗагрузитьЧек(Неопределено,ЭтаФорма);
				ИзменитьРежим("Чек");
				ЭтаФорма.УправлениеИнтерфейсом();
			КонецЕсли;
			
		ИначеЕсли Состояние="010" Тогда ДействиеПолучитьВес(ЭтаФорма);             	 	// CTRL
		ИначеЕсли Состояние="100" Тогда ДействиеСтатус(ЭтаФорма);                   	// SHIFT
		ИначеЕсли Состояние="110" Тогда ДействиеДокумент(ЭтаФорма);                 	// SHIFT + CTRL
		ИначеЕсли Состояние="101" Тогда ДействиеХарактеристика(ЭтаФорма);               // SHIFT + ALT
		ИначеЕсли Состояние="111" Тогда ДействиеКодыМаркировки(ЭтаФорма,,, Истина);     // SHIFT + CTRL + ALT
 		КонецЕсли;	
			
	ИначеЕсли  КодКлавиши = 118 Тогда 													// <F7>
		Если Состояние = "000" Тогда	ДействиеПробить(ЭтаФорма); 
		ИначеЕсли Состояние="010" Тогда ДействиеУправлениеКассой(ЭтаФорма);     		// CTRL
		ИначеЕсли Состояние="100" Тогда ДействиеПодИтог(ЭтаФорма); // SHIFT
		КонецЕсли;	
	
	ИначеЕсли  КодКлавиши = 119 Тогда 													// <F8>
		Если Состояние = "000" Тогда	ДействиеСторно(ЭтаФорма);	
		ИначеЕсли Состояние="010" Тогда Если НЕ ОтменитьЧек(ЭтаФорма) Тогда  Возврат; КонецЕсли; // CTRL
		ИначеЕсли Состояние="101" Тогда ДействиеВниз(ЭтаФорма);                        // SHIFT + ALT
		ИначеЕсли Состояние="110" Тогда ДействиеВверх(ЭтаФорма);                       // SHIFT + CTRL	
		КонецЕсли;	
	
	ИначеЕсли  КодКлавиши = 120 Тогда 													// <F9>
		Если Состояние = "000" Тогда	ДействиеСкидка(ЭтаФорма);
		ИначеЕсли Состояние="010" Тогда ДействиеДисконтнаяКарта(ЭтаФорма);				// CTRL
		ИначеЕсли Состояние="011" Тогда ДействиеВернуть(ЭтаФорма); 				 		// CTRL + ALT
		ИначеЕсли Состояние="100" Тогда ДействиеОтдел(ЭтаФорма);   				 		// SHIFT
		ИначеЕсли Состояние="101" Тогда ДействиеЗаполнить(ЭтаФорма); 			 		// SHIFT + ALT
		ИначеЕсли Состояние="110" Тогда ДействиеОтложить(ЭтаФорма);  			 		// SHIFT + CTRL	
		ИначеЕсли Состояние="111" Тогда ДействиеКасса(ЭтаФорма);     			 		// SHIFT + CTRL + ALT
		КонецЕсли;	                                                             	

	ИначеЕсли  КодКлавиши = 121 Тогда 													// <F10>
			
		Если Состояние="001" Тогда ДействиеТовЧек(ЭтаФорма);                    		//ALT
		ИначеЕсли Состояние="010" Тогда ДействиеВозврат(ЭтаФорма);    		  			// CTRL
		ИначеЕсли Состояние="011" Тогда ДействиеКопия(ЭтаФорма);       		 			// CTRL + ALT
		ИначеЕсли Состояние="111" Тогда ДействиеБонусы(ЭтаФорма);     			 		// SHIFT + CTRL + ALT
		КонецЕсли;	

	ИначеЕсли  КодКлавиши = 122 Тогда 													// <F11>
		Если Состояние = "000" Тогда	Результат = ДействиеВыход(ЭтаФорма);
		ИначеЕсли Состояние="010" Тогда Результат = ДействиеБлокировка(ЭтаФорма); ОбработкаРезультата(Результат); // CTRL
		КонецЕсли;	
	ИначеЕсли  КодКлавиши = 13 Тогда 													// <Enter>
		Если Состояние = "000" Тогда	
			Если ПустаяСтрока(Поле) Тогда Возврат; КонецЕсли;
			
			Если ( УстановленРежим("Чек")ИЛИ 
				     (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата)) Тогда

				//Попробуем подобрать по коду
				Номенклатура=Справочники.Номенклатура.НайтиПоКоду(Поле);
				Если Номенклатура.Пустая() Тогда
					//Попробуем подобрать по артикулу
					Если ДействиеАртикул(ЭтаФорма,Ложь)<>0 Тогда
						Возврат;
					КонецЕсли; 
				КонецЕсли; 
				
				// добавляем товар
				Если НЕ Номенклатура.Пустая() Тогда						
					ДобавитьНоменклатуру(ЭтаФорма,Номенклатура);
					Поле=""; 
					Возврат;
				КонецЕсли;
			
				//Попробуем подобрать дисконтную карту
				Если флРазрешитьРучнойВыборКарточки Тогда
					НоваяКарточка=Справочники.Карточки.НайтиПоКоду(СокрЛП(Поле));
					Если НоваяКарточка<>Справочники.Карточки.ПустаяСсылка() Тогда
						Если НоваяКарточка<>Карточка Тогда
							Карточка=НоваяКарточка; ОбработкаРеквизита("Карточка",,ЭтаФорма);
							Поле=""; Возврат;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;					
			КонецЕсли;
			
			//Попробуем подобрать номенклатуру по ШК
			РаспознатьКод(ЭтаФорма, СокрЛП(Поле), Истина);
			Поле="";
		КонецЕсли;	
		
	КонецЕсли;
					
	Возврат;
	
КонецПроцедуры

// Индикатор
Процедура ОбновитьИндикатор(ИмяИндикатора,ЭтаФорма, ТекущаяСтрока=Неопределено) Экспорт
	
	ИмяИндикатора = ВРег(ИмяИндикатора);	
	
	Если ИмяИндикатора =  ВРег("Товар") Тогда
		ИндикаторТовар =""; ИндикаторРасчетСтроки = ""; ИндикаторОтдел = "";
	    ИндикаторСуммаСкидкиНаПоз = ""; ИндикаторЗаголовокСкидкиНаПоз="";
				
		тТекущаяСтрокаДополнительно = "";
		стрНоменклатура="";стрЦена="";
        стрКоличество="";стрСуммаСкидки="";
		стрСумма = ""; стрТипСкидки = "Скидка";

		Если ТекущаяСтрока=Неопределено ИЛИ обЗначениеНеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда 
			// очистим информации с дисплея
			ВывестиЗаставку();
			Возврат; 
		КонецЕсли;

		стрНоменклатура=СокрЛП(ТекущаяСтрока.Номенклатура);
		стрХарактеристика=СокрЛП(ТекущаяСтрока.ХарактеристикаНоменклатуры);
		стрОтдел=СокрЛП(ТекущаяСтрока.МестоРазмещения);
		
		стрЦена=ФРМсумма(ТекущаяСтрока.Цена);
		
		ЕдИзм = ТекущаяСтрока.ЕдиницаИзмерения;
		Точность = ТекущаяСтрока.ЕдиницаИзмерения.Точность;
		Количество = ТекущаяСтрока.Количество*ТекущаяСтрока.Коэффициент;
		стрКоличество = ФРМколичество(Количество,?(Точность>3,3,Точность)); 
		стрКоличество= стрКоличество+СокрЛП(ТекущаяСтрока.ЕдиницаИзмерения);
		
		стрСуммаСкидкиСтроки = ФРМсумма(ТекущаяСтрока.СуммаСкидкиСтроки);
		Если ТекущаяСтрока.СуммаСкидкиСтроки<0 Тогда
			стрТипСкидки = "Надбавка";
		КонецЕсли; 
		
		Если ТекущаяСтрока.СуммаСкидки>=0 Тогда
			стрСуммаСкидки = ФРМсумма(ТекущаяСтрока.СуммаСкидки);
			стрЗнакСкидки = " + ";
		Иначе
			стрСуммаСкидки = ФРМсумма(-ТекущаяСтрока.СуммаСкидки);
			стрЗнакСкидки = " - ";
		КонецЕсли;
		
		стрСумма = ФРМсумма(ТекущаяСтрока.СуммаВсего);
		стрСуммаИтого = ФРМсумма(Товары.Итог("СуммаВсего"));
		стрСуммаСкидкиБонусами = ФРМсумма(ТекущаяСтрока.СуммаСкидкиБонусами);	
		ИндикаторТовар = стрНоменклатура+" / "+стрХарактеристика;
		Если ТекущаяСтрока.СебестоимостьАвтомобиля<>0 Тогда
			Если ВыделениеМежценовойРазницыОтдельнойСтрокой Тогда
				ИндикаторТовар=ИндикаторТовар+" (Межценовая разница отдельной строкой)";
			Иначе
				ИндикаторТовар=ИндикаторТовар+" (НДС расчитан от межценовой разницы)";
			КонецЕсли;
		КонецЕсли;
		ИндикаторРасчетСтроки = стрКоличество+
			" х "+
			стрЦена+
			" - "+
			"( " +стрСуммаСкидкиСтроки + стрЗнакСкидки + стрСуммаСкидки + " + " + стрСуммаСкидкиБонусами + " )"+
			" = "+
			стрСумма+
			" "+СокрЛП(ВалютаДокумента);
		ИндикаторСуммаСкидкиНаПоз = стрСуммаСкидки;	
		ИндикаторЗаголовокСкидкиНаПоз = стрТипСкидки + " на поз.:";	
		ИндикаторОтдел = стрОтдел;
		ДисплейСтрока1Начало=стрНоменклатура; 
		ДисплейСтрока1Конец = стрСумма;
		ДисплейСтрока2Начало="Итого:";
		ДисплейСтрока2Конец=стрСуммаИтого;		
		ВывестиНаДисплей(ДисплейСтрока1Начало,ДисплейСтрока1Конец,ДисплейСтрока2Начало,ДисплейСтрока2Конец,1);

		// Обновим индикатор сдачи
		ОбновитьИндикатор("Сдача", ЭтаФорма);
		// Обновим индикатор сдачи
		ОбновитьИндикатор("СкидкаНаЧек", ЭтаФорма);
	ИначеЕсли ИмяИндикатора =  ВРег("Получено") Тогда
		
		ПолученоВсего = ПолученоНал + ПолученоБезНал;
		стрПолучено = ФРМсумма(ПолученоВсего);
		
		// определим какой тип оплаты
		Если ПолученоНал*ПолученоБезНал > 0 Тогда
			ИндикаторПолученоЗаголовок="НАЛ/БЕЗНАЛ";
		ИначеЕсли ПолученоБезНал = 0 Тогда
			ИндикаторПолученоЗаголовок="НАЛ";
		ИначеЕсли ПолученоБезНал > 0 Тогда
			ИндикаторПолученоЗаголовок="БЕЗНАЛ";
		КонецЕсли;
		
		ИндикаторПолучено = стрПолучено;
		
		// Обновим индикатор сдачи
		ОбновитьИндикатор("Сдача", ЭтаФорма);
	ИначеЕсли ИмяИндикатора =  ВРег("Сдача") Тогда
		// посчитаем сдачу
		Сдача = ПолученоНал + ПолученоБезнал - СуммаДокумента;
		// проверим не больше ли сдача
		Если Сдача > ПолученоНал Тогда Сдача = ПолученоНал; КонецЕсли;
			
		// выведем доплату/оплату	
		Если Сдача<0 Тогда
			ИндикаторСдачаЗаголовок="ДОПЛАТА !";
			ИндикаторСдача=Формат(-Сдача,"ЧЦ=15; ЧДЦ=2; ЧН=0.00");
		Иначе
			ИндикаторСдачаЗаголовок="СДАЧА";
			ИндикаторСдача=Формат(Сдача,"ЧЦ=15; ЧДЦ=2; ЧН=0.00");
		КонецЕсли; 	
	ИначеЕсли ИмяИндикатора =  ВРег("СкидкаНаЧек") Тогда
		стрСуммаСкидки="";стрТипСкидки = "Скидка";
		
		СуммаСкидкиНаЧек = Товары.Итог("СуммаСкидки");
		СуммаСтрочныхСкидок = Товары.Итог("СуммаСкидкиСтроки");
		СуммаСкидкиБонусами = Товары.Итог("СуммаСкидкиБонусами");
        СуммаОбщейСкидки = СуммаСкидкиНаЧек+СуммаСтрочныхСкидок+СуммаСкидкиБонусами;
		
		Если СуммаОбщейСкидки<0 Тогда 
			стрСуммаОбщейСкидки = ФРМсумма(СуммаОбщейСкидки);
		Иначе
			стрСуммаОбщейСкидки = ФРМсумма(СуммаОбщейСкидки);
		КонецЕсли;
		
		стрСуммаСтрочныхСкидок = ФРМсумма(СуммаСтрочныхСкидок);

		Если СуммаСкидкиНаЧек>=0 Тогда
			стрСуммаСкидкиНаЧек = ФРМсумма(СуммаСкидкиНаЧек);
			стрЗнакСкидки = " + ";
		Иначе
			стрСуммаСкидкиНаЧек = ФРМсумма(-СуммаСкидкиНаЧек);
			стрЗнакСкидки = " - ";
		КонецЕсли;
		
		стрСуммаСкидкиБонусами = ФРМсумма(СуммаСкидкиБонусами);

		ИндикаторСуммаСкидкиНаЧек = стрСуммаСтрочныхСкидок + стрЗнакСкидки + стрСуммаСкидкиНаЧек + " + " + стрСуммаСкидкиБонусами 
									+ " = " +стрСуммаОбщейСкидки;	
		ИндикаторЗаголовокСкидкиНаЧек = стрТипСкидки + " на чек:"	
	КонецЕсли;
	
	// Вызовем обработчик формы
	ЭтаФорма.Индикация(ИмяИндикатора);
	
КонецПроцедуры

// Обновить индикаторы
Процедура ОбновитьИндикаторы(ЭтаФорма)
	ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
	ОбновитьИндикатор("Товар",ЭтаФорма,  ТекущаяСтрока);
	ОбновитьИндикатор("Получено",ЭтаФорма);
	ОбновитьИндикатор("СкидкаНаЧек",ЭтаФорма);
КонецПроцедуры

Функция ДоступностьПробитияЧекаКонтрагенту(Документ)
	
	//!!!_Пока убрали проверку для открытия фронта
	//Если НЕ Документ.ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки
	//	И дкДокументЕстьРеквизитШапки("Контрагент", Документ.Ссылка.Метаданные().Имя)
	//	И ЗначениеЗаполнено(Документ.Контрагент)
	//	И НЕ Документ.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
	//	ОткрытьДиалог("Пробитие чека запрещено. Контрагент <"+Документ.Контрагент+"> не является физ лицом!", СтатусСообщения.Важное);
	//	Возврат Ложь;
	//КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция РежимПередачиТовараСЗакрытиемОстаткаОплатыНаКредит(ДокументОплаты=Неопределено)
	
	Если НЕ ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
		
		Возврат Ложь;
		
	КонецЕсли;	

	//Если режим еще не установлен, проверяем по документу оплаты
	Если ДокументОплаты <> Неопределено Тогда
		
		Возврат ТипЗнч(ДокументОплаты)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
			ИЛИ ТипЗнч(ДокументОплаты) = Тип("ДокументСсылка.РасходныйКассовыйОрдер");
			
	Иначе
			
		Возврат УстановленРежим("ПриходныйКассовыйОрдер") 
			ИЛИ УстановленРежим("РасходныйКассовыйОрдер")	

	КонецЕсли;
		
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ДЛЯ РАБОТЫ С СНО

Функция ПолучитьКодНалогообложения(ОбъектНалогообложенияСсылка)
	Попытка
		ВидНалога=ОбъектНалогообложенияСсылка.ВидНалога;
	Исключение
		ВидНалога=Перечисления.ВидыНалогов.ПустаяСсылка();
	КонецПопытки;
	Попытка
		ОбъектНалогообложения=ОбъектНалогообложенияСсылка.ОбъектНалогообложения;
	Исключение
		ОбъектНалогообложения=Перечисления.ВидыОбъектовНалогообложения.ПустаяСсылка();	
	КонецПопытки;
	Попытка
		СистемаНалогообложения=ОбъектНалогообложенияСсылка.СистемаНалогообложения;
	Исключение
		СистемаНалогообложения=Перечисления.СистемыНалогообложения.ПустаяСсылка();	
	КонецПопытки; 
	
	Если ЗначениеЗаполнено(ВидНалога) Тогда
		Если ВидНалога=Перечисления.ВидыНалогов.ЕНВД Тогда
			Возврат 8;
		ИначеЕсли ВидНалога=Перечисления.ВидыНалогов.ЕСХН Тогда
			Возврат 16;
		ИначеЕсли ВидНалога=Перечисления.ВидыНалогов.ПСН Тогда
			Возврат 32;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ОбъектНалогообложения) Тогда
		Если ОбъектНалогообложения=Перечисления.ВидыОбъектовНалогообложения.Доходы Тогда
			Возврат 2;
		ИначеЕсли ОбъектНалогообложения=Перечисления.ВидыОбъектовНалогообложения.ДоходыМинусРасходы Тогда
			Возврат 4;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(СистемаНалогообложения) Тогда
		Если СистемаНалогообложения=Перечисления.СистемыНалогообложения.Общая Тогда
			Возврат 1;
		Иначе
			Возврат Неопределено;
		КонецЕсли; 
	КонецЕсли; 
КонецФункции

Функция ПолучитьКодНалогообложенияОрганизации(Организация)
	ЗначениеКодаНалогообложения=ПолучитьКодНалогообложения(Организация);
	Возврат ЗначениеКодаНалогообложения;	
КонецФункции

Функция ПолучитьКодНалогообложенияПодразделенияКомпании(Организация,Знач ПодразделениеКомпании)
	ЗначениеКодаНалогообложения=ПолучитьКодНалогообложения(ПодразделениеКомпании);
	Если ЗначениеКодаНалогообложения=Неопределено Тогда
		ПодразделениеКомпании=ПодразделениеКомпании.Родитель;
		Пока ЗначениеКодаНалогообложения=Неопределено И ЗначениеЗаполнено(ПодразделениеКомпании) Цикл
			ЗначениеКодаНалогообложения=ПолучитьКодНалогообложения(ПодразделениеКомпании);
			ПодразделениеКомпании=ПодразделениеКомпании.Родитель;
		КонецЦикла;
	КонецЕсли; 
	
	Если ЗначениеКодаНалогообложения=Неопределено Тогда
		ЗначениеКодаНалогообложения=ПолучитьКодНалогообложенияОрганизации(Организация);
	КонецЕсли;
	
	Возврат ЗначениеКодаНалогообложения;
КонецФункции
	
Функция ПолучитьКодНалогообложенияСкладыКомпании(Организация,ПодразделениеКомпании,СкладКомпании=Неопределено)
	
	Если ЗначениеЗаполнено(СкладКомпании) Тогда
		Возврат ПолучитьКодНалогообложенияПодразделенияКомпании(СкладКомпании.Организация,СкладКомпании.Подразделение);
	Иначе	
		Возврат ПолучитьКодНалогообложенияПодразделенияКомпании(Организация,ПодразделениеКомпании);
	КонецЕсли; 
	
КонецФункции // ПолучитьКодНалогообложенияСкладыКомпании()

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ДЛЯ РАБОТЫ С РЕЖИМАМИ

// возвращает последний режим
Функция ПоследнийРежим()
	
	// уберем точки вначале и вконце
	врРежим = Сред(Режим,2,СтрДлина(Режим)-2); 
	// заменим все точки на символы перехода строки
	врРежим = СтрЗаменить(врРежим,".",Символы.ПС); 
	// получим последний режим
	ПоследнийРежим = СтрПолучитьСтроку(врРежим, СтрЧислоСтрок(врРежим));
	
	Возврат ПоследнийРежим;
КонецФункции

// сохраняем текущий режим во временную переменную
Процедура СохранитьРежим() Экспорт
	ВременныйРежим = Режим;		
КонецПроцедуры

// восстанавливаем режим из временной переменной
Процедура ВосстановитьРежим() Экспорт
	Режим = ВременныйРежим;	
КонецПроцедуры

// проверяет является ли переданный режим текущим
Функция УстановленРежим(НовыйРежим) Экспорт
	Результат = Ложь;
	
	// проверим в этом ли режиме мы находимся
	ПоследнийРежим = ВРег(ПоследнийРежим());
	Если ПоследнийРежим = ВРег(НовыйРежим) Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Переходим в указанный режим (под режим)
Процедура ПерейтиВРежим(НовыйРежим) Экспорт
// Регламентированы следующие режимы:
// НачалоРаботыФронта
// ОкончаниеРаботыФронта
// Блокировка
// Чек
// ЧекНаВозврат
// ЧекНаОплату
// ЧекНаОплатуВозврат
// ПриходныйКассовыйОрдер
// РасходныйКассовыйОрдер
// Копия
// ..Сервис
// ..ВыборТовара

	Если Режим = Неопределено Тогда
		Режим = ".";
	КонецЕсли;

	НовыйРежим = ВРег(НовыйРежим);
	// проверим может мы уже находимся в этом режиме
	Если УстановленРежим(НовыйРежим) Тогда
		
	Иначе
		Режим = Режим  + НовыйРежим + ".";
	КонецЕсли;
		
КонецПроцедуры

// Устанавливает главный режим без всяких переходов
Процедура УстановитьРежим(НовыйРежим) Экспорт

	Режим = "." + ВРег(НовыйРежим) + ".";	
	ЗаписатьВФайлЛога("Установлен режим", ,НовыйРежим);
	
КонецПроцедуры

// Меняет текущий режим(под режим) на переданный
Процедура ИзменитьРежим(НовыйРежим) Экспорт
	
	// сначала выйдем из текущего режима
	ВыйтиИзРежима();
	// перейдем в нужный режим
	ПерейтиВРежим(НовыйРежим);
	
КонецПроцедуры

// Выходит из текущего режима
Процедура ВыйтиИзРежима(РежимДляВыхода=Неопределено) Экспорт
	
	// если не указан режим из которого надо выйти, то выйдем из последнего режима
	Если РежимДляВыхода = Неопределено Тогда
		ПоследнийРежим = ПоследнийРежим();
		Если НЕ ПустаяСтрока(ПоследнийРежим) Тогда
			ВыйтиИзРежима(ПоследнийРежим);
		КонецЕсли;
	Иначе
		РежимДляВыхода = ВРег(РежимДляВыхода);
		Поз = Найти(Режим,РежимДляВыхода);
		Если Поз>0 Тогда
			Режим = Лев(Режим,Поз-1); 
		Иначе
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ДЛЯ ОБРАБОТКИ РАЗЛИЧНЫХ "ДЕЙСТВИЙ"

// действие забой
Процедура ДействиеЗабой(ЭтаФорма) Экспорт
// действие поддерживается основной формой	
	
	Если УстановленРежим("Блокировка")ИЛИ
		 УстановленРежим("Чек")ИЛИ
		 УстановленРежим("ЧекНаВозврат")ИЛИ
		 УстановленРежим("ЧекНаОплату")ИЛИ
		 УстановленРежим("ЧекНаОплатуВозврат")ИЛИ
		 УстановленРежим("ПриходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("РасходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("БанковскаяВыписка") ИЛИ
		 УстановленРежим("Копия") Тогда
		 	
		Длина = СтрДлина(Поле);
		Если Длина > 0 Тогда
			Поле = Лев(Поле,Длина-1);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// действие очистить
Процедура ДействиеОчистить(ЭтаФорма) Экспорт
// действие поддерживается основной формой	

	Если УстановленРежим("Блокировка")ИЛИ
		 УстановленРежим("Чек")ИЛИ
		 УстановленРежим("ЧекНаВозврат")ИЛИ
		 УстановленРежим("ЧекНаОплату")ИЛИ
		 УстановленРежим("ЧекНаОплатуВозврат")ИЛИ
		 УстановленРежим("ПриходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("РасходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("БанковскаяВыписка") ИЛИ
		 УстановленРежим("Копия") Тогда

		Поле = "";
	КонецЕсли;
	
КонецПроцедуры

// действие пробить
Процедура ДействиеПробить(ЭтаФорма) Экспорт
	
	Если НЕ (УстановленРежим("Чек")ИЛИ
		 УстановленРежим("ЧекНаВозврат")ИЛИ
		 УстановленРежим("ЧекНаОплату")ИЛИ
		 УстановленРежим("ЧекНаОплатуВозврат")ИЛИ
		 УстановленРежим("ПриходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("РасходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("Копия") ИЛИ
		 УстановленРежим("ВозвратТоваровОтПокупателя") ИЛИ
		 УстановленРежим("РКО") ИЛИ
		 УстановленРежим("БанковскаяВыписка")) Тогда
		Возврат;
	КонецЕсли;

	Если Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПредоплата = Ложь;
	ЕстьОбычныйТовар = Ложь;
	Для Каждого СтрокаТоваров ИЗ Товары Цикл
		Если СтрокаТоваров.Номенклатура = Справочники.Номенклатура.Предоплата Тогда
			ЕстьПредоплата = Истина;
		Иначе
			ЕстьОбычныйТовар = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПредоплата И ЕстьОбычныйТовар И НЕ УстановленРежим("БанковскаяВыписка") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вместе с предоплатой нельзя указывать другие товары!!!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Пробитие чека отменено.",СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;

	Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") ИЛИ
		УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат") ИЛИ 
		УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер") ИЛИ
		УстановленРежим("БанковскаяВыписка") Тогда
		// Проверим, нужно ли вообще это пробивать
		Если обЗначениеНеЗаполнено(СуммаДокумента) И 
			(УстановленРежим("ЧекНаОплатуВозврат") ИЛИ УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ
			УстановленРежим("РасходныйКассовыйОрдер") ИЛИ УстановленРежим("БанковскаяВыписка")) Тогда
			ОткрытьДиалог("Сумма чека для пробития на Фискальном регистраторе нулевая! Пробитие чека прервано.",СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;
		
		СуммаОплаты=ПолученоНал+ПолученоБезнал;
		Если СуммаОплаты<СуммаДокумента Тогда
			Если флДоступностьНал И флПогашатьЧекЦеликом И НЕ УстановленРежим("БанковскаяВыписка") Тогда
				ДействиеОплатаНаличными(ЭтаФорма, СуммаДокумента-ПолученоБезНал);
			Иначе
				ОткрытьДиалог("Для пробития чека необходимо сначала оплатить его!",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли;
		КонецЕсли; 
		
		// проверим корректность оплат	
		Если НЕ ПроверитьОплаты() Тогда Возврат; КонецЕсли; 
		ОплатыПриОкончанииРедактирования(ЭтаФорма);
		
		// посчитаем сдачу и выведем на дисплей
		Сдача=ПолученоНал+ПолученоБезнал-СуммаДокумента;
		ДисплейИнформацияОСуммеЧека=Истина;
		ВывестиНаДисплей("Получено: ",ФРМсумма(ПолученоНал+ПолученоБезНал),?(Сдача>=0,"Сдача: ","Доплата: "),ФРМсумма(?(Сдача>0,Сдача,-Сдача)));
		
		//Установим пока фиктивные реквизиты чека
		НомерЧека=-1;
		ДатаФР=ТекущаяДата();
		НомерДокумента=-1;
		НомерСмены=-1;
		СообщениеОбОшибке="";
		
		// Производим первоначальную запись документа (выполняем проверку корректности заполнения реквизит документа)
		Если НЕ ЗаписатьЧек(ЛОЖЬ, СообщениеОбОшибке,,, ИСТИНА) Тогда
			ОткрытьДиалог("ПРИ ЗАПИСИ ЧЕКА ОБНАРУЖЕНЫ ОШИБКИ:"+Символы.ПС+СообщениеОбОшибке, СтатусСообщения.ОченьВажное, 0);
			Возврат;
		КонецЕсли;
		
		// Произведем заполнение свойств документа
		Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") Тогда
			// Выберем свойства чека , если это необходимо
			тзЗначенияСвойств = ПолучитьЗначениеСвойств(ЭтаФорма);
			Если тзЗначенияСвойств = Неопределено Тогда
				Рарус_Компонента.Диалог("ВЫБЕРИТЕ СВОЙСТВО ЧЕКА",1, 20); Возврат;
			КонецЕсли;
			
			// Производим запись свойств чека
			ЗаписатьЗначениеСвойств(тзЗначенияСвойств);
		КонецЕсли;
		
		// Сохраним ТЧ чека до внесения подарков для восстановления в случае ошибок
		ТоварыБезПодарков = Товары.Выгрузить();

		// Если есть скидки-подарки, то добавим их в табличную часть перед пробитием 
		Если НЕ ДобавитьСкидкиПодарки(ЭтаФорма) Тогда
			ОткрытьДиалог("НЕ ВЫБРАН ПОДАРОК", СтатусСообщения.ОченьВажное, 0);
			Товары.Загрузить(ТоварыБезПодарков);
			Возврат;
		КонецЕсли;
		
    	//ПРОБИТИЕ ЧЕКА
		//Если КодыМаркировки.Количество() = 0 Тогда
			СтруктураВозвратныхПараметров=Новый Структура;
			Если НЕ ПробитьЧек(СообщениеОбОшибке,СтруктураВозвратныхПараметров) Тогда
				// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ 15.07.2025 ++
				// Сохраняем в объекте флаг ошибки чтобы при автопробитии не выполнять дополнительные процедуры
				ЭтотОбъект.ОшибкаПробития = Истина;
				// БИЗТЕХ КОВАЛЬ АВТОПРОБИТИЕ 15.07.2025 --
				ОткрытьДиалог("ПРИ ПРОБИТИИ ЧЕКА ОБНАРУЖЕНЫ ОШИБКИ:"+Символы.ПС+СообщениеОбОшибке, СтатусСообщения.ОченьВажное, 0);
				Товары.Загрузить(ТоварыБезПодарков);
				Возврат;
			КонецЕсли; 
		//Иначе
			//СтруктураВозвратныхПараметров=Новый Структура;
			//ФормаПроверкиКодовМаркировки = ПолучитьФормуФронта("ФормаПроверкиКодовМаркировки", ЭтаФорма);
			//ФормаПроверкиКодовМаркировки.АвтоматическаяПроверкаКодовМаркировки = Истина;
			//ФормаПроверкиКодовМаркировки.АвтоматическоеПробитиеЧека = Истина;
			//РезультатПробитияЧека = ОткрытьФормуВыбора(ФормаПроверкиКодовМаркировки, , "ФормаПроверкиКодовМаркировки");
			//Если ТипЗнч(РезультатПробитияЧека) = Тип("Структура") Тогда 
			//	Если НЕ РезультатПробитияЧека.РезультатПробитияЧека Тогда 
			//		ОткрытьДиалог("ПРИ ПРОБИТИИ ЧЕКА ОБНАРУЖЕНЫ ОШИБКИ:"+Символы.ПС+СообщениеОбОшибке, СтатусСообщения.ОченьВажное, 0);
			//		Товары.Загрузить(ТоварыБезПодарков);
			//		Возврат;
			//	КонецЕсли;
			//Иначе 
			//	Возврат;
			//КонецЕсли;
		//КонецЕсли;
			
    	//Окончательно проводим чек
    	Если НЕ ЗаписатьЧек(Истина,СообщениеОбОшибке) Тогда
    		ЗаписьЖурналаРегистрации("ФронтКассира",УровеньЖурналаРегистрации.Ошибка,,,"Ошибка перезаписи чека "+СообщениеОбОшибке);
    		ОткрытьДиалог("ОШИБКА ПРИ ЗАПИСИ ЧЕКА:
    		|"+СообщениеОбОшибке+"
    		|ВНИМАНИЕ!!! ЗАПИШИТЕ ФИСКАЛЬНЫЕ РЕКВИЗИТЫ ПРОБИТОГО ЧЕКА:
    		|Внесите эти данные вручную в документ пробитого чека"+Ссылка,СтатусСообщения.ОченьВажное,0);
			
			Возврат;
		КонецЕсли;
		
	ИначеЕсли УстановленРежим("Копия") Тогда
    	//ПРОБИТИЕ ЧЕКА
    	Если НЕ ПробитьЧек(СообщениеОбОшибке,) Тогда
    		 Возврат;
		 КонецЕсли; 
		 
	ИначеЕсли УстановленРежим("ВозвратТоваровОтПокупателя") Тогда
		
		Если НЕ ЗаписатьЧек(Истина,СообщениеОбОшибке) Тогда
    		ЗаписьЖурналаРегистрации("ФронтКассира",УровеньЖурналаРегистрации.Ошибка,,,"Ошибка записи чека "+СообщениеОбОшибке);
    		ОткрытьДиалог("ОШИБКА ПРИ ЗАПИСИ ЧЕКА:
    		|"+СообщениеОбОшибке+"
    		|ВНИМАНИЕ!!! ЗАПИШИТЕ ФИСКАЛЬНЫЕ РЕКВИЗИТЫ ПРОБИТОГО ЧЕКА:
    		|Внесите эти данные вручную в документ пробитого чека"+Ссылка,СтатусСообщения.ОченьВажное,0);
			
			Возврат;
		КонецЕсли; 
		
		// Произведем заполнение свойств документа
		// Выберем свойства чека , если это необходимо
		тзЗначенияСвойств = ПолучитьЗначениеСвойств(ЭтаФорма);
		Если тзЗначенияСвойств <> Неопределено Тогда
			// Производим запись свойств чека
			ЗаписатьЗначениеСвойств(тзЗначенияСвойств);
		КонецЕсли;
		
		Если фкОформлятьРКОПриВозврате Тогда
			УстановитьРежим("РКО");
			ХозОперация = Справочники.ХозОперации.РасходныйКассовыйОрдер;
			ДействиеПробить(ЭтаФорма);
		КонецЕсли;
	ИначеЕсли УстановленРежим("РКО") Тогда

		Если фкОформлятьРКОПриВозврате Тогда
			
			Если НЕ ЗаписатьЧек(Истина,СообщениеОбОшибке) Тогда
				ЗаписьЖурналаРегистрации("ФронтКассира",УровеньЖурналаРегистрации.Ошибка,,,"Ошибка записи чека "+СообщениеОбОшибке);
				ОткрытьДиалог("ОШИБКА ПРИ ЗАПИСИ ЧЕКА:
				|"+СообщениеОбОшибке+"
				|ВНИМАНИЕ!!! ЗАПИШИТЕ ФИСКАЛЬНЫЕ РЕКВИЗИТЫ ПРОБИТОГО ЧЕКА:
				|Внесите эти данные вручную в документ пробитого чека"+Ссылка,СтатусСообщения.ОченьВажное,0);
				
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

	//БИЗТЕХ МАМОНОВ 19.12.2025 ++
	//Сохраняем последний пробитый документ
	ХранилищеОбщихНастроек.Сохранить("ПоследнийПробитыйЧек","Ссылка", Ссылка);
	//БИЗТЕХ МАМОНОВ 19.12.2025 --
    //Если все нормально, загрузим пустой чек
	GUID_ДисплейСтарый=GUID_Дисплей;
	GUID_Дисплей="";
	ЗагрузитьЧек(Неопределено,ЭтаФорма); 
    GUID_Дисплей=GUID_ДисплейСтарый;
	//ДисплейИнформацияОСуммеЧека=Истина;
	
	// БИЗТЕХ КОВАЛЬ 01.04.2025 ++
	// Выключить ККТ если успешно пробили чек
	Попытка
		Если НЕ ПустаяСтрока(GUID_ФР) Тогда
			ОписаниеОшибкиОборудования = "";
			Если глТорговоеОборудование.ВыключитьОборудование(GUID_ФР,ОписаниеОшибкиОборудования)<>0 Тогда
				Сообщить(ОписаниеОшибкиОборудования,СтатусСообщения.Важное);
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	// БИЗТЕХ КОВАЛЬ 31.03.2025 --
	
КонецПроцедуры

// действие оплата наличными
Процедура ДействиеОплатаНаличными(ЭтаФорма, Сумма=Неопределено) Экспорт
	
	Если НЕ (УстановленРежим("Чек")ИЛИ
		 УстановленРежим("ЧекНаВозврат")ИЛИ
		 УстановленРежим("ЧекНаОплату")ИЛИ
		 УстановленРежим("ЧекНаОплатуВозврат")ИЛИ
		 УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ
		 УстановленРежим("РасходныйКассовыйОрдер") ИЛИ
		 УстановленРежим("БанковскаяВыписка")) Тогда

		Возврат;
	КонецЕсли;

	Если Сумма = Неопределено Тогда
		// получим введенную сумму
		Если ПустаяСтрока(Поле) Тогда
			Сумма=СуммаДокумента - ПолученоБезНал;
		Иначе
			Попытка
				Сумма = Число(Поле);
			Исключение
				ОткрытьДиалог("Сумма наличными должна быть числом",СтатусСообщения.Внимание);
				Возврат;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Если (СуммаДокумента > 0) И (ПолученоБезНал >= СуммаДокумента) Тогда
		//т.к. весь чек уже оплачен безналом, нечего еще и наличными платить
		ОткрытьДиалог("Чек оплачен по безналичному расчету", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;	
	
	ТипОплаты = Перечисления.ТипыОплатыВРознице.Наличные;
	СтрПроверка = ПроверитьТипОплаты(ТипОплаты);
	Если СтрПроверка= Неопределено Тогда
	    ОткрытьДиалог("Оплата наличными не поддерживается !", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	СтрокаОплатыНаличными = ПолучитьСтрокуТипаОплаты(ТипОплаты);
	Если СтрокаОплатыНаличными = Неопределено Тогда
		СтрокаОплатыНаличными 			= Оплаты.Добавить();
		СтрокаОплатыНаличными.ТипОплаты = СтрПроверка.ТипОплаты;;
	КонецЕсли;
	
	СтрокаОплатыНаличными.Сумма 	= Сумма;
	Если Сумма <> 0 Тогда
		флДобавилиНал = Истина;
	Иначе
		флДобавилиНал = Ложь;
	КонецЕсли;
	
	ОплатыПриОкончанииРедактирования(ЭтаФорма);
	
	Поле = "";
		
КонецПроцедуры

// действие блокировка
Функция ДействиеБлокировка(ЭтаФорма, Карточка = Неопределено) Экспорт
	
	Если НЕ (УстановленРежим("Чек")ИЛИ
		 УстановленРежим("ЧекНаВозврат")ИЛИ
		 УстановленРежим("ЧекНаОплату")ИЛИ
		 УстановленРежим("ЧекНаОплатуВозврат")ИЛИ
		 УстановленРежим("ПриходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("РасходныйКассовыйОрдер")ИЛИ
		 УстановленРежим("Копия")ИЛИ
		 УстановленРежим("Блокировка") ИЛИ
		 УстановленРежим("БанковскаяВыписка")) Тогда
		 
		ТекстОшибки = "Команда вызвана из неправильного режима!";
		Возврат РезультатыФронта.Ошибка;
	КонецЕсли;

	Результат = РезультатыФронта.Ошибка;
	СохранитьРежим();
	
	// Если не передана карточка, тогда осуществим поиск карточки по введенному значению
	Если Карточка = Неопределено Тогда
		Карточка = Поле;
	КонецЕсли;
	
	// Блокировать фронт мы можем только из режима Продажи
	Если УстановленРежим("Блокировка") Тогда
		флБлокировать = Ложь;
	// другой режим
	Иначе
		флБлокировать = Истина;
	КонецЕсли;
	
	// Проверим в каком режиме мы находимся
	Если флБлокировать Тогда // блокировать
		// проверим не выбраны ли товары и не произведена ли безналичная оплата
		Если НадоЗавершитьРаботуСЧеком() Тогда
			Результат = РезультатыФронта.Предупреждение;
			ТекстОшибки = "";
		Иначе
			// переходим в режим блокировки
			Результат = РезультатыФронта.Ок;
			ТекстОшибки = "";
		КонецЕсли;
	ИначеЕсли НЕ флБлокировать Тогда // разблокировать
		// если передали строку то попытаемся ее распознать
		Если ТипЗнч(Карточка) = Тип("Строка") Тогда
			// строка пустая		
			Если ПустаяСтрока(Карточка) Тогда
				Результат = РезультатыФронта.Предупреждение;
				ТекстОшибки = "Введите код пользователя!";
			КонецЕсли; 
			// поищем карточку с переданным ШтрихКодом
			СтруктураПоиска = НайтиСтруктуруПоШтрихКоду(Карточка,Истина);
			Если СтруктураПоиска <> Неопределено Тогда
				Карточка = СтруктураПоиска.Объект;
				Если ТипЗнч(Карточка)=Тип("СправочникСсылка.Карточки") Тогда
					// Карточка найдена, теперь занова вызываем блокировку
					Результат = ДействиеБлокировка(ЭтаФорма,Карточка);
				Иначе  
					Результат = РезультатыФронта.Предупреждение;
					ТекстОшибки = "Код не найден!";
				КонецЕсли;
			Иначе
				Результат = РезультатыФронта.Предупреждение;
				ТекстОшибки = "Код не найден!";
			КонецЕсли;
		ИначеЕсли ТипЗнч(Карточка)=Тип("СправочникСсылка.Карточки") Тогда  // передали карточку
			// проверим, что за карточку нам передали
			Если Карточка.ВидКарточки=Перечисления.ВидыКарточек.КарточкаПользователя Тогда
				// проверим есть у нового пользователя права для работы с Фронтом
				НовыйКассир=Карточка.Объект;
				Если фкРаботаСФронтомКассира=Перечисления.РаботаСФронтомКассира.Запрещено Тогда
					Результат = РезультатыФронта.Предупреждение;
					ТекстОшибки = "Вам запрещено работать с фронтом кассира";
				Иначе
					ТекущийКассир = Автор;
					// авторизуем нового пользователя
					Если Автор <> НовыйКассир Тогда
						Состояние("Считываются права нового пользователя");
						// попытаемся установить нового кассира
						Если ЗагрузитьПараметрыПользователя(НовыйКассир) Тогда
							Результат = РезультатыФронта.Ок;
							ТекстОшибки = "Установлен новый пользователь: "+НовыйКассир.Наименование;
						Иначе
							ЗагрузитьПараметрыПользователя(ТекущийКассир);
							Результат = РезультатыФронта.Ошибка;
							ТекстОшибки = "Ошибка при загрузке прав пользователя: "+НовыйКассир.Наименование;
						КонецЕсли;
					Иначе
						Результат = РезультатыФронта.Ок;
						ТекстОшибки = "";
					КонецЕсли;
				КонецЕсли;
			Иначе
				Результат = РезультатыФронта.Предупреждение;
				ТекстОшибки = "Ожидается код карточки пользователя!";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат = РезультатыФронта.Ок Тогда
		Если флБлокировать Тогда
			ИзменитьРежим("Блокировка");
		Иначе
			ИзменитьРежим("Чек");
			ВывестиЗаставку();
		КонецЕсли;
		ЭтаФорма.УправлениеИнтерфейсом();
		Поле = "";
	Иначе
		ВосстановитьРежим();
	КонецЕсли;
	
	ЗаписатьВФайлЛога(?(флБлокировать,"Блокировка", "Разблокировка"), Число(Результат), ТекстОшибки);
	
	Возврат Результат;
	
КонецФункции

// действие выход
Функция ДействиеВыход(ЭтаФорма) Экспорт
	
	Результат = РезультатыФронта.Ошибка;
	
	Если НЕ (УстановленРежим("Чек")ИЛИ
		УстановленРежим("ЧекНаВозврат")ИЛИ
		УстановленРежим("ЧекНаОплату")ИЛИ
		УстановленРежим("ЧекНаОплатуВозврат")ИЛИ
		УстановленРежим("ПриходныйКассовыйОрдер")ИЛИ
		УстановленРежим("РасходныйКассовыйОрдер")ИЛИ
		УстановленРежим("Копия") ИЛИ
		УстановленРежим("ВозвратТоваровОтПокупателя") ИЛИ
		УстановленРежим("БанковскаяВыписка")) Тогда
		
		Возврат Результат;
	КонецЕсли;
	
	СохранитьРежим();
		
	Если ОтменитьЧек(ЭтаФорма) Тогда  
		
		// Установлен новый режим, сохраним его
		СохранитьРежим();
		
		// Переходим в новый режим
		УстановитьРежим("ОкончаниеРаботыФронта");

		Если фкРаботаСФронтомКассира=Перечисления.РаботаСФронтомКассира.ТолькоСФронтом Тогда
			ЗавершитьРаботуСистемы(ЛОЖЬ);
		КонецЕсли;
		
		Результат = РезультатыФронта.Ок; 
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
	Если Результат <> РезультатыФронта.Ок Тогда
		ВосстановитьРежим();
		Результат = РезультатыФронта.Ок;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// действие возврат
Процедура ДействиеВозврат(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаОплату") Тогда  
	
		Если НадоЗавершитьРаботуСЧеком() Тогда
			Возврат;
		КонецЕсли;
		
		Если УстановленРежим("ЧекНаОплату") Тогда
			// Сформируем структуру параметров отбора
			ПараметрыОтбора = Новый Структура("ВидДокумента", Метаданные.Документы.ЧекНаОплату);
			СписокДокументовВозврата = Новый СписокЗначений();
			СписокДокументовВозврата.Добавить(Справочники.ХозОперации.ЧекНаОплатуВозврат);
			СписокДокументовВозврата.Добавить(Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат);
			ПараметрыОтбора.Вставить("Операция",Новый Структура("Значение,ВидСравнения", СписокДокументовВозврата, ВидСравнения.НеВСписке));
			ПараметрыОтбора.Вставить("ДатаФР",	Новый Структура("Значение,ВидСравнения", '00010101', ВидСравнения.НеРавно));
			ПараметрыОткрытия = Новый Структура("НачальноеЗначениеВыбора, Отбор, Заголовок", Ссылка, ПараметрыОтбора, "Выбор документа на оплату для возврата");
			
			// Открываем форму выбора
			ФормаВыбораФронта = ПолучитьФормуФронта("ВыборДокументаНаОплату", ЭтаФорма,, ПараметрыОткрытия);
			ОткрытьФормуВыбора(ФормаВыбораФронта, ПараметрыОткрытия, "ВыборДокументаНаОплатуВозврат");
		Иначе
			ФормаВыбораВозврата= ПолучитьФормуФронта("ВыборЧекаПоПараметрам", ЭтаФорма);
			ОткрытьФормуВыбора(ФормаВыбораВозврата,,"ВыборЧекаВозврат");
		КонецЕсли;
	КонецЕсли;
	
	Возврат;

КонецПроцедуры

// действие отложить
Функция ДействиеОтложить(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	Если УстановленРежим("Копия") Тогда
		ЗагрузитьЧек(Неопределено,ЭтаФорма);

	Иначе
		Если Товары.Количество()=0 Тогда
			ОткрытьДиалог("ЧЕК ПУСТ!", СтатусСообщения.Внимание);
			Возврат 0;
		КонецЕсли;
		
		Если УстановленРежим("ЧекНаВозврат") И ДокументОснование.Пустая() Тогда
			ОткрытьДиалог("Нельзя откладывать чеки на возврат, введенные вручную!",СтатусСообщения.Информация);
			Возврат 0;
		КонецЕсли; 
		
		Если тзПлатежиЗаУслуги.Количество()<>0 Тогда
			ОткрытьДиалог("В чеки присутствуют сотовые платежи! Отложить чек невозможно!", СтатусСообщения.Внимание);
			Возврат 0;
		КонецЕсли;

		ХозОперация=Справочники.ХозОперации.ЧекОтложенный;
		ОбработкаРеквизита("ХозОперация",,ЭтаФорма);
		СообщениеОбОшибке="";
		
		// Перед тем как отложить чек и записать его, отменим чек с нулевым гуидом
		Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") Тогда
			Если ТипЗнч(АрхивЧековВосстановить())=Тип("ДокументОбъект.Чек") Тогда
				// Производим сохранение редактируемого чека со статусом "Отмена"
				Попытка
					Объект = Ссылка.ПолучитьОбъект();
				Исключение
					Объект = Документы.Чек.СоздатьДокумент();
				КонецПопытки;
				ЗаполнитьЗначенияСвойств(Объект, ЭтотОбъект);
				Объект.Товары.Загрузить(Товары.Выгрузить());
				Объект.Оплаты.Загрузить(Оплаты.Выгрузить());
				АрхивЧековСохранить(Объект, 3);
			КонецЕсли;
		КонецЕсли;

		Если ЗаписатьЧек(Ложь,СообщениеОбОшибке)=0 Тогда
			ОткрытьДиалог("НЕВОЗМОЖНО ЗАПИСАТЬ ОТЛОЖЕННЫЙ ЧЕК:"+Символы.ПС+СообщениеОбОшибке, СтатусСообщения.ОченьВажное);
			ЗаписатьВФайлЛога("Отложить чек",1,"НЕВОЗМОЖНО ЗАПИСАТЬ ОТЛОЖЕННЫЙ ЧЕК: "+СообщениеОбОшибке,,1);
			Возврат 0;
		Иначе
			Если ПолученоБезнал>0 Тогда
				ОткрытьДиалог("Не забудьте сохранить чек безналичной оплаты!",СтатусСообщения.Информация);
			КонецЕсли;
			ЗаписатьВФайлЛога("Отложить чек",,"Чек отложен: "+Ссылка);
			ЗагрузитьЧек(Неопределено,ЭтаФорма); 
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// действие вернуть
Процедура ДействиеВернуть(ЭтаФорма) Экспорт
		
	Если НадоЗавершитьРаботуСЧеком() Тогда
		Возврат;	
	КонецЕсли;
	
	Если УстановленРежим("Чек") Тогда
		// Сформируем структуру параметров отбора
		ПараметрыОтбора = Новый Структура("ПометкаУдаления,ХозОперация,КассаККМ", ЛОЖЬ, Справочники.ХозОперации.ЧекОтложенный, КассаККМ);
		ПараметрыОтбора.Вставить("Дата", Новый Структура("ВидСравнения,ЗначениеС,ЗначениеПо", ВидСравнения.Интервал, НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата())));
		ПараметрыОткрытия = Новый Структура("НачальноеЗначениеВыбора,Отбор", Карточка, ПараметрыОтбора);
		
		// Открываем форму выбора
		ФормаВыбора = ПолучитьФормуФронта("ВыборЧека", ЭтаФорма,, ПараметрыОткрытия);
		ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборЧека");
	КонецЕсли;
		
КонецПроцедуры

// действие копия
Процедура ДействиеКопия(ЭтаФорма) Экспорт
	
	// проверяем необходимость закрытия текущего чека без проверки чека копии
	Если НадоЗавершитьРаботуСЧеком(Ложь) Тогда  Возврат; КонецЕсли;
	
	Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаОплату")
		ИЛИ УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
		ИЛИ УстановленРежим("БанковскаяВыписка") Тогда
		ФормаВыбораВозврата=ПолучитьФормуФронта("ВыборЧекаПоПараметрам", ЭтаФорма);
		ОткрытьФормуВыбора(ФормаВыбораВозврата,,"КопияЧека");
	КонецЕсли;
КонецПроцедуры

// действие товарный чек
Процедура ДействиеТовЧек(ЭтаФорма) Экспорт
		
	Если НЕ УстановленРежим("Чек") Тогда Возврат; КонецЕсли;
	
	ТоварныйЧекДляПечати = Неопределено;
	// Если товаров нет, то предложим выбрать уже пробитый чек для печати товарного. Иначе пробьем текущий.
	Если Товары.Количество() = 0 Тогда 
		
		// Предложим выбрать чек.
		ФормаВыбораЧека = ПолучитьФормуФронта("ВыборЧекаПоПараметрам", ЭтаФорма);
		ОткрытьФормуВыбора(ФормаВыбораЧека,,"ВыборТоварногоЧека");
		
		// Посмотрим выбран ли чек.
		Если НЕ ЗначениеЗаполнено(ТоварныйЧекДляПечати) Тогда Возврат; КонецЕсли;
		
		// Подготовим параметры для дальнейшей обработки.
		НомерЧека      = ТоварныйЧекДляПечати.НомерЧека;
		ДатаДокумента  = ТоварныйЧекДляПечати.Дата;
		ДатаФР         = ТоварныйЧекДляПечати.ДатаФР;
		НомерДокумента = ТоварныйЧекДляПечати.НомерДокумента;
		НомерСмены     = ТоварныйЧекДляПечати.НомерСмены;
		НомерОбщий     = ТоварныйЧекДляПечати.Номер;
		ТЧТовары       = ТоварныйЧекДляПечати.Товары;
		
	Иначе
		// Выполняем проверку прав пользователя. Можно ли ему печатать непробитый чек?
		Если НЕ фкПечатьНепробитыхЧеков Тогда
			ОткрытьДиалог("Нет прав на печать товарного чека!", СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;
		
		// Подготовим параметры для дальнейшей обработки.
		ДатаДокумента  = Дата;
		НомерОбщий     = "";
		НомерЧека      = 0;
		ДатаФР         = 0;
		НомерДокумента = 0;
		НомерСмены     = 0;
		ТЧТовары       = Товары;
	КонецЕсли;
	
	// Печатаем.
	Если НЕ (ПустаяСтрока(GUID_ФР) И ПустаяСтрока(GUID_ПЧ))Тогда
		
		//У нас установлен принтер чеков или ФР - будем на него печатать
		ТоварныйЧекШапка=Новый ТекстовыйДокумент;
		МакетТоварногоЧека=ЭтотОбъект.ПолучитьМакет("ТоварныйЧек");
		//Формирование шапки товарного чека
		ОбластьТоварногоЧека=МакетТоварногоЧека.ПолучитьОбласть("Шапка");
		// Печатаем реквизиты только для пробитых чеков
		Если ЗначениеЗаполнено(ТоварныйЧекДляПечати) Тогда
        	ОбластьТоварногоЧека.Параметры.НомерЧека=НомерОбщий;
			Если обЗначениеНеЗаполнено(ДатаДокумента) Тогда
				ОбластьТоварногоЧека.Параметры.ДатаЧека=Формат(РабочаяДата,"ДФ=dd.MM.yyyy");
			Иначе
				ОбластьТоварногоЧека.Параметры.ДатаЧека=Формат(ДатаДокумента,"ДФ=dd.MM.yyyy");
			КонецЕсли; 
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Организация) Тогда
			ОбластьТоварногоЧека.Параметры.Поставщик=спПолучитьНаименование(ПараметрыСеанса.Организация);
		Иначе
			ОбластьТоварногоЧека.Параметры.Поставщик=спПолучитьНаименование(Организация);
		КонецЕсли; 
		// Печатаем фискальные реквизиты только для пробитых чеков
		Если ЗначениеЗаполнено(ТоварныйЧекДляПечати) Тогда
			ОбластьТоварногоЧека.Параметры.ФРНомер1 = Формат(НомерЧека,"ЧЦ=6; ЧДЦ=0; ЧН=0; ЧГ=");
			ОбластьТоварногоЧека.Параметры.ФРДата   = Формат(ДатаФР,"ДФ='dd.MM.yy ЧЧ:мм:сс'");
			ОбластьТоварногоЧека.Параметры.ФРНомер2 = Формат(НомерДокумента,"ЧЦ=10; ЧДЦ=0; ЧН=0; ЧГ=");
			ОбластьТоварногоЧека.Параметры.ФРСмена  = Формат(НомерСмены,"ЧЦ=6; ЧДЦ=0; ЧН=0; ЧГ=");
		КонецЕсли;	
		ТоварныйЧекШапка.Вывести(ОбластьТоварногоЧека);
		ТоварныйЧекСтроки=Новый ТекстовыйДокумент;
		//Формирование строк товарного чека
		Для Каждого СтрокаТоваров Из ТЧТовары Цикл
			ОбластьТоварногоЧека=МакетТоварногоЧека.ПолучитьОбласть("СтрокаТоваров");
			ОбластьТоварногоЧека.Параметры.НомерСтроки=Формат(СтрокаТоваров.НомерСтроки,"ЧЦ=2; ЧДЦ=0; ЧН=0; ЧГ=");
			ОбластьТоварногоЧека.Параметры.Товар=СтрокаТоваров.Номенклатура.НаименованиеПолное;
			ОбластьТоварногоЧека.Параметры.Количество=Формат(СтрокаТоваров.Количество,"ЧЦ=10; ЧДЦ=2; ЧН=0; ЧГ=");
			ОбластьТоварногоЧека.Параметры.Цена=Формат(СтрокаТоваров.Цена,"ЧЦ=10; ЧДЦ=2; ЧН=0; ЧГ=");
			ОбластьТоварногоЧека.Параметры.Сумма=Формат(СтрокаТоваров.СуммаВсего,"ЧЦ=10; ЧДЦ=2; ЧН=0; ЧГ=");
			ОбластьТоварногоЧека.Параметры.ТоварКод=СтрокаТоваров.Номенклатура.Код;
			ОбластьТоварногоЧека.Параметры.ТоварАрт=СтрокаТоваров.Номенклатура.Артикул;
			ТоварныйЧекСтроки.Вывести(ОбластьТоварногоЧека);
		КонецЦикла; 
		//Формирование подвала товарного чека
		ТоварныйЧекПодвал=Новый ТекстовыйДокумент;
		ОбластьТоварногоЧека=МакетТоварногоЧека.ПолучитьОбласть("Подвал");
		ОбластьТоварногоЧека.Параметры.СуммаВсего=Формат(ТЧТовары.Итог("СуммаВсего"),"ЧЦ=10; ЧДЦ=2; ЧН=0; ЧГ=");
		ТоварныйЧекПодвал.Вывести(ОбластьТоварногоЧека);
		//Вывод чека на принтер чеков
		SafeArrayСтрокиЧека=Рарус_Компонента.СоздатьПараметры(4,1);
		SafeArrayСтрокиЧека.SetValue(0,0,ТоварныйЧекШапка.ПолучитьТекст());
		SafeArrayСтрокиЧека.SetValue(1,0,ТоварныйЧекСтроки.ПолучитьТекст());
		SafeArrayСтрокиЧека.SetValue(2,0,ТоварныйЧекПодвал.ПолучитьТекст());
		SafeArrayСтрокиЧека.SetValue(3,0,0);
		//Пробитие строки товарного чека
		Если GUID_ПЧ = "" Тогда
			КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"Напечатать",SafeArrayСтрокиЧека,ТаймаутФР);
		Иначе
			КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ПЧ,"Напечатать",SafeArrayСтрокиЧека,ТаймаутПЧ);
		КонецЕсли;
		
		Если НЕ РезультатРаботыСОборудованием("Принтер чеков",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
			//ОткрытьДиалог("Ошибка при печати товарного чека: "+Рарус_Компонента.ОписаниеОшибки, СтатусСообщения.ОченьВажное);
		КонецЕсли;
	Иначе
		// Определим что надо отправить как данные для печати.
		ПередаваемыйОбъект = ?(ТоварныйЧекДляПечати = Неопределено, ЭтотОбъект, ТоварныйЧекДляПечати);
		//А если принтера чеков нет, то будем печатать на принтер Windows
		Документ = Новый ТабличныйДокумент();
		НовыйДокумент=Документы.Чек.СоздатьДокумент();
		НовыйДокумент.ПечатьЧек(Документ, ПередаваемыйОбъект);
		// Отобразить печатный документ на экране
		Документ.ОтображатьЗаголовки = 	Ложь;
		Документ.ОтображатьСетку =		Ложь;
		Документ.Защита = 				фкЗащитаПечатныхФорм;
		Документ.ТолькоПросмотр = 		фкОткрытиеПечатныхФормДокументовВРежимеПросмотра;
		Документ.Напечатать();
	КонецЕсли;
	
КонецПроцедуры

// действие сторно
Процедура ДействиеСторно(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли; 
	Если УстановленРежим("Копия") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("Сервис") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("ЧекНаОплату") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("ПриходныйКассовыйОрдер") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("РасходныйКассовыйОрдер") Тогда  Возврат; КонецЕсли;
	//Если УстановленРежим("ЧекНаОплатуВозврат") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("БанковскаяВыписка") Тогда Возврат; КонецЕсли;
	Если НЕ обПраво("РазрешитьСторнированиеТовара",Права) Тогда  Возврат; КонецЕсли;
	
	ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
	Если обЗначениеНеЗаполнено(ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	Если (ТекущаяСтрока.Платеж) И (НЕ фкУдалятьПлатежи) Тогда
		ОткрытьДиалог("НЕЛЬЗЯ УДАЛЯТЬ ПЛАТЕЖИ СОТОВЫМ ОПЕРАТОРАМ !", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;	
	
	Если НЕ ОткрытьДиалогВопроса("Удалить номенклатуру <"+ТекущаяСтрока.Номенклатура+"> из чека?", ЛОЖЬ) Тогда
		Возврат;
	КонецЕсли; 

	// проверим входит ли удаляемая строка в набор
	Если НЕ обЗначениеНеЗаполнено(ТекущаяСтрока.Набор) Тогда
		ИмяТовара = СокрЛП(ТекущаяСтрока.Номенклатура);
		ИмяНабора = СокрЛП(ТекущаяСтрока.Набор);
		Если НЕ ОткрытьДиалогВопроса("Товар: " + ИмяТовара + " входит в набор: " + ИмяНабора +" !" +Символы.ПС+
					  				 "Удалить весь набор?", ЛОЖЬ) Тогда
			Возврат;
		КонецЕсли;
	    Набор = ТекущаяСтрока.Набор;
		УдалитьНоменклатуру(ЭтаФорма, Набор,,,0);
	Иначе		
		Количество = ТекущаяСтрока.Количество;
		Характеристика = ТекущаяСтрока.ХарактеристикаНоменклатуры;
		ЕдИзмерения = ТекущаяСтрока.ЕдиницаИзмерения;
		Товар = ТекущаяСтрока.Номенклатура;
		Платеж = ТекущаяСтрока.Платеж;
		НомерПлатежа = ТекущаяСтрока.НомерПлатежа;
				
		УдалитьНоменклатуру(ЭтаФорма, Товар, Характеристика, ЕдИзмерения, Количество);
		
		Если Платеж Тогда
			УдалитьПлатеж(НомерПлатежа);
		КонецЕсли;
		
	КонецЕсли;

	Если НЕ ЧекОткрыт() Тогда
		// Проверим существование чека со статусом "Редактирование"
		Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") Тогда
			Если ТипЗнч(АрхивЧековВосстановить())=Тип("ДокументОбъект.Чек") Тогда
				// Производим сохранение редактируемого чека со статусом "Отмена"
				Попытка
					Объект = Ссылка.ПолучитьОбъект();
				Исключение
					Объект = Документы.Чек.СоздатьДокумент();
				КонецПопытки;
				ЗаполнитьЗначенияСвойств(Объект, ЭтотОбъект);
				Объект.Товары.Загрузить(Товары.Выгрузить());
				Объект.Оплаты.Загрузить(Оплаты.Выгрузить());
				АрхивЧековСохранить(Объект, 3);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПеренумероватьТаблицуОплат();

КонецПроцедуры

// действие заполнить
Функция ДействиеЗаполнить(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли; 
	Если УстановленРежим("Копия") Тогда  Возврат 0; КонецЕсли;
	
	Если ТСД=Неопределено Тогда
		ОткрытьДиалог("Устройство <"+ТСД+"> не подключено", СтатусСообщения.ОченьВажное);
		
		Возврат 0;
	ИначеЕсли ТСД.КлассОборудования<>Перечисления.КлассыОборудования.ТСД Тогда
		ОткрытьДиалог("Устройство <"+ТСД+"> не является терминалом сбора данных", СтатусСообщения.ОченьВажное);
		
		Возврат 0;
	КонецЕсли;

	// Выгрузим данные из ТСД
	// устанавливаем параметры
	ОчищатьТерминал = 0;
	Параметры=Рарус_Компонента.СоздатьПараметры(1,1);
	Параметры.SetValue(0,0,ОчищатьТерминал);

	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ТСД,"ПрочитатьДанные",Параметры,ТаймаутТСД);
	Если НЕ РезультатРаботыСОборудованием("Терминал сбора данных",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
		Возврат 0;
	КонецЕсли;	
	
	// получим параметры
	Попытка
		КоличествоСтрок=0; КоличествоСтолбцов=0;
		SafeArrayТаблицаТоваров=Параметры.GetValue(0,0);
		SafeArrayТаблицаТоваров.GetBounds(КоличествоСтрок,КоличествоСтолбцов);
	Исключение
		Предупреждение("Получена неверная структура данных их устройства",30);
	КонецПопытки;
	
	// заполним таблицу данных
	СписокТоваров=Новый СписокЗначений();
	Для Индекс=0 По КоличествоСтрок-1 Цикл
		// получим данные
		Ячейка		= SafeArrayТаблицаТоваров.GetValue(Индекс,0);
		ШтрихКод	= SafeArrayТаблицаТоваров.GetValue(Индекс,1);
		НомерСтроки	= SafeArrayТаблицаТоваров.GetValue(Индекс,3);
		Количество	= SafeArrayТаблицаТоваров.GetValue(Индекс,4);
		Документ	= SafeArrayТаблицаТоваров.GetValue(Индекс,5);
		
		// проверим данные
		ШтрихКод = СокрЛП(Строка(ШтрихКод));
		Если ПустаяСтрока(ШтрихКод) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Количество <> Неопределено Тогда
			Попытка
				Количество = Число(Количество);
			Исключение
				Количество = 0;
			КонецПопытки;
		Иначе
			Количество = 0;
		КонецЕсли;
		
		// пробуем найти товар по ШК и добавить в ТЧ
		СтруктураПоиска = НайтиСтруктуруПоШтрихКоду(ШтрихКод,Истина);
		Если  обЗначениеНеЗаполнено(СтруктураПоиска) Тогда
			СписокТоваров.Добавить(Строка(ШтрихКод),Количество);
		ИначеЕсли ТипЗнч(СтруктураПоиска.Объект)=Тип("СправочникСсылка.Номенклатура") Тогда			
			Товар = СтруктураПоиска.Объект;
			ХарактеристикаНоменклатуры = Неопределено; ЕдиницаИзмерения = Неопределено;
			
			Попытка ХарактеристикаНоменклатуры = СтруктураПоиска.ХарактеристикаНоменклатуры; Исключение КонецПопытки;
			Попытка ЕдиницаИзмерения = СтруктураПоиска.ЕдиницаИзмерения; Исключение КонецПопытки;
			
			СтруктураМаркировки = Новый Структура("КодМаркировки");
			Если СтруктураПоиска.Свойство("КодМаркировки") Тогда
				ЗаполнитьЗначенияСвойств(СтруктураМаркировки, СтруктураПоиска);
			КонецЕсли;
			
			ДобавитьНоменклатуру(ЭтаФорма,Товар,ХарактеристикаНоменклатуры,ЕдиницаИзмерения, Количество,, СтруктураМаркировки);
		КонецЕсли;

	КонецЦикла;
		
	 // определим что не нашли
	 Стр="В базе не найдены товары с штрих-кодами: ";
	 Для Сч=0 По СписокТоваров.Количество()-1 Цикл
		 Стр=Стр+Символы.ВК+Символы.ПС+Строка(СписокТоваров.Получить(Сч).Значение);
	 КонецЦикла;
	 
	 ОткрытьДиалог(Стр, СтатусСообщения.Важное);

	
КонецФункции

// действие дисконтная карта
Процедура ДействиеДисконтнаяКарта(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли; 
	Если УстановленРежим("Копия") Тогда  Возврат; КонецЕсли;
	Если фкСпособВыбораСкидки<>Перечисления.СкидкиСпособыВыбора.РучныеСкидки И
		 фкСпособВыбораСкидки<>Перечисления.СкидкиСпособыВыбора.АвтоматическиеСкидки И
		 фкСпособВыбораСкидки<>Перечисления.СкидкиСпособыВыбора.АвтоматическиеИРучныеСкидки И
		 фкСпособВыбораСкидки<>Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов Тогда
		 Возврат;
	КонецЕсли;
	
	Если Карточка<>Справочники.Карточки.ПустаяСсылка() Тогда	// уже есть карта
		
	КонецЕсли;
	
	Если ПустаяСтрока(Поле) И флРазрешитьРучнойВыборКарточки Тогда
		
		// Сформируем структуру параметров отбора
		СписокВидовКарточек = Новый СписокЗначений;
		СписокВидовКарточек.Добавить(Перечисления.ВидыКарточек.ДисконтнаяКарта);
		
		ПараметрыОтбораЗначение	= Новый Структура("ВидСравнения, Значение", ВидСравнения.ВСписке, СписокВидовКарточек);
		ПараметрыОтбора		= Новый Структура("ВидКарточки", ПараметрыОтбораЗначение);
		ПараметрыОтбора.Вставить("ПометкаУдаления", Ложь);
		
		ПараметрыОткрытия	= Новый Структура("НачальноеЗначениеВыбора,Отбор", Карточка, ПараметрыОтбора);
			
		// Открываем форму выбора
		ФормаВыбора = ПолучитьФормуФронта("ВыборКарточки", ЭтаФорма,, ПараметрыОткрытия);
		ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборКарточки");
		
	ИначеЕсли НЕ ПустаяСтрока(Поле) Тогда
		
		ШтрихКод = СокрЛП(Поле);
		Поле="";
		СтруктураПоиска = НайтиСтруктуруПоШтрихКоду(ШтрихКод);
		
		Если обЗначениеНеЗаполнено(СтруктураПоиска) ИЛИ ТипЗнч(СтруктураПоиска.Объект)<>Тип("СправочникСсылка.Карточки") Тогда
			ОткрытьДиалог("Карточка <" + ШтрихКод + "> не найдена или заблокирована!", СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;
		
		НоваяКарточка = СтруктураПоиска.Объект;
		
		Если НоваяКарточка<>Карточка
			 И (НоваяКарточка.ВидКарточки=Перечисления.ВидыКарточек.ДисконтнаяКарта)Тогда
			Карточка=НоваяКарточка;
			ОбработкаРеквизита("Карточка",,ЭтаФорма);			
		Иначе
			ОткрытьДиалог("Вид карточки <" + ШтрихКод + "> должен быть 
			              |""Дисконтная карта""!", СтатусСообщения.Важное);
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры

Процедура ДействиеБонусы(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли; 
	Если УстановленРежим("Копия") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("ЧекНаВозврат") Тогда Возврат; КонецЕсли;
	
	Если ЗначениеЗаполнено(Карточка.БонуснаяПрограмма) Тогда
		ФормаВыбора = ПолучитьФормуФронта("ФормаВыбораБонусов", ЭтаФорма);
		ОткрытьФормуВыбора(ФормаВыбора, , "ФормаВыбораБонусов");
		ОбработкаРеквизита("КоличествоКСписанию",, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

// действие скидка
Функция ДействиеСкидка(ЭтаФорма) Экспорт
	
	Если НЕ УстановленРежим("Чек") И
		НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
		Возврат 0; 
	КонецЕсли; 
	
	ЭлементыФормы = ЭтаФорма.ЭлементыФормы;
	
	//Право выбора скидки в ручную
	Если (фкСпособВыбораСкидки <> Перечисления.СкидкиСпособыВыбора.СкидкиЗапрещены)И(фкСпособВыбораСкидки <> Перечисления.СкидкиСпособыВыбора.АвтоматическиеСкидки) Тогда
		// Определим сумму чека и строки без учета скидок
		ТекСтрока = ЭлементыФормы.Товары.ТекущаяСтрока;
		
		// Создадим структуру отбора скидок
		Отбор = Новый Структура("ФронтОфис,ЭтоПодарок,СкидкиШапки,СкидкиСтроки,Карточка,СкладКомпании,ПодразделениеКомпании", ИСТИНА, ?(фкЗапретитьПодарки,ЛОЖЬ,Неопределено));
		ЗаполнитьЗначенияСвойств(Отбор, ЭтотОбъект);
		Объект = ?(ТекСтрока=Неопределено, Справочники.Номенклатура.ПустаяСсылка(), ТекСтрока.Номенклатура);
		Отбор.Вставить("СуммаЧека",		Товары.Итог("Сумма"));
		Отбор.Вставить("Объект",		Объект);
		Отбор.Вставить("СуммаСтроки",	?(ТекСтрока=Неопределено, 0, ТекСтрока.Сумма));
		Отбор.Вставить("Количество",	?(ТекСтрока=Неопределено, 0, ТекСтрока.Количество * ТекСтрока.Коэффициент));
		Отбор.Вставить("СкидкаНаТовары",Истина);
		
		// Получим список доступных ручных скидок
		Скидки = рсПолучитьСписокДоступныхСкидок(ТекущаяДата(), Отбор);
		
		// Сформируем структуру параметров отбора
		СписокЗначений = Новый СписокЗначений;
		СписокЗначений.ЗагрузитьЗначения(Скидки.ВыгрузитьКолонку("Скидка"));
		ПараметрыОтбора		= Новый Структура("Ссылка", Новый Структура("Значение,ВидСравнения", СписокЗначений, ВидСравнения.ВСписке));
		ПараметрыОткрытия	= Новый Структура("НачальноеЗначениеВыбора,Отбор,Данные", СкидкаНаценка, ПараметрыОтбора, Скидки);
		
		// Открываем форму выбора
		ФормаВыбора = ПолучитьФормуФронта("ВыборСкидки", ЭтаФорма,, ПараметрыОткрытия);
		ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборСкидки");
	КонецЕсли;
	
КонецФункции

// действие код
Процедура ДействиеКод(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли; 
	Если УстановленРежим("Копия") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("Сервис") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("ЧекНаОплату") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("ПриходныйКассовыйОрдер") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("РасходныйКассовыйОрдер") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("ЧекНаОплатуВозврат") Тогда  Возврат; КонецЕсли;
	Если УстановленРежим("БанковскаяВыписка") Тогда Возврат; КонецЕсли;
	
	Если ПустаяСтрока(СокрЛП(Поле)) Тогда  Возврат; КонецЕсли; 	

	Номенклатура 				= Неопределено;
	ЕдиницаИзмерения 			= Неопределено;
	ХарактеристикаНоменклатуры 	= Неопределено;
	Количество 					= 1;
	СтруктураПоиска 			= Неопределено;
	
	//сперва поищем по штрих-коду
	Попытка СтруктураПоиска = НайтиСтруктуруПоШтрихКоду(Поле,Истина);
	Исключение КонецПопытки;
	
	Если обЗначениеНеЗаполнено(СтруктураПоиска) Тогда
	ИначеЕсли ТипЗнч(СтруктураПоиска.Объект)=Тип("СправочникСсылка.Номенклатура") Тогда
		Номенклатура = СтруктураПоиска.Объект;
		ЕдиницаИзмерения=СтруктураПоиска.Объект.ОсновнаяЕдиницаИзмерения;
		Если НЕ СтруктураПоиска.Свойство("ЕдиницаИзмерения",ЕдиницаИзмерения) Тогда
			ЕдиницаИзмерения=СтруктураПоиска.Объект.ОсновнаяЕдиницаИзмерения;
		КонецЕсли;
		ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		Если НЕ СтруктураПоиска.Свойство("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры) Тогда
			ХарактеристикаНоменклатуры=Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		Если СтруктураПоиска.Объект.ТипНоменклатуры=Справочники.ТипыНоменклатуры.Весовой Тогда
			Количество=Число(СтруктураПоиска.Вес);
			Если Количество<=0 Тогда Количество=1; КонецЕсли;
		Иначе
			Количество=1;
		КонецЕсли;
	КонецЕсли;

	//если не нашли ищем по коду/артикулу
	Если Номенклатура=Неопределено Тогда
		Номенклатура=Справочники.Номенклатура.НайтиПоКоду(Поле);
	КонецЕсли;
	Если Номенклатура=Справочники.Номенклатура.ПустаяСсылка() Тогда
		ОткрытьДиалог("Не найден товар с кодом "+Поле,СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	ДобавитьНоменклатуру(ЭтаФорма,Номенклатура,ХарактеристикаНоменклатуры, ЕдиницаИзмерения,Количество);

	Поле="";
	
КонецПроцедуры

// действие артикул
Функция ДействиеАртикул(ЭтаФорма,СообщениеОРезультатахПоиска=Истина) Экспорт
	
	Если УстановленРежим("Блокировка") ИЛИ
		 УстановленРежим("Копия") ИЛИ
		 УстановленРежим("Сервис") ИЛИ
		 УстановленРежим("ЧекНаОплату") ИЛИ
		 УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ
		 УстановленРежим("РасходныйКассовыйОрдер") ИЛИ
		 УстановленРежим("ЧекНаОплатуВозврат") ИЛИ
		 УстановленРежим("БанковскаяВыписка") ИЛИ
		 ПустаяСтрока(СокрЛП(Поле)) Тогда 
		Возврат 0;
	КонецЕсли;
	
	Номенклатура 				= Неопределено;
	ЕдиницаИзмерения 			= Неопределено;
	ХарактеристикаНоменклатуры 	= Неопределено;
	Количество 					= 1;
	СтруктураПоиска 			= Неопределено;
	МассивНоменклатуры			= Неопределено;
	
	////если не нашли ищем по коду/артикулу
	//МассивНоменклатуры = зфПоискСПреобразованиемАртикула(Поле,"Номенклатура");
	//Если МассивНоменклатуры <> Неопределено Тогда
	//	Если МассивНоменклатуры.Количество()=1 Тогда
	//		Номенклатура = МассивНоменклатуры[0];
	//	ИначеЕсли МассивНоменклатуры.Количество()>1 Тогда
	//		СписокЗначенийНоменклатуры=Новый СписокЗначений;
	//		СписокЗначенийНоменклатуры.ЗагрузитьЗначения(МассивНоменклатуры);
	//		ЭтаФорма.ОткрытьФормуВыбораТовара(СписокЗначенийНоменклатуры);
	//		Поле="";
	//		Возврат 1;
	//	КонецЕсли; 
	//КонецЕсли;
	
	ЗапросПоАртикулуДляПоиска=Новый Запрос;
	ЗапросПоАртикулуДляПоиска.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	(Номенклатура.АртикулДляПоиска ПОДОБНО &АртикулДляПоиска
	|			ИЛИ Номенклатура.АртикулДляПоиска ПОДОБНО &Артикул)";
	ЗапросПоАртикулуДляПоиска.УстановитьПараметр("АртикулДляПоиска", "%"+Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Поле)+"%");
	ЗапросПоАртикулуДляПоиска.УстановитьПараметр("Артикул", "%"+СокрЛП(Поле)+"%");
	РезультатЗапросаПоАртикулуДляПоиска=ЗапросПоАртикулуДляПоиска.Выполнить().Выгрузить();
	Если РезультатЗапросаПоАртикулуДляПоиска.Количество()=1 Тогда
		Номенклатура = РезультатЗапросаПоАртикулуДляПоиска[0].Номенклатура;
	ИначеЕсли РезультатЗапросаПоАртикулуДляПоиска.Количество()>1 Тогда
		СписокЗначенийНоменклатуры=Новый СписокЗначений;
		СписокЗначенийНоменклатуры.ЗагрузитьЗначения(РезультатЗапросаПоАртикулуДляПоиска.ВыгрузитьКолонку("Номенклатура"));
		ЭтаФорма.ОткрытьФормуВыбораТовара(СписокЗначенийНоменклатуры);
		Поле="";
		Возврат 1;
	Иначе
		Если СообщениеОРезультатахПоиска Тогда
			ОткрытьДиалог("Не найден товар с артикулом "+Поле,СтатусСообщения.Важное);
		КонецЕсли; 
		Возврат 0;
	КонецЕсли;
	
	ДобавитьНоменклатуру(ЭтаФорма,Номенклатура);

	Поле="";
	
	Возврат 1;
	
КонецФункции

// действие получить вес
Функция ДействиеПолучитьВес(ЭтаФорма) Экспорт

	Если НЕ УстановленРежим("Чек") И
		НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
		Возврат 0;
	КонецЕсли;

	ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
	Если обЗначениеНеЗаполнено(ТекущаяСтрока) Тогда
		Возврат 0;
	КонецЕсли;

	Товар = ТекущаяСтрока.Номенклатура;
	Если НЕ Товар.ТипНоменклатуры.Весовой Тогда
		ОткрытьДиалог("Товар не весовой!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;

	Если ПустаяСтрока(GUID_Весы) Тогда
		ОткрытьДиалог("Весы не установлены. Получить вес невозможно.", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;

	Вес = 0;
	Если ПолучитьВес(Вес) Тогда
		// Рассчитаем вес с необходимой нам точностью
		ЕдиницаИзмерения = ТекущаяСтрока.ЕдиницаИзмерения;
		Точность = ЕдиницаИзмерения.Точность;
		Количество = Окр(Вес, Точность);
		Если Количество = 0 Тогда
			ОткрытьДиалог("Количество должно быть больше 0!", СтатусСообщения.Внимание);
			Возврат 0;
		ИначеЕсли (Количество < ТекущаяСтрока.Количество) И (ХозОперация = Справочники.ХозОперации.Чек) И (НЕ фкРазрешитьУменьшатьКолво) Тогда
			ОткрытьДиалог("Нельзя уменьшить количество товара!", СтатусСообщения.Важное);
			Возврат 0;
		ИначеЕсли (Количество > ТекущаяСтрока.Количество) И (ХозОперация = Справочники.ХозОперации.ЧекНаВозврат) Тогда
			//подлежит исправлению, добавить контроль прав
			ОткрытьДиалог("Текущий чек является возвратным.
			              |Нельзя увеличить количество товара!", СтатусСообщения.Важное);
			Возврат 0;
		КонецЕсли;

		//Установим рассчитанный вес
		ТекущаяСтрока.Количество=Количество;
		ОбработкаРеквизита("Товары.Количество", ТекущаяСтрока, ЭтаФорма);
		ОтменаРедактирования = Ложь;
	КонецЕсли;

КонецФункции

// действие количество
Процедура ДействиеКоличество(ЭтаФорма, ТекущаяСтрока) Экспорт
	
	Если НЕ УстановленРежим("Чек") И
		НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
		Возврат; 
	КонецЕсли; 
	
	Если ПустаяСтрока(СокрЛП(Поле)) Тогда  Возврат; КонецЕсли; 
	
	Если ТекущаяСтрока=Неопределено Тогда  Возврат; КонецЕсли; 
	Товар = ТекущаяСтрока.Номенклатура;
	
	Попытка
		Количество=Окр(Число(Поле),3);
	Исключение
		ОткрытьДиалог("Количество должно быть числом!", СтатусСообщения.Внимание);
		Возврат;
	КонецПопытки;
	
	Если Количество <= 0 Тогда
		ОткрытьДиалог("Количество должно быть больше 0!", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;

	// Выполняем проверку прав пользователя
	Если (Количество<ТекущаяСтрока.Количество) И (УстановленРежим("Чек")) И (НЕ фкРазрешитьУменьшатьКолво) Тогда
		ОткрытьДиалог("Нет прав на уменьшение количества!", СтатусСообщения.Важное);
		 Возврат;
	ИначеЕсли (Количество>ТекущаяСтрока.Количество) И (УстановленРежим("ЧекНаВозврат") И НЕ (ДокументОснование = Документы.Чек.ПустаяСсылка())) Тогда
		ОткрытьДиалог("Нет прав на увеличение количества при возврате!", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если Товар.ТипНоменклатуры.Весовой = Ложь Тогда
		Если Количество<>Цел(Количество) Тогда
			ОткрытьДиалог("Количество товара должным целым числом!", СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;
	КонецЕсли;
			
	ТекущаяСтрока.Количество=Количество;
	ОбработкаРеквизита("Товары.Количество",ТекущаяСтрока,ЭтаФорма);
	Поле="";
	
КонецПроцедуры

// действие выбор
Функция ДействиеВыбор(ЭтаФорма, ТекущаяСтрока) Экспорт

	Если УстановленРежим("ЧекНаОплату")ИЛИ
		 УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ
		 УстановленРежим("РасходныйКассовыйОрдер") ИЛИ 
		 УстановленРежим("БанковскаяВыписка") Тогда
		// Сформируем структуру параметров отбора
		ПараметрыОтбора = Новый Структура("ДляПробитияНаФР,ДатаФР", ИСТИНА, '00010101');
		СписокДокументовВозврата = Новый СписокЗначений();
		СписокДокументовВозврата.Добавить(Справочники.ХозОперации.ЧекНаОплатуВозврат);
		СписокДокументовВозврата.Добавить(Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат);
		ПараметрыОтбора.Вставить("Операция", Новый Структура("Значение,ВидСравнения", СписокДокументовВозврата, ВидСравнения.НеВСписке));
		ПараметрыОткрытия = Новый Структура("НачальноеЗначениеВыбора, Отбор, Заголовок", Ссылка, ПараметрыОтбора, "Выбор документа на оплату");
		
		// Открываем форму выбора
		ФормаВыбораФронта = ПолучитьФормуФронта("ВыборДокументаНаОплату", ЭтаФорма,, ПараметрыОткрытия);
		ОткрытьФормуВыбора(ФормаВыбораФронта, ПараметрыОткрытия, "ВыборДокументаНаОплату");
		
	ИначеЕсли УстановленРежим("Чек") ИЛИ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
		
		Если ТекущаяСтрока <> Неопределено Тогда
			ЭтаФорма.ОткрытьФормуВыбораТовара(ТекущаяСтрока.Номенклатура);
		Иначе
			ЭтаФорма.ОткрытьФормуВыбораТовара();
		КонецЕсли;
	КонецЕсли;

КонецФункции

// Открывает форму для редактирования кодов маркировки строки.
Процедура ДействиеКодыМаркировки(ЭтаФорма, ТекущаяСтрока = Неопределено, ТолькоПросмотр = Ложь, ВыводитьСообщение=Ложь) Экспорт
	
	//Если ТекущаяСтрока = Неопределено Тогда
	//	ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
	//КонецЕсли;
	//
	//Если обЗначениеНеЗаполнено(ТекущаяСтрока) Тогда
	//	
	//	Возврат;
	//КонецЕсли;
	//
	//Если НЕ дкЭтоМаркируемаяНоменклатура(ТекущаяСтрока.Номенклатура) Тогда
	//	ОткрытьДиалог("У данного товара не ведется учет по маркировке!",СтатусСообщения.Важное);
	//	Возврат;
	//КонецЕсли;
	
	//ФормаВыбора = ПолучитьФормуФронта("ФормаКодовМаркировки", ЭтаФорма);
	//ФормаВыбора.ИдентификаторТовара = ТекущаяСтрока.ИдентификаторТовара;
	//ФормаВыбора.Номенклатура        = ТекущаяСтрока.Номенклатура;
	//ФормаВыбора.ТолькоПросмотр      = ТолькоПросмотр;
	//ОткрытьФормуВыбора(ФормаВыбора, , "ФормаКодовМаркировки");
	ФормаПроверкиКодовМаркировки = ПолучитьФорму("ФормаПроверкиКодовМаркировки", ЭтаФорма);
	ФормаПроверкиКодовМаркировки.АвтоматическаяПроверкаКодовМаркировки = Ложь;
	ФормаПроверкиКодовМаркировки.АвтоматическоеПробитиеЧека = Ложь;
	//ФормаВыбора.ИдентификаторТовара = ТекущаяСтрока.ИдентификаторТовара;
	//ФормаВыбора.Номенклатура        = ТекущаяСтрока.Номенклатура;
	//ФормаВыбора.ТолькоПросмотр      = ТолькоПросмотр;
	ФормаПроверкиКодовМаркировки.ОткрытьМодально();
	//ОткрытьФормуВыбора(ФормаВыбора, , "ФормаПроверкиКодовМаркировки");
	
КонецПроцедуры

// действие характеристика номенклатуры
Функция ДействиеХарактеристика(ЭтаФорма) Экспорт
	
	Если УстановленРежим("ЧекНаОплату") Тогда
		
		Если ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
			ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Постоплата;
		ИначеЕсли ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Постоплата Тогда
			ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
		Иначе
			ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
		КонецЕсли;
		
		Попытка
			ЭтаФорма.ОтображениеКолонкиКодовМаркировки();
		Исключение
		КонецПопытки;
		
	ИначеЕсли УстановленРежим("Чек") ИЛИ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
		
		ТекущаяСтрока = ЭтаФорма.ЭлементыФормы.Товары.ТекущаяСтрока;
		Если ТекущаяСтрока=Неопределено Тогда Возврат 0; КонецЕсли;
		
		Номенклатура = ТекущаяСтрока.Номенклатура;
		
		Если ПроверитьИспользованиеХарактеристикиУТовара(Номенклатура) = 0 Тогда // характеристика не ведется
			ОткрытьДиалог("У данного товара отсутствуют характеристики!",СтатусСообщения.Важное);	
		Иначе
			// Сформируем структуру параметров отбора
			Владелец = ?(Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=1, Номенклатура.ТипНоменклатуры, Номенклатура);
			ПараметрыОткрытия = Новый Структура("Номенклатура,Отбор", Номенклатура, Новый Структура("Владелец", Владелец));
			
			// Открываем форму выбора
			ФормаВыбора = ПолучитьФормуФронта("ВыборХарактеристики", ЭтаФорма,, ПараметрыОткрытия);
			флУстанавливатьХарактеристику = Истина;
			ХарактеристикаНоменклатуры = ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборХарактеристики");
			флУстанавливатьХарактеристику = Ложь;
			
		КонецЕсли;
	Иначе
		Возврат 0; 
	КонецЕсли;
	
КонецФункции

// действие подвести итог
Функция ДействиеПодИтог(ЭтаФорма) Экспорт
		
	Если НЕ УстановленРежим("Чек") И
		НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
		Возврат 0; 
	КонецЕсли; 
	
	РедактированиеСтроки = ИСТИНА;
	// Обработаем все товары в табличной части
	ДопПараметры = Новый Структура("ФронтОфис,ЭтоПодарок,БэкОфис,Терминал", ИСТИНА, ?(фкЗапретитьПодарки,ЛОЖЬ,Неопределено));
	ДопПараметры.Вставить("СкидкаНаТовары",Истина);
	ПараметрыСкидкиШапки = Неопределено;		
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		ОбработкаРеквизита("Товары.Номенклатура",СтрокаТоваров,ЭтаФорма,ДопПараметры);
	КонецЦикла;
	
	// Пересчитаем итоговую сумму документа
	СуммаДокумента = Товары.Итог("СуммаВсего");
	ОбработкаРеквизита("СуммаДокумента",,ЭтаФорма,ДопПараметры);
	ЗаписатьВФайлЛога("Получен подитог");
	
	РедактированиеСтроки = ЛОЖЬ;
	
	ВывестиНаДисплей("  ","  ","К оплате: ",ФРМсумма(СуммаДокумента));
	
КонецФункции

// действие безналичный расчет
Процедура ДействиеБезНал(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли; 
	Если УстановленРежим("Копия") Тогда  Возврат; КонецЕсли;
	
	Если тзДоступныеВидыОплат.Количество() = 0 Тогда
		ОткрытьДиалог("Нет доступных видов оплат. Проверьте настройки ФР!", СтатусСообщения.Внимание);
		Возврат; 
	КонецЕсли;
	флОткрытаФормаОплат = Истина;
	ФормаОплат = ПолучитьФормуФронта("ВыборОплаты", ЭтаФорма);
	ОткрытьФормуВыбора(ФормаОплат,, "ВыборОплаты");
	
	ОплатыПриОкончанииРедактирования(ЭтаФорма);
	Поле="";
	
	флОткрытаФормаОплат = Ложь;
	Возврат;	
КонецПроцедуры

// действие отдел
Процедура ДействиеОтдел(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли; 
	Если УстановленРежим("Копия") Тогда  Возврат; КонецЕсли;
	
	// Определим начальное значение выбора
	Попытка
		НачальноеЗначениеВыбора = СкладКомпании;
		НачальноеЗначениеВыбора = ?(обЗначениеНеЗаполнено(ЭтаФорма.ЭлементыФормы.Товары.ТекущаяСтрока.МестоРазмещения), СкладКомпании, ЭтаФорма.ЭлементыФормы.Товары.ТекущаяСтрока.МестоРазмещения);
	Исключение КонецПопытки;
	
	// Сформируем структуру параметров отбора
	ПараметрыОткрытия = Новый Структура("НачальноеЗначениеВыбора, Отбор", НачальноеЗначениеВыбора, Новый Структура("Розничный", ИСТИНА));
	
	// Открываем форму выбора
	ФормаВыбора = ПолучитьФормуФронта("ВыборСклада", ЭтаФорма,, ПараметрыОткрытия);
	ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборСклада");

КонецПроцедуры

// действие вверх
Функция ДействиеВверх(ЭтаФорма) Экспорт
	
	ЭлементыФормы = ЭтаФорма.ЭлементыФормы;	
	 
	Если ЭлементыФормы.Товары.ТекущаяСтрока<>Неопределено Тогда
		ТекСтрока=ЭлементыФормы.Товары.ТекущаяСтрока.НомерСтроки;
		Если ТекСтрока>1 Тогда
			ЭлементыФормы.Товары.ТекущаяСтрока=Товары.Получить(ТекСтрока-1-1);
		КонецЕсли;
	Иначе
		// активируем первую строку в ТЧ
		Если Товары.Количество() <> 0 Тогда
			ЭлементыФормы.Товары.ТекущаяСтрока = Товары.Получить(0);	
		КонецЕсли;
	КонецЕсли; 

КонецФункции

// действие вниз
Функция ДействиеВниз(ЭтаФорма) Экспорт
	
	ЭлементыФормы = ЭтаФорма.ЭлементыФормы;
	Если ЭлементыФормы.Товары.ТекущаяСтрока<>Неопределено Тогда
		ТекСтрока=ЭлементыФормы.Товары.ТекущаяСтрока.НомерСтроки;
		Если ТекСтрока<Товары.Количество() Тогда
			ЭлементыФормы.Товары.ТекущаяСтрока=Товары.Получить(ТекСтрока+1-1);
		КонецЕсли; 
	Иначе
		// активируем первую строку в ТЧ
		Если Товары.Количество() <> 0 Тогда
			ЭлементыФормы.Товары.ТекущаяСтрока = Товары.Получить(0);	
			ДействиеВниз(ЭтаФорма);
		КонецЕсли;
	КонецЕсли; 
	
КонецФункции

// действие минус 1
Функция ДействиеМинус1(ЭтаФорма) Экспорт
	
	Если НЕ УстановленРежим("Чек") И НЕ УстановленРежим("ЧекНаВозврат") Тогда
		Возврат 0; 
	КонецЕсли; 
	
	// Выполняем проверку прав пользователя
	Если (НЕ фкРазрешитьУменьшатьКолво) И УстановленРежим("Чек") Тогда
		ОткрытьДиалог("Нет прав на уменьшение количества!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
		
	Если Поле="" Тогда
		Если ТекущаяСтрока<>Неопределено  Тогда
			Если обЗначениеНеЗаполнено(ТекущаяСтрока.Набор) Тогда
				Количество = Число(ТекущаяСтрока.Количество);
				Если Количество <= 1 Тогда Возврат 0; КонецЕсли; 
				Количество = Количество - 1;
				ТекущаяСтрока.Количество=Количество;
				ОбработкаРеквизита("Товары.Количество",ТекущаяСтрока,ЭтаФорма);
			Иначе
				ДобавитьНоменклатуру(ЭтаФорма,ТекущаяСтрока.Набор,,,-1);		
			КонецЕсли;
		КонецЕсли; 
	Иначе
		Попытка 
			Количество = Число(Поле);
			Количество = ?((Количество - 1)<0,0,(Количество - 1));
			Поле=Формат(Количество,"ЧЦ=15; ЧДЦ=2; ЧРГ=''; ЧН=0");
		Исключение
			ОткрытьДиалог("Значение должно быть числом!", СтатусСообщения.Внимание);
			Возврат 0;
		КонецПопытки;
	КонецЕсли; 

КонецФункции

// действие плюс 1
Функция ДействиеПлюс1(ЭтаФорма) Экспорт
	
	Если НЕ УстановленРежим("Чек") И
		НЕ (УстановленРежим("ЧекНаВозврат") И фкРучнойВводВозврата) Тогда
		Возврат 0; 
	КонецЕсли; 
	
	ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
	
	Если Поле="" Тогда
		Если ТекущаяСтрока<>Неопределено Тогда
			Если обЗначениеНеЗаполнено(ТекущаяСтрока.Набор) Тогда

				ТекущаяСтрока.Количество=ТекущаяСтрока.Количество+1;
				ОбработкаРеквизита("Товары.Количество",ТекущаяСтрока,ЭтаФорма);
			Иначе
				ДобавитьНоменклатуру(ЭтаФорма,ТекущаяСтрока.Набор,,,1);		
			КонецЕсли;
				
		КонецЕсли; 
	Иначе
		Попытка
			Поле=Формат(Число(Поле)+1,"ЧЦ=15; ЧДЦ=2; ЧРГ=''; ЧН=0");
		Исключение
			ОткрытьДиалог("Значение должно быть числом", СтатусСообщения.Внимание);
			Возврат 0;
		КонецПопытки;
	КонецЕсли;
	
КонецФункции

// Блок управления кассой
Функция ДействиеУправлениеКассой(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	Форма = ПолучитьФормуФронта("УправлениеКассой", ЭтаФорма);
	ОткрытьФормуВыбора(Форма,,"УправлениеКассой");

КонецФункции

// Выбор ФРа
Функция ДействиеКасса(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	// Сформируем структуру параметров отбора
	ПараметрыОткрытия = Новый Структура("НачальноеЗначениеВыбора, Отбор", КассаККМ, Новый Структура("КлассОборудования", Перечисления.КлассыОборудования.ФР));
	
	// Открываем форму выбора
	ФормаВыбора = ПолучитьФормуФронта("ВыборКассы", ЭтаФорма,, ПараметрыОткрытия);
	ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборКассы");
КонецФункции

// Открытие смены
 Функция Действие0Чек(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Начало смены выполнено быть не может.", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	//Начнем смену на кассе ККМ
	SafeArrayПараметры=Рарус_Компонента.СоздатьПараметры(3,1);
	CashierID = ПараметрыСеанса.Пользователь.ПарольККМ;
	SafeArrayПараметры.SetValue(0,0,CashierID);
	
	ТекущийПользователь = ПараметрыСеанса.Пользователь;
	SafeArrayПараметры.SetValue(1,0,Строка(ТекущийПользователь.Наименование));// Кассир - наименование ответственного за чек
	SafeArrayПараметры.SetValue(2,0,ТекущийПользователь.ИНН);// ИНН Кассира
	
	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"НачатьСмену",SafeArrayПараметры,ТаймаутФР);
	Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
		ЗаписатьВФайлЛога("Начало смены",КодОшибки,"Операция не выполнена",GUID_ФР,1);
		Возврат 0;
	Иначе
		ЗаписатьВФайлЛога("Начало смены",КодОшибки,Рарус_Компонента.ОписаниеОшибки);
	КонецЕсли;	
КонецФункции

// Открытие ДЯ
Функция ДействиеЯщик(ЭтаФорма) Экспорт
		
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Открытие денежного ящика выполнено быть не может.", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	//Откроем денежный ящик ФР
	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"ОткрытьДенежныйЯщик",,ТаймаутФР);
	Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
		ЗаписатьВФайлЛога("Открытие денежного ящика",КодОшибки,"Операция не выполнена",GUID_ФР,1);
		Возврат 0;
	Иначе
		ЗаписатьВФайлЛога("Открытие денежного ящика",КодОшибки,Рарус_Компонента.ОписаниеОшибки);
	КонецЕсли;

КонецФункции

// Внесение наличности
Функция ДействиеВнесение(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	// Выполняем проверку возможности произвести действие
	Если НЕ флРазрешитьПроводитьИнкассацию Тогда
		ОткрытьДиалог("Нет прав на проведение инкассации!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	ФормаИнкассация=ПолучитьФормуФронта("Инкассация", ЭтаФорма);
	ФормаИнкассация.Внесение=ИСТИНА;
	ОткрытьФормуВыбора(ФормаИнкассация,,"Инкассация");
	
КонецФункции

// Изъятие наличности
Функция ДействиеИзъятие(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	// Выполняем проверку возможности произвести действие
	Если НЕ флРазрешитьПроводитьИнкассацию Тогда
		ОткрытьДиалог("Нет прав на проведение инкассации!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	ФормаИнкассация=ПолучитьФормуФронта("Инкассация", ЭтаФорма);
	ФормаИнкассация.Внесение=ЛОЖЬ;
	ОткрытьФормуВыбора(ФормаИнкассация,,"Инкассация");
	
КонецФункции

// Печать Х отчета
Функция ДействиеХОтчет(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	//проверим право
	Если НЕ фкСмотретьФискальныеСчетчики Тогда
		ОткрытьДиалог("Вам запрещено выполнять эту операцию", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. X-отчет снят быть не может.", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	//Снимем отчет
	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"Х_Отчет",,ТаймаутФР);
	Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
		ЗаписатьВФайлЛога("Снять X-отчет",КодОшибки,"Операция не выполнена",GUID_ФР,1);
		Возврат 0;
	Иначе
		ЗаписатьВФайлЛога("Снять X-отчет",КодОшибки,Рарус_Компонента.ОписаниеОшибки);
	КонецЕсли;
	
КонецФункции

// Печать Z отчета
Функция ДействиеZОтчет(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	//проверим право
	Если НЕ фкРазрешитьГашениеФискСчетчиков Тогда
		ОткрытьДиалог("Вам запрещено выполнять эту операцию", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Z-отчет снят быть не может.", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	//переспросим пользователя
	Если ОткрытьДиалогВопроса("Выполнить закрытие кассовой смены на кассе "+Строка(ФР)+"?") Тогда
		
		Если НЕ фкГашенияКассыБезОбработки Тогда
			ОбработкаЗакрытияСмены = Обработки.ЗакрытиеСмены.Создать();
			Форма = ОбработкаЗакрытияСмены.ПолучитьФорму("Форма",ЭтаФорма);
			
			Параметры = Новый СписокЗначений;
			Параметры.Добавить(Истина); // флаг открытия из фронта
			Параметры.Добавить(ФР);     // закрытие смены по текущему ФРу/Кассе
			Параметры.Добавить(КассаККМ); 
			Форма.НачальноеЗначениеВыбора = Параметры;
			Форма.ОткрытьМодально();
			
		Иначе
			
			SafeArrayПараметры=Рарус_Компонента.СоздатьПараметры(2,1);
			
			ТекущийПользователь = ПараметрыСеанса.Пользователь;
			SafeArrayПараметры.SetValue(0,0,Строка(ТекущийПользователь.Наименование));// Кассир - наименование ответственного за чек
			SafeArrayПараметры.SetValue(1,0,ТекущийПользователь.ИНН);// ИНН Кассира
			
			//Снимем отчет
			КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"Z_Отчет",SafeArrayПараметры,ТаймаутФР);
			Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
				ЗаписатьВФайлЛога("Снять Z-отчет",КодОшибки,"Операция не выполнена",GUID_ФР,1);
				Возврат 0;
			Иначе
				ЗаписатьВФайлЛога("Снять Z-отчет",КодОшибки,Рарус_Компонента.ОписаниеОшибки);
			КонецЕсли;
		КонецЕсли;	
			
	КонецЕсли;	

КонецФункции

// Вывод счетчиков ФРа
Функция ДействиеСтатус(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	//проверим право
	Если НЕ фкСмотретьФискальныеСчетчики Тогда
		ОткрытьДиалог("Вам запрещено выполнять эту операцию", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Статус получен быть не может.", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	//Получить статус ФР
	SafeArrayСтатусыФР=Рарус_Компонента.СоздатьПараметры(,);
	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"ПолучитьСтатус",SafeArrayСтатусыФР,ТаймаутФР);
	Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
		 Возврат 0;
	КонецЕсли;
	
	Попытка
		SafeArrayСчетчикиФР=SafeArrayСтатусыФР.GetValue(0, 0);
		Попытка		
			ЧистыйИтогСмены=Число(SafeArrayСчетчикиФР.GetValue(0, 0));
		Исключение
			ЧистыйИтогСмены=-1;
		КонецПопытки;
		Попытка		
			ПолныйИтогСмены=Число(SafeArrayСчетчикиФР.GetValue(1, 0));
		Исключение
			ПолныйИтогСмены=-1;
		КонецПопытки;			
		Попытка				
			НеобнуляемаяСумма=Число(SafeArrayСчетчикиФР.GetValue(2, 0));
		Исключение
			НеобнуляемаяСумма=-1;
		КонецПопытки;			
		Попытка				
			КоличествоПродаж=Число(SafeArrayСчетчикиФР.GetValue(3, 0));
		Исключение			
			КоличествоПродаж=-1;
		КонецПопытки;			
		Попытка				
			КоличествоВозвратов=Число(SafeArrayСчетчикиФР.GetValue(4, 0));
		Исключение			
			КоличествоВозвратов=-1;
		КонецПопытки;			
		Попытка				
			СуммаПродаж=Число(SafeArrayСчетчикиФР.GetValue(5, 0));
		Исключение			
			СуммаПродаж=-1;
		КонецПопытки;			
		Попытка				
			СуммаВозвратов=Число(SafeArrayСчетчикиФР.GetValue(6, 0));
		Исключение			
			СуммаВозвратов=-1;
		КонецПопытки;			
		Попытка				
			КоличествоСкидокПродаж=Число(SafeArrayСчетчикиФР.GetValue(7, 0));
		Исключение			
			КоличествоСкидокПродаж=-1;
		КонецПопытки;			
		Попытка				
			КоличествоСкидокВозвратов=Число(SafeArrayСчетчикиФР.GetValue(8, 0));
		Исключение			
			КоличествоСкидокВозвратов=-1;
		КонецПопытки;			
		Попытка				
			КоличествоНадбавокПродаж=Число(SafeArrayСчетчикиФР.GetValue(9, 0));
		Исключение			
			КоличествоНадбавокПродаж=-1;
		КонецПопытки;			
		Попытка				
			КоличествоНадбавокВозвратов=Число(SafeArrayСчетчикиФР.GetValue(10, 0));
		Исключение			
			КоличествоНадбавокВозвратов=-1;
		КонецПопытки;			
		Попытка				
			СуммаСкидокПродаж=Число(SafeArrayСчетчикиФР.GetValue(11, 0));
		Исключение			
			СуммаСкидокПродаж=-1;
		КонецПопытки;			
		Попытка				
			СуммаСкидокВозвратов=Число(SafeArrayСчетчикиФР.GetValue(12, 0));
		Исключение			
			СуммаСкидокВозвратов=-1;
		КонецПопытки;			
		Попытка				
			СуммаНадбавокПродаж=Число(SafeArrayСчетчикиФР.GetValue(13, 0));
		Исключение			
			СуммаНадбавокПродаж=-1;
		КонецПопытки;			
		Попытка				
			СуммаНадбавокВозвратов=Число(SafeArrayСчетчикиФР.GetValue(14, 0));
		Исключение			
			СуммаНадбавокВозвратов=-1;
		КонецПопытки;			
		Попытка				
			КоличествоИзъятий=Число(SafeArrayСчетчикиФР.GetValue(15, 0));
		Исключение			
			КоличествоИзъятий=-1;
		КонецПопытки;			
		Попытка				
			КоличествоВнесений=Число(SafeArrayСчетчикиФР.GetValue(16, 0));
		Исключение			
			КоличествоВнесений=-1;
		КонецПопытки;			
		Попытка				
			СуммаИзъятий=Число(SafeArrayСчетчикиФР.GetValue(17, 0));
		Исключение			
			СуммаИзъятий=-1;
		КонецПопытки;			
		Попытка				
			СуммаВнесений=Число(SafeArrayСчетчикиФР.GetValue(18, 0));
		Исключение			
			СуммаВнесений=-1;
		КонецПопытки;			
		СообщениеСтатуса=
		"Количество продаж: "+?(КоличествоПродаж<0,"<Не известно>",Формат(КоличествоПродаж,"ЧН=0"))+" на сумму "+?(СуммаПродаж<0,"<Не известно>",Формат(СуммаПродаж,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Количество возвратов: "+?(КоличествоВозвратов<0,"<Не известно>",Формат(КоличествоВозвратов,"ЧН=0"))+" на сумму "+?(СуммаВозвратов<0,"<Не известно>",Формат(СуммаВозвратов,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Количество скидок продаж: "+?(КоличествоСкидокПродаж<0,"<Не известно>",Формат(КоличествоСкидокПродаж,"ЧН=0"))+" на сумму "+?(СуммаСкидокПродаж<0,"<Не известно>",Формат(СуммаСкидокПродаж,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Количество скидок возвратов: "+?(КоличествоСкидокВозвратов<0,"<Не известно>",Формат(КоличествоСкидокВозвратов,"ЧН=0"))+" на сумму "+?(СуммаСкидокВозвратов<0,"<Не известно>",Формат(СуммаСкидокВозвратов,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Количество надбавок продаж: "+?(КоличествоНадбавокПродаж<0,"<Не известно>",Формат(КоличествоНадбавокПродаж,"ЧН=0"))+" на сумму "+?(СуммаНадбавокПродаж<0,"<Не известно>",Формат(СуммаНадбавокПродаж,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Количество надбавок возвратов: "+?(КоличествоНадбавокВозвратов<0,"<Не известно>",Формат(КоличествоНадбавокВозвратов,"ЧН=0"))+" на сумму "+?(СуммаНадбавокВозвратов<0,"<Не известно>",Формат(СуммаНадбавокВозвратов,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Количество изъятий: "+?(КоличествоИзъятий<0,"<Не известно>",Формат(КоличествоИзъятий,"ЧН=0"))+" на сумму "+?(СуммаИзъятий<0,"<Не известно>",Формат(СуммаИзъятий,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Количество внесений: "+?(КоличествоВнесений<0,"<Не известно>",Формат(КоличествоВнесений,"ЧН=0"))+" на сумму "+?(СуммаВнесений<0,"<Не известно>",Формат(СуммаВнесений,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"));
		
		SafeArrayТаблицаОстатковВКассе=SafeArrayСтатусыФР.GetValue(1,0);
		КоличествоСтрок=0; КоличествоСтолбцов=0;
		SafeArrayТаблицаОстатковВКассе.GetBounds(КоличествоСтрок,КоличествоСтолбцов);
		Если КоличествоСтрок>0 Тогда
			СообщениеСтатуса=СообщениеСтатуса+"
			|
			|ОСТАТКИ В КАССЕ:";
		КонецЕсли; 
		Для Сч=0 По КоличествоСтрок-1 Цикл
			Попытка				
				ОстаткиТипОплаты=Число(SafeArrayТаблицаОстатковВКассе.GetValue(Сч,0));
			Исключение			
				ОстаткиТипОплаты=-1;
			КонецПопытки;			
			Попытка				
				ОстаткиНаименованиеОплаты=Строка(SafeArrayТаблицаОстатковВКассе.GetValue(Сч,1));
			Исключение			
				Если ОстаткиТипОплаты=0 Тогда
					ОстаткиНаименованиеОплаты="(Наличные)";
				ИначеЕсли ОстаткиТипОплаты=1 Тогда
					ОстаткиНаименованиеОплаты="(Платежные карты)";
				ИначеЕсли ОстаткиТипОплаты=2 Тогда
					ОстаткиНаименованиеОплаты="(Кредит)";
				ИначеЕсли ОстаткиТипОплаты=3 Тогда
					ОстаткиНаименованиеОплаты="(Безналичные)";
				Иначе
					ОстаткиНаименованиеОплаты="(<Неизвестный тип оплаты>)";
				КонецЕсли; 
			КонецПопытки;			
			Попытка				
				ОстаткиСумма=Число(SafeArrayТаблицаОстатковВКассе.GetValue(Сч,2));
			Исключение			
				ОстаткиСумма=-1;
			КонецПопытки;			
			СообщениеСтатуса=СообщениеСтатуса+"
			|"+ОстаткиНаименованиеОплаты+" ";
			СообщениеСтатуса=СообщениеСтатуса+" на сумму "+Формат(?(ОстаткиСумма<0,"<Не известно>",ОстаткиСумма),"ЧЦ=15; ЧДЦ=2; ЧН=0.00");
		КонецЦикла; 
		SafeArrayТаблицаОборотовПоКассе=SafeArrayСтатусыФР.GetValue(2,0);
		КоличествоСтрок=0; КоличествоСтолбцов=0;
		SafeArrayТаблицаОборотовПоКассе.GetBounds(КоличествоСтрок,КоличествоСтолбцов);
		Если КоличествоСтрок>0 Тогда
			СообщениеСтатуса=СообщениеСтатуса+"
			|
			|ОБОРОТЫ ПО КАССЕ:";
		КонецЕсли; 
		Для Сч=0 По КоличествоСтрок-1 Цикл
			Попытка				
				ОборотыТипОплаты=Число(SafeArrayТаблицаОборотовПоКассе.GetValue(Сч,0));
			Исключение			
				ОборотыТипОплаты=-1;
			КонецПопытки;			
			Попытка
				ОборотыНаименованиеОплаты=Строка(SafeArrayТаблицаОборотовПоКассе.GetValue(Сч,1));
			Исключение
				Если ОборотыТипОплаты=0 Тогда
					ОборотыНаименованиеОплаты="(Наличные)";
				ИначеЕсли ОборотыТипОплаты=1 Тогда
					ОборотыНаименованиеОплаты="(Платежные карты)";
				ИначеЕсли ОборотыТипОплаты=2 Тогда
					ОборотыНаименованиеОплаты="(Кредит)";
				ИначеЕсли ОборотыТипОплаты=3 Тогда
					ОборотыНаименованиеОплаты="(Безналичные)";
				Иначе
					ОборотыНаименованиеОплаты="(<Неизвестный тип оплаты>)";
				КонецЕсли; 
			КонецПопытки;
			Попытка				
				ОборотыКоличество=Число(SafeArrayТаблицаОборотовПоКассе.GetValue(Сч,2));
			Исключение			
				ОборотыКоличество=-1;
			КонецПопытки;			
			Попытка				
				ОборотыСумма=Число(SafeArrayТаблицаОборотовПоКассе.GetValue(Сч,3));
			Исключение			
				ОборотыСумма=-1;
			КонецПопытки;			
			СообщениеСтатуса=СообщениеСтатуса+"
			|"+ОборотыНаименованиеОплаты+" : ";
			СообщениеСтатуса=СообщениеСтатуса+
			?(ОборотыКоличество<0,"<Не известно>",Формат(ОборотыКоличество,"ЧН=0"))+
			" на сумму "+Формат(?(ОборотыСумма<0,"<Не известно>",ОборотыСумма),"ЧЦ=15; ЧДЦ=2; ЧН=0.00");
		КонецЦикла; 
		
		СообщениеСтатуса=СообщениеСтатуса+"
		|
		|ИТОГИ:
		|Необнуляемая сумма: "+?(НеобнуляемаяСумма<0,"<Не известно>",Формат(НеобнуляемаяСумма,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Чистый итог смены: "+?(ЧистыйИтогСмены<0,"<Не известно>",Формат(ЧистыйИтогСмены,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"))+"
		|Полный итог смены: "+?(ПолныйИтогСмены<0,"<Не известно>",Формат(ПолныйИтогСмены,"ЧЦ=15; ЧДЦ=2; ЧН=0.00"));
		
		
		ОткрытьДиалог(КассаККМ.Наименование+":"+Символы.ПС+СообщениеСтатуса, СтатусСообщения.Информация, 0);
	Исключение
		ОткрытьДиалог("Ошибка получения статусов ФР.", СтатусСообщения.ОченьВажное);
		Возврат 0;
	КонецПопытки; 
	
КонецФункции

// Вывод информации о последнем пробитом документе
Функция ДействиеДокумент(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Информация о документе получена быть не может.",СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	//Получить последний документ из ФР
	SafeArrayПоследнийДокумент=Рарус_Компонента.СоздатьПараметры(,);
	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"ПоследнийДокумент",SafeArrayПоследнийДокумент,ТаймаутФР);
	Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
		 Возврат 0;
	КонецЕсли;
	
	//Извлечение информации о последнем документе
	Попытка
		Попытка
			ПоследнийТипДокумента=Число(SafeArrayПоследнийДокумент.GetValue(0,0));
		Исключение
			ПоследнийТипДокумента=-1;
		КонецПопытки;
		Попытка
			ПоследняяСуммаДокумента=Число(SafeArrayПоследнийДокумент.GetValue(1,0));
		Исключение
			ПоследняяСуммаДокумента=-1;
		КонецПопытки;
		SafeArrayПоследнийДокументРеквизиты=SafeArrayПоследнийДокумент.GetValue(2,0);
		Попытка
			ПоследнийНомерСмены=Число(SafeArrayПоследнийДокументРеквизиты.GetValue(0,0));
		Исключение
			ПоследнийНомерСмены=-1;
		КонецПопытки;
		Попытка
			ПоследнийНомерЧека=Число(SafeArrayПоследнийДокументРеквизиты.GetValue(1,0));
		Исключение
			ПоследнийНомерЧека=-1;
		КонецПопытки;
		Попытка
			ПоследнийНомерДокумента=Число(SafeArrayПоследнийДокументРеквизиты.GetValue(2,0));
		Исключение
			ПоследнийНомерДокумента=-1;
		КонецПопытки;
		Попытка
			ПоследняяДатаФР=Вычислить("'"+SafeArrayПоследнийДокументРеквизиты.GetValue(3,0)+"'");
		Исключение
			ПоследняяДатаФР='00010101';
		КонецПопытки; 
	Исключение
		ОткрытьДиалог("Ошибка получения реквизитов последнего документа.", СтатусСообщения.ОченьВажное);
		 Возврат 0;
	КонецПопытки; 
	//Вывод информации о последнем документе кассиру
	Если ПоследнийТипДокумента=0 Тогда
		ПоследнийТипДокументаПредставление="Чек продажи";
	ИначеЕсли ПоследнийТипДокумента=1 Тогда
		ПоследнийТипДокументаПредставление="Чек возврата";
	ИначеЕсли ПоследнийТипДокумента=2 Тогда
		ПоследнийТипДокументаПредставление="Инкассация";
	ИначеЕсли ПоследнийТипДокумента=3 Тогда
		ПоследнийТипДокументаПредставление="Внесение";
	ИначеЕсли ПоследнийТипДокумента=4 Тогда
		ПоследнийТипДокументаПредставление="X-отчет";
	ИначеЕсли ПоследнийТипДокумента=5 Тогда
		ПоследнийТипДокументаПредставление="Z-отчет";
	Иначе
		ПоследнийТипДокументаПредставление="<Неизвестный документ>";
	КонецЕсли; 
	ОткрытьДиалог(КассаККМ.Наименование+":
	|
	|"+ПоследнийТипДокументаПредставление+"
	|№ смены: "+?(ПоследнийНомерСмены<0,"<Не известен>",Формат(ПоследнийНомерСмены,"ЧЦ=6; ЧДЦ=0; ЧГ=0"))+"
	|№ чека: "+?(ПоследнийНомерЧека<0,"<Не известен>",Формат(ПоследнийНомерЧека,"ЧЦ=6; ЧДЦ=0; ЧГ=0"))+"
	|Фискальный документ №: "+?(ПоследнийНомерДокумента<0,"<Не известен>",Формат(ПоследнийНомерДокумента,"ЧЦ=10; ЧДЦ=0; ЧГ=0"))+"
	|Дата документа: "+?(обЗначениеНеЗаполнено(ПоследняяДатаФР),"<Не известна>",Формат(ПоследняяДатаФР,"ДФ='dd.MM.yyyy ЧЧ:мм:сс'"))+"
	|Сумма документа: "+?(ПоследняяСуммаДокумента<0,"<Не известна>",Формат(ПоследняяСуммаДокумента,"ЧЦ=15; ЧДЦ=2; ЧН=0.00")), СтатусСообщения.Информация, 0);

	
КонецФункции

// Проверка на наличие неотправленных документов
//
Процедура ДействиеПроверкаНеотправленныхДокументов(Предупреждать = Ложь) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Начало смены выполнено быть не может.", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	НаименованиеМодели = ФР.МодельОборудования;
	Если (НаименованиеМодели = "Учебный ФР") ИЛИ (НаименованиеМодели = "ФР Учебный/ЕНВД") ИЛИ (НаименованиеМодели = "ФР Учебный/ЕНВД (Windows-принтер)") Тогда
		Возврат;
	КонецЕсли;
		
	Результат = ВыполнитьПроверкуНеотправленныхДокументов(Предупреждать);
	
	Если ТипЗнч(Результат) = Тип("COMОбъект") Тогда
		Попытка
			ВерсияФФД = Результат.GetValue(10,0);
		Исключение
			ВерсияФФД = "";
		КонецПопытки;
		
		Если ВерсияФФД <> "" Тогда
			Попытка
				КоличествоДокументов = Результат.GetValue(11,0);
			Исключение
				КоличествоДокументов = 0;
			КонецПопытки;
			
			Если КоличествоДокументов > 0 Тогда
				Попытка
					ДатаПервого = Результат.GetValue(12,0);
				Исключение
					ДатаПервого = Неопределено;
				КонецПопытки;
			КонецЕсли;
			
			Если КоличествоДокументов > 0 ИЛИ Предупреждать Тогда
				
				ТекстСообщения = "Найдено <" + КоличествоДокументов + "> неотправленных документов.";
				
				Если КоличествоДокументов > 0 И ДатаПервого <> Неопределено Тогда
					ТекстСообщения = ТекстСообщения + Символы.ПС + "Дата самого раннего: " + ДатаПервого;
				КонецЕсли;
				
				ОткрытьДиалог(ТекстСообщения, СтатусСообщения.Информация, 15);
			КонецЕсли;
		ИначеЕсли Предупреждать Тогда 
			Обработка = Обработки.ТорговоеОборудование.Создать();
			Обработка.ПроинформироватьОбОтсутствииПоддержкиФЗ(54,"Фискальный регистратор");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//siniko удалить
//// Пробитие чека коррекции
//Функция ДействиеПробитьЧекКоррекцииНажатие(ЭтаФорма) Экспорт
//	
//	Если УстановленРежим("Блокировка") Тогда  Возврат 0; КонецЕсли;
//		
//	ФормаЧекаКоррекции=ПолучитьФормуФронта("ЧекКоррекции", ЭтаФорма);

//	ОткрытьФормуВыбора(ФормаЧекаКоррекции,,"ЧекКоррекции");

//КонецФункции

// Формирование отчета о состоянии расчетов
//
Процедура ДействиеРасчетОФД() Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Начало смены выполнено быть не может.", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	СостояниеСмены = ТребуетсяОткрытиеСмены();
	
	Если СостояниеСмены = 0 Тогда
		ОткрытьДиалог("Кассовая смена открыта."+ Символы.ПС+ "Для выполнения данной команды необходимо закрыть кассовую смену.", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Результат = ВыполнитьКомандуОборудования("ФР",GUID_ФР, "РасчетОФД", ,ТаймаутФР,ТекстОшибки);
	
КонецПроцедуры

// Копия чека из ФН
//
Процедура ДействиеКопияФН(ЭтаФорма) Экспорт
	
	Если УстановленРежим("Блокировка") Тогда  Возврат; КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Фискальный регистратор не установлен. Начало смены выполнено быть не может.", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	НомерДокумента = 0;

	Форма = ПолучитьФормуФронта("Калькулятор", ЭтаФорма);
	ВыбранноеЗначение = ОткрытьФормуВыбора(Форма,,"Калькулятор");;
	Если НЕ ВыбранноеЗначение = Неопределено Тогда 
		ПараметрыСобытия = Новый Структура;
		ПараметрыСобытия.Вставить("ФР", GUID_ФР);
		ПараметрыСобытия.Вставить("НомерДокумента", ВыбранноеЗначение);
		Если Не (ПробитьЧекИзФН(ПараметрыСобытия)) И ПараметрыСобытия.Свойство("Комментарий") Тогда 
			ОткрытьДиалог(ПараметрыСобытия.Комментарий, СтатусСообщения.Важное);
		КонецЕсли;
	КонецЕсли;
	
	
	
КонецПроцедуры

// Ввод данных клиента для последующей отправки электронного чека
//
Процедура ДействиеДанныеКлиента(ЭтаФорма) Экспорт
	
	Форма = ПолучитьФормуФронта("ВводДанныхКлиента", ЭтаФорма);
	Если УстановленРежим("Копия") = Истина Тогда
		Форма.ТолькоПросмотр = Истина;
	КонецЕсли;
	ТекДанныеКлиента = ОткрытьФормуВыбора(Форма,,"ВводДанныхКлиента");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УСТАНОВКИ ОБЩЕЙ СКИДКИ/НАЦЕНКИ

 // Открывает форму выбора
//
// Параметры:
//  НоваяФормаВыбора        - форма - ссылка на форму, которую хотим открыть,
//  НачальноеЗначениеВыбора - произвольный - содержит начальное значение выбора, переданное в форму.
//
Функция ОткрытьФормуВыбора( НоваяФормаВыбора, НачальноеЗначениеВыбора = Неопределено, РежимФормыВыбора) Экспорт 
	
	ТекущаяФорма = НоваяФормаВыбора;
	
	ВладелецФормы = ТекущаяФорма.ВладелецФормы;

	Если (ТекущаяФорма <> Неопределено)И(НЕ ТекущаяФорма.Открыта()) Тогда
		Если НачальноеЗначениеВыбора <> Неопределено Тогда
			ТекущаяФорма.НачальноеЗначениеВыбора = НачальноеЗначениеВыбора;
		КонецЕсли;
		
		// Установим режим открытия формы как "выбор"
		ТекущаяФорма.РежимВыбора = ИСТИНА;		
		
		// перейдем в режим нужной нам форме
		ПерейтиВРежим(РежимФормыВыбора);
		// Откроем форму выбора
		Параметры = ТекущаяФорма.ОткрытьМодально();
		НачальноеЗначениеВыбора = ТекущаяФорма.НачальноеЗначениеВыбора;
		// Выходим из режима
		ВыйтиИзРежима(РежимФормыВыбора);
		ТекущаяФорма = Неопределено;
		
		Если ЧекОткрыт() Тогда
			Если РежимФормыВыбора = ВРег("КопияЧека") Тогда
				УстановитьРежим("Копия");	
			ИначеЕсли ХозОперация=Справочники.ХозОперации.Чек Тогда
				УстановитьРежим("Чек");	
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаВозврат Тогда	
				УстановитьРежим("ЧекНаВозврат");
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат
				ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда	
				УстановитьРежим("ЧекНаОплатуВозврат");
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаОплату
				ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки Тогда	
				УстановитьРежим("ЧекНаОплату");
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер Тогда	
				УстановитьРежим("ПриходныйКассовыйОрдер");
			ИначеЕсли ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер Тогда	
				УстановитьРежим("РасходныйКассовыйОрдер");
			ИначеЕсли ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя Тогда
				УстановитьРежим("ВозвратТоваровОтПокупателя");
			ИначеЕсли ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
				УстановитьРежим("БанковскаяВыписка");
			КонецЕсли;	
		ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаВозврат И фкРучнойВводВозврата Тогда
			УстановитьРежим("ЧекНаВозврат");	
		КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(ВладелецФормы) Тогда
			ВладелецФормы.УправлениеИнтерфейсом();
		КонецЕсли;
		
		Возврат Параметры;
		
	КонецЕсли;
	
	ТекущаяФорма = Неопределено;
    
	Возврат Неопределено;
КонецФункции

// Функция проверяет наличие скидки подарка и открывает окно выбора подарка 
// Возвращаемое значение - Булево
//  Истина - Чек можно пробивать
//  Ложь   - Чек нельзя пробивать, критическая ситуация
Функция ДобавитьСкидкиПодарки(ЭтаФорма) Экспорт
	Если НЕ УстановленРежим("Чек") Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Если пользователю запрещена работа с подарками, то выходим
	Если фкЗапретитьПодарки Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Создаем вспомогательную таблицу для накапливания информации о параметрах подарков
	ПараметрыПодарков = Новый ТаблицаЗначений;
	ПараметрыПодарков.Колонки.Добавить("Скидка");
	ПараметрыПодарков.Колонки.Добавить("ВидПрайсЛиста");
	ПараметрыПодарков.Колонки.Добавить("НаСумму");
	ПараметрыПодарков.Колонки.Добавить("ЭтоСкидкаШапки");
	
	// Если скидка шапки является подарком
	Если СкидкаНаценка.ЭтоПодарок Тогда
		// Определим сумму чека без учета скидок
		СуммаВсего = Товары.Итог("Сумма");
		
		// Сформируем структуру отбора и получим значение скидки
		Отбор = Новый Структура("Скидка,РучнаяСкидка,БазаРасчета,ЭтоПодарок,ФронтОфис,ПодразделениеКомпании,Карточка,СкладКомпании,БэкОфис,Терминал", СкидкаНаценка, СкидкаНаценка.РучнаяСкидка, СуммаВсего, ИСТИНА, ИСТИНА);
		ЗаполнитьЗначенияСвойств(Отбор, ЭтотОбъект);
		Отбор.Вставить("СкидкаНаТовары",Истина);
		ПараметрыСкидкиШапки = рсСкидкиПолучитьШапочные(Дата, Отбор, СуммаВсего);
		
		Если ТипЗнч(ПараметрыСкидкиШапки) = Тип("ТаблицаЗначений") И ПараметрыСкидкиШапки.Количество() <> 0 Тогда
			ТекПараметры = ПараметрыПодарков.Добавить();
			ТекПараметры.Скидка			= СкидкаНаценка;
			ТекПараметры.ВидПрайсЛиста	= ПараметрыСкидкиШапки[0].ВидПрайсЛистаПодарка;
			ТекПараметры.НаСумму		= ПараметрыСкидкиШапки[0].МаксимальнаяСуммаПодарка;
			ТекПараметры.ЭтоСкидкаШапки	= ИСТИНА;
		КонецЕсли;
	КонецЕсли;
	
	// Подготовимся к обходу всей табличной части
	Отбор = Новый Структура("ЭтоПодарок,ФронтОфис,ПодразделениеКомпании,СкладКомпании,Карточка,БэкОфис,Терминал", ИСТИНА,  ИСТИНА);
	ЗаполнитьЗначенияСвойств(Отбор, ЭтотОбъект);
	Отбор.Вставить("СкидкаНаТовары",Истина);
	
	// Ищем подарки в табличной части
	Для Каждого ТекСтрока Из Товары Цикл
		Если ТекСтрока.СкидкаНаТовар.ЭтоПодарок Тогда
			// Определим вид прайс листа, максимальную сумму подарка и откроем форму выбора подарка
			Отбор.Вставить("Скидка",		ТекСтрока.СкидкаНаТовар);
			Отбор.Вставить("РучнаяСкидка",	ТекСтрока.СкидкаНаТовар.РучнаяСкидка);
			Отбор.Вставить("Объект",		ТекСтрока.Номенклатура);
			Отбор.Вставить("СуммаСтроки",	ТекСтрока.Сумма);
			Отбор.Вставить("Количество",	ТекСтрока.Количество * ТекСтрока.Коэффициент);
			Отбор.Вставить("БазаРасчета",	ТекСтрока.Сумма);
			ПараметрыСкидкиСтроки = рсСкидкиПолучитьСтрочные(Дата, Отбор);
			
			Если ТипЗнч(ПараметрыСкидкиСтроки) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
				ТекПараметры = ПараметрыПодарков.Добавить();
				ТекПараметры.Скидка			= ТекСтрока.СкидкаНаТовар;
				ТекПараметры.ВидПрайсЛиста	= ПараметрыСкидкиСтроки.ВидПрайсЛистаПодарка;
				ТекПараметры.НаСумму		= ПараметрыСкидкиСтроки.МаксимальнаяСуммаПодарка;
				ТекПараметры.ЭтоСкидкаШапки	= ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Приступаем к добавлению подарков
	РедактированиеСтроки = ИСТИНА;
	флНепрерывныйВыборПодарков = Истина;
	Для Каждого ТекПараметры Из ПараметрыПодарков Цикл
		ВыбратьПодарок(ЭтаФорма, ТекПараметры.ВидПрайсЛиста, ТекПараметры.НаСумму, ТекПараметры.ЭтоСкидкаШапки);
		Если флНепрерывныйВыборПодарков=Ложь Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	РедактированиеСтроки = ЛОЖЬ;
	Возврат флНепрерывныйВыборПодарков;
КонецФункции

// Процедура открывает окно выбора элемента прайс-листа добавляет 
Процедура ВыбратьПодарок(ЭтаФорма, ВидПрайсЛиста, НаСумму, ЭтоСкидкаШапки = ЛОЖЬ)
	
	Если ВидПрайсЛиста = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаИерархииПодразделений = Новый ТаблицаЗначений;
	ТаблицаИерархииПодразделений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаИерархииПодразделений.Колонки.Добавить("Уровень",       Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10)));
	
	ТекущееПодразделение = ПодразделениеКомпании;
	Уровень = 0;
	Пока ЗначениеЗаполнено(ТекущееПодразделение) Цикл
		СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
		СтрокаПодразделения.Подразделение = ТекущееПодразделение;
		СтрокаПодразделения.Уровень = Уровень;
		// Получаем родителя подразделения
		ТекущееПодразделение = ТекущееПодразделение.Родитель;
		Уровень = Уровень + 1;
	КонецЦикла;
	СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
	СтрокаПодразделения.Подразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
	СтрокаПодразделения.Уровень = Уровень;
	
	ТекстЦена     = "ТаблицаЦен.Цена";
	РабочийТипЦен = ТипЦен;
	
	Запрос = Новый Запрос;
	
	ТекстСоединенийПроцентыСкидкиНаценки = "";
	// Если расчетная цена получим базовую цену
	Счетчик = 1;
	Пока РабочийТипЦен.Рассчитывается Цикл
		// ведется ли расчет по типу номенклатуры
		Если НаСумму > 0 Тогда
			Если РабочийТипЦен.ПроцентыСкидкиНаценки.Количество()>0 Тогда
				
				ИмяТаблицы = "ПроцентыСкидкиНаценки" + Строка(Счетчик);
				ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК " + ИмяТаблицы + "
				|ПО
				|	" + ИмяТаблицы + ".Ссылка = &ТипЦены" + Строка(Счетчик) + " И 
				|	ТаблицаЦен.Номенклатура.ТипНоменклатуры = " + ИмяТаблицы + ".ТипНоменклатуры";
				
				ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
				
				Запрос.УстановитьПараметр("ТипЦены" + Строка(Счетчик), РабочийТипЦен);
				
				Счетчик = Счетчик + 1;
				
			Иначе
				
				Если РабочийТипЦен.ПроцентСкидкиНаценки <> 0 Тогда
					ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
	КонецЦикла;
	
	ТекстСоединениеКурсов = "";
	Если НаСумму > 0 Тогда
		
		Если НЕ РабочийТипЦен.ЦенаВключаетНДС Тогда
			ТекстЦена = "(" + ТекстЦена + " * (1 + ТаблицаЦен.Номенклатура.СтавкаНДС.Ставка/100))";
		КонецЕсли;
		
		ТекстЦена = ТекстЦена + "*КурсыВалютСрезПоследних.Курс/ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1)";
		
		Если РабочийТипЦен.ВВалютеУчета Тогда
			ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалютСрезПоследних
			|ПО
			|	ТаблицаЦен.Номенклатура.ВалютаУчета = КурсыВалютСрезПоследних.Валюта"
		Иначе
			ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &ВалютаКурса) КАК КурсыВалютСрезПоследних
			|ПО
			|	ИСТИНА";
			Запрос.УстановитьПараметр("ВалютаКурса", РабочийТипЦен.ВалютаЦены);
		КонецЕсли;
		
	КонецЕсли;
	
	// Определим и наложим дополнительный отбора по виду прайс-листа
	ДопТекстЗапроса = "";
	ФильтрПоВидуПрайсЛиста = "";
	Если НЕ ВидПрайсЛиста.Пустая() Тогда
		ДопТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрайсЛист.Номенклатура
		|ПОМЕСТИТЬ
		|	ТаблицаОтбораНоменклатуры
		|ИЗ
		|	Справочник.ПрайсЛист КАК ПрайсЛист
		|ГДЕ
		|	ПрайсЛист.Владелец = &ВидПрайсЛиста
		|;
		|";
		ФильтрПоВидуПрайсЛиста = " И
		|				Номенклатура В (ВЫБРАТЬ Номенклатура ИЗ ТаблицаОтбораНоменклатуры)";
	КонецЕсли;
	
	ТекстЗапроса = ДопТекстЗапроса + "
	|ВЫБРАТЬ
	|	ТаблицаПодразделений.Подразделение КАК Подразделение,
	|	ТаблицаПодразделений.Уровень КАК Уровень
	|ПОМЕСТИТЬ
	|	ТаблицаПодразделений
	|ИЗ
	|	&ТаблицаПодразделений КАК ТаблицаПодразделений
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;
	|
	|ВЫБРАТЬ
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	|	ТаблицаПодразделений.Уровень КАК Уровень,
	|	ЦеныСрезПоследних.Цена КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦен
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			&Дата,
	|			Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) И 
	|			(ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) ИЛИ 
	|			Номенклатура.БазоваяЕдиницаИзмерения = ЕдиницаИзмерения.ЕдиницаПоКлассификатору И 
	|			ЕдиницаИзмерения.Коэффициент = 1) И 
	|			ТипЦен = &ТипЦен И 
	|			ПодразделениеКомпании В (ВЫБРАТЬ Подразделение ИЗ ТаблицаПодразделений) "+ФильтрПоВидуПрайсЛиста+" ) КАК ЦеныСрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаПодразделений КАК ТаблицаПодразделений
	|ПО
	|	ЦеныСрезПоследних.ПодразделениеКомпании = ТаблицаПодразделений.Подразделение
	|ГДЕ
	|	ЦеныСрезПоследних.Цена > 0
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаПодразделений
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаЦен КАК ТаблицаЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура
	|ИЗ
	|	СрезПоследних КАК СрезПоследних
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаЦен КАК ТаблицаЦен
	|ПО
	|	СрезПоследних.Номенклатура = ТаблицаЦен.Номенклатура И 
	|	СрезПоследних.Уровень = ТаблицаЦен.Уровень
	|"+ТекстСоединенийПроцентыСкидкиНаценки+?(НаСумму > 0, "
	|ГДЕ
	|	&МаксимальнаяСуммаПодарка >= ("+ТекстЦена+")", "") + "
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Номенклатура
	|";
	
	// Создадим объект запроса и установим параметры
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Дата",						ЭтотОбъект.Дата);
	Запрос.УстановитьПараметр("ТипЦен",						РабочийТипЦен);
	Запрос.УстановитьПараметр("ВидПрайсЛиста",				ВидПрайсЛиста);
	Запрос.УстановитьПараметр("МаксимальнаяСуммаПодарка",	НаСумму);
	Запрос.УстановитьПараметр("ТаблицаПодразделений",	ТаблицаИерархииПодразделений);
	
	// Выполним запрос и сформируем список товаров
	СписокЗначений = Новый СписокЗначений();
	СписокЗначений.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	
	// Сформируем структуру параметров отбора
	ПараметрыОтбора		= Новый Структура("Ссылка", Новый Структура("Значение,ВидСравнения", СписокЗначений, ВидСравнения.ВСписке));
	ПараметрыОткрытия	= Новый Структура("Отбор", ПараметрыОтбора);
	
	// Установим значение глобальной переменной для корректного выбора характеристик
	ПодаркиМаксимальнаяСумма = НаСумму;
	
	// Откроем форму и проанализируем что было выбрано
	ФормаВыбора	= ПолучитьФормуФронта("ВыборПодарка", ЭтаФорма,, ПараметрыОткрытия);
	Номенклатура = ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборПодарка");
	Если обЗначениеНеЗаполнено(Номенклатура) Тогда
		флНепрерывныйВыборПодарков = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Процедура открывает окно выбора элемента с отбором по максимальной цене 
Функция ВыбратьХарактеристикуПодарка(ЭтаФорма, Товар)
	// Сформируем структуру параметров отбора
	Владелец = ?(Товар.ТипНоменклатуры.ИспользованиеХарактеристик=1, Товар.ТипНоменклатуры, Товар);
	Отбор = Новый Структура("Владелец", Владелец);
	
	// Если цены в разрезе характеристик и есть ограничение по сумме подарка отбросим заведомо невозможные позиции
 	Если ТипЦен.АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике И ПодаркиМаксимальнаяСумма>0 Тогда
		// Выберем не подходящие характеристики, отсеем их по правилу "Не в списке"
		
		ТаблицаИерархииПодразделений = Новый ТаблицаЗначений;
		ТаблицаИерархииПодразделений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
		ТаблицаИерархииПодразделений.Колонки.Добавить("Уровень",       Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10)));
		
		ТекущееПодразделение = ПодразделениеКомпании;
		Уровень = 0;
		Пока ЗначениеЗаполнено(ТекущееПодразделение) Цикл
			СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
			СтрокаПодразделения.Подразделение = ТекущееПодразделение;
			СтрокаПодразделения.Уровень = Уровень;
			// Получаем родителя подразделения
			ТекущееПодразделение = ТекущееПодразделение.Родитель;
			Уровень = Уровень + 1;
		КонецЦикла;
		СтрокаПодразделения = ТаблицаИерархииПодразделений.Добавить();
		СтрокаПодразделения.Подразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
		СтрокаПодразделения.Уровень = Уровень;
		
		ТекстЦена       = "ТаблицаЦен.Цена";
		ТипНоменклатуры = Товар.ТипНоменклатуры;
		РабочийТипЦен   = ТипЦен;
		
		// Если расчетная цена получим базовую цену
		Пока РабочийТипЦен.Рассчитывается Цикл
			ПроцентСкидкиПоТипу = РабочийТипЦен.ПроцентыСкидкиНаценки.Найти(ТипНоменклатуры, "ТипНоменклатуры");
			Если ПроцентСкидкиПоТипу = Неопределено Тогда
				ПроцентСкидкиНаценки = РабочийТипЦен.ПроцентСкидкиНаценки;
			Иначе
				ПроцентСкидкиНаценки = ПроцентСкидкиПоТипу.ПроцентСкидкиНаценки;
			КонецЕсли;
			
			Если ПроцентСкидкиНаценки <> 0 Тогда
				ТекстЦена = "("+ТекстЦена + "+" + ТекстЦена + "*" + Формат(ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
			КонецЕсли;
			РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
		КонецЦикла;
		
		Если НЕ РабочийТипЦен.ЦенаВключаетНДС Тогда
			ТекстЦена = "(" + ТекстЦена + " * (1 + " + Формат(Товар.СтавкаНДС.Ставка/100,"ЧРД=.") + "))";
		КонецЕсли;
		
		ВалютаЦены = обВалютаТипаЦены(Товар, РабочийТипЦен, Ложь);
		Если обЗначениеНеЗаполнено(ВалютаЦены) Тогда
			ВалютаЦены = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		КонецЕсли;
		СтруктураКурса = обКурсДляВалюты(ВалютаЦены, Дата);
		КурсВалютыЦены = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаПодразделений.Подразделение КАК Подразделение,
		|	ТаблицаПодразделений.Уровень КАК Уровень
		|ПОМЕСТИТЬ
		|	ТаблицаПодразделений
		|ИЗ
		|	&ТаблицаПодразделений КАК ТаблицаПодразделений
		|ИНДЕКСИРОВАТЬ ПО
		|	Подразделение
		|;
		|";
		
		Если Товар.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	ЦеныСрезПоследних.ХарактеристикаНоменклатуры КАК Характеристика,
			|	ТаблицаПодразделений.Уровень КАК Уровень,
			|	ЦеныСрезПоследних.Цена КАК Цена
			|ПОМЕСТИТЬ
			|	ТаблицаЦен
			|ИЗ
			|	РегистрСведений.Цены.СрезПоследних(
			|			&Дата,
			|			Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) И 
			|			ТипЦен = &ТипЦен И 
			|			Номенклатура = &Номенклатура И 
			|			(НЕ ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) И 
			|			ПодразделениеКомпании В (ВЫБРАТЬ Подразделение ИЗ ТаблицаПодразделений)) КАК ЦеныСрезПоследних
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	ТаблицаПодразделений КАК ТаблицаПодразделений
			|ПО
			|	ЦеныСрезПоследних.ПодразделениеКомпании = ТаблицаПодразделений.Подразделение
			|ГДЕ
			|	ЦеныСрезПоследних.Цена > 0
			|;
			|
			|УНИЧТОЖИТЬ
			|	ТаблицаПодразделений
			|;
			|
			|ВЫБРАТЬ
			|	ТаблицаЦен.Характеристика КАК Характеристика
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТаблицаЦен.Характеристика КАК Характеристика,
			|		МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
			|	ИЗ
			|		ТаблицаЦен КАК ТаблицаЦен
			|	СГРУППИРОВАТЬ ПО
			|		ТаблицаЦен.Характеристика) КАК СрезПоследних
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	ТаблицаЦен КАК ТаблицаЦен
			|ПО
			|	СрезПоследних.Характеристика = ТаблицаЦен.Характеристика И 
			|	СрезПоследних.Уровень = ТаблицаЦен.Уровень
			|ГДЕ
			|	&МаксимальнаяСуммаПодарка < ("+ТекстЦена+"*&КурсВалютыЦены)
			|";
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	ТаблицаХарактеристик.Ссылка КАК Характеристика
			|ПОМЕСТИТЬ
			|	ТаблицаХарактеристик
			|ИЗ
			|	Справочник.ХарактеристикиНоменклатуры КАК ТаблицаХарактеристик
			|ГДЕ
			|	ТаблицаХарактеристик.Владелец = &Владелец
			|;
			|
			|ВЫБРАТЬ
			|	ЦеныСрезПоследних.ХарактеристикаНоменклатуры КАК Характеристика,
			|	ТаблицаПодразделений.Уровень КАК Уровень,
			|	ЦеныСрезПоследних.Цена КАК Цена
			|ПОМЕСТИТЬ
			|	ТаблицаЦен
			|ИЗ
			|	РегистрСведений.Цены.СрезПоследних(
			|			&Дата,
			|			Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) И 
			|			ТипЦен = &ТипЦен И 
			|			Номенклатура = &Номенклатура И 
			|			ПодразделениеКомпании В (ВЫБРАТЬ Подразделение ИЗ ТаблицаПодразделений)) КАК ЦеныСрезПоследних
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	ТаблицаПодразделений КАК ТаблицаПодразделений
			|ПО
			|	ЦеныСрезПоследних.ПодразделениеКомпании = ТаблицаПодразделений.Подразделение
			|ГДЕ
			|	ЦеныСрезПоследних.Цена > 0
			|;
			|
			|УНИЧТОЖИТЬ
			|	ТаблицаПодразделений
			|;
			|
			|ВЫБРАТЬ
			|	ТаблицаЦен.Характеристика КАК Характеристика,
			|	"+ТекстЦена+"*&КурсВалютыЦены КАК Цена
			|ПОМЕСТИТЬ
			|	ТаблицаЦенСрезПоследних
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТаблицаЦен.Характеристика КАК Характеристика,
			|		МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень
			|	ИЗ
			|		ТаблицаЦен КАК ТаблицаЦен
			|	СГРУППИРОВАТЬ ПО
			|		ТаблицаЦен.Характеристика) КАК СрезПоследних
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	ТаблицаЦен КАК ТаблицаЦен
			|ПО
			|	СрезПоследних.Характеристика = ТаблицаЦен.Характеристика И 
			|	СрезПоследних.Уровень = ТаблицаЦен.Уровень
			|;
			|
			|УНИЧТОЖИТЬ
			|	ТаблицаЦен
			|;
			|
			|ВЫБРАТЬ
			|	ТаблицаХарактеристик.Характеристика КАК Характеристика
			|ИЗ
			|	ТаблицаХарактеристик КАК ТаблицаХарактеристик
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	ТаблицаЦенСрезПоследних КАК ТаблицаЦен
			|ПО
			|	ТаблицаХарактеристик.Характеристика = ТаблицаЦен.Характеристика
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	ТаблицаЦенСрезПоследних КАК ТаблицаЦенОбщ
			|ПО
			|	ТаблицаЦенОбщ.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|ГДЕ
			|	&МаксимальнаяСуммаПодарка < ЕСТЬNULL(ТаблицаЦен.Цена, ЕСТЬNULL(ТаблицаЦенОбщ.Цена, 0))
			|";
		КонецЕсли;
		
		// Создадим объект запроса и установим параметры
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Дата",                     Дата);
		Запрос.УстановитьПараметр("ТипЦен",                   РабочийТипЦен);
		Запрос.УстановитьПараметр("КурсВалютыЦены",           КурсВалютыЦены);
		Запрос.УстановитьПараметр("МаксимальнаяСуммаПодарка", ПодаркиМаксимальнаяСумма);
		Запрос.УстановитьПараметр("Номенклатура",             Товар);
		Запрос.УстановитьПараметр("Владелец",                 Владелец);
		Запрос.УстановитьПараметр("ТаблицаПодразделений",     ТаблицаИерархииПодразделений);
		
		// Выполним запрос и сформируем список товаров
		СписокЗначений = Новый СписокЗначений();
		СписокЗначений.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Характеристика"));
	
		// Сформируем структуру параметров отбора
		Если СписокЗначений.Количество()>0 Тогда
			Отбор.Вставить("Ссылка", Новый Структура("Значение, ВидСравнения", СписокЗначений, ВидСравнения.НеВСписке));
		КонецЕсли;
	КонецЕсли;
	ПараметрыОткрытия = Новый Структура("Номенклатура,Отбор", Товар, Отбор);
	
	// Открываем форму выбора
	ФормаВыбора = ПолучитьФормуФронта("ВыборХарактеристики", ЭтаФорма,, ПараметрыОткрытия);
	ХарактеристикаНоменклатуры = ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборХарактеристики");
	Если обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры) Тогда
		флНепрерывныйВыборПодарков = Ложь;
	КонецЕсли;
    Возврат ХарактеристикаНоменклатуры;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ СВОЙСТВ ЧЕКА

// Процедура получает доступные свойства чека и кэширует их для дальнейшего использования
//
// Параметры:
//                               
// Возвращаемое значение:
//
Процедура ПолучитьТаблицуСвойств() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
 	 |ВЫБРАТЬ
	 |	НазначенияСвойствОбъектовВидыСвойств.Свойство
	 |ИЗ
	 |	ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойствОбъектовВидыСвойств
	 |ГДЕ
	 |	НазначенияСвойствОбъектовВидыСвойств.Ссылка = &Ссылка";
	 
	 Запрос.УстановитьПараметр("Ссылка",ПланыВидовХарактеристик.НазначенияСвойствОбъектов["Документ_Чек"]);
	 тзСвойства = Запрос.Выполнить().Выгрузить();
КонецПроцедуры

// Получает значения заданных свойства чека
//
// Параметры:
//                               
// Возвращаемое значение:
//   тзЗначения - таблица выбранных свойств и значений или Неопределенно в случаи, если значение не выбрали
//
Функция ПолучитьЗначениеСвойств(ЭтаФорма) Экспорт
	
	// Если у нас чек на возврат, то получим свойства по основанию
	Если (УстановленРежим("ЧекНаВозврат") ИЛИ УстановленРежим("ВозвратТоваровОтПокупателя")) 
		  И ЗначениеЗаполнено(ДокументОснование) Тогда
		  
		Возврат ПолучитьЗначениеСвойствПоВозврату(ДокументОснование);
	КонецЕсли;
	
	тзЗначения = Новый ТаблицаЗначений;
	тзЗначения.Колонки.Добавить("Свойство");
	тзЗначения.Колонки.Добавить("Значение");
	
	Для Каждого стрТЗ Из тзСвойства Цикл
		// Обрабатываем только справочники
		МассивТипов = стрТЗ.Свойство.ТипЗначения.Типы();
		Если МассивТипов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ТипЗначенияСвойства = МассивТипов[0];
		
		Если НЕ Справочники.ТипВсеСсылки().СодержитТип(ТипЗначенияСвойства) Тогда
			Продолжить;
		КонецЕсли;
		
		МетаданныеСпр = Метаданные.НайтиПоТипу(ТипЗначенияСвойства);
		
		ТипЗначения = Новый  ОписаниеТипов("СправочникСписок."+МетаданныеСпр.Имя);
		
		ПараметрыОткрытия = Новый Структура("ТипЗначения,Свойство",ТипЗначения, стрТЗ.Свойство); 
		
		Форма = ПолучитьФормуФронта("ВыборСвойства", ЭтаФорма,, ПараметрыОткрытия);
		Значение = ОткрытьФормуВыбора(Форма, ПараметрыОткрытия, "ВыборСвойства");
		
		// Значение не выбрали, ай ай не хорошо, попросим повторить операцию
		Если Значение = Неопределено Тогда Возврат Неопределено КонецЕсли;
		 
		СтрокаЗначение 		 	= тзЗначения.Добавить();
		СтрокаЗначение.Свойство = стрТЗ.Свойство;
		СтрокаЗначение.Значение = Значение;
	КонецЦикла;
	
	Возврат тзЗначения;
КонецФункции

// Записывает значения свойств чека в регистр сведений
// Параметры:
//	 тзЗначенияСвойств - таблица значений свойств чека
//                               
// Возвращаемое значение:
//   Истина если запись прошла успешна иначе ложь 
//
Функция ЗаписатьЗначениеСвойств(тзЗначенияСвойств)  Экспорт
	Если обЗначениеНеЗаполнено(Ссылка) ИЛИ (НЕ ТипЗнч(тзЗначенияСвойств)=Тип("ТаблицаЗначений")) Тогда
		Возврат ЛОЖЬ;
	ИначеЕсли НЕ Метаданные.ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Тип.СодержитТип(ТипЗнч(Ссылка)) Тогда
		Возврат ИСТИНА;
	КонецЕсли;
	
	// Производим заполнение набора записей
	НаборЗаписейСвойстваОбъектов = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьНаборЗаписей();
	НаборЗаписейСвойстваОбъектов.Отбор.Объект.Установить(Ссылка, ИСТИНА);
	Для Каждого стрСвойство из тзЗначенияСвойств Цикл
		НоваяЗапись = НаборЗаписейСвойстваОбъектов.Добавить();
		НоваяЗапись.Объект 	= Ссылка;
		НоваяЗапись.Свойство= стрСвойство.Свойство;
		НоваяЗапись.Значение= стрСвойство.Значение;
	КонецЦикла;
	
	// Производим запись набор записей
	Попытка
		НаборЗаписейСвойстваОбъектов.Записать(ИСТИНА);
		Возврат ИСТИНА;
	Исключение
		Возврат ЛОЖЬ;
	КонецПопытки;
КонецФункции

// ФУНКЦИИ ДЛЯ РАБОТЫ С ТОВАРОМ

// Функция возвращает цену товара.
//
// Параметры:
//	ТипЦен       - СправочникСсылка - Тип цен для которого получаем цену.
//  Номенклатура - СправочникСсылка - Товар для которого цена получается.
//	НаМомент     - Дата 			-  Дата,МоментВремени,ДокументСсылка на какой момент получаем цену,
//				 если неопределен, то на конец рабочей даты
//	Контрагент   - СправочникСсылка - для получения цен контрагентов.
//	Валюта       - СправочникСсылка - Валюта (элемент справочника "Валюты")
//	Курс	     - Число 			- Курс валюты, с учетом кратности
//
// Возвращаемое значение:
//	Число - Цена
//
Функция ПолучитьЦену(Номенклатура,ХарактеристикаНоменклатуры=Неопределено) Экспорт
	
	Валюта = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(ВалютаДокумента,Дата);
	Курс  = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Цена = обПолучитьЦену(ТипЦен,Номенклатура,,,Валюта,Курс,ХарактеристикаНоменклатуры,,ПодразделениеКомпании);
	
	Возврат Цена;
	
КонецФункции // обПолучитьЦену()

// получить доступные виды оплат
Функция ПолучитьДоступныеВидыОплат() Экспорт
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		Возврат ЛОЖЬ;
	КонецЕсли;
	тзДоступныеВидыОплат.Очистить();
	
	ЗначениеСвойства = "";
	ТекстОшибки      = "";
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_ФР, "НаименованияВидовПлатежей", ЗначениеСвойства, ТекстОшибки);
	ЗначениеСвойства = СтрЗаменить(ЗначениеСвойства, ";", Символы.ПС);
	
	Для Сч = 1 по СтрЧислоСтрок(ЗначениеСвойства) Цикл
		стрТипОплаты = СокрЛП(СтрПолучитьСтроку(ЗначениеСвойства,Сч));
		НомерОплаты  = Лев(стрТипОплаты,1);
		стрТипОплаты = СокрЛП(СтрЗаменить(стрТипОплаты,НомерОплаты+"=",""));
		ТипОплаты = Справочники.ТипыОплат.НайтиПоНаименованию(стрТипОплаты);
		
		Если НЕ ТипОплаты.Пустая() Тогда
			СтрокаОплат 		  = тзДоступныеВидыОплат.Добавить();
			СтрокаОплат.Номер 	  = Число(НомерОплаты);
			СтрокаОплат.ТипОплаты = ТипОплаты;
			
			Если ТипОплаты.ВидОплаты = Перечисления.ВидыОплаты.НаличныйРасчет Тогда
				флДоступностьНал = Истина;
			Иначе
				флДоступностьБН = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Возвращает соответствие в котором перечислены виды номенклатуры, которые нельзя вводить
// в таблицу товаров. Если таких нет, то возвращает неопределено
//
// Возвращаемое значение;
//  Результат - Соответствие.
//
Функция ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры() Экспорт
	Результат=Новый Соответствие();
	Возврат Результат;
КонецФункции // ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры()

Функция ПечатьПречекаПлатежа(Параметры)
	
	Если ПустаяСтрока(GUID_ФР) И ПустаяСтрока(GUID_ПЧ) Тогда
		ОткрытьДиалог("Не заданы ни ФР и ни Принтер квитанций! Печать невозможна!", СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(GUID_ФР) Тогда
		GUID_Устройства = GUID_ПЧ;
		ТаймаутУстройства = ТаймаутПЧ;
	Иначе
		GUID_Устройства = GUID_ФР;
		ТаймаутУстройства = ТаймаутФР;
	КонецЕсли;
	
	ПрограммнаяШапка = 0; ТекстШапки = "";
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_Устройства,"ПрограммнаяШапка",ПрограммнаяШапка);
	Если ПрограммнаяШапка = 1 Тогда  
		глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_Устройства,"ТекстШапки",ТекстШапки);
		ТекстШапки = СтрЗаменить(ТекстШапки, """", Символы.ПС);
	КонецЕсли;
		
	// Параметры введены,получим их
	Оператор=Параметры.Получить(0).Значение;
	тПараметр1=Параметры.Получить(1).Значение;
	тПараметр2=Параметры.Получить(2).Значение;
	тПараметр3=Параметры.Получить(3).Значение;
	Параметр1=Параметры.Получить(4).Значение;
	Параметр2=Параметры.Получить(5).Значение;
	Параметр3=Параметры.Получить(6).Значение;
	СуммаПлатежа=Параметры.Получить(7).Значение;
	Сумма=Параметры.Получить(8).Значение;
	СуммаКомиссии=СуммаПлатежа-Сумма; 
	Номенклатура = Параметры.Получить(9).Значение;
	ВидПлатежа = Параметры.Получить(10).Значение;
	
	// Текст пречека
	ПречекШапка = 
	        "Услуга: "+СокрЛП(Номенклатура.Наименование)+Символы.ПС+
			"Вид платежа: "+ВидПлатежа+Символы.ПС+
			?(обЗначениеНеЗаполнено(Параметр1)=0,тПараметр1+" "+Параметр1+Символы.ПС,"")+
			?(обЗначениеНеЗаполнено(Параметр2)=0,тПараметр2+" "+Параметр2+Символы.ПС,"")+
			?(обЗначениеНеЗаполнено(Параметр3)=0,тПараметр3+" "+Параметр3+Символы.ПС,"")+
			"Сумма платежа: "+ФРМСумма(Сумма)+"руб."+Символы.ПС+
			"Сумма комиссии: "+ФРМСумма(СуммаКомиссии)+"руб."+Символы.ПС+
			"Сумма итого: "+ФРМСумма(СуммаПлатежа)+"руб.";
			
    Текст = ПречекШапка+Символы.ПС+Символы.ПС+
			"Подпись клиента: _________________
			|
			|Подпись кассира: _________________
			|"+Символы.ПС+Символы.ПС+Символы.ПС+Символы.ПС;
	// Печатаем первую копию		
	ПараметрыДействия=Рарус_Компонента.СоздатьПараметры(4,1);
	ПараметрыДействия.SetValue(0,0,ТекстШапки);
	ПараметрыДействия.SetValue(1,0,Текст);
	ПараметрыДействия.SetValue(2,0,"");   
	ПараметрыДействия.SetValue(3,0,0);
	Результат = ВыполнитьКомандуОборудования("ПринтерКвитанций",GUID_Устройства,"Напечатать",ПараметрыДействия,ТаймаутУстройства,ТекстОшибки);
	
	Если НЕ Результат Тогда
		ОткрытьДиалог("ОШИБКА ПЕЧАТИ КВИТАНЦИИ ПЛАТЕЖА!"+Символы.ПС+
					  "АВТОРИЗАЦИЯ ПЛАТЕЖА ПРЕРВАНА !!!",СтатусСообщения.ОченьВажное);
		Возврат Ложь;			  
	КонецЕсли;
	
	// Печатаем вторую копию
	ПараметрыДействия=Рарус_Компонента.СоздатьПараметры(4,1);
	ПараметрыДействия.SetValue(0,0,ТекстШапки);
	ПараметрыДействия.SetValue(1,0,Текст);
	ПараметрыДействия.SetValue(2,0,"");   
	ПараметрыДействия.SetValue(3,0,0);
	Результат = ВыполнитьКомандуОборудования("ПринтерКвитанций",GUID_Устройства,"Напечатать",ПараметрыДействия,ТаймаутУстройства,ТекстОшибки);
	Если НЕ Результат Тогда
		ОткрытьДиалог("ОШИБКА ПЕЧАТИ КВИТАНЦИИ ПЛАТЕЖА!"+Символы.ПС+
					  "АВТОРИЗАЦИЯ ПЛАТЕЖА ПРЕРВАНА !!!",СтатусСообщения.ОченьВажное);
		Возврат Ложь;			  
	КонецЕсли;

	// А не ошибся ли кассир?
	Результат = ОткрытьДиалогВопроса("КЛИЕНТ ПОДТВЕРДИЛ ПРАВИЛЬНОСТЬ ПЛАТЕЖА ?"+Символы.ПС+ПречекШапка);
	
	Возврат Результат;
КонецФункции

Процедура ФискализироватьПлатежи()
	
	Попытка
		НачатьТранзакцию();
		// Пройдемся по таблице чека, найдем и перезапишем платежи
		Для Каждого СтрокаПлатежа Из тзПлатежиЗаУслуги Цикл
			
			Платеж = РегистрыСведений.Платежи.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Платеж, СтрокаПлатежа);
			Платеж.Прочитать();
			// Запишем фискальные реквизиты в платеж
			Платеж.НомерЧека = НомерЧека;
			Платеж.ДатаЧека  = ДатаФР;
			Платеж.ВремяЧека = ДатаФР;
			Платеж.Фискализация = 1;
			Платеж.Записать();
			
			ЗаполнитьЗначенияСвойств(СтрокаПлатежа,Платеж);
		КонецЦикла;
		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
	
		// Шеф все пропало..., больше нам не чего не остается, как сообщить об этом и вернуть в зад транзакцию записи платежей
		ОткрытьДиалог("ОШИБКА: "+ОписаниеОшибки()+Символы.ПС+
					  "НЕВОЗМОЖНО ЗАПИСАТЬ ФИСКАЛЬНЫЕ РЕКВИЗИТЫ ЧЕКА В ПЛАТЕЖИ"+Символы.ПС+Символы.ПС+
					  "НОМЕР ЧЕКА "+СокрЛП(НомерЧека)+" ОТ "+Строка(ДатаФР)+Символы.ПС+Символы.ПС+
					  "СООБЩИТЕ СИСТЕМНОМУ АДМИНИСТРАТОРУ !!!",СтатусСообщения.ОченьВажное);
	КонецПопытки;
	
КонецПроцедуры

// Удаляет все платежи из таблицы платежей
Процедура УдалитьПлатежи()
	
	Попытка
		НачатьТранзакцию();
		// Пройдемся по таблице чека, найдем и перезапишем платежи
		Для Каждого СтрокаПлатежа Из тзПлатежиЗаУслуги Цикл
			
			Платеж = РегистрыСведений.Платежи.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Платеж, СтрокаПлатежа);
			Платеж.Прочитать();
			Платеж.Удалить();			
		КонецЦикла;
		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();	
		ОткрытьДиалог("ОШИБКА: "+ОписаниеОшибки()+Символы.ПС+
					  "НЕВОЗМОЖНО УДАЛИТЬ ПЛАТЕЖИ"+Символы.ПС+Символы.ПС+
					  "НОМЕР ЧЕКА "+СокрЛП(НомерЧека)+" ОТ "+Строка(ДатаФР)+Символы.ПС+Символы.ПС+
					  "СООБЩИТЕ СИСТЕМНОМУ АДМИНИСТРАТОРУ !!!",СтатусСообщения.ОченьВажное);
	КонецПопытки;
	тзПлатежиЗаУслуги.Очистить();
КонецПроцедуры

// Удаляет платеж из таблицы платежей
Процедура УдалитьПлатеж(НомерПлатежа)
	
	// перенумеруем индексы в табличной части
	Для Сч = 0 По Товары.Количество()-1 Цикл
		
		Товар = Товары.Получить(Сч);
		
		Если (Товар.Платеж) И (Товар.НомерПлатежа > НомерПлатежа) Тогда
			Товар.НомерПлатежа = Товар.НомерПлатежа -1;
		КонецЕсли;			
	КонецЦикла;
	
	Попытка
		НачатьТранзакцию();
		
		СтрокаПлатежа = тзПлатежиЗаУслуги.Получить(НомерПлатежа);
		Платеж = РегистрыСведений.Платежи.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Платеж, СтрокаПлатежа);
		Платеж.Прочитать();
		Платеж.Удалить();			
		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
		ОткрытьДиалог("ОШИБКА: "+ОписаниеОшибки()+Символы.ПС+
					  "НЕВОЗМОЖНО УДАЛИТЬ ПЛАТЕЖ"+Символы.ПС+Символы.ПС+
					  "СООБЩИТЕ СИСТЕМНОМУ АДМИНИСТРАТОРУ !!!",СтатусСообщения.ОченьВажное);
	КонецПопытки;

	// удалим платеж
	тзПлатежиЗаУслуги.Удалить(НомерПлатежа);
КонецПроцедуры

// Производит перенумерацию в таблице оплат, изменяя ФискальныйНомерПозиции
// заносит изменения в регистр сведений
Процедура ПеренумероватьТаблицуОплат()
	
	Если тзПлатежиЗаУслуги.Количество()=0 Тогда Возврат; КонецЕсли;
	
	Для Сч =0 По Товары.Количество()-1 Цикл
		
		Товар = Товары.Получить(Сч);
		
		Если Товар.Платеж Тогда
			
			СтрокаПлатежа = тзПлатежиЗаУслуги.Получить(Товар.НомерПлатежа);
			СтрокаПлатежа.ФискальныйНомерПозиции = Сч;
			Попытка
				НачатьТранзакцию();
				
				Платеж = РегистрыСведений.Платежи.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Платеж, СтрокаПлатежа);
				Платеж.Прочитать();
				// Запишем фискальные реквизиты в платеж
				Платеж.ФискальныйНомерПозиции = Сч;
				Платеж.Записать();
					
				ЗафиксироватьТранзакцию();	
			Исключение
				ОтменитьТранзакцию();
				ОткрытьДиалог("ОШИБКА ЗАПИСИ НОМЕРОВ ПЛАТЕЖЕЙ !"+Символы.ПС+
							  "НЕМЕДЛЕННО СООБЩИТЕ АДМИНИСТРАТОРУ !",СтатусСообщения.ОченьВажное);
			КонецПопытки;

		КонецЕсли;
		
	КонецЦикла
	
КонецПроцедуры

// заменяет шаблонный платеж на платеж по коду оператора
Процедура ЗаменитьПлатеж(Номенклатура,КодОператора)
	
	ВидПлатежа = Номенклатура.ВидПлатежа;
	
	// ищем соответствие в Виде платежа по коду оператора
	Для Каждого ТекСоответсвие Из ВидПлатежа.ТаблицаСоответствий Цикл
		КодыОператора = ТекСоответсвие.КодыОператора;
		Если Найти(КодыОператора, КодОператора)>0 Тогда
			Платеж = ТекСоответсвие.Платеж;
			Если НЕ обЗначениеНеЗаполнено(Платеж) Тогда
				Номенклатура = Платеж;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	    		
КонецПроцедуры
	
Функция АвторизоватьСотовыйПлатеж(Номенклатура) 
	
	// Все параметры оговорены в виде платежа
	ВидПлатежа=Номенклатура.ВидПлатежа;
	
	// Получим оборудование для авторизации
	АвторизаторПлатежей=ВидПлатежа.Авторизатор;
	Если АвторизаторПлатежей.Пустая() Тогда
		ОткрытьДиалог("ОШИБКА! АВТОРИЗАТОР ДЛЯ ДАННОГО ВИДА ПЛАТЕЖА НЕ ЗАДАН !", СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
	
	// Включим его
	Если НЕ ВключитьАвторизаторПлатежей(АвторизаторПлатежей) Тогда
		ОткрытьДиалог("ОШИБКА! АВТОРИЗАТОР НЕ ВКЛЮЧЕН !", СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
	
	СписокОператоров = Новый СписокЗначений;
	CписокКодов		 = Новый СписокЗначений;
	// Получим у авторизатора список доступных авторизаторов
	ПолучитьСписокОператоровИКодов(СписокОператоров,CписокКодов);
	
	// В параметрах передадим товар и дисплей, а получим результат
	Параметры = Новый СписокЗначений;
	Параметры.Добавить(СписокОператоров);
	Параметры.Добавить(Номенклатура);
	Параметры.Добавить(GUID_Дисплей);
	Параметры.Добавить(CписокКодов);
	
	Хранилище = ВидПлатежа.Обработка;
    ВнешняяОбработка=Хранилище.Получить();
	Если ТипЗнч(ВнешняяОбработка)<>Тип("ДвоичныеДанные") Тогда
		ОткрытьДиалог("Не удалось получить обработку для вида платежа: "+ВидПлатежа.Наименование,СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли; 
	
	Каталог = КаталогВременныхФайлов()+"temp.epf";

	ВнешняяОбработка.Записать(Каталог);   //КаталогВременныхФайлов()
	ВнешняяОбработка = ВнешниеОбработки.Создать(Каталог);
	ФормаВыбора = ВнешняяОбработка.ПолучитьФорму();
	ОткрытьФормуВыбора( ФормаВыбора, Параметры, "ВводПараметровПлатежа");

	// проверим, что нам вернули обратно
	Если (ТипЗнч(Параметры)<>Тип("СписокЗначений"))ИЛИ(НЕ Параметры.Количество()=9) Тогда Возврат Ложь; КонецЕсли;
	
	// Скорректируем сумму авторизации с учетом взятия комиссионного вознаграждения
	// (на сумму, которая берется с клиента это не влияет)
	СуммаПлатежа=Параметры.Получить(7).Значение;
	КодОператора = Параметры.Получить(8).Значение; Параметры.Удалить(8);
	ТекСуммаПлатежа = СуммаПлатежа;
	ДоговорКомиссии=ВидПлатежа.Договор;
	Если ДоговорКомиссии.ТипСуммыКомиссии = 0 Тогда
		СуммаЗаВычетомКомиссии=СуммаПлатежа-Окр((СуммаПлатежа*ДоговорКомиссии.ПроцентКомиссионногоВознаграждения)/100,2);
	ИначеЕсли ДоговорКомиссии.ТипСуммыКомиссии = 1 Тогда
		СуммаЗаВычетомКомиссии=СуммаПлатежа-ДоговорКомиссии.СуммаКомиссионногоВознаграждения; 
	КонецЕсли;
	
	Параметры.Добавить(СуммаЗаВычетомКомиссии);
	Параметры.Добавить(Номенклатура);
	Параметры.Добавить(ВидПлатежа);
		
	// Печать квитанции (пречека платежа)
	Если фкПечатьПречекаПлатежа Тогда
		Если НЕ	ПечатьПречекаПлатежа(Параметры) Тогда Возврат Ложь; КонецЕсли;
	КонецЕсли;
	
	// проведем авторизацию платежа
	Если НЕ АвторизацияСотовогоПлатежа(Параметры) Тогда
		
		// если нельзя проводить асинхронные платежи то заканчиваем
		Если НЕ фкПроводитьАсинхронныеПлатежи Тогда	
			Возврат Ложь;
		КонецЕсли;
		
		// Если "Нет такого номера", отменим платеж
		КодОшибки = Параметры.Получить(12).Значение;
		Если КодОшибки=123 Тогда
			Возврат Ложь;
		КонецЕсли;
		
		// спросим можно ли провести авторизацию позже
		Если НЕ ОткрытьДиалогВопроса("ОТЛОЖИТЬ ПЛАТЕЖ ДЛЯ АВТОРИЗАЦИИ ПОЗЖЕ ?") Тогда;
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	КодОшибки = Параметры.Получить(12).Значение;
	ОписаниеОшибки = Параметры.Получить(13).Значение;
	
	// устанавливаем платежи
	ТекПлатеж = РегистрыСведений.Платежи.СоздатьМенеджерЗаписи();
	
	ТекПлатеж.Период = ТекущаяДата();
	ТекПлатеж.Оборудование= 			АвторизаторПлатежей;
	ТекПлатеж.Компьютер= 				Компьютер;
	ТекПлатеж.Оператор= 				Параметры.Получить(0).Значение;
	ТекПлатеж.Параметр1= 				Параметры.Получить(4).Значение;
	ТекПлатеж.Параметр2= 				Параметры.Получить(5).Значение;
	ТекПлатеж.Параметр3= 				Параметры.Получить(6).Значение;
	ТекПлатеж.НомерЧека= 				НомерЧека;
	ТекПлатеж.ДатаЧека= 					ДатаФР;
	ТекПлатеж.ВремяЧека=					ДатаФР;              
	ТекПлатеж.Касса= 					КассаККМ;
	ТекПлатеж.ФискальныйНомерПозиции= 	Товары.Количество()+1;
	ТекПлатеж.Фискализация= 				Ложь;  
	ТекПлатеж.Сумма=						СуммаЗаВычетомКомиссии;
	ТекПлатеж.Статус= ?(КодОшибки=0,Перечисления.СтатусВыполненияДействия.ОК,Перечисления.СтатусВыполненияДействия.Ошибка);
	ТекПлатеж.КодОшибки		 = КодОшибки;
	ТекПлатеж.ОписаниеОшибки = ?(КодОшибки=0,"Авторизация прошла успешно",ОписаниеОшибки); 
	ТекПлатеж.КодАвторизации = Параметры.Получить(11).Значение; 
	ТекПлатеж.ЛогАвторизации = "1|"+ТекущаяДата()+"|"+КодОшибки+"|"+?(КодОшибки=0,"Авторизация прошла успешно",ОписаниеОшибки)+"|"; 	
	ТекПлатеж.Записать();
	
	НовыйПлатеж = тзПлатежиЗаУслуги.Добавить();
	ЗаполнитьЗначенияСвойств(НовыйПлатеж,ТекПлатеж);
	
	// заменим шаблонный платеж на платеж с учетом оператора
	Оператор = Параметры.Получить(0).Значение;
	ЗаменитьПлатеж(Номенклатура,КодОператора);
			
    Возврат Истина;
КонецФункции

//Добавление номенклатуры в ТЧ
//
// Параметры:
//  Номенклатура               - СправочникСсылка - ссылка на номенклатуру,
//  ЕдиницаИзмерения           - СправочникСсылка - ссылка на единицу измерения,
//  ХарактеристикаНоменклатуры - СправочникСсылка - ссылка на характеристику номенклатуры,
//  Количество                 - число - определяет, сколько добавляем.
//
Функция ДобавитьНоменклатуру(ЭтаФорма, Номенклатура,ХарактеристикаНоменклатуры = Неопределено, ЕдиницаИзмерения = Неопределено, Количество = 1, Набор = Неопределено, КодМаркировки = Неопределено) Экспорт
	
	ИмяТовара = СокрЛП(Номенклатура.Наименование);
	ЭлементыФормы = ЭтаФорма.ЭлементыФормы;
	Платеж = Ложь;

	// Сохраним текущее состояние признака пакетного редактирования строки
	РедактированиеСтроки = Истина;
	
	// проверим не набор ли мы добавляем
	Если Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Набор Тогда
		
		// проверим не платеж ли мы добавляем
		Если (Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Услуга)
			И (НЕ Номенклатура.ВидПлатежа.Пустая()) Тогда
			
			Если УстановленРежим("ЧекНаВозврат") Тогда
				ОткрытьДиалог("НЕЛЬЗЯ ВОЗВРАЩАТЬ ПЛАТЕЖИ !", СтатусСообщения.Важное);	
				Возврат Ложь;
			КонецЕсли;	
			
			Если НЕ АвторизоватьСотовыйПлатеж(Номенклатура) Тогда
				// необходимо изменить информации на дисплее
				ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
				ОбновитьИндикатор("Товар",ЭтаФорма,  ТекущаяСтрока);
				Возврат Ложь;
			КонецЕсли;
			Платеж = Истина;
		Иначе
			
			// проверим заполнена ли единица измерения
			Если обЗначениеНезаполнено(ЕдиницаИзмерения) Тогда
				ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения;
				Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
					ОткрытьДиалог("Для товара: " + ИмяТовара +" не задана единица измерения!" +Символы.ПС+
								  "Товары без единицы измерения запрещены! ", СтатусСообщения.ОченьВажное);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			
			// проверим надо ли выбрать характеристику
			Если обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры) И ПроверитьИспользованиеХарактеристикиУТовара(Номенклатура) = 1 Тогда // ручное списание характеристик
				// Сформируем структуру параметров отбора
				Владелец = ?(Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=1, Номенклатура.ТипНоменклатуры, Номенклатура);
				ПараметрыОткрытия = Новый Структура("Номенклатура,Отбор", Номенклатура, Новый Структура("Владелец", Владелец));
				
				// Открываем форму выбора
				ФормаВыбора = ПолучитьФормуФронта("ВыборХарактеристики", ЭтаФорма,, ПараметрыОткрытия);
				ХарактеристикаНоменклатуры = ОткрытьФормуВыбора(ФормаВыбора, ПараметрыОткрытия, "ВыборХарактеристики");
				Если обЗначениеНеЗаполнено(ХарактеристикаНоменклатуры) Тогда
					ОткрытьДиалог("Для данного товара обязательна характеристика!",СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
					
			КонецЕсли;
			
			Если фкАвтоматическиСкрыватьКолонкуХарактеристик И ЭтаФорма<> Неопределено Тогда
				Попытка
					Если НЕ ЭтаФорма.ЭлементыФормы.Товары.Колонки.ХарактеристикаНоменклатуры.Видимость Тогда
						ЭтаФорма.ЭлементыФормы.Товары.Колонки.ХарактеристикаНоменклатуры.Видимость = (Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1 ИЛИ Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2);
					КонецЕсли; 
				Исключение
				КонецПопытки; 
			КонецЕсли;
			
			Если ЭтаФорма<> Неопределено Тогда
				Попытка
					Если НЕ ЭтаФорма.ЭлементыФормы.Товары.Колонки.КодыМаркировки.Видимость
						И НЕ (ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки
						ИЛИ ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат) Тогда
						ЭтаФорма.ЭлементыФормы.Товары.Колонки.КодыМаркировки.Видимость = Номенклатура.ТипНоменклатуры.ВедетсяМаркировка;
					КонецЕсли; 
				Исключение
				КонецПопытки; 
			КонецЕсли;
			
			// проверим заполнена ли цена    		
			Цена = ПолучитьЦену(Номенклатура,ХарактеристикаНоменклатуры);
			Если обЗначениеНезаполнено(Цена) Тогда
				ОткрытьДиалог("Для товара: " + ИмяТовара +" не задана цена!" +Символы.ПС+
							  "Товары без цены запрещены! ", СтатусСообщения.ОченьВажное);
				Возврат Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		// Приступим к редактированию/добавлению строки
		РедактированиеСтроки = Истина;
		СтрокаНоменклатура = Неопределено;
		
		// подготовим структуру отбора для поиска товара в ТЧ
		СтруктураОтбора=Новый Структура("Номенклатура",Номенклатура);
		
		СтруктураОтбора.Вставить("ЕдиницаИзмерения",ЕдиницаИзмерения);
		
		Если ХарактеристикаНоменклатуры <> Неопределено Тогда
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры);
		КонецЕсли;
		Набор = ?(Набор = Неопределено,Справочники.Номенклатура.ПустаяСсылка(),Набор);
		СтруктураОтбора.Вставить("Набор",Набор);
		
		// проверим есть ли в ТЧ хоть одна строка, удовлетворяющая нашим условиям
		МассивСтрок = Товары.НайтиСтроки(СтруктураОтбора);
		Если МассивСтрок.Количество() > 0 Тогда
			СтрокаНоменклатура = МассивСтрок[0];
		КонецЕсли;
		
		//если товар в ТЧ не найден то добавим строку
		Если обЗначениеНеЗаполнено(СтрокаНоменклатура) ИЛИ НЕ (Номенклатура.ВидПлатежа.Пустая()) Тогда
			Если Количество < 0 Тогда
				ОткрытьДиалог("Набор с товаром """ + Номенклатура + """ не найден!",СтатусСообщения.Важное);
				Возврат Ложь;
			КонецЕсли;
			
			//добавим новую строчку в ТЧ 
			СтрокаНоменклатура = Товары.Добавить();
			Попытка
				ЭлементыФормы.Товары.ТекущаяСтрока = СтрокаНоменклатура;
				СтрокаНоменклатура.Номенклатура = Номенклатура;
				Если ЕдиницаИзмерения<>Неопределено И ТипЗнч(ЕдиницаИзмерения)=Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
					СтрокаНоменклатура.ЕдиницаИзмерения = ЕдиницаИзмерения;
				КонецЕсли; 
				Если ХарактеристикаНоменклатуры<>Неопределено И ТипЗнч(ХарактеристикаНоменклатуры)=Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда
					СтрокаНоменклатура.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;
				КонецЕсли; 
				Если ТипЗнч(Количество)=Тип("Число") Тогда
					СтрокаНоменклатура.Количество=Количество;
				КонецЕсли;
				СтрокаНоменклатура.Набор = Набор;
				Если Платеж Тогда
					СтрокаНоменклатура.Платеж = Платеж;	
					СтрокаНоменклатура.НомерПлатежа = тзПлатежиЗаУслуги.Количество()-1;
					СтрокаНоменклатура.Цена = ТекСуммаПлатежа;
				КонецЕсли;	
				СтрокаНоменклатура.ИдентификаторТовара = Новый УникальныйИдентификатор;
			Исключение
				Возврат Ложь;
			КонецПопытки;
			
			// Обработаем реквизиты строки ТЧ
			ОбработкаРеквизита("Товары.Номенклатура",СтрокаНоменклатура,ЭтаФорма);
			НоваяСтрока=Истина;
		Иначе  // товар найден, увеличим количество
			ЭлементыФормы.Товары.ТекущаяСтрока = СтрокаНоменклатура;
			Если ТипЗнч(Количество)=Тип("Число") Тогда
				Если СтрокаНоменклатура.Количество + Количество > 0 Тогда
					
					КоличествоУвеличить = Количество;
					Если КодМаркировки <> Неопределено И ЗначениеЗаполнено(КодМаркировки.КодМаркировки) Тогда
						
						// Проверим сколько маркировок считано
						СписокКодовМаркировки = дкСписокМаркировкиНоменклатуры(ЭтаФорма, СтрокаНоменклатура);
						
						Если СписокКодовМаркировки.Количество() + КоличествоУвеличить <= СтрокаНоменклатура.Количество * СтрокаНоменклатура.Коэффициент Тогда
							КоличествоУвеличить = 0;
						Иначе
							КоличествоУвеличить = (СписокКодовМаркировки.Количество() + КоличествоУвеличить) - СтрокаНоменклатура.Количество * СтрокаНоменклатура.Коэффициент;
						КонецЕсли;
						
					КонецЕсли;
					
					СтрокаНоменклатура.Количество = СтрокаНоменклатура.Количество + КоличествоУвеличить;
					ОбработкаРеквизита("Товары.Количество",СтрокаНоменклатура,ЭтаФорма);
				Иначе
					ОткрытьДиалог("Остался только 1 набор """+СокрЛП(Набор)+""" ", СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			НоваяСтрока=Ложь;
		КонецЕсли;
		
		Попытка
			Если КодМаркировки <> Неопределено И ЗначениеЗаполнено(КодМаркировки.КодМаркировки) Тогда
				НоваяСтрокаМаркировки = КодыМаркировки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаМаркировки, КодМаркировки);
				НоваяСтрокаМаркировки.ИдентификаторТовара = СтрокаНоменклатура.ИдентификаторТовара;
			ИначеЕсли дкЭтоМаркируемаяНоменклатура(СтрокаНоменклатура.Номенклатура) Тогда
				дкОткрытьФормуСканированияМаркировки(ЭтаФорма, СтрокаНоменклатура);
			КонецЕсли;
		Исключение
		КонецПопытки;
		
	Иначе
		Набор=Номенклатура; 
		КоличествоНаборов=Количество;
									
		// теперь будем рекурсивно добавлять состав набора
		СоставНабора = Набор.СоставНабора.Выгрузить();
		НТовараИзНабора = 0;   Результат = Истина;
		Для Каждого ТоварИзНабора Из СоставНабора Цикл
			Если ТоварИзНабора.Номенклатура.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Набор Тогда
				//Набор в наборе не обрабатываем (хотя чисто теоретически можно и обработать
				ОткрытьДиалог("В наборе """+СокрЛП(Набор)+""" присутствует набор """+СокрЛП(ТоварИзНабора.Номенклатура)+""". Добавление набора """+СокрЛП(ТоварИзНабора.Номенклатура)+""" отменено.", СтатусСообщения.Важное);
				Результат = Ложь;	
				Прервать;
			Иначе
				// получаем составляющие строки товара из набора
				КоличествоТовараИзНабора = ТоварИзНабора.Количество*КоличествоНаборов;
				ХарактеристикаТовараИзНабора = ТоварИзНабора.ХарактеристикаНоменклатуры;
				Товар = ТоварИзНабора.Номенклатура;
				// добавляем товар в таблицу товаров
				Если НЕ ДобавитьНоменклатуру(ЭтаФорма, Товар,ХарактеристикаТовараИзНабора,,КоличествоТовараИзНабора, Набор) Тогда;
					Если КоличествоНаборов > 0 Тогда  // иначе - ДействиеМинус1
						ОткрытьДиалог("При добавлении набора " + ИмяТовара + " возникла ошибка!"+ Символы.ПС +
									  "Набор будет удален из табличной части!", СтатусСообщения.Важное);			  
					Иначе
									  
					КонецЕсли;				  
					Результат = Ложь;  
					Прервать;
				Иначе
					ТоварИзНабора.ХарактеристикаНоменклатуры = ХарактеристикаТовараИзНабора;
				КонецЕсли;
			КонецЕсли;
			НТовараИзНабора = НТовараИзНабора + 1;
		КонецЦикла;
		
		// Если что то не так, то удаляем то что 
		Если НЕ Результат Тогда
			Для Сч = 0 По НТовараИзНабора - 1 Цикл
				ТоварИзНабора = СоставНабора[Сч];
				// получаем составляющие строки товара из набора
				КоличествоТовараИзНабора = ТоварИзНабора.Количество*КоличествоНаборов;
				ХарактеристикаТовараИзНабора = ТоварИзНабора.ХарактеристикаНоменклатуры;
				ТоварИзНабора = ТоварИзНабора.Номенклатура;

				УдалитьНоменклатуру(ЭтаФорма, ТоварИзНабора,ХарактеристикаТовараИзНабора,,КоличествоТовараИзНабора,Номенклатура);	
			КонецЦикла;
		КонецЕсли;
	
	КонецЕсли;
	
	// Восстанавливаем сохраненное состояние признака пакетного редактирования строки
	РедактированиеСтроки = Ложь;
	
	// вызовем обработчик при окончании редактирования строки
    ОтменаРедактирования = Ложь;
	ЭтаФорма.ТоварыПриОкончанииРедактирования(Товары, НоваяСтрока, ОтменаРедактирования);

	// вызовем обработчик при активизации редактирования строки
	ЭтаФорма.ТоварыПриАктивизацииСтроки(Неопределено);
	
	Возврат Истина;
КонецФункции // ДобавитьНоменклатуру()

// удаление номенклатуры из ТЧ
//
// Параметры:
//  Номенклатура               - СправочникСсылка - ссылка на номенклатуру,
//  ЕдиницаИзмерения           - СправочникСсылка - ссылка на единицу измерения,
//  ХарактеристикаНоменклатуры - СправочникСсылка - ссылка на характеристику номенклатуры,
//  Количество                 - число - определяет, сколько добавляем.
//
Процедура УдалитьНоменклатуру(ЭтаФорма, Номенклатура,ХарактеристикаНоменклатуры=Неопределено,ЕдиницаИзмерения=Неопределено,Количество=1, Набор=Неопределено) Экспорт
	
	РедактированиеСтроки=Истина;
	
	// проверим не набор ли мы добавляем
	Если Номенклатура.ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Набор Тогда
		// подготовим структуру отбора для поиска товара в ТЧ
		СтруктураОтбора=Новый Структура("Номенклатура",Номенклатура);
		Если ЕдиницаИзмерения<>Неопределено Тогда
			СтруктураОтбора.Вставить("ЕдиницаИзмерения",ЕдиницаИзмерения);
		КонецЕсли; 
		Если ХарактеристикаНоменклатуры<>Неопределено Тогда
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры",ХарактеристикаНоменклатуры);
		КонецЕсли;
		Если Набор<>Неопределено Тогда
			СтруктураОтбора.Вставить("Набор",Набор);
		КонецЕсли;
		
		// проверим есть ли в ТЧ хоть одна строка, удовлетворяющая нашим условиям
		МассивСтрок = Товары.НайтиСтроки(СтруктураОтбора);
		Если МассивСтрок.Количество() > 0 Тогда
			СтрокаНоменклатура = МассивСтрок[0];
		КонецЕсли;
		
		//если товар в ТЧ не найден то можно выходить из процедуры
		Если обЗначениеНеЗаполнено(СтрокаНоменклатура) Тогда
			Возврат;
		Иначе
			Если Количество = 0 Тогда Количество = СтрокаНоменклатура.Количество; КонецЕсли;
			
			Если СтрокаНоменклатура.Количество > Количество Тогда
				СтрокаНоменклатура.Количество = СтрокаНоменклатура.Количество - Количество;
			Иначе
				Товары.Удалить(СтрокаНоменклатура);	
			КонецЕсли;
		КонецЕсли;
	Иначе
		
		Набор=Номенклатура; 
											
		// подготовим структуру отбора для поиска товара в ТЧ
		СтруктураОтбора=Новый Структура("Набор",Набор);
		
		// проверим есть ли в ТЧ хоть одна строка, удовлетворяющая нашим условиям
		МассивСтрок = Товары.НайтиСтроки(СтруктураОтбора);
		Для Каждого СтрокаТЧ Из МассивСтрок Цикл
			Товары.Удалить(СтрокаТЧ);		
		КонецЦикла;

	КонецЕсли;
			
	РедактированиеСтроки=Ложь;
	ЭтаФорма.ТоварыПослеУдаления(Товары);

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму обработки. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа,
//										реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Результат = ИСТИНА;	
	
	Если Найти(Имя, "Товары.") > 0 И Имя <> "Товары.СуммаНДС"
		И НЕ УстановленРежим("ЧекНаОплатуВозврат") Тогда
		Для Каждого Строка Из Товары Цикл
			Если НЕ (Имя = "Товары.СуммаВсего" И ТекСтрока = Строка) Тогда
				Строка.СуммаВсего = Строка.СуммаВсего + Строка.СуммаСкидкиБонусами;
			КонецЕсли;
			Строка.СуммаСкидкиБонусами = 0;
		КонецЦикла;
	КонецЕсли;
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА
	Если Имя="ХозОперация" Тогда
		#Если Клиент Тогда
		Если ЭтаФорма<>Неопределено Тогда ЭтаФорма.УправлениеИнтерфейсом(); КонецЕсли; 
		#КонецЕсли
	ИначеЕсли Имя="Автор" Тогда  
		// ПУСТО
	ИначеЕсли Имя="Ссылка" Тогда 
		// ПУСТО
	ИначеЕсли Имя="Дата" Тогда  
		// ПУСТО
	ИначеЕсли Имя="Комментарий" Тогда  
		// ПУСТО
	ИначеЕсли Имя="ДокументОснование" Тогда 
		// ПУСТО
	ИначеЕсли Имя="ПометкаУдаления" Тогда  
		// ПУСТО
	ИначеЕсли Имя="РегламентированныйУчет" Тогда  
		// ПУСТО
	ИначеЕсли Имя="Номер" Тогда  
		// ПУСТО		
	ИначеЕсли Имя="ПодразделениеКомпании" Тогда  
		#Если Клиент Тогда
		ЭтотОбъект.Организация = ЭтотОбъект.ПодразделениеКомпании.Организация;
		зфОрганизацияПодразделениеИндикация(ЭтаФорма);
		// должен быть обработчик типа цен, но мы пришли к выводу что он не нужен
		// а тип цен берется из настроек
		#КонецЕсли
		
		// Произведем перерасчет скидок
		Если НЕ обПолучитьЗначениеПараметраСтруктуры(ДопПараметры, "ЗапретРасчетаСкидкокШапки", Ложь) Тогда
			Если НЕ (УстановленРежим("ВыборДокументаНаОплатуВозврат") ИЛИ УстановленРежим("ВыборДокументаНаОплату")
					ИЛИ УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат")
					ИЛИ УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
					ИЛИ УстановленРежим("БанковскаяВыписка") ИЛИ (УстановленРежим("КОПИЯЧЕКА") И (ХозОперация = Справочники.ХозОперации.ПриходныйКассовыйОрдер
					ИЛИ ХозОперация = Справочники.ХозОперации.РасходныйКассовыйОрдер ИЛИ ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки))) Тогда
				ДопПараметры = Новый Структура("ФронтОфис,ЭтоПодарок,БэкОфис,Терминал", ИСТИНА, ?(фкЗапретитьПодарки,ЛОЖЬ,Неопределено));
				ДопПараметры.Вставить("СкидкаНаТовары",Истина);
				рсУстановитьСкидкуШапки(ЭтотОбъект, ЛОЖЬ, ИСТИНА, ЭтаФорма, ДопПараметры);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя="Организация" Тогда  
		
	ИначеЕсли Имя="СкладКомпании" Тогда 
		// Склад не меняется в течение работы фронта	
	ИначеЕсли Имя="Контрагент" Тогда 
		Если НЕ обЗначениеНеЗаполнено(Контрагент) Тогда
			// Контрагент может поменяться только при смене дисконтной Карточки
			// т.к. выбор карточек доступен только при продаже(при оплате ПКО не используются)
			// установим обязательный вид договора "Продажа".
			//  При необходимости будет создан новый договор автоматически
			ДопПараметры = Новый Структура("ТипЦенПродажи",ТипЦен);
			ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(Контрагент, Перечисления.ВидыДоговоров.Продажа, ЭтотОбъект, ДопПараметры,, Истина);
			Результат = ОбработкаРеквизита("ДоговорВзаиморасчетов", ТекСтрока, ЭтаФорма, ДопПараметры);
		Иначе
			ДоговорВзаиморасчетов = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
			Результат = ОбработкаРеквизита("ДоговорВзаиморасчетов",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя="ДоговорВзаиморасчетов" Тогда 

	ИначеЕсли Имя="Карточка" Тогда
		// ДисконтнаяКарта
		// КарточкаПользователя
		// СлужебнаяОперация
		// Чистая
		// Талон
		// КлубнаяКарта
		
		// проверим выбрана ли карточка
		Если НЕ Карточка.Пустая() Тогда
			// Дисконтная карточка
			Если Карточка.ВидКарточки=Перечисления.ВидыКарточек.ДисконтнаяКарта Тогда
				Если Контрагент<>Карточка.Объект Тогда
					//Контрагент изменен
					Контрагент=Карточка.Объект;
					Результат = ОбработкаРеквизита("Контрагент",ТекСтрока,ЭтаФорма,ДопПараметры);
				КонецЕсли;
				
			Иначе // любая другая карта
				#Если Клиент Тогда
				ОткрытьДиалог("ВЫБРАНА НЕ ДИСКОНТНАЯ КАРТА!", СтатусСообщения.Важное);
				#КонецЕсли
				Карточка=Справочники.Карточки.ПустаяСсылка(); 
				ОбработкаРеквизита("Карточка",ТекСтрока,ЭтаФорма,ДопПараметры);
				Возврат Ложь;
			КонецЕсли;
		Иначе
			// Карточка не выбрана, или эта карта не дисконтная, в этом случае сбросим Контрагента
			Контрагент = Справочники.Контрагенты.ПустаяСсылка();
			Результат = ОбработкаРеквизита("Контрагент", ТекСтрока, ЭтаФорма, ДопПараметры);
		КонецЕсли;
		
		// Произведем перерасчет скидок
		Если НЕ обПолучитьЗначениеПараметраСтруктуры(ДопПараметры, "ЗапретРасчетаСкидкокШапки", Ложь) Тогда
			Если НЕ (УстановленРежим("ВыборДокументаНаОплатуВозврат") ИЛИ УстановленРежим("ВыборДокументаНаОплату")
					ИЛИ УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат")
					ИЛИ УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
					ИЛИ УстановленРежим("БанковскаяВыписка") ИЛИ (УстановленРежим("КОПИЯЧЕКА") И (ХозОперация = Справочники.ХозОперации.ПриходныйКассовыйОрдер
					ИЛИ ХозОперация = Справочники.ХозОперации.РасходныйКассовыйОрдер ИЛИ ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки))) Тогда
				ДопПараметры = Новый Структура("ФронтОфис,ЭтоПодарок,БэкОфис,Терминал", ИСТИНА, ?(фкЗапретитьПодарки,ЛОЖЬ,Неопределено));
				ДопПараметры.Вставить("СкидкаНаТовары",Истина);
				рсУстановитьСкидкуШапки(ЭтотОбъект, ЛОЖЬ, ИСТИНА, ЭтаФорма, ДопПараметры);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Карточка.БонуснаяПрограмма) И Товары.Количество() > 0 И НЕ УстановленРежим("ЧекНаВозврат") Тогда
			ФормаВыбора = ПолучитьФормуФронта("ФормаВыбораБонусов", ЭтаФорма);
			ОткрытьФормуВыбора(ФормаВыбора, , "ФормаВыбораБонусов");
			ОбработкаРеквизита("КоличествоКСписанию",, ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли Имя = "КоличествоКСписанию" Тогда
		
		// сравним бонусные баллы с суммой всего
		Если НЕ БонусныеПрограммыСервер.БонуснаяПрограммаАктивна(Карточка.БонуснаяПрограмма, Дата) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		КоличествоКСписанию = Мин(КоличествоКСписанию,
								  БонусныеПрограммыСервер.КоличествоБалловВСуммеДокумента(ЭтотОбъект, Карточка.БонуснаяПрограмма),
								  БонусныеПрограммыСервер.МаксимальноеКоличетсвоБоллов(ЭтотОбъект, Карточка.БонуснаяПрограмма));
		
		// расписываем сумму по строкам документа документам
		СводнаяТаблица = СформироватьСводнуюТаблицу();
		
		БонусныеПрограммыСервер.РаспределитьСуммуПоТаблице(СводнаяТаблица, БонусныеПрограммыСервер.БаллыВВалюту(КоличествоКСписанию, Карточка.БонуснаяПрограмма, ВалютаДокумента, Дата), "Сумма");
		
		Для Каждого Строка Из СводнаяТаблица Цикл
			Строка.Строка.СуммаВсего          = Строка.Сумма;
			Строка.Строка.СуммаНДС            = Строка.Строка.СуммаВсего*Строка.Строка.СтавкаНДС.Ставка/(100+Строка.Строка.СтавкаНДС.Ставка);
			Строка.Строка.СуммаСкидкиБонусами = Строка.РаспределенноеЗначение;
		КонецЦикла;
		
		БонусныеПрограммыСервер.РасчитатьБонусныеБаллыКНачислению(ЭтотОбъект, Карточка.БонуснаяПрограмма);
		
		ОбновитьИндикаторы(ЭтаФорма);
		СуммаДокумента = Товары.Итог("СуммаВсего");
		ОбработкаРеквизита("СуммаДокумента",, ЭтаФорма);
		
		Возврат Истина;
		
	ИначеЕсли Имя="ВалютаДокумента" Тогда
		// ПУСТО
	ИначеЕсли Имя="КурсДокумента" Тогда 
		// ПУСТО
	ИначеЕсли Имя="СуммаДокумента" Тогда 
		
		// Произведем перерасчет скидок
		Если НЕ обПолучитьЗначениеПараметраСтруктуры(ДопПараметры, "ЗапретРасчетаСкидкокШапки", Ложь) Тогда		
			Если НЕ (УстановленРежим("ВыборДокументаНаОплатуВозврат") ИЛИ УстановленРежим("ВыборДокументаНаОплату")
					ИЛИ УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат")
					ИЛИ УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
					ИЛИ УстановленРежим("БанковскаяВыписка") ИЛИ (УстановленРежим("КОПИЯЧЕКА") И (ХозОперация = Справочники.ХозОперации.ПриходныйКассовыйОрдер
					ИЛИ ХозОперация = Справочники.ХозОперации.РасходныйКассовыйОрдер ИЛИ ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки))) Тогда				
				ДопПараметры = Новый Структура("ФронтОфис,ЭтоПодарок,БэкОфис,Терминал", ИСТИНА, ?(фкЗапретитьПодарки,ЛОЖЬ,Неопределено));
				ДопПараметры.Вставить("СкидкаНаТовары",Истина);
				рсУстановитьСкидкуШапки(ЭтотОбъект, ЛОЖЬ, ЛОЖЬ, ЭтаФорма, ДопПараметры);
			КонецЕсли;
		КонецЕсли;
			
		// Производим сохранение редактируемого чека со статусом "Редактирование"
		Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") Тогда
			Попытка
				Объект = Ссылка.ПолучитьОбъект();
			Исключение
				Объект = Документы.Чек.СоздатьДокумент();
			КонецПопытки;
			ЗаполнитьЗначенияСвойств(Объект, ЭтотОбъект);
			Объект.Товары.Загрузить(Товары.Выгрузить());
			Объект.Оплаты.Загрузить(Оплаты.Выгрузить());
			Объект.КоличествоКНачислению = КоличествоКНачислению;
			Объект.КоличествоКСписанию   = КоличествоКСписанию;
			
			АрхивЧековСохранить(Объект, 0);
		КонецЕсли;
		
		// Обновим индикатор 
		ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
		ОбновитьИндикатор("Получено",ЭтаФорма);

	ИначеЕсли Имя="ТипЦен" Тогда 
				
	ИначеЕсли Имя="ВидОплаты" Тогда
		Если ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет И ПолученоБезнал<>0 Тогда
			ПолученоБезнал=0;
			КодОшибки = ОбработкаРеквизита("ПолученоБезнал",ТекСтрока,ЭтаФорма,ДопПараметры); 
			Возврат КодОшибки;

		ИначеЕсли ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет И ПолученоНал<>0 Тогда
			ПолученоНал=0;
			КодОшибки = ОбработкаРеквизита("ПолученоНал",ТекСтрока,ЭтаФорма,ДопПараметры);
			Возврат КодОшибки;
		КонецЕсли; 
		
		Возврат Истина;
		
	ИначеЕсли Имя="ПолученоНал" ИЛИ Имя="ПолученоБезнал" Тогда
		КодОшибки=Истина;
		ЕстьНал=(ПолученоНал>0);
		ЕстьБезнал=(ПолученоБезнал>0);
		Если (ЕстьНал) И (НЕ ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.НаличныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет;
			КодОшибки=ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (НЕ ЕстьНал) И (ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.БезналичныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.БезналичныйРасчет;
			КодОшибки=ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (ЕстьНал) И (ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.ПроизвольнаяОплата) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			КодОшибки=ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		ИначеЕсли (НЕ ЕстьНал) И (НЕ ЕстьБезнал) И (ВидОплаты<>Перечисления.ВидыОплаты.НаличныйРасчет) Тогда
			ВидОплаты=Перечисления.ВидыОплаты.НаличныйРасчет;
			КодОшибки=ОбработкаРеквизита("ВидОплаты",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
		// Производим сохранение редактируемого чека со статусом "Редактирование"
		Если (УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат")) И (Товары.Количество() > 0) Тогда
			Попытка
				Объект = Ссылка.ПолучитьОбъект();
			Исключение
				Объект = Документы.Чек.СоздатьДокумент();
			КонецПопытки;
			ЗаполнитьЗначенияСвойств(Объект, ЭтотОбъект);
			Объект.Товары.Загрузить(Товары.Выгрузить());
			Объект.Оплаты.Загрузить(Оплаты.Выгрузить());
			АрхивЧековСохранить(Объект, 0);
		КонецЕсли;
		
		ОбновитьИндикатор("Получено",ЭтаФорма);
		Возврат КодОшибки;
		
	ИначеЕсли Имя="ПлатежнаяКарта" Тогда 
		
	ИначеЕсли Имя="СкидкаНаценка" Тогда 
		Если НЕ обПолучитьЗначениеПараметраСтруктуры(ДопПараметры, "ЗапретРасчетаСкидкокШапки", Ложь) Тогда
			Если НЕ (УстановленРежим("ВыборДокументаНаОплатуВозврат") ИЛИ УстановленРежим("ВыборДокументаНаОплату")
					ИЛИ УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат")
					ИЛИ УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
					ИЛИ УстановленРежим("БанковскаяВыписка") ИЛИ (УстановленРежим("КОПИЯЧЕКА") И (ХозОперация = Справочники.ХозОперации.ПриходныйКассовыйОрдер
					ИЛИ ХозОперация = Справочники.ХозОперации.РасходныйКассовыйОрдер ИЛИ ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки))) Тогда
				ДопПараметры = Новый Структура("ФронтОфис,ЭтоПодарок,БэкОфис,Терминал", ИСТИНА, ?(фкЗапретитьПодарки,ЛОЖЬ,Неопределено));
				ДопПараметры.Вставить("СкидкаНаТовары",Истина);
				Результат = рсУстановитьСкидкуШапки(ЭтотОбъект, ЛОЖЬ, ИСТИНА, ЭтаФорма, ДопПараметры); // Установим признак возможности установки автоматической скидки на место пустой
			КонецЕсли;
			ОбновитьИндикатор("СкидкаНаЧек",ЭтаФорма);
			ОбработкаРеквизита("КоличествоКСписанию",, ЭтаФорма);
		КонецЕсли;	
	ИначеЕсли Имя="КассаККМ" Тогда 
		Если (КассаККМ.ВалютаДенежныхСредств<>Справочники.Валюты.НайтиПоКоду("643"))И(КассаККМ<>Справочники.КассыККМ.ПустаяСсылка()) Тогда
			ОткрытьДиалог("Касса должна быть рублевая!", СтатусСообщения.Важное);
			Возврат 1;
		КонецЕсли;
		
		Если КассаККМ.Организация<>Организация И (НЕ обЗначениеНеЗаполнено(Организация)) Тогда
			Если ОткрытьДиалогВопроса("Организация кассы отлична от текущей организации документа. Установить организацию документа в соответствие с организацией кассы?", ИСТИНА) Тогда
				Организация=КассаККМ.Организация;
				ОбработкаРеквизита("Организация",,ЭтаФорма);
			КонецЕсли; 
		КонецЕсли; 
		Если КассаККМ.Подразделение<>ПодразделениеКомпании И (НЕ обЗначениеНеЗаполнено(ПодразделениеКомпании)) Тогда
			Если ОткрытьДиалогВопроса("Подразделение кассы отлично от текущего подразделения документа. Установить подразделение документа в соответствии с подразделением кассы?", ИСТИНА) Тогда
				ПодразделениеКомпании=КассаККМ.Подразделение;
				ОбработкаРеквизита("ПодразделениеКомпании",,ЭтаФорма);
			КонецЕсли; 
		КонецЕсли; 
		Если КассаККМ.ВалютаДенежныхСредств<>ВалютаДокумента И (НЕ обЗначениеНеЗаполнено(ВалютаДокумента)) Тогда
			Если ОткрытьДиалогВопроса("Валюта кассы отлична от текущей валюты документа. Установить валюту документа в соответствие с валютой кассы?", ИСТИНА) Тогда
				ВалютаДокумента=КассаККМ.ВалютаДенежныхСредств;
				ОбработкаРеквизита("ВалютаДокумента",,ЭтаФорма);
			КонецЕсли; 
		КонецЕсли;

	ИначеЕсли Имя="ТСД" Тогда  
	ИначеЕсли Имя="Компьютер" Тогда  
	ИначеЕсли Имя="Авторизатор" Тогда  
	ИначеЕсли Имя="ФР" Тогда 		
	ИначеЕсли Имя="ДатаФР" Тогда  		
	ИначеЕсли Имя="НомерДокумента" Тогда  
	ИначеЕсли Имя="НомерСмены" Тогда  
	ИначеЕсли Имя="НомерЧека" Тогда 
		
	// ВСПОМОГАТЕЛЬНЫЕ ОБРАБОТКИ
	ИначеЕсли Имя="ПрименитьСкидкуКТабличнымЧастям" Тогда
		
		Если НЕ (УстановленРежим("ВыборДокументаНаОплатуВозврат") ИЛИ УстановленРежим("ВыборДокументаНаОплату")
				ИЛИ УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат")
				ИЛИ УстановленРежим("ПриходныйКассовыйОрдер")  ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
				ИЛИ УстановленРежим("БанковскаяВыписка")) Тогда
			РезультатОбработки = ПрименитьСкидкиКТабличнойЧасти(ДопПараметры);
			СуммаДокумента = Товары.Итог("СуммаВсего");
		КонецЕсли;
			
		ОбновитьИндикаторы(ЭтаФорма);

	// ОБРАБОТКА ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	ИначеЕсли Лев(Имя,7)="Товары." Тогда
	
		Если Имя <> "Товары.СуммаНДС" И НЕ УстановленРежим("ЧекНаОплатуВозврат") Тогда
			Для Каждого Строка Из Товары Цикл
				Если НЕ (Имя = "Товары.СуммаВсего" И ТекСтрока = Строка) Тогда
					Строка.СуммаВсего = Строка.СуммаВсего + Строка.СуммаСкидкиБонусами;
				КонецЕсли;
				Строка.СуммаСкидкиБонусами = 0;
			КонецЦикла;
		КонецЕсли;
	    ИмяРекТовары = Прав(Имя,стрДлина(Имя)-7);
		Рез=ОбработкаРеквизитаТовары(ИмяРекТовары,ТекСтрока,ЭтаФорма,ДопПараметры);
		Если НЕ обДопустимаяСтрока(ТекСтрока) Тогда Возврат Рез; КонецЕсли; //Это если это был набор и мы удалили эту строку
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		Если НЕ ЭтаФорма = Неопределено Тогда
			ЭтаФорма.ТоварыПриОкончанииРедактирования(ТекСтрока, ЛОЖЬ, ЛОЖЬ, ДопПараметры);
		КонецЕсли;
		
		Если Имя = "Товары.Номенклатура" Тогда
			Если ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
				ТекСтрока.ПризнакПредметаРасчета = ТекСтрока.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета;
			Иначе
				ТекСтрока.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		
		Возврат Рез;
		
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Истина; 
КонецФункции // ОбработкаРеквизита()

// Обработка изменения реквизитов документа
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Товары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму обработки. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
Функция ОбработкаРеквизитаТовары(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	
	// обработаем бонусы при изменении ТЧ
	Если Имя = "Номенклатура" Тогда
		// получим добавляемую нами позицию
		Номенклатура = ТекСтрока.Номенклатура;
		// получим необходимые нам переменные
		МестоРазмещения = ТекСтрока.МестоРазмещения;
		Количество = ТекСтрока.Количество;
		ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
		Характеристика = ТекСтрока.ХарактеристикаНоменклатуры;
		БлокироватьВидыНоменклатуры=ПолучитьСоответствиеЗапрещенныхВидовНоменклатуры();
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(МестоРазмещения) Тогда
			МестоРазмещения = СкладКомпании;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Количество) Тогда
			Количество = 1;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
			ЕдиницаИзмерения = Номенклатура.ОсновнаяЕдиницаИзмерения;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(Характеристика) Тогда
			Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		Если БлокироватьВидыНоменклатуры<>Неопределено И ТипЗнч(БлокироватьВидыНоменклатуры)=Тип("Соответствие") И БлокироватьВидыНоменклатуры.Количество()>0 Тогда
			ЕстьОшибка=(БлокироватьВидыНоменклатуры[ТекСтрока.Номенклатура.ВидНоменклатуры]<>Неопределено); 
			Если ЕстьОшибка Тогда
				ОткрытьДиалог("В таблицу нельзя вводить номенклатуру вида """+СокрЛП(ТекСтрока.Номенклатура.ВидНоменклатуры)+"""", СтатусСообщения.Важное);
				ТекСтрока.Номенклатура=Неопределено;
				Возврат 0;
			КонецЕсли;
		КонецЕсли;
		
		Если ПустаяСтрока(ТекСтрока.ИдентификаторТовара) Тогда
			ТекСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		// Проверим
		#Если Клиент Тогда
		Попытка
			НайденнаяМаркировка = ЭтаФорма.КодыМаркировки.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекСтрока.ИдентификаторТовара));
			Если НайденнаяМаркировка.Количество() > 0 Тогда
				УдалятьМаркировку = Истина;
				Если дкЭтоМаркируемаяНоменклатура(ТекСтрока.Номенклатура) Тогда
					Если Вопрос("Для данной строки указаны коды маркировки. Удалить считанные коды маркировки?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
						УдалятьМаркировку = Ложь;
					КонецЕсли;
				КонецЕсли;
				Если УдалятьМаркировку Тогда
					Для Каждого ТекущаяМаркировка Из НайденнаяМаркировка Цикл
						ЭтаФорма.КодыМаркировки.Удалить(ТекущаяМаркировка);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		// Откроем форму сканирования маркировки
		Попытка
			Если ТипЗнч(ДопПараметры) = Тип("Структура")
				И ДопПараметры.Свойство("ОткрытьФормуСканированияМаркировки")
				И ДопПараметры.ОткрытьФормуСканированияМаркировки Тогда
				дкОткрытьФормуСканированияМаркировки(ЭтаФорма, ТекСтрока);
			КонецЕсли;
		Исключение
		КонецПопытки;
		#КонецЕсли
		
		// произведем необходимые преобразования и расчеты
		Если НЕ ТекСтрока.Платеж Тогда
			Цена = ПолучитьЦену(Номенклатура,Характеристика);
		Иначе
			Цена = ТекСтрока.Цена;
		КонецЕсли;
		ОсвобожденОтНДС = ПодразделениеКомпании.ОсвобожденОтНДС ИЛИ Организация.ОсвобожденОтНДС;
		Если ОсвобожденОтНДС Тогда
			СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		Иначе
			СтавкаНДС = Номенклатура.СтавкаНДС;
		КонецЕсли;
		Коэффициент = ЕдиницаИзмерения.Коэффициент;
		
		// заполняем ТЧ и вызываем соответствующие обработчики
		// Цена
		ТекСтрока.Цена = Цена;
		ОбработкаРеквизитаТовары("Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		// МестоРазмещения
		ТекСтрока.МестоРазмещения = СкладКомпании;
		ОбработкаРеквизитаТовары("МестоРазмещения",ТекСтрока,ЭтаФорма,ДопПараметры);
		// Количество
		ТекСтрока.Количество = Количество;
		ОбработкаРеквизитаТовары("Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
		// ЕдиницаИзмерения 
		ТекСтрока.ЕдиницаИзмерения = ЕдиницаИзмерения;
		ОбработкаРеквизитаТовары("ЕдиницаИзмерения",ТекСтрока,ЭтаФорма,ДопПараметры);
		// Характеристика
		ТекСтрока.ХарактеристикаНоменклатуры = Характеристика;
		ОбработкаРеквизитаТовары("Характеристика",ТекСтрока,ЭтаФорма,ДопПараметры);
		// СтавкаНДС 
		ТекСтрока.СтавкаНДС = СтавкаНДС;
		ОбработкаРеквизитаТовары("СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		// Коэффициент
		ТекСтрока.Коэффициент = Коэффициент;
		ОбработкаРеквизитаТовары("Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ХарактеристикаНоменклатуры" Тогда
		// получим необходимые нам переменные
		Номенклатура = ТекСтрока.Номенклатура;
		Характеристика = ТекСтрока.ХарактеристикаНоменклатуры;
		Коэффициент = ТекСтрока.Коэффициент; 
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(Номенклатура) Тогда
			Возврат 0;
		КонецЕсли;
		// проверим переменные
		Если обЗначениеНеЗаполнено(Характеристика) Тогда
			Возврат 0;
		КонецЕсли;
		
		// произведем необходимые преобразования и расчеты
		Если НЕ ТекСтрока.Платеж Тогда
			Цена = ПолучитьЦену(Номенклатура,Характеристика);
		
			// заполняем ТЧ и вызываем соответствующие обработчики
			// Цена
			ТекСтрока.Цена = Цена;
			ОбработкаРеквизитаТовары("Цена",ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
	
	ИначеЕсли Имя="МестоРазмещения" Тогда
		// получим необходимые нам переменные
		МестоРазмещения = ТекСтрока.МестоРазмещения;
		Номенклатура = ТекСтрока.Номенклатура;

		// проверим переменные
		Если обЗначениеНеЗаполнено(МестоРазмещения) Тогда
			МестоРазмещения = СкладКомпании;
		КонецЕсли;
		
	ИначеЕсли Имя="Количество" Тогда
		// получим необходимые нам переменные
		Количество = ТекСтрока.Количество;
		ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
		Цена = ТекСтрока.Цена;
		Коэффициент = ТекСтрока.Коэффициент; 
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(Коэффициент) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(Цена) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(Количество) Тогда 
			Возврат 0;
		КонецЕсли; 
		
		// произведем необходимые преобразования и расчеты
		Точность = ЕдиницаИзмерения.Точность;
		Количество = Окр(Количество, Точность);
		КоличествоСУчетомКоэффициента = Окр(Количество*Коэффициент, 3);
		
		Сумма =  Окр(КоличествоСУчетомКоэффициента*Цена, 2);
		
		// заполняем ТЧ и вызываем соответствующие обработчики   
		//Количество
		Если ТекСтрока.Количество <> Количество Тогда
			ТекСтрока.Количество = Количество;
			ОбработкаРеквизитаТовары("Количество",ТекСтрока,ЭтаФорма,ДопПараметры);
			ОткрытьДиалог("Для данного товара количество можно задавать только с точностью: " + Строка(Точность), СтатусСообщения.ОченьВажное);
			Возврат 0;
		КонецЕсли;
		
		Если УстановленРежим("ЧекНаОплатуВозврат") Тогда
			СуммаБонусовЗаЕдиницу = ТекСтрока.СуммаСкидкиБонусами / ТекСтрока.Сумма;
			ТекСтрока.СуммаСкидкиБонусами = Окр(СуммаБонусовЗаЕдиницу*Сумма, 2, РежимОкругления.Окр15как20);
		КонецЕсли;
		
		// Сумма
		ТекСтрока.Сумма = Сумма;
		ОбработкаРеквизитаТовары("Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="ЕдиницаИзмерения" Тогда
		// получим необходимые нам переменные
		ЕдиницаИзмерения = ТекСтрока.ЕдиницаИзмерения;
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(ЕдиницаИзмерения) Тогда
			Возврат 0;
		КонецЕсли;
		
		// произведем необходимые преобразования и расчеты
		Коэффициент = ЕдиницаИзмерения.Коэффициент;
		
		// заполняем ТЧ и вызываем соответствующие обработчики
		// Коэффициент
        ТекСтрока.Коэффициент = Коэффициент;
		ОбработкаРеквизитаТовары("Коэффициент",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Коэффициент" Тогда  
		// получим необходимые нам переменные
		Коэффициент = ТекСтрока.Коэффициент;
		Количество = ТекСтрока.Количество;
		Цена = ТекСтрока.Цена;
		Номенклатура = ТекСтрока.Номенклатура;
		Характеристика = ТекСтрока.ХарактеристикаНоменклатуры;
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(Коэффициент) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(Количество) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(Цена) Тогда
			Возврат 0;
		КонецЕсли;
				
		// произведем необходимые преобразования и расчеты
		Если НЕ ТекСтрока.Платеж Тогда
			Цена = ПолучитьЦену(Номенклатура,Характеристика);
		Иначе
			Цена = ТекСтрока.Цена;
		КонецЕсли;
		
		// заполняем ТЧ и вызываем соответствующие обработчики
		// Коэффициент
        ТекСтрока.Цена = Цена;
		ОбработкаРеквизитаТовары("Цена",ТекСтрока,ЭтаФорма,ДопПараметры);

	ИначеЕсли Имя="Цена" Тогда 
		// получим необходимые нам переменные
		Количество  = ТекСтрока.Количество;
		Цена        = ТекСтрока.Цена;
		Коэффициент = ТекСтрока.Коэффициент;
		
		// проверим переменные
		Если обЗначениеНеЗаполнено(Цена) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(Количество) Тогда 
			Возврат 0;
		КонецЕсли; 
		
		// произведем необходимые преобразования и расчеты
		КоличествоСУчетомКоэффициента = Окр(Количество*Коэффициент, 3);
		Сумма =  Окр(КоличествоСУчетомКоэффициента*Цена, 2);
		
		// заполняем ТЧ и вызываем соответствующие обработчики
		// Сумма
		ТекСтрока.Сумма = Сумма;
		ОбработкаРеквизитаТовары("Сумма",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="Сумма" Тогда
		// получим необходимые нам переменные
		СуммаСкидкиСтроки = ТекСтрока.СуммаСкидкиСтроки;
		Сумма = ТекСтрока.Сумма;
		СуммаСкидки = ТекСтрока.СуммаСкидки;
		
	    // проверим переменные
		Если обЗначениеНеЗаполнено(Сумма) Тогда
			Возврат 0;
		КонецЕсли;
		
		// произведем необходимые преобразования и расчеты
		Если УстановленРежим("ЧекНаОплатуВозврат") Тогда
			ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
			СуммаБезСкидки              = обМод(Сумма) * ?(ЦенаВключаетНДС, 1, 1 + ТекСтрока.СтавкаНДС.Ставка / 100);
			ТекСтрока.СуммаСкидки       = СуммаБезСкидки * ТекСтрока.ПроцентСкидки / 100;
			ТекСтрока.СуммаСкидкиСтроки = СуммаБезСкидки * ТекСтрока.ПроцентСкидкиСтроки / 100;
			СуммаВсего                  = Сумма - ТекСтрока.СуммаСкидкиСтроки - ТекСтрока.СуммаСкидки - ТекСтрока.СуммаСкидкиБонусами;
		Иначе
			СуммаВсего = Сумма - СуммаСкидкиСтроки - СуммаСкидки;
		КонецЕсли;
		
		// заполняем ТЧ и вызываем соответствующие обработчики
		ТекСтрока.СуммаВсего = СуммаВсего;
		ОбработкаРеквизитаТовары("СуммаВсего",ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя="СкидкаНаТовар" Тогда
	ИначеЕсли Имя="ПроцентСкидкиСтроки" Тогда
	ИначеЕсли Имя="СуммаСкидкиСтроки" Тогда
	ИначеЕсли Имя="ПроцентСкидки" Тогда
	ИначеЕсли Имя="СуммаСкидки" Тогда
	ИначеЕсли Имя="СуммаВсего" Тогда
		// получим необходимые нам переменные
		СуммаВсего = ТекСтрока.СуммаВсего;
		СтавкаНДС = ТекСтрока.СтавкаНДС;
 
	    // проверим переменные
		Если обЗначениеНеЗаполнено(СуммаВсего) Тогда
			Возврат 0;
		ИначеЕсли обЗначениеНеЗаполнено(СтавкаНДС) Тогда
			Возврат 0;
		КонецЕсли;
		
		// произведем необходимые преобразования и расчеты
		ОбработкаРеквизитаТовары("СтавкаНДС",ТекСтрока,ЭтаФорма,ДопПараметры);
		
		// ничего не делаем, скидка сама добавится и пересчитает все в событии после редактирования строки
		//УстановитьСкидкуШапки(ЭтаФорма);
		
	ИначеЕсли Имя="СтавкаНДС" Тогда
		
		СуммаСоСкидкой = дкПолучитьСуммуСтрокиСоСкидкой(ЭтотОбъект,ТекСтрока);
		Попытка
			СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
			ТекСтрока.СуммаНДС = Окр((СуммаСоСкидкой * СтавкаНДС)/(100 + СтавкаНДС),2);
		Исключение
		КонецПопытки;
		ТекСтрока.СуммаВсего = СуммаСоСкидкой;
		
	ИначеЕсли Имя="СуммаНДС" Тогда
		
	КонецЕсли;
КонецФункции
	
///////////////////////////////////////
// ПРОЦЕДУРЫ РАБОТЫ С ЧЕКОМ

// отмена текущего чека
//
// Параметры:
//  ЭтаФорма - форма - ссылка на форму.
//
Функция ОтменитьЧек(ЭтаФорма) Экспорт
	Результат = Ложь;
	
	// проверяем необходимость закрытия текущего чека
	Если (НЕ ЧекОткрыт()) И (НЕ УстановленРежим("ЧекНаВозврат") И НЕ УстановленРежим("ЧекНаОплатуВозврат")) Тогда 
		Возврат Истина; 
	КонецЕсли;	
	Если НЕ обПраво("РазрешитьОтменуТекущегоЧека",Права) Тогда  
		ОткрытьДиалог("НЕТ ПРАВА НА ОТМЕНУ ЧЕКА !", СтатусСообщения.Важное);
		Возврат Ложь; 
	КонецЕсли;
	
	Если ОткрытьДиалогВопроса("АННУЛИРОВАТЬ ЧЕК!") Тогда
		
		Если тзПлатежиЗаУслуги.Количество()>0 Тогда
			Если фкУдалятьПлатежи Тогда
				Если НЕ ОткрытьДиалогВопроса("В ЧЕКЕ ЕСТЬ ПЛАТЕЖИ !"+Символы.ПС+"ОТМЕНИТЬ ЧЕК ?") Тогда Возврат 0; КонецЕсли;
			Иначе
				ОткрытьДиалог("В ЧЕКЕ ЕСТЬ ПЛАТЕЖИ !"+Символы.ПС+"ОТМЕНА ЧЕКА НЕ ВОЗМОЖНА !", СтатусСообщения.Важное);
				Возврат 0;
			КонецЕсли;
		КонецЕсли;	
		
		// Проверим существование чека со статусом "Редактирование"
		Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") Тогда
			Если ТипЗнч(АрхивЧековВосстановить())=Тип("ДокументОбъект.Чек") Тогда
				// Производим сохранение редактируемого чека со статусом "Отмена"
				Попытка
					Объект = Ссылка.ПолучитьОбъект();
				Исключение
					Объект = Документы.Чек.СоздатьДокумент();
				КонецПопытки;
				ЗаполнитьЗначенияСвойств(Объект, ЭтотОбъект);
				Объект.Товары.Загрузить(Товары.Выгрузить());
				Объект.Оплаты.Загрузить(Оплаты.Выгрузить());
				АрхивЧековСохранить(Объект, 3);
			КонецЕсли;
		КонецЕсли;
		
		Если ПолученоБезнал>0 Тогда
			//Подлежит исправлению
			//1. Проверим право на авторизацию безналичных платежей
			//2. Проверка включения авторизатора
			//3. Проверка включения ФР
			//4. Возврат денег с использованием платежной карты или ручная авторизация
			//5. Проверка того что деньги возвращены. Если нет права что можно
			//	 аннулировать чек без оплаты - то отбой
			ПолученоБезнал=0;
		КонецЕсли;
		
		Если (НЕ Ссылка.Пустая())
				И (Ссылка.ХозОперация=Справочники.ХозОперации.ЧекОтложенный)
				И ОткрытьДиалогВопроса("УДАЛИТЬ ОТЛОЖЕННЫЙ ЧЕК?") Тогда
			
			УбиваемыйЧек=Ссылка.ПолучитьОбъект();
			Попытка
				УбиваемыйЧек.УстановитьПометкуУдаления(Истина);
			Исключение
			КонецПопытки;
		КонецЕсли;
		ЗагрузитьЧек(Неопределено,ЭтаФорма); 
		Результат = Истина;
		
		// удаляем платежи
		УдалитьПлатежи();
	КонецЕсли;	
	
	ЗаписатьВФайлЛога("Отмена чека", Число(НЕ Результат), "Отмененный чек: "+Ссылка);
	Возврат Результат;
КонецФункции // ОтменитьЧек()

//загружает отложенный чек или создает новый (если параметр ЗагружаемыйЧек = Неопределено)
//
// Параметры:
//  ЗагружаемыйЧек - документСсылка - Чек. 
//  ЭтаФорма       - форма - ссылка на форму.
//
Процедура ЗагрузитьЧек(ЗагружаемыйЧек,ЭтаФорма=Неопределено) Экспорт
	
	// считанное значение с дорожки платежной карты сохранять в базе запрещено!
	СчитанныйНомерКарты = "";
	СозданиеНовогоЧека=(ЗагружаемыйЧек=Неопределено);
	// Если ничего не было передано, то производим заполнение по умолчанию
	Если ЗагружаемыйЧек=Неопределено Тогда
		ЗагружаемыйЧек=Документы.Чек.СоздатьДокумент();
		ЗагружаемыйЧек.Заполнить(Неопределено);
		ПрефиксДокументаЧек = обПолучитьПрефиксОбъекта(ЗагружаемыйЧек);
		ЗагружаемыйЧек.ТипЦен=ТипЦен;
		ЗагружаемыйЧек.Контрагент = Неопределено; 
		
		СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
		ПараметрыСкидкиШапки = Неопределено;
		Если НЕ УстановленРежим("НачалоРаботыФронта") Тогда
			УстановитьРежим("Чек");
		КонецЕсли;
		// очистим информации с дисплея
		ОбновитьИндикаторы(ЭтаФорма);
	КонецЕсли; 
	
	// Установим Способ расчета в значение Передача (при необходимости переопределим параметр ниже)
	ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача;
	ДанныеКлиента = "";
	КодВидаДокументаУдостоверяющегоЛичность = 0;
	ДанныеДокументаУдостоверяющегоЛичность = "";
	АдресПокупателя = "";
	
	ПечатьТабличнойЧастиТовары = Истина;
	
	// Переносим всю информацию из загружаемого документа в наш объект
	//РеквизитыДляСохранения = Новый Структура("ТипЦен,ФР,Организация,ПодразделениеКомпании,СкладКомпании,КассаККМ", ТипЦен, ФР, Организация, ПодразделениеКомпании, СкладКомпании, КассаККМ);
	РеквизитыДляСохранения = Новый Структура();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗагружаемыйЧек,, ?(УстановленРежим("ВыборЧекаВозврат"), "КоличествоКНачислению,КоличествоКСписанию", ""));
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыДляСохранения);
	
	// Установим Кассира
	Автор = ПараметрыСеанса.Пользователь; 
		
	// Определим к какому компьютеру относится данное рабочее место
	Компьютер = ПараметрыСеанса.Компьютер;
	ОбработкаРеквизита("Компьютер",,ЭтаФорма);
	
	// Определим основной склад продажи
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(ЗагружаемыйЧек));
	Если НЕ(МетаданныеОбъекта=Неопределено ИЛИ МетаданныеОбъекта.Реквизиты.Найти("СкладКомпании")=Неопределено) И ЗначениеЗаполнено(ЗагружаемыйЧек.СкладКомпании) И НЕ СозданиеНовогоЧека Тогда
		СкладКомпании = ЗагружаемыйЧек.СкладКомпании;
	Иначе
		СкладКомпании = ПараметрыСеанса.Компьютер.Отдел;
	КонецЕсли;
	ОбработкаРеквизита("СкладКомпании",, ЭтаФорма);
	
	Если обЗначениеНеЗаполнено(КассаККМ) Тогда
		КассаККМ=ПараметрыСеанса.Компьютер.КассаККМ;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ФР) Тогда
		ФР=ПараметрыСеанса.Компьютер.ФР;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Организация) Тогда
		Организация=КассаККМ.Организация;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
		ПодразделениеКомпании=КассаККМ.Подразделение;
	КонецЕсли;
	
	АвтоЗаполнениеТоваров = Ложь;
	ЕстьДокументыРеализации = Ложь;
	
	// Обработаем некоторые моменты раздельно (в зависимости от вида документа)
	Если ТипЗнч(ЗагружаемыйЧек)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		ИЛИ ТипЗнч(ЗагружаемыйЧек)=Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		ИЛИ ТипЗнч(ЗагружаемыйЧек)=Тип("ДокументСсылка.Выписка") Тогда
		// Производим корректировку валюты
		ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		КурсДокумента	= 1;
		
		// Очищаем таблицы чека
		Товары.Очистить();
		ОплатыОчистить(ЭтаФорма);
		
		// Добавляем предопределенный элемент в таблицу
		Если ТипЗнч(ЗагружаемыйЧек)=Тип("ДокументСсылка.Выписка") Тогда
			
			Если ЗагружаемыйЧек.Товары.Количество() > 0 Тогда
				Товары.Загрузить(ЗагружаемыйЧек.Товары.Выгрузить());
				КодыМаркировки.Загрузить(ЗагружаемыйЧек.КодыМаркировки.Выгрузить());
				ПечатьТабличнойЧастиТовары = (Товары[0].Номенклатура = Справочники.Номенклатура.Предоплата
					И Товары.Количество() > 0 ИЛИ НЕ Товары[0].Номенклатура = Справочники.Номенклатура.Предоплата);
				ЭтотОбъект.СуммаДокумента = ЗагружаемыйЧек.СуммаДокументаПриход + ЗагружаемыйЧек.СуммаДокументаРасход;
			Иначе
				
				//anton
				Попытка
					Если ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ЧекНаОплату") И ЗначениеЗаполнено(ЗагружаемыйЧек.ДокументОснование.ТипСпособаРасчетаККТ) Тогда
						ТипСпособаРасчетаККТ = ЗагружаемыйЧек.ДокументОснование.ТипСпособаРасчетаККТ;
					КонецЕсли;
				Исключение
				КонецПопытки;
				//anton
				
				// Определим тип способа расчета ККТ
				Если ЗначениеЗаполнено(ЗагружаемыйЧек.ТипСпособаРасчетаККТ) Тогда
					ТипСпособаРасчетаККТ = ЗагружаемыйЧек.ТипСпособаРасчетаККТ;
				КонецЕсли;
				
				// Получим сумму оплаты и сделки
				ТаблицаСделок = ЗагружаемыйЧек.Состав.Выгрузить(, "Сделка,СуммаПриход,СуммаРасход");
				ТаблицаСделок.Свернуть("Сделка", "СуммаПриход,СуммаРасход");
				ТаблицаСделок.Сортировать("Сделка");
				
				// Подготовим данные для получение номенклатуры по сделке
				СтрукутраСделки = Новый Структура("ДокументОснование,ВалютаДокумента,КурсДокумента");
				ЗаполнитьЗначенияСвойств(СтрукутраСделки, ЗагружаемыйЧек,, "ДокументОснование");
				
				КопияТовары = Товары.Выгрузить();
				
				// Добавим строку для формирование без сделки
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура = Справочники.Номенклатура.Предоплата;
				ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока, ЭтаФорма);
				НоваяСтрока.СтавкаНДС = ЗагружаемыйЧек.СтавкаНДС;
				КурсДокументаОплаты = ЗагружаемыйЧек.КурсДокумента;
				
				// Введем для формирования считанных кодов маркировки
				КодыМаркировки.Очистить();
				СтруктураОбъекта = Новый Структура("КодыМаркировки, Товары");
				СтруктураОбъекта.КодыМаркировки = КодыМаркировки.Выгрузить();
				
				// Распределим сумму оплатц/возрата по товарам сделки
				Для Каждого ТекущаяСтрока Из ТаблицаСделок Цикл
					СтрукутраСделки.ДокументОснование = ТекущаяСтрока.Сделка;
					ТабличнаяЧастьДокументаОснования=Документы.ПриходныйКассовыйОрдер.ПолучитьТабличнуюЧастьДокументаОснования(СтрукутраСделки);
					
					// Укажем сумму сделки как предоплата
					Если ТабличнаяЧастьДокументаОснования=Неопределено ИЛИ ТабличнаяЧастьДокументаОснования.Количество()=0 Тогда
						Товары[0].Цена = Товары[0].Цена + (ТекущаяСтрока.СуммаПриход + ТекущаяСтрока.СуммаРасход) * ЗагружаемыйЧек.КурсДокумента;
					Иначе
						
						Для Каждого ТекущаяСтрокаТоваров Из ТабличнаяЧастьДокументаОснования Цикл
							НоваяСтрока = КопияТовары.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрокаТоваров);
							НоваяСтрока.Цена = ТекущаяСтрокаТоваров.Цена * КурсДокументаОплаты;
							НоваяСтрока.Сумма = ТекущаяСтрокаТоваров.Сумма * КурсДокументаОплаты;
							НоваяСтрока.СуммаНДС = ТекущаяСтрокаТоваров.СуммаНДС * КурсДокументаОплаты;
							НоваяСтрока.СуммаСкидки = ТекущаяСтрокаТоваров.СуммаСкидки * КурсДокументаОплаты;
							НоваяСтрока.СуммаВсего = ТекущаяСтрокаТоваров.СуммаВсего * КурсДокументаОплаты;
							НоваяСтрока.СуммаСкидкиСтроки = ТекущаяСтрокаТоваров.СуммаСкидкиСтроки * КурсДокументаОплаты;
							НоваяСтрока.СуммаСкидкиБонусами = ТекущаяСтрокаТоваров.СуммаСкидкиБонусами * КурсДокументаОплаты;
						КонецЦикла;
						
						дкЗаполнитьГТД(ЗагружаемыйЧек.ПолучитьОбъект(),КопияТовары,ТекущаяСтрока.Сделка,ЭтаФорма);
						
						// Распределим текущую сумму оплат на сделку
						Документы.ЧекНаОплату.ПроизвестиРаспределениеСуммыОплаты((ТекущаяСтрока.СуммаПриход + ТекущаяСтрока.СуммаРасход) * КурсДокументаОплаты, КопияТовары);
						
						// Заполним маркировку
						СтруктураОбъекта.Товары = КопияТовары;
						дкЗаполнитьКодыМаркировкиТоваров(СтруктураОбъекта, ТекущаяСтрока.Сделка);
						
						// Перенесем в Товары
						Для Каждого ТекущаяСтрокаНоменклатуры Из КопияТовары Цикл
							ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТекущаяСтрокаНоменклатуры,, "НомерСтроки");
						КонецЦикла;
						
						// Перенесем в КодыМаркировки
						Для Каждого ТекущаяСтрокаМаркировки Из СтруктураОбъекта.КодыМаркировки Цикл
							ЗаполнитьЗначенияСвойств(КодыМаркировки.Добавить(), ТекущаяСтрокаМаркировки);
						КонецЦикла;
						
						КопияТовары.Очистить();
						
					КонецЕсли;
					
				КонецЦикла;
				
				// Проверим наличие предоплат
				Если Товары[0].Цена = 0 Тогда
					Товары.Удалить(0);
					ПечатьТабличнойЧастиТовары = (Товары.Количество() > 0);
				Иначе
					ОбработкаРеквизита("Товары.Цена", Товары[0], ЭтаФорма);
					Товары[0].СуммаОплаты = Товары[0].Цена;
					ПечатьТабличнойЧастиТовары = (Товары.Количество() > 1);
				КонецЕсли;
				
				ЭтотОбъект.СуммаДокумента = ЗагружаемыйЧек.СуммаДокументаПриход + ЗагружаемыйЧек.СуммаДокументаРасход;
				
			КонецЕсли;
			
			// Проверим наличие сделок реализации товаров
			МассивТиповСделок = Новый Массив;
			МассивТиповСделок.Добавить(Тип("ДокументСсылка.РеализацияТоваров"));
			МассивТиповСделок.Добавить(Тип("ДокументСсылка.ЗаказНаряд"));
			
			Для Каждого ТекущаяСделка Из ЗагружаемыйЧек.Состав Цикл
				Если ЗначениеЗаполнено(ТекущаяСделка)
					И МассивТиповСделок.Найти(ТипЗнч(ТекущаяСделка.Сделка)) <> Неопределено Тогда
					ЕстьДокументыРеализации = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Если ЗагружаемыйЧек.Товары.Количество() > 0 Тогда
				Товары.Загрузить(ЗагружаемыйЧек.Товары.Выгрузить());
				Попытка
					КодыМаркировки.Загрузить(ЗагружаемыйЧек.КодыМаркировки.Выгрузить());
				Исключение
				КонецПопытки;
				ПечатьТабличнойЧастиТовары = Истина;
			Иначе
				АвтоЗаполнениеТоваров = Истина;
				ДокументОплаты = ?(ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер"), ЗагружаемыйЧек.ДокументОснование, ЗагружаемыйЧек);
				ТабличнаяЧастьДокументаОснования=Документы.ПриходныйКассовыйОрдер.ПолучитьТабличнуюЧастьДокументаОснования(ДокументОплаты);
				
				Если ТабличнаяЧастьДокументаОснования=Неопределено ИЛИ ТабличнаяЧастьДокументаОснования.Количество()=0 Тогда
					НоваяСтрока = Товары.Добавить();
					НоваяСтрока.Номенклатура = Справочники.Номенклатура.Предоплата;
					ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока, ЭтаФорма);
					НоваяСтрока.Цена = ЗагружаемыйЧек.СуммаДокумента * ЗагружаемыйЧек.КурсДокумента;
					НоваяСтрока.СтавкаНДС = ЗагружаемыйЧек.СтавкаНДС;
					ОбработкаРеквизита("Товары.Цена", НоваяСтрока, ЭтаФорма);
					ПечатьТабличнойЧастиТовары = Ложь;
				Иначе
					Товары.Загрузить(ТабличнаяЧастьДокументаОснования);
					КурсДокументаОплаты = ДокументОплаты.КурсДокумента;
					Для Каждого ТекущаяСтрока Из Товары Цикл
						ТекущаяСтрока.Цена = ТекущаяСтрока.Цена * КурсДокументаОплаты;
						ТекущаяСтрока.Сумма = ТекущаяСтрока.Сумма * КурсДокументаОплаты;
						ТекущаяСтрока.СуммаНДС = ТекущаяСтрока.СуммаНДС * КурсДокументаОплаты;
						ТекущаяСтрока.СуммаСкидки = ТекущаяСтрока.СуммаСкидки * КурсДокументаОплаты;
						ТекущаяСтрока.СуммаВсего = ТекущаяСтрока.СуммаВсего * КурсДокументаОплаты;
						ТекущаяСтрока.СуммаСкидкиСтроки = ТекущаяСтрока.СуммаСкидкиСтроки * КурсДокументаОплаты;
						ТекущаяСтрока.СуммаСкидкиБонусами = ТекущаяСтрока.СуммаСкидкиБонусами * КурсДокументаОплаты;
					КонецЦикла;
				КонецЕсли;
				
				Документы.ЧекНаОплату.ПроизвестиРаспределениеСуммыОплаты(ЗагружаемыйЧек.СуммаДокумента * ЗагружаемыйЧек.КурсДокумента, Товары);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗагружаемыйЧек.ТипСпособаРасчетаККТ) Тогда
				ТипСпособаРасчетаККТ = ЗагружаемыйЧек.ТипСпособаРасчетаККТ;
			ИначеЕсли ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") И ЗначениеЗаполнено(ЗагружаемыйЧек.ДокументОснование.ТипСпособаРасчетаККТ) Тогда
				ТипСпособаРасчетаККТ = ЗагружаемыйЧек.ДокументОснование.ТипСпособаРасчетаККТ;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ЗагружаемыйЧек) = Тип("ДокументСсылка.Чек") И УстановленРежим("ВыборЧекаВозврат") Тогда
		ВключаетНДС = ЗагружаемыйЧек.ТипЦен.ЦенаВключаетНДС;
		
		ДопПараметры = Новый Структура("ЗапретРасчетаСкидкокШапки", Истина); //Скидки будет расчитаны вне цикла
		Для Каждого Товар Из ЗагружаемыйЧек.Товары.Выгрузить() Цикл
			
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Товар,, "СуммаСкидкиБонусами");
			
			Если ЗначениеЗаполнено(Товар.СуммаСкидкиБонусами) Тогда
				
				СтавкаНДС = НоваяСтрока.СтавкаНДС.Ставка;
				СуммаБезСкидки = НоваяСтрока.СуммаВсего + НоваяСтрока.СуммаСкидки + НоваяСтрока.СуммаСкидкиСтроки - ?(ВключаетНДС, 0, Окр((НоваяСтрока.СуммаВсего * СтавкаНДС)/(100 + СтавкаНДС),2));
				
				НоваяСтрока.Сумма = СуммаБезСкидки;
				НоваяСтрока.Цена = СуммаБезСкидки / ?(Товар.Количество*Товар.Коэффициент = 0, 1, НоваяСтрока.Количество*НоваяСтрока.Коэффициент);
				
			КонецЕсли;
			
			ОбработкаРеквизита("Товары.СуммаВсего", НоваяСтрока, ЭтаФорма, ДопПараметры);
			НоваяСтрока.ИдентификаторТовара = Новый УникальныйИдентификатор;
		КонецЦикла;
		
		ДопПараметры = Неопределено;
		ОбработкаРеквизита("СуммаДокумента", ,ЭтаФорма); //Будут расчитаны скидки шапки		
		Попытка
			СуммаСкидкиНаценкиКЕШ    = Товары.Итог("СуммаСкидки");
			ЗначениеСкидкиНаценкиКЕШ = ЗагружаемыйЧек.ЗначениеСкидкиНаценки;
		Исключение
		КонецПопытки;
		Оплаты.Загрузить(ЗагружаемыйЧек.Оплаты.Выгрузить());
		ОплатыПриОкончанииРедактирования(ЭтаФорма);
	Иначе
		Товары.Загрузить(ЗагружаемыйЧек.Товары.Выгрузить());
		Попытка
			СуммаСкидкиНаценкиКЕШ    = Товары.Итог("СуммаСкидки");
			ЗначениеСкидкиНаценкиКЕШ = ЗагружаемыйЧек.ЗначениеСкидкиНаценки;
		Исключение
		КонецПопытки;
		Попытка
			КодыМаркировки.Загрузить(ЗагружаемыйЧек.КодыМаркировки.Выгрузить());
		Исключение
		КонецПопытки;
		Оплаты.Загрузить(ЗагружаемыйЧек.Оплаты.Выгрузить());
		ОплатыПриОкончанииРедактирования(ЭтаФорма);
	КонецЕсли;
	
	Если АвтоЗаполнениеТоваров И Товары.Количество()>0 И НЕ (ТипЗнч(ЗагружаемыйЧек) = Тип("ДокументСсылка.Чек") ИЛИ ТипЗнч(ЗагружаемыйЧек) = Тип("ДокументСсылка.ЧекНаОплату")) 
		И ЗначениеЗаполнено(ЗагружаемыйЧек.ДокументОснование) И (ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров")
		ИЛИ ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ВозвратПоставщику") ИЛИ ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")
		ИЛИ ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитенту")
		ИЛИ ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ВозвратОтПокупателя") ИЛИ (ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплату")
		И НЕ обЗначениеНеЗаполнено(ЗагружаемыйЧек.ДокументОснование.ДокументОснование) И (ТипЗнч(ЗагружаемыйЧек.ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров")
		ИЛИ ТипЗнч(ЗагружаемыйЧек.ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")))
		ИЛИ (ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.СчетОтПоставщика") И 
		НЕ обЗначениеНеЗаполнено(ЗагружаемыйЧек.ДокументОснование.ДокументОснование) И ТипЗнч(ЗагружаемыйЧек.ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров"))) Тогда
		дкЗаполнитьГТД(ЗагружаемыйЧек.ПолучитьОбъект(),Товары,,ЭтаФорма);
		дкЗаполнитьКодыМаркировкиТоваров(ЭтотОбъект, ЗагружаемыйЧек.ДокументОснование);
	КонецЕсли;
	
	ЕстьОшибки = Ложь;	
	// Проверим заполнение кодов маркировки при реализации
	Если ((ТипЗнч(ЗагружаемыйЧек)=Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		И (ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров")
		ИЛИ ТипЗнч(ЗагружаемыйЧек.ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")))
		ИЛИ (ТипЗнч(ЗагружаемыйЧек)=Тип("ДокументСсылка.Выписка") И ЕстьДокументыРеализации))
		И ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача
		И ЗагружаемыйЧек.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда
		ТекстыОшибок = "";
		дкПроверитьКорректностьМаркировки(ЭтотОбъект,,, ТекстыОшибок);
		Если НЕ ПустаяСтрока(ТекстыОшибок) Тогда
			ЕстьОшибки = Истина;
			ТекстОшибки = "Проверьте корректность ввода кодов маркировки в документе оплаты. Операция отменена.";			
		КонецЕсли;
	КонецЕсли;	
	
	// Проверим корректность суммы оплаты при передачи товара и услуги
	Если РежимПередачиТовараСЗакрытиемОстаткаОплатыНаКредит(ЗагружаемыйЧек) Тогда
			
		СписокДоступныхСпособовРасчетаККТ = ПроцедурыОбщегоНазначенияКлиентСервер.СписокДоступныхСпособовРасчетаККТ(ЗагружаемыйЧек.ДокументОснование);
		Если СписокДоступныхСпособовРасчетаККТ.НайтиПоЗначению(Перечисления.ТипыСпособаРасчетаККТ.Передача) = Неопределено
			ИЛИ Товары.Количество() = 0
			ИЛИ Товары[0].Номенклатура = Справочники.Номенклатура.Предоплата Тогда		
			
			ЕстьОшибки = Истина;
			ТекстОшибки = СтрШаблон("Способ расчета <Передача товара/услуг> недоступен для документа основания <%1>, выберите другой способ расчета в документе оплаты",
				ЗагружаемыйЧек.ДокументОснование);
				
		КонецЕсли;
		
		Если НЕ ЕстьОшибки Тогда
		
			СуммаДокументаОплаты = ЗагружаемыйЧек.СуммаДокумента;
			СуммаТовараУслуг = Товары.Итог("СуммаВсего"); 
			
			Если СуммаДокументаОплаты > СуммаТовараУслуг Тогда
				
				ЕстьОшибки = Истина;				
				ТекстОшибки = СтрШаблон("Сумма документа оплаты <%1> превышает сумму товара и услуг <%2>.
										|Выберите способ расчета, отличный от <Передача товара/услуг> или скорректируйте сумму оплаты",
					СуммаДокументаОплаты, СуммаТовараУслуг); 
						
			КонецЕсли;	
		
		КонецЕсли;				 
		
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		
		ЗагрузитьЧек(Неопределено,ЭтаФорма);
		ЗагружаемыйЧек = Неопределено; 
		ОткрытьДиалог(ТекстОшибки, СтатусСообщения.Важное);
		Возврат;
		
	КонецЕсли;	
	
	УстановитьфлИспользованиеХарактеристик();
	
	// Обработаем интерфейсное изменение объекта
	Если ЭтаФорма<>Неопределено Тогда
		ЭтаФорма.ЭлементыФормы.Товары.Колонки.ИтоговаяСуммаСкидки.ТекстПодвала = Товары.Итог("СуммаСкидки") + Товары.Итог("СуммаСкидкиСтроки"); 
		ЭтаФорма.УправлениеИнтерфейсом();
		ЭтаФорма.ОтображениеКолонкиХарактеристик();
		Попытка
			ЭтаФорма.ОтображениеКолонкиКодовМаркировки();
		Исключение
		КонецПопытки;
		ТекущаяСтрока = ПолучитьТекущуюСтрокуФронта(ЭтаФорма);
		ОбновитьИндикаторы(ЭтаФорма);
	КонецЕсли;
	
	// очистим все 
	тзПлатежиЗаУслуги.Очистить();
	
	Поле = "";
	
КонецПроцедуры // ЗагрузитьЧек()

//Запись текущего чека
//Если ФлагПроведения = Истина - документ проводиться, в противном случае только записывается
//Возвращаемое значение
//	Истина - удалось сохранить чек
//	Ложь - ошибка при сохранении
Функция ЗаписатьЧек(ФлагПроведения,СообщениеОбОшибке,ПраваДополнительно=НЕОПРЕДЕЛЕНО, КПопыток=-1, ПроверитьКорректность=ЛОЖЬ) Экспорт
	Результат=ЛОЖЬ;
	
	// Сформируем и заполним объект для записи в БД
	Если ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер ИЛИ (НЕ УстановленРежим("РКО") И ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер)
		ИЛИ ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки Тогда
		ДокументОбъект = Ссылка.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(ДокументОбъект, ЭтотОбъект, "НомерЧека,ДатаФР,НомерДокумента,НомерСмены,ФР,КассаККМ,СкладКомпании,ФискальныйПризнак");
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПриходныйКассовыйОрдер") Тогда
			ДокументОбъект.ТипСпособаРасчетаККТ = ЭтотОбъект.ТипСпособаРасчетаККТ;
		КонецЕсли;
	ИначеЕсли (ХозОперация=Справочники.ХозОперации.ЧекНаОплату 
		ИЛИ ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупки) И (НЕ Ссылка.Пустая()) Тогда
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		ДокументОбъект.Оплаты.Загрузить(Оплаты.Выгрузить()); 
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ЭтотОбъект, "НомерЧека,ДатаФР,НомерДокумента,НомерСмены,ФР,КассаККМ,СкладКомпании,ПолученоНал,ПолученоБезНал,ТипСпособаРасчетаККТ,ФискальныйПризнак");
		Попытка ДокументОбъект.АвтоЗакрытиеСделок = ЭтотОбъект.ДоговорВзаиморасчетов.АвтоЗакрытиеСделок; Исключение КонецПопытки;
		ДокументОбъект.Дата=ТекущаяДата();
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя Тогда
		ДокументОбъект = Документы.ВозвратОтПокупателя.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ЭтотОбъект,,"Номер");
		ДокументОбъект.Дата=ТекущаяДата();
		Если обЗначениеНеЗаполнено(ДокументОбъект.Контрагент) Тогда 
			ДокументОбъект.Контрагент            = Справочники.Контрагенты.ОсновнойПокупатель;
			ДокументОбъект.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ДокументОбъект.Контрагент, Перечисления.ВидыДоговоров.Продажа, ДокументОбъект,,, Истина);
		КонецЕсли;
		ДокументОбъект.Товары.Загрузить(Товары.Выгрузить());
		мсКолва = Товары.ВыгрузитьКолонку("Количество");
		ДокументОбъект.Товары.ЗагрузитьКолонку(мсКолва,"КоличествоБазовое");
		Ссылка = ДокументОбъект.Ссылка;
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			ДокументОбъект.Комментарий = "Возврат по чеку № "+ДокументОснование.НомерЧека+" от "+ДокументОснование.ДатаФР;
		КонецЕсли;
	ИначеЕсли ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер Тогда
		ДокументОбъект = Документы.РасходныйКассовыйОрдер.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ЭтотОбъект,,"Номер");
		ДокументОбъект.Дата = ТекущаяДата();
		ДокументОбъект.КассаКомпании = ОсновнаяКассаКомпании; //КассаККМ;
		Если обЗначениеНеЗаполнено(ДокументОбъект.Контрагент) Тогда 
			ДокументОбъект.Контрагент            = Справочники.Контрагенты.ОсновнойПокупатель; 
			ДокументОбъект.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ДокументОбъект.Контрагент, Перечисления.ВидыДоговоров.Продажа, ДокументОбъект,,, Истина);
		КонецЕсли;
		ДокументОбъект.СтатьяДДС = Справочники.СтатьиДДС.ВозвратПокупателю;
	Иначе
		Если Ссылка.Пустая() Тогда
			Если ХозОперация=Справочники.ХозОперации.Чек ИЛИ
				ХозОперация=Справочники.ХозОперации.ЧекНаВозврат ИЛИ
				ХозОперация=Справочники.ХозОперации.ЧекОтложенный Тогда
				ДокументОбъект=Документы.Чек.СоздатьДокумент();
			Иначе
				ДокументОбъект=Документы.ЧекНаОплату.СоздатьДокумент();
			КонецЕсли;
		Иначе
			ДокументОбъект=Ссылка.ПолучитьОбъект();
		КонецЕсли;
		ДокументОбъект.Дата=ТекущаяДата();
		ДокументОбъект.Автор=Автор;
		ДокументОбъект.Организация=Организация;
		ДокументОбъект.ПодразделениеКомпании=ПодразделениеКомпании;
		ДокументОбъект.СкладКомпании=СкладКомпании;
		Попытка ДокументОбъект.РегламентированныйУчет=РегламентированныйУчет; Исключение КонецПопытки;
		ДокументОбъект.Комментарий=Комментарий;
		ДокументОбъект.Контрагент=Контрагент;
		ДокументОбъект.Карточка=Карточка;
		Попытка ДокументОбъект.ДоговорВзаиморасчетов=ДоговорВзаиморасчетов; Исключение КонецПопытки; 
		ДокументОбъект.ВалютаДокумента=ВалютаДокумента;
		ДокументОбъект.КурсДокумента=КурсДокумента;
		ДокументОбъект.ДокументОснование=ДокументОснование;
		Если ПроцедурыОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, "Сделка") Тогда		
			ДокументОбъект.Сделка=Сделка;		
		КонецЕсли;		
		ДокументОбъект.ТипЦен=ТипЦен;
		Если ПроцедурыОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, "БлокироватьПерерасчетСкидок") Тогда		
			ДокументОбъект.БлокироватьПерерасчетСкидок=БлокироватьПерерасчетСкидок;		
		КонецЕсли;
		ДокументОбъект.СкидкаНаценка=СкидкаНаценка;
		ДокументОбъект.ЗначениеСкидкиНаценки = ЗначениеСкидкиНаценкиКЕШ;
		ДокументОбъект.СуммаСкидкиНаценки = ?(СуммаСкидкиНаценкиКЕШ = Неопределено, 0, СуммаСкидкиНаценкиКЕШ) + Товары.Итог("СуммаСкидкиСтроки");
		ДокументОбъект.СуммаДокумента=СуммаДокумента;
		Попытка ДокументОбъект.СкладКомпании=СкладКомпании; Исключение КонецПопытки; 
		ДокументОбъект.ХозОперация=ХозОперация;
		ДокументОбъект.ВидОплаты=ВидОплаты;
		Попытка
			ДокументОбъект.ДляПробитияНаФР=Истина;
		Исключение
		КонецПопытки; 
		ДокументОбъект.КассаККМ=КассаККМ;
		ДокументОбъект.ФР=ФР;
		ДокументОбъект.НомерЧека=НомерЧека;
		ДокументОбъект.НомерДокумента=НомерДокумента;
		ДокументОбъект.НомерСмены=НомерСмены;
		ДокументОбъект.ДатаФР=ДатаФР;
		ДокументОбъект.ПолученоНал=ПолученоНал;
		ДокументОбъект.ПолученоБезнал=ПолученоБезнал;
		ДокументОбъект.ФискальныйПризнак=ФискальныйПризнак;
		ДокументОбъект.Товары.Загрузить(Товары.Выгрузить());
		ДокументОбъект.Оплаты.Загрузить(Оплаты.Выгрузить());
		
		Попытка
			ДокументОбъект.КодыМаркировки.Загрузить(КодыМаркировки.Выгрузить());
		Исключение
		КонецПопытки;
		
		Если ХозОперация = Справочники.ХозОперации.Чек Тогда
			ДокументОбъект.КоличествоКНачислению = КоличествоКНачислению;
			ДокументОбъект.КоличествоКСписанию   = КоличествоКСписанию;
		КонецЕсли;
		
		Если ХозОперация = Справочники.ХозОперации.ЧекНаВозврат Тогда
			Если ЗначениеЗаполнено(ДокументОснование) Тогда
				ДокументОбъект.Комментарий = "Возврат по чеку № "+ДокументОснование.НомерЧека+" от "+ДокументОснование.ДатаФР;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЧекНаОплату") Тогда
			ДокументОбъект.СтавкаНДС = СтавкаНДСЧекаНаОплату;
			ДокументОбъект.СуммаНДС  = СуммаНДСЧекаНаОплату;
			ДокументОбъект.ТипСпособаРасчетаККТ = ТипСпособаРасчетаККТ;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ (ХозОперация=Справочники.ХозОперации.ПриходныйКассовыйОрдер ИЛИ
		      ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя ИЛИ
			  ХозОперация=Справочники.ХозОперации.РасходныйКассовыйОрдер ИЛИ
			  ХозОперация=Справочники.ХозОперации.СтрокаБанковскойВыписки) Тогда
		Если ПолученоНал + ПолученоБезнал > СуммаДокумента Тогда
			// Скорректируем оплату наличными
			СуммаСдачи = (ПолученоНал + ПолученоБезнал) - СуммаДокумента;
			Если СуммаСдачи > ПолученоНал Тогда СуммаСдачи = ПолученоНал; КонецЕсли;
			Для Каждого СтрокаОплат из ДокументОбъект.Оплаты Цикл
				Если  СтрокаОплат.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Наличные Тогда
					Если СуммаСдачи < СтрокаОплат.Сумма Тогда
						СтрокаОплат.Сумма = СтрокаОплат.Сумма-СуммаСдачи;
						СуммаСдачи = 0;
					ИначеЕсли СуммаСдачи = СтрокаОплат.Сумма Тогда
						ДокументОбъект.Оплаты.Удалить(СтрокаОплат);
						СуммаСдачи = 0;
					Иначе
						СуммаСдачи = СуммаСдачи - СтрокаОплат.Сумма;
						ДокументОбъект.Оплаты.Удалить(СтрокаОплат);
					КонецЕсли;
				КонецЕсли;
				Если  СуммаСдачи <= 0 Тогда Прервать; КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Выполняем проверку корректности заполнения документа
	Если ПроверитьКорректность Тогда
		ДокументОбъект.СообщениеОбОшибке="";
		Если ХозОперация=Справочники.ХозОперации.Чек Тогда 
			// Проверка чека должна включать проверку остатков товаров
			Если НЕ ДокументОбъект.ПроверитьКорректность(ДокументОбъект.СообщениеОбОшибке,, ИСТИНА, ИСТИНА, ИСТИНА) Тогда 
				СообщениеОбОшибке = ДокументОбъект.СообщениеОбОшибке;
				ЗаписатьВФайлЛога("Записать чек", 1, "ОШИБКА ПРИ ЗАПИСИ ЧЕКА: "+СообщениеОбОшибке,,1);
				Возврат ЛОЖЬ;
			КонецЕсли;
		Иначе
			Если НЕ ДокументОбъект.ПроверитьКорректность(ДокументОбъект.СообщениеОбОшибке,, ИСТИНА, ИСТИНА) Тогда 
				СообщениеОбОшибке = ДокументОбъект.СообщениеОбОшибке;
				ЗаписатьВФайлЛога("Записать чек", 1, "ОШИБКА ПРИ ЗАПИСИ ЧЕКА: "+СообщениеОбОшибке,,1);
				Возврат ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Заносим данные в базу
	Попытка
		ДокументОбъект.СообщениеОбОшибке = "";
		ДокументОбъект.Записать(?(ФлагПроведения,РежимЗаписиДокумента.Проведение,РежимЗаписиДокумента.Запись));
		Результат = Истина;
		Ссылка = ДокументОбъект.Ссылка;
		
		// Производим сохранение редактируемого чека со статусом "Пробит" или "Отложен"
		Если УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") Тогда
			АрхивЧековСохранить(ДокументОбъект,?(ДокументОбъект.ХозОперация=Справочники.ХозОперации.ЧекОтложенный, 2, 1));
		КонецЕсли;
		
		Оповестить("ОбновитьФорму", Ссылка, ЭтотОбъект);
	Исключение
		
		Если КПопыток <> 0 Тогда
			
			// Сделаем еще 3 попытки записи чека
			КПопыток = ?(КПопыток=-1, 3, КПопыток-1);
			Возврат ЗаписатьЧек(Ложь, СообщениеОбОшибке,, КПопыток);
			
		КонецЕсли;
		
		СообщениеОбОшибке = ОписаниеОшибки();
		СообщениеОбОшибке = ?(ПустаяСтрока(ДокументОбъект.СообщениеОбОшибке), СообщениеОбОшибке, ДокументОбъект.СообщениеОбОшибке);
		ЗаписатьВФайлЛога(
			?(ФлагПроведения, "Провести чек", "Записать чек"),
			1,
			?(ФлагПроведения, "ОШИБКА ПРИ ПРОВЕДЕНИИ ЧЕКА: ", "ОШИБКА ПРИ ЗАПИСИ ЧЕКА: ") + СообщениеОбОшибке,,
			1);
		
	КонецПопытки;
	
	// Запишем информацию о чеке в Регистр сведений платежи
	Если УстановленРежим("Чек") Тогда
		ФискализироватьПлатежи();
	ИначеЕсли УстановленРежим("ЧекНаВозврат") Тогда 
		УдалитьПлатежи();
	КонецЕсли;
	
	Если РежимДиагностики И (НЕ Результат ИЛИ НЕ ПустаяСтрока(СообщениеОбОшибке)) Тогда
		ЗаписьЖурналаРегистрации("ФронтКассира."+?(ФлагПроведения,"ПровестиЧек","ЗаписатьЧек"),УровеньЖурналаРегистрации.Ошибка,,,СтрЗаменить(СообщениеОбОшибке,Символы.ПС," "));
	КонецЕсли;
	
	Если Результат Тогда
		ЗаписатьВФайлЛога(?(ФлагПроведения,"Провести чек","ЗаписатьЧек"), 0, ?(ФлагПроведения,"Документ проведен ","Документ записан ") + ДокументОбъект.Ссылка);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ЗаписатьЧек()


// Открытие калькулятора для ввода числового значения.
//
// Параметры:
//  ТекущееЗначение - Число, начальное значение поля калькулятора
//  ФорматСуммы  - Булево, флаг, определяющий формат значения в поле ввода.
//  Заголовок    - Строка, заголовок формы.
//  Состояние    - Строка, заголовок элемента-состояния.
//  ДоступностьМинус - Доступность кнопки минус.
//  ДоступностьЗапятая - Доступность кнопки запятая
//
Функция ОткрытьКалькулятор(ТекущееЗначение = 0, ФорматСуммы = Ложь, Заголовок = "", Состояние = "", ДоступностьВес = Ложь, ТекстВыполнить = "Выбор", ДоступностьЗапятая = Истина, ТолькоВес = Ложь) Экспорт
	
	ФормаКалькулятор = ПолучитьФорму("Обработка.ФронтКассира.Форма.Калькулятор",);
	
	Если ФормаКалькулятор.Открыта() Тогда Возврат Неопределено КонецЕсли;
	
	ФормаКалькулятор.ТекущееЗначение     = ТекущееЗначение;
	ФормаКалькулятор.ФорматСуммы         = ФорматСуммы;
	ФормаКалькулятор.Заголовок           = Заголовок;
	ФормаКалькулятор.ИнформацияСостояние = Состояние;
		
	ФормаКалькулятор.ЭлементыФормы.КнопкаВыбрать.Заголовок = ТекстВыполнить;

	Возврат ФормаКалькулятор.ОткрытьМодально();
	
КонецФункции
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ОБОРУДОВАНИЕМ

// Выводит информацию на дисплей покупателя (если он подключен).
// Рассчитана на работу с дисплеями формата 2 строки х 20 символов
//
// Параметры:
//	Стр1      - Строка для вывода в верхнюю строчку или товарная позиция (слева)
// 	ОкончСтр1 - Строка для вывода в верхнюю строчку справа
//  Стр2      - Строка для вывода в нижнюю строчку дисплея (слева)
//  ОкончСтр1 - Строка для вывода в нижнюю строчку справа
//  Реж       - режим дополнения левой части строки (0-слева, 1-справа, 2-размещать по центру)
//
Процедура ВывестиНаДисплей(Знач Стр1="", Знач ОкончСтр1="", Знач Стр2="", Знач ОкончСтр2="",Знач Реж=1) Экспорт
	Если ПустаяСтрока(GUID_Дисплей) Тогда Возврат; КонецЕсли;
	// Формируем первую строку
	ТипСтр1=ТипЗнч(Стр1);
	Если НЕ ПустаяСтрока(СокрЛП(Строка(Стр1))) Тогда
		Если ТипСтр1=Тип("Строка") Тогда									Стр1=СокрЛП(Стр1);
		ИначеЕсли ТипСтр1=Тип("СправочникСсылка.Номенклатура") Тогда		Стр1=СокрЛП(Стр1.Наименование);
		Иначе																Стр1=Строка(Стр1);
		КонецЕсли;
		ОкончСтр1=СокрЛП(ОкончСтр1);
		Длина=ДлинаСтрокиДисплея-СтрДлина(ОкончСтр1);
		ПолнаяСтр1=СтрокаПривести(Лев(Стр1,?(ОкончСтр1="",Длина,Длина-1))," ",Длина,Реж+1)+ОкончСтр1;
	Иначе
		Если ДисплейИнформацияОСуммеЧека Тогда
			ДисплейИнформацияОСуммеЧека=Ложь;
			Возврат;
		КонецЕсли; 
		ПолнаяСтр1=СтрокаПривести(ОкончСтр1," ",ДлинаСтрокиДисплея,1);
	КонецЕсли;
	// Формируем вторую строку
	ТипСтр2=ТипЗнч(Стр2);
	Если НЕ ПустаяСтрока(СокрЛП(Строка(Стр2))) Тогда
		Если ТипСтр2=Тип("Строка") Тогда									Стр2=СокрЛП(Стр2);
		ИначеЕсли ТипСтр2=Тип("СправочникСсылка.Номенклатура") Тогда		Стр2=СокрЛП(Стр2.Наименование);
		Иначе																Стр2=Строка(Стр2);
		КонецЕсли;
		ОкончСтр2=СокрЛП(ОкончСтр2);
		Длина=ДлинаСтрокиДисплея-СтрДлина(ОкончСтр2);
		ПолнаяСтр2=СтрокаПривести(Лев(Стр2,?(ОкончСтр2="",Длина,Длина-1))," ",Длина,Реж+1)+ОкончСтр2;
	Иначе 
		Если ДисплейИнформацияОСуммеЧека Тогда
			ДисплейИнформацияОСуммеЧека=Ложь;
			Возврат;
		КонецЕсли; 
		ПолнаяСтр2=СтрокаПривести(ОкончСтр2," ",ДлинаСтрокиДисплея,1);
	КонецЕсли;
	// Выводим на дисплей
	Параметры=Рарус_Компонента.СоздатьПараметры(2,1);
	Параметры.SetValue(0,0,ПолнаяСтр1);
	Параметры.SetValue(1,0,ПолнаяСтр2);
	Попытка
		КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_Дисплей,"Показать",Параметры,ТаймаутДисплея);
	Исключение КонецПопытки;
	Параметры=НЕОПРЕДЕЛЕНО;
КонецПроцедуры // ВывестиНаДисплей()

// Выводит на дисплей заставку
Процедура ВывестиЗаставку() Экспорт
	Если НЕ ДисплейИнформацияОСуммеЧека Тогда
		// получим строки текста заставки
		Стр1=СтрПолучитьСтроку(ТекстЗаставкиДисплея,1); 
		Стр2=СтрПолучитьСтроку(ТекстЗаставкиДисплея,2);
		// выведем полученные строки на дисплей
		ВывестиНаДисплей(Стр1,,Стр2,,2);
	КонецЕсли; 
КонецПроцедуры

// Получить вес
Функция ПолучитьВес(Вес, ТекстОшибки = "")

	Результат=Ложь;
	Параметры=Неопределено;

	// Вызов метода оборудования
	Результат = ВыполнитьКомандуОборудования("Весы", GUID_Весы, "ПолучитьВес", Параметры, ТаймаутВесы, ТекстОшибки);

	Если Результат Тогда
		Попытка
			Вес = Число(Параметры.GetValue(0, 0));
		Исключение
			Результат=Ложь;
		КонецПопытки;
	КонецЕсли;

	Если РежимДиагностики И (Не Результат) И (НЕ ПустаяСтрока(ТекстОшибки)) Тогда
		ЗаписьЖурналаРегистрации("ФронтКассира.ПолучитьВес", ?(Лев(ТекстОшибки, 2) = "ОК",
		                         УровеньЖурналаРегистрации.Предупреждение, УровеньЖурналаРегистрации.Ошибка),,,
		                         СтрЗаменить(ТекстОшибки, Символы.ПС, " "));
	КонецЕсли;

	// Вернем "наверх" результат операции	
	Возврат Результат;

КонецФункции

// Подготавливает Параметры шапки для пробития чека
Функция ПодготовитьПараметрыШапкиДляПробития()
	// Подготовим Параметры
	ПараметрыШапки = Новый Структура;
	
	// определим тип чека: приход (0), возврат прихода (1), расход (6), возврат расхода (7)
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипЗнч(Ссылка));
	Если дкДокументЕстьРеквизитШапки("ТипРасчета", МетаданныеДокумента.Имя) Тогда
		ТипРасчета = Ссылка.ТипРасчета;
	Иначе
		ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПустаяСсылка();
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат ИЛИ
		ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств Тогда
		ПараметрыШапки.Вставить("ТипЧека",7);
	ИначеЕсли ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат ИЛИ
		ХозОперация=Справочники.ХозОперации.ЧекНаВозврат ИЛИ
		ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя ИЛИ
		ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств Тогда
		ПараметрыШапки.Вставить("ТипЧека",1);
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
		ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств Тогда
		ПараметрыШапки.Вставить("ТипЧека",6);
	Иначе
		ПараметрыШапки.Вставить("ТипЧека",0);
	КонецЕсли;
	
	Если УстановленРежим("Копия") Тогда
		ПараметрыШапки.Вставить("Фискальный",0);
		ПараметрыШапки.Вставить("КопияЧека",1);
	Иначе
		ПараметрыШапки.Вставить("Фискальный",1);
		ПараметрыШапки.Вставить("КопияЧека",0);
	КонецЕсли;
	
	// Для оплаты документов информацию о скидках и ДК топим в общей сумме. Например приняв предоплату 
	// в чеке должна появиться итоговая сумма
	//Если УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат")
	Если НЕ РежимПередачиТовараСЗакрытиемОстаткаОплатыНаКредит()
		И (УстановленРежим("ЧекНаОплатуВозврат")
		ИЛИ УстановленРежим("ПриходныйКассовыйОрдер")
		ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
		ИЛИ УстановленРежим("БанковскаяВыписка") 
		ИЛИ УстановленРежим("ЧекКоррекции")) Тогда
		
		// Подитог, т.е. сумма чека с учетом скидки на товары (с пустой скидкой на чек)
		ПараметрыШапки.Вставить("Всего",СуммаДокумента);
		// Итоговая сумма документа с учетов всех примененных скидок
		ПараметрыШапки.Вставить("Итог", СуммаДокумента);
		
		Если  УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер") Тогда
			ПараметрыШапки.Вставить("ВсегоПоДокументу", Ссылка.СуммаДокумента);
		КонецЕсли;
		
		// Скидка на чек не отражаем
		ПараметрыШапки.Вставить("СкидкаНаЧек","");
		ПараметрыШапки.Вставить("ПроцентСкидкиНаЧек",0);
		ПараметрыШапки.Вставить("СуммаСкидкиНаЧек",0);
		
		// Дисконтная карточка - не отражаем
		ПараметрыШапки.Вставить("ДисконтнаяКарта", "");
		
	Иначе
		
		Если РежимПередачиТовараСЗакрытиемОстаткаОплатыНаКредит() Тогда
			
			// Подитог, т.е. сумма чека с учетом скидки на товары (без скидок на чек)
			СуммаТоваровДокумента = Товары.Итог("СуммаВсего");
			// Всего = СуммаТоваровДокумента+Товары.Итог("СуммаСкидки");
			Всего = СуммаТоваровДокумента;
			ПараметрыШапки.Вставить("Всего",Всего);
		
		    // Итоговая сумма документа с учетов всех примененных скидок
			ПараметрыШапки.Вставить("Итог", СуммаТоваровДокумента);
			
			ПараметрыШапки.Вставить("ВсегоПоДокументу", Ссылка.СуммаДокумента);
	
		Иначе
			
		    // Подитог, т.е. сумма чека с учетом скидки на товары (без скидок на чек)
			// Всего = СуммаДокумента+Товары.Итог("СуммаСкидки");
			Всего = СуммаДокумента;
			ПараметрыШапки.Вставить("Всего",Всего);
		
		    // Итоговая сумма документа с учетов всех примененных скидок
			ПараметрыШапки.Вставить("Итог", СуммаДокумента);
			
		КонецЕсли;
		
		// Скидка на чек
		Если обЗначениеНеЗаполнено(СкидкаНаценка) Тогда
			ПараметрыШапки.Вставить("СкидкаНаЧек","");
			ПараметрыШапки.Вставить("ПроцентСкидкиНаЧек",0);
			ПараметрыШапки.Вставить("СуммаСкидкиНаЧек",Товары.Итог("СуммаСкидкиБонусами"));
		Иначе
			ПараметрыШапки.Вставить("СкидкаНаЧек",СкидкаНаценка.Наименование);
			СуммаСкидкиНаЧек = Товары.Итог("СуммаСкидки") + Товары.Итог("СуммаСкидкиБонусами");
			ПараметрыШапки.Вставить("ПроцентСкидкиНаЧек",?(Всего = 0, 0, СуммаСкидкиНаЧек/Всего*100));
			ПараметрыШапки.Вставить("СуммаСкидкиНаЧек",СуммаСкидкиНаЧек);
		КонецЕсли;
		
		// Дисконтная карточка
		Если обЗначениеНеЗаполнено(Карточка) Тогда
			ПараметрыШапки.Вставить("ДисконтнаяКарта", "");
		Иначе
			ПараметрыШапки.Вставить("ДисконтнаяКарта", Карточка.Наименование);
		КонецЕсли;
		
		ПараметрыШапки.Вставить("СкладКомпании",СкладКомпании);	
	КонецЕсли;
	
	Если УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат") ИЛИ УстановленРежим("ЧекКоррекции")Тогда
		ПараметрыШапки.Вставить("СтавкаНДС",СтавкаНДСЧекаНаОплату);
		ПараметрыШапки.Вставить("СуммаНДС", СуммаНДСЧекаНаОплату);
	КонецЕсли;
	
	// Параметры чека для копии
	ПараметрыШапки.Вставить("НомерСмены",НомерСмены);
	ПараметрыШапки.Вставить("НомерЧека",НомерЧека);
	ПараметрыШапки.Вставить("НомерДокумента",НомерДокумента);
	ПараметрыШапки.Вставить("ДатаФР",Формат(ДатаФР,"ДФ='yyyy.MM.dd ЧЧ:мм:сс'"));   //Пример: "2004.12.28 16:53:00" 
	
	ПараметрыШапки.Вставить("НастройкаПечати",0);  // неизвестно что это
	ПараметрыШапки.Вставить("ПодкладнаяПечать",0); // по умолчанию будем печатать на ЧЛ (1 - ПД)
	
	// получим отдел для печати на ККМ
	ОсновнойОтдел = ПолучитьНомерОтдела(СкладКомпании);
	ПараметрыШапки.Вставить("ОсновнойОтдел",ОсновнойОтдел);
	
	ПараметрыШапки.Вставить("ТекстШапки","");
	ПараметрыШапки.Вставить("ТекстПодвала","");
	
	// кидаем по умолчанию неопределено - пусть ФР по своим настройкам определит печатать или нет информацию о налоге
	ПараметрыШапки.Вставить("ВыделятьНалоги",Неопределено);
	
	Попытка ПарольКассира = Автор.ПарольККМ; Исключение ПарольКассира=0; КонецПопытки;
	
	Попытка
		ИННКассира = Автор.ИНН;
	Исключение
		ИННКассира = "";
	КонецПопытки;
	
	ИмяКассира = Автор.Наименование;
	ПараметрыШапки.Вставить("ПарольКассира",ПарольКассира);
	ПараметрыШапки.Вставить("ИмяКассира",ИмяКассира);
	ПараметрыШапки.Вставить("ИННКассира", ИННКассира);
	
	ПараметрыШапки.Вставить("Организация",Организация);	
	ПараметрыШапки.Вставить("ПодразделениеКомпании",ПодразделениеКомпании);	
	
	// Передаяа ИНН и представление контрагента
	Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо 
		ИЛИ Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
		
		ПаспортныеДанные = "";
		Если Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПодтверждающиеДокументы.Серия КАК Серия,
			|	ПодтверждающиеДокументы.Номер КАК Номер
			|ИЗ
			|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
			|ГДЕ
			|	ПодтверждающиеДокументы.Владелец = &Владелец
			|	И ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыДокументов.Паспорт)
			|	И ПодтверждающиеДокументы.Текущий = ИСТИНА";
			Запрос.УстановитьПараметр("Владелец",Контрагент);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ПаспортныеДанные = Выборка.Серия + " " + Выборка.Номер;
			КонецЕсли;
		КонецЕсли;
		
		Получатель = Контрагент.НаименованиеПолное + " " + ПаспортныеДанные;
		ПараметрыШапки.Вставить("Получатель",Получатель);
		ПараметрыШапки.Вставить("ПолучательИНН",?(ЗначениеЗаполнено(Контрагент.ИНН), Контрагент.ИНН, ""));
		
		ПараметрыШапки.Вставить("ПолучательДатаРождения",?(ЗначениеЗаполнено(Контрагент.ДатаРождения), Контрагент.ДатаРождения, Дата("00000000000000")));
		ПараметрыШапки.Вставить("ПолучательГражданство",?(ЗначениеЗаполнено(Контрагент.Гражданство), Контрагент.Гражданство, Справочники.КлассификаторСтранМира.ПустаяСсылка()));
		
	Иначе
		ПараметрыШапки.Вставить("Получатель","");
		ПараметрыШапки.Вставить("ПолучательИНН","");
		
		ПараметрыШапки.Вставить("ПолучательДатаРождения", Дата("00000000000000"));
		ПараметрыШапки.Вставить("ПолучательГражданство", Справочники.КлассификаторСтранМира.ПустаяСсылка());
	КонецЕсли;
	
	ПараметрыШапки.Вставить("ДополнительныйРеквизитЧека", "");
	ПараметрыШапки.Вставить("ЭтоЧекКоррекции", УстановленРежим("ЧекКоррекции"));
	Если УстановленРежим("ЧекКоррекции") Тогда
		ПараметрыШапки.Вставить("ТипКоррекции", Ссылка.ТипКоррекции);
		
		ПараметрыШапки.Вставить("ДатаКоррекции", Ссылка.ДатаКоррекции);
		ПараметрыШапки.ДополнительныйРеквизитЧека = Ссылка.ФискальныйПризнак;
		ПараметрыШапки.Вставить("НомерПредписания", "");
		//Если ЗначениеЗаполнено(Ссылка.НомерПредписания) Тогда
			ПараметрыШапки.Вставить("НомерПредписания", Ссылка.НомерПредписания);
		//КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыШапки;
КонецФункции

Функция ПодготовитьПараметрыДляПробитияЧекаКоррекции(ЧекКоррекции)
	
	// Сформируем структуру данных чека
	ПараметрыДокумента = Новый Структура;
	
	Если ЧекКоррекции.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда
		ПараметрыДокумента.Вставить("ТипРасчета", 1);
	ИначеЕсли ЧекКоррекции.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств Тогда
		ПараметрыДокумента.Вставить("ТипРасчета", 2);
	ИначеЕсли ЧекКоррекции.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств Тогда
		ПараметрыДокумента.Вставить("ТипРасчета", 3);
	Иначе
		ПараметрыДокумента.Вставить("ТипРасчета", 4);
	КонецЕсли;
	
	Если ЧекКоррекции.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 1);
	ИначеЕсли ЧекКоррекции.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 2);
	ИначеЕсли ЧекКоррекции.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 3);
	ИначеЕсли ЧекКоррекции.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕСН Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 4);
	ИначеЕсли ЧекКоррекции.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 5);
	Иначе
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 0);
	КонецЕсли;
	
	Попытка
		ИННКассира = ЧекКоррекции.Автор.ИНН;
	Исключение
		ИННКассира = "";
	КонецПопытки;
	
	ПараметрыДокумента.Вставить("Кассир",                 ?(ЗначениеЗаполнено(ЧекКоррекции.Автор), СокрЛП(ЧекКоррекции.Автор.Наименование), ""));
	ПараметрыДокумента.Вставить("КассирИНН",              ИННКассира);
	ПараметрыДокумента.Вставить("ТипКоррекции",           ЧекКоррекции.ТипКоррекции);
	ПараметрыДокумента.Вставить("НаименованиеОснования",  ЧекКоррекции.ПредставлениеОснования);
	ПараметрыДокумента.Вставить("ДатаДокументаОснования", ЧекКоррекции.ДатаДокументаОснования);
	ПараметрыДокумента.Вставить("НомерДокументаОснования",ЧекКоррекции.НомерДокументаОснования);
	ПараметрыДокумента.Вставить("Сумма",                  ЧекКоррекции.СуммаДокумента);
	ПараметрыДокумента.Вставить("СуммаБезНДС",            ЧекКоррекции.СуммаБезНДС);
	ПараметрыДокумента.Вставить("СуммаНДС0",              ЧекКоррекции.СуммаНДС0);
	ПараметрыДокумента.Вставить("СуммаНДС10",             ЧекКоррекции.СуммаНДС10);
	ПараметрыДокумента.Вставить("СуммаНДС18",             ЧекКоррекции.СуммаНДС18);
	ПараметрыДокумента.Вставить("СуммаНДС110",            ЧекКоррекции.СуммаНДС110);
	ПараметрыДокумента.Вставить("СуммаНДС118",            ЧекКоррекции.СуммаНДС118);
	
	СуммаНал                     = 0;
	СуммаЭлектро                 = 0;
	СуммаПредоплата              = 0;
	СуммаПостоплата              = 0;
	СуммаВстречноеПредоставление = 0;
	
	Для Каждого ТекущаяСтрока Из ЧекКоррекции.Оплата Цикл
		
		Если ТекущаяСтрока.ТипОплаты = Перечисления.ТипыОплатыККТ.Наличные Тогда
			СуммаНал = СуммаНал + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты = Перечисления.ТипыОплатыККТ.Электронно Тогда
			СуммаЭлектро = СуммаЭлектро + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты = Перечисления.ТипыОплатыККТ.Предоплата Тогда
			СуммаПредоплата = СуммаПредоплата + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты = Перечисления.ТипыОплатыККТ.Постоплата Тогда
			СуммаПостоплата = СуммаПостоплата + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты = Перечисления.ТипыОплатыККТ.ВстречноеПредоставление Тогда
			СуммаВстречноеПредоставление = СуммаВстречноеПредоставление + ТекущаяСтрока.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыДокумента.Вставить("СуммаНал",                     СуммаНал);
	ПараметрыДокумента.Вставить("СуммаЭлектро",                 СуммаЭлектро);
	ПараметрыДокумента.Вставить("СуммаПредоплата",              СуммаПредоплата);
	ПараметрыДокумента.Вставить("СуммаПостоплата",              СуммаПостоплата);
	ПараметрыДокумента.Вставить("СуммаВстречноеПредоставление", СуммаВстречноеПредоставление);
	
	Возврат ПараметрыДокумента;
	
КонецФункции // ПодготовитьПарамтерыДляПробитияЧекаКоррекции

Функция ТребуетсяОткрытиеСмены(Комментировать = Истина) Экспорт
	ПараметрыВозврата = Неопределено;
	СостояниеСмены = Неопределено;
	Результат = ВыполнитьКомандуОборудования("ФР",GUID_ФР, "ПолучитьСостояние", ПараметрыВозврата ,ТаймаутФР,ТекстОшибки);
	Если НЕ Результат Тогда
		Возврат СостояниеСмены;
	КонецЕсли;
	Если ПараметрыВозврата = Неопределено Тогда
		
		Если Комментировать Тогда
		
			ТекстОшибки = "Драйвер ФР не поддерживает определение состояния ФР." 
				+ Символы.ВК + "Для отключения проверки состояния ФР необходимо отключить настройку пользователя"
				+ Символы.ВК + """Проверять состояние ФР при открытии фронта"".";
			ОткрытьДиалог(ТекстОшибки, СтатусСообщения.Важное);
		КонецЕсли;
		
		Возврат СостояниеСмены;
	КонецЕсли;
	
	Попытка
		СостояниеСмены = ПараметрыВозврата.GetValue(1,0);
	Исключение КонецПопытки; 
	
	Возврат СостояниеСмены;
КонецФункции

Функция ПроверитьТребоватьОткрытиеСмены() Экспорт
	
	СостояниеСмены = ТребуетсяОткрытиеСмены();
	
	Если СостояниеСмены = 0 Тогда
		
		// Смена открыта
		ТребуетсяОткрытиеСменыФР = Ложь;
		
	ИначеЕсли СостояниеСмены = 1 Тогда
		
		// Смена закрыта, необходимо открыть
		ТребуетсяОткрытиеСменыФР = Истина; 
		
	ИначеЕсли СостояниеСмены = 2 Тогда
		
		// Хоть смена и истекла, но надо дать работать дальше.
		ТребуетсяОткрытиеСменыФР = Ложь;
		
	Иначе
		// Ошибка при получении состояния смены (нет такого метода у ФР)
		ТребуетсяОткрытиеСменыФР = Ложь;
	КонецЕсли;
	РазделительСтрок = Символы.ВК + Символы.ПС;
	// СМЕНА ПРОСРОЧЕНА
	Если СостояниеСмены = 2 Тогда
		// Смена открыта, но прошло свыше 24 часов, необходимо закрыть смену, затем открыть
		ОписаниеОшибки = НСтр("ru = 'С момента открытия кассовой смены на кассе ККМ <" + СокрЛП(КассаККМ) + "> 
		|истекло более 24 часов." + РазделительСтрок 
		+ "Ряд операций может быть недоступен." + РазделительСтрок 
		+ "Необходимо закрыть кассовую смену или снять Z-отчет.'");
		ОткрытьДиалог(ОписаниеОшибки, СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
	
	
	// СМЕНУ НАДО ОТКРЫТЬ
	Если ТребуетсяОткрытиеСменыФР Тогда
		
		ТекстВопроса = "Не открыта кассовая смена." + РазделительСтрок + "Открыть смену?";
		
		Если ОткрытьДиалогВопроса(ТекстВопроса) Тогда
			// Смена на оборудовании открывается только если пользователь нажал кнопку "Да" сам.
			Действие0Чек(Неопределено);
		КонецЕсли;
		
	КонецЕсли;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Блок работы с авторизациями безналичных платежей

// Проверяет есть ли в таблице оплат оплаты платежной картой
Функция НетОплатПлатежнойКартой()

	НетОплатПлатежнойКартой = Истина;
	Для Каждого СтрокаОплаты Из Оплаты Цикл
		Если СтрокаОплаты.ТипОплаты.Объект = Перечисления.ТипыОплатыВРознице.Безналичные Тогда
			НетОплатПлатежнойКартой = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат НетОплатПлатежнойКартой;

КонецФункции

// Формирует таблицу безналичных платежей для авторизации
Процедура СформироватьТаблицуБП(ПараметрыОтбора = Неопределено)

	Если ПараметрыОтбора = Неопределено Тогда

		ТаблицаБП.Очистить();

		НОплаты = 1;
		Для Каждого СтрокаОплаты Из Оплаты Цикл
			Если СтрокаОплаты.ТипОплаты.Объект <> Перечисления.ТипыОплатыВРознице.Безналичные Тогда Продолжить;	КонецЕсли;

			СуммаОплаты = СтрокаОплаты.Сумма;
			ТипКарты = СтрокаОплаты.ТипПлатежнойКарты.Наименование;
			НомерКарты = СокрЛП(СтрокаОплаты.СчитанныйНомерКарты);

			НовыйБП = ТаблицаБП.Добавить();
			НовыйБП.ТипКарты = ТипКарты;
			НовыйБП.НомерКарты = НомерКарты;
			НовыйБП.СуммаОплаты = СуммаОплаты;
			НовыйБП.АвторизацияПрошла = Ложь;
			НовыйБП.АвторизацияОтменена = Ложь;
			НовыйБП.КвитанцияНапечатана = Ложь;
			//// Получаем Номер транзакции
			//НомерДокументаЧек = Ссылка.Номер;
			//Попытка
			//	УникальныйНомер = Число(Формат(Ссылка.Дата, "ДФ=yyyyMMdd")
			//	+ СтрЗаменить(НомерДокументаЧек, ПрефиксДокументаЧек, "") + Строка(НОплаты));
			//	ВнешнийНомерТранзакции = Число(УникальныйНомер);
			//Исключение
			//	ВнешнийНомерТранзакции = 0;
			//КонецПопытки;
			//НовыйБП.ВнешнийНомерТранзакции = ВнешнийНомерТранзакции;
			НовыйБП.ВнешнийНомерТранзакции = 0;
			НовыйБП.НомерСсылки = "";
			НОплаты = НОплаты + 1;
		КонецЦикла;
	Иначе
		ТаблицаБП = ТаблицаБП.Скопировать(ПараметрыОтбора);
	КонецЕсли;

КонецПроцедуры

// Выполняет авторизацию безналичного платежа на авторизаторе
Функция ОборудованиеАвторизоватьБП(Платеж, ТипОперации, ЭтоОтмена = Ложь, ВыводитьОшибки)

	// Создадим флаг, указывающий, что квитанция обязательна только для Платежа и для Возврата и если нету печати на пинпаде
	ТребоватьКвитанцию = (НЕ ЭтоОтмена) И (НЕ ПечатьНаПинПадеАвторизатора);

	// Подготовка к выполнению задания на оборудовании	
	Параметры = Рарус_Компонента.СоздатьПараметры(20,1);
	Параметры.SetValue(0, 0, Платеж.ТипКарты);
	Параметры.SetValue(1, 0, Платеж.НомерКарты);
	Параметры.SetValue(2, 0, ТипОперации);
	Параметры.SetValue(3, 0, Платеж.СуммаОплаты);
	Параметры.SetValue(4, 0, "");
	//Параметры.SetValue(5, 0, 0);
	Параметры.SetValue(6, 0, Платеж.НомерСсылки);
	//Параметры.SetValue(7, 0, 0);
	//Параметры.SetValue(8, 0, 0);
	Параметры.SetValue(9, 0, Платеж.ВнешнийНомерТранзакции);
	//Параметры.SetValue(10, 0, 0);
	//Параметры.SetValue(11, 0, 0);
	//Параметры.SetValue(12, 0, 0);
	Параметры.SetValue(19, 0, 0);

	// Выполняем команду авторизации
	ТекстОшибки = ""; КодОшибки = 0;
	Результат = ВыполнитьКомандуОборудования("Авторизатор", GUID_Авторизатор, "Авторизовать", Параметры, ТаймаутАвторизатора, ТекстОшибки, КодОшибки, Ложь);
	// Нужно для отладки
	//Ответ = ОткрытьДиалогВопросаРасш("Авторизация прошла?", "Да+Нет");
	//Результат = Ответ = "Да";

	// Сразу пометим результат операции
	Если ЭтоОтмена Тогда
		Платеж.АвторизацияОтменена = Результат;
	Иначе
		Платеж.АвторизацияПрошла = Результат;
	КонецЕсли;

	//Возврат Результат;

	// Получаем и проверяем возвратные параметры
	Если Результат Тогда
		Попытка
			КолвоСвойств = 0; КолвоПолей=0;
			Параметры.GetBounds(КолвоСвойств,КолвоПолей);

			КодАвторизации = Параметры.GetValue(2,0);
			ТекстКвитанции = СокрЛП(Параметры.GetValue(3,0));
			Если КолвоСвойств >= 14 Тогда ВнешнийНомерТранзакции = Параметры.GetValue(13,0); Иначе ВнешнийНомерТранзакции = 0; КонецЕсли;
			Если КолвоСвойств >= 10 Тогда НомерСсылки = Параметры.GetValue(9,0); Иначе НомерСсылки = ""; КонецЕсли;

			// Текст квитанции платежа не может быть пустым
			Если ПустаяСтрока(ТекстКвитанции) И ТребоватьКвитанцию Тогда
				Если ВыводитьОшибки Тогда
					ТекстОшибки = "Авторизатор не вернул текст квитанции авторизации платежа!
					              |Невозможно распечатать квитанцию!";
					ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
				КонецЕсли;
				Результат = Ложь;
			Иначе
				Платеж.КодАвторизации         = КодАвторизации;
				Платеж.ТекстКвитанции         = ТекстКвитанции;
				Платеж.ВнешнийНомерТранзакции = ВнешнийНомерТранзакции;
				Платеж.НомерСсылки            = НомерСсылки;
			КонецЕсли;
		Исключение
			Результат = Ложь;
			Если ВыводитьОшибки Тогда
				ТекстОшибки = "Авторизатор вернул некорректные параметры совершенного платежа!
				              |Невозможно распечатать квитанцию!";
				ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
			КонецЕсли;
			Если РежимДиагностики Тогда
				ЗаписьЖурналаРегистрации("Оборудование.Авторизатор", УровеньЖурналаРегистрации.Ошибка,,, СтрЗаменить(ТекстОшибки,Символы.ПС," "));
			КонецЕсли;
		КонецПопытки;

		Если НЕ Результат Тогда
			ЗаписатьВФайлЛога("Авторизация платежа", Число(НЕ Результат), СтрЗаменить(ТекстОшибки, Символы.ПС, " "),, 1);
		Иначе
			ЗаписатьВФайлЛога("Авторизация платежа", Число(НЕ Результат), СтрЗаменить(ТекстОшибки, Символы.ПС, " "));
		КонецЕсли;
	ИначеЕсли ВыводитьОшибки Тогда
		ТекстОшибки = "Ошибка авторизации:" + Символы.ПС + ТекстОшибки;
		ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Выполняет печать квитанции авторизации
Функция ПолучитьТекстКвитанции(ПолученныйТекст)

	ТекстыКвитанции = Новый Массив;

	// Разбиваем полученный текст на отдельные квитанции
	ТекстДляПечати = "";
	Для НСтр = 1 По СтрЧислоСтрок(ПолученныйТекст) Цикл
		Стр = СтрПолучитьСтроку(ПолученныйТекст, НСтр);
		Если (СтрДлина(Стр) > 0) И (КодСимвола(Стр, 1) < 32) Тогда
			ТекстыКвитанции.Добавить(ТекстДляПечати);
			ТекстДляПечати = "";
		Иначе
			ТекстДляПечати = ТекстДляПечати + Стр + Символы.ПС;
		КонецЕсли;
	КонецЦикла;

	// В конце текста квитанции может и не быть символа отреза
	Если (СтрЧислоСтрок(ТекстДляПечати) > 1) Тогда
		ТекстыКвитанции.Добавить(ТекстДляПечати);
	КонецЕсли;

	Возврат ТекстыКвитанции;

КонецФункции

// Печатает копию квитанции безналичного платежа на устройстве
Функция ОборудованиеПечатьКвитанцииБП(ТекстКвитанции, ВыводитьОшибки)

	// Нужно для отладки
	//Ответ = ОткрытьДиалогВопросаРасш("Квитанция напечаталась?", "Да+Нет");
	//Возврат Ответ = "Да";

	Результат = Истина;

	// Если нету принтера квитанций то печатаем
	Если ПустаяСтрока(GUID_ПЧ) Тогда
		GUID_Устройства = GUID_ФР;
		ТаймаутУстройства = ТаймаутФР;
	Иначе
		GUID_Устройства = GUID_ПЧ;
		ТаймаутУстройства = ТаймаутПЧ;
	КонецЕсли;

	Для Каждого ТекстДляПечати Из ТекстКвитанции Цикл
		КодОшибки = 0; ТекстОшибки = "";
		ПараметрыДействия = Рарус_Компонента.СоздатьПараметры(4, 1);
		ПараметрыДействия.SetValue(0, 0, "");
		ПараметрыДействия.SetValue(1, 0, ТекстДляПечати);
		ПараметрыДействия.SetValue(2, 0, "");
		ПараметрыДействия.SetValue(3, 0, 0);
		Результат = ВыполнитьКомандуОборудования("ПринтерКвитанций", GUID_Устройства, "Напечатать", ПараметрыДействия,
		                                         ТаймаутУстройства, ТекстОшибки, КодОшибки, Ложь);

		Если НЕ Результат Тогда
			Если ВыводитьОшибки Тогда
				ТекстОшибки = "Ошибка печати квитанции:" + Символы.ПС + ТекстОшибки;
				ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Выполняет авторизацию и печатает квитанцию платежа
Функция АвторизоватьБП(ТипОперации, ЭтоОтмена = Ложь, ВыводитьОшибки = Истина)

	Для Каждого Платеж Из ТаблицаБП Цикл

		// Сообщаем пользователю какую карту мы отменяем
		Если ЭтоОтмена Тогда
			ОткрытьДиалог("Внимание!!! Производится отмена авторизации карты " + Платеж.НомерКарты + 
			              " на сумму: " + Платеж.СуммаОплаты + " руб.", СтатусСообщения.Внимание);
		КонецЕсли;

		// Начинаем цикл авторизаций платежа
		ПовторАвторизации = Истина;
		Пока ПовторАвторизации Цикл
			ПовторАвторизации = Ложь;
			// Проводим авторизацию
			Результат = ОборудованиеАвторизоватьБП(Платеж, ТипОперации, ЭтоОтмена, ВыводитьОшибки);

			АвторизацияПрошла = ?(ЭтоОтмена, Платеж.АвторизацияОтменена, Платеж.АвторизацияПрошла);
			// Печатаем квитанцию
			Если Результат И (НЕ ПечатьНаПинПадеАвторизатора) Тогда
				// Разбиваем полученный текст на несколько квитанций
				ТекстКвитанции = ПолучитьТекстКвитанции(Платеж.ТекстКвитанции);
				// Начинаем цикл печати квитанций
				ПовторПечатиКвитанции = Истина;
				Пока ПовторПечатиКвитанции Цикл
					ПовторПечатиКвитанции = Ложь;
					Результат = ОборудованиеПечатьКвитанцииБП(ТекстКвитанции, ВыводитьОшибки);
					Платеж.КвитанцияНапечатана = Результат;
					Если (НЕ Результат) И ВыводитьОшибки Тогда
						ТекстВопроса = "Произошла ошибка печати квитанции авторизации
						               |Попробовать выполнить печать еще раз?";
						Ответ = ОткрытьДиалогВопросаРасш(ТекстВопроса, "Да+Нет");
						Если Ответ = "Да" Тогда
							ПовторПечатиКвитанции = Истина;
						Иначе
							// Для отмены не так страшно, если квитанция не напечатана
							Если ЭтоОтмена Тогда
								Результат = Истина;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли (НЕ АвторизацияПрошла) И ВыводитьОшибки Тогда
				ТекстВопроса = "Произошла ошибка авторизации
				               |Попробовать выполнить авторизацию еще раз?";
				Ответ = ОткрытьДиалогВопросаРасш(ТекстВопроса, "Да+Нет");
				Если Ответ = "Да" Тогда
					ПовторАвторизации = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;

		// Прервем выполнение операций, если произошла ошибка
		Если НЕ Результат Тогда
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции

// Выполняет все необходимые операции по авторизации и отмене авторизации
Функция ВыполнитьАвторизациюБП(ЭтоОтмена = Ложь)

	Результат = Истина;

	ЭтоВозврат = (ХозОперация=Справочники.ХозОперации.ЧекНаВозврат) ИЛИ
	             (ХозОперация=Справочники.ХозОперации.ВозвратТоваровОтПокупателя) ИЛИ
	             (ХозОперация=Справочники.ХозОперации.ЧекНаОплатуВозврат) ИЛИ 
	             (ХозОперация=Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат);

	// Проверяем ФР и Принтер чеков
	Если ПустаяСтрока(GUID_ФР)И ПустаяСтрока(GUID_ПЧ) Тогда
		ТекстОшибки = "Не подключен принтер квитанций!
		              |Операция авторизации платежа прервана!";
		ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
	// Проверяем авторизатор
	Если ПустаяСтрока(GUID_Авторизатор) Тогда
		ТекстОшибки = "Не подключен авторизатор платежей
		              |Операция авторизации платежа прервана!";
		ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;

	// Проверяем отмену ли мы производим
	Если ЭтоОтмена Тогда
		// Заполняем таблицу безналичных платежей уже совершенными платежами
		ПараметрыОтбора = Новый Структура("АвторизацияПрошла", Истина);
	Иначе
		ПараметрыОтбора = Неопределено;
	КонецЕсли;
	СформироватьТаблицуБП(ПараметрыОтбора);

	// Выполняем авторизации возврата безналичных платежей
	Если ЭтоВозврат Тогда

		Если ЭтоОтмена Тогда
			// Выполняем авторизацию отмены возврата платежа
			//ТипОперации = 3;
			//Результат = АвторизоватьБП(ТипОперации, ЭтоОтмена);
			Результат = ЛОЖЬ;
			ОткрытьДиалог("Отмена возврата платежа невозможна", СтатусСообщения.ОченьВажное);
		Иначе
			//0=Платеж
			//1=Возврат
			//2=Отмена;Платежа
			//3=Отмена Возврата
			//4=Продажа Off-line
			//5=Возврат Off-line
			//6=Отмена Off-line
			//7=Аварийная отмена последней операции			
			// Выполняем авторизацию отмены платежа, вдруг этот платеж был сделан в этой смене
			ТипОперации = 1; //2;
			Результат = АвторизоватьБП(ТипОперации,,Ложь);
			Если НЕ Результат Тогда
				// Проверяем надо ли отменять предыдущую команду отмены
				// Отменять надо, если хотя бы одна предыдущая авторизация прошла
				ПараметрыОтбора = Новый Структура("АвторизацияПрошла", Истина);
				ПрошедшиеПлатежи = ТаблицаБП.НайтиСтроки(ПараметрыОтбора);
				НадоПровестиОтменуПлатежей = ПрошедшиеПлатежи.Количество() > 0;
				Если НЕ НадоПровестиОтменуПлатежей Тогда
					// Выполняем авторизацию возврат платежа
					ТипОперации = 2; //1;
					Результат = АвторизоватьБП(ТипОперации);
				КонецЕсли;
			КонецЕсли;
			// Эсли хоть один из возвратов прошел, отменить ничего невозможно,
			// Поскольку возврат можетбыть выполнен, как отмена. Отменить отмену нельзя
			// Даже если мы выполняем именно возврат, ПО Сбербанка может заменить его на отмену
			Если НЕ Результат Тогда
				ПрошедшиеПлатежи = ТаблицаБП.НайтиСтроки(ПараметрыОтбора);
				ЕстьПрошедшиеПлатежи = ПрошедшиеПлатежи.Количество() > 0;
				Если ЕстьПрошедшиеПлатежи Тогда
					Результат = ИСТИНА; // Чек нужно пробить, уже пути назад нет
					ПараметрыОтбора = Новый Структура("АвторизацияПрошла", ЛОЖЬ);
					НеПрошедшиеПлатежи = ТаблицаБП.НайтиСтроки(ПараметрыОтбора);
					Если НеПрошедшиеПлатежи.Количество() > 0 Тогда
						ТекстОшибки = "Следующие авторизации провести невозможно!
						|Необходимо связаться с банком и провести операции вручную!";
						Для Каждого Платеж Из НеПрошедшиеПлатежи Цикл
							ТекстОшибки = ТекстОшибки + Символы.ПС + "Сумма операции: " + Платеж.СуммаОплаты + " руб.";
						КонецЦикла;
						ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;

	// Выполняем авторизации безналичных платежей
	Иначе

		Если ЭтоОтмена Тогда
			// Выполняем авторизацию отмены платежа
			ТипОперации = 2;
			Результат = АвторизоватьБП(ТипОперации, ЭтоОтмена);
		Иначе
			// Выполняем авторизацию платежа
			ТипОперации = 0;
			Результат = АвторизоватьБП(ТипОперации);
		КонецЕсли;

	КонецЕсли;

	// Отменяем выполненные авторизации, если хотя бы одна из них не прошла успешно
	Если (НЕ Результат) И (НЕ ЭтоОтмена) Тогда
		ПараметрыОтбора = Новый Структура("АвторизацияПрошла", Истина);
		ПрошедшиеПлатежи = ТаблицаБП.НайтиСтроки(ПараметрыОтбора);
		НадоПровестиОтменуПлатежей = ПрошедшиеПлатежи.Количество() > 0;

		Если НадоПровестиОтменуПлатежей Тогда
			ТекстОшибки = "В процессе выполнения операции авторизации произошла ошибка
			              |Сейчас будет выполнена автоматическая отмена произведенных авторизаций!";
			ОткрытьДиалог(ТекстОшибки, СтатусСообщения.Информация);
			ВыполнитьАвторизациюБП(Истина);
		КонецЕсли;
	КонецЕсли;

	// Выводим сообщение о результатах отмены авторизаций
	Если ЭтоОтмена Тогда
		Если Результат Тогда
			ТекстОшибки = "Отмена всех авторизаций прошла успешно!";
			ОткрытьДиалог(ТекстОшибки, СтатусСообщения.Информация);
		Иначе
			ТекстОшибки = "Отмена следующих авторизаций не прошла!
			              |Необходимо связаться с банком и провести операции вручную!";
			ПараметрыОтбора = Новый Структура("АвторизацияПрошла,АвторизацияОтменена", Истина, Ложь);
			НеотмененныеПлатежи = ТаблицаБП.НайтиСтроки(ПараметрыОтбора);
			Для Каждого Платеж Из НеотмененныеПлатежи Цикл
				ТекстОшибки = ТекстОшибки + Символы.ПС + "Сумма операции: " + Платеж.СуммаОплаты + " руб.";
			КонецЦикла;
			ОткрытьДиалог(ТекстОшибки, СтатусСообщения.ОченьВажное);
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции


// Выполняет пробитие документа чек.
//
Функция ВыполнитьПроверкуНеотправленныхДокументов(ВыводитьПредупреждение) Экспорт
	
	ВозвратныеПараметры = Неопределено;
	
	Результат = ВыполнитьКомандуОборудования("ФР",GUID_ФР, "ПолучитьСостояние", ВозвратныеПараметры ,ТаймаутФР,ТекстОшибки,,ВыводитьПредупреждение);
	
	Если НЕ Результат Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Возврат ВозвратныеПараметры;
	
КонецФункции

// Блок работы с авторизациями безналичных платежей
////////////////////////////////////////////////////////////////////////////////

Процедура ДобавитьСтрокиМежценовойРазницы(ТабличнаяЧастьДокументаОснования)
	//Если ВыделениеМежценовойРазницыОтдельнойСтрокой=Ложь Тогда
	//	Возврат;
	//КонецЕсли;   
	
	//ЧалапАЮ БизТех 10.10.2023 
	Попытка
		ПравоПоПодразделению = обПолучитьПраваИНастройкиПользователя(ЭтотОбъект.ДокументОснование.ПодразделениеКомпании,ПланыВидовХарактеристик.ПраваИНастройки.ВыделениеМежценовойРазницыОтдельнойСтрокой,ЭтотОбъект.ДокументОснование);
	Исключение 
		//ничего не исключаем, идем дальше
	КонецПОпытки;

	Если ВыделениеМежценовойРазницыОтдельнойСтрокой=Ложь Тогда   //ЧалапАЮ - это право проверяется по пользователю
		Попытка
			Если ПравоПоПодразделению = ложь тогда    //ЧалапАЮ БизТех 10.10.2023 - добавила условие на право подразделения, если и у подразделения ложь, то возврат
				Возврат;
			КонецЕсли;  
		Исключение 
			возврат; //если и по пользователю ложь, и по подразделению не получилось, то выходим
		КонецПОпытки;
	КонецЕсли;

	
	Для Каждого СтрокаТоваров Из ТабличнаяЧастьДокументаОснования Цикл
		Если СтрокаТоваров.СебестоимостьАвтомобиля<>0 Тогда
			Если СтрокаТоваров.СебестоимостьАвтомобиля<СтрокаТоваров.СуммаВсего Тогда
				НоваяСтрокаТоваров=ТабличнаяЧастьДокументаОснования.Вставить(СтрокаТоваров.НомерСтроки);
				НоваяСтрокаТоваров.Номенклатура=СтрокаТоваров.Номенклатура;
				СтрокаТоваров.Номенклатура=Справочники.Номенклатура.ПустаяСсылка();
				НоваяСтрокаТоваров.Количество=СтрокаТоваров.Количество;
				НоваяСтрокаТоваров.ЕдиницаИзмерения=СтрокаТоваров.ЕдиницаИзмерения;
				НоваяСтрокаТоваров.Коэффициент=СтрокаТоваров.Коэффициент;
				НоваяСтрокаТоваров.Цена=СтрокаТоваров.СебестоимостьАвтомобиля;
					СтрокаТоваров.Цена=СтрокаТоваров.Цена-НоваяСтрокаТоваров.Цена;
				НоваяСтрокаТоваров.Сумма=СтрокаТоваров.СебестоимостьАвтомобиля;
					СтрокаТоваров.Сумма=СтрокаТоваров.Сумма-НоваяСтрокаТоваров.Сумма;
				НоваяСтрокаТоваров.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
				НоваяСтрокаТоваров.СуммаНДС=0;
				НоваяСтрокаТоваров.ПроцентСкидки=0;
				НоваяСтрокаТоваров.СуммаСкидки=0;
				НоваяСтрокаТоваров.ХарактеристикаНоменклатуры=СтрокаТоваров.ХарактеристикаНоменклатуры;
				НоваяСтрокаТоваров.СуммаОплаты=СтрокаТоваров.СебестоимостьАвтомобиля*СтрокаТоваров.СуммаОплаты/СтрокаТоваров.СуммаВсего;
					СтрокаТоваров.СуммаОплаты=СтрокаТоваров.СуммаОплаты-НоваяСтрокаТоваров.СуммаОплаты;
				НоваяСтрокаТоваров.СуммаВсего=СтрокаТоваров.СебестоимостьАвтомобиля;
					СтрокаТоваров.СуммаВсего=СтрокаТоваров.СуммаВсего-НоваяСтрокаТоваров.СуммаВсего;
				НоваяСтрокаТоваров.МестоРазмещения=СтрокаТоваров.МестоРазмещения;
				НоваяСтрокаТоваров.СкидкаНаТовар=Справочники.ТипыСкидок.ПустаяСсылка();
				НоваяСтрокаТоваров.ПроцентСкидкиСтроки=0;
				НоваяСтрокаТоваров.СуммаСкидкиСтроки=0;
				НоваяСтрокаТоваров.Подарок=Ложь;
				НоваяСтрокаТоваров.Набор=Справочники.Номенклатура.ПустаяСсылка();
				НоваяСтрокаТоваров.Платеж=Ложь;
				НоваяСтрокаТоваров.НомерПлатежа=0;
				НоваяСтрокаТоваров.СуммаСкидкиБонусами=0;
				НоваяСтрокаТоваров.СебестоимостьАвтомобиля=0;
				НоваяСтрокаТоваров.ГТД = СтрокаТоваров.ГТД;
				
				Попытка
					НомерСтрокиТоваров=0;
					Для Каждого СтрокаТоваровНоменклатуры Из ТабличнаяЧастьДокументаОснования Цикл
						НомерСтрокиТоваров=НомерСтрокиТоваров+1;
						СтрокаТоваровНоменклатуры.НомерСтроки=НомерСтрокиТоваров;
					КонецЦикла;
				Исключение
					// Для чека на оплату не доступна для записи НомерСтроки
				КонецПопытки;
			Иначе
				СтрокаТоваров.СтавкаНДС=Справочники.СтавкиНДС.БезНДС;
				СтрокаТоваров.СуммаНДС=0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция РазбитьСтрокиПоКодамМаркировки(ТабличнаяЧастьДокументаОснования, ТаблицаМаркировок, ТекстОшибки="")
	
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("ЦенаПерерасчитана", Новый ОписаниеТипов("Булево"));
	
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("ПланируемыйСтатусМаркируемогоТовара", Новый ОписаниеТипов("ПеречислениеСсылка.ПланируемыйСтатусМаркируемогоТовара"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("КоличествоПредметаРасчета", Новый ОписаниеТипов("Число"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("МераКоличестваПредметаРасчетаККТ", Новый ОписаниеТипов("ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ"));
	//ТабличнаяЧастьДокументаОснования.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("РезультатПроверкиКМ", Новый ОписаниеТипов("Число"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("РезультатПроверкиСведенийОТоваре", Новый ОписаниеТипов("Число"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("ОтветОИСМОСтатусеТовара", Новый ОписаниеТипов("ПеречислениеСсылка.ОтветОИСМОСтатусеТовара"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("КодыОбработкиЗапроса", Новый ОписаниеТипов("ПеречислениеСсылка.КодыОбработкиЗапроса"));
	ТабличнаяЧастьДокументаОснования.Колонки.Добавить("РезультатыОбработкиЗапроса", Новый ОписаниеТипов("Число"));
	
	КопияТаблицаМаркировок = ТаблицаМаркировок.Выгрузить();
	
	ОбработкаОбъектШК = Обработки.ШтрихКоды.Создать();
	
	Для Каждого СтрокаТоваров Из ТабличнаяЧастьДокументаОснования Цикл
		
		Если ПустаяСтрока(СтрокаТоваров.ИдентификаторТовара)
			ИЛИ ЗначениеЗаполнено(СтрокаТоваров.GTIN) Тогда
			Продолжить;
		КонецЕсли;
		
		НайденныеСтроки = КопияТаблицаМаркировок.НайтиСтроки(Новый Структура("ИдентификаторТовара", СтрокаТоваров.ИдентификаторТовара));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ТипЦен.ЦенаВключаетНДС И НЕ СтрокаТоваров.ЦенаПерерасчитана Тогда
			СтрокаТоваров.Цена = Окр(СтрокаТоваров.Цена * (1 + СтрокаТоваров.СтавкаНДС.Ставка / 100), 2);
			СтрокаТоваров.Сумма = Окр(СтрокаТоваров.Сумма * (1 + СтрокаТоваров.СтавкаНДС.Ставка / 100), 2);
		КонецЕсли;
		
		Для Каждого ТекущаяМаркировка Из НайденныеСтроки Цикл
			
			СтруктураМаркировки = ОбработкаОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяМаркировка.КодМаркировки);
			
			Если НЕ СтруктураМаркировки.Разобран Тогда
				Продолжить;
			КонецЕсли;
			
			Попытка
				GTIN          = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
			Исключение
				ТекстОшибки = "Маркировка <"+ТекущаяМаркировка.КодМаркировки+"> не распознана. Проверьте ее корректность";
				ЗаписатьВФайлЛога("Пробить чек", 1, "ОШИБКА ПРИ ПРОБИТИИ ЧЕКА: "+СтрЗаменить(ТекстОшибки,Символы.ПС," "),,1);
				Возврат Ложь;
			КонецПопытки;
			
			// Весь товар распределен по маркировке
			Если СтрокаТоваров.Количество * СтрокаТоваров.Коэффициент = 1 Тогда
				СтрокаТоваров.GTIN = GTIN;
				СтрокаТоваров.СерийныйНомер = СерийныйНомер;
				СтрокаТоваров.КодМаркировки = ТекущаяМаркировка.КодМаркировкиBase64;
				СтрокаТоваров.ПланируемыйСтатусМаркируемогоТовара = ТекущаяМаркировка.ПланируемыйСтатусМаркируемогоТовара;
				СтрокаТоваров.КоличествоПредметаРасчета = ТекущаяМаркировка.КоличествоПредметаРасчета;
				СтрокаТоваров.МераКоличестваПредметаРасчетаККТ = ТекущаяМаркировка.МераКоличестваПредметаРасчетаККТ;
				СтрокаТоваров.РезультатПроверкиКМ = ТекущаяМаркировка.РезультатПроверкиКМ;
				// Для штриха: можем получить отричательное число и выведется ошибка при пробитии
				СтрокаТоваров.РезультатПроверкиСведенийОТоваре = ?(ТекущаяМаркировка.РезультатПроверкиСведенийОТоваре < 0, 0, ТекущаяМаркировка.РезультатПроверкиСведенийОТоваре);
				СтрокаТоваров.ОтветОИСМОСтатусеТовара = ТекущаяМаркировка.ОтветОИСМОСтатусеТовара;
				СтрокаТоваров.КодыОбработкиЗапроса = ТекущаяМаркировка.КодыОбработкиЗапроса;
				СтрокаТоваров.РезультатыОбработкиЗапроса = ТекущаяМаркировка.РезультатыОбработкиЗапроса;
				Прервать;
			КонецЕсли;
			
			НоваяСтрокаТоваров = ТабличнаяЧастьДокументаОснования.Вставить(СтрокаТоваров.НомерСтроки);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТоваров, СтрокаТоваров);
			
			НоваяСтрокаТоваров.Количество          = 1;
			НоваяСтрокаТоваров.Коэффициент         = 1;
			НоваяСтрокаТоваров.Сумма               = Окр(СтрокаТоваров.Сумма / СтрокаТоваров.Количество, 2);
			НоваяСтрокаТоваров.СуммаНДС            = Окр(СтрокаТоваров.СуммаНДС / СтрокаТоваров.Количество, 2);
			НоваяСтрокаТоваров.СуммаСкидки         = Окр(СтрокаТоваров.СуммаСкидки / СтрокаТоваров.Количество, 2);
			НоваяСтрокаТоваров.СуммаОплаты         = Окр(СтрокаТоваров.СуммаОплаты / СтрокаТоваров.Количество, 2);
			НоваяСтрокаТоваров.СуммаСкидкиСтроки   = Окр(СтрокаТоваров.СуммаСкидкиСтроки / СтрокаТоваров.Количество, 2);
			НоваяСтрокаТоваров.СуммаСкидкиБонусами = Окр(СтрокаТоваров.СуммаСкидкиБонусами / СтрокаТоваров.Количество, 2);
			НоваяСтрокаТоваров.GTIN                = GTIN;
			НоваяСтрокаТоваров.СерийныйНомер       = СерийныйНомер;
			НоваяСтрокаТоваров.СуммаВсего          = Окр(СтрокаТоваров.Сумма / СтрокаТоваров.Количество, 2);
			НоваяСтрокаТоваров.ЦенаПерерасчитана   = Истина;
			
			НоваяСтрокаТоваров.КодМаркировки = ТекущаяМаркировка.КодМаркировкиBase64;
			НоваяСтрокаТоваров.ПланируемыйСтатусМаркируемогоТовара = ТекущаяМаркировка.ПланируемыйСтатусМаркируемогоТовара;
			НоваяСтрокаТоваров.КоличествоПредметаРасчета = ТекущаяМаркировка.КоличествоПредметаРасчета;
			НоваяСтрокаТоваров.МераКоличестваПредметаРасчетаККТ = ТекущаяМаркировка.МераКоличестваПредметаРасчетаККТ;
			НоваяСтрокаТоваров.РезультатПроверкиКМ = ТекущаяМаркировка.РезультатПроверкиКМ;
			НоваяСтрокаТоваров.РезультатПроверкиСведенийОТоваре = ?(ТекущаяМаркировка.РезультатПроверкиСведенийОТоваре< 0, 0, ТекущаяМаркировка.РезультатПроверкиСведенийОТоваре);
			НоваяСтрокаТоваров.ОтветОИСМОСтатусеТовара = ТекущаяМаркировка.ОтветОИСМОСтатусеТовара;
			НоваяСтрокаТоваров.КодыОбработкиЗапроса = ТекущаяМаркировка.КодыОбработкиЗапроса;
			НоваяСтрокаТоваров.РезультатыОбработкиЗапроса = ТекущаяМаркировка.РезультатыОбработкиЗапроса;
			
			СтрокаТоваров.Количество          = СтрокаТоваров.Количество * СтрокаТоваров.Коэффициент - 1;
			СтрокаТоваров.Коэффициент         = 1;
			СтрокаТоваров.Сумма               = СтрокаТоваров.Сумма - НоваяСтрокаТоваров.Сумма;
			СтрокаТоваров.СуммаНДС            = СтрокаТоваров.СуммаНДС - НоваяСтрокаТоваров.СуммаНДС;
			СтрокаТоваров.СуммаСкидки         = СтрокаТоваров.СуммаСкидки - НоваяСтрокаТоваров.СуммаСкидки;
			СтрокаТоваров.СуммаОплаты         = СтрокаТоваров.СуммаОплаты - НоваяСтрокаТоваров.СуммаОплаты;
			СтрокаТоваров.СуммаСкидкиСтроки   = СтрокаТоваров.СуммаСкидкиСтроки - НоваяСтрокаТоваров.СуммаСкидкиСтроки;
			СтрокаТоваров.СуммаСкидкиБонусами = СтрокаТоваров.СуммаСкидкиБонусами - НоваяСтрокаТоваров.СуммаСкидкиБонусами;
			СтрокаТоваров.СуммаВсего          = СтрокаТоваров.СуммаВсего - НоваяСтрокаТоваров.СуммаВсего;
			
			ТабличнаяЧастьДокументаОснования.Сдвинуть(НоваяСтрокаТоваров, -1);
			
			Попытка
				НомерСтрокиТоваров=0;
				Для Каждого СтрокаТоваровНоменклатуры Из ТабличнаяЧастьДокументаОснования Цикл
					НомерСтрокиТоваров=НомерСтрокиТоваров+1;
					СтрокаТоваровНоменклатуры.НомерСтроки=НомерСтрокиТоваров;
				КонецЦикла;
			Исключение
				// Для чека на оплату не доступна для записи НомерСтроки
			КонецПопытки;
			
			КопияТаблицаМаркировок.Удалить(ТекущаяМаркировка);
		КонецЦикла;
		
		СтрокаТоваров.ЦенаПерерасчитана   = Истина;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ТипМаркировкиПоТипуНоменклатуры(ТипНоменклатуры)
	
	Возврат ТипНоменклатуры.ТипМаркировки.КодМаркировки;
	
КонецФункции

// Пробитие чека
Функция ПробитьЧек(ТекстОшибки,СтруктураВозвратныхПараметров=НЕОПРЕДЕЛЕНО) Экспорт
	Результат=Ложь; ТекстОшибки = "";
	
	// Проверим необходимость проведения авторизаций безналичных платежей
	АвторизацийБПЕсть = НЕ (НетОплатПлатежнойКартой() ИЛИ (УстановленРежим("Копия")) ИЛИ (ПолученоБезНал = 0) ИЛИ фкРазрешитьРучнуюАвторизациюБезналичныхПлатежей ИЛИ ТипЗнч(Ссылка) = Тип("ДокументСсылка.Выписка"));
	Если АвторизацийБПЕсть Тогда
		// Выполняем авторизации безналичных платежей
		Если НЕ ВыполнитьАвторизациюБП() Тогда
			ЗаписатьВФайлЛога("Пробить чек", 1, "ОШИБКА ПРИ АВТОРИЗАЦИИ БЕЗНАЛИЧНОЙ ОПЛАТЫ",,1);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	ПараметрыШапки = ПодготовитьПараметрыШапкиДляПробития();
	СформированнаяСтрока = "";
	СформироватьДопИнформациюОПлатежах(СформированнаяСтрока);
	
	ЭтоРасходВыписки = (ТипЗнч(Ссылка) = Тип("ДокументСсылка.Выписка") И (Ссылка.СуммаДокументаРасход <> 0 ИЛИ Ссылка.СтатьяДДС.ВидДвижения=Перечисления.ВидыДвижений.Расход));
	
	КонтрагентЮрЧп = (Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо ИЛИ Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель);
	
	Если (НЕ УстановленРежим("Копия")) И (УстановленРежим("РасходныйКассовыйОрдер")) Тогда
		Если НЕ ПробитьЧекИнкассации(Истина,ПараметрыШапки.ВсегоПоДокументу) Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	флПробитьЧек = Истина;
	
	ПризнакСпособаРасчета = Перечисления.ТипыСпособаРасчетаККТ.ПолучитьКодПризнакаСпособаРасчета(ТипСпособаРасчетаККТ, Товары, Оплаты);
	
	Пока флПробитьЧек Цикл
		Попытка // ПРОБИТИЕ ЧЕКА
			// Сформируем параметры задания
			SafeArrayПараметрыЧека=Рарус_Компонента.СоздатьПараметры(4,1);
			//Формирование параметров шапки
			SafeArrayПараметрыШапки=Рарус_Компонента.СоздатьПараметры(46,1);
			SafeArrayПараметрыШапки.SetValue(0,0,ПараметрыШапки.ТипЧека);            //Тип чека - чек прихода/возрат прихода/расхода/возврат расхода
			SafeArrayПараметрыШапки.SetValue(1,0,ПараметрыШапки.Фискальный);         //Признак НЕ фискального чека (для копии)
			SafeArrayПараметрыШапки.SetValue(2,0,ПараметрыШапки.Всего);              //Сумма документа со скидками на товары, без скидки на чек
			SafeArrayПараметрыШапки.SetValue(3,0,ПараметрыШапки.Итог);               //Сумма документа со всеми скидками (в нашем случае это и есть сумма документа)
			SafeArrayПараметрыШапки.SetValue(4,0,ПараметрыШапки.СкидкаНаЧек);        //Наименование скидки на чек
			SafeArrayПараметрыШапки.SetValue(5,0,ПараметрыШапки.ПроцентСкидкиНаЧек); //Процент скидки на чек
			SafeArrayПараметрыШапки.SetValue(7,0,ПараметрыШапки.СуммаСкидкиНаЧек);   //Сумма скидки на чек
			SafeArrayПараметрыШапки.SetValue(6,0,ПараметрыШапки.ДисконтнаяКарта);    //Дисконтная карта
			SafeArrayПараметрыШапки.SetValue(8,0,ПараметрыШапки.КопияЧека);          //Копия чека
			SafeArrayПараметрыШапки.SetValue(9,0,ПараметрыШапки.НомерСмены);         //Номер смены (известен для копии чека)
			SafeArrayПараметрыШапки.SetValue(10,0,ПараметрыШапки.НомерЧека);         //Номер чека (известен для копии чека)
			SafeArrayПараметрыШапки.SetValue(11,0,ПараметрыШапки.НомерДокумента);    //Номер документа (известен для копии чека)
			SafeArrayПараметрыШапки.SetValue(12,0,ПараметрыШапки.ДатаФР);            //Дата документа (известна для копии чека)
			SafeArrayПараметрыШапки.SetValue(13,0,ПараметрыШапки.НастройкаПечати);   //Настройки печати ???
			SafeArrayПараметрыШапки.SetValue(14,0,ПараметрыШапки.ПодкладнаяПечать);  //Печать на чековую ленту
			SafeArrayПараметрыШапки.SetValue(15,0,ПараметрыШапки.ОсновнойОтдел);     //Основной отдел
			SafeArrayПараметрыШапки.SetValue(16,0,ПараметрыШапки.ТекстШапки);        //Текст шапки
			SafeArrayПараметрыШапки.SetValue(17,0,ПараметрыШапки.ТекстПодвала);      //Текст подвала
			SafeArrayПараметрыШапки.SetValue(18,0,ПараметрыШапки.ВыделятьНалоги);    //Выделять Налоги
			SafeArrayПараметрыШапки.SetValue(19,0,ПараметрыШапки.ПарольКассира);     //Пароль кассира
			SafeArrayПараметрыШапки.SetValue(20,0,ПараметрыШапки.ИмяКассира);        //Имя кассира
			SafeArrayПараметрыШапки.SetValue(21,0,СформированнаяСтрока);             //Дополнительная информация о реквизитах сотового платежа
			SafeArrayПараметрыШапки.SetValue(22,0,0);                                //Сумма за свой счет
			
			Если УстановленРежим("ЧекНаОплату") ИЛИ
				 УстановленРежим("ЧекНаОплатуВозврат") ИЛИ
				 УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ
				 УстановленРежим("РасходныйКассовыйОрдер") ИЛИ
				 УстановленРежим("БанковскаяВыписка") Тогда
				 КодНалогообложения=ПолучитьКодНалогообложенияПодразделенияКомпании(ПараметрыШапки.Организация,ПараметрыШапки.ПодразделениеКомпании);
				 Если КодНалогообложения=Неопределено Тогда
					КодНалогообложения=0;
				 КонецЕсли; 
			ИначеЕсли УстановленРежим("Чек") ИЛИ
				 УстановленРежим("ЧекНаВозврат") Тогда
				 КодНалогообложения=ПолучитьКодНалогообложенияСкладыКомпании(ПараметрыШапки.Организация,ПараметрыШапки.ПодразделениеКомпании,ПараметрыШапки.СкладКомпании);
				 Если КодНалогообложения=Неопределено Тогда
					КодНалогообложения=0;
				 КонецЕсли;
			Иначе
				 КодНалогообложения=Неопределено;
			КонецЕсли;  
			
			//KamikadZe begin add 24.03.2017 18:06:32
			обОбщие.обОпределениеФРОтделСНО(Ссылка,ПараметрыШапки.ОсновнойОтдел,КодНалогообложения);
			//SafeArrayПараметрыШапки.SetValue(15,0,ПараметрыШапки.ОсновнойОтдел);     //Основной отдел
			//KamikadZe end add 
			
			Если КодНалогообложения<>Неопределено Тогда
				SafeArrayПараметрыШапки.SetValue(23,0,КодНалогообложения);				// Система налогооблажения
			КонецЕсли; 
			
			SafeArrayПараметрыШапки.SetValue(24,0, ДанныеКлиента);                   //Контактная информация
			SafeArrayПараметрыШапки.SetValue(25,0,?(НЕ НеПечататьБумажныйЧек, 0, 1));//НЕ печатать бумажный чек: 0 - Печатать, 1 - НЕ печатать
			SafeArrayПараметрыШапки.SetValue(26,0, ПараметрыШапки.ИННКассира);// ИНН Кассира
			SafeArrayПараметрыШапки.SetValue(27,0, ПараметрыШапки.Получатель);// Предавление получателя (юр. лицо, частный предприниматель) - тег 1227
			SafeArrayПараметрыШапки.SetValue(28,0, ПараметрыШапки.ПолучательИНН);// ИНН получателя - тег 1228
			
			// Заполним тег агента 1057 и данные поставщика в шапке
			ПризнакАгента = -1;
			ТелефонПоставщика = "";
			НаименованиеПоставщика = "";
			ИННПоставщика = "";
			
			СписокДоговоров = Товары.Выгрузить(, "ДоговорВзаиморасчетов");
			СписокДоговоров.Свернуть("ДоговорВзаиморасчетов");
			Если СписокДоговоров.Количество() = 1
				И НЕ обЗначениеНеЗаполнено(СписокДоговоров[0].ДоговорВзаиморасчетов)
				И НЕ обЗначениеНеЗаполнено(СписокДоговоров[0].ДоговорВзаиморасчетов.ПризнакАгента) Тогда
				ПризнакАгента = Перечисления.ПризнакиАгента.КодПризнакаАгента(СписокДоговоров[0].ДоговорВзаиморасчетов.ПризнакАгента);
				ТелефонПоставщика = СписокДоговоров[0].ДоговорВзаиморасчетов.ТелефонПоставщика;
				НаименованиеПоставщика = СписокДоговоров[0].ДоговорВзаиморасчетов.НаименованиеПоставщика;
				ИННПоставщика = СписокДоговоров[0].ДоговорВзаиморасчетов.ИННПоставщика;
			КонецЕсли;
			
			SafeArrayПараметрыШапки.SetValue(29,0, ПризнакАгента);// Признак агента 
			SafeArrayПараметрыШапки.SetValue(30,0, ТелефонПоставщика);// Телефон поставщика
			SafeArrayПараметрыШапки.SetValue(31,0, НаименованиеПоставщика);// Наименование поставщика
			SafeArrayПараметрыШапки.SetValue(32,0, ИННПоставщика);// ИНН поставщика

			SafeArrayПараметрыШапки.SetValue(33,0,Формат(ПараметрыШапки.ПолучательДатаРождения, "ДФ=dd.MM.yyyy"));	// Покупатель - дата рождения
			SafeArrayПараметрыШапки.SetValue(34,0,ПараметрыШапки.ПолучательГражданство.Код);	// Покупатель - код страны гражданства
			SafeArrayПараметрыШапки.SetValue(35,0,КодВидаДокументаУдостоверяющегоЛичность);	// Покупатель - код вида подтверждающего документа
			SafeArrayПараметрыШапки.SetValue(36,0,ДанныеДокументаУдостоверяющегоЛичность);	// Покупатель - данные документа удостоверяющего личность
			SafeArrayПараметрыШапки.SetValue(37,0,АдресПокупателя);	// Покупатель - адрес покупателя
			
			SafeArrayПараметрыШапки.SetValue(38,0,"");	// Операционный реквизит чека. Пока передакем пустым.
			SafeArrayПараметрыШапки.SetValue(39,0,"");	// Отраслевой реквизит чека. Пока передакем пустым.
			Если НеПечататьФискальныйЧекПриОтправкеЭлектронногоЧекаПокупателю И ЗначениеЗаполнено(ДанныеКлиента) Тогда
				SafeArrayПараметрыШапки.SetValue(40,0, Ложь); // Не печатать фискальный чек при отправке электронного чека покупателю
			Иначе
				SafeArrayПараметрыШапки.SetValue(40,0, Истина); // Печатать фискальный чек при отправке электронного чека покупателю
			КонецЕсли;
			
			SafeArrayПараметрыШапки.SetValue(41,0,ПараметрыШапки.ДополнительныйРеквизитЧека);	// Признак чека коррекции. Передаем в любом случае.
			SafeArrayПараметрыШапки.SetValue(42,0, ПараметрыШапки.ЭтоЧекКоррекции И ВерсияФФД>105);	// Признак чека коррекции. Передаем в любом случае.
			SafeArrayПараметрыШапки.SetValue(43,0, 0); // тип коррекции
			SafeArrayПараметрыШапки.SetValue(44,0, " "); // номер предписания
			SafeArrayПараметрыШапки.SetValue(45,0,-1); // дата коррекции
			Если УстановленРежим("ЧекКоррекции") Тогда
				SafeArrayПараметрыШапки.SetValue(43,0,?(ПараметрыШапки.ТипКоррекции=Перечисления.ТипыЧековКоррекции.Самостоятельно,0,1));	// Тип коррекции
				SafeArrayПараметрыШапки.SetValue(45,0,ПараметрыШапки.ДатаКоррекции - Дата(1970,1,1,1,0,0));	// Дата коррекции
				Если ПараметрыШапки.Свойство("НомерПредписания") Тогда
					SafeArrayПараметрыШапки.SetValue(44,0,ПараметрыШапки.НомерПредписания);	// Описание коррекции
				КонецЕсли;
			КонецЕсли;
			
			SafeArrayПараметрыЧека.SetValue(0,0,SafeArrayПараметрыШапки);
			
			ПечатьТабличнойЧастиНаЧек=Истина;
		 	ТабличнаяЧастьДокументаОснования=Неопределено;
			ТипЦенОснования = Неопределено;
			
			Если УстановленРежим("ЧекНаОплату")
			 ИЛИ УстановленРежим("ЧекНаОплатуВозврат")
			 ИЛИ УстановленРежим("ПриходныйКассовыйОрдер")
			 ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
			 ИЛИ УстановленРежим("БанковскаяВыписка")
			 ИЛИ УстановленРежим("ЧекКоррекции")
			 // копию чека на оплату печатаем так же как и просто чек на оплату.
			 ИЛИ (ХозОперация = Справочники.ХозОперации.ЧекНаОплатуВозврат И УстановленРежим("Копия"))
			 ИЛИ (ХозОперация = Справочники.ХозОперации.ЧекНаОплату И УстановленРежим("Копия"))
			 ИЛИ (ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат И УстановленРежим("Копия"))
			 ИЛИ (ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки И УстановленРежим("Копия"))
			 ИЛИ (ХозОперация = Справочники.ХозОперации.ПриходныйКассовыйОрдер И УстановленРежим("Копия"))
			 ИЛИ (ХозОперация = Справочники.ХозОперации.РасходныйКассовыйОрдер И УстановленРежим("Копия"))
			 ИЛИ (ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки И УстановленРежим("Копия")) Тогда
			 
				Если ПечатьТабличнойЧастиТовары И (УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ (ХозОперация = Справочники.ХозОперации.ПриходныйКассовыйОрдер И УстановленРежим("Копия")) ИЛИ (УстановленРежим("ЧекКоррекции") И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер"))) Тогда
					Если Ссылка.Товары.Количество() = 0  Тогда
						Если УстановленРежим("ЧекКоррекции") Тогда
							ТабличнаяЧастьДокументаОснования=Документы.ПриходныйКассовыйОрдер.ПолучитьТабличнуюЧастьДокументаОснования(ДокументОснование);
						Иначе
							ТабличнаяЧастьДокументаОснования=Документы.ПриходныйКассовыйОрдер.ПолучитьТабличнуюЧастьДокументаОснования(Ссылка);
						КонецЕсли;
					КонецЕсли;
					// Получим тип цен основания
					Попытка
						ТипЦенОснования = ДокументОснование.ТипЦен;
					Исключение
					КонецПопытки;
				ИначеЕсли ПечатьТабличнойЧастиТовары И (УстановленРежим("РасходныйКассовыйОрдер") ИЛИ (ХозОперация = Справочники.ХозОперации.РасходныйКассовыйОрдер И УстановленРежим("Копия")) ИЛИ (УстановленРежим("ЧекКоррекции") И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РасходныйКассовыйОрдер"))) Тогда
					Если Ссылка.Товары.Количество() = 0 Тогда
						Если УстановленРежим("ЧекКоррекции") Тогда
							ДокументОплаты = ?(ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер"), ДокументОснование.ДокументОснование, ДокументОснование);
						Иначе
							ДокументОплаты = ?(ТипЗнч(Ссылка.ДокументОснование) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер"), Ссылка.ДокументОснование, Ссылка);
						КонецЕсли;
						ТабличнаяЧастьДокументаОснования=Документы.ПриходныйКассовыйОрдер.ПолучитьТабличнуюЧастьДокументаОснования(ДокументОплаты);
						Если НЕ ДокументОплаты = Ссылка И ТабличнаяЧастьДокументаОснования <> Неопределено Тогда
							КурсДокументаОплаты = ДокументОплаты.КурсДокумента;
							ВалютаДокументаОплаты = ДокументОплаты.ВалютаДокумента;
							Для Каждого ТекущаяСтрока Из ТабличнаяЧастьДокументаОснования Цикл
								ТекущаяСтрока.Цена = обПересчет(ТекущаяСтрока.Цена, ВалютаДокументаОплаты, КурсДокументаОплаты, Ссылка.ВалютаДокумента, Ссылка.КурсДокумента);
								ТекущаяСтрока.Сумма = обПересчет(ТекущаяСтрока.Сумма, ВалютаДокументаОплаты, КурсДокументаОплаты, Ссылка.ВалютаДокумента, Ссылка.КурсДокумента);
								ТекущаяСтрока.СуммаНДС = обПересчет(ТекущаяСтрока.СуммаНДС, ВалютаДокументаОплаты, КурсДокументаОплаты, Ссылка.ВалютаДокумента, Ссылка.КурсДокумента);
								ТекущаяСтрока.СуммаСкидки = обПересчет(ТекущаяСтрока.СуммаСкидки, ВалютаДокументаОплаты, КурсДокументаОплаты, Ссылка.ВалютаДокумента, Ссылка.КурсДокумента);
								ТекущаяСтрока.СуммаВсего = обПересчет(ТекущаяСтрока.СуммаВсего, ВалютаДокументаОплаты, КурсДокументаОплаты, Ссылка.ВалютаДокумента, Ссылка.КурсДокумента);
								ТекущаяСтрока.СуммаСкидкиСтроки = обПересчет(ТекущаяСтрока.СуммаСкидкиСтроки, ВалютаДокументаОплаты, КурсДокументаОплаты, Ссылка.ВалютаДокумента, Ссылка.КурсДокумента);
								ТекущаяСтрока.СуммаСкидкиБонусами = обПересчет(ТекущаяСтрока.СуммаСкидкиБонусами, ВалютаДокументаОплаты, КурсДокументаОплаты, Ссылка.ВалютаДокумента, Ссылка.КурсДокумента);
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
					// Получим тип цен основания
					Попытка
						ТипЦенОснования = ДокументОплаты.ДокументОснование.ТипЦен;
					Исключение
					КонецПопытки;
				КонецЕсли;
				Если ТабличнаяЧастьДокументаОснования<>Неопределено Тогда
					Если ТабличнаяЧастьДокументаОснования.Колонки.Найти("СуммаОплаты") = Неопределено Тогда
						ТабличнаяЧастьДокументаОснования.Колонки.Добавить("СуммаОплаты");
					КонецЕсли;
					Документы.ЧекНаОплату.ПроизвестиРаспределениеСуммыОплаты(Ссылка.СуммаДокумента, ТабличнаяЧастьДокументаОснования);
					
					Если ТабличнаяЧастьДокументаОснования.Количество()>0
						И ЗначениеЗаполнено(ДокументОснование) И (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров")  
						ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратПоставщику")
						ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")
						ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратОтПокупателя")
						ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитенту")
						ИЛИ (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетНаОплату") И ЗначениеЗаполнено(ДокументОснование.ДокументОснование)
						И (ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваров")
						ИЛИ ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ЗаказНаряд")))
						ИЛИ (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетОтПоставщика") И ЗначениеЗаполнено(ДокументОснование.ДокументОснование)
						И ТипЗнч(ДокументОснование.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваров"))) Тогда
							дкЗаполнитьГТД(Ссылка.ПолучитьОбъект(),ТабличнаяЧастьДокументаОснования);
					КонецЕсли;
					СтруктураОбъекта = Новый Структура();
					СтруктураОбъекта.Вставить("Товары", ТабличнаяЧастьДокументаОснования);
					СтруктураОбъекта.Вставить("КодыМаркировки", КодыМаркировки);
					дкЗаполнитьКодыМаркировкиТоваров(СтруктураОбъекта, ДокументОснование);
					
					// БИЗТЕХ КОВАЛЬ 07.11.2025 ++
					// Пробежим каждую маркировку и заполним параметры для пробития Так как через ПКО не пробивается нормально маркированный товар
					// TODO: Подозреваю что ответ от ЧЗ о разрешительном режиме тоже стоит внести в новую таблицу чтобы через ПКО можно было пробить маркированный товар с проверкой в разрешительном режиме
					Для Каждого СтрокаКодаМаркировки_ПКО Из КодыМаркировки Цикл
						
						НайденныеСтроки_ПКО = ТабличнаяЧастьДокументаОснования.НайтиСтроки(Новый Структура("ИдентификаторТовара", СтрокаКодаМаркировки_ПКО.ИдентификаторТовара));
						Если НайденныеСтроки_ПКО.Количество() Тогда
							
							НайденнаяСтрокаТовара_ПКО = НайденныеСтроки_ПКО[0];
							
							//ПланируемыйСтатусМаркируемогоТовара
							Если НЕ ЗначениеЗаполнено(СтрокаКодаМаркировки_ПКО.ПланируемыйСтатусМаркируемогоТовара) Тогда 
								Если УстановленРежим("Чек") ИЛИ
									УстановленРежим("ЧекНаОплату") ИЛИ
									УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ
									УстановленРежим("БанковскаяВыписка") ИЛИ
									(УстановленРежим("ЧекКоррекции") И
									((Ссылка.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств) ИЛИ (Ссылка.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств)))Тогда
									СтрокаКодаМаркировки_ПКО.ПланируемыйСтатусМаркируемогоТовара = Перечисления.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварРеализован;
								ИначеЕсли УстановленРежим("ЧекНаВозврат") ИЛИ
									УстановленРежим("ЧекНаОплатуВозврат") ИЛИ
									УстановленРежим("РасходныйКассовыйОрдер") ИЛИ
									УстановленРежим("ВозвратТоваровОтПокупателя") ИЛИ
									УстановленРежим("РКО") ИЛИ
									(УстановленРежим("ЧекКоррекции") И
									((Ссылка.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств) ИЛИ (Ссылка.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств)))Тогда 	
									СтрокаКодаМаркировки_ПКО.ПланируемыйСтатусМаркируемогоТовара = Перечисления.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварВозвращен;
								КонецЕсли;
							КонецЕсли;
							
							//Количество предмета расчета
							СтрокаКодаМаркировки_ПКО.КоличествоПредметаРасчета = 1;
							
							//МераКоличестваПредметаРасчетаККТ
							Если НЕ ЗначениеЗаполнено(СтрокаКодаМаркировки_ПКО.МераКоличестваПредметаРасчетаККТ) Тогда 
								СтрокаКодаМаркировки_ПКО.МераКоличестваПредметаРасчетаККТ = НайденнаяСтрокаТовара_ПКО.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.МераКоличестваПредметаРасчетаККТ;
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					//БИЗТЕХ КОВАЛЬ 07.11.2025 --
					
				КонецЕсли;
				Если ПечатьТабличнойЧастиТовары И
					((Ссылка.Товары.Количество() = 0 И (УстановленРежим("БанковскаяВыписка") ИЛИ (ХозОперация = Справочники.ХозОперации.СтрокаБанковскойВыписки И УстановленРежим("Копия"))))
						ИЛИ (УстановленРежим("ЧекКоррекции") И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.Выписка") И Ссылка.Товары.Количество() = 0))
				Тогда
					Если УстановленРежим("ЧекКоррекции") Тогда
						// Получим сумму оплаты и сделки
						ТаблицаСделок = ДокументОснование.Состав.Выгрузить(, "Сделка,СуммаПриход,СуммаРасход");
						ТаблицаСделок.Свернуть("Сделка", "СуммаПриход,СуммаРасход");
						ТаблицаСделок.Сортировать("Сделка");
						
						// Подготовим данные для получение номенклатуры по сделке
						СтрукутраСделки = Новый Структура("ДокументОснование,ВалютаДокумента,КурсДокумента");
						ЗаполнитьЗначенияСвойств(СтрукутраСделки, ДокументОснование,, "ДокументОснование");
						
						КопияТовары = Товары.Выгрузить();
						КопияТовары.Очистить();
						
						КурсДокументаОплаты = ДокументОснование.КурсДокумента;
						СуммаБезСделки = 0;
						
						КодыМаркировки.Очистить();
						
						СтруктураОбъекта = Новый Структура("КодыМаркировки, Товары");
						СтруктураОбъекта.КодыМаркировки = КодыМаркировки.Выгрузить();
						
						// Распределим сумму оплатц/возрата по товарам сделки
						Для Каждого ТекущаяСтрока Из ТаблицаСделок Цикл
							СтрукутраСделки.ДокументОснование = ТекущаяСтрока.Сделка;
							ТабличнаяЧастьОснования=Документы.ПриходныйКассовыйОрдер.ПолучитьТабличнуюЧастьДокументаОснования(СтрукутраСделки);
							
							// Укажем сумму сделки как предоплата
							Если ТабличнаяЧастьОснования=Неопределено Тогда
								СуммаБезСделки = СуммаБезСделки + (ТекущаяСтрока.СуммаПриход + ТекущаяСтрока.СуммаРасход) * КурсДокументаОплаты;
							Иначе
								Если ТабличнаяЧастьДокументаОснования = Неопределено Тогда
									ТабличнаяЧастьДокументаОснования = ТабличнаяЧастьОснования.Скопировать();
									ТабличнаяЧастьДокументаОснования.Очистить();
									Если ТабличнаяЧастьДокументаОснования.Колонки.Найти("СуммаОплаты") = Неопределено Тогда
										ТабличнаяЧастьДокументаОснования.Колонки.Добавить("СуммаОплаты");
									КонецЕсли;
								КонецЕсли;
								
								// Получим тип цен
								ТипЦенСделки = Неопределено;
								
								Попытка
									ТипЦенСделки = ТекущаяСтрока.Сделка.ТипЦен;
								Исключение
								КонецПопытки;
								
								Для Каждого ТекущаяСтрокаТоваров Из ТабличнаяЧастьОснования Цикл
									НоваяСтрока = КопияТовары.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрокаТоваров);
									НоваяСтрока.Цена = ТекущаяСтрокаТоваров.Цена * КурсДокументаОплаты;
									НоваяСтрока.Сумма = ТекущаяСтрокаТоваров.Сумма * КурсДокументаОплаты;
									НоваяСтрока.СуммаНДС = ТекущаяСтрокаТоваров.СуммаНДС * КурсДокументаОплаты;
									
									Если НЕ ТипЦенСделки = Неопределено И НЕ ТипЦенСделки.ЦенаВключаетНДС Тогда 
										НоваяСтрока.Цена = Окр(НоваяСтрока.Цена * (1 + НоваяСтрока.СтавкаНДС.Ставка / 100), 2);
										НоваяСтрока.Сумма = Окр(НоваяСтрока.Сумма * (1 + НоваяСтрока.СтавкаНДС.Ставка / 100), 2);
									КонецЕсли;
									НоваяСтрока.СуммаСкидки = ТекущаяСтрокаТоваров.СуммаСкидки * КурсДокументаОплаты;
									НоваяСтрока.СуммаВсего = ТекущаяСтрокаТоваров.СуммаВсего * КурсДокументаОплаты;
									НоваяСтрока.СуммаСкидкиСтроки = ТекущаяСтрокаТоваров.СуммаСкидкиСтроки * КурсДокументаОплаты;
									НоваяСтрока.СуммаСкидкиБонусами = ТекущаяСтрокаТоваров.СуммаСкидкиБонусами * КурсДокументаОплаты;
								КонецЦикла;
								
								дкЗаполнитьГТД(ДокументОснование.ПолучитьОбъект(),КопияТовары,ТекущаяСтрока.Сделка);
								
								// Распределим текущую сумму оплат на сделку
								Документы.ЧекНаОплату.ПроизвестиРаспределениеСуммыОплаты((ТекущаяСтрока.СуммаПриход + ТекущаяСтрока.СуммаРасход) * КурсДокументаОплаты, КопияТовары);
								
								// Заполним маркировку
								СтруктураОбъекта.Товары = КопияТовары;
								дкЗаполнитьКодыМаркировкиТоваров(СтруктураОбъекта, ТекущаяСтрока.Сделка);
								
								// Перенесем в Товары
								Для Каждого ТекущаяСтрокаНоменклатуры Из КопияТовары Цикл
									ЗаполнитьЗначенияСвойств(ТабличнаяЧастьДокументаОснования.Добавить(), ТекущаяСтрокаНоменклатуры);
								КонецЦикла;
								
								// Перенесем в КодыМаркировки
								Для Каждого ТекущаяСтрокаМаркировки Из СтруктураОбъекта.КодыМаркировки Цикл
									ЗаполнитьЗначенияСвойств(КодыМаркировки.Добавить(), ТекущаяСтрокаМаркировки);
								КонецЦикла;
								
								КопияТовары.Очистить();
								
							КонецЕсли;
							
						КонецЦикла;
						
						Если ТабличнаяЧастьДокументаОснования <> Неопределено И СуммаБезСделки <> 0 Тогда
							НоваяСтрока = ТабличнаяЧастьДокументаОснования.Добавить();
							НоваяСтрока.Номенклатура = Справочники.Номенклатура.Предоплата;
							ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
							НоваяСтрока.СтавкаНДС = ДокументОснование.СтавкаНДС;
							НоваяСтрока.Цена = СуммаБезСделки;
							ОбработкаРеквизита("Товары.Цена", НоваяСтрока);
							НоваяСтрока.СуммаОплаты = НоваяСтрока.Цена;
							НомерСтроки = 2;
							Для Каждого ТекущаяСтрока Из ТабличнаяЧастьДокументаОснования Цикл
								ТекущаяСтрока.НомерСтроки = НомерСтроки;
								НомерСтроки = НомерСтроки + 1;
							КонецЦикла;
							НоваяСтрока.НомерСтроки = 1;
							ТабличнаяЧастьДокументаОснования.Сортировать("НомерСтроки");
						Иначе
							// Перенумеруем позиции
							НомерСтроки = 1;
							Для Каждого ТекущаяСтрока Из ТабличнаяЧастьДокументаОснования Цикл
								ТекущаяСтрока.НомерСтроки = НомерСтроки;
								НомерСтроки = НомерСтроки + 1;
							КонецЦикла;
						КонецЕсли;
					Иначе
						// Получим сумму оплаты и сделки
						ТаблицаСделок = Ссылка.Состав.Выгрузить(, "Сделка,СуммаПриход,СуммаРасход");
						ТаблицаСделок.Свернуть("Сделка", "СуммаПриход,СуммаРасход");
						ТаблицаСделок.Сортировать("Сделка");
						
						// Подготовим данные для получение номенклатуры по сделке
						СтрукутраСделки = Новый Структура("ДокументОснование,ВалютаДокумента,КурсДокумента");
						ЗаполнитьЗначенияСвойств(СтрукутраСделки, Ссылка,, "ДокументОснование");
						
						КопияТовары = Товары.Выгрузить();
						КопияТовары.Очистить();
						
						//// Добавим строку для формирование без сделки
						//НоваяСтрока = Товары.Добавить();
						//НоваяСтрока.Номенклатура = Справочники.Номенклатура.Предоплата;
						//ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока, ЭтаФорма);
						//НоваяСтрока.СтавкаНДС = ЗагружаемыйЧек.СтавкаНДС;
						КурсДокументаОплаты = Ссылка.КурсДокумента;
						СуммаБезСделки = 0;
						
						КодыМаркировки.Очистить();
						
						СтруктураОбъекта = Новый Структура("КодыМаркировки, Товары");
						СтруктураОбъекта.КодыМаркировки = КодыМаркировки.Выгрузить();
						
						// Распределим сумму оплатц/возрата по товарам сделки
						Для Каждого ТекущаяСтрока Из ТаблицаСделок Цикл
							СтрукутраСделки.ДокументОснование = ТекущаяСтрока.Сделка;
							ТабличнаяЧастьОснования=Документы.ПриходныйКассовыйОрдер.ПолучитьТабличнуюЧастьДокументаОснования(СтрукутраСделки);
							
							// Укажем сумму сделки как предоплата
							Если ТабличнаяЧастьОснования=Неопределено Тогда
								СуммаБезСделки = СуммаБезСделки + (ТекущаяСтрока.СуммаПриход + ТекущаяСтрока.СуммаРасход) * КурсДокументаОплаты;
							Иначе
								Если ТабличнаяЧастьДокументаОснования = Неопределено Тогда
									ТабличнаяЧастьДокументаОснования = ТабличнаяЧастьОснования.Скопировать();
									ТабличнаяЧастьДокументаОснования.Очистить();
									Если ТабличнаяЧастьДокументаОснования.Колонки.Найти("СуммаОплаты") = Неопределено Тогда
										ТабличнаяЧастьДокументаОснования.Колонки.Добавить("СуммаОплаты");
									КонецЕсли;
								КонецЕсли;
								
								// Получим тип цен
								ТипЦенСделки = Неопределено;
								
								Попытка
									ТипЦенСделки = ТекущаяСтрока.Сделка.ТипЦен;
								Исключение
								КонецПопытки;
								
								Для Каждого ТекущаяСтрокаТоваров Из ТабличнаяЧастьОснования Цикл
									НоваяСтрока = КопияТовары.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрокаТоваров);
									НоваяСтрока.Цена = ТекущаяСтрокаТоваров.Цена * КурсДокументаОплаты;
									НоваяСтрока.Сумма = ТекущаяСтрокаТоваров.Сумма * КурсДокументаОплаты;
									НоваяСтрока.СуммаНДС = ТекущаяСтрокаТоваров.СуммаНДС * КурсДокументаОплаты;
									
									Если НЕ ТипЦенСделки = Неопределено И НЕ ТипЦенСделки.ЦенаВключаетНДС Тогда 
										НоваяСтрока.Цена = Окр(НоваяСтрока.Цена * (1 + НоваяСтрока.СтавкаНДС.Ставка / 100), 2);
										НоваяСтрока.Сумма = Окр(НоваяСтрока.Сумма * (1 + НоваяСтрока.СтавкаНДС.Ставка / 100), 2);
									КонецЕсли;
									НоваяСтрока.СуммаСкидки = ТекущаяСтрокаТоваров.СуммаСкидки * КурсДокументаОплаты;
									НоваяСтрока.СуммаВсего = ТекущаяСтрокаТоваров.СуммаВсего * КурсДокументаОплаты;
									НоваяСтрока.СуммаСкидкиСтроки = ТекущаяСтрокаТоваров.СуммаСкидкиСтроки * КурсДокументаОплаты;
									НоваяСтрока.СуммаСкидкиБонусами = ТекущаяСтрокаТоваров.СуммаСкидкиБонусами * КурсДокументаОплаты;
								КонецЦикла;
								
								дкЗаполнитьГТД(Ссылка.ПолучитьОбъект(),КопияТовары,ТекущаяСтрока.Сделка);
								
								// Распределим текущую сумму оплат на сделку
								Документы.ЧекНаОплату.ПроизвестиРаспределениеСуммыОплаты((ТекущаяСтрока.СуммаПриход + ТекущаяСтрока.СуммаРасход) * КурсДокументаОплаты, КопияТовары);
								
								// Заполним маркировку
								СтруктураОбъекта.Товары = КопияТовары;
								дкЗаполнитьКодыМаркировкиТоваров(СтруктураОбъекта, ТекущаяСтрока.Сделка);
								
								// Перенесем в Товары
								Для Каждого ТекущаяСтрокаНоменклатуры Из КопияТовары Цикл
									ЗаполнитьЗначенияСвойств(ТабличнаяЧастьДокументаОснования.Добавить(), ТекущаяСтрокаНоменклатуры);
								КонецЦикла;
								
								// Перенесем в КодыМаркировки
								Для Каждого ТекущаяСтрокаМаркировки Из СтруктураОбъекта.КодыМаркировки Цикл
									ЗаполнитьЗначенияСвойств(КодыМаркировки.Добавить(), ТекущаяСтрокаМаркировки);
								КонецЦикла;
								
								КопияТовары.Очистить();
								
							КонецЕсли;
							
						КонецЦикла;
						
						Если ТабличнаяЧастьДокументаОснования <> Неопределено И СуммаБезСделки <> 0 Тогда
							НоваяСтрока = ТабличнаяЧастьДокументаОснования.Добавить();
							НоваяСтрока.Номенклатура = Справочники.Номенклатура.Предоплата;
							ОбработкаРеквизита("Товары.Номенклатура", НоваяСтрока);
							НоваяСтрока.СтавкаНДС = Ссылка.СтавкаНДС;
							НоваяСтрока.Цена = СуммаБезСделки;
							ОбработкаРеквизита("Товары.Цена", НоваяСтрока);
							НоваяСтрока.СуммаОплаты = НоваяСтрока.Цена;
							НомерСтроки = 2;
							Для Каждого ТекущаяСтрока Из ТабличнаяЧастьДокументаОснования Цикл
								ТекущаяСтрока.НомерСтроки = НомерСтроки;
								НомерСтроки = НомерСтроки + 1;
							КонецЦикла;
							НоваяСтрока.НомерСтроки = 1;
							ТабличнаяЧастьДокументаОснования.Сортировать("НомерСтроки");
						Иначе
							// Перенумеруем позиции
							НомерСтроки = 1;
							Для Каждого ТекущаяСтрока Из ТабличнаяЧастьДокументаОснования Цикл
								ТекущаяСтрока.НомерСтроки = НомерСтроки;
								НомерСтроки = НомерСтроки + 1;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если УстановленРежим("ЧекКоррекции") И ТабличнаяЧастьДокументаОснования = Неопределено И Ссылка.Товары.Количество() = 0 Тогда
					ПечатьТабличнойЧастиТовары=ЛОЖЬ;
				КонецЕсли;

				Если НЕ ПечатьТабличнойЧастиТовары Тогда
					ПечатьТабличнойЧастиНаЧек=Ложь;
					// Формирование таблицы товаров (пока из одной строки)
					SafeArrayТаблицаТоваров=Рарус_Компонента.СоздатьПараметры(1,43);
					НомерСтрокиТоваров=0;
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,0,1); //Номер строки ТЧ
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,1,ПараметрыШапки.ОсновнойОтдел); // N Отдела берем из настроек рабочего места
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,2,""); // Артикул
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,3, ?(УстановленРежим("РасходныйКассовыйОрдер"), "Возврат по документу", "Оплата по документу"));
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,4,ПараметрыШапки.Всего); //Цена товара
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,5,1); // Количество товара т.к. ФР могут не понимать нулевое количество то придется всегда ставить =1 (услуга такая-то в кол-ве 1 штука)
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,6,ПараметрыШапки.Всего); //Сумма товара
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,7,""); //Наименование скидки (для строки нет наименования скидки) ???
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,8,0); //Сумма скидки на товар нет, она на весь чек
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,9,0); //Процент скидки на товар нет, она на весь чек
					// вместо характеристики передаем информацию о документе
					ДокОснование=?(обЗначениеНеЗаполнено(ДокументОснование),Ссылка,ДокументОснование);
					СтрТМП=": "+ДокОснование.Метаданные().Синоним+" ";
					СтрТМП=СтрТМП+"№"+СокрЛП(ДокОснование.Номер)+" от "+Формат(ДокОснование.Дата,"ДФ=dd.MM.yy");
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров,10,СтрТМП);
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 11, 0);  // Сумма налога 1
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 12, 0);  // тип позиции (0-товар, 1-модификатор товара)
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 13, 0);  // Ставка налога 1 в %
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 14, -1); // Номер налоговой группы 1 в ФР
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 15, 0);  // Сумма налога 2
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 16, 0);  // Ставка налога 2 в %
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 17, -1); // Номер налоговой группы 2 в ФР
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 18, ПараметрыШапки.СуммаСкидкиНаЧек);  // Сумма распределенной скидки
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 19, ПризнакСпособаРасчета); // Признак способа расчета
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 20, 10); // Признак предмета расчета
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 21, 1);  // Количество упаковок
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 22,"Платеж");  // Единица измерения предмета расчета
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 23, ""); // ГТД представление
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 24, ""); // код станы происхождения товара
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 25, -1);  // Признак агента по предмету расчета
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 26,"");  // Телефон поставщика
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 27, ""); // Наименование поставщика
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 28, ""); // ИНН поставщика
					
					Если НЕ ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Постоплата Тогда
						Если УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер")
							ИЛИ УстановленРежим("БанковскаяВыписка") Тогда
							SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 11, Товары[0].СуммаНДС);  // Сумма налога 1
							Если ЗначениеЗаполнено(Товары[0].СтавкаНДС) Тогда
								НомерНалоговойГруппыФР = ПолучитьНомерНалоговойГруппыДляПечати(ТипСпособаРасчетаККТ, Товары[0].СтавкаНДС);
								SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 13, Товары[0].СтавкаНДС.Ставка);  // Ставка налога 1 в %
								SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 14, НомерНалоговойГруппыФР); // Номер налоговой группы 1 в ФР
							КонецЕсли; 
						ИначеЕсли УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат") ИЛИ УстановленРежим("ЧекКоррекции") Тогда
							SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 11, ПараметрыШапки.СуммаНДС);  // Сумма налога 1
							Если ЗначениеЗаполнено(ПараметрыШапки.СтавкаНДС) Тогда
								НомерНалоговойГруппыФР = ПолучитьНомерНалоговойГруппыДляПечати(ТипСпособаРасчетаККТ, ПараметрыШапки.СтавкаНДС);
								SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 13, ПараметрыШапки.СтавкаНДС.Ставка);  // Ставка налога 1 в %
								SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 14, НомерНалоговойГруппыФР); // Номер налоговой группы 1 в ФР
							КонецЕсли; 
						КонецЕсли;
					КонецЕсли;
					
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 29, -1);  // Тип маркировки
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 30, ""); // GTIN
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 31, ""); // Серийный номер
					
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 32, ""); // Код маркировки
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 33, 0); // Мера количества предмета расчета
					
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 34, 1); //ДробноеКоличествоМаркированногоТовара.Числитель); // Дробное количество маркированного товара (числитель)
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 35, 1); //ДробноеКоличествоМаркированногоТовара.Знаменатель); // Дробное количество маркированного товара (знаменатель)
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 36, 255); // Результат проверки сведений о товаре (тег 2106)
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 37, 1); // Присвоенный статус товара (тег 2110)
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 38, Неопределено); // Идентификатор ФОИВ
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 39, Неопределено); // Дата документа основания
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 40, Неопределено); // Номер документа основания
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 41, Неопределено); // Значение отраслевого реквизита
					
					SafeArrayТаблицаТоваров.SetValue(НомерСтрокиТоваров, 42, 0);
				КонецЕсли; 
			КонецЕсли; 
				
			Если ПечатьТабличнойЧастиНаЧек Тогда
				ТоварыКопия=Товары.Выгрузить();
				
				//Формирование таблицы товаров
				Если ТабличнаяЧастьДокументаОснования=Неопределено Тогда
					ТабличнаяЧастьДокументаОснования=Товары.Выгрузить();
				КонецЕсли;
				
				Если ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
					Если НЕ РазбитьСтрокиПоКодамМаркировки(ТабличнаяЧастьДокументаОснования, КодыМаркировки, ТекстОшибки) Тогда
						Возврат Ложь;
					КонецЕсли;
				КонецЕсли;
				ДобавитьСтрокиМежценовойРазницы(ТабличнаяЧастьДокументаОснования);
				
				врТаблицаТоваров = ДайТаблицуТоваров();
				Для Каждого СтрокаТоваров Из ТабличнаяЧастьДокументаОснования Цикл
					
					Если ТипСпособаРасчетаККТ <> Перечисления.ТипыСпособаРасчетаККТ.Передача И СтрокаТоваров.СуммаОплаты = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					врСтрокаТоваров = врТаблицаТоваров.Добавить();
					
					Если ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
						Если СтрокаТоваров.Номенклатура.Пустая() Тогда
							ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПолучитьКодПризнакаПредметаРасчета(Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата);
							Наименование = "Межценовая разница";
						Иначе
							
							Попытка
								ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПолучитьКодПризнакаПредметаРасчета(СтрокаТоваров.ПризнакПредметаРасчета);
								// Бизтех Коваль 18.07.2025 ++
								Если ПризнакПредметаРасчета = 13 Тогда
									ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПолучитьКодПризнакаПредметаРасчета(СтрокаТоваров.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета);
								КонецЕсли;
								// Бизтех Коваль 18.07.2025 --
							Исключение
								ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПолучитьКодПризнакаПредметаРасчета(СтрокаТоваров.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета);
							КонецПопытки;
							Если НЕ ЗначениеЗаполнено(ПризнакПредметаРасчета) Тогда
								ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПолучитьКодПризнакаПредметаРасчета(СтрокаТоваров.Номенклатура.ТипНоменклатуры.ПризнакПредметаРасчета);
							КонецЕсли;
							Наименование = Строка(СтрокаТоваров.Номенклатура.Наименование);
						КонецЕсли;
						ЕдиницаИзмеренияПредметаРасчета = Строка(СтрокаТоваров.ЕдиницаИзмерения.Наименование);
						Цена = СтрокаТоваров.Цена;
						КоличествоСУчетомКоэффициента = Окр(СтрокаТоваров.Количество*СтрокаТоваров.Коэффициент, 3);
						Сумма = СтрокаТоваров.Сумма;
						Если НЕ УстановленРежим("БанковскаяВыписка") И ((ТипЦенОснования = Неопределено И НЕ ТипЦен.ЦенаВключаетНДС)
							 ИЛИ (НЕ ТипЦенОснования = Неопределено И НЕ ТипЦенОснования.ЦенаВключаетНДС))
							 И НЕ (ТабличнаяЧастьДокументаОснования.Колонки.Найти("ЦенаПерерасчитана") <> Неопределено И СтрокаТоваров.ЦенаПерерасчитана)Тогда
							Цена = Окр(Цена * (1 + СтрокаТоваров.СтавкаНДС.Ставка / 100), 2);
							Сумма = Окр(Сумма * (1 + СтрокаТоваров.СтавкаНДС.Ставка / 100), 2);
						КонецЕсли;
						СуммаНДС = СтрокаТоваров.СуммаНДС;
						НомерНалоговойГруппыФР = ПолучитьНомерНалоговойГруппыДляПечати(ТипСпособаРасчетаККТ, СтрокаТоваров.СтавкаНДС, СтрокаТоваров.СебестоимостьАвтомобиля);
						Ставка = СтрокаТоваров.СтавкаНДС.Ставка;
						СуммаСкидкиСтроки = Число(СтрокаТоваров.СуммаСкидкиСтроки);
						СуммаСкидки = Число(СтрокаТоваров.СуммаСкидки);
						СуммаСкидкиБонусами = Число(СтрокаТоваров.СуммаСкидкиБонусами);
					Иначе
						Если СтрокаТоваров.Номенклатура.Пустая() Тогда
							ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.ПолучитьКодПризнакаПредметаРасчета(Перечисления.ПризнакиПредметаРасчета.ПлатежВыплата);
							Наименование = "Межценовая разница";
						Иначе
							ПризнакПредметаРасчета = 10; // Платеж/выплата для предоплаты/постоплаты
							Наименование = Строка(СтрокаТоваров.Номенклатура.Наименование) + " платеж за " + Окр(СтрокаТоваров.Количество, 3)
							+ Символы.НПП + Строка(СтрокаТоваров.ЕдиницаИзмерения.Наименование) + "";
						КонецЕсли;
						ЕдиницаИзмеренияПредметаРасчета = "Платеж";
						Цена = СтрокаТоваров.СуммаОплаты;
						КоличествоСУчетомКоэффициента = 1;
						Сумма = СтрокаТоваров.СуммаОплаты;
						НомерНалоговойГруппыФР = ПолучитьНомерНалоговойГруппыДляПечати(ТипСпособаРасчетаККТ, СтрокаТоваров.СтавкаНДС, СтрокаТоваров.СебестоимостьАвтомобиля);
						Ставка = СтрокаТоваров.СтавкаНДС.Ставка;
						СуммаСкидкиСтроки = 0;
						СуммаСкидки = 0;
						СуммаСкидкиБонусами = 0;
						Если ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Предоплата И Товары.Итог("СуммаВсего") <> Оплаты.Итог("Сумма") Тогда
							СуммаНДС = Окр(СтрокаТоваров.СуммаОплаты - СтрокаТоваров.СуммаОплаты / (1 + СтрокаТоваров.СтавкаНДС.Ставка / 100), 2);
						ИначеЕсли ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Постоплата Тогда
							СуммаНДС = 0;
							Ставка = Справочники.СтавкиНДС.БезНДС.Ставка;
							НомерНалоговойГруппыФР = Справочники.СтавкиНДС.БезНДС.НомерНалоговойГруппыФР;
						Иначе
							СуммаНДС = СтрокаТоваров.СуммаНДС;
						КонецЕсли;
					КонецЕсли;
					
					врСтрокаТоваров.НомерСтроки = Число(СтрокаТоваров.НомерСтроки); //Номер строки ТЧ
					Если ЗначениеЗаполнено(СтрокаТоваров.МестоРазмещения) Тогда
						врСтрокаТоваров.НомерОтдела = ПолучитьНомерОтдела(СтрокаТоваров.МестоРазмещения); // N Отдела
					Иначе
						врСтрокаТоваров.НомерОтдела = ПараметрыШапки.ОсновнойОтдел; // N Отдела
					КонецЕсли;
					врСтрокаТоваров.Артикул = СтрокаТоваров.Номенклатура.Артикул; // Артикул
					врСтрокаТоваров.Наименование = Наименование; //Наименование товара
					врСтрокаТоваров.Цена = Число(Цена); //Цена товара
					врСтрокаТоваров.Количество = КоличествоСУчетомКоэффициента; // Количество товара
					врСтрокаТоваров.Сумма = Число(Сумма); //Сумма товара
					врСтрокаТоваров.НаименованиеСкидки = Строка(СтрокаТоваров.СкидкаНаТовар.Наименование); //Наименование скидки для строки
					врСтрокаТоваров.СуммаСкидкиСтроки = СуммаСкидкиСтроки; //Сумма скидки на товар нет, она на весь чек
					врСтрокаТоваров.ПроцентСкидкиСтроки = Число(СтрокаТоваров.ПроцентСкидкиСтроки); //Процент скидки на товар нет, она на весь чек
					врСтрокаТоваров.ХарактеристикаНоменклатуры = ?(обЗначениеНеЗаполнено(СтрокаТоваров.ХарактеристикаНоменклатуры),"",
						Строка(СтрокаТоваров.ХарактеристикаНоменклатуры)); // характеристика номенклатуры 
					врСтрокаТоваров.СуммаНДС = СуммаНДС;                                    // Сумма налога 1
					врСтрокаТоваров.ТипПозиции = 0;                                           // тип позиции (0-товар, 1-модификатор товара)
					врСтрокаТоваров.СтавкаНДС = Ставка;                                      // Ставка налога 1 в %
					врСтрокаТоваров.НомерНалоговойГруппыФР = НомерНалоговойГруппыФР;                      // Номер налоговой группы 1 в ФР
					врСтрокаТоваров.СуммаНалога2 = 0;                                           // Сумма налога 2
					врСтрокаТоваров.СтавкаНалога2 = 0;                                           // Ставка налога 2 в %
					врСтрокаТоваров.НомерНалоговойГруппыФР2 = -1;                                          // Номер налоговой группы 2 в ФР
					врСтрокаТоваров.СуммаРаспределеннойСкидки = СуммаСкидки+СуммаСкидкиБонусами; // Сумма распределенной скидки
					врСтрокаТоваров.ПризнакСпособаРасчета = ПризнакСпособаРасчета; // Признак способа расчета
					Если ВерсияФФД < 120 Тогда
						Если ПризнакПредметаРасчета = 27 Тогда
							 ПризнакПредметаРасчета = 12;
						КонецЕсли;
						Если ПризнакПредметаРасчета = 30 ИЛИ
							 ПризнакПредметаРасчета = 31 ИЛИ
							 ПризнакПредметаРасчета = 32 ИЛИ
							 ПризнакПредметаРасчета = 33 Тогда
							 ПризнакПредметаРасчета = 2;
						КонецЕсли;
					КонецЕсли;
					врСтрокаТоваров.ПризнакПредметаРасчета = ПризнакПредметаРасчета; // Признак предмета расчета
					врСтрокаТоваров.КоличествоУпаковок = СтрокаТоваров.Количество; // Количество упаковок
					врСтрокаТоваров.ЕдиницаИзмеренияПредметаРасчета = ЕдиницаИзмеренияПредметаРасчета; // Единица измерения предмета расчета
					
					// Реквизиты "код страны происхождения товара" (тег 1230) и "номер таможенной декларации" (тег 1231) включаются
					// в состав кассового чека (БСО) в случае, если страной происхождения товаров не является Российская Федерация.
					Если КонтрагентЮрЧп И НЕ обЗначениеНеЗаполнено(СтрокаТоваров.ГТД) И НЕ СтрокаТоваров.ГТД.Страна.Код = "643" Тогда
						врСтрокаТоваров.ГТДПредставление = СтрокаТоваров.ГТД.Наименование; // ГТД представление
						врСтрокаТоваров.КодСтраныПроисхожденияТоваров = СтрокаТоваров.ГТД.Страна.Код; // Код страны происхождения товара
					Иначе
						врСтрокаТоваров.ГТДПредставление = ""; // ГТД представление
						врСтрокаТоваров.КодСтраныПроисхожденияТоваров = ""; // Код страны происхождения товара
					КонецЕсли;
					
					// Заполняем реквизиты платежного агента
					ПризнакАгента = -1;
					ТелефонПоставщика = "";
					НаименованиеПоставщика = "";
					ИННПоставщика = "";
					
					Если НЕ обЗначениеНеЗаполнено(СтрокаТоваров.ДоговорВзаиморасчетов)
						И НЕ обЗначениеНеЗаполнено(СтрокаТоваров.ДоговорВзаиморасчетов.ПризнакАгента) Тогда
						ПризнакАгента = Перечисления.ПризнакиАгента.КодПризнакаАгента(СтрокаТоваров.ДоговорВзаиморасчетов.ПризнакАгента);
						ТелефонПоставщика = СтрокаТоваров.ДоговорВзаиморасчетов.ТелефонПоставщика;
						НаименованиеПоставщика = СтрокаТоваров.ДоговорВзаиморасчетов.НаименованиеПоставщика;
						ИННПоставщика = СтрокаТоваров.ДоговорВзаиморасчетов.ИННПоставщика;
					КонецЕсли;
					
					врСтрокаТоваров.ПризнакАгента = ПризнакАгента; // Признак агента по предмету расчета
					врСтрокаТоваров.ТелефонПоставщика = ТелефонПоставщика; // Телефон поставщика
					врСтрокаТоваров.НаименованиеПоставщика = НаименованиеПоставщика; // Наименование поставщика
					врСтрокаТоваров.ИННПоставщика = ИННПоставщика; // ИНН поставщика
					
					ТипМаркировки = -1;
					GTIN = "";
					СерийныйНомер = "";
					// Передадим маркировку товара только при передаче товара
					Если ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача 
						И НЕ СтрокаТоваров.Номенклатура.Пустая() И ЗначениеЗаполнено(СтрокаТоваров.GTIN)
						И ЗначениеЗаполнено(СтрокаТоваров.СерийныйНомер) Тогда
						GTIN = СтрокаТоваров.GTIN;
						СерийныйНомер = СтрокаТоваров.СерийныйНомер;
						ТипМаркировки = 1;
					КонецЕсли;
					
					врСтрокаТоваров.ТипМаркировки = ТипМаркировки; // Тип маркировки
					врСтрокаТоваров.GTIN = GTIN; // GTIN
					врСтрокаТоваров.СерийныйНомер = СерийныйНомер; // Серийный номер
					
					Если ВерсияФФД >= 120 Тогда 
						Если ТипСпособаРасчетаККТ = Перечисления.ТипыСпособаРасчетаККТ.Передача Тогда
							// Новые параметры для ФФД 1.2
							врСтрокаТоваров.КодМаркировки = СтрокаТоваров.КодМаркировки; // Код маркировки
							МераКоличестваПредметаРасчетаККТКод = Перечисления.МераКоличестваПредметаРасчетаККТ.КодМерыКоличестваПредметаРасчетаККТ(СтрокаТоваров.МераКоличестваПредметаРасчетаККТ);
							врСтрокаТоваров.МераКоличестваПредметаРасчетаККТКод = МераКоличестваПредметаРасчетаККТКод; // Мера количества предмета расчета
							// получить числитель/знаменатель по строке
							СтруктураРасчетаДробногоКоличества = Новый Структура("Номенклатура, ЕдиницаИзмерения, Коэффициент, Количество",
								СтрокаТоваров.Номенклатура,
								СтрокаТоваров.ЕдиницаИзмерения,
								СтрокаТоваров.Коэффициент,
								СтрокаТоваров.Количество);
							// Временно убрали передачу знаменателя и числителя в оборудование - считаем, что они равны 1
							ДробноеКоличествоМаркированногоТовара = Справочники.ЕдиницыИзмерения.ДробноеКоличествоМаркированногоТовара(СтруктураРасчетаДробногоКоличества);
							врСтрокаТоваров.ДробноеКоличествоМаркированногоТовараЧислитель = 1; //ДробноеКоличествоМаркированногоТовара.Числитель); // Дробное количество маркированного товара (числитель)
							врСтрокаТоваров.ДробноеКоличествоМаркированногоТовараЗнаменатель =1; //ДробноеКоличествоМаркированногоТовара.Знаменатель); // Дробное количество маркированного товара (знаменатель)
							врСтрокаТоваров.РезультатПроверкиСведенийОТоваре = СтрокаТоваров.РезультатПроверкиСведенийОТоваре; // Результат проверки сведений о товаре (тег 2106)
							ПланируемыйСтатусМаркируемогоТовара = Перечисления.ПланируемыйСтатусМаркируемогоТовара.КодПланируемыйСтатусМаркируемогоТовара(СтрокаТоваров.ПланируемыйСтатусМаркируемогоТовара);
							врСтрокаТоваров.ПланируемыйСтатусМаркируемогоТовара = ПланируемыйСтатусМаркируемогоТовара; // Присвоенный статус товара (тег 2110)
							врСтрокаТоваров.ИдентификаторФОИВ = Неопределено; // Идентификатор ФОИВ
							врСтрокаТоваров.ДатаДокументаОснования = Неопределено; // Дата документа основания
							врСтрокаТоваров.НомерДокументаОснования = Неопределено; // Номер документа основания
							врСтрокаТоваров.ЗначениеОтраслевогоРеквизита = Неопределено; // Значение отраслевого реквизита
						КонецЕсли;
					КонецЕсли;
					
					СуммаАкциза = -1;
					// Контроль суммы акциза подакцизных товаров
					Если ПризнакПредметаРасчета = 2 ИЛИ
						 ПризнакПредметаРасчета = 30 ИЛИ
						 ПризнакПредметаРасчета = 31 Тогда
						 
						 Если ЗначениеЗаполнено(ПараметрыШапки.Получатель) И
							  ЗначениеЗаполнено(ПараметрыШапки.ПолучательИНН) Тогда
							  // Пока сумма акциза для подакцизных товаров для юрлиц = 0.
							  // Внимание. Сумма акциза передается в копейках.
							  СуммаАкциза = 0;
						 Иначе
							  СуммаАкциза = 0;
						 КонецЕсли;
					Иначе
						 СуммаАкциза = -1;
					КонецЕсли;
					врСтрокаТоваров.СуммаАкциза = СуммаАкциза;
				КонецЦикла;
				
				Товары.Загрузить(ТоварыКопия);
				
				ПровестиФорматноЛогическийКонтрольТоваров(врТаблицаТоваров);
				КоличествоСтрокТоваров = врТаблицаТоваров.Количество();
				SafeArrayТаблицаТоваров=Рарус_Компонента.СоздатьПараметры(КоличествоСтрокТоваров,43);
				ЗаполнитьТаблицуТоваров(SafeArrayТаблицаТоваров, врТаблицаТоваров);
			
			КонецЕсли;
			
			SafeArrayПараметрыЧека.SetValue(1,0,SafeArrayТаблицаТоваров);
			
			Если Оплаты.Количество()=0 Тогда
				НоваяСтрокаОплат=Оплаты.Добавить();
				НоваяСтрокаОплат.ТипОплаты=Справочники.ТипыОплат.Наличные;
				НоваяСтрокаОплат.Сумма=0;
			КонецЕсли;	
			
			тзОплаты = Оплаты.Выгрузить();
			
			Если РежимПередачиТовараСЗакрытиемОстаткаОплатыНаКредит() Тогда
				
				ОсталосьЗакрытьНаКредит = ПараметрыШапки.Итог - ПараметрыШапки.ВсегоПоДокументу;
				Если ОсталосьЗакрытьНаКредит > 0 Тогда
					
					СтрокаОплатКредит = тзОплаты.Найти(Справочники.ТипыОплат.ПостоплатаКредит);
					Если СтрокаОплатКредит = Неопределено Тогда
						СтрокаОплатКредит=тзОплаты.Добавить();
						СтрокаОплатКредит.ТипОплаты=Справочники.ТипыОплат.ПостоплатаКредит;				
					КонецЕсли;					
					СтрокаОплатКредит.Сумма=СтрокаОплатКредит.Сумма + ОсталосьЗакрытьНаКредит;	
					
				КонецЕсли;
				
			КонецЕсли;
			
			КоличествоОплат=0;
	
			SafeArrayТаблицаОплат = Рарус_Компонента.СоздатьПараметры(тзОплаты.Количество(),4);
			Для Каждого СтрокаОплат из тзОплаты Цикл
				СтрНомер = тзДоступныеВидыОплат.Найти(СтрокаОплат.ТипОплаты,"ТипОплаты");
				Если СтрНомер=Неопределено Тогда
					ТекстОшибки = "Недопустимый тип оплаты <"+СтрокаОплат.ТипОплаты+">";
					ЗаписатьВФайлЛога("Пробить чек", 1, "ОШИБКА ПРИ ПРОБИТИИ ЧЕКА: "+СтрЗаменить(ТекстОшибки,Символы.ПС," "),,1);
					Возврат ЛОЖЬ;
				КонецЕсли;
				SafeArrayТаблицаОплат.SetValue(КоличествоОплат,0,СтрНомер.Номер); //Тип оплаты - наличными
				SafeArrayТаблицаОплат.SetValue(КоличествоОплат,1,Строка(СтрокаОплат.ТипОплаты.Наименование)); //Параметр из таблицы ФР
				SafeArrayТаблицаОплат.SetValue(КоличествоОплат,2,СтрокаОплат.Сумма); //Сумма оплаты
				SafeArrayТаблицаОплат.SetValue(КоличествоОплат,3,Строка(СтрокаОплат.ТипОплаты.Наименование)); //Комментарий		
				КоличествоОплат = КоличествоОплат+1;
			КонецЦикла;
			
			SafeArrayПараметрыЧека.SetValue(2,0,SafeArrayТаблицаОплат);
			
			ТаблицаОплат = Неопределено;
			РезультатОплаты = ПреобразоватьТаблицуОплат(ТекстОшибки, ТаблицаОплат, тзОплаты);
			Если НЕ РезультатОплаты Тогда
				Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
					Сообщить(ТекстОшибки, СтатусСообщения.ОченьВажное);
				КонецЕсли;
				Возврат РезультатОплаты;
			КонецЕсли;
			
			SafeArrayТаблицаОплатККТ = Рарус_Компонента.СоздатьПараметры(1,5);
			Для КодОплаты = 0 По 4 Цикл
				Сумма = ТаблицаОплат.Получить(КодОплаты);
				Если Сумма = Неопределено Тогда
					Сумма = 0;
				КонецЕсли;
				SafeArrayТаблицаОплатККТ.SetValue(0, КодОплаты, Сумма); //Сумма оплаты
			КонецЦикла;
			SafeArrayПараметрыЧека.SetValue(3,0,SafeArrayТаблицаОплатККТ);
			
			// Нужно для отладки
			//Ответ = ОткрытьДиалогВопросаРасш("Чек пробился?", "Да+Нет");
			//Пробитие чека
			Если ВыполнитьКомандуОборудования("ФР",GUID_ФР,"ПробитьЧек",SafeArrayПараметрыЧека,ТаймаутФР,ТекстОшибки) Тогда
			//Если Ответ = "Да" Тогда
				Результат=Истина; // Ура! Чек пробили!
				флПробитьЧек = Ложь;
			ИначеЕсли АвторизацийБПЕсть Тогда 
				Ответ = ОткрытьДиалогВопросаРасш("Произошла ошибка при пробитии чека!" +Символы.ПС+
									"Попробовать пробить чек еще раз?"+Символы.ПС+
									"Нет - будет произведена отмена авторизаций безналичных платежей!", 
									"Да+Нет");
				Если Ответ = "Нет" Тогда

					флПробитьЧек = Ложь;
					//ОткрытьДиалог("Произошла ошибка при пробитии чека!
					//			  |Будет произведена отмена авторизаций безналичных платежей!", СтатусСообщения.ОченьВажное);

					//При ошибке ФР нужно сразу же вернуть/отменить только что проведенную транзакцию по карте взад!
					ВыполнитьАвторизациюБП(Истина);
				КонецЕсли;
			Иначе
				// если у нас нет безналичных платежей то не будем задавать вопроса о повторном пробитии чека
				флПробитьЧек = Ложь; 
			КонецЕсли;
		Исключение
			ТекстОшибки="Ошибка при пробитии чека: "+ОписаниеОшибки();
		КонецПопытки;
	КонецЦикла;

	//Теперь подставим реальные фискальные реквизиты чека, если это не копия
	Если Результат И (УстановленРежим("Чек") ИЛИ УстановленРежим("ЧекНаВозврат") ИЛИ
		 УстановленРежим("ЧекНаОплату") ИЛИ УстановленРежим("ЧекНаОплатуВозврат") ИЛИ 
		 УстановленРежим("ПриходныйКассовыйОрдер") ИЛИ УстановленРежим("РасходныйКассовыйОрдер") ИЛИ
		 УстановленРежим("БанковскаяВыписка")ИЛИ УстановленРежим("ЧекКоррекции")) Тогда
		Попытка
			КоличествоСтрок=0; КоличествоСтолбцов=0;
			SafeArrayПараметрыЧека.GetBounds(КоличествоСтрок,КоличествоСтолбцов);
			НомерСмены=Число(SafeArrayПараметрыЧека.GetValue(0,0));
			НомерЧека=Число(SafeArrayПараметрыЧека.GetValue(1,0));
			ДатаФР=Вычислить("'"+SafeArrayПараметрыЧека.GetValue(3,0)+"'");
			НомерДокумента=Число(SafeArrayПараметрыЧека.GetValue(2,0));
			Если КоличествоСтрок > 4 Тогда 
				ФискальныйПризнак=SafeArrayПараметрыЧека.GetValue(4,0);
			Иначе
				ФискальныйПризнак="";
            КонецЕсли;
			СтруктураВозвратныхПараметров=Новый Структура("ДатаФР,НомерЧека,НомерДокумента,НомерСмены,ФискальныйПризнак",ДатаФР,НомерЧека,НомерДокумента,НомерСмены, ФискальныйПризнак);
		Исключение
			ТекстОшибки="Ошибка получения фискальных реквизитов пробитого чека: "+ОписаниеОшибки();
		КонецПопытки; 
	КонецЕсли;
	Если РежимДиагностики И НЕ ПустаяСтрока(ТекстОшибки) И (ТекстОшибки<>"ОК") Тогда
		ЗаписьЖурналаРегистрации("ФронтКассира.ПробитьЧек",?(Лев(ТекстОшибки,2)="ОК",УровеньЖурналаРегистрации.Предупреждение,УровеньЖурналаРегистрации.Ошибка),,,СтрЗаменить(ТекстОшибки,Символы.ПС," "));
	КонецЕсли;

	Если НЕ ПустаяСтрока(ТекстОшибки) И (ТекстОшибки<>"ОК") Тогда
		ЗаписатьВФайлЛога("Пробить чек", 1, СтрЗаменить(ТекстОшибки,Символы.ПС," "),,1);
	Иначе
		ЗаписатьВФайлЛога("Пробить чек", 0, "Чек №" + НомерЧека + " пробит");
	КонецЕсли;
	
	Если (НЕ УстановленРежим("Копия")) И (УстановленРежим("ПриходныйКассовыйОрдер")) Тогда
		Если НЕ ПробитьЧекИнкассации(Ложь,ПараметрыШапки.ВсегоПоДокументу) Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	// Вернем "наверх" результат операции	
	Возврат Результат;
КонецФункции // ПробитьЧек()

// Функция пробития чеков внесения/изъятия в кассу ККМ
Функция ПробитьЧекИнкассации(ЧекВнесения,СуммаИнкассации)
	//Пробьем чек внесения/изъятия в кассу ККМ
	SafeArrayПараметрыИнкассации=Рарус_Компонента.СоздатьПараметры(3,1);
	Если ЧекВнесения Тогда
		SafeArrayПараметрыИнкассации.SetValue(0,0,1); //Тип инкассация - внесение
	Иначе
		SafeArrayПараметрыИнкассации.SetValue(0,0,0); //Тип инкассация - изъятие
	КонецЕсли; 
	SafeArrayПараметрыИнкассации.SetValue(2,0,ПараметрыСеанса.Пользователь.ПарольККМ);
	SafeArrayТаблицаОплат=Рарус_Компонента.СоздатьПараметры(1,4);
	СтрНомер = тзДоступныеВидыОплат.Найти(Справочники.ТипыОплат.Наличные,"ТипОплаты");
	Если СтрНомер = Неопределено Тогда 
		ТекстОшибки = "ОШИБКА ПРИ ИНКАССАЦИИ:"+Символы.ПС+"Не определен тип оплаты "+Справочники.ТипыОплат.Наличные+". Проверите настройки ФР!";
		//ЗаписьЖурналаРегистрации("ФронтКассира.ПробитьЧек",?(Лев(ТекстОшибки,2)="ОК",УровеньЖурналаРегистрации.Предупреждение,УровеньЖурналаРегистрации.Ошибка),,,СтрЗаменить(ТекстОшибки,Символы.ПС," "));
		ЗаписатьВФайлЛога("Пробить чек", 1, СтрЗаменить(ТекстОшибки,Символы.ПС," "),,1);
		Возврат Ложь;
	КонецЕсли;
	SafeArrayТаблицаОплат.SetValue(0,0,СтрНомер.Номер); //Тип оплаты - наличными
	SafeArrayТаблицаОплат.SetValue(0,1,Строка(Справочники.ТипыОплат.Наличные.Наименование)); //Параметр из таблицы ФР
	SafeArrayТаблицаОплат.SetValue(0,2,СуммаИнкассации); //Сумма инкассации
	Если ЧекВнесения Тогда
		SafeArrayТаблицаОплат.SetValue(0,3,("Внесение "+Строка(Справочники.ТипыОплат.Наличные))); //Комментарий
	Иначе
		SafeArrayТаблицаОплат.SetValue(0,3,("Изъятие "+Строка(Справочники.ТипыОплат.Наличные))); //Комментарий
	КонецЕсли; 

	SafeArrayПараметрыИнкассации.SetValue(1,0,SafeArrayТаблицаОплат);
	КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"Инкассировать",SafeArrayПараметрыИнкассации,ТаймаутФР);
	Если КодОшибки <> 0 Тогда
		ТекстОшибки = "Ошибка при выполнении Инкассации на ФРе " + ФР.Наименование +Символы.ПС + Рарус_Компонента.ОписаниеОшибки;
		ЗаписьЖурналаРегистрации("ФронтКассира.ПробитьЧек",?(Лев(ТекстОшибки,2)="ОК",УровеньЖурналаРегистрации.Предупреждение,УровеньЖурналаРегистрации.Ошибка),,,СтрЗаменить(ТекстОшибки,Символы.ПС," "));
		//ЗаписатьВФайлЛога("Пробить чек", 1, СтрЗаменить(ТекстОшибки,Символы.ПС," "),,1);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции // ПробитьЧекИнкассации()

// Пробитие чека коррекции
Функция ПробитьЧекКоррекции(ЧекКоррекции, ТекстОшибки="", СтруктураВозвратныхПараметров=Неопределено, ПараметрыДокумента = Неопределено) Экспорт
	
	Результат = Ложь;
	ТекстОшибки = "";
	
	НефискальныйЧек = Ложь;
	
	СформированнаяСтрока = "";
	
	флПробитьЧек = Истина;
	
	Если ПараметрыДокумента = Неопределено Тогда
		ПараметрыДокумента = ПодготовитьПараметрыДляПробитияЧекаКоррекции(ЧекКоррекции);
	КонецЕсли;
	
	Пока флПробитьЧек Цикл
		
		ПрефиксЖурнала = "ФронтКассира.ПробитьЧекКоррекции";
		Попытка
			SafeArrayПараметрыЧека=Рарус_Компонента.СоздатьПараметры(20,1);
			SafeArrayПараметрыЧека.SetValue(0,0,ПараметрыДокумента.ТипРасчета);                    //Тип чека - чек прихода (1)/возрат прихода (2)/расхода (3)/возврат расхода (4)
			SafeArrayПараметрыЧека.SetValue(1,0,ПараметрыДокумента.СистемаНалогообложения);        //Система налогооблажения - УСНДоход (1)/ УСНДоходРасход (2)/ ЕНВД (3)/ ЕСН (4)/ Патент (5)/ Остальное (0)
			SafeArrayПараметрыЧека.SetValue(2,0,ПараметрыДокумента.Кассир);                        //Кассир - наименование ответственного за чек коррекции
			SafeArrayПараметрыЧека.SetValue(3,0,ПараметрыДокумента.КассирИНН);                     //ИНН кассира - всегда пустое
			SafeArrayПараметрыЧека.SetValue(4,0,ПараметрыДокумента.ТипКоррекции);                  //Тип коррекции: 0 - Самостоятельная операция; 1 - Операция по предписанию
			SafeArrayПараметрыЧека.SetValue(5,0,ПараметрыДокумента.НаименованиеОснования);         //Наименвоание основания коррекции
			SafeArrayПараметрыЧека.SetValue(7,0,ПараметрыДокумента.ДатаДокументаОснования);        //Дата документа основания
			SafeArrayПараметрыЧека.SetValue(6,0,ПараметрыДокумента.НомерДокументаОснования);       //Номер документа основания
			SafeArrayПараметрыЧека.SetValue(8,0,ПараметрыДокумента.Сумма);                         //Сумма оплат
			SafeArrayПараметрыЧека.SetValue(9,0,ПараметрыДокумента.СуммаБезНДС);                   //Cумма расчета по чеку без НДС
			SafeArrayПараметрыЧека.SetValue(10,0,ПараметрыДокумента.СуммаНДС0);                    //Cумма расчета по чеку с НДС по ставке 0%
			SafeArrayПараметрыЧека.SetValue(11,0,ПараметрыДокумента.СуммаНДС10);                   //Cумма расчета по чеку с НДС по ставке 10%
			SafeArrayПараметрыЧека.SetValue(12,0,ПараметрыДокумента.СуммаНДС18);                   //Cумма расчета по чеку с НДС по ставке 18%
			SafeArrayПараметрыЧека.SetValue(13,0,ПараметрыДокумента.СуммаНДС110);                  //Cумма расчета по чеку с НДС по ставке 110%
			SafeArrayПараметрыЧека.SetValue(14,0,ПараметрыДокумента.СуммаНДС118);                  //Cумма расчета по чеку с НДС по ставке 118%
			SafeArrayПараметрыЧека.SetValue(15,0,ПараметрыДокумента.СуммаНал);                     //Сумма оплаты наличными
			SafeArrayПараметрыЧека.SetValue(16,0,ПараметрыДокумента.СуммаЭлектро);                 //Сумма оплаты электронными
			SafeArrayПараметрыЧека.SetValue(17,0,ПараметрыДокумента.СуммаПредоплата);              //Сумма оплаты предоплатой
			SafeArrayПараметрыЧека.SetValue(18,0,ПараметрыДокумента.СуммаПостоплата);              //Сумма оплаты постоплатой
			SafeArrayПараметрыЧека.SetValue(19,0,ПараметрыДокумента.СуммаВстречноеПредоставление); //Сумма оплаты встречным предложением
			
			// Пробитие чека на ФР
			КодОшибкиПробития = "";
			ТекстОшибкиПробития = "";
			ВыводитьПредупреждение = Ложь;
			
			СтруктураВозвратныхПараметров = Неопределено;
			
			РезультатПробития =ВыполнитьКомандуОборудования("ФР",GUID_ФР,"ПробитьЧекКоррекции",SafeArrayПараметрыЧека,ТаймаутФР,ТекстОшибкиПробития);
			
			Если РезультатПробития Тогда
				Результат=Истина;
				флПробитьЧек = Ложь;
			Иначе
				ЗаписьЖурналаРегистрации(ПрефиксЖурнала, УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибкиПробития);
			КонецЕсли;
			
		Исключение
			ТекстОшибки="Ошибка при пробитии копии чека коррекции: " + ОписаниеОшибки();
			ЗаписьЖурналаРегистрации(ПрефиксЖурнала, УровеньЖурналаРегистрации.Информация,,,ТекстОшибки);
		КонецПопытки;
		
		флПробитьЧек = Ложь;
		
	КонецЦикла;
	
	//Теперь подставим реальные фискальные реквизиты чека, если это не копия
	Если Результат Тогда
		Попытка
			КоличествоСтрок=0; КоличествоСтолбцов=0;
			SafeArrayПараметрыЧека.GetBounds(КоличествоСтрок,КоличествоСтолбцов);
			НомерСмены=Число(SafeArrayПараметрыЧека.GetValue(0,0));
			НомерЧека=Число(SafeArrayПараметрыЧека.GetValue(1,0));
			ДатаФР=Вычислить("'"+SafeArrayПараметрыЧека.GetValue(3,0)+"'");
			НомерДокумента=Число(SafeArrayПараметрыЧека.GetValue(2,0));
			Если КоличествоСтрок > 4 Тогда 
				ФискальныйПризнак = SafeArrayПараметрыЧека.GetValue(4,0);
			Иначе
				ФискальныйПризнак="";
            КонецЕсли;
			СтруктураВозвратныхПараметров=Новый Структура("ДатаФР,НомерЧека,НомерДокумента,НомерСмены,ФискальныйПризнак",ДатаФР,НомерЧека,НомерДокумента,НомерСмены,ФискальныйПризнак);
		Исключение
			ТекстОшибки="Ошибка получения фискальных реквизитов пробитого чека: "+ОписаниеОшибки();
		КонецПопытки; 
	КонецЕсли;

	Если ПустаяСтрока(ТекстОшибки) Тогда
		ЗаписатьВФайлЛога("Пробить чек", 0, "Чек №" + НомерЧека + " пробит");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПробитьЧекКоррекции()

// Пробитие чека на оборудовании
//
Функция ПробитьЧекИзФН(ПараметрыСобытия, ТекстОшибки="",СтруктураВозвратныхПараметров=НЕОПРЕДЕЛЕНО) Экспорт

	Результат = Ложь;
	ТекстОшибки = "";

	НефискальныйЧек = Ложь;

	СформированнаяСтрока = "";
	
	флПробитьЧек = Истина;
	
	Попытка ПарольКассира = ПараметрыСеанса.Пользователь.ПарольККМ;
	Исключение ПарольКассира = 0;
	КонецПопытки;
	
	Пока флПробитьЧек Цикл

		ПрефиксЖурнала = "ФронтКассира.ПробитьЧекФН";
		Попытка
			SafeArrayПараметрыЧека = глРарус_Компонента.СоздатьПараметры(4,1);
			
			// Шапка чека
			SafeArrayПараметрыШапки = глРарус_Компонента.СоздатьПараметры(2,1);
			SafeArrayПараметрыШапки.SetValue(0,0,ПараметрыСобытия.НомерДокумента); 		//Номер документа
			SafeArrayПараметрыШапки.SetValue(1,0,ПарольКассира);
			SafeArrayПараметрыЧека.SetValue(0,0,SafeArrayПараметрыШапки);
						
			
			// Пробитие чека на ФР
			КодОшибкиПробития = "";
			ТекстОшибкиПробития = "";
			ВыводитьПредупреждение = Ложь;
			
			СтруктураВозвратныхПараметров = Неопределено;
						
			РезультатПробития =ВыполнитьКомандуОборудования("ФР",GUID_ФР,"ПробитьЧекИзФН",SafeArrayПараметрыЧека,ТаймаутФР,ТекстОшибкиПробития);
			
			Если РезультатПробития Тогда
				Результат=Истина;
				флПробитьЧек = Ложь;
			Иначе
				ЗаписьЖурналаРегистрации(ПрефиксЖурнала, УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибкиПробития);
			КонецЕсли;
			
		Исключение
			ТекстОшибки="Ошибка при пробитии копии чека из ФН: " + ОписаниеОшибки();
			ПараметрыСобытия.Вставить("Комментарий", "ОШИБКА ПРОБИТИЯ ЧЕКА: " + ОписаниеОшибки());
			ЗаписьЖурналаРегистрации(ПрефиксЖурнала, УровеньЖурналаРегистрации.Информация,,,ТекстОшибки);
		КонецПопытки;
		
		флПробитьЧек = Ложь;

	КонецЦикла;
	
	Возврат Результат;

КонецФункции //ПробитьЧекИзФН() 

Функция ВключитьАвторизаторПлатежей(Авторизатор)
	
	// Авторизатор платежей
	GUID_АвторизаторПлатежей=Авторизатор.ИдентификаторОборудования;
	
	Если глТорговоеОборудование.СтатусОборудования(GUID_АвторизаторПлатежей)<>Перечисления.СтатусыОборудования.Включено Тогда
		ОписаниеОшибки=""; Таймаут=Авторизатор.Таймаут;
		КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_АвторизаторПлатежей,ОписаниеОшибки);
		Если НЕ РезультатРаботыСОборудованием("Авторизатор платежей",КодОшибки,ОписаниеОшибки) Тогда
			GUID_АвторизаторПлатежей=""; 
		КонецЕсли;
	КонецЕсли;
	 
	Возврат НЕ ПустаяСтрока(GUID_АвторизаторПлатежей);
КонецФункции

// Проверка готовности оборудования рабочего места и заполнение соответствующих переменных
//
Функция ВключитьОборудование() Экспорт
	Результат=Истина; 
	Попытка
		// Дисплей покупателя
		Если ПараметрыСеанса.Компьютер.Дисплей.Пустая() Тогда	GUID_Дисплей="";
		Иначе GUID_Дисплей=ПараметрыСеанса.Компьютер.Дисплей.ИдентификаторОборудования;
		КонецЕсли;
		Если НЕ ПустаяСтрока(GUID_Дисплей) Тогда
			// Есть устройство, включаем его
			Если глТорговоеОборудование.СтатусОборудования(GUID_Дисплей)<>Перечисления.СтатусыОборудования.Включено Тогда
				ОписаниеОшибки="";
				КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_Дисплей,ОписаниеОшибки);
				Если НЕ РезультатРаботыСОборудованием("Дисплей покупателя",КодОшибки,ОписаниеОшибки) Тогда
					Результат=Ложь;
					GUID_Дисплей=""; // Будем считать что у нас нет дисплея в текущем сеансе работы
				КонецЕсли; 
			КонецЕсли;
			Если НЕ ПустаяСтрока(GUID_Дисплей) Тогда
				ТмпЗнач=0;
				Если глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_Дисплей,"ДлинаСтрокиСообщения",ТмпЗнач) И (ТмпЗнач>0) Тогда
					ДлинаСтрокиДисплея=ТмпЗнач;
				КонецЕсли;
				Если глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_Дисплей,"ТекстЗаставки",ТмпЗнач) Тогда
					// проверим разделитель строк "\n"
					ТекстЗаставкиДисплея=СтрЗаменить(ТмпЗнач,"\n", Символы.ПС);
					// если все умещается в 1 строке то принудительно добавим еще 1 строчку
					Если СтрЧислоСтрок(ТекстЗаставкиДисплея) = 1 Тогда
						ТекстЗаставкиДисплея = ТекстЗаставкиДисплея+ Символы.ПС;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			ВывестиЗаставку();
		КонецЕсли;
		// Фискальный регистратор
		Если ПараметрыСеанса.Компьютер.ФР.Пустая() Тогда GUID_ФР="";
		Иначе GUID_ФР=ПараметрыСеанса.Компьютер.ФР.ИдентификаторОборудования;
		КонецЕсли;
		Если НЕ ПустаяСтрока(GUID_ФР) Тогда
			
			// Есть устройство, включаем его
			ТаймаутФР=ПараметрыСеанса.Компьютер.ФР.Таймаут;
			Если глТорговоеОборудование.СтатусОборудования(GUID_ФР)=Перечисления.СтатусыОборудования.Включено Тогда
				ОписаниеОшибки="";
				// Выключим оборудование
				КодОшибки=глТорговоеОборудование.ВыключитьОборудование(GUID_ФР,ОписаниеОшибки);
				Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,ОписаниеОшибки) Тогда
					Результат=Ложь;
					GUID_ФР = ""; // Считаем что у нас нет ФР'а в текущем сеансе работы
				КонецЕсли;
			КонецЕсли;
			ОписаниеОшибки="";
			КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_ФР,ОписаниеОшибки);
			// Включаем оборудование
			Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,ОписаниеОшибки) Тогда
				Результат=Ложь;
				GUID_ФР = ""; // Считаем что у нас нет ФР'а в текущем сеансе работы
			Иначе
				Результат=Истина;
				ФР=ПараметрыСеанса.Компьютер.ФР;
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(GUID_ФР) Тогда
				ТмпЗнач=0;
				Если глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_ФР,"ДлинаСтроки",ТмпЗнач) И (ТмпЗнач>0) Тогда
					ДлинаСтрокиФР=ТмпЗнач;
				КонецЕсли;
                // Получим доступные виды оплат
				ПолучитьДоступныеВидыОплат();
			КонецЕсли;
		КонецЕсли;
		// ПринтерЧеков
		Если фкУстройствоПечатиНефискальныхДокументов.Пустая() Тогда GUID_ПЧ="";
		Иначе GUID_ПЧ=фкУстройствоПечатиНефискальныхДокументов.ИдентификаторОборудования;
		КонецЕсли;
		Если НЕ ПустаяСтрока(GUID_ПЧ) Тогда
			// Есть устройство, включаем его
			ТаймаутФР=фкУстройствоПечатиНефискальныхДокументов.Таймаут;			
			Если глТорговоеОборудование.СтатусОборудования(GUID_ПЧ)=Перечисления.СтатусыОборудования.Включено Тогда
				ПринтерЧеков=фкУстройствоПечатиНефискальныхДокументов;
			Иначе
				ОписаниеОшибки="";
				КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_ФР,ОписаниеОшибки);
				Если НЕ РезультатРаботыСОборудованием("Фискальный регистратор",КодОшибки,ОписаниеОшибки) Тогда
					Результат=Ложь;
					GUID_ПЧ=""; // Считаем что у нас нет ПЧ в текущем сеансе работы
				Иначе
					ПринтерЧеков=фкУстройствоПечатиНефискальныхДокументов;
				КонецЕсли;
			КонецЕсли;
			Если НЕ ПустаяСтрока(GUID_ПЧ) Тогда
				ТмпЗнач=0;
				Если глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_ПЧ,"ДлинаСтроки",ТмпЗнач) И (ТмпЗнач>0) Тогда
					ДлинаСтрокиПЧ=ТмпЗнач;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Авторизатор платежей
		Если ПараметрыСеанса.Компьютер.Авторизатор.Пустая() Тогда GUID_Авторизатор="";
		Иначе GUID_Авторизатор=ПараметрыСеанса.Компьютер.Авторизатор.ИдентификаторОборудования;
		КонецЕсли;
		Если НЕ ПустаяСтрока(GUID_Авторизатор) Тогда
			Если глТорговоеОборудование.СтатусОборудования(GUID_Авторизатор)<>Перечисления.СтатусыОборудования.Включено Тогда
				ОписаниеОшибки=""; Таймаут=ПараметрыСеанса.Компьютер.Авторизатор.Таймаут;  ТаймаутАвторизатора = Таймаут;
				КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_Авторизатор,ОписаниеОшибки);
				Если НЕ РезультатРаботыСОборудованием("Авторизатор платежей",КодОшибки,ОписаниеОшибки) Тогда
					GUID_Авторизатор=""; // Считаем что у нас нет авторизатора в текущем сеансе работы
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
		Если НЕ ПустаяСтрока(GUID_Авторизатор) Тогда
			ТаймаутАвторизатора=ПараметрыСеанса.Компьютер.Авторизатор.Таймаут;
			// надо получить из него чего эта модель умеет
			ТмпЗнач="";
			Если глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_Авторизатор,"ПоддерживаемыеТипыКарт",ТмпЗнач) Тогда
				ТмпЗнач=ВРЕГ(СокрЛП(ТмпЗнач));
				Если ПустаяСтрока(ТмпЗнач) Тогда
					GUID_Авторизатор=""; // Считаем что у нас нет авторизатора в текущем сеансе работы
					ОткрытьДиалог("Ошибка: Текущий авторизатор платежных карт:
									|<"+ПараметрыСеанса.Компьютер.Авторизатор+">
									|не вернул список поддерживаемых видов платежных карт. Авторизатор отключен!
									|Для использования авторизатора следует его предварительно настроить.",СтатусСообщения.ОченьВажное);
					Иначе
					КартыАвторизатрора=ТмпЗнач;					
				КонецЕсли;
			КонецЕсли;
			ТмпЗнач ="";
			Если глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_Авторизатор,"ПечатьКвитанцийНаPinPad",ТмпЗнач) Тогда
				Попытка
					ПечатьНаПинПадеАвторизатора = Булево(ТмпЗнач);
				Исключение
					ПечатьНаПинПадеАвторизатора = Ложь;
				КонецПопытки;
			Иначе
				ПечатьНаПинПадеАвторизатора = Ложь;
			КонецЕсли;
		КонецЕсли;
		// Весы
		Если ПараметрыСеанса.Компьютер.Весы.Пустая() Тогда GUID_Весы="";
		Иначе GUID_Весы=ПараметрыСеанса.Компьютер.Весы.ИдентификаторОборудования;
		КонецЕсли;
		Если НЕ ПустаяСтрока(GUID_Весы) Тогда
			// Есть устройство, включаем его
			Если глТорговоеОборудование.СтатусОборудования(GUID_Весы)<>Перечисления.СтатусыОборудования.Включено Тогда
				ОписаниеОшибки=""; ТаймаутВесы=ПараметрыСеанса.Компьютер.Весы.Таймаут;
				КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_Весы,ОписаниеОшибки);
				Если НЕ РезультатРаботыСОборудованием("Весы",КодОшибки,ОписаниеОшибки) Тогда
					Результат=Ложь;
					GUID_Весы=""; // Считаем что у нас нет весов в текущем сеансе работы
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		// Терминал сбора данных		
		Если ПараметрыСеанса.Компьютер.ТСД.Пустая() Тогда GUID_ТСД="";
		Иначе GUID_ТСД=ПараметрыСеанса.Компьютер.ТСД.ИдентификаторОборудования;
		КонецЕсли;
		Если НЕ ПустаяСтрока(GUID_ТСД) Тогда
			// Есть устройство, включаем его
 			ТаймаутТСД=ПараметрыСеанса.Компьютер.ТСД.Таймаут;						
			Если (глТорговоеОборудование.СтатусОборудования(GUID_ТСД)=Перечисления.СтатусыОборудования.Включено)
			 ИЛИ (глТорговоеОборудование.СтатусОборудования(GUID_ТСД)=Перечисления.СтатусыОборудования.Готово) Тогда
				ТСД=ПараметрыСеанса.Компьютер.ТСД;
			Иначе
				ОписаниеОшибки="";
				КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_ТСД,ОписаниеОшибки);
				Если НЕ РезультатРаботыСОборудованием("Терминал сбора данных",КодОшибки,ОписаниеОшибки) Тогда
					Результат=Ложь;
					GUID_ТСД=""; // Считаем что у нас нет ТСД в текущем сеансе работы
				Иначе
					ТСД=ПараметрыСеанса.Компьютер.ТСД;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		// Сканер штрих-кодов
		Если ПараметрыСеанса.Компьютер.Сканер.Пустая() Тогда GUID_Сканер="";
		Иначе GUID_Сканер=ПараметрыСеанса.Компьютер.Сканер.ИдентификаторОборудования;
		КонецЕсли;
		Если НЕ ПустаяСтрока(GUID_Сканер) Тогда
			// Есть устройство - проверим его готовность
			СтатусУстройства=глТорговоеОборудование.СтатусОборудования(GUID_Сканер);
			Если СтатусУстройства<>Перечисления.СтатусыОборудования.Готово Тогда
				// Для получения событий от сканера он должен быть заблокирован на нас!
				ОписаниеОшибки="";
				КодОшибки=глТорговоеОборудование.ВключитьОборудование(GUID_Сканер,ОписаниеОшибки);
				Если РезультатРаботыСОборудованием("Сканер штрих-кодов",КодОшибки,ОписаниеОшибки) Тогда
					СтатусУстройства=глТорговоеОборудование.СтатусОборудования(GUID_Сканер);
				Иначе
					Результат=Ложь;
					GUID_Сканер=""; // Считаем что у нас нет сканера в текущем сеансе работы
				КонецЕсли;
			КонецЕсли;
			// Сканер должен быть не просто включен, но еще и заблокирован на нас иначе не будет
			// нам присылать результаты сканирования
			Если СтатусУстройства=Перечисления.СтатусыОборудования.Готово Тогда
				Сканер=ПараметрыСеанса.Компьютер.Сканер;
			Иначе
				ОткрытьДиалог("Сканер рабочего места не подключен!", СтатусСообщения.ОченьВажное);
			КонецЕсли;
		КонецЕсли;
		// Клавиатура 
		Если НЕ ПараметрыСеанса.Компьютер.Клавиатура.Пустая() Тогда
			Клавиатура = ПараметрыСеанса.Компьютер.Клавиатура;
		КонецЕсли;

	Исключение
		ТекстОшибки=ОписаниеОшибки(); // Используется при отладке
		Результат=Ложь;
		ОткрытьДиалог("Внутреннее исключение при включении оборудования:"+Символы.ПС+ТекстОшибки, СтатусСообщения.ОченьВажное);
	КонецПопытки;
	ПараметрыДействия=НЕОПРЕДЕЛЕНО;
	Возврат Результат;
КонецФункции // ВключитьОборудование()

// Выключает использовавшееся оборудование
//
Функция ВыключитьОборудование() Экспорт
	Результат=Истина; 
	Попытка
		// Выключим дисплей если есть
		Если НЕ ПустаяСтрока(GUID_Дисплей) Тогда
			// Выведем заставку и отключим устройство
			ВывестиНаДисплей("КАССА",,"НЕ РАБОТАЕТ",,2);
			ОписаниеОшибки="";
			КодОшибки=глТорговоеОборудование.ВыключитьОборудование(GUID_Дисплей,ОписаниеОшибки);
			GUID_Дисплей="";
			Если НЕ РезультатРаботыСОборудованием("Дисплей покупателя",КодОшибки,ОписаниеОшибки) Тогда
				Результат=Ложь;
			КонецЕсли;
		КонецЕсли;
	Исключение
		ТекстОшибки=ОписаниеОшибки(); // Используется при отладке
		Результат=Ложь;
		ОткрытьДиалог("Внутреннее исключение при выключении оборудования:"+Символы.ПС+ТекстОшибки, СтатусСообщения.ОченьВажное);
	КонецПопытки;
	ПараметрыДействия=НЕОПРЕДЕЛЕНО;
	Возврат Результат;
КонецФункции // ВыключитьОборудование()

// Получение версии ФФД ККМ
//
Функция ПолучитьВерсиюФФД() Экспорт
	ВерсияФФД = 0;

	Попытка
		SafeArrayПараметрыПроверкиФФД=Рарус_Компонента.СоздатьПараметры(1,1);
		ТекстОшибки = "";
		КодОшибки = Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_ФР,"ПолучитьВерсиюФФД",SafeArrayПараметрыПроверкиФФД,ТаймаутФР);
		ТекстОшибки = Рарус_Компонента.ОписаниеОшибки;
		Если КодОшибки = 0 Тогда 
			ВерсияФФД = SafeArrayПараметрыПроверкиФФД.GetValue(0,0)
		КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации("Фронт кассира", УровеньЖурналаРегистрации.Предупреждение, Метаданные.Обработки.ФронтКассира, , ТекстОшибки);
	КонецПопытки;

	Возврат ВерсияФФД;
КонецФункции // ПолучитьВерсиюФФД()

// получает у авторизатора информацию о кредитной организации
Функция ПолучитьПараметрыКредОрганизации()
	
	СтрокаКО = "";
	// Параметры кредитной организации
	ТмпЗнач=0;
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_АвторизаторПлатежей,"Организация",ТмпЗнач);
	СтрокаКО = СтрокаКО + ?(ПустаяСтрока(ТмпЗнач),"",ТмпЗнач+Символы.ПС);
	
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_АвторизаторПлатежей,"АдресОбслуживания",ТмпЗнач);
	СтрокаКО = СтрокаКО + ?(ПустаяСтрока(ТмпЗнач),"",ТмпЗнач+Символы.ПС);
	
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_АвторизаторПлатежей,"БИК",ТмпЗнач);
	СтрокаКО = СтрокаКО + ?(ПустаяСтрока(ТмпЗнач),"","БИК: "+ ТмпЗнач+Символы.ПС);
	
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_АвторизаторПлатежей,"Телефон",ТмпЗнач);
	СтрокаКО = СтрокаКО + ?(ПустаяСтрока(ТмпЗнач),"","тел. "+ ТмпЗнач+Символы.ПС);
	
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_АвторизаторПлатежей,"НомерДоговора",ТмпЗнач);
	СтрокаКО = СтрокаКО + ?(ПустаяСтрока(ТмпЗнач),"","Договор № "+ ТмпЗнач);
	
	глТорговоеОборудование.ПолучитьЗначениеСвойстваОборудования(GUID_АвторизаторПлатежей,"ДатаДоговора",ТмпЗнач);
	СтрокаКО = СтрокаКО + " от " + ТмпЗнач+Символы.ПС;
	
	СтрокаКО = ?(ПустаяСтрока(СтрокаКО),"","Кредитная организация:"+Символы.ПС+СтрокаКО); 
	
	Возврат СтрокаКО;
КонецФункции

// формирует доп информацию о платежах в 1 строку
Процедура СформироватьДопИнформациюОПлатежах(СформированнаяСтрока)
	
	Если тзПлатежиЗаУслуги.Количество() = 0 Тогда Возврат; КонецЕсли;
	
	// Сформируем таблицу операторов таким образом чтобы удобно было
	// перебирать по оборудованию
	тзПлатежи = тзПлатежиЗаУслуги.Скопировать(,"Оборудование,Оператор");
	тзПлатежи.Свернуть("Оборудование,Оператор");
	тзПлатежи.Сортировать("Оборудование");
	
	Оборудование = тзПлатежи[0].Оборудование; GUID_АвторизаторПлатежей = Оборудование.ИдентификаторОборудования;
	СтрокаКО = ПолучитьПараметрыКредОрганизации();
	СформированнаяСтрока = ?(ПустаяСтрока(СтрокаКО),"",СтрокаКО);
	тзРеквизитов = ПолучитьРеквизитыОператоров();
	// пройдемся по всем платежам
	Для Каждого Платеж Из тзПлатежи Цикл
		
		// необходимо проверить по какому авторизатору проходил платеж
		// и если по другому, то обновим таблицу реквизитов
		ТекОборудование = Платеж.Оборудование;
		Если ТекОборудование <> Оборудование Тогда
			Оборудование = Платеж.Оборудование; GUID_АвторизаторПлатежей = Оборудование.Идентификатор;
			тзРеквизитов = ПолучитьРеквизитыОператоров();	
			СтрокаКО = ПолучитьПараметрыКредОрганизации();
			СформированнаяСтрока = ?(ПустаяСтрока(СтрокаКО),СформированнаяСтрока,СформированнаяСтрока + Символы.ПС + СтрокаКО);
		КонецЕсли;
		
		// Ищем в таблице реквизитов нужного оператора
		РеквизитыТекПлатежа = тзРеквизитов.Найти(Платеж.Оператор,"Оператор"); 
		// получаем реквизиты получателя средств
		Если РеквизитыТекПлатежа <> Неопределено Тогда			
			ИмяПС = РеквизитыТекПлатежа.ИмяПС;
			ИННПС = РеквизитыТекПлатежа.ИННПС;
			Если НЕ ПустаяСтрока(СокрЛП(ИмяПС+ИННПС)) Тогда
				СтрокаПС = "Получатель средств:" + Символы.ПС;
				СтрокаПС = СтрокаПС + ?(ПустаяСтрока(ИмяПС),"",ИмяПС+Символы.ПС);
				СтрокаПС = СтрокаПС + ?(ПустаяСтрока(ИННПС),"","ИНН: "+ИННПС+Символы.ПС);
				СформированнаяСтрока = СформированнаяСтрока + Символы.ПС + СтрокаПС;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

// получает реквизиты операторов  - Оператор, ИМЯ, ИНН
Функция ПолучитьРеквизитыОператоров()
	// запросим у Авторизатора список операторов
	ПараметрыДействия = "";
	ВыполнитьКомандуОборудования("ПолучитьОператоров",GUID_АвторизаторПлатежей,"ПолучитьОператоров",ПараметрыДействия,ТаймаутФР,ТекстОшибки);
	КолвоСтрок=0; КолвоСтолбцов=0;
	ПараметрыДействия.GetBounds(КолвоСтрок,КолвоСтолбцов);		

	тзРеквизитов = Новый ТаблицаЗначений;
	тзРеквизитов.Колонки.Добавить("Оператор");
	тзРеквизитов.Колонки.Добавить("ИмяПС");
	тзРеквизитов.Колонки.Добавить("ИННПС");
	
	// запишем список операторов в список значений
	СписокОператоров = Новый СписокЗначений();
	Для НСтроки = 0 По КолвоСтрок-1 Цикл		
		ИмяОператора = ПараметрыДействия.GetValue(НСтроки,0);
		ИмяПС = ПараметрыДействия.GetValue(НСтроки,4);
		ИННПС = ПараметрыДействия.GetValue(НСтроки,5);
		
		СтрРеквизитов = тзРеквизитов.Добавить();
		СтрРеквизитов.Оператор = ИмяОператора;
		СтрРеквизитов.ИмяПС = ИмяПС;
		СтрРеквизитов.ИННПС = ИННПС;
	КонецЦикла;
	
	Возврат тзРеквизитов;
КонецФункции

// получает список операторов и список кодов
Функция ПолучитьСписокОператоровИКодов(СписокОператоров,СписокКодов)
	
	// запросим у Авторизатора список операторов
	ПараметрыДействия = "";
	ВыполнитьКомандуОборудования("ПолучитьОператоров",GUID_АвторизаторПлатежей,"ПолучитьОператоров",ПараметрыДействия,ТаймаутФР,ТекстОшибки);
	КолвоСтрок=0; КолвоСтолбцов=0;
	ПараметрыДействия.GetBounds(КолвоСтрок,КолвоСтолбцов);		

	// запишем список операторов в список значений
	СписокОператоров = Новый СписокЗначений();
	Для НСтроки = 0 По КолвоСтрок-1 Цикл		
		ИмяОператора = ПараметрыДействия.GetValue(НСтроки,0);
		КодОператора = ПараметрыДействия.GetValue(НСтроки,3);
		
		СписокОператоров.Добавить(НСтроки, ИмяОператора);
		СписокКодов.Добавить(НСтроки, КодОператора);
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

// производит авторизацию платежа
Функция АвторизацияСотовогоПлатежа(Параметры)
	
	Оператор=Параметры.Получить(0).Значение;
	Параметр1=Параметры.Получить(4).Значение;
	Параметр2=Параметры.Получить(5).Значение;
	Параметр3=Параметры.Получить(6).Значение;
	Сумма=Параметры.Получить(8).Значение;
    Сессия = "";
	
	// Подготоваливаем задание печати квитанции	
	ПараметрыДействия=Рарус_Компонента.СоздатьПараметры(5,1);
	ПараметрыДействия.SetValue(0,0,Оператор);
	ПараметрыДействия.SetValue(1,0,Параметр1);
	ПараметрыДействия.SetValue(2,0,Сумма);
	ПараметрыДействия.SetValue(3,0,Параметр2);
	ПараметрыДействия.SetValue(4,0,Сессия);
	
	КодОшибки = 0; ТекстОшибки = "";
	ВыполнитьКомандуОборудования("Авторизатор",GUID_АвторизаторПлатежей,"Авторизация",ПараметрыДействия,ТаймаутАвторизатора,ТекстОшибки,КодОшибки);
	Попытка Сессия = ПараметрыДействия.GetValue(4,0); Исключение КонецПопытки;
	Параметры.Добавить(Сессия); 
	Параметры.Добавить(КодОшибки);  
	Параметры.Добавить(ТекстОшибки);

	Возврат ?(КодОшибки=0,Истина,Ложь);
	
КонецФункции

//Вывод на экран ошибки работы с оборудованием
//
// Параметры:
//  ИмяОборудования - строка,
//  КодОшибки       - число - код ошибки,
//  ОписаниеОшибки  - строка.
//
Функция РезультатРаботыСОборудованием(ИмяОборудования,КодОшибки,ОписаниеОшибки) Экспорт
	Если ТипЗнч(КодОшибки)<>Тип("Число") Тогда 
		КодОшибки=22051;
		ОписаниеОшибки="Транспорт вернул не числовой код результата операции. "+ОписаниеОшибки;
	КонецЕсли;
	Если КодОшибки=0 Тогда Возврат Истина; КонецЕсли;
	Если РежимДиагностики Тогда
		ЗаписьЖурналаРегистрации(ИмяОборудования,УровеньЖурналаРегистрации.Ошибка,,,"КодОшибки: "+КодОшибки+" Описание: "+ОписаниеОшибки);
	КонецЕсли;
	
	ЗаписатьВФайлЛога("Ошибка оборудования: " + ИмяОборудования,КодОшибки,ОписаниеОшибки,,1);
	
	ОткрытьДиалог(ИмяОборудования+"
					|КодОшибки: "+КодОшибки+"
					|Описание: "+ОписаниеОшибки,СтатусСообщения.ОченьВажное);
	Возврат Ложь;
КонецФункции // РезультатРаботыСОборудованием()

// Вызов метода оборудования и анализ результата
//
// Параметры:
//  ИмяОборудования - строка, пользовательское описание типа оборудования
//	GUID			- строка, GUID-идентификатор конкретного экземпляра оборудования которому идет вызов
//	Команда			- строка, вызываемый метод (команда) оборудования
//	Параметры		- comSafeArray, параметры выполнения команды
//	Таймаут			- число,  таймаут ожидания завершения работы оборудования (в сек.)
//	ВыводитьПредупреждение - булево, флаг необходимости вывода предупреждения об ошибке на экран пользователя
// Возвратные параметры:
//  ТекстОшибки  	- строка, текст с сообщением о возникшей ошибке (он же выводится в предупреждении)
//  КодОшибки       - число - код возникшей ошибки
//
Функция ВыполнитьКомандуОборудования(ИмяОборудования="Оборудование",GUID,Команда,Параметры,Таймаут=90,ТекстОшибки="",КодОшибки=0,ВыводитьПредупреждение=Истина) Экспорт
	ОписаниеОшибки="";
	Попытка 
		КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID,Команда,Параметры,Таймаут);
		Попытка ОписаниеОшибки=Рарус_Компонента.ОписаниеОшибки; Исключение КонецПопытки;
	Исключение
		ТекстИсключения=ОписаниеОшибки(); КодОшибки=22050;
		Попытка ОписаниеОшибки=Рарус_Компонента.ОписаниеОшибки; Исключение КонецПопытки;
		ОписаниеОшибки="Произошло исключение при вызове метода устройства (GUID="+GUID+") "+?(ПустаяСтрока(ОписаниеОшибки),ТекстИсключения,ОписаниеОшибки);
	КонецПопытки;
	Если ТипЗнч(КодОшибки)<>Тип("Число") Тогда 
		КодОшибки=22051;
		ОписаниеОшибки="Транспорт вернул не числовой код результата операции. "+ОписаниеОшибки;
	КонецЕсли;
	Если КодОшибки=0 Тогда Возврат Истина; КонецЕсли;
	ТекстОшибки=ИмяОборудования+": Ошибка выполнения команды """+Команда+"""
						|КодОшибки: "+КодОшибки+"
						|Описание: "+ОписаниеОшибки;
	Если РежимДиагностики Тогда
		Если Найти(ВРЕГ(ИмяОборудования),"ФРОНТ")>0 Тогда
			// не меняем
		ИначеЕсли Найти(ИмяОборудования,"Оборудование")=0 Тогда
			ИмяОборудования="Оборудование."+ИмяОборудования;
		КонецЕсли;
		ЗаписьЖурналаРегистрации(ИмяОборудования,УровеньЖурналаРегистрации.Ошибка,,,СтрЗаменить(ТекстОшибки,Символы.ПС," "));
	КонецЕсли;
	Если ВыводитьПредупреждение Тогда	
		ОткрытьДиалог(ТекстОшибки,СтатусСообщения.ОченьВажное);
	КонецЕсли;
	Возврат Ложь;
КонецФункции // ВыполнениеКомандыОборудованием

// Функция возвращает по коду клавиши структуру содержащую объект и имя действия над ним
Функция ПолучитьКлавишуПоКоду(Клавиатура,КодКлавиши) Экспорт 
	Возврат РегистрыСведений.Клавиши.Получить(Новый Структура("Клавиатура,КодКлавиши",Клавиатура,КодКлавиши));
КонецФункции // ПолучитьКлавишуПоКоду

// Распознать код карты
Процедура РаспознатьКодКарты(ЭтаФорма,Код) Экспорт
	СчитанныйНомер=ДекодироватьДанныеСчитывателяМК(Код);
	// Проверим номер карты
	КоличествоПопыток=3; // нужно ли его менять или задавать жестко?
	флОК=0; Сп=0; Номер=Прав(СтрЗаменить(Лев(СчитанныйНомер,Найти(СчитанныйНомер,"=")-1)," ",""),4);
	Для Сч=1 по КоличествоПопыток Цикл
		Попытка
			Значение="";
			Статус=0;
			Если ВвестиСтроку(Значение,"Введите 4 последние цифры номера карты",4) Тогда
				Статус=1;
			КонецЕсли;
			Если (Статус=1) и (Значение=Номер) Тогда флОК=1; Прервать; // Номер введен и он правильный
			ИначеЕсли (Статус=1) Тогда // Номер введен и он неправильный
				Если Сч<>КоличествоПопыток Тогда ОткрытьДиалог("Четыре последних цифры не совпадают с записанными на карте!",СтатусСообщения.Важное);
				Иначе ОткрытьДиалог("Изымите карту. Неверный номер карты.",СтатусСообщения.Важное);
				КонецЕсли;
			Иначе // Отказ от ввода карты
				Возврат;
			КонецЕсли;
		Исключение
			Прервать;
		КонецПопытки;
	КонецЦикла;
	Если флОК=1 Тогда 
		СчитанныйНомерКарты=СчитанныйНомер; 
	КонецЕсли;
КонецПроцедуры

// действие гашение авторизатора
Функция ДействиеГашениеАвторизатора(ЭтаФорма) Экспорт
	
	Если фкРазрешитьРучнуюАвторизациюБезналичныхПлатежей Тогда
		ОткрытьДиалог("Авторизация платежей производится вручную.
		|Закрытие смены не требуется.", СтатусСообщения.Важное);
		Возврат 0; // Авторизатор работает автономно
	КонецЕсли; 
	
	//проверим право
	Если НЕ фкРазрешитьГашениеАвторизаторов Тогда
		ОткрытьДиалог("Вам запрещено выполнять эту операцию", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;

	// Закрытие смены должно производится с использованием оборудования, проверим его
	// сначала принтер квитанций
	Если ПустаяСтрока(GUID_ФР) Тогда
		ОткрытьДиалог("Не подключен принтер квитанций!
					|Операция закрытия смены авторизатора прервана!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	СтатусУстройства=глТорговоеОборудование.СтатусОборудования(GUID_ФР);
	Если (СтатусУстройства<>Перечисления.СтатусыОборудования.Включено) И (СтатусУстройства<>Перечисления.СтатусыОборудования.Готово) Тогда
		ОткрытьДиалог("Принтер для печати квитанций не готов!
					|Операция закрытия смены авторизатора прервана!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	// проверим авторизатор
	Если ПустаяСтрока(GUID_Авторизатор) Тогда
		ОткрытьДиалог("Не подключен авторизатор платежей
					|Операция закрытия смены авторизатора прервана!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	СтатусУстройства=глТорговоеОборудование.СтатусОборудования(GUID_Авторизатор);
	Если (СтатусУстройства<>Перечисления.СтатусыОборудования.Включено) И (СтатусУстройства<>Перечисления.СтатусыОборудования.Готово) Тогда
		ОткрытьДиалог("Авторизатор платежей не готов!
					|Операция закрытия смены авторизатора прервана!", СтатусСообщения.Важное);
		Возврат 0;
	КонецЕсли;
	
	//переспросим пользователя
	Если ОткрытьДиалогВопроса("Выполнить закрытие смены авторизатора безналичных платежей "+Строка(Авторизатор)+"?") Тогда
		//Снимем отчет
		Параметры=Рарус_Компонента.СоздатьПараметры(2,1);
		КодОшибки=Рарус_Компонента.ЗаказатьВыполнениеДействияСинхронно(GUID_Авторизатор,"ЗакрытьСмену",Параметры,ТаймаутАвторизатора);
		Если НЕ РезультатРаботыСОборудованием("Авторизатор",КодОшибки,Рарус_Компонента.ОписаниеОшибки) Тогда
			ЗаписатьВФайлЛога("Закрытие смены авторизатора", КодОшибки,"Операция не выполнена",GUID_Авторизатор,1);
			Возврат 0;
		КонецЕсли;
		ЗаписатьВФайлЛога("Закрытие смены авторизатора", КодОшибки, Рарус_Компонента.ОписаниеОшибки);
		Попытка 
			ТекстКвитанцииПлатежа=СокрЛП(Параметры.GetValue(1,0));
			Если ПустаяСтрока(ТекстКвитанцииПлатежа) Тогда 
				ОткрытьДиалог("Авторизатор не вернул текст квитанции закрытия смены!
				|Невозможно распечатать квитанцию!", СтатусСообщения.Важное);
			Иначе
				// Подготоваливаем задание печати квитанции	
				ПараметрыДействия=Рарус_Компонента.СоздатьПараметры(4,1);
				ПараметрыДействия.SetValue(0,0,"");
				ПараметрыДействия.SetValue(1,0,ТекстКвитанцииПлатежа);
				ПараметрыДействия.SetValue(2,0,"");
				ПараметрыДействия.SetValue(3,0,0);
				Если GUID_ПЧ = "" Тогда
					ВыполнитьКомандуОборудования("ПринтерКвитанций",GUID_ФР,"Напечатать",ПараметрыДействия,ТаймаутФР,ТекстОшибки);
				Иначе
					ВыполнитьКомандуОборудования("ПринтерКвитанций",GUID_ПЧ,"Напечатать",ПараметрыДействия,ТаймаутПЧ,ТекстОшибки);
				КонецЕсли;

			КонецЕсли;
		Исключение
			ОткрытьДиалог("Авторизатор вернул некорректные параметры закрытия смены!
			|Невозможно распечатать квитанцию!
			|"+ОписаниеОшибки(), СтатусСообщения.Важное);
			Если РежимДиагностики Тогда
				ЗаписьЖурналаРегистрации("Оборудование.Авторизатор",УровеньЖурналаРегистрации.Ошибка,,,СтрЗаменить(ТекстОшибки,Символы.ПС," "));
			КонецЕсли;
			
			ЗаписатьВФайлЛога("Закрытие смены авторизатора", 1, СтрЗаменить(ТекстОшибки,Символы.ПС," "),,1);
			
		КонецПопытки;
	КонецЕсли;
	
КонецФункции

// Функция открывает системную клавиатуру для ввода значения.
//
// Параметры:
//  Текст        - Строка - текст по умолчанию, отображающийся в поле ввода системной клавиатуры.
//  Таймаут      - Число - время ,через которое клавиатура закроется с эффектом Esc.
//  СимволПароля - Строка - символ, которым будут заменяться символы в поле ввода.
// 	ЦветФона     - Число - основной цвет формы.
// 
// Возвращаемое  значение:
//  Строка       - Введенная на клавиатуре строка.
//
Функция ВвестиНаКлавиатуре(Знач Текст = "", ЦветФона = Неопределено, Таймаут = 0, СимволПароля = "", ПропускатьПустыеСтроки = Истина) Экспорт
	
	Если ЦветФона = Неопределено Тогда            
		ЦветФона = 14214634;
	КонецЕсли;
	
	Попытка
		ВведенноеЗначение = Рарус_Компонента.ОтобразитьКлавиатуру(ЦветФона, Текст, СимволПароля, Таймаут);
	Исключение
		ЗаписьЖурналаРегистрации("ВводНаЭкраннойКлавиатуре", УровеньЖурналаРегистрации.Ошибка, Неопределено, , глРарус_Компонента.ОписаниеОшибки);
		ВведенноеЗначение = "";
	Конецпопытки;
	
	// Если был нажат Esc, то компонента вернет пустую строку. При этом не стоит стирать значение по умолчанию.
	Если ПропускатьПустыеСтроки И ПустаяСтрока(ВведенноеЗначение) Тогда
		ВведенноеЗначение = Текст;
	КонецЕсли;
	
	Возврат ВведенноеЗначение;
	
КонецФункции

//////////////////////////////////////
// СЕРВИСНЫЕ ПРОЦЕДУРЫ
//////////////////////////////////////

// Функция, приводящая строку к необходимой длине строки символом
//
// Параметры:
//  Стр    - строка - строка которую приводим,
//  Символ - строка - символ, который вставляем, 
//  Длина  - число - до какой длины надо привести, 
//  Реж    - число - 1 - добавлять символы слева, 2 - справа, 3 - слева и справа.
//
// Возвращаемое значение:
//  Стр - строка - отредактированная строка.
//
Функция СтрокаПривести(Стр,Символ,Длина,Реж=1) Экспорт
 	ДопСтр=""; Сч=Длина-СтрДлина(Стр);
	Если Сч>0  Тогда
		Для к=1 По Сч Цикл
			ДопСтр=ДопСтр+Символ;
		КонецЦикла;
		Если Реж=1 Тогда Возврат ДопСтр+Стр;
		ИначеЕсли Реж=2 Тогда Возврат Стр+ДопСтр;
		Иначе к=Окр(Сч/2); Возврат Лев(ДопСтр,к)+Стр+Прав(ДопСтр,Сч-к);
		КонецЕсли;
	Иначе Возврат Стр;
	КонецЕсли;
КонецФункции // СтрокаПривести()

// Получает номер отдела
//
// Параметры:
// СсылкаНаОтдел - произвольное - ссылка на отдел.
//
Функция ПолучитьНомерОтдела(СсылкаНаОтдел)
	Результат=1; // по умолчанию 1-ый отдел
	Если обЗначениеНеЗаполнено(СсылкаНаОтдел) Тогда
		Возврат Результат;
	КонецЕсли;
	Если ТипЗнч(СсылкаНаОтдел)=Тип("СправочникСсылка.СкладыКомпании") Тогда
		Результат=СсылкаНаОтдел.НомерОтделаДляККМ;
	КонецЕсли;
	Если Результат=0 Тогда
		Результат=1; // Если реквизит не заполнен пусть будет значение по умолчанию
	КонецЕсли;
	Возврат Результат;
КонецФункции // ПолучитьНомерОтдела()

// Записать в файл лога
Процедура ЗаписатьВФайлЛога(Команда,Результат = 0, Описание = "", GUID = "", ТипСообщения = 0) Экспорт 
	
	Если НЕ флВестиЛогДействийПользователяВоФронте Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = Найти(Описание,"КодОшибки: ");
	Если Индекс > 0 Тогда
		Индекс = Индекс + 11;
		КодОшибки = "";
		Пока Индекс < СтрДлина(Описание) И Найти(" 0123456789",Сред(Описание,Индекс,1)) > 0 Цикл
			КодОшибки = КодОшибки + Сред(Описание, Индекс, 1);
			Индекс = Индекс + 1;
		КонецЦикла;
		Попытка
			Результат = Число(СокрЛП(КодОшибки));
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Индекс = Найти(Описание,"Описание: ");
	Если Индекс > 0 Тогда
		Индекс = Индекс + 10;
		Описание = Сред(Описание, Индекс);
	КонецЕсли;
	
	ЖурналДействийКассира = РегистрыСведений.ЖурналРегистрацииДействийКассира.СоздатьМенеджерЗаписи();
	
	ЖурналДействийКассира.Пользователь 	= ?(Автор.Пустая(),ПараметрыСеанса.Пользователь,Автор);
	ЖурналДействийКассира.Оборудование 	= ?(ЗначениеЗаполнено(ФР),ФР,ПараметрыСеанса.Компьютер.ФР);
	ЖурналДействийКассира.Источник 		= ПараметрыСеанса.Компьютер;
	ЖурналДействийКассира.GUID 			= GUID;
	ЖурналДействийКассира.Дата 			= ТекущаяДата();
	ЖурналДействийКассира.Действие 		= Команда;
	ЖурналДействийКассира.Код 			= Результат;
	ЖурналДействийКассира.Описание 		= Строка(Описание);
	ЖурналДействийКассира.Объект 		= Ссылка;
	ЖурналДействийКассира.Тип 			= ТипСообщения;
	
	ЖурналДействийКассира.Записать();

КонецПроцедуры

Функция ПреобразоватьТаблицуОплат(ТекстОшибки, ТаблицаОплат, тзОплаты)
	
	Преобразована = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Оплаты.ТипОплаты КАК Справочник.ТипыОплат) КАК ТипыОплат,
	|	Оплаты.Сумма
	|ПОМЕСТИТЬ втОплаты
	|ИЗ
	|	&Оплаты КАК Оплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОплаты.ТипыОплат.Представление КАК Представление,
	|	втОплаты.ТипыОплат.ТипОплатыККТ КАК ТипОплатыККТ,
	|	втОплаты.Сумма
	|ИЗ
	|	втОплаты КАК втОплаты";
	Запрос.УстановитьПараметр("Оплаты", тзОплаты);
	
	Результат = Запрос.Выполнить();
	
	СуммаНаличные = 0;
	СуммаЭлектронно = 0;
	СуммаПредоплата = 0;
	СуммаПостоплата = 0;
	СуммаПредоставление = 0;
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл 
			Если Выборка.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Наличные Тогда
				СуммаНаличные = СуммаНаличные + Выборка.Сумма;
			ИначеЕсли Выборка.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Электронно Тогда
				СуммаЭлектронно = СуммаЭлектронно + Выборка.Сумма;
			ИначеЕсли Выборка.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Предоплата Тогда
				СуммаПредоплата = СуммаПредоплата + Выборка.Сумма;
			ИначеЕсли Выборка.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Постоплата Тогда
				СуммаПостоплата = СуммаПостоплата + Выборка.Сумма;
			ИначеЕсли Выборка.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.ВстречноеПредоставление Тогда
				СуммаПредоставление = СуммаПредоставление + Выборка.Сумма;
			ИначеЕсли НЕ ЗначениеЗаполнено(Выборка.ТипОплатыККТ) Тогда
				Если ПустаяСтрока(ТекстОшибки) Тогда
					ТекстОшибки = "Для типа оплаты """+ Выборка.Представление + """ не указан ""Тип оплаты ККТ""";
				Иначе
					ТекстОшибки = ТекстОшибки + Символы.ПС + "Для типа оплаты """+ Выборка.Представление + """ не указан ""Тип оплаты ККТ""";
				КонецЕсли;
				Преобразована = Ложь;
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ Преобразована Тогда
			Возврат Преобразована;
		КонецЕсли;
		
		Если ТаблицаОплат = Неопределено Тогда
			ТаблицаОплат = Новый Соответствие;
		КонецЕсли;
		
		// Наличные
		Если СуммаНаличные > 0 Тогда
			ТаблицаОплат.Вставить(0, СуммаНаличные);
		КонецЕсли;
		
		// Платежная карта
		Если СуммаЭлектронно > 0 Тогда
			ТаблицаОплат.Вставить(1, СуммаЭлектронно);
		КонецЕсли;
		
		// Подарочный сертификат
		Если СуммаПредоплата > 0 Тогда
			ТаблицаОплат.Вставить(2, СуммаПредоплата);
		КонецЕсли;
		
		// Рассрочка
		Если СуммаПостоплата > 0 Тогда
			ТаблицаОплат.Вставить(3, СуммаПостоплата);
		КонецЕсли;
		
		// Встречное предоставление
		Если СуммаПредоставление > 0 Тогда
			ТаблицаОплат.Вставить(4, СуммаПредоставление);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Преобразована;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УСТАНОВКИ ОБЩЕЙ СКИДКИ/НАЦЕНКИ

// Получение суммы табличной части документа без скидки
// Параметры
//  ЭтотОбъект	– ДокументОбъект – документ, сумму которого необходимо получить
//  Товары		– ТабличнаяЧасть – табличная часть документа, сумму по которой необходимо получить
// Возвращаемое значение:
//   Число		– Сумма документа без скидки
Функция ПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, Знач Товары = Неопределено, МассивСтрокСВытеснением = Неопределено, ИмяТЧ ="")
	
	СуммаДокументаБезСкидки = орПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, ИмяТЧ, МассивСтрокСВытеснением);
	Если СуммаДокументаБезСкидки <> Неопределено Тогда
		Возврат СуммаДокументаБезСкидки;
	КонецЕсли; 
	
	// Получим табличную часть документа
	Попытка
		Товары = ?(Товары = Неопределено, ЭтотОбъект.Товары, Товары);
	Исключение КонецПопытки;
	
	// Если так и не удалось получить табличную часть или она не содержит ни одной строки, то сумма документа равна "0"
	Если (Товары = Неопределено)ИЛИ(Товары.Количество() = 0) Тогда
		Возврат 0;
	КонецЕсли;
	
	ЕстьВытеснение = Ложь;
	Если ТипЗнч(МассивСтрокСВытеснением) = Тип("Массив") Тогда
		ЕстьВытеснение = Истина;
	КонецЕсли;
	
	// Определим включают ли в себя НДС цены документа
	ЦенаВключаетНДС = ИСТИНА;
	Попытка
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
	Исключение
	КонецПопытки;
	
	СуммаДокументаБезСкидки = 0;
	Попытка
		Если ЕстьВытеснение Тогда
			КоличествоСтрок = Товары.Количество()-1;
			Для Инд = 0 По КоличествоСтрок Цикл
				Если НЕ МассивСтрокСВытеснением.Найти(Инд) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ТекСтрока = Товары[Инд];
				Если ЦенаВключаетНДС Тогда
					// Получим сумму документу без учета НДС и скидки
					СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ТекСтрока.Сумма;
				Иначе
					//Если цена не включает НДС, то получим суммарную НДС по всем строкам
					СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ТекСтрока.Сумма + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
				КонецЕсли;
			КонецЦикла;
		Иначе
			// Получим сумму документу без учета НДС и скидки
			СуммаДокументаБезСкидки = Товары.Итог("Сумма");
			//Если цена не включает НДС, то получим суммарную НДС по всем строкам
			Если НЕ ЦенаВключаетНДС Тогда
				Попытка
					Для Каждого ТекСтрока Из Товары Цикл
						СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
					КонецЦикла;
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Возврат СуммаДокументаБезСкидки;
	
КонецФункции // ПолучитьСуммуДокументаБезСкидки()

// Функция применяет полученные параметры скидки шапки к указанной табличной части документа, а также производит
// поиск и установку товарных скидок для каждой строки
//
// Параметры:
//	ЭтотОбъект   - ДокументОбъект - Документ, для которого производится поиск скидки
//  ИмяТаблицы   – Строка         - Имя табличной части, к которой необходимо применить скидку
//  ЭтаФорма     - Форма          - Форма документа, для которого производится поиск скидки
//	ДопПараметры - Произвольный   - Дополнительные параметры, которые могут использоваться при расчете табличной части
//
// Возвращаемое значение:
//	БУЛЕВО – Признак успешного завершения действия
//
Функция ПрименитьСкидкиКТабличнойЧасти(ДопПараметры=Неопределено)
	// Если таблица не содержит ни одной строки, то нет смысла в дальнейшем расчете
	Если Товары.Количество()=0 Тогда
		СуммаДокумента = 0;
		Возврат ИСТИНА;
	КонецЕсли;
	
	// Получим право управляющее способом назначения скидок
	ВсеСкидкиЗапрещены	= (фкСпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.СкидкиЗапрещены);
	ТолькоРучныеСкидки	= (фкСпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.РучныеСкидки)ИЛИ(фкСпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов);
	ТолькоАвтоматСкидки	= (фкСпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.АвтоматическиеСкидки);
	
	// Формируем и заполняем структуру отбора
	Отбор = Новый Структура("ФронтОфис,ЭтоПодарок,БэкОфис,Терминал,ПодразделениеКомпании,СкладКомпании,Карточка", ИСТИНА, ?(фкЗапретитьПодарки,ЛОЖЬ,Неопределено));
	ЗаполнитьЗначенияСвойств(Отбор, ЭтотОбъект);
	Отбор.Вставить("СкидкаНаТовары",Истина);
	
	// Дополним структуру отбора дополнительными значениями из переменной ДопПараметры
	Попытка
		ЗаполнитьЗначенияСвойств(Отбор, ДопПараметры);
	Исключение КонецПопытки;
	
	// Обходим табличную часть: расставляем скидки строк, учитывая что общая скидка может их вытеснять
	СкидкаШапкиВытесняющая = СкидкаНаценка.ФлагВытеснения;
	Для Каждого ТекСтрока Из Товары Цикл
		Сумма = дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока);
		// Проверим имеем ли мы право назначать данную скидку
		РучнаяСкидка = ТекСтрока.СкидкаНаТовар.РучнаяСкидка;
		Если ВсеСкидкиЗапрещены ИЛИ (ТолькоРучныеСкидки И (НЕ РучнаяСкидка)) ИЛИ (ТолькоАвтоматСкидки И РучнаяСкидка) Тогда
			ТекСтрока.СкидкаНаТовар	= Справочники.ТипыСкидок.ПустаяСсылка();
			РучнаяСкидка			= ИСТИНА;
		КонецЕсли;
		
		// Устанавливается новая скидка. Получим значение скидки для данной строки
		Отбор.Вставить("СкладКомпании",	?(обЗначениеНеЗаполнено(ТекСтрока.МестоРазмещения), ЭтотОбъект.СкладКомпании, ТекСтрока.МестоРазмещения));
		Отбор.Вставить("Скидка",		?(РучнаяСкидка, ТекСтрока.СкидкаНаТовар, Неопределено));
		Отбор.Вставить("РучнаяСкидка",	РучнаяСкидка);
		Отбор.Вставить("Объект",		ТекСтрока.Номенклатура);
		Отбор.Вставить("СуммаСтроки",	Сумма);
		Отбор.Вставить("Количество",	ТекСтрока.Количество * ТекСтрока.Коэффициент);
		Отбор.Вставить("БазаРасчета",	Сумма);
		Отбор.Вставить("СкидкаНаТовары",Истина);
		ПараметрыСкидкиСтроки = рсСкидкиПолучитьСтрочные(ЭтотОбъект.Дата, Отбор);
		
		// Обработаем результат получения параметров скидки строки
		Установить = (ПараметрыСкидкиСтроки <> Неопределено) И ((НЕ СкидкаШапкиВытесняющая) ИЛИ ПараметрыСкидкиСтроки.ФлагВытеснения ИЛИ ПараметрыСкидкиСтроки.ЭтоПодарок);
		Если Установить Тогда
			ТекСтрока.СкидкаНаТовар			= ПараметрыСкидкиСтроки.Скидка;
			ТекСтрока.СуммаСкидкиСтроки		= МИН(ПараметрыСкидкиСтроки.СуммаСкидки, Сумма);
			ТекСтрока.СуммаСкидкиСтроки		= дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидкиСтроки,ТекСтрока.СкидкаНаТовар);
			Если ПараметрыСкидкиСтроки.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
				ТекСтрока.ПроцентСкидкиСтроки = ПараметрыСкидкиСтроки.ЗначениеСкидки;
			Иначе
				ТекСтрока.ПроцентСкидкиСтроки = ?(ТекСтрока.Сумма = 0, 0, ТекСтрока.СуммаСкидкиСтроки / Сумма) * 100;
			КонецЕсли;
		Иначе
			ТекСтрока.СкидкаНаТовар			= ?(ТекСтрока.СкидкаНаТовар.РучнаяСкидка, ТекСтрока.СкидкаНаТовар, Справочники.ТипыСкидок.ПустаяСсылка());
			ТекСтрока.СуммаСкидкиСтроки		= 0;
			ТекСтрока.ПроцентСкидкиСтроки	= 0;
		КонецЕсли;
	КонецЦикла;
	
	// Получим общие суммовые показатели по документу
	Если ТипЦен.ЦенаВключаетНДС Тогда
		СуммаДокумента = Товары.Итог("Сумма");
	Иначе
		СуммаДокумента = ПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, ЭтотОбъект.Товары);
	КонецЕсли;
	
	СуммаДляСкидки = СуммаДокумента - Товары.Итог("СуммаСкидкиСтроки");
	
	// Определим основные показатели скидки шапки
	Если обЗначениеНеЗаполнено(СкидкаНаценка) ИЛИ (НЕ ТипЗнч(ПараметрыСкидкиШапки)=Тип("ТаблицаЗначений")) ИЛИ (ПараметрыСкидкиШапки.Количество()=0) ИЛИ (ПараметрыСкидкиШапки[0].СуммаЧека>СуммаДокумента) Тогда
		Процентная		= ИСТИНА;
		ЗначениеСкидки	= 0;
		ПроцентСкидки	= 0;
		
	Иначе
		Процентная		=(ПараметрыСкидкиШапки[0].СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная);
		ЗначениеСкидки	= ПараметрыСкидкиШапки[0].ЗначениеСкидки;
			
		// Определим на какой процент делается скидка по отношению к оставшейся сумме для скидок документа
		Если Процентная Тогда
			ПроцентСкидки = ?(ЗначениеСкидки > 100, 100, ?(ЗначениеСкидки < -100, -100, ЗначениеСкидки));
		Иначе
			Если СуммаДляСкидки=0 Тогда
				ЗначениеСкидки= 0;
				ПроцентСкидки = 0;
			Иначе
				Если ЗначениеСкидки >= 0 Тогда
					ЗначениеСкидки =  МАКС(МИН(ЗначениеСкидки, СуммаДляСкидки), 0);
				КонецЕсли;
				ПроцентСкидки = ЗначениеСкидки / СуммаДокумента * 100;//СуммаДляСкидки
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Устанавливаем общую скидку на каждую строку документа
	Для Каждого ТекСтрока Из Товары Цикл
		Сумма = дкПолучитьСуммуСтрокиБезСкидки(ЭтотОбъект,ТекСтрока);
		СуммаСкидкиСтроки = ?(ТекСтрока.СкидкаНаТовар.ФлагВытеснения, Сумма, ТекСтрока.СуммаСкидкиСтроки);
		
		ТекСтрока.СуммаСкидки	= МИН(ПроцентСкидки / 100 * Сумма, Сумма-СуммаСкидкиСтроки);//ПроцентСкидки / 100 * (ТекСтрока.Сумма-СуммаСкидкиСтроки);
		ТекСтрока.СуммаСкидки	= дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценка);
		ТекСтрока.ПроцентСкидки	= ?(ТекСтрока.СуммаСкидки = 0, 0, ПроцентСкидки);
	КонецЦикла;
	ОстатокСкидки = ?(Процентная, 0, ЗначениеСкидки - Товары.Итог("СуммаСкидки"));
	
	// Выполним расчет прочих суммовых реквизитов таблицы, также перераспределим
	// "последнюю копейку" в случае абсолютной скидки
	Для Каждого ТекСтрока Из Товары Цикл
		// Если скидка абсолютная, и еще остались не распределенные "копейки", то произведем уточнение суммы скидки
		Если ОстатокСкидки <> 0 Тогда
			СуммаСкидкиСтроки = ?(ТекСтрока.СкидкаНаТовар.ФлагВытеснения, ТекСтрока.Сумма, ТекСтрока.СуммаСкидкиСтроки);
			
			ДопСкидка = МИН(ТекСтрока.Сумма-ТекСтрока.СуммаСкидки-СуммаСкидкиСтроки,ОстатокСкидки);
			ДопСкидка = ?(ЗначениеСкидки >= 0, МАКС(ДопСкидка,-ТекСтрока.СуммаСкидки), МИН(ДопСкидка,-ТекСтрока.СуммаСкидки));
			ОстатокСкидки = ОстатокСкидки - ДопСкидка;
			
			ТекСтрока.СуммаСкидки	= ТекСтрока.СуммаСкидки + ДопСкидка;
			ТекСтрока.СуммаСкидки	= дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценка);
			ТекСтрока.ПроцентСкидки	= ?(ТекСтрока.Сумма = 0, 0, 100 * ТекСтрока.СуммаСкидки / ТекСтрока.Сумма);
		КонецЕсли;
		
		// Выполним расчет прочих реквизитов строки
		СуммаСоСкидкой = дкПолучитьСуммуСтрокиСоСкидкой(ЭтотОбъект,ТекСтрока);
		СуммаСоСкидкой = СуммаСоСкидкой - ТекСтрока.СуммаСкидкиБонусами;
		Попытка
			СтавкаНДС = ТекСтрока.СтавкаНДС.Ставка;
			ТекСтрока.СуммаНДС = Окр((СуммаСоСкидкой * СтавкаНДС)/(100 + СтавкаНДС),2);
		Исключение
		КонецПопытки;
		ТекСтрока.СуммаВсего = СуммаСоСкидкой;
		
	КонецЦикла;
	
	// Установим значение итоговой суммы документа
	СуммаДокумента = Товары.Итог("СуммаВсего");
	
	// Заполним в переменные значения шапочных скидок в КЭШ
	
	ЗначениеСкидкиНаценкиКЕШ = ПроцентСкидки;
	СуммаСкидкиНаценкиКЕШ    = Товары.Итог("СуммаСкидки");
	
	Возврат ИСТИНА;
КонецФункции // ПрименитьСкидкиКТабличнойЧасти()

Функция ДекодироватьДанныеСчитывателяМК(Данные)
	Символ1="ж"; // Русская раскладка
	НачПозиция=Найти(Данные,Символ1);
	Если НачПозиция=0 Тогда
		Символ1=";"; // Английская раскладка
		НачПозиция=Найти(Данные,Символ1);
	КонецЕсли;
	Если НачПозиция>0 Тогда // Данные на второй дорожке и отделены старт/стоп символами
		КонПозиция=Найти(Данные,?(Символ1="ж",",","?"));
		Возврат Сред(Данные,НачПозиция+1,КонПозиция-НачПозиция-1);
	Иначе // Ридер не передает старт/стоп символы, возвращаем данные как есть
		Возврат Данные;
	КонецЕсли;		
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С АРХИВОМ ЧЕКОВ

// Функция возвращает признак ведения архива чеков на данном рабочем месте
//
// Возвращаемое значение:
//   Булево   - Признак ведения архива чеков
//
Функция АрхивЧековАктивен() Экспорт
	Если НЕ ЗначениеЗаполнено(Компьютер) Тогда
		Попытка
			Компьютер = ПараметрыСеанса.Компьютер;
		Исключение
			Компьютер = Справочники.Компьютеры.НайтиПоНаименованию(ИмяКомпьютера(), ИСТИНА);
		КонецПопытки;
	КонецЕсли;
	Возврат Компьютер.ВестиАрхивЧеков;
КонецФункции // АрхивЧековАктивен() 

// Функция возвращает строку содержащую путь к каталогу архива чеков
//
// Параметры:
//   Компьютер - СправочникСсылка.Компьютеры - Компьютер, локальный каталог архива чеков которого необходимо получить
//
// Возвращаемое значение:
//   Строка – Каталог архива чеков
//
Функция АрхивЧековПолучитьКаталогАрхиваЧеков(Компьютер=Неопределено) Экспорт
	Если НЕ ЗначениеЗаполнено(Компьютер) Тогда
		Попытка
			Компьютер = ПараметрыСеанса.Компьютер;
		Исключение
			Компьютер = Справочники.Компьютеры.НайтиПоНаименованию(ИмяКомпьютера(), ИСТИНА);
		КонецПопытки;
	КонецЕсли;
	
	КаталогОбщий = Новый Файл(Компьютер.ЛокальныйКаталог);
	Если КаталогОбщий.Существует() Тогда
		Возврат КаталогОбщий.Путь + "ARCHIVE\";
	Иначе
		Возврат АрхивЧековКаталог;
	КонецЕсли;
КонецФункции // АрхивЧековПолучитьКаталогАрхиваЧеков()

// Функция производит проверку необходимости выполнения обновления архива чеков
//
// Параметры:
//   ВыполнитьОбновление - Булево - признак необходимости произвести обновление
//
// Возвращаемое значение:
//   Булево – Статус выполненной операции
//
Функция АрхивЧековВыполнитьОбновление(ВыполнитьОбновление=ИСТИНА) Экспорт
	Перем Версия;
	
	// Получим каталог архива чеков
	АрхивЧековКаталог = АрхивЧековПолучитьКаталогАрхиваЧеков();
	
	// Получим значение дополнительных параметров архива чеков
	Попытка
		Параметры = Неопределено;
		Параметры = ЗначениеИзФайла(АрхивЧековКаталог+"config.dat");
	Исключение КонецПопытки;
	
	// Производим анализ считанных данных
	Если ТипЗнч(Параметры)=Тип("Структура") Тогда
		Параметры.Свойство("Версия", Версия);
	Иначе
		Параметры = Новый Структура;
	КонецЕсли;
	Если Версия=Неопределено Тогда
		Версия = "";
	КонецЕсли;
	
	// Производим подключение основной таблицы архива
	Если ТипЗнч(АрхивЧековДокументы)=Тип("XBase") И АрхивЧековДокументы.Открыта() Тогда
		// Таблица подключена
	Иначе
		АрхивЧековДокументы = Новый XBase;
		АрхивЧековКаталог   = АрхивЧековПолучитьКаталогАрхиваЧеков();
		АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
		
		// Проверяем статус открытия таблицы
		Если НЕ АрхивЧековДокументы.Открыта() Тогда
			Сообщить("ВНИМАНИЕ! Невозможно открыть файл основной таблицы архива чеков: archive.dbf"+Символы.ПС+ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
			Возврат ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	
	// Получим признак необходимости произвести реструктуризацию таблицы
	ВыполнитьРеструктуризацию = (АрхивЧековДокументы.Поля.Найти("DOC")=Неопределено);
	
	// Возвращаем признак необходимости выполнения обновления
	Если НЕ ВыполнитьОбновление Тогда
		Возврат НЕ((Версия < АрхивЧековВерсияАрхиваЧеков) ИЛИ ВыполнитьРеструктуризацию);
	КонецЕсли;
	
	// Изначально установим оптимистичных статус выполнения обновления
	Результат = ИСТИНА;
	
	// Прежде всего необходимо выполнить реструктуризацию таблиц архива чеков
	Если Результат И ВыполнитьРеструктуризацию Тогда
		ИмяФайлаТаблицы = ПРАВ(СтрЗаменить(Новый УникальныйИдентификатор(),"-",""), 8)+".dbf";
		
		// Основная таблица будет обновлена, необходимо удалить старый индексный файл
		Попытка
			УдалитьФайлы(АрхивЧековКаталог+"archive.cdx");
		Исключение
			Сообщить("ВНИМАНИЕ! Невозможно выполнить удаление устаревшего файла индексов: archive.cdx"+Символы.ПС+ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
			Возврат ЛОЖЬ;
		КонецПопытки;
		
		// Создаем новую таблицу архива чеков
		АрхивЧековТаблица = Новый XBase;
		АрхивЧековТаблица.ОткрытьФайл(ИмяФайлаТаблицы);
		АрхивЧековТаблица.Кодировка = КодировкаXBase.ANSI;
		АрхивЧековТаблица.Поля.Добавить("ID",      "S", 32);
		АрхивЧековТаблица.Поля.Добавить("DATE",    "S", 14);
		АрхивЧековТаблица.Поля.Добавить("DOC",     "S", 10);
		АрхивЧековТаблица.Поля.Добавить("ZNUM",    "S", 06);
		АрхивЧековТаблица.Поля.Добавить("NUM",     "S", 06);
		АрхивЧековТаблица.Поля.Добавить("MODE",    "S", 01);
		АрхивЧековТаблица.Поля.Добавить("ID_CASH", "S", 32);
		АрхивЧековТаблица.Поля.Добавить("ID_CARD", "S", 32);
		АрхивЧековТаблица.Поля.Добавить("COST",    "S", 16);
		
		// Создаем файл новой таблицы архива чеков
		Попытка
			АрхивЧековТаблица.СоздатьФайл(АрхивЧековКаталог+ИмяФайлаТаблицы);
		Исключение КонецПопытки;
		
		// Проверяем статус создания таблицы
		Если НЕ АрхивЧековТаблица.Открыта() Тогда
			Сообщить("ВНИМАНИЕ! Невозможно выполнить реструктуризацию файла основной таблицы архива чеков: archive.dbf"+Символы.ПС+ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
			Возврат ЛОЖЬ;
		КонецЕсли;
		Сообщить("Производится реструктуризация таблиц архива чеков...");
		
		// Проверим результат подключения таблицы
		СтатусЧтения = АрхивЧековДокументы.Открыта() И АрхивЧековДокументы.Первая();
		
		// Производим последовательное чтение таблицы архива чеков
		Пока СтатусЧтения Цикл
			Состояние("Обновление записи "+АрхивЧековДокументы.НомерЗаписи()+"/"+АрхивЧековДокументы.КоличествоЗаписей());
			
			// Получим объект чека
			Если АрхивЧековДокументы.MODE="1" Тогда
				Объект = АрхивЧековВосстановить(АрхивЧековДокументы.ID+Символы.ПС+АрхивЧековДокументы.DATE);
			Иначе
				Объект = Неопределено;
			КонецЕсли;
			
			// Добавляем новую запись в таблицу архива чеков
			АрхивЧековТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(АрхивЧековТаблица, АрхивЧековДокументы, "ID,DATE,NUM,MODE,ID_CASH,ID_CARD,COST");
			АрхивЧековТаблица.DOC  = ?(Объект=Неопределено, "0000000000", Формат(МАКС(0, Объект.НомерДокумента), "ЧЦ=10; ЧН=0000000000; ЧВН=; ЧГ=0"));
			АрхивЧековТаблица.ZNUM = ?(Объект=Неопределено, "000000",     Формат(МАКС(0, Объект.НомерСмены),     "ЧЦ= 6; ЧН=    000000; ЧВН=; ЧГ=0"));
			АрхивЧековТаблица.Записать();
			
			// Производим чтение следующей записи таблицы
			СтатусЧтения = АрхивЧековДокументы.Следующая();
		КонецЦикла;
		
		// Выполним отключение таблицы
		АрхивЧековДокументы.ЗакрытьФайл();
		АрхивЧековТаблица  .ЗакрытьФайл();
		
		// Переименовываем файл основной таблицы
		Попытка
			УдалитьФайлы(АрхивЧековКаталог+"archive.bak");
			ПереместитьФайл(АрхивЧековКаталог+"archive.dbf",   АрхивЧековКаталог+"archive.bak");
			ПереместитьФайл(АрхивЧековКаталог+ИмяФайлаТаблицы, АрхивЧековКаталог+"archive.dbf");
		Исключение
			Сообщить("ВНИМАНИЕ! Невозможно выполнить реструктуризацию файла основной таблицы архива чеков: archive.dbf"+Символы.ПС+ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
			Возврат ЛОЖЬ;
		КонецПопытки;
		
		// Производим переиндексацию архива чеков
		Сообщить("Выполняется переиндексация архива чеков...", СтатусСообщения.БезСтатуса);
		Результат = АрхивЧековПереиндексация();
		
		// Выводим разделительную черту
		Сообщить("--------------------------------------------------------");
	КонецЕсли;
	
	// Обновление архива чеков до версии 1.03
	Если Результат И Версия < "1.03" Тогда
		Сообщить("Производится обновление архива чеков до версии 1.03");
		
		// (А) Если в каталоге "CHECKS" расположены чеки, то их необходимо распределить по группам
		НеРаспределенныеЧеки = НайтиФайлы(АрхивЧековКаталог+"CHECKS\", "*.xml");
		Если НеРаспределенныеЧеки.Количество()>0 Тогда
			Сообщить("Выполняется реструктуризация каталога архива чеков...", СтатусСообщения.БезСтатуса);
			
			// Определим состояние подключения основной таблицы архива чеков
			Если ТипЗнч(АрхивЧековДокументы)=Тип("XBase") И АрхивЧековДокументы.Открыта() Тогда
				АрхивЧековДокументы.ЗакрытьФайл();
			КонецЕсли;
			
			// Инициализация объекта таблицы
			АрхивЧековДокументы = Новый XBase;
			АрхивЧековКаталог   = АрхивЧековПолучитьКаталогАрхиваЧеков();
			АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
			
			// Проверим результат подключения таблицы
			СтатусЧтения = АрхивЧековДокументы.Открыта() И АрхивЧековДокументы.Первая();
			
			// Производим последовательное чтение таблицы архива чеков
			Пока СтатусЧтения Цикл
				GUID_Чек = АрхивЧековДокументы.ID;
				DATE_Чек = Дата(?(ПустаяСтрока(АрхивЧековДокументы.DATE),"00010101000000",АрхивЧековДокументы.DATE));
				
				// При необходимости обновляем текущую запись архива чеков
				Если НЕ ЗначениеЗаполнено(DATE_Чек) Тогда
					// Попытаемся получить дату соответствующего файла образа чека
					ФайлОбъекта = Новый Файл(АрхивЧековКаталог+"CHECKS\"+GUID_Чек+".xml");
					DATE_Чек = ?(ФайлОбъекта.Существует() И ФайлОбъекта.ЭтоФайл(), ФайлОбъекта.ПолучитьВремяИзменения(), ТекущаяДата());
					
					// Обновляем запись таблицы
					АрхивЧековДокументы.DATE = Формат(DATE_Чек,"ДФ=yyyyMMddHHmmss");;
					АрхивЧековДокументы.Записать();
				КонецЕсли;
				
				// Создадим каталог группы чеков и переместим в него найденный файл
				Попытка
					СоздатьКаталог(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd"));
				Исключение
					Сообщить("ВНИМАНИЕ!!! Невозможно создать локальный каталог группы архива чеков. Возможно диск защищен от записи.");
				КонецПопытки;
				Попытка
					ПереместитьФайл(АрхивЧековКаталог+"CHECKS\"+GUID_Чек+".xml", АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+".xml");
				Исключение КонецПопытки;
				Попытка
					ПереместитьФайл(АрхивЧековКаталог+"CHECKS\"+GUID_Чек+"_Свойства.xml", АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+"_Свойства.xml");
				Исключение КонецПопытки;
				
				// Производим чтение следующей записи таблицы
				СтатусЧтения = АрхивЧековДокументы.Следующая();
			КонецЦикла;
			
			// Выполним отключение таблицы
			АрхивЧековДокументы.ЗакрытьФайл();
			
			// Выполняем проверку результата реструктуризации архива
			НеРаспределенныеЧеки = НайтиФайлы(АрхивЧековКаталог+"CHECKS\", "*.xml");
			Если НЕ НеРаспределенныеЧеки.Количество()=0 Тогда
				Сообщить("Не все чеки удалось распределить по группам! Проверьте содержимое каталога архива чеков.", СтатусСообщения.Внимание);
			КонецЕсли;
		КонецЕсли;
		
		// (Б) Выполняем обновление свойств для уже пробитых чеков
		Сообщить("Выполняется помещение свойств ранее пробитых чеков в архив...", СтатусСообщения.БезСтатуса);
		
		// Определим состояние подключения основной таблицы архива чеков
		Если ТипЗнч(АрхивЧековДокументы)=Тип("XBase") И АрхивЧековДокументы.Открыта() Тогда
			АрхивЧековДокументы.ЗакрытьФайл();
		КонецЕсли;
		
		// Инициализация объекта таблицы
		АрхивЧековДокументы = Новый XBase;
		АрхивЧековКаталог   = АрхивЧековПолучитьКаталогАрхиваЧеков();
		АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
		
		// Проверим результат подключения таблицы
		СтатусЧтения = АрхивЧековДокументы.Открыта() И АрхивЧековДокументы.Первая();
		
		// Производим последовательное чтение таблицы архива чеков
		Пока СтатусЧтения Цикл
			Если АрхивЧековДокументы.MODE="1" Тогда
				GUID_Чек = АрхивЧековДокументы.ID;
				DATE_Чек = Дата(АрхивЧековДокументы.DATE);
				
				// Получаем ссылку на пробитый чек
				ЧекСсылка = Документы.Чек.ПолучитьСсылку(Новый УникальныйИдентификатор(Сред(GUID_Чек,25,8)+"-"+Сред(GUID_Чек,21,4)+"-"+Сред(GUID_Чек,17,4)+"-"+Сред(GUID_Чек,1,4)+"-"+Сред(GUID_Чек,5,12)));
				
				// Производим сохранение свойств чека
				Если ЗначениеЗаполнено(ЧекСсылка.Дата) Тогда
					Попытка
						// Производим удаление ранее созданного файла
						УдалитьФайлы(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+"_Свойства.xml");
						
						// Получаем набор свойств
						НаборЗаписей = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьНаборЗаписей();
						НаборЗаписей.Отбор.Объект.Установить(ЧекСсылка, ИСТИНА);
						НаборЗаписей.Прочитать();
						
						// Производим запись набора свойств
						ЗаписьXML = Новый ЗаписьXML;
						ЗаписьXML.ОткрытьФайл(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+"_Свойства.xml");
						ЗаписьXML.ЗаписатьОбъявлениеXML();
						ЗаписатьXML(ЗаписьXML, НаборЗаписей);
						ЗаписьXML.Закрыть();
					Исключение
						Сообщить("ВНИМАНИЕ!!! Ошибка доступа к файлу: "+АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+"_Свойства.xml"+Символы.ПС+"Возможно диск защищен от записи.", СтатусСообщения.Внимание);
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;
			
			// Производим чтение следующей записи таблицы
			СтатусЧтения = АрхивЧековДокументы.Следующая();
		КонецЦикла;
		
		// Выполним отключение таблицы
		АрхивЧековДокументы.ЗакрытьФайл();
		
		// (В) Производим переиндексацию архива чеков
		Сообщить("Выполняется переиндексация архива чеков...", СтатусСообщения.БезСтатуса);
		Результат = АрхивЧековПереиндексация();
		
		// Выводим разделительную черту
		Сообщить("--------------------------------------------------------");
		
		// Обновляем значение версии архива чеков
		Версия = ?(Результат, "1.03", Версия);
	КонецЕсли;
	
	//// Обновление архива чеков до версии 1.04
	//Если Результат И Версия < "1.04" Тогда
	//	Сообщить("Производится обновление архива чеков до версии 1.04");
	//	
	//	// Производим переиндексацию архива чеков
	//	Сообщить("Выполняется переиндексация архива чеков...", СтатусСообщения.БезСтатуса);
	//	Результат = АрхивЧековПереиндексация();
	//	
	//	// Выводим разделительную черту
	//	Сообщить("--------------------------------------------------------");
	//	
	//	// Обновляем значение версии архива чеков
	//	Версия = ?(Результат, "1.04", Версия);
	//КонецЕсли;
	
	// Производим сохранение параметров архива чеков
	Параметры.Вставить("Версия", Версия);
	ЗначениеВФайл(АрхивЧековКаталог+"config.dat", Параметры);
	
	// Возвращаем статус
	Возврат Результат;
КонецФункции // АрхивЧековВыполнитьОбновление()

// Функция производит формирование нового или подключение к существующему архиву чеков, его
// переиндексацию и подготовку к работе
//
// Возвращаемое значение:
//   Булево – Статус выполненной операции
//
Функция АрхивЧековИнициализация() Экспорт
	АрхивЧековПодключен = Ложь;
	Если НЕ АрхивЧековАктивен() Тогда
		Возврат Истина;
	КонецЕсли; 
	АрхивЧековКаталог = АрхивЧековПолучитьКаталогАрхиваЧеков();
	Попытка
		СоздатьКаталог(АрхивЧековКаталог);
	Исключение
		Сообщить("ВНИМАНИЕ!!! Невозможно создать локальный каталог архива чеков. Возможно диск защищен от записи.");
		АрхивЧековФинализация();
		Возврат ЛОЖЬ;
	КонецПопытки;
	
	// Инициализация объектов таблиц
	АрхивЧековДокументы = Новый XBase;
	АрхивЧековТовары    = Новый XBase;
	
	////////////////////////////////////////////////////////////////////////////////
	// ПОДКЛЮЧЕНИЕ ОСНОВНОЙ ТАБЛИЦЫ
	//
	// Откроем 
	АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf", АрхивЧековКаталог+"archive.cdx");		
	Если НЕ АрхивЧековДокументы.Открыта() Тогда
		АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
	КонецЕсли;
	
	// Проверим результат подключения таблицы
	Если АрхивЧековДокументы.Открыта() Тогда
		// Если основная таблица существует, то необходимо проверить версию архива чеков
		Если НЕ АрхивЧековВыполнитьОбновление(ЛОЖЬ) Тогда
			Сообщить("ВНИМАНИЕ! Обнаружено изменение формата архива чеков! Необходимо открыть форму ""Просмотр архива чеков"" и произвести обновление данных архива чеков.", СтатусСообщения.ОченьВажное);
			АрхивЧековФинализация();
			Возврат ЛОЖЬ;
		КонецЕсли;
	Иначе
		// Проверим существует ли файл архива, если нет, то создадим его
		АрхивЧековФайл = Новый Файл(АрхивЧековКаталог+"archive.dbf");
		Если АрхивЧековФайл.Существует() Тогда
			Сообщить("ВНИМАНИЕ! Невозможно открыть файл основной таблицы архива чеков: archive.dbf. Возможно таблица заблокирована другой задачей.", СтатусСообщения.ОченьВажное);
			АрхивЧековФинализация();
			Возврат ЛОЖЬ;
		Иначе
			// Основной таблицы нет, на всякий случай необходимо удалить и старый индексный файл
			Попытка
				УдалитьФайлы(АрхивЧековКаталог+"archive.cdx");
			Исключение
				Сообщить("ВНИМАНИЕ! Невозможно выполнить удаление устаревшего файла индексов: archive.cdx"+Символы.ПС+ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
				АрхивЧековФинализация();
				Возврат ЛОЖЬ;
			КонецПопытки;
			
			// Создаем основную таблицу архива чеков
			АрхивЧековДокументы.Кодировка = КодировкаXBase.ANSI;
			АрхивЧековДокументы.Поля.Добавить("ID",      "S", 32);
			АрхивЧековДокументы.Поля.Добавить("DATE",    "S", 14);
			АрхивЧековДокументы.Поля.Добавить("DOC",     "S", 10);
			АрхивЧековДокументы.Поля.Добавить("ZNUM",    "S", 06);
			АрхивЧековДокументы.Поля.Добавить("NUM",     "S", 06);
			АрхивЧековДокументы.Поля.Добавить("MODE",    "S", 01);
			АрхивЧековДокументы.Поля.Добавить("ID_CASH", "S", 32);
			АрхивЧековДокументы.Поля.Добавить("ID_CARD", "S", 32);
			АрхивЧековДокументы.Поля.Добавить("COST",    "S", 16);
			
			// Создаем файл таблицы архива чеков
			Попытка
				СоздатьКаталог(АрхивЧековКаталог);
				АрхивЧековДокументы.СоздатьФайл(АрхивЧековКаталог+"archive.dbf");
			Исключение КонецПопытки;
			
			// Проверяем статус создания таблицы
			Если НЕ АрхивЧековДокументы.Открыта() Тогда
				Сообщить("ВНИМАНИЕ! Невозможно создать файл основной таблицы архива чеков: archive.dbf"+Символы.ПС+ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
				АрхивЧековФинализация();
				Возврат ЛОЖЬ;
			КонецЕсли;
			
			// Основная таблица создана, необходимо произвести сохранение основных параметров архива
			Параметры = Новый Структура("Версия", АрхивЧековВерсияАрхиваЧеков);
			ЗначениеВФайл(АрхивЧековКаталог+"config.dat", Параметры);
		КонецЕсли;
	КонецЕсли;
	
	// Таблица архива чеков подключена, проверим корректность подключения файла индексов
	Если АрхивЧековДокументы.Индексы.Количество()=0 Тогда
		АрхивЧековДокументы.Индексы.Добавить("IndID",   "ID", ИСТИНА);
		АрхивЧековДокументы.Индексы.Добавить("IndMODE", "MODE");
		АрхивЧековДокументы.Индексы.Добавить("IndDOC", "ID_CASH+SUBSTR(DATE,1,8)+DOC+NUM",,,"MODE='1'");
		АрхивЧековДокументы.Индексы.Добавить("IndNUM", "ID_CASH+SUBSTR(DATE,1,8)+NUM"    ,,,"MODE='1'");
		
		// Создаем индексный файл таблицы архива чеков
		Попытка
			АрхивЧековДокументы.СоздатьИндексныйФайл(АрхивЧековКаталог+"archive.cdx");
		Исключение
			Сообщить("ВНИМАНИЕ! Невозможно создать индексный файл основной таблицы архива чеков: archive.cdx
			|	" + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
			АрхивЧековФинализация();
			Возврат ЛОЖЬ;
		КонецПопытки;
	КонецЕсли;
	
	// Установим порядок сортировки таблицы
	АрхивЧековДокументы.ТекущийИндекс = АрхивЧековДокументы.Индексы.Найти("IndID");
	АрхивЧековДокументы.Первая();
	
	
	///////////////////////////////////////////////////////////////////////////////
	// ПОДКЛЮЧЕНИЕ ДОПОЛНИТЕЛЬНОЙ ТАБЛИЦЫ "ТОВАРЫ"
	//
	АрхивЧековТовары.ОткрытьФайл(АрхивЧековКаталог+"goods.dbf", АрхивЧековКаталог+"goods.cdx");
	Если НЕ АрхивЧековТовары.Открыта() Тогда
		АрхивЧековТовары.ОткрытьФайл(АрхивЧековКаталог+"goods.dbf");
	КонецЕсли;
	
	// Проверим результат подключения таблицы
	Если НЕ АрхивЧековТовары.Открыта() Тогда
		// Проверим существует ли файл архива, если нет, то создадим его
		АрхивЧековФайл = Новый Файл(АрхивЧековКаталог+"goods.dbf");
		Если АрхивЧековФайл.Существует() Тогда
			Сообщить("ВНИМАНИЕ! Невозможно открыть файл дополнительной таблицы архива чеков: goods.dbf. Возможно таблица заблокирована другой задачей.", СтатусСообщения.ОченьВажное);
			АрхивЧековФинализация();
			Возврат ЛОЖЬ;
		Иначе
			// Основной таблицы нет, на всякий случай необходимо удалить и старый индексный файл
			УдалитьФайлы(АрхивЧековКаталог+"goods.cdx");
			
			// Создаем дополнительную таблицу архива чеков
			АрхивЧековТовары.Кодировка = КодировкаXBase.ANSI;
			АрхивЧековТовары.Поля.Добавить("ID", "S", 32);
			АрхивЧековТовары.Поля.Добавить("ID_GOODS", "S", 32);
			
			// Создаем файл таблицы архива чеков
			Попытка
				СоздатьКаталог(АрхивЧековФайл.Путь);
				АрхивЧековТовары.СоздатьФайл(АрхивЧековФайл.ПолноеИмя);
			Исключение КонецПопытки;
			
			// Проверяем статус создания таблицы
			Если НЕ АрхивЧековТовары.Открыта() Тогда
				Сообщить("ВНИМАНИЕ! Невозможно создать файл дополнительной таблицы архива чеков: goods.dbf"+Символы.ПС+ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
				АрхивЧековФинализация();
				Возврат ЛОЖЬ;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Таблица архива чеков подключена, проверим корректность подключения файла индексов
	Если АрхивЧековТовары.Индексы.Количество()=0 Тогда
		АрхивЧековТовары.Индексы.Добавить("IndID", "ID");
		
		// Создаем индексный файл таблицы архива чеков
		Попытка
			АрхивЧековТовары.СоздатьИндексныйФайл(АрхивЧековКаталог+"goods.cdx");
		Исключение
			Сообщить("ВНИМАНИЕ! Невозможно создать файл дополнительной таблицы архива чеков: goods.cdx
			|	" + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
			АрхивЧековФинализация();
			Возврат ЛОЖЬ;
		КонецПопытки;
	КонецЕсли;
	
	// Установим порядок сортировки таблицы
	АрхивЧековТовары.ТекущийИндекс = АрхивЧековТовары.Индексы.Найти("IndID");
	АрхивЧековТовары.Первая();
	
	АрхивЧековПодключен = Истина;
	
	Возврат ИСТИНА;
КонецФункции // АрхивЧековИнициализация()

// Процедура производит закрытие таблиц связанных с архивом чеков
//
Функция АрхивЧековФинализация() Экспорт
	АрхивЧековПодключен = Ложь;

	// Производим отключение основных таблиц архива чека
	Если ТипЗнч(АрхивЧековДокументы)=Тип("XBase") И АрхивЧековДокументы.Открыта() Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
	КонецЕсли;
	Если ТипЗнч(АрхивЧековТовары)=Тип("XBase") И АрхивЧековТовары.Открыта() Тогда
		АрхивЧековТовары.ЗакрытьФайл();
	КонецЕсли;
	
	// Очищаем переменные 
	АрхивЧековДокументы = Неопределено;  
	АрхивЧековТовары    = Неопределено;
	
	// Производим очистку временного каталога (распаковка сжатых образов чеков)
	УдалитьФайлы(КаталогВременныхФайлов()+"CHECKS\", "*.xml");	
КонецФункции // АрхивЧековФинализация()

// Функция производит сохранение переданного объекта чека в архив
//
// Параметры
//  Объект  – ДокументОбъект.Чек – чек, который будет помещен в архив
//  Статус  – Число – Определяет состояние записываемого чека
//				0 - Чек находится в работе
//				1 - Чек пробит
//				2 - Чек отложен
//				3 - Отмена ввода чека
//
// Возвращаемое значение:
//   Булево – Статус выполненной операции
//
Функция АрхивЧековСохранить(Объект, Статус = 0) Экспорт
	// Проверим корректность подключения таблиц архива
	Если НЕ АрхивЧековПодключен Тогда
		Возврат ЛОЖЬ;
	КонецЕсли;
	
	// Проверим какой индексный файл сейчас используется и установим текущий индекс
	Если АрхивЧековДокументы.Индексы.Найти("IndMODE")=Неопределено Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
		АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf", АрхивЧековКаталог+"archive.cdx");
	КонецЕсли;
	АрхивЧековДокументы.ТекущийИндекс = АрхивЧековДокументы.Индексы.Найти("IndID");
	
	// Получим уникальный идентификатор текущего чека
	Если (Статус=3) И Объект.ЭтоНовый() Тогда
		GUID_Чек = СтрЗаменить(Новый УникальныйИдентификатор,"-","");
	Иначе
		GUID_Чек = Лев(Прав(ЗначениеВСтрокуВнутр(Объект.Ссылка),33),32);
	КонецЕсли;
	DATE_Чек = ?(ЗначениеЗаполнено(Объект.ДатаФР), Объект.ДатаФР, ТекущаяДата());
	
	// Попробуем найти упоминание о данном чеке в нашем архиве
	Нашли = (АрхивЧековДокументы.Найти(GUID_Чек,"=") ИЛИ АрхивЧековДокументы.Найти("00000000000000000000000000000000","="));
	Если НЕ Нашли Тогда
		АрхивЧековДокументы.Добавить();
	КонецЕсли;
	
	// Определим значение некоторых переменных
	ИмяФайлаЧека  = АрхивЧековДокументы.ID;
	ДатаФайлаЧека = Дата(?(ПустаяСтрока(АрхивЧековДокументы.DATE),"00010101000000",АрхивЧековДокументы.DATE));
	
	// Заполняем строку таблицы архива чеков
	АрхивЧековДокументы.ID      = GUID_Чек;
	АрхивЧековДокументы.DATE    = Формат(DATE_Чек,"ДФ=yyyyMMddHHmmss");
	АрхивЧековДокументы.DOC     = Формат(Объект.НомерДокумента,"ЧЦ=10; ЧН=0000000000; ЧВН=; ЧГ=0");
	АрхивЧековДокументы.ZNUM    = Формат(Объект.НомерСмены,    "ЧЦ= 6; ЧН=    000000; ЧВН=; ЧГ=0");
	АрхивЧековДокументы.NUM     = Формат(Объект.НомерЧека,     "ЧЦ= 6; ЧН=    000000; ЧВН=; ЧГ=0");
	АрхивЧековДокументы.MODE    = Статус;
	АрхивЧековДокументы.ID_CASH = Лев(Прав(ЗначениеВСтрокуВнутр(Объект.ФР),33),32);
	АрхивЧековДокументы.ID_CARD = Лев(Прав(ЗначениеВСтрокуВнутр(Объект.Карточка),33),32);
	АрхивЧековДокументы.COST    = Формат(Объект.Товары.Итог("СуммаВсего"), "ЧЦ=15; ЧДЦ=2; ЧН=0000000000000,00; ЧВН=; ЧГ=0");
	
	// Сохраняем изменения в таблице архива
	АрхивЧековДокументы.Записать();
	
	// Получим файлы образа и свойств нового чека
	ФайлОбъекта = Новый Файл(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+".xml");
	ФайлСвойств = Новый Файл(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+"_Свойства.xml");
	СоздатьКаталог(ФайлОбъекта.Путь);
	
	// Определим имя файла в котором был ранее сохранен объект чека и при необходимости
	// переименуем/переместим его в новый каталог
	Если Нашли Тогда
		АрхивОбъекта = Новый Файл(АрхивЧековКаталог+"CHECKS\"+Формат(ДатаФайлаЧека,"ДФ=yyyy-MM-dd")+"\"+ИмяФайлаЧека+".xml");
		АрхивСвойств = Новый Файл(АрхивЧековКаталог+"CHECKS\"+Формат(ДатаФайлаЧека,"ДФ=yyyy-MM-dd")+"\"+ИмяФайлаЧека+"_Свойства.xml");
		Если НЕ АрхивОбъекта.Существует() Тогда
			АрхивОбъекта = Новый Файл(АрхивЧековКаталог+"CHECKS\"+ИмяФайлаЧека+".xml");
			АрхивСвойств = Новый Файл(АрхивЧековКаталог+"CHECKS\"+ИмяФайлаЧека+"_Свойства.xml");
		КонецЕсли;
		
		// Если файлы ранее сохраненного чека удалось найти, то переместим их на новое место
		Если АрхивОбъекта.Существует() И (ВРег(АрхивОбъекта.ПолноеИмя) <> ВРег(ФайлОбъекта.ПолноеИмя))Тогда
			// Перемещаем файл объекта чека
			Попытка
				ПереместитьФайл(АрхивОбъекта.ПолноеИмя, ФайлОбъекта.ПолноеИмя);
			Исключение
				УдалитьФайлы(АрхивОбъекта.ПолноеИмя);
			КонецПопытки;
			
			// Перемещаем файл свойств чека
			Если АрхивСвойств.Существует() Тогда
				Попытка
					ПереместитьФайл(АрхивСвойств.ПолноеИмя, ФайлСвойств.ПолноеИмя);
				Исключение
					УдалитьФайлы(АрхивСвойств.ПолноеИмя);
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Удалим запрещенные символы
	Попытка
		Для Каждого ТекущаяСтрока Из Объект.КодыМаркировки Цикл
			ТекущаяСтрока.КодМаркировки = СтрЗаменить(ТекущаяСтрока.КодМаркировки, Символ(29), "\x1d");
		КонецЦикла;
	Исключение
	КонецПопытки;
		
	
	// Производим XML-сериализацию объекта чека и записываем данные в файл
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ФайлОбъекта.ПолноеИмя);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписатьXML(ЗаписьXML, Объект);
	ЗаписьXML.Закрыть();
	
	// Производим сохранение свойств чека
	Если НЕ Объект.ЭтоНовый() Тогда
		НаборЗаписей = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Объект.Ссылка, ИСТИНА);
		НаборЗаписей.Прочитать();
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.ОткрытьФайл(ФайлСвойств.ПолноеИмя);
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписатьXML(ЗаписьXML, НаборЗаписей);
		ЗаписьXML.Закрыть();
	КонецЕсли;
	
	// Необходимо расширить таблицу соответствий для быстрого поиска чеков по товарам
	Если НЕ АрхивЧековТовары.Открыта() Тогда
		Возврат ЛОЖЬ;
	ИначеЕсли Статус=1 Тогда
		АрхивЧековТовары.ТекущийИндекс = АрхивЧековТовары.Индексы.Найти("IndID");
		
		// Сворачиваем информацию о товарах в чеке
		спТовары = Объект.Товары.Выгрузить(,"Номенклатура");
		спТовары.Свернуть("Номенклатура");
		
		// Устанавливаем соответствие товар-чек
		Для Каждого ТекСтрока Из спТовары Цикл
			АрхивЧековТовары.Добавить();
			АрхивЧековТовары.ID = GUID_Чек;
			АрхивЧековТовары.ID_GOODS = Лев(Прав(ЗначениеВСтрокуВнутр(ТекСтрока.Номенклатура),33),32);
			АрхивЧековТовары.Записать();
		КонецЦикла;
	КонецЕсли;
	
	Возврат ИСТИНА;
КонецФункции // АрхивЧековСохранить()

// Функция восстанавливает чек из архива. При этом имеется возможность указывать как и прямой GUID
// восстанавливаемого чека, так и элементарный отбор по Дата/Время
//
// Параметры
//	Параметры  – Строка,Структура
//	           – Дополнительный параметр содержащий информацию для поиска чека
//
// Возвращаемое значение:
//   ДокументОбъект.Чек, Неопределено – Результат выполненной операции
//
Функция АрхивЧековВосстановить(Параметры=Неопределено, СвойстваЧека=Неопределено) Экспорт
	СвойстваЧека = Неопределено;
	
	// Проверим корректность подключения таблиц архива
	Если НЕ АрхивЧековПодключен Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Выполним различные действия, в зависимости от типа переданного параметра
	Если ТипЗнч(Параметры)=Тип("Строка") И СтрДлина(СокрЛП(Параметры))=32+14+1 Тогда
		GUID_Чек = СтрПолучитьСтроку(Параметры, 1);
		DATE_Чек = СтрПолучитьСтроку(Параметры, 2);
		DATE_Чек = Дата(?(ПустаяСтрока(DATE_Чек),"00010101000000",DATE_Чек))
	ИначеЕсли ТипЗнч(Параметры)=Тип("ДокументСсылка.Чек") Тогда
		GUID_Чек = Лев(Прав(ЗначениеВСтрокуВнутр(Параметры),33),32);
		DATE_Чек = ?(ЗначениеЗаполнено(Параметры.ДатаФР), Параметры.ДатаФР, ТекущаяДата());
	Иначе		
		// Проверим какой индексный файл сейчас используется
		Если АрхивЧековДокументы.Индексы.Найти("IndMODE")=Неопределено Тогда
			АрхивЧековДокументы.ЗакрытьФайл();
			АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf", АрхивЧековКаталог+"archive.cdx");
		КонецЕсли;
		
		// Скорее всего нужно будет произвести поиск в таблице ...
		Если Параметры=Неопределено Тогда
			АрхивЧековДокументы.ТекущийИндекс = АрхивЧековДокументы.Индексы.Найти("IndMODE");
			АрхивЧековДокументы.Ключ.MODE     = "0";
			
			// Выполняем поиск нужной информации в архиве по установленному ключу
			Если НЕ АрхивЧековДокументы.НайтиПоКлючу("=") Тогда
				Возврат Неопределено;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Параметры)=Тип("Структура") Тогда
			// Закэшируем значения параметров поиска
			ID_CASH = Лев(Прав(ЗначениеВСтрокуВнутр(Параметры.Касса),33),32);
			DATE    = Формат(?(ЗначениеЗаполнено(Параметры.Дата), Параметры.Дата, ТекущаяДата()),"ДФ=yyyyMMdd");
			DOC     = Формат(Параметры.Документ,"ЧЦ=10; ЧН=0000000000; ЧВН=; ЧГ=0");
			NUM     = Формат(Параметры.Номер,   "ЧЦ= 6; ЧН=    000000; ЧВН=; ЧГ=0");
			
			// Производим установку ключа поиска
			АрхивЧековДокументы.ТекущийИндекс = ?(ЗначениеЗаполнено(Параметры.Документ), АрхивЧековДокументы.Индексы.Найти("IndDOC"), АрхивЧековДокументы.Индексы.Найти("IndNUM"));
			АрхивЧековДокументы.Ключ.ID_CASH  = ID_CASH;
			АрхивЧековДокументы.Ключ.DATE     = DATE;
			АрхивЧековДокументы.Ключ.DOC      = DOC;
			АрхивЧековДокументы.Ключ.NUM      = NUM;
			
			// Определим режим поиска записи
			РежимПоиска = ?(ЗначениеЗаполнено(Параметры.Документ) И ЗначениеЗаполнено(Параметры.Номер), "=", ">=");
			
			// Выполняем поиск нужной информации в архиве по установленному ключу
			Если НЕ АрхивЧековДокументы.НайтиПоКлючу(РежимПоиска) Тогда
				Возврат Неопределено;
			Иначе
				// Найденную строку необходимо проверить на соответствие критерию
				Если РежимПоиска="=" ИЛИ (АрхивЧековДокументы.ID_CASH=ID_CASH И ЛЕВ(АрхивЧековДокументы.DATE, 8)=DATE И ?(ЗначениеЗаполнено(Параметры.Документ), АрхивЧековДокументы.DOC=DOC, АрхивЧековДокументы.NUM=NUM)) Тогда
					// Строка соответствует критерию поиска
				Иначе
					Возврат Неопределено;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
		// Получаем значение GUID-а и даты сохраненного чека
		GUID_Чек = АрхивЧековДокументы.ID;
		DATE_Чек = Дата(?(ПустаяСтрока(АрхивЧековДокументы.DATE),"00010101000000",АрхивЧековДокументы.DATE));
	КонецЕсли;
	
	// Идентификатор и дата чека определены, получим имя файла чека
	ФайлОбъекта = Новый Файл(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+".xml");
	ФайлСвойств = Новый Файл(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+"\"+GUID_Чек+"_Свойства.xml");
	
	// Производим уточнение имя файла чека
	Если (НЕ ФайлОбъекта.Существует()) ИЛИ ФайлОбъекта.ЭтоКаталог() Тогда
		ФайлОбъекта = Новый Файл(АрхивЧековКаталог+"CHECKS\"+GUID_Чек+".xml");
		ФайлСвойств = Новый Файл(АрхивЧековКаталог+"CHECKS\"+GUID_Чек+"_Свойства.xml");
	КонецЕсли;
	
	// Если файл не найден, то следует попытаться найти его в сжатой части архива
	Если (НЕ ФайлОбъекта.Существует()) ИЛИ ФайлОбъекта.ЭтоКаталог() Тогда
		// Проверим существование сжатого файла
		ZipФайл = Новый Файл(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+".zip");
		Если (НЕ ZipФайл.Существует()) ИЛИ ZipФайл.ЭтоКаталог() Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		// Открываем сжатый файл и производим чтение интересующих нас элементов
		ZipФайл = Новый ЧтениеZipФайла(АрхивЧековКаталог+"CHECKS\"+Формат(DATE_Чек,"ДФ=yyyy-MM-dd")+".zip");
		ЭлементОбъекта = ZipФайл.Элементы.Найти(""+GUID_Чек+".xml");
		ЭлементСвойств = ZipФайл.Элементы.Найти(""+GUID_Чек+"_Свойства.xml");
		
		// Если файл найден в архиве, то производим его извлечение
		Если НЕ ЭлементОбъекта=Неопределено Тогда
			ВременныйКаталогЧеков = КаталогВременныхФайлов()+"CHECKS\";
			
			Попытка
				// Выполняем извлечение файла содержащего описание объекта чека
				ZipФайл.Извлечь(ЭлементОбъекта, ВременныйКаталогЧеков, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
				ФайлОбъекта = Новый Файл(ВременныйКаталогЧеков+GUID_Чек+".xml");
				
				// Выполняем извлечение файла содержащего описание назначенных свойств чека
				Если НЕ ЭлементСвойств=Неопределено Тогда
					ZipФайл.Извлечь(ЭлементСвойств, ВременныйКаталогЧеков, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
					ФайлСвойств = Новый Файл(ВременныйКаталогЧеков+GUID_Чек+"_Свойства.xml");
				КонецЕсли;
			Исключение
				// Архив поврежден или защищен паролем
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	// Проверим существование чека
	Если (НЕ ФайлОбъекта.Существует()) ИЛИ ФайлОбъекта.ЭтоКаталог() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Все готово для восстановления чека, приступаем ...
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ФайлОбъекта.ПолноеИмя);
	Попытка
		Объект = Неопределено;
		Объект = ПрочитатьXML(ЧтениеXML, Тип("ДокументОбъект.Чек"));
	Исключение КонецПопытки;
	ЧтениеXML.Закрыть();
	
	// Если внутренний механизм сериализации не сработал, то попробуем самостоятельно разобрать XML-файл
	Если Объект=Неопределено Тогда
		// Переинициализируем объект чтения XML-файла и установим курсор на первом реквизите объекта
		ЧтениеXML.ОткрытьФайл(ФайлОбъекта.ПолноеИмя);
		
		// Создадим объект для хранения значений реквизитов
		Стек = Новый Массив;
		Стек.Добавить(Новый Структура);
		
		// Приступаем к последовательному чтению XML-файла
		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				Стек.Добавить(Новый Структура);
				
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
				Стек[Стек.ВГраница()] = ЧтениеXML.Значение;
				
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				// Получаем значение дочерних узлов
				Содержимое = Стек[Стек.ВГраница()];
				Стек.Удалить(Стек.ВГраница());
				
				// Если значение дочерних узлов пусто, то не нужно добавлять его в список реквизитов текущего узла
				Если ПустаяСтрока(Содержимое) ИЛИ (ТипЗнч(Содержимое)=Тип("Структура") И Содержимое.Количество()=0) Тогда
					Продолжить;
				КонецЕсли;
				
				// Получаем структуру для хранения значений реквизитов текущего уровня
				Реквизиты = Стек[Стек.ВГраница()];
				
				// Производим корректировку имени текущего узла
				ИмяУзла = СтрЗаменить(ЧтениеXML.Имя,"DocumentObject.","");
				ИмяУзла = ?(ИмяУзла = "Row", "Row"+Реквизиты.Количество(), ИмяУзла);
				
				// Сохраняем значение дочерних реквизитов текущего узла
				Реквизиты.Вставить(ИмяУзла, Содержимое);
			КонецЕсли;
		КонецЦикла;
		
		// Отключаемся от файла
		ЧтениеXML.Закрыть();
		ОбъектXML = Стек[0].Чек;
		
		// Получаем основной объект и производим его начальную инициализацию
		СсылкаНаЧек = XMLЗначение(Тип("ДокументСсылка.Чек"), ОбъектXML.Ref);
		Попытка
			Объект = Неопределено;
			Объект = СсылкаНаЧек.ПолучитьОбъект();
		Исключение КонецПопытки;
		
		// Проверяем результат выполнения операции поднятия чека из архива
		Если НЕ ТипЗнч(Объект)=Тип("ДокументОбъект.Чек") Тогда
			Объект = Документы.Чек.СоздатьДокумент();
			Попытка
				Объект.УстановитьСсылкуНового(СсылкаНаЧек);
			Исключение КонецПопытки;
		КонецЕсли;
		ОбъектXML.Удалить("Ref");
		
		// Обходим все реквизиты документа и пытаемся заполнить их информацией из считанного XML-файла
		Для Каждого ТабличнаяЧасть Из Метаданные.Документы.Чек.ТабличныеЧасти Цикл
			ИмяТаблицы = ТабличнаяЧасть.Имя;
			Объект[ИмяТаблицы].Очистить();
			
			// Попытаемся получить информацию о данной таблице из считанного XML-файла
			Попытка
				ТаблицаXML = ОбъектXML[ИмяТаблицы];
			Исключение
				Продолжить;
			КонецПопытки;
			ОбъектXML.Удалить(ИмяТаблицы);
			
			// Приступаем к заполнению текущей таблицы документа
			Для Каждого ТекСтрокаXML Из ТаблицаXML Цикл
				ТекСтрока = Объект[ИмяТаблицы].Добавить();
				
				// Заполняем новую строку
				Для Каждого Реквизит Из ТекСтрокаXML.Значение Цикл
					Попытка
						ТекСтрока[Реквизит.Ключ] = XMLЗначение(ТипЗнч(ТекСтрока[Реквизит.Ключ]), Реквизит.Значение);
					Исключение КонецПопытки;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		// Все табличные части заполнены и информация о них удалена из структуры. Теперь заполняем реквизиты документа
		Для Каждого Реквизит Из ОбъектXML Цикл
			Попытка
				Объект[Реквизит.Ключ] = XMLЗначение(ТипЗнч(Объект[Реквизит.Ключ]), Реквизит.Значение);
			Исключение КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	// Необходимо снять признак проведения, так как документ восстанавливается без движений
	Объект.Проведен = ЛОЖЬ;
	
	// Проверим существование файла свойств чека
	Если (НЕ ФайлСвойств.Существует()) ИЛИ ФайлСвойств.ЭтоКаталог() Тогда
		Возврат Объект;
	КонецЕсли;
	
	// Все готово для восстановления свойств чека, приступаем ...
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ФайлСвойств.ПолноеИмя);
	Попытка
		СвойстваЧека = ПрочитатьXML(ЧтениеXML, Тип("РегистрСведенийНаборЗаписей.ЗначенияСвойствОбъектов"));
	Исключение КонецПопытки;
	ЧтениеXML.Закрыть();
	
	Возврат Объект;
КонецФункции // АрхивЧековВосстановить()

// Функция по переданному отбору рассчитывает выражения фильтров, создает индексный файл и формирует специальную
// структуру содержащую параметры поиска, которая далее используется при каждом поиске чека
//
// Параметры
//  Отбор – Структура – Список параметров отбора чеков
//
// Возвращаемое значение:
//   Структура – Список параметров поиска
//
Функция АрхивЧековСформироватьПараметрыПоиска(Отбор) Экспорт
	// Проверим корректность подключения таблиц архива
	Если НЕ АрхивЧековПодключен Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// На основании переданного отбора сформируем рабочую структуру параметров
	ПараметрыОтбора = Новый Структура("НачДата,КонДата,НачНомер,КонНомер,НачСумма,КонСумма,Касса,Карточка");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, Отбор);
	
	// Составим соответствие параметров переданного отбора частям индекса
	Фильтры = Новый Соответствие;
	Фильтры.Вставить("НачДата",	"'$НачДата$'<=DATE");
	Фильтры.Вставить("КонДата",	"DATE<='$КонДата$'");
	Фильтры.Вставить("НачНомер","'$НачНомер$'<=NUM");
	Фильтры.Вставить("КонНомер","NUM<='$КонНомер$'");
	Фильтры.Вставить("НачСумма","'$НачСумма$'<=COST");
	Фильтры.Вставить("КонСумма","COST<='$КонСумма$'");
	Фильтры.Вставить("Касса",	"ID_CASH='$Касса$'");
	Фильтры.Вставить("Карточка","ID_CARD='$Карточка$'");
	
	// Составляем выражение для фильтра индекса
	// ВНИМАНИЕ!!! Длина выражения фильтра имеет ограничение в 254 символов
	Выражение = "";
	Для Каждого ТекПараметр Из ПараметрыОтбора Цикл
		Если обЗначениеНеЗаполнено(ТекПараметр.Значение) ИЛИ (ТипЗнч(ТекПараметр.Значение)=Тип("Число") И ТекПараметр.Значение < 0) Тогда
			Продолжить;
		ИначеЕсли Прав(ТекПараметр.Ключ,4)="Дата" Тогда
			Значение = Формат(ТекПараметр.Значение, "ДФ='yyyyMMddHHmmss'; ДП=00010101000000");
		ИначеЕсли Прав(ТекПараметр.Ключ,5)="Номер" Тогда
			Значение = Формат(ТекПараметр.Значение, "ЧЦ=6; ЧВН=; ЧГ=0");
		ИначеЕсли Прав(ТекПараметр.Ключ,5)="Сумма" Тогда
			Значение = Формат(ТекПараметр.Значение, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧВН=; ЧГ=0");
		Иначе
			Значение = Лев(Прав(ЗначениеВСтрокуВнутр(ТекПараметр.Значение),33),32);
		КонецЕсли;
		Выражение = Выражение +" .AND. "+ СтрЗаменить(Фильтры.Получить(ТекПараметр.Ключ),"$"+ТекПараметр.Ключ+"$",Значение);
	КонецЦикла;
	Выражение = Сред(Выражение, 8);
	
	// Производим формирование специального фильтра для основной таблицы
	ИмяИндексногоФайла = КаталогВременныхФайлов()+"archive.cdx";
	
	// Приступаем к формированию нового индексного файла, но прежде необходимо закрыть прежний
	АрхивЧековДокументы.ЗакрытьФайл();
	
	// Подключаем таблицу, добавляем новый индекс и создаем индексный файл
	АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
	АрхивЧековДокументы.Индексы.Добавить("IndDATE","DATE+NUM+MODE",,,Выражение);
	АрхивЧековДокументы.СоздатьИндексныйФайл(ИмяИндексногоФайла);
	
	// Теперь получим список товаров
	Если Отбор.Свойство("Товары") Тогда
		спТовары = "";
		Для Каждого ТекТовар Из Отбор.Товары Цикл
			GUID_Товар = Лев(Прав(ЗначениеВСтрокуВнутр(ТекТовар),33),32);
			Если (НЕ ПустаяСтрока(GUID_Товар)) И (Найти(спТовары, GUID_Товар)=0) Тогда
				спТовары = спТовары+","+GUID_Товар;
			КонецЕсли;
		КонецЦикла;
		спТовары = ?(СтрДлина(спТовары)<32, Неопределено, Сред(спТовары,2));
	Иначе
		спТовары = Неопределено;
	КонецЕсли;
	
	// Заполняем структуру параметров поиска чеков в архиве
	ПараметрыПоиска = Новый Структура("ИмяИндексногоФайла,Выражение,НомерЗаписи,Товары,Отбор,РезультатПоиска", ИмяИндексногоФайла, Выражение, -1, спТовары, Отбор, Неопределено);
	
	Возврат ПараметрыПоиска;
КонецФункции // АрхивЧековСформироватьПараметрыПоиска()

// Функция универсального поиска чека в архиве, позволяющая получить список документов удовлетворяющих
// переданным параметрам
//
// Параметры
//	Отбор		– Структура – <описание параметра>
//                 <продолжение описания параметра>
//	Направление	- Число - аргумент обозначающий направление
//				перемещения курсора таблицы:
//					+2: В конец
//					+1: Следующая
//					 0: Текущая запись
//					-1: Предыдущая
//					-2: В начало
//
// Возвращаемое значение:
//   Неопределено, Структура – Результат выполненной операции
//
Функция АрхивЧековНайти(ПараметрыПоиска, Направление = +1) Экспорт
	// Проверим корректность подключения таблиц архива
	Если ПараметрыПоиска=Неопределено ИЛИ НЕ АрхивЧековПодключен Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Проверим какой индексный файл сейчас используется
	Если НЕ АрхивЧековДокументы.Индексы.Найти("IndMODE")=Неопределено Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
		АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf", ПараметрыПоиска.ИмяИндексногоФайла);
		
		// Вполне возможно в таблицу были добавлены или изменены некоторые записи - необходима переиндексация
		АрхивЧековДокументы.Переиндексировать();
	КонецЕсли;
	АрхивЧековДокументы.ТекущийИндекс = АрхивЧековДокументы.Индексы.Найти("IndDATE");
	АрхивЧековТовары.	ТекущийИндекс = АрхивЧековТовары.	Индексы.Найти("IndID");
	
	// Произведем первоначальное позиционирование курсора таблицы
	Если АрхивЧековДокументы.КоличествоЗаписей()=0 Тогда
		Возврат Неопределено;
	ИначеЕсли Направление > 1 Тогда
		АрхивЧековДокументы.Последняя();
		Направление = -1;
	ИначеЕсли Направление < -1 Тогда
		АрхивЧековДокументы.Первая();
		Направление = +1;
	Иначе
		НомерЗаписи = МИН(МАКС(ПараметрыПоиска.НомерЗаписи,1), АрхивЧековДокументы.КоличествоЗаписей());
		АрхивЧековДокументы.Перейти(НомерЗаписи);
		
		// Производим смещение курсора в нужную сторону
		Результат = ?(Направление >= 0, АрхивЧековДокументы.Следующая(), АрхивЧековДокументы.Предыдущая());
	КонецЕсли;
	
	// Приступаем к поиску подходящей по критерию записи
	Нашли = ЛОЖЬ;
	Пока НЕ ?(Направление >= 0, АрхивЧековДокументы.ВКонце(), АрхивЧековДокументы.ВНачале()) Цикл
		// Получили новую запись, необходимо проверить подходит ли она нам
		Нашли = (ПараметрыПоиска.Товары=Неопределено);
		Если НЕ Нашли Тогда
			Нашли = ?(АрхивЧековДокументы.MODE="1", Неопределено, ЛОЖЬ);
		КонецЕсли;
		
		// Предварительный анализ не дал ответа, необходимо обратиться к дополнительной таблице архива чеков
		Если Нашли=Неопределено Тогда
			Нашли = АрхивЧековТовары.Найти(АрхивЧековДокументы.ID,"=");
			Если Нашли Тогда
				спТовары = ПараметрыПоиска.Товары;
				Пока (НЕ АрхивЧековТовары.ВКонце()) И (АрхивЧековТовары.ID=АрхивЧековДокументы.ID) Цикл
					спТовары = СтрЗаменить(спТовары, АрхивЧековТовары.ID_GOODS, "");
					АрхивЧековТовары.Следующая();
				КонецЦикла;
				Нашли = ПустаяСтрока(СтрЗаменить(спТовары,",",""));
			КонецЕсли;
		КонецЕсли;
		
		// Проверяем результат поиска
		Если Нашли Тогда
			Прервать;
		КонецЕсли;
		
		// Перемещаем курсор на новую позицию
		Результат = ?(Направление >= 0, АрхивЧековДокументы.Следующая(), АрхивЧековДокументы.Предыдущая());
	КонецЦикла;
	
	// Если поиск успешный, то запомним позицию и формируем структуру результата
	Если Нашли Тогда
		ПараметрыПоиска.НомерЗаписи = АрхивЧековДокументы.НомерЗаписи();
		ПараметрыПоиска.РезультатПоиска = Новый Структура("Чек,Дата,Номер,Статус,Касса,Карточка,Сумма", АрхивЧековДокументы.ID, АрхивЧековДокументы.DATE, АрхивЧековДокументы.NUM, Число(АрхивЧековДокументы.MODE), АрхивЧековДокументы.ID_CASH, АрхивЧековДокументы.ID_CARD, АрхивЧековДокументы.COST);
		Возврат ПараметрыПоиска.РезультатПоиска;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции // АрхивЧековНайти()

// Функция производит переиндексацию таблиц архива чеков
//
// Возвращаемое значение:
//   Булево – Статус выполненной операции
//
Функция АрхивЧековПереиндексация() Экспорт
	Открыта = ТипЗнч(АрхивЧековДокументы)=Тип("XBase") И АрхивЧековДокументы.Открыта();
	
	// Инициализация объектов таблиц
	Если Открыта Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
		АрхивЧековТовары   .ЗакрытьФайл();
	Иначе
		АрхивЧековКаталог = АрхивЧековПолучитьКаталогАрхиваЧеков();
	КонецЕсли;
	
	// Инициализация объектов таблиц
	АрхивЧековДокументы = Новый XBase;
	АрхивЧековТовары    = Новый XBase;
	
	// Подключение основной таблицы
	АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
	АрхивЧековТовары   .ОткрытьФайл(АрхивЧековКаталог+"goods.dbf");
	
	// Проверим результат подключения таблицы
	Если НЕ АрхивЧековДокументы.Открыта() Тогда
		// Проверим существование основного файла архива
		АрхивЧековФайл = Новый Файл(АрхивЧековКаталог+"archive.dbf");
		Если АрхивЧековФайл.Существует() Тогда
			Сообщить("ВНИМАНИЕ! Невозможно открыть файл основной таблицы архива чеков: archive.dbf. Возможно таблица заблокирована другой задачей.", СтатусСообщения.ОченьВажное);
			Возврат ЛОЖЬ;
		Иначе
			Сообщить("На данном компьютере не обнаружено использование архива чеков. Переиндексация не выполнена.", СтатусСообщения.Информация);
			Возврат ИСТИНА;
		КонецЕсли;
		
	ИначеЕсли НЕ АрхивЧековТовары.Открыта() Тогда
		Сообщить("ВНИМАНИЕ! Невозможно открыть файл дополнительной таблицы архива чеков: goods.dbf. Возможно таблица заблокирована другой задачей.", СтатусСообщения.ОченьВажное);
		Возврат ЛОЖЬ;
	КонецЕсли;
	Состояние("Выполняется переиндексация архива чеков.");
	
	// Производим сжатие таблиц архива чеков
	АрхивЧековДокументы.Сжать();
	АрхивЧековТовары.Сжать();
	
	// Таблицы архива чеков подключены, создаем описание индексов
	АрхивЧековДокументы.Индексы.Добавить("IndID",   "ID", ИСТИНА);
	АрхивЧековДокументы.Индексы.Добавить("IndMODE", "MODE");
	АрхивЧековДокументы.Индексы.Добавить("IndDOC", "ID_CASH+SUBSTR(DATE,1,8)+DOC+NUM",,,"MODE='1'");
	АрхивЧековДокументы.Индексы.Добавить("IndNUM", "ID_CASH+SUBSTR(DATE,1,8)+NUM"    ,,,"MODE='1'");
	АрхивЧековТовары   .Индексы.Добавить("IndID",   "ID");
	
	// Создаем индексный файл основной таблицы архива чеков
	Попытка
		АрхивЧековДокументы.СоздатьИндексныйФайл(АрхивЧековКаталог+"archive.cdx");
	Исключение
		Сообщить("ВНИМАНИЕ! Невозможно создать индексный файл основной таблицы архива чеков: archive.cdx
		|	" + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
		Возврат ЛОЖЬ;
	КонецПопытки;
	
	// Создаем индексный файл дополнительной таблицы архива чеков
	Попытка
		АрхивЧековТовары.СоздатьИндексныйФайл(АрхивЧековКаталог+"goods.cdx");
	Исключение
		Сообщить("ВНИМАНИЕ! Невозможно создать файл дополнительной таблицы архива чеков: goods.cdx
		|	" + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
		Возврат ЛОЖЬ;
	КонецПопытки;
	
	// Если ранее архив чеков не был подключен, то выполним его отключение
	Если НЕ Открыта Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
		АрхивЧековТовары   .ЗакрытьФайл();
	КонецЕсли;
	
	Состояние();
	Возврат ИСТИНА;
КонецФункции // АрхивЧековПереиндексация()

// Функция производит переиндексацию таблиц архива чеков
//
// Возвращаемое значение:
//   Булево – Статус выполненной операции
//
Функция АрхивЧековПолучитьПериодВедения(НачПериода, КонПериода) Экспорт
	Открыта = ТипЗнч(АрхивЧековДокументы)=Тип("XBase") И АрхивЧековДокументы.Открыта();
	
	// Инициализация объектов таблиц
	Если Открыта Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
	Иначе
		АрхивЧековКаталог = АрхивЧековПолучитьКаталогАрхиваЧеков();
	КонецЕсли;
	
	// Инициализация объектов таблиц
	АрхивЧековДокументы = Новый XBase;
	
	// Подключение основной таблицы (для определения периода ведения архива не будем использовать индексный файл)
	АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
	
	// Получим даты периода ведения архива чеков
	Результат = АрхивЧековДокументы.Открыта();
	Если Результат Тогда
		НачПериода = ?(АрхивЧековДокументы.Первая(),    Дата(АрхивЧековДокументы.DATE), '00010101000000');
		КонПериода = ?(АрхивЧековДокументы.Последняя(), Дата(АрхивЧековДокументы.DATE), '00010101000000');
	Иначе
		НачПериода = '00010101000000';
		КонПериода = '00010101000000';
	КонецЕсли;
	
	// Если ранее архив чеков не был подключен, то выполним его отключение
	Если НЕ Открыта Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // АрхивЧековПолучитьПериодВедения()

// Функция производит сокращение архива чеков до указанной даты
//
//Параметры
//   ПоДату        - 
//
//Возвращаемое значение:
//   Булево – Статус выполненной операции
//
Функция АрхивЧековСократить(Знач ПоДату=Неопределено) Экспорт
	ПоДату = ?(ЗначениеЗаполнено(ПоДату), ПоДату, ТекущаяДата());
	
	// Определим состояние подключения основной таблицы архива чеков
	Открыта = ТипЗнч(АрхивЧековДокументы)=Тип("XBase") И АрхивЧековДокументы.Открыта();
	
	// Инициализация объектов таблиц
	Если Открыта Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
		АрхивЧековТовары   .ЗакрытьФайл();
	Иначе
		АрхивЧековКаталог = АрхивЧековПолучитьКаталогАрхиваЧеков();
	КонецЕсли;
	
	// Инициализация объектов таблиц
	АрхивЧековДокументы = Новый XBase;
	АрхивЧековТовары    = Новый XBase;
	
	// Подключение основной таблицы
	АрхивЧековДокументы.ОткрытьФайл(АрхивЧековКаталог+"archive.dbf");
	АрхивЧековТовары   .ОткрытьФайл(АрхивЧековКаталог+"goods.dbf");
	
	// Проверим результат подключения таблицы
	Если НЕ АрхивЧековДокументы.Открыта() Тогда
		// Проверим существование основного файла архива
		АрхивЧековФайл = Новый Файл(АрхивЧековКаталог+"archive.dbf");
		Если АрхивЧековФайл.Существует() Тогда
			Сообщить("ВНИМАНИЕ! Невозможно открыть файл основной таблицы архива чеков: archive.dbf. Возможно таблица заблокирована другой задачей.", СтатусСообщения.ОченьВажное);
			Возврат ЛОЖЬ;
		Иначе
			Сообщить("На данном компьютере не обнаружено ведение архива чеков. Сокращение архива не выполнено.", СтатусСообщения.Информация);
			Возврат ИСТИНА;
		КонецЕсли;
		
	ИначеЕсли НЕ АрхивЧековТовары.Открыта() Тогда
		Сообщить("ВНИМАНИЕ! Невозможно открыть файл дополнительной таблицы архива чеков: goods.dbf. Возможно таблица заблокирована другой задачей.", СтатусСообщения.ОченьВажное);
		Возврат ЛОЖЬ;
	КонецЕсли;
	Состояние("Выполняется сокращение архива чеков.");
	
	// Создадим специальную структуру для накопления информации об удаленных чеках
	УдаленныеЧеки = Новый Соответствие;
	
	// Проверим результат подключения основной таблицы
	СтатусЧтения = АрхивЧековДокументы.Первая();
	
	// Производим последовательную обработку записей основной таблицы архива чеков
	Пока СтатусЧтения Цикл
		GUID_Чек = АрхивЧековДокументы.ID;
		DATE_Чек = Дата(?(ПустаяСтрока(АрхивЧековДокументы.DATE),"00010101000000",АрхивЧековДокументы.DATE));
		
		// Выполняем проверку и помечаем текущую строку на удаление, сохраняем GUID удаленного чека
		Если НачалоДня(DATE_Чек) <= НачалоДня(ПоДату) Тогда
			АрхивЧековДокументы.Удалить();
			УдаленныеЧеки.Вставить(GUID_Чек, ИСТИНА);
		КонецЕсли;
		
		// Производим чтение следующей записи таблицы
		СтатусЧтения = АрхивЧековДокументы.Следующая();
	КонецЦикла;
	
	// Проверим результат подключения дополнительной таблицы
	СтатусЧтения = (УдаленныеЧеки.Количество() > 0) И АрхивЧековТовары.Первая();
	
	// Производим последовательную обработку записей дополнительно таблицы архива чеков
	Пока СтатусЧтения Цикл
		// Выполняем проверку и помечаем текущую строку на удаление
		Если НЕ УдаленныеЧеки.Получить(АрхивЧековТовары.ID)=Неопределено Тогда
			АрхивЧековТовары.Удалить();
		КонецЕсли;
		
		// Производим чтение следующей записи таблицы
		СтатусЧтения = АрхивЧековТовары.Следующая();
	КонецЦикла;
	
	// Если ранее архив чеков не был подключен, то выполним его отключение
	Если НЕ Открыта Тогда
		АрхивЧековДокументы.ЗакрытьФайл();
		АрхивЧековТовары   .ЗакрытьФайл();
	КонецЕсли;
	
	// Выполняем сжатие и переиндексацию таблиц
	Результат = АрхивЧековПереиндексация();
	
	// Производим удаление образов чеков и их свойств
	ГруппыЧеков = НайтиФайлы(АрхивЧековКаталог+"CHECKS\", "????-??-??.*");
	Для Каждого ГруппаЧеков Из ГруппыЧеков Цикл
		Если ГруппаЧеков.ЭтоКаталог() И НЕ ПустаяСтрока(ГруппаЧеков.Расширение) Тогда
			Продолжить;
		ИначеЕсли ГруппаЧеков.ЭтоФайл() И НЕ ВРег(ГруппаЧеков.Расширение)=".ZIP" Тогда
			Продолжить;
		ИначеЕсли ГруппаЧеков.ИмяБезРасширения <= Формат(ПоДату,"ДФ=yyyy-MM-dd") Тогда
			УдалитьФайлы(ГруппаЧеков.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;
	
	// Очищаем строку состояния
	Состояние();
	Возврат Результат;
КонецФункции // АрхивЧековСократить()

// Функция производит сжатие чеков помещенных в архив до указанной даты
//
//Параметры
//   ДоДаты        - 
//   МетодСжатия   - 
//   УровеньСжатия - 
//
//Возвращаемое значение:
//   Булево – Статус выполненной операции
//
Функция АрхивЧековСжать(Знач ДоДаты=Неопределено, МетодСжатия=Неопределено, УровеньСжатия=Неопределено) Экспорт
	Если НЕ АрхивЧековАктивен() Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	// Производим уточнение параметров сжатия архива
	ДоДаты        = ?(ЗначениеЗаполнено(ДоДаты), ДоДаты, ТекущаяДата());
	МетодСжатия   = ?(ЗначениеЗаполнено(МетодСжатия), МетодСжатия, МетодСжатияZIP.Сжатие);
	УровеньСжатия = ?(ЗначениеЗаполнено(УровеньСжатия), УровеньСжатия, УровеньСжатияZIP.Максимальный);
	АрхивЧековКаталог = АрхивЧековПолучитьКаталогАрхиваЧеков();
	
	// Произведем поиск и сжатие групп чеков
	ГруппыЧеков = НайтиФайлы(АрхивЧековКаталог+"CHECKS\", "????-??-??");
	Для Каждого ГруппаЧеков Из ГруппыЧеков Цикл
		Состояние("Выполняется сжатие группы чеков за "+ГруппаЧеков.Имя);
		
		// Выполним проверки возможности сжатия группы
		Если НЕ ГруппаЧеков.ЭтоКаталог() Тогда
			Продолжить;
		ИначеЕсли НЕ ГруппаЧеков.Имя < Формат(ДоДаты,"ДФ=yyyy-MM-dd") Тогда
			Продолжить;
		Иначе
			// Если уже существует сжатый файл на диске, то пропускаем эту группу. Zip файлы объединять/дополнять нельзя.
			ФайлНаДиске = Новый Файл(ГруппаЧеков.ПолноеИмя+".zip");
			Если ФайлНаДиске.Существует() Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Производим архивацию группы чеков
		Попытка
			ZipФайл = Новый ЗаписьZipФайла(ГруппаЧеков.ПолноеИмя+".zip",, "Сжатый том архива чеков за "+ГруппаЧеков.Имя, МетодСжатия, УровеньСжатия, МетодШифрованияZIP.Zip20);
			ZipФайл.Добавить(ГруппаЧеков.ПолноеИмя+"\*.xml", РежимСохраненияПутейZIP.НеСохранятьПути, РежимОбработкиПодкаталоговZIP.НеОбрабатывать);
			ZipФайл.Записать();
		Исключение
			Продолжить;
		КонецПопытки;
		
		// Удаляем все чеки из сжатой группы
		Попытка
			УдалитьФайлы(ГруппаЧеков.ПолноеИмя);
		Исключение
			Сообщить("ВНИМАНИЕ!!! Ошибка доступа к файлу: "+ГруппаЧеков.ПолноеИмя+Символы.ПС+"Возможно диск защищен от записи.", СтатусСообщения.Внимание);
		КонецПопытки;
	КонецЦикла;
	
	// Очищаем строку состояния
	Состояние();
	Возврат ИСТИНА;
КонецФункции // АрхивЧековСжать()

// Получает значения свойств чека из регистр сведений
// Параметры:
//	 ЧекОснование - чек основание по которому выполняется возврат
//                               
// Возвращаемое значение:
//   Таблица свойств объекта
//
Функция ПолучитьЗначениеСвойствПоВозврату(ЧекОснование)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЗначенияСвойствОбъектов.Свойство,
	                      |	ЗначенияСвойствОбъектов.Значение
	                      |ИЗ
	                      |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	                      |ГДЕ
	                      |	ЗначенияСвойствОбъектов.Объект = &Объект");
	
	Запрос.УстановитьПараметр("Объект",ЧекОснование);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

// Процедура выполняет операцию, назначенную на клавишу или карточку операции.
//
// Параметры:
//  Операция     - Строка - Строка, содержащая операцию к выполнению.
//  ЭтаФорма     - Форма - форма, в которой произошло это событие.
//  ОбъектДействия - СправочникСсылка.Номенклатура - связанный объект.
//
Процедура ВыполнитьОперацию(Операция, ЭтаФорма, ОбъектДействия = Неопределено) Экспорт
	Если ОбъектДействия = Неопределено Тогда
		ОбъектДействия = Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
	Если Найти(Операция,"ЗавершитьРаботу")=1 Тогда
		ЗавершитьРаботуСистемы();
		
	ИначеЕсли Найти(Операция, "ОбновитьПраваПользователя") Тогда
		ЗагрузитьПараметрыПользователя(Автор);
		
	ИначеЕсли Найти(Операция, "ПереинициализироватьОборудование") Тогда
		// Для переинициализации оборудования выключим и включим все экземпляры оборудования,
		// подключенные к данному компьютеру.
		
		// Запишем все GUID'ы в массив, с целью дальнейшей обработки его в цикле.
		МассивGUID = Новый Массив();
		МассивGUID.Добавить(GUID_Дисплей);
		МассивGUID.Добавить(GUID_ФР);
		МассивGUID.Добавить(GUID_Весы);
		МассивGUID.Добавить(GUID_ТСД);
		МассивGUID.Добавить(GUID_Сканер);
		МассивGUID.Добавить(GUID_Авторизатор);
		МассивGUID.Добавить(GUID_АвторизаторПлатежей);
		МассивGUID.Добавить(GUID_ПЧ);
		// Выключим все
		Для Каждого ТекGUID Из МассивGUID Цикл
			Попытка
				ОписаниеОшибки = "";
				глТорговоеОборудование.ВыключитьОборудование(ТекGUID,ОписаниеОшибки);	
			Исключение
				ТекстОшибки = ОписаниеОшибки();
				ОткрытьДиалог("Внутреннее исключение при выключении оборудования:"+Символы.ПС+ТекстОшибки, СтатусСообщения.ОченьВажное);
			КонецПопытки;
		КонецЦикла;
		// Включим все.
		ВключитьОборудование();
		// "Уничтожим" массив GUID'ов
		МассивGUID = Неопределено;
			
	// Вызов произвольной клавиатурной комбинации
	ИначеЕсли Найти(Операция,"НажатьКлавишу.")=1 Тогда
		Операнд=СтрЗаменить(Операция,"НажатьКлавишу.","");
		// сначала определим флаги модификаторов
		Если Найти(Операнд,"Shift")=0 Тогда Shift=0;
		Иначе Операнд=СтрЗаменить(Операнд,"Shift",""); Shift=1;
		КонецЕсли;
		Если Найти(Операнд,"Ctrl")=0 Тогда Ctrl=0;
		Иначе Операнд=СтрЗаменить(Операнд,"Ctrl",""); Ctrl=1;
		КонецЕсли;
		Если Найти(Операнд,"Alt")=0 Тогда Alt=0;
		Иначе Операнд=СтрЗаменить(Операнд,"Alt",""); Alt=1;
		КонецЕсли;
		Операнд=Число(СтрЗаменить(Операнд,"+",""));
		Если Операнд>0 Тогда
			Попытка	Рарус_Компонента.НажатьКлавишу(Операнд,Shift,Ctrl,Alt);
			Исключение
			КонецПопытки;
		КонецЕсли;  
		
	// Запуск приложения пользователя
	ИначеЕсли Найти(Операция,"ЗапуститьПриложение.")=1 Тогда
		Попытка
			Операнд=СтрЗаменить(Операция,"ЗапуститьПриложение.","");
			ЗапуститьПриложение(СокрЛП(Операнд));
		Исключение
			Предупреждение("Не могу выполнить операцию "+Операция,2);
		КонецПопытки;                
		
	// Выполним команду системы
	ИначеЕсли Найти(Операция,"КомандаСистемы.")=1 Тогда
		Попытка
			Операнд=СтрЗаменить(Операция,"КомандаСистемы.","");
			КомандаСистемы(СокрЛП(Операнд));
		Исключение
			Предупреждение("Не могу выполнить операцию "+Операция,2);
		КонецПопытки;			

	// Попробуем закрыть форму при помощи её же собственной кнопки
	ИначеЕсли Найти(Операция,"Форма.Закрыть")=1 Тогда
		Попытка
			ДействияФормы = ЭтаФорма.ЭлементыФормы.ОсновныеДействияФормы.ОсновныеДействияФормы.Кнопки;
			Кнопка = ДействияФормы.Найти("Закрыть");
			Если НЕ Кнопка = Неопределено Тогда
				Рарус_Компонента.НажатьКлавишу(13,0,0,0);	// <Enter>
			КонецЕсли;  
			Рарус_Компонента.НажатьКлавишу(27,0,0,0);	// <ESC>
			Возврат;
		Исключение
			Предупреждение("Не могу выполнить операцию "+Операция,2);
		КонецПопытки;
	// Попробуем нажать кнопку ОК на форме
	ИначеЕсли Найти(Операция,"Форма.ОК")=1 Тогда
		Попытка
			ДействияФормы = ЭтаФорма.ЭлементыФормы.ОсновныеДействияФормы.ОсновныеДействияФормы.Кнопки;
			Кнопка = ДействияФормы.Найти("Ок");
			Если НЕ Кнопка = Неопределено Тогда
				Рарус_Компонента.НажатьКлавишу(13,0,0,0);	// <Enter>
			КонецЕсли; 
		Исключение
			Предупреждение("Не могу выполнить операцию "+Операция,2);
		КонецПопытки;
	
	ИначеЕсли Найти(Операция,"Действие.")=1 ИЛИ Найти(Операция,"Форма.")=1 Тогда
		Попытка
			
			Если Найти(Операция,"Действие.")=1 Тогда
				Операнд  = СтрЗаменить(Операция,"Действие.","");
			Иначе
				Операнд  = СтрЗаменить(Операция,"Форма.","");
			КонецЕсли;
			Параметр = ""; НачалоПараметра =  Найти(Операнд,"["); КонецПараметра  =  Найти(Операнд,"]");
			Если НачалоПараметра > 0 Тогда
				Параметр = Сред(Операнд,НачалоПараметра+1,КонецПараметра-НачалоПараметра-1);
				Операнд  = Лев(Операнд,НачалоПараметра-1);
			КонецЕсли;
			Если НЕ ОбъектДействия.Пустая()  Тогда
				Действие = "ЭтаФорма.ЭтотОбъект.Действие"+Операнд+"(ЭтаФорма"+","""+СокрЛП(ОбъектДействия.Код)+""",0)";
			Иначе	
				Действие = "ЭтаФорма.ЭтотОбъект.Действие"+Операнд+"(ЭтаФорма"+?(ПустаяСтрока(Параметр)=1,")",","+СокрЛП(Параметр)+")");
			КонецЕсли;	 
			Выполнить(Действие);
		Исключение
			Предупреждение("Не могу выполнить операцию """+Операция+""" с объектом """+ОбъектДействия+"""",2);
		КонецПопытки;

		// Операция не распознана
	Иначе Сигнал();
		Предупреждение("Не могу выполнить операцию """+Операция+""" с объектом """+ОбъектДействия+"""",2);
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьНомерНалоговойГруппыДляПечати(ТипСпособаРасчета, СтавкаНДС, СебестоимостьАвтомобиля = 0)
	
	Аванс = СебестоимостьАвтомобиля > 0 ИЛИ ТипСпособаРасчета = Перечисления.ТипыСпособаРасчетаККТ.Предоплата;
	
	Если СтавкаНДС.Ставка = 20 И Аванс Тогда
		Возврат Справочники.СтавкиНДС.ОсновнаяСтавкаНДСРасчетная.НомерНалоговойГруппыФР;
	КонецЕсли;
	
	Возврат СтавкаНДС.НомерНалоговойГруппыФР;
	
КонецФункции

#Область ЧекКоррекции

Процедура ДействиеПробитьЧекКоррекции(ЭтаФорма) Экспорт
	
	Если ЕстьОшибкиЗаполненияЧекаКоррекции(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	СообщениеОбОшибке = Неопределено;
	
	СтруктураВозвратныхПараметров = Новый Структура;
	
	Если ВерсияФФД = 105 И ЭтаФорма.НеприменениеККТ Тогда
		ПараметрыДокумента = ПодготовитьПараметрыДляПробитияЧекаКоррекцииФФД105(ЭтаФорма);
		
		Если НЕ ПробитьЧекКоррекции(Ссылка,СообщениеОбОшибке,СтруктураВозвратныхПараметров,ПараметрыДокумента) Тогда 
			ОткрытьДиалог("ПРИ ПРОБИТИИ ЧЕКА ОБНАРУЖЕНЫ ОШИБКИ:"+Символы.ПС+СообщениеОбОшибке, СтатусСообщения.ОченьВажное, 0);
		КонецЕсли;
	Иначе
		
		СтруктураВозвратныхПараметров=Новый Структура;
		Если НЕ ПробитьЧек(СообщениеОбОшибке,СтруктураВозвратныхПараметров) Тогда
			ОткрытьДиалог("ПРИ ПРОБИТИИ ЧЕКА ОБНАРУЖЕНЫ ОШИБКИ:"+Символы.ПС+СообщениеОбОшибке, СтатусСообщения.ОченьВажное, 0);
			Возврат;
		КонецЕсли; 
	КонецЕсли;
	
	ДокументЧек = Ссылка.ПолучитьОбъект();
	ДокументЧек.ПробитЧек    = Истина;
	ДокументЧек.НомерЧека = СтруктураВозвратныхПараметров.НомерЧека;
	ДокументЧек.НомерСмены = СтруктураВозвратныхПараметров.НомерСмены;
	ДокументЧек.ДатаФР = СтруктураВозвратныхПараметров.ДатаФР;
	ДокументЧек.НомерДокумента = СтруктураВозвратныхПараметров.НомерДокумента;
	ДокументЧек.ФискальныйПризнак = СтруктураВозвратныхПараметров.ФискальныйПризнак;
	// Если изменились товары или КМ, то 
	ДокументЧек.Товары.Очистить();
	Для Каждого Строка Из Товары Цикл
		НоваяСтрока = ДокументЧек.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	ДокументЧек.КодыМаркировки.Очистить();
	Для Каждого Строка Из КодыМаркировки Цикл
		НоваяСтрока = ДокументЧек.КодыМаркировки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	Попытка
		ДокументЧек.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
	КонецПопытки;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

Функция ЕстьОшибкиЗаполненияЧекаКоррекции(ЭтаФорма)
	ТекстОшибки = ПроверитьДанныеКлиента();
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		ОткрытьДиалог(ТекстОшибки,СтатусСообщения.Важное,5);
		Возврат ИСТИНА;
	КонецЕсли;
	СуммаНалогов = ЭтаФорма.СуммаБезНДС+ЭтаФорма.СуммаНДС0+ЭтаФорма.СуммаНДС10+ЭтаФорма.СуммаНДС110+ЭтаФорма.СуммаНДС120+ЭтаФорма.СуммаНДС20;
	Если СуммаНалогов <> СуммаДокумента И Товары.Количество() > 0 Тогда
		ОткрытьДиалог("Сумма документа не равна сумме налогов!",СтатусСообщения.Важное,5);
		Возврат ИСТИНА;
	КонецЕсли;
	Возврат ЛОЖЬ;
КонецФункции

Функция ПроверитьДанныеКлиента()
	Результат = СокрЛП(ДанныеКлиента);
	ТекстОшибки = "";
	Если НЕ ПустаяСтрока(ДанныеКлиента) Тогда
		// Делаем проверку:
		// Если первый символ +, тогда в поле данных указан телефон
		// Иначе считаем, что это электронная почта, т.к. она может быть и полностью из цифр 
		ПервыйСимвол = Лев(Результат,1);
		Если ПервыйСимвол = "+" Тогда
			Если КонтактныйТелефонВалиден(Результат) Тогда
				ДанныеКлиента = Результат;
			Иначе
				ТекстОшибки = "Указан неверный мобильный телефон!";
			КонецЕсли;
		Иначе
			Если АдресЭлектроннойПочтыВалиден(Результат) Тогда
				ДанныеКлиента = Результат;
			ИначеЕсли НЕ Найти(Результат, "@") = 0 Тогда
				ТекстОшибки = "Указан неверный адрес электронный почты!";
			Иначе
				//Возможно это все таки телефон, просто забыли указать "+"
				Результат = "+" + Результат;
				Если КонтактныйТелефонВалиден(Результат) Тогда
					ДанныеКлиента = Результат;
				Иначе
					ТекстОшибки = "Указан неверный мобильный телефон или адрес электронной почты!";
					ДанныеКорректные = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ТекстОшибки;
КонецФункции

Функция ПодготовитьПараметрыДляПробитияЧекаКоррекцииФФД105(ЭтаФорма)
	
	// Сформируем структуру данных чека
	ПараметрыДокумента = Новый Структура;
	
	Если Ссылка.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда
		ПараметрыДокумента.Вставить("ТипРасчета", 1);
	ИначеЕсли Ссылка.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств Тогда
		ПараметрыДокумента.Вставить("ТипРасчета", 2);
	ИначеЕсли Ссылка.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств Тогда
		ПараметрыДокумента.Вставить("ТипРасчета", 3);
	Иначе
		ПараметрыДокумента.Вставить("ТипРасчета", 4);
	КонецЕсли;
	
	Если Ссылка.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 1);
	ИначеЕсли Ссылка.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 2);
	ИначеЕсли Ссылка.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕНВД Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 3);
	ИначеЕсли Ссылка.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ЕСН Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 4);
	ИначеЕсли Ссылка.СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.Патент Тогда
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 5);
	Иначе
		ПараметрыДокумента.Вставить("СистемаНалогообложения", 0);
	КонецЕсли;
	
	Попытка
		ИННКассира = Ссылка.Автор.ИНН;
	Исключение
		ИННКассира = "";
	КонецПопытки;
	
	ПараметрыДокумента.Вставить("Кассир",                 ?(ЗначениеЗаполнено(Ссылка.Автор), СокрЛП(Ссылка.Автор.Наименование), ""));
	ПараметрыДокумента.Вставить("КассирИНН",              ИННКассира);
	ПараметрыДокумента.Вставить("ТипКоррекции",           ?(Ссылка.ТипКоррекции = Перечисления.ТипыЧековКоррекции.Самостоятельно, 0, 1));
	ПараметрыДокумента.Вставить("НаименованиеОснования",  "");
	ПараметрыДокумента.Вставить("ДатаДокументаОснования", Ссылка.ДатаКоррекции);
	ПараметрыДокумента.Вставить("НомерДокументаОснования",Ссылка.НомерПредписания);
	ПараметрыДокумента.Вставить("Сумма",                  Ссылка.СуммаДокумента);
	ПараметрыДокумента.Вставить("СуммаБезНДС",            ЭтаФорма.СуммаБезНДС);	// Сумма без НДС
	ПараметрыДокумента.Вставить("СуммаНДС0",              ЭтаФорма.СуммаНДС0);		// Сумма НДС
	ПараметрыДокумента.Вставить("СуммаНДС10",             ЭтаФорма.СуммаНДС10);		// СуммаНДС 10
	ПараметрыДокумента.Вставить("СуммаНДС18",             ЭтаФорма.СуммаНДС20);		// СуммаНДС 20
	ПараметрыДокумента.Вставить("СуммаНДС110",            ЭтаФорма.СуммаНДС110);	// СуммаНДС 10/110
	ПараметрыДокумента.Вставить("СуммаНДС118",            ЭтаФорма.СуммаНДС120);	// Сумма НДС 20/120
	
	СуммаНал                     = 0;
	СуммаЭлектро                 = 0;
	СуммаПредоплата              = 0;
	СуммаПостоплата              = 0;
	СуммаВстречноеПредоставление = 0;
	
	Для Каждого ТекущаяСтрока Из Ссылка.Оплаты Цикл
		
		Если ТекущаяСтрока.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Наличные Тогда
			СуммаНал = СуммаНал + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Электронно Тогда
			СуммаЭлектро = СуммаЭлектро + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Предоплата Тогда
			СуммаПредоплата = СуммаПредоплата + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.Постоплата Тогда
			СуммаПостоплата = СуммаПостоплата + ТекущаяСтрока.Сумма;
		ИначеЕсли ТекущаяСтрока.ТипОплаты.ТипОплатыККТ = Перечисления.ТипыОплатыККТ.ВстречноеПредоставление Тогда
			СуммаВстречноеПредоставление = СуммаВстречноеПредоставление + ТекущаяСтрока.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыДокумента.Вставить("СуммаНал",                     СуммаНал);
	ПараметрыДокумента.Вставить("СуммаЭлектро",                 СуммаЭлектро);
	ПараметрыДокумента.Вставить("СуммаПредоплата",              СуммаПредоплата);
	ПараметрыДокумента.Вставить("СуммаПостоплата",              СуммаПостоплата);
	ПараметрыДокумента.Вставить("СуммаВстречноеПредоставление", СуммаВстречноеПредоставление);
	
	Возврат ПараметрыДокумента;
	
КонецФункции // ПодготовитьПарамтерыДляПробитияЧекаКоррекцииФФД105

#КонецОбласти


//Функция провереяет контактный телефон на корректность.
//
// Параметры:
//	АдресЭП - Строка - Строка для преобразования.
//
// Возвращаемое значение:
//	Булево.
//
Функция КонтактныйТелефонВалиден(КонтактныйТелефон) Экспорт
	
	// Проверим, чтобы первый символ был +
	ПервыйСимвол = Лев(КонтактныйТелефон,1);
	
	Если ПервыйСимвол = "+" Тогда
		
		// Проверим контактный телефон на содержание символов отличных от цифр
		ОшибочныеСимволы = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЪЭЮЯ
		|ABCDEFGHIJKLMNOPQRSTUVWXYZ
		|._@";
		
		Позиция = 2;
		
		Пока Позиция <= СтрДлина(КонтактныйТелефон) Цикл
			
			ТекущийСимвол = ВРег(Сред(КонтактныйТелефон, Позиция, 1));
			
			Если Найти(ОшибочныеСимволы, ТекущийСимвол) > 0 Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Позиция = Позиция + 1;
			
		КонецЦикла;
		
		
		// Навскякий случай уберем знаки "() -"
		КонтактныйТелефон = СтрЗаменить(КонтактныйТелефон, "(","");
		КонтактныйТелефон = СтрЗаменить(КонтактныйТелефон, ")","");
		КонтактныйТелефон = СтрЗаменить(КонтактныйТелефон, " ","");
		КонтактныйТелефон = СтрЗаменить(КонтактныйТелефон, "-","");

		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

//Функция провереяет электронную почту на корректность.
//
// Параметры:
//	АдресЭП - Строка - Строка для преобразования.
//
// Возвращаемое значение:
//	Булево.
//
Функция АдресЭлектроннойПочтыВалиден(АдресЭП) Экспорт
	
	// Проверим наличие в адресе @
	ПозицияСобаки = Найти(АдресЭП, "@");
	
	Если Не ПозицияСобаки = 0 Тогда
		// и наличие точки после собаки
		Если Найти(Сред(АдресЭП, ПозицияСобаки + 1), ".") = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	// Проверим адрес на допустимые символы
	ДопустимыеСимволы = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЪЭЮЯ
	|ABCDEFGHIJKLMNOPQRSTUVWXYZ
	|0123456789
	|._-@";
	
	Позиция = 1;
	
	Пока Позиция <= СтрДлина(АдресЭП) Цикл
		
		ТекущийСимвол = ВРег(Сред(АдресЭП, Позиция, 1));
		
		Если Найти(ДопустимыеСимволы, ТекущийСимвол) = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Позиция = Позиция + 1;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ПровестиФорматноЛогическийКонтрольТоваров(ТабличнаяЧастьДокументаОснования)
	
	КопияТабличнойЧасти = ТабличнаяЧастьДокументаОснования.Скопировать();
	КопияТабличнойЧасти.Очистить();
	
	ДопустимоеРасхождение = 0.01;
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧастьДокументаОснования Цикл
		
		Количество = ?(СтрокаТЧ.Количество = 0, 1, СтрокаТЧ.Количество);
		Если Количество = 1 Тогда
			НоваяСтрока = КопияТабличнойЧасти.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			НоваяСтрока.Цена = НоваяСтрока.Сумма;
			Продолжить;
		КонецЕсли;
		
		НоваяСуммаБезОкругления = СтрокаТЧ.Количество * СтрокаТЧ.Цена;
		Сумма = СтрокаТЧ.Сумма;
		РазницаСуммДляПроверки = НоваяСуммаБезОкругления - Сумма;
		
		СтавкаНДС = СтрокаТЧ.СтавкаНДС;
		
		// Получаем расчетную цену с учетом скидок делением входящей суммы на входящее количество.
		РасчетнаяЦена = Окр(Сумма / Количество, 2, 1);
		// Здесь и далее все цены и суммы округляем до 2 знаков после запятой, количества - до 3 знаков.
		// Умножаем входящее количество  на расчетную цену и получаем новую промежуточную сумму.
		НоваяСумма = Окр(Количество * РасчетнаяЦена, 2, 1);
		// Вычисляем разницу между промежуточной и входящей суммой.
		РазницаСумм = НоваяСумма - Сумма;
		Если Окр(СтрокаТЧ.Цена * Количество, 2, 1) = СтрокаТЧ.Сумма Тогда
			// Цену не пересчитываем.
			НоваяСтрока = КопияТабличнойЧасти.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			Продолжить;
		ИначеЕсли РазницаСуммДляПроверки >= -ДопустимоеРасхождение И РазницаСуммДляПроверки <= ДопустимоеРасхождение Тогда
			// Если разница допустима - оставляем одну строку.
			// Если разницы нет - оставляем одну строку.
			НоваяСтрока = КопияТабличнойЧасти.Добавить();
			// Но подменяем расчетную цену.
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			НоваяСтрока.Цена = РасчетнаяЦена;
			Продолжить;
		ИначеЕсли РазницаСумм = 0 Тогда
			// Если разница неокругленная есть, а округленной нет.
			// То передаем в ККТ как есть - оставляем одну строку.
			НоваяСтрока = КопияТабличнойЧасти.Добавить();
			// Но подменяем расчетную цену.
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			НоваяСтрока.Цена = РасчетнаяЦена;
			Продолжить;
		Иначе
			// Разделяем строку.
			РазделитьФискальнуюСтроку(СтрокаТЧ, КопияТабличнойЧасти, РасчетнаяЦена, РазницаСумм);
		КонецЕсли;
		
	КонецЦикла;
	
	ТабличнаяЧастьДокументаОснования = КопияТабличнойЧасти.Скопировать();
	
КонецПроцедуры

Процедура РазделитьФискальнуюСтроку(ТекущаяПозиция, НовыеПозицииЧека, РасчетнаяЦена, РазницаСумм)
	
	// Запоминаем начальные значения.
	КоличествоНачальное = ТекущаяПозиция.Количество;
	СуммаСкидокНачальная = ТекущаяПозиция.СуммаСкидкиСтроки;
	СуммаНДСНачальная = ТекущаяПозиция.СуммаНДС; // Рассчитывается формально, т.к. сумма НДС текущими драйверами игнорируется.
	СуммаНачальная = ТекущаяПозиция.Сумма;
	
	// Если разница есть, то ее делим на копейку.
	// Получаем количество, которое нужно переоценить.
	КоличествоПереоценки = Окр(РазницаСумм / 0.01, 3, 1);
	Если КоличествоПереоценки < 0 Тогда
		КоличествоПереоценки = -КоличествоПереоценки;
	КонецЕсли;
	
	// Из начального количества отнимаем количество переоценки.
	// Получаем количество, которое остается по расчетной цене.
	КоличествоРасчетное = ТекущаяПозиция.Количество - КоличествоПереоценки;
	
	// Цена переоценки во всех примерах отличается от расчетной на 1 копейку.
	// В  большую или меньшую сторону в зависимости от знака разницы - зависимость обратная.
	ЦенаПереоценки = РасчетнаяЦена - Окр(РазницаСумм / КоличествоПереоценки, 2, 1);
	
	// Теперь считаются суммы, т.к. по ним выполняется анализ коэффициента для пересчета
	СуммаПереоценки = Окр(КоличествоПереоценки * ЦенаПереоценки, 2, 1);
	СуммаРасчетная = Окр(КоличествоРасчетное * РасчетнаяЦена, 2, 1);
	
	// Распределяем суммы скидок пропорционально количеству.
	// Если суммы есть. А если их нет, то должно остаться Неопределено.
	Если СуммаСкидокНачальная <> Неопределено Тогда
		Если КоличествоПереоценки > КоличествоРасчетное Тогда
			СуммаСкидокПереоценки = Окр(СуммаСкидокНачальная * КоличествоПереоценки / КоличествоНачальное, 2, 1);
			СуммаСкидокРасчетная = СуммаСкидокНачальная - СуммаСкидокПереоценки;
		Иначе
			СуммаСкидокРасчетная = Окр(СуммаСкидокНачальная * КоличествоРасчетное / КоличествоНачальное, 2, 1);
			СуммаСкидокПереоценки = СуммаСкидокНачальная - СуммаСкидокРасчетная;
		КонецЕсли;
	КонецЕсли;
	// А суммы распределяем НДС пропорционально суммам.
	// Потому что распределение пропорционально количеству.
	// Вызывает расхождения в некоторых критичных ситуациях.
	// Если суммы есть. А если их нет, то должно остаться Неопределено.
	Если СуммаНДСНачальная <> Неопределено Тогда
		Если СуммаПереоценки > СуммаРасчетная Тогда
			СуммаНДСПереоценки = Окр(СуммаНДСНачальная * СуммаПереоценки / СуммаНачальная, 2, 1);
			СуммаНДСРасчетная = СуммаНДСНачальная - СуммаНДСПереоценки;
		Иначе
			СуммаНДСРасчетная = Окр(СуммаНДСНачальная * СуммаРасчетная / СуммаНачальная, 2, 1);
			СуммаНДСПереоценки = СуммаНДСНачальная - СуммаНДСРасчетная;
		КонецЕсли;
	КонецЕсли;
	
	// Выводим вместо первоначальной строки две новых.
	// Сначала с расчетной ценой и остатком количества.
	// И суммой - произведение цены и количества.
	НоваяПозиция = НовыеПозицииЧека.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПозиция, ТекущаяПозиция);
	НоваяПозиция.Количество = КоличествоРасчетное;
	НоваяПозиция.Цена = РасчетнаяЦена;
	НоваяПозиция.Сумма = СуммаРасчетная;
	Если СуммаСкидокНачальная <> Неопределено Тогда
		НоваяПозиция.СуммаСкидкиСтроки = СуммаСкидокНачальная - СуммаСкидокПереоценки;
	КонецЕсли;
	Если СуммаНДСНачальная <> Неопределено Тогда
		НоваяПозиция.СуммаНДС = СуммаНДСРасчетная;
	КонецЕсли;
	
	// Потом с переоцененной ценой и переоцененным количеством.
	// И суммой - произведение цены и количества.
	НоваяПозиция = НовыеПозицииЧека.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПозиция, ТекущаяПозиция);
	НоваяПозиция.Количество = КоличествоПереоценки;
	НоваяПозиция.Цена = ЦенаПереоценки;
	НоваяПозиция.Сумма = СуммаПереоценки;
	Если СуммаСкидокНачальная <> Неопределено Тогда
		НоваяПозиция.СуммаСкидкиСтроки = СуммаСкидокПереоценки;
	КонецЕсли;
	Если СуммаНДСНачальная <> Неопределено Тогда
		НоваяПозиция.СуммаНДС = СуммаНДСПереоценки;
	КонецЕсли;
	
КонецПроцедуры

Функция ДайТаблицуТоваров()
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("НомерСтроки"); // 0
	Результат.Колонки.Добавить("НомерОтдела"); // 1
	Результат.Колонки.Добавить("Артикул"); // 2
	Результат.Колонки.Добавить("Наименование"); // 3
	Результат.Колонки.Добавить("Цена"); // 4
	Результат.Колонки.Добавить("Количество"); // 5
	Результат.Колонки.Добавить("Сумма"); // 6
	Результат.Колонки.Добавить("НаименованиеСкидки"); // 7
	Результат.Колонки.Добавить("СуммаСкидкиСтроки"); // 8
	Результат.Колонки.Добавить("ПроцентСкидкиСтроки"); // 9
	Результат.Колонки.Добавить("ХарактеристикаНоменклатуры"); // 10
	Результат.Колонки.Добавить("СуммаНДС"); // 11
	Результат.Колонки.Добавить("ТипПозиции"); // 12
	Результат.Колонки.Добавить("СтавкаНДС"); // 13
	Результат.Колонки.Добавить("НомерНалоговойГруппыФР"); // 14
	Результат.Колонки.Добавить("СуммаНалога2"); // 15
	Результат.Колонки.Добавить("СтавкаНалога2"); // 16
	Результат.Колонки.Добавить("НомерНалоговойГруппыФР2"); // 17
	Результат.Колонки.Добавить("СуммаРаспределеннойСкидки"); // 18
	Результат.Колонки.Добавить("ПризнакСпособаРасчета"); // 19
	Результат.Колонки.Добавить("ПризнакПредметаРасчета"); // 20
	Результат.Колонки.Добавить("КоличествоУпаковок"); // 21
	Результат.Колонки.Добавить("ЕдиницаИзмеренияПредметаРасчета"); // 22
	Результат.Колонки.Добавить("ГТДПредставление"); // 23
	Результат.Колонки.Добавить("КодСтраныПроисхожденияТоваров"); // 24
	Результат.Колонки.Добавить("ПризнакАгента"); // 25
	Результат.Колонки.Добавить("ТелефонПоставщика"); // 26
	Результат.Колонки.Добавить("НаименованиеПоставщика"); // 27
	Результат.Колонки.Добавить("ИННПоставщика"); // 28
	Результат.Колонки.Добавить("ТипМаркировки"); // 29
	Результат.Колонки.Добавить("GTIN"); // 30
	Результат.Колонки.Добавить("СерийныйНомер"); // 31
	Результат.Колонки.Добавить("КодМаркировки"); // 32
	Результат.Колонки.Добавить("МераКоличестваПредметаРасчетаККТКод"); // 33
	Результат.Колонки.Добавить("ДробноеКоличествоМаркированногоТовараЧислитель"); // 34
	Результат.Колонки.Добавить("ДробноеКоличествоМаркированногоТовараЗнаменатель"); // 35
	Результат.Колонки.Добавить("РезультатПроверкиСведенийОТоваре"); // 36
	Результат.Колонки.Добавить("ПланируемыйСтатусМаркируемогоТовара"); // 37
	Результат.Колонки.Добавить("ИдентификаторФОИВ"); // 38
	Результат.Колонки.Добавить("ДатаДокументаОснования"); // 39
	Результат.Колонки.Добавить("НомерДокументаОснования"); // 40
	Результат.Колонки.Добавить("ЗначениеОтраслевогоРеквизита"); // 41
	Результат.Колонки.Добавить("СуммаАкциза"); // 42
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТаблицуТоваров(SafeArrayТаблицаТоваров, врТаблицаТоваров)
	
	НомерСтроки = 0;
	Для Каждого Строка Из врТаблицаТоваров Цикл
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 0, Строка.НомерСтроки);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 1, Строка.НомерОтдела);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 2, Строка.Артикул);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 3, Строка.Наименование);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 4, Строка.Цена);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 5, Строка.Количество);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 6, Строка.Сумма);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 7, Строка.НаименованиеСкидки);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 8, Строка.СуммаСкидкиСтроки);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 9, Строка.ПроцентСкидкиСтроки);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 10, Строка.ХарактеристикаНоменклатуры);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 11, Строка.СуммаНДС);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 12, Строка.ТипПозиции);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 13, Строка.СтавкаНДС);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 14, Строка.НомерНалоговойГруппыФР);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 15, Строка.СуммаНалога2);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 16, Строка.СтавкаНалога2);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 17, Строка.НомерНалоговойГруппыФР2);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 18, Строка.СуммаРаспределеннойСкидки);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 19, Строка.ПризнакСпособаРасчета);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 20, Строка.ПризнакПредметаРасчета);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 21, Строка.КоличествоУпаковок);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 22, Строка.ЕдиницаИзмеренияПредметаРасчета);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 23, Строка.ГТДПредставление);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 24, Строка.КодСтраныПроисхожденияТоваров);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 25, Строка.ПризнакАгента);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 26, Строка.ТелефонПоставщика);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 27, Строка.НаименованиеПоставщика);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 28, Строка.ИННПоставщика);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 29, Строка.ТипМаркировки);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 30, Строка.GTIN);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 31, Строка.СерийныйНомер);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 32, Строка.КодМаркировки);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 33, Строка.МераКоличестваПредметаРасчетаККТКод);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 34, Строка.ДробноеКоличествоМаркированногоТовараЧислитель);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 35, Строка.ДробноеКоличествоМаркированногоТовараЗнаменатель);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 36, Строка.РезультатПроверкиСведенийОТоваре);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 37, Строка.ПланируемыйСтатусМаркируемогоТовара);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 38, Строка.ИдентификаторФОИВ);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 39, Строка.ДатаДокументаОснования);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 40, Строка.НомерДокументаОснования);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 41, Строка.ЗначениеОтраслевогоРеквизита);
		SafeArrayТаблицаТоваров.SetValue(НомерСтроки, 42, Строка.СуммаАкциза);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	////БИЗТЕХ КОВАЛЬ 01.11.2025 ++
	//ЗаписьXML = Новый ЗаписьXML;
	//ЗаписьXML.УстановитьСтроку();
	//ОбъектXDTO = СериализаторXDTO.ЗаписатьXDTO(врТаблицаТоваров);
	//ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектXDTO);
	//ДанныеXML = ЗаписьXML.Закрыть();
	//ЗаписьЖурналаРегистрации("МониторингОшибкиККМприМаркировкеТаблицаТоваров", УровеньЖурналаРегистрации.Информация,,ДанныеXML);
	////БИЗТЕХ КОВАЛЬ 01.11.2025 --
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
НомерВнешнегоСобытия = -1;
Режим = ".";
ТекстОшибки				= "";
РезультатыФронта = Новый Структура("Ок, Ошибка, Предупреждение");
РезультатыФронта.Вставить("Ок",0);
РезультатыФронта.Вставить("Ошибка",1);
РезультатыФронта.Вставить("Предупреждение",2);
флУстанавливатьХарактеристику = Ложь;
     
Рарус_Компонента		= глРарус_Компонента;
Права 					= глПрава;
ПометкаУдаления			= Ложь;
GUID_Дисплей			= "";
GUID_ФР					= "";
GUID_ТСД				= "";
GUID_Весы				= "";
GUID_ПЧ					= "";
ТаймаутДисплея			= 30;
ТаймаутАвторизатора		= 90;
ТаймаутФР				= 60;
ТаймаутВесы             = 45;
ТаймаутТСД				= 45;
ТаймаутПЧ				= 60;
ДлинаСтрокиФР			= 24;	// По умолчанию берем поменьше, позже спросим у оборудования
ДлинаСтрокиПЧ			= 24;	// По умолчанию берем поменьше, позже спросим у оборудования
ДлинаСтрокиДисплея		= 20; 	// По умолчанию берем стандартное, потом спросим у дисплея
ДисплейИнформацияОСуммеЧека = Ложь;

тзДоступныеВидыОплат	= Новый ТаблицаЗначений;
тзДоступныеВидыОплат.Колонки.Добавить("Номер");
тзДоступныеВидыОплат.Колонки.Добавить("ТипОплаты");

// Таблица платежей
ТаблицаБП = Новый ТаблицаЗначений;
ТаблицаБП.Колонки.Добавить("ТипКарты");
ТаблицаБП.Колонки.Добавить("НомерКарты");
ТаблицаБП.Колонки.Добавить("СуммаОплаты");
ТаблицаБП.Колонки.Добавить("ВнешнийНомерТранзакции");
ТаблицаБП.Колонки.Добавить("НомерСсылки");
ТаблицаБП.Колонки.Добавить("КодАвторизации");
ТаблицаБП.Колонки.Добавить("ТекстКвитанции");
ТаблицаБП.Колонки.Добавить("АвторизацияПрошла");
ТаблицаБП.Колонки.Добавить("АвторизацияОтменена");
ТаблицаБП.Колонки.Добавить("КвитанцияНапечатана");
//ТаблицаБП.Колонки.Добавить("ТипОперации");


тзПлатежиЗаУслуги	= Новый ТаблицаЗначений;
тзПлатежиЗаУслуги.Колонки.Добавить("Компьютер");
тзПлатежиЗаУслуги.Колонки.Добавить("Касса");
тзПлатежиЗаУслуги.Колонки.Добавить("Оборудование");
тзПлатежиЗаУслуги.Колонки.Добавить("Оператор");
тзПлатежиЗаУслуги.Колонки.Добавить("Фискализация");
тзПлатежиЗаУслуги.Колонки.Добавить("Статус");
тзПлатежиЗаУслуги.Колонки.Добавить("КодАвторизации");
тзПлатежиЗаУслуги.Колонки.Добавить("ЛогАвторизации");
тзПлатежиЗаУслуги.Колонки.Добавить("Сумма");
тзПлатежиЗаУслуги.Колонки.Добавить("Параметр1");
тзПлатежиЗаУслуги.Колонки.Добавить("Параметр2");
тзПлатежиЗаУслуги.Колонки.Добавить("Параметр3");
тзПлатежиЗаУслуги.Колонки.Добавить("ФискальныйНомерПозиции");
тзПлатежиЗаУслуги.Колонки.Добавить("КодОшибки");
тзПлатежиЗаУслуги.Колонки.Добавить("ОписаниеОшибки");
тзПлатежиЗаУслуги.Колонки.Добавить("НомерЧека");
тзПлатежиЗаУслуги.Колонки.Добавить("ДатаЧека");
тзПлатежиЗаУслуги.Колонки.Добавить("ВремяЧека");

ТекСуммаПлатежа = 0;

флДоступностьНал		= Ложь;
флДоступностьБН			= Ложь;
РежимДиагностики		= Ложь;
фкРучнойВводВозврата	= Ложь;

флВестиЛогДействийПользователяВоФронте = обПраво("ВестиЛогДействийПользователяВоФронте", Права);

// Архив чеков
АрхивЧековПодключен     = Ложь;
АрхивЧековВерсияАрхиваЧеков = "1.03";  // Определим текущую версию архива чеков

// Инициализируем переменные для автоматического теста АРМ
РежимТестирования                          = ЛОЖЬ;
ОтветДиалогаПоумолчаниюВРежимеТестирования = Неопределено;

ВыделениеМежценовойРазницыОтдельнойСтрокой=обПраво("ВыделениеМежценовойРазницыОтдельнойСтрокой",Права);

НеПечататьФискальныйЧекПриОтправкеЭлектронногоЧекаПокупателю = Константы.НеПечататьФискальныйЧекПриОтправкеЭлектронногоЧекаПокупателю.Получить();

#КонецЕсли
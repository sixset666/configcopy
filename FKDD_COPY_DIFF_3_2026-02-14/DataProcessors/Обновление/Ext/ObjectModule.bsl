//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СПЕЦИАЛЬНОГО НАЗНАЧЕНИЯ
//////////////////////////////////////////////////////////////////////////////////

// Формируют дерево доступных релизов, и информирует о наличии обновлений
Функция СформироватьДеревоДоступныхРелизов(ДеревоРелизов) Экспорт
	Структура = эпПолучитьАдресСервераИРесурса(элСформироватьАдресНаНовостнойПотокПродукта());	
	Если ТипЗнч(Структура)<>Тип("Структура") Тогда 		
		Возврат "Неправильно задан адрес новости!";			
	КонецЕсли;
	
	// Пробуем получить XML файл новости с сервера	
	#Если Клиент Тогда 
		Состояние("Получение списка доступных обновлений...");		
	#КонецЕсли 
	
	Попытка 
		HTTPСоединение = Новый HTTPСоединение(СтрЗаменить(Структура.Сервер, "www.",""));
		HTTPСоединение.Получить(Структура.Ресурс, КаталогВременныхФайлов() + "News.xml");		
		HTTPСоединение = "";	
	Исключение
		Попытка 
			HTTPСоединение = Новый HTTPСоединение(Структура.Сервер);
			HTTPСоединение.Получить(Структура.Ресурс, КаталогВременныхФайлов() + "News.xml");		
			HTTPСоединение = "";
		Исключение 			
			Возврат "Ошибка при получении списка доступных обновлений: " + РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке());
		КонецПопытки;	
	КонецПопытки; 	
	
	// Теперь разберем XML, который получили.
	#Если Клиент Тогда 
		Состояние("Получение списка доступных обновлений...");  		
	#КонецЕсли 
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(КаталогВременныхФайлов() + "News.xml");
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Имя");
	Дерево.Колонки.Добавить("Значение");
	Дерево.Колонки.Добавить("Атрибуты");
	
	эпЗаполнитьДеревоЗначенийИзXML(ЧтениеXML, Дерево);	
	ЧтениеXML = "";
	
	// Обрабатываем дерево.
	Если Дерево.Строки.Количество()=0 Тогда  	
		Возврат "Доступных обновлений нет!";			
	КонецЕсли;
	
	Версия   = 0;
	Атрибуты = Дерево.Строки[0].Атрибуты;
	Имя      = Дерево.Строки[0].Имя;
	Если НЕ Атрибуты.Свойство("version", Версия) И Имя<>"RDF" Тогда
		Возврат "Ошибка при получении списка доступных обновлений. Неверный формат.";
	КонецЕсли;
	
	тзНовости = Новый ТаблицаЗначений;
	тзНовости.Колонки.Добавить("Заголовок");
	тзНовости.Колонки.Добавить("Содержание");
	тзНовости.Колонки.Добавить("Ссылка");
	тзНовости.Колонки.Добавить("КопиРайт");
	тзНовости.Колонки.Добавить("ВебМастер");
	тзНовости.Колонки.Добавить("ДатаПубликации");
	тзНовости.Колонки.Добавить("ЭлПочтаАвтора");  
	
	Если Версия="2.0" Тогда
		эпОбработатьНовостиФормата_2_0(Дерево.Строки[0].Строки[0], тзНовости); // Переходим к каналу
	ИначеЕсли (Версия="1.0" ИЛИ Имя="RDF") Тогда
		эпОбработатьНовостиФормата_1_0(Дерево.Строки[0], тзНовости); // Переходим к каналу
	ИначеЕсли Версия="0.91" Тогда
		эпОбработатьНовостиФормата_2_0(Дерево.Строки[0].Строки[0], тзНовости); // Переходим к каналу
	Иначе
		Возврат "Ошибка при получении списка доступных обновлений"; 	
	КонецЕсли;  	
	
	Если тзНовости.Количество()=0 Тогда
		Возврат "Доступных обновлений нет!";
	Иначе
		тзНовости.Сортировать("ДатаПубликации");
	КонецЕсли;
	
	ДеревоРелизов.Строки.Очистить(); 	
	Корень = ДеревоРелизов.Строки.Добавить();
	Корень.Релиз = СокрЛП(СтрЗаменить(тзНовости[0].Заголовок, "Сервер обновлений ООО ""1С-Рарус Альфа-Авто"":", ""));
	
	// Содержимое каждой новости представляет собой HTML текст. Из него нам необходимо получить
	// ссылки на конкретные файлы с обновлениями. Для этого необходимо использовать парсер.
	НовыйHTMLДокумент = Новый COMОбъект("HtmlFile");  	
	
	HTTPСоединение = Новый HTTPСоединение("update.rarus.ru",, СформироватьЛогин(Логин), Пароль);
	Попытка
		HTTPСоединение.Получить("update/checkconnection.txt",ПолучитьИмяВременногоФайла());		
	Исключение
		HTTPСоединение = Новый HTTPСоединение("update.rarus.ru",, Логин, Пароль);
		Попытка
			HTTPСоединение.Получить("update/checkconnection.txt",ПолучитьИмяВременногоФайла());		
		Исключение
			HTTPСоединение = "";
			Возврат РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке());
		КонецПопытки; 
	КонецПопытки; 
	Результат = "";
	
	Для Каждого ТекНовость Из тзНовости Цикл
		НомерРелиза = ВыделитьНомерРелиза(ТекНовость.Заголовок);
		Если НомерРелиза="" Тогда
			Продолжить;
		КонецЕсли;		
		
		Результат = РазобратьСодержание(ТекНовость.Содержание, НовыйHTMLДокумент, HTTPСоединение);
		Если Результат=Неопределено Тогда // Пустое наполнение новости (в идеале быть не должно)
			Продолжить;
		ИначеЕсли ТипЗнч(Результат)<>Тип("Структура") Тогда  // Возникла ошибка выполнения, возможно неправильный пароль.
			Прервать;
		КонецЕсли; 	
		
		Содержание = Результат.Содержание;
		спСостав    = Результат.Состав;
		
		// Разобрали текущую новость. При этом также разобрали содержание новости,
		// получив в результате необходимые ссылки
		// на файлы с обновлениями. Далее, добавляем саму новость в дерево релизов,
		// а ссылки на файлы добавляем уже в эту новость (т.е. подчиненные строки)
		НоваяСтрока       = Корень.Строки.Добавить();
		НоваяСтрока.Релиз = "(" + Формат(ТекНовость.ДатаПубликации, "ДЛФ=D")  + ") " + НомерРелиза;
		Если СтрДлина(НомерРелиза)>12 Тогда
			НоваяСтрока.Содержание = "<u><b>" + НомерРелиза + "</b></u></br></br>" + Содержание;
		Иначе
			НоваяСтрока.Содержание = "<u><b> Релиз " + НомерРелиза + " от " + Формат(ТекНовость.ДатаПубликации, "ДЛФ=D") +  "</b></u></br></br>" + Содержание; 
		КонецЕсли;
		НоваяСтрока.ДатаРелиза  = ТекНовость.ДатаПубликации;
		НоваяСтрока.НомерРелиза = НомерРелиза;
		
		Для Каждого ТекСодержимое Из спСостав  Цикл
			СтрокаСостава = НоваяСтрока.Строки.Добавить();
			СтрокаСостава.Релиз        = ТекСодержимое.Представление;
			СтрокаСостава.АдресСервера = ТекСодержимое.Значение.АдресСервера; 
			СтрокаСостава.АдресРесурса = ТекСодержимое.Значение.АдресРесурса;
			СтрокаСостава.РазмерФайла  = ТекСодержимое.Значение.РазмерФайла;
			СтрокаСостава.ДатаРелиза   = ТекНовость.ДатаПубликации;
			СтрокаСостава.НомерРелиза  = НомерРелиза;
		КонецЦикла;  						
	КонецЦикла;  
	
	HTTPСоединение = "";
	
	Если ТипЗнч(Результат)=Тип("Строка") Тогда
		Возврат Результат;	
	Иначе
		Возврат "";	
	КонецЕсли;

	КонецФункции // СформироватьДеревоДоступныхРелизов()

// Возвращает из переданной строки номер релиза
Функция ВыделитьНомерРелиза(Строка)
	
	Строка = СокрЛП(Строка);
	ОткрывающаяСкобка = Найти(Строка, "[");
	Если ОткрывающаяСкобка=0 Тогда
		Возврат "";
	КонецЕсли;
	
	ЗакрывающаяСкобка = Найти(Строка, "]");
	Если ЗакрывающаяСкобка=0 Тогда
		Возврат "";
	КонецЕсли;
	
	НомерРелиза = СокрЛП(Сред(Строка, ОткрывающаяСкобка+1, ЗакрывающаяСкобка-ОткрывающаяСкобка-1)); 
	
	Возврат НомерРелиза;
КонецФункции // ВыделитьНомерРелиза()

// Преобразование строки в html документ
Функция РазобратьСодержание(Знач Строка, НовыйHTMLДокумент, HTTPСоединение)
	
	Результат = Новый Структура;
	Результат.Вставить("Содержание", "");	
	Результат.Вставить("Состав", Новый СписокЗначений);
	
	// Парсим HTML текст.
	НовыйHTMLДокумент.close();
	НовыйHTMLДокумент.write(Строка);
	
	// Получаем коллекцию тегов <li>. Причем эти теги содержат дочерние теги <A>,
	// в которых находятся так необходимые нам ссылки.
	Ссылки = НовыйHTMLДокумент.all.tags("li");
	
	Для Каждого ТекСсылка Из Ссылки Цикл
		стрВрем = ТекСсылка.innerText;
		
		ВложенныйЭлемент = ТекСсылка.children.item(0);
		стрАдрес = Новый Структура;
		стрАдрес.Вставить("АдресСервера", ВложенныйЭлемент.hostname);
		стрАдрес.Вставить("АдресРесурса", ВложенныйЭлемент.pathname);
		
		
		// Теперь из текста тега-родителя, уберем текст-дитя
		стрВрем = СтрЗаменить(стрВрем, ВложенныйЭлемент.innerText, ""); 
		стрВрем = СтрЗаменить(стрВрем, "(", ""); 
		стрВрем = СтрЗаменить(стрВрем, ")", "");
		стрВрем = СокрЛП(стрВрем);
		
		стрАдрес.Вставить("РазмерФайла", стрВрем); 
		Результат.Состав.Добавить(стрАдрес, ВложенныйЭлемент.innerText); 
	КонецЦикла;
	
	// Получим описание релиза. 
	Если Результат.Состав.Количество()=0 Тогда
		Возврат Неопределено;	
	КонецЕсли;
	
	АдресРесурса = Результат.Состав[0].Значение.АдресРесурса;	
	// Теперь переформируем адрес
	Номер = Найти(АдресРесурса, "storage/");
	Если Номер=0 Тогда
		Возврат Результат;		
	КонецЕсли;
	
	Длина                = СтрДлина(АдресРесурса);
	КолСлэшей            = 0;
	НомерПоследнегоСлэша = 0;
	
	Для Сч = Номер По Длина Цикл
		Если Сред(АдресРесурса, Сч, 1)="/" Тогда
			КолСлэшей = КолСлэшей + 1;
		КонецЕсли;
		Если КолСлэшей=3 Тогда
			НомерПоследнегоСлэша = Сч;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если КолСлэшей = 0 Тогда
		Возврат Результат;		
	КонецЕсли;

	АдресРесурса = Сред(АдресРесурса, 1, НомерПоследнегоСлэша) + "release.txt"; 
	
	ПутьКПолученномуФайлу = ПолучитьИмяВременногоФайла();
	Попытка 
		HTTPСоединение.Получить(АдресРесурса, ПутьКПолученномуФайлу);		
	Исключение		
		Возврат РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке())
	КонецПопытки;	
	
	ЧтениеТекста         = Новый ЧтениеТекста(ПутьКПолученномуФайлу);
	Результат.Содержание = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	ЧтениеТекста = "";
	
	Результат.Содержание = СтрЗаменить(Результат.Содержание, Символы.ПС, "</br>");
	Результат.Содержание = Результат.Содержание;
	
	Возврат Результат;	
КонецФункции // РазобратьСодержание()

// получение имени и расширения ресурса из url
// Параметры:
//	Ресурс		 - ресурс имя и расширение которого ищем
//	ИмяФайла	 - имя файла ресурса
//	Расширение	 - расширение ресурса
// Возвращает ложь в случае не удачи и истину если удалось получить данные
//
Функция ПолучитьИмяИРасширениеРесурсаИзURL(Ресурс, ИмяФайла="", Расширение="") Экспорт
	// Найдем последний слэш, все, что после него - имя файла
	НомерСлэша  = 0;
	ДлинаСтроки = СтрДлина(Ресурс);
	Для Сч = 1 По ДлинаСтроки Цикл
		Если Сред(Ресурс, Сч, 1)="/" Тогда
			НомерСлэша = Сч;
		КонецЕсли;
	КонецЦикла;
	
	Если НомерСлэша = 0 ИЛИ НомерСлэша = ДлинаСтроки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПолнИмяФайла = Сред(Ресурс, НомерСлэша+1);
	
	// Теперь найдем точку.
	НомерТочки = Найти(ПолнИмяФайла, ".");
	Если НомерТочки=0 Тогда
		ИмяФайла   = ПолнИмяФайла;
		Расширение = "";
	Иначе
		ИмяФайла   = Лев(ПолнИмяФайла, НомерТочки-1);
		Расширение = Сред(ПолнИмяФайла, НомерТочки+1);
	КонецЕсли;
	
	Возврат Истина;
КонецФункции // ПолучитьИмяИРасширениеРесурса()

// Загрузка и распаковка файла с обновлением
// Параметры:
//	АдресСервера - адрес сервера обновлений
//	АдресРесурса - адрес загружаемого ресурса
//	ПутьДляЗагружаемогоОбновления - путь для сохранении загружаемых файлов
// Возвращаемое значение:
//	Строку с описание ошибок(если их не было то пустую строку)
//
Функция ЗагрузитьИРаспаковатьФайлСОбновлением(АдресСервера, АдресРесурса, ПутьДляЗагружаемогоОбновления) Экспорт
	#Если Клиент Тогда
		Состояние("Загрузка файла...");	
	#КонецЕсли
	
	ПутьКАрхиву = "";
	Форма = ПолучитьФорму("ЗагрузкаФайла");
	Форма.АдресСервера = АдресСервера;
	Форма.АдресРесурса = АдресРесурса;
	Результат = Форма.ОткрытьМодально();
	
	Если Результат = Неопределено Тогда
		Возврат Форма.Ошибка;
	Иначе
		ПутьКАрхиву = Результат;  
	КонецЕсли;
	
	// Файл загрузили, теперь нужно распаковать
	#Если Клиент Тогда
		Состояние("Распаковка архива...");	
	#КонецЕсли	
	
	Попытка
		УдалитьФайлы(ПутьДляЗагружаемогоОбновления, "*");
		ЧтениеАрхива = Новый ЧтениеZipФайла(ПутьКАрхиву);
		ЧтениеАрхива.ИзвлечьВсе(ПутьДляЗагружаемогоОбновления);
		ЧтениеАрхива = "";
	Исключение
		Возврат "" + ОписаниеОшибки();
	КонецПопытки;
	
	Возврат "";
КонецФункции // ЗагрузитьИРаспаковатьФайлСОбновлением()

// возвращает путь к каталогу по умолчанию
Функция ПолучитьКаталогПоУмолчаниюДляЗагружаемогоОбновления() Экспорт 
	Возврат КаталогВременныхФайлов() + "TempOfUpdates\";	
КонецФункции // ПолучитьКаталогПоУмолчанию()

// возвращает строку с параметрами подключения к базе
Функция ПолучитьСтрокуСПараметрамиПодключенияКБазе(Знач Строка) Экспорт
	Строка = СокрЛП(Строка);
	
	// Сначала путь к базе (либо имя базы и имя сервера)  	
	Номер = Найти(Строка, "File=");
	Если Номер<>0 Тогда // Значит файловый режим работы
		Номер  = Найти(Строка, """");
		Строка = Сред(Строка, Номер+1);
		Номер  = Найти(Строка, """");
		Строка = Сред(Строка, 1, Номер-1);
		Строка = "/F """ + Строка + """";
	Иначе
		ИмяСервера = "";
		ИмяБазы    = ""; 		
		Номер = Найти(Строка, "Srvr=""");
		ИмяСервера = Сред(Строка, Номер+6);
		Номер = Найти(ИмяСервера, """");
		ИмяСервера = Сред(ИмяСервера, 1, Номер-1);
		Номер = Найти(Строка, "Ref=""");
		ИмяБазы = Сред(Строка, Номер+5);
		Номер = Найти(ИмяБазы, """");
		ИмяБазы = Сред(ИмяБазы, 1, Номер-1);
		Строка = "/S """ + ИмяСервера + "\" + ИмяБазы + """"; 		
	КонецЕсли;
	
	// Пользователя и пароль получим
	Пользователь = ?(ПустаяСтрока(ИмяПользователя()), Неопределено, ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователя()));
	Если Пользователь<>Неопределено Тогда
		Строка = Строка + " /N " + """" + Пользователь.Имя + """";		
		Если Пользователь.ПарольУстановлен Тогда
			Строка = Строка + " /P " + """" + Пользователь.Пароль + """";
		КонецЕсли;    		
	КонецЕсли;
	
	Возврат Строка; 	
КонецФункции // ПолучитьСтрокуСПараметрамиПодключенияКБазе()

// возвращает строку с полным путем для обновления
Функция ПолучитьПолныйПутьДляОбновления(Знач Путь, ТекСтрока=Неопределено) Экспорт
	ДопПапка  = "";
	ДопПапка = СокрЛП(ТекСтрока.Родитель.НомерРелиза);  		                
	Если СтрДлина(ДопПапка)<12 Тогда
		ДопПапка = "\Релиз (" + ДопПапка + ")_от_" + СтрЗаменить(Формат(ТекСтрока.ДатаРелиза,"ДЛФ=D"), ".", "_");
	Иначе
		ДопПапка = "\" + СтрЗаменить(ДопПапка, " ", "_");
		ДопПапка = "\" + СтрЗаменить(ДопПапка, Символы.ПС, "_");
	КонецЕсли;
	
	ДопПапка = ДопПапка + "\" + СтрЗаменить(ТекСтрока.Релиз, " ", "_");
	ДопПапка = СтрЗаменить(ДопПапка, ".", "_");
	
	Если Найти(Путь, ДопПапка)=0 Тогда
		Путь = Путь + ДопПапка;	
	КонецЕсли; 
	
	Путь = СтрЗаменить(Путь, "\\", "\");	
	Возврат Путь; 	
КонецФункции // ПолучитьПолныйПутьДляОбновления()

// Возвращает стандартный каталог для архива информационной базы
Функция ПолучитьСтандартныйКаталогДляАрхиваИБ() Экспорт 	
	Путь = "";	
	
	Файл = Новый Файл("D:\");
	Если Файл.Существует() Тогда
		Путь = "D:\1CArchive";
	Иначе
		Путь = "C:\1CArchive";
	КонецЕсли;  	
	Путь = Путь;	
	Возврат Путь;
КонецФункции // ПолучитьСтандартныйКаталогДляАрхиваИБ()

// возвращает строку содержащую путь для выгрузки конфигурации
Функция ПолучитьПолныйПутьДляВыгрузкиКонфигурации(Знач Путь) Экспорт
	
	// Сначала выясним, задал ли пользователь имя файла.
	// Для этого получим последнее имя    	
	ДлинаПути = СтрДлина(Путь);
	Сч = ДлинаПути;     	
	Пока Сред(Путь, Сч, 1)<>"\" Цикл
		Сч = Сч-1;
		Если Сч=0 Тогда
			Прервать;
		КонецЕсли;			
	КонецЦикла;
	
	ПоследнееИмя = Сред(Путь, Сч+1);
	НомерТочки   = Найти(ПоследнееИмя, ".");
	ПутьБезПоследнегоИмени = Сред(Путь, 1, Сч-1); 
	
	Если НомерТочки=0 Тогда // Значит файл не указан. Что ж, не будем расстраиваться и сами его добавим.
		СоздатьКаталог(Путь);
		ТекДата = ТекущаяДата();
		Путь    = Путь + "\" + Формат(Год(ТекДата), "ЧГ=0") + "_" + Формат(Месяц(ТекДата), "ЧЦ=2; ЧВН=") + "_" + Формат(День(ТекДата), "ЧЦ=2; ЧВН=");
		Путь    = Путь + " (" + СтрЗаменить(Метаданные.Версия, ".", "_") + ")";	
		Путь    = Путь + ".cf";		
		Путь    = СтрЗаменить(Путь, "\\", "\");		
	Иначе
		СоздатьКаталог(ПутьБезПоследнегоИмени);	
	КонецЕсли;
			
	Возврат Путь;
КонецФункции // ПолучитьПолныйПутьДляВыгрузкиКонфигурации()




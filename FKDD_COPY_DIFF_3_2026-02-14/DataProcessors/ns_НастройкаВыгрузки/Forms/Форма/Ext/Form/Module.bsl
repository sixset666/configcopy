&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтрУникальныйИдентификатор = ns_ОбщегоНазначенияПовтИсп.ПолучитьНастройку(Перечисления.ns_НастройкиАвито.ИдентификаторРегламентногоЗадания);
	Если НЕ СтрУникальныйИдентификатор = Неопределено Тогда
		ИдентификаторРегламентногоЗадания = Новый УникальныйИдентификатор(СтрУникальныйИдентификатор);
	Иначе
		ИдентификаторРегламентногоЗадания = Неопределено;
	КонецЕсли; 
	
	НайденноеРегламентноеЗадание = Неопределено;
	Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗадания) Тогда			
		НайденноеРегламентноеЗадание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗадания);
	КонецЕсли;			
	
	Если НайденноеРегламентноеЗадание = Неопределено Тогда
		ИдентификаторРегламентногоЗадания = Неопределено;
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	Иначе
		РасписаниеРегламентногоЗадания = НайденноеРегламентноеЗадание.Расписание;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьНадписьРасписанияОбмена();
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОбмен(Команда)
	Если РасписаниеРегламентногоЗадания = НеОпределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьИзменятьРасписание", ЭтотОбъект);
	
	Диалог.Показать(ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменятьРасписание(РасписаниеЗадания, ДополнительныеПараметры) Экспорт
	
	Если РасписаниеЗадания = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасписаниеРегламентногоЗадания = РасписаниеЗадания;
	
	УстановитьНадписьРасписанияОбмена();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбмена()
	
	Если РасписаниеРегламентногоЗадания = НеОпределено Тогда
		ТекстЗаголовка = НСтр("ru='Настроить обмен'");
	Иначе
		ТекстЗаголовка = РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Элементы.НастроитьОбмен.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	СохранитьНастройкиНаСервере();
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	УстановитьПривилегированныйРежим(Истина); //Для возможности всем пользователям с правами устанавливать расписания		
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеОтчетыИОбработки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДополнительныеОтчетыИОбработки КАК ДополнительныеОтчетыИОбработки
	|ГДЕ
	|	ДополнительныеОтчетыИОбработки.ИмяОбъекта = &ИмяОбъекта";
	Запрос.УстановитьПараметр("ИмяОбъекта", "ns_ВыгрузкаНаАвито");
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
		
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Выборка.Ссылка);
	ПараметрыЗапуска.Добавить("ВыгрузкаНаАвитоРегламент");
	
	Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗадания);
	Если Задание <> Неопределено Тогда
		
		ПараметрыИзмененияЗадания = Новый Структура();
		ПараметрыИзмененияЗадания.Вставить("Использование", Истина);
		ПараметрыИзмененияЗадания.Вставить("Параметры", ПараметрыЗапуска);
		ПараметрыИзмененияЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);
		ПараметрыИзмененияЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
		ПараметрыИзмененияЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
		
		РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторРегламентногоЗадания, ПараметрыИзмененияЗадания);
		
	Иначе
		
		ПараметрыЗадания = Новый Структура();
		ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ЗапускДополнительныхОбработок);
		ПараметрыЗадания.Вставить("Наименование", "Выгрузка на Авито");
		ПараметрыЗадания.Вставить("Ключ", "ВыгрузкаНаАвито");
		ПараметрыЗадания.Вставить("Использование", Истина);
		ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Ложь);
		ПараметрыЗадания.Вставить("Параметры", ПараметрыЗапуска);
		ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
		ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
		ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);
		
		Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			ИдентификаторРегламентногоЗадания = Задание.Идентификатор.УникальныйИдентификатор();
		Иначе
			ИдентификаторРегламентногоЗадания = Задание.УникальныйИдентификатор;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Задание = Неопределено Тогда
		ИдентификаторРегламентногоЗадания = Неопределено;
	КонецЕсли;
	
	//РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторРегламентногоЗадания);
	//ИдентификаторРегламентногоЗадания = Неопределено;
	
	МенеджерЗаписи = РегистрыСведений.ns_НастройкиАвито.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Настройка = Перечисления.ns_НастройкиАвито.ИдентификаторРегламентногоЗадания;
	МенеджерЗаписи.Значение = XMLСтрока(ИдентификаторРегламентногоЗадания);
	МенеджерЗаписи.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);	
	
	//ТекущийОбъект.ИдентификаторРегламентногоЗадания = ИдентификаторРегламентногоЗадания;				
	//ТекущийОбъект.Записать();	
	
КонецПроцедуры


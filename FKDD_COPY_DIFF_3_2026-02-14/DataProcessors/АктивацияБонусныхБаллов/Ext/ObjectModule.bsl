
Процедура ЗаполнитьКонтактнуюИнфрмациюРассылок()
	
	// получим КИ для рассылки
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Объект КАК Контрагент,
	|	КонтактнаяИнформация.Вид,
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект В(&Контрагенты)
	|	И КонтактнаяИнформация.Вид В(&ВидыКИ)
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтактнаяИнформация.Вид,
	|	КонтактнаяИнформация.Объект,
	|	КонтактнаяИнформация.Представление";
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	
	Запрос.УстановитьПараметр("Контрагенты", ТаблицаРассылок.ВыгрузитьКолонку("Контрагент"));
	Запрос.УстановитьПараметр("ВидыКИ", ВидыКИ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Отбор = Новый Структура("Контрагент");
	Пока Выборка.Следующий() Цикл
		Отбор.Контрагент = Выборка.Контрагент;
		ИмяКи = ?(Выборка.Вид = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний, "Адрес", "Телефон");
		НайденныеСтроки = ТаблицаРассылок.НайтиСтроки(Отбор);
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка[ИмяКИ] = Выборка.Представление;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьДокументыКорректировкиБонусов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БонусныеБаллыОстатки.БонуснаяКарта,
	|	БонусныеБаллыОстатки.ДатаСписанияБонусов,
	|	БонусныеБаллыОстатки.БонуснаяКарта.БонуснаяПрограмма КАК БонуснаяПрограмма,
	|	БонусныеБаллыОстатки.КоличествоКНачислениюОстаток,
	|	БонусныеБаллыОстатки.БонуснаяКарта.Объект КАК Контрагент
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы.Остатки(, ДатаСписанияБонусов < &ТекДата) КАК БонусныеБаллыОстатки";
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		// создадим документ
		НовыйДокумент = Документы.КорректировкаБонусов.СоздатьДокумент();
		НовыйДокумент.Заполнить(Неопределено);
		НовыйДокумент.УстановитьНовыйНомер();
		НовыйДокумент.Комментарий = "Создан регламентным заданием ""Активация бонусных баллов""";
		
		#Если Клиент Тогда
		Сообщить("-------------------------------------------------------------");
		Сообщить("Активация накопленных балллов.");
		
		Сч = 0;
		#КонецЕсли
		
		Пока Выборка.Следующий() Цикл
			
			#Если Клиент Тогда
			Сч = Сч + 1;
			Состояние(Строка(Окр(Сч/Выборка.Количество()*100,0)) + "%");
			#КонецЕсли
			
			НоваяСтрока = НовыйДокумент.БонусыКНачислению.Добавить();
			НоваяСтрока.Карточка              = Выборка.БонуснаяКарта;
			НоваяСтрока.ДатаСписания          = ?(Выборка.БонуснаяПрограмма.СрокСгоранияБонусов <> 0, НачалоДня(ТекущаяДата() + 24*60*60*Выборка.БонуснаяПрограмма.СрокСгоранияБонусов), Дата("39990101"));
			НоваяСтрока.Количество            = Выборка.КоличествоКНачислениюОстаток;
			НоваяСтрока.КоличествоКНачислению = 0;
			
			НовыйДокумент.БонусыКНачислению.Свернуть("Карточка,ДатаСписания", "Количество,КоличествоКНачислению");
			
			НоваяСтрокаСписания = НовыйДокумент.БонусыКСписанию.Добавить();
			НоваяСтрокаСписания.Карточка              = Выборка.БонуснаяКарта;
			НоваяСтрокаСписания.ДатаСписания          = Выборка.ДатаСписанияБонусов;
			НоваяСтрокаСписания.Количество            = 0;
			НоваяСтрокаСписания.КоличествоКНачислению = Выборка.КоличествоКНачислениюОстаток;
			
			// добавим строку в рассылку
			ИспользоватьSMSРассылку   = Выборка.БонуснаяПрограмма.ИспользоватьSMSРассылку И ЗначениеЗаполнено(ШаблонРассылкиSMS);
			ИспользоватьEMailРассылку = Выборка.БонуснаяПрограмма.ИспользоватьEMailРассылку И ЗначениеЗаполнено(ШаблонРассылкиEMail);
			
			Если ИспользоватьSMSРассылку ИЛИ ИспользоватьEMailРассылку Тогда
				Рассылка = ТаблицаРассылок.Добавить();
				Рассылка.Контрагент        = Выборка.Контрагент;
				Рассылка.БонуснаяПрограмма = Выборка.БонуснаяПрограмма;
				Рассылка.БонуснаяКарта     = Выборка.БонуснаяКарта;
				Рассылка.ДатаСписания      = Выборка.ДатаСписанияБонусов;
				Рассылка.Количество        = Выборка.КоличествоКНачислениюОстаток;
				Рассылка.SMS               = ИспользоватьSMSРассылку;
				Рассылка.Email             = ИспользоватьEMailРассылку;
			КонецЕсли;
			
		КонецЦикла;
		
		Отказ = Ложь;
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
			
			#Если Клиент Тогда
			Сообщить(НСтр("ru = 'Сформирован документ '") + Строка(НовыйДокумент.Ссылка) + ".");
			#КонецЕсли
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке(); Отказ = Истина;
			
			#Если Клиент Тогда
			Сообщить(ИнформацияОбОшибке.Описание +"|"+ ИнформацияОбОшибке.Причина);
			#КонецЕсли
		КонецПопытки;
		
		#Если Клиент Тогда
		Сообщить("-------------------------------------------------------------");
		#КонецЕсли
		
		Если Отказ Тогда
			ТаблицаРассылок.Очистить();
		КонецЕсли;
		
		Возврат НовыйДокумент.Ссылка;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ВыполнитьРассылкуУведомлений(ДокументОснование) Экспорт
	
	//создаем документ
	ДокументSMS = Неопределено; ДокументыКОтправке = Новый Массив; АдресатыSMS = "";
	
	ЗаполнитьКонтактнуюИнфрмациюРассылок();
	
	Если ТаблицаРассылок.Количество() > 0 Тогда
		#Если Клиент Тогда
			Сообщить(НСтр("ru = 'Формирование документов рассылки:'"));
		#КонецЕсли
		
		Для Каждого Рассылка Из ТаблицаРассылок Цикл
			// SMS
			Если Рассылка.SMS И НЕ ПустаяСтрока(Рассылка.Телефон)
				 И ЗначениеЗаполнено(ШаблонРассылкиSMS)
				 И Рассылка.Контрагент.СогласиеНаПолучениеSMS Тогда
				Если ДокументSMS = Неопределено Тогда
					ДокументSMS = Документы.SMS.СоздатьДокумент();
					ДокументSMS.Заполнить(Неопределено);
					ДокументSMS.УстановитьНовыйНомер();
					
					ДокументSMS.НомерОтправителя = Константы.смсИмяПользователяПоУмолчанию.Получить();
					ДокументSMS.НачалоОтправки   = ТекущаяДата();
					
					ДокументSMS.ТекстСообщения = ШаблонРассылкиSMS;
					
					ДокументSMS.ДокументОснование  = ДокументОснование;
					ДокументSMS.Входящее_Исходящее = Перечисления.ТипыСообщений.Исходящее;
					ДокументSMS.Комментарий        = "Создан регламентным заданием ""Активация бонусных баллов""";
				КонецЕсли;
				
				Получатель = ДокументSMS.Получатели.Добавить();
				Получатель.ВладелецКИ    = Рассылка.Контрагент;
				Получатель.НомерТелефона = Рассылка.Телефон;
				Получатель.Контрагент    = Рассылка.Контрагент;
				
				АдресатыSMS = АдресатыSMS + ?(ПустаяСтрока(АдресатыSMS), "", ", ") + спПолучитьНаименование(Получатель.Контрагент);
				
				Получатель.ТекстСообщения = Справочники.ШаблоныТекстовыхСообщений.ЗаполнитьШаблонПоОбъекту(ШаблонРассылкиSMS, Рассылка);
			КонецЕсли;
			
			//Email
			Если Рассылка.Email И НЕ ПустаяСтрока(Рассылка.Адрес) И ЗначениеЗаполнено(УчетнаяЗаписьЭлектроннойПочты) И ЗначениеЗаполнено(ШаблонРассылкиEMail) Тогда
				НовоеЭлектронноеПисьмо = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
				НовоеЭлектронноеПисьмо.Заполнить(Неопределено);
				НовоеЭлектронноеПисьмо.УстановитьНовыйНомер();
				НовоеЭлектронноеПисьмо.ХозОперация              = Справочники.ХозОперации.ЭлектронноеПисьмо;
				НовоеЭлектронноеПисьмо.ОбменДанными.Загрузка    = Истина;
				НовоеЭлектронноеПисьмо.УчетнаяЗапись            = УчетнаяЗаписьЭлектроннойПочты;
				НовоеЭлектронноеПисьмо.ОтправительПредставление = УчетнаяЗаписьЭлектроннойПочты.АдресЭлектроннойПочты;
				НовоеЭлектронноеПисьмо.ГруппаПисем              = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие");
				НовоеЭлектронноеПисьмо.ФорматТекста             = Перечисления.ФорматТекста.ПростойТекст;
				НовоеЭлектронноеПисьмо.ДокументОснование        = ДокументОснование;
				НовоеЭлектронноеПисьмо.Комментарий              = "Создан регламентным заданием ""Активация бонусных баллов""";
				
				Получатель = НовоеЭлектронноеПисьмо.Получатели.Добавить();
				Получатель.АдресЭлектроннойПочты = Рассылка.Адрес;
				Получатель.СсылкаНаОбъект        = Рассылка.Контрагент;
				Получатель.Представление         = Рассылка.Адрес;
				Получатель.КодГруппыАдреса       = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
				
				НовоеЭлектронноеПисьмо.Тема        = "Оповещение о бонусах";
				НовоеЭлектронноеПисьмо.ТекстПисьма = Справочники.ШаблоныТекстовыхСообщений.ЗаполнитьШаблонПоОбъекту(ШаблонРассылкиEMail, Рассылка);
				
				НовоеЭлектронноеПисьмо.Записать();
				
				ДокументыКОтправке.Добавить(НовоеЭлектронноеПисьмо);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ДокументSMS <> Неопределено Тогда
		Попытка
			ДокументSMS.Записать(РежимЗаписиДокумента.Запись);
			#Если Клиент Тогда
				Сообщить(НСтр("ru = 'Сформирован документ sms для адресатов: '") + АдресатыSMS);
			#КонецЕсли
			ДокументSMS.ОтправитьSMS();
		Исключение
			ИнформацияООшибке = ИнформацияОбОшибке();
		КонецПопытки;
	КонецЕсли;
	
	Если ДокументыКОтправке.Количество() > 0 И ЗначениеЗаполнено(УчетнаяЗаписьЭлектроннойПочты) Тогда
		Попытка
			СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Ложь, Истина);
			СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(УчетнаяЗаписьЭлектроннойПочты, СтруктураРежимовОбмена);
			Если СоединениеСПочтовымСервером <> Неопределено Тогда
				Для Каждого ДокументEMail Из ДокументыКОтправке Цикл
					эпОтправитьПисьмо(ДокументEMail, СоединениеСПочтовымСервером);
					#Если Клиент Тогда
						Сообщить(НСтр("ru = 'Отправлено электронное письмо '") + Строка(ДокументEMail.Ссылка));
					#КонецЕсли
				КонецЦикла;
				эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры
// Модуль обработки АРМЗаписьНаТестДрайв

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем Права Экспорт; // права пользователя

Перем ДетальнаяТаблицаЗагрузкиРабочихМест Экспорт; // детальная таблица рабочих мест
Перем ДетальнаяТаблицаЗагрузкиМоделей Экспорт; // детальная таблица в разрезе моделей
Перем ТаблицаРабочихМест Экспорт; // таблица автомобилей(сотрудников) для вывода в планировании
Перем ТаблицаРабочихИнтервалов Экспорт; // таблица рабочего времени с признаком является интервал рабочим или нет
Перем ТаблицаГраницГрафика Экспорт; //содержат дату и время начала и конца каждого дня на календаре 
Перем ТаблицаДокументов Экспорт; // таблица документов
Перем ТаблицаОформления Экспорт; // таблица оформления
Перем ТаблицаРасшифровок Экспорт; // таблица расшифровок
Перем ТаблицаСмещений Экспорт; // таблица смещений
Перем ТаблицаБазовогоГрафика Экспорт; // таблица базового графика
Перем МВТАвтомобили Экспорт; // менеджер временных таблиц
Перем МВТСотрудники Экспорт; // менеджер временных таблиц
Перем ПредставленияАвтомобилей; //соответствие - наименование автомобиля по ссылке

Перем ЦветаРаскраски Экспорт; //цвета раскраски ячеек календаря
Перем ОтступСлева Экспорт; // отступ слева
Перем ОтступСверху Экспорт; // отступ сверху
Перем ЛинияРамкиВыделенияДокумента Экспорт; // линия рамки выделения
Перем ЛинияРамкиВыделенияРаботы Экспорт; // линия рамки выделения
Перем ЛинияРамкиВнутренняя Экспорт; // линия рамки внутренняя
Перем ЛинияРамкиСплошная Экспорт; // линия рамки сплошная
Перем ЛинияРамкиТочечная Экспорт; // линия рамки сплошная

Перем РежимКалендаря Экспорт; //режим календаря (по автомобилям/по сотрудникам)
Перем ДатаОтчета Экспорт; //Дата, выбранная на календаре
Перем ДлительностьИнтервала Экспорт; // длительность интервала
Перем ЧислоРабочихМест Экспорт; // число рабочих мест
Перем ДатаНачалоПоГрафику Экспорт; // дата начало дня
Перем ДатаКонецПоГрафику Экспорт; // дата конец дня

////////////////////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Формирует соответствие "ПредставленияАвтомобилей"
Процедура СформироватьПредставления(ТаблицаАвто)
	ПредставленияАвтомобилей.Очистить();
	Для Каждого ТекСтрока ИЗ ТаблицаАвто Цикл
		Если ТипЗнч(ТекСтрока.Ссылка)=ТипЗнч(Справочники.Автомобили.ПустаяСсылка()) Тогда
			Представление = Строка(ТекСтрока.Модель) + Символы.Пс + Строка(ТекСтрока.ГосНомер);
		Иначе
			Представление = Строка(ТекСтрока.Ссылка);
		КонецЕсли;
		ПредставленияАвтомобилей.Вставить(ТекСтрока.Ссылка,Представление);
	КонецЦикла
КонецПроцедуры

// Возвращает представление объекта по ссылке
Функция ПолучитьПредставлениеРабочегоМеста(Ссылка) Экспорт
	//отображение а/м в виде: FORD FOCUS АК 4444 АС
	//Если ТипЗнч(Ссылка)=ТипЗнч(Справочники.Автомобили.ПустаяСсылка()) Тогда
	//	НайденноеЗначение= ПредставленияАвтомобилей.Получить(Ссылка);
	//	Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
	//		Возврат НайденноеЗначение;
	//	Иначе
	//		ГосНомер = Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(Ссылка, Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер);
	//		Представление = Строка(Ссылка.Модель) + Символы.Пс + Строка(ГосНомер);
	//		ПредставленияАвтомобилей.Вставить(Ссылка,Представление);
	//		Возврат Представление;
	//	КонецЕсли;
	//Иначе
	//	Возврат Строка(Ссылка);
	//КонецЕсли;
	
	Возврат Строка(Ссылка);	
КонецФункции

//Заполнить настройки графиков работ
//
// Параметры:
// РежимКалендаря - Число - Текущий режим просмотра календаря (1 - по автомобилям, 2 - по сотрудникам, 3 - по моделям)
//
Процедура ЗаполнитьНастройкиГрафиковРабот() Экспорт
	Если РежимКалендаря = 1 Тогда
		ЗаполнитьНастройкиГрафиковРаботДляАвтомобилей(); 
	ИначеЕсли РежимКалендаря = 2 Тогда
		ЗаполнитьНастройкиГрафиковРаботДляСотрудников(); 
	ИначеЕсли РежимКалендаря = 3 Тогда
		ЗаполнитьНастройкиГрафиковРаботДляАвтомобилей(); 
	КонецЕсли
КонецПроцедуры

//Заполнить настройки графиков работ для автомобилей
//
Процедура ЗаполнитьНастройкиГрафиковРаботДляАвтомобилей() Экспорт
	
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если ЗначениеЗаполнено(БазовыйГрафик) Тогда
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
	КонецЕсли;
	
	ТаблицаГраницГрафика = Новый ТаблицаЗначений;
	ТаблицаГраницГрафика.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаГраницГрафика.Колонки.Добавить("НачалоДня", Новый ОписаниеТипов("Дата"));
	ТаблицаГраницГрафика.Колонки.Добавить("КонецДня", Новый ОписаниеТипов("Дата"));
	
	ТаблицаРабочихМест = Новый ТаблицаЗначений;
	
	МВТАвтомобили = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТАвтомобили;
	Запрос.Текст = "ВЫБРАТЬ
	               |	АвтомобилиДляТестДрайваОстатки.Автомобиль КАК Ссылка,
	               |	АвтомобилиДляТестДрайваОстатки.Автомобиль.ПометкаУдаления КАК ПометкаУдаления,
	               |	АвтомобилиДляТестДрайваОстатки.Модель,
	               |	ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)) КАК ГрафикРаботы,
	               |	ЕСТЬNULL(АвтомобилиСрезПоследних.Значение, """") КАК ГосНомер
	               |ИЗ
	               |	РегистрНакопления.АвтомобилиДляТестДрайва.Остатки(
	               |			&Дата,
	               |			&ОтборПоПодразделению
	               |				И Автомобиль.ПометкаУдаления = ЛОЖЬ) КАК АвтомобилиДляТестДрайваОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ЗначенияСвойствОбъектов.Объект КАК ОбъектНазначения,
	               |			ЗначенияСвойствОбъектов.Свойство КАК Свойство,
	               |			ЗначенияСвойствОбъектов.Значение КАК Значение
	               |		ИЗ
	               |			РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	               |		ГДЕ
	               |			ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.ГрафикРаботы)) КАК ЗначенияСвойствОбъектов
	               |		ПО АвтомобилиДляТестДрайваОстатки.Автомобиль = ЗначенияСвойствОбъектов.ОбъектНазначения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК АвтомобилиСрезПоследних
	               |		ПО АвтомобилиДляТестДрайваОстатки.Автомобиль = АвтомобилиСрезПоследних.Автомобиль";
	Запрос.УстановитьПараметр("Дата",ДатаОтчета);			   
	
	Если ЗначениеЗаполнено(ОтборПоПодразделению) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ОтборПоПодразделению","ПодразделениеКомпании = &ОтборПоПодразделению");
		Запрос.УстановитьПараметр("ОтборПоПодразделению",ОтборПоПодразделению);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ОтборПоПодразделению","Истина");
	КонецЕсли;   
	
	ТаблицаРабочихМест = Запрос.Выполнить().Выгрузить();
	СформироватьПредставления(ТаблицаРабочихМест);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаРабочихМест.Ссылка КАК Ссылка,
	               |	ТаблицаРабочихМест.Модель КАК Модель,
	               |	ТаблицаРабочихМест.ПометкаУдаления КАК ПометкаУдаления,
	               |	ТаблицаРабочихМест.ГрафикРаботы КАК ГрафикРаботы
	               |ПОМЕСТИТЬ Автомобили
	               |ИЗ
	               |	&ТаблицаРабочихМест КАК ТаблицаРабочихМест
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Автомобили.Ссылка КАК Ссылка,
	               |	Автомобили.ГрафикРаботы КАК ГрафикРаботы
	               |ИЗ
	               |	Автомобили КАК Автомобили";
	Запрос.УстановитьПараметр("ТаблицаРабочихМест",ТаблицаРабочихМест);
	ГрафикиРабочихМест = Запрос.Выполнить().Выгрузить();
	
	Для СчетчикДней = 0 По Продолжительность-1 Цикл	
		СтрокаТаблицыГраниц = ТаблицаГраницГрафика.Добавить();
		СтрокаТаблицыГраниц.Дата = ДатаОтчета+24*60*60*(СчетчикДней);
		СтрокаТаблицыГраниц.НачалоДня = '00010101';
		СтрокаТаблицыГраниц.КонецДня = '00010101';
		
		Если ИспользованиеГрафиковРабот = 2 Тогда
				
			//сформирует список графиков на текущую дату и определим по списку границы
			СписокТаблицГрафиков = Новый Соответствие;
			Для Каждого СтрокаТаблицыГрафиков Из ГрафикиРабочихМест Цикл
				Если НЕ обЗначениеНеЗаполнено(СтрокаТаблицыГрафиков.ГрафикРаботы) Тогда
					ДанныеТаблицыГрафика = Новый ТаблицаЗначений;
					ДанныеТаблицыГрафика = кпПолучитьГрафик(СтрокаТаблицыГрафиков.ГрафикРаботы, НачалоДня(ДатаОтчета+60*60*24*(СчетчикДней)), КонецДня(ДатаОтчета+60*60*24*(СчетчикДней)));
					Если ДанныеТаблицыГрафика.Количество() <> 0 Тогда
						СписокТаблицГрафиков.Вставить(СтрокаТаблицыГрафиков.Ссылка,ДанныеТаблицыГрафика);	
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Для Каждого ЭлементСписка Из СписокТаблицГрафиков Цикл
				КопияТаблицаГрафика = ЭлементСписка.Значение.Скопировать();
				
				//исключим из рассмотрения строки, где время не заполнено,
				//для этого время начала установим равным концу дня
				Для Каждого СтрокаГрафика Из КопияТаблицаГрафика Цикл
					Если СтрокаГрафика.НачалоРабочегоВремени = '00010101' И  СтрокаГрафика.КонецРабочегоВремени = '00010101' Тогда
						СтрокаГрафика.НачалоРабочегоВремени =  КонецДня(СтрокаГрафика.НачалоРабочегоВремени);
					КонецЕсли;
				КонецЦикла;
				
				КопияТаблицаГрафика.Сортировать("Дата ВОЗР, НачалоРабочегоВремени ВОЗР");
				ГрафикДатаНачалоПоГрафику = КопияТаблицаГрафика[0].Дата + Час(КопияТаблицаГрафика[0].НачалоРабочегоВремени)*60*60 + Минута(КопияТаблицаГрафика[0].НачалоРабочегоВремени)*60 + Секунда(КопияТаблицаГрафика[0].НачалоРабочегоВремени);
				Если СтрокаТаблицыГраниц.НачалоДня = '00010101' ИЛИ ГрафикДатаНачалоПоГрафику < СтрокаТаблицыГраниц.НачалоДня Тогда
					СтрокаТаблицыГраниц.НачалоДня = ГрафикДатаНачалоПоГрафику;	
				КонецЕсли;
				КопияТаблицаГрафика.Сортировать("Дата УБЫВ,КонецРабочегоВремени УБЫВ");
				ГрафикДатаКонецПоГрафику = КопияТаблицаГрафика[0].Дата + Час(КопияТаблицаГрафика[0].КонецРабочегоВремени)*60*60 + Минута(КопияТаблицаГрафика[0].КонецРабочегоВремени)*60 + Секунда(КопияТаблицаГрафика[0].КонецРабочегоВремени);
				Если СтрокаТаблицыГраниц.КонецДня = '00010101' ИЛИ ГрафикДатаКонецПоГрафику > СтрокаТаблицыГраниц.КонецДня Тогда
					СтрокаТаблицыГраниц.КонецДня = ГрафикДатаКонецПоГрафику;	
				КонецЕсли;
			КонецЦикла;
		
		ИначеЕсли ИспользованиеГрафиковРабот = 1 ИЛИ ИспользованиеГрафиковРабот = 3 Тогда
			Если ИспользованиеГрафиковРабот = 3 Тогда
				Если ЗначениеЗаполнено(ОтборПоПодразделению) Тогда
					ИспользуемыйГрафик = ОтборПоПодразделению.ГрафикРаботы;
				КонецЕсли;
			Иначе
				ИспользуемыйГрафик = БазовыйГрафик;
			КонецЕсли;
			ДанныеТаблицыГрафика = кпПолучитьГрафик(ИспользуемыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
			Если ДанныеТаблицыГрафика.Количество() > 0 Тогда
				КопияТаблицаБазовогоГрафика = ДанныеТаблицыГрафика.Скопировать();
				
				//исключим из рассмотрения строки, где время не заполнено,
				//для этого время начала установим равным концу дня
				Для Каждого СтрокаГрафика Из КопияТаблицаБазовогоГрафика Цикл
					Если СтрокаГрафика.НачалоРабочегоВремени = '00010101' И  СтрокаГрафика.КонецРабочегоВремени = '00010101' Тогда
						СтрокаГрафика.НачалоРабочегоВремени =  КонецДня(СтрокаГрафика.НачалоРабочегоВремени);
					КонецЕсли;
				КонецЦикла;
				
				КопияТаблицаБазовогоГрафика.Сортировать("Дата ВОЗР,НачалоРабочегоВремени ВОЗР");
				СтрокаТаблицыГраниц.НачалоДня = КопияТаблицаБазовогоГрафика[0].Дата + Час(КопияТаблицаБазовогоГрафика[0].НачалоРабочегоВремени)*60*60 + Минута(КопияТаблицаБазовогоГрафика[0].НачалоРабочегоВремени)*60 + Секунда(КопияТаблицаБазовогоГрафика[0].НачалоРабочегоВремени);
				КопияТаблицаБазовогоГрафика.Сортировать("Дата УБЫВ,КонецРабочегоВремени УБЫВ");
				СтрокаТаблицыГраниц.КонецДня = КопияТаблицаБазовогоГрафика[0].Дата + Час(КопияТаблицаБазовогоГрафика[0].КонецРабочегоВремени)*60*60 + Минута(КопияТаблицаБазовогоГрафика[0].КонецРабочегоВремени)*60 + Секунда(КопияТаблицаБазовогоГрафика[0].КонецРабочегоВремени);
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТаблицыГраниц.НачалоДня = '00010101' Тогда
			СтрокаТаблицыГраниц.НачалоДня = НачалоДня(ДатаОтчета+24*60*60*СчетчикДней);
		КонецЕсли;
		Если СтрокаТаблицыГраниц.КонецДня = '00010101' Тогда
			СтрокаТаблицыГраниц.КонецДня = КонецДня(ДатаОтчета+24*60*60*СчетчикДней);
		КонецЕсли;	
		
	КонецЦикла;
	
	//заново заполним временную таблицу с учетом изменений
	Запрос.Текст = "
	| УНИЧТОЖИТЬ Автомобили;
	|ВЫБРАТЬ 
	|	ТаблицаРабочихМест.Ссылка КАК Ссылка,
	|	ТаблицаРабочихМест.Модель КАК Модель,
	|	ТаблицаРабочихМест.ПометкаУдаления КАК ПометкаУдаления,
	|	ТаблицаРабочихМест.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ Автомобили
	|ИЗ &ТаблицаРабочихМест КАК ТаблицаРабочихМест";
	Запрос.УстановитьПараметр("ТаблицаРабочихМест",ТаблицаРабочихМест);
	Запрос.Выполнить();

КонецПроцедуры

//Заполнить настройки графиков работ для сотрудников
Процедура ЗаполнитьНастройкиГрафиковРаботДляСотрудников() Экспорт
	
	ТаблицаБазовогоГрафика = Новый ТаблицаЗначений;
	Если НЕ обЗначениеНеЗаполнено(БазовыйГрафик) Тогда
		ТаблицаБазовогоГрафика = кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
	КонецЕсли;
	
	ТаблицаГраницГрафика = Новый ТаблицаЗначений;
	ТаблицаГраницГрафика.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаГраницГрафика.Колонки.Добавить("НачалоДня", Новый ОписаниеТипов("Дата"));
	ТаблицаГраницГрафика.Колонки.Добавить("КонецДня", Новый ОписаниеТипов("Дата"));
	
	ТаблицаРабочихМест = Новый ТаблицаЗначений;
	
	// Получим список рабочих мест
	МВТСотрудники = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТСотрудники;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Сотрудники.Ссылка,
	               |	Сотрудники.ПометкаУдаления,
	               |	Сотрудники.ГрафикРаботы
	               |ИЗ
	               |	Справочник.Сотрудники КАК Сотрудники
	               |ГДЕ
	               |	Сотрудники.ЭтоГруппа = ЛОЖЬ
	               |	И Сотрудники.ПометкаУдаления = ЛОЖЬ
	               |	И Сотрудники.ФлагУволен = ЛОЖЬ";
   	Если ЗначениеЗаполнено(ОтборПоПодразделению) Тогда
		Запрос.Текст = Запрос.Текст + "   
		   		|	И Сотрудники.Подразделение = &ОтборПоПодразделению";
		Запрос.УстановитьПараметр("ОтборПоПодразделению",ОтборПоПодразделению);
	КонецЕсли;   
					  
   // пока не ясно, как определить сотрудников, на которых планируются тест-драйвы
   //|ГДЕ
   //|	Сотрудники.Должность = ЗНАЧЕНИЕ(Справочник.Должности.Мастер)
   //|	И Сотрудники.Исполнитель = ИСТИНА";
   
	ТаблицаРабочихМест = Запрос.Выполнить().Выгрузить();
	
	// Используем таблицу для получения различных мастеров
	Запрос.Текст = "
	|ВЫБРАТЬ 
    |	ТаблицаРабочихМест.Ссылка,
    |	ТаблицаРабочихМест.ПометкаУдаления,
    |	ТаблицаРабочихМест.ГрафикРаботы
	|ПОМЕСТИТЬ Сотрудники
	|ИЗ &ТаблицаРабочихМест КАК ТаблицаРабочихМест;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|Сотрудники.Ссылка КАК Ссылка,
	|Сотрудники.ГрафикРаботы КАК ГрафикРаботы
	|ИЗ Сотрудники КАК Сотрудники
	|";
	Запрос.УстановитьПараметр("ТаблицаРабочихМест",ТаблицаРабочихМест);
	ГрафикиРабочихМест = Запрос.Выполнить().Выгрузить();
	
	Для СчетчикДней = 0 По Продолжительность-1 Цикл	
		СтрокаТаблицыГраниц = ТаблицаГраницГрафика.Добавить();
		СтрокаТаблицыГраниц.Дата = ДатаОтчета+24*60*60*(СчетчикДней);
		СтрокаТаблицыГраниц.НачалоДня = '00010101';
		СтрокаТаблицыГраниц.КонецДня = '00010101';
		
		Если ИспользованиеГрафиковРабот = 2 Тогда
			//сформирует список графиков на текущую дату и определим по списку границы
			СписокТаблицГрафиков = Новый Соответствие;
			Для Каждого СтрокаТаблицыГрафиков Из ГрафикиРабочихМест Цикл
				Если НЕ обЗначениеНеЗаполнено(СтрокаТаблицыГрафиков.ГрафикРаботы) Тогда
					ДанныеТаблицыГрафика = Новый ТаблицаЗначений;
					ДанныеТаблицыГрафика = кпПолучитьГрафик(СтрокаТаблицыГрафиков.ГрафикРаботы, НачалоДня(ДатаОтчета+60*60*24*(СчетчикДней)), КонецДня(ДатаОтчета+60*60*24*(СчетчикДней)));
					Если ДанныеТаблицыГрафика.Количество() <> 0 Тогда
						СписокТаблицГрафиков.Вставить(СтрокаТаблицыГрафиков.Ссылка,ДанныеТаблицыГрафика);	
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Для Каждого ЭлементСписка Из СписокТаблицГрафиков Цикл
				КопияТаблицаГрафика = ЭлементСписка.Значение.Скопировать();
				
				//исключим из рассмотрения строки, где время не заполнено,
				//для этого время начала установим равным концу дня
				Для Каждого СтрокаГрафика Из КопияТаблицаГрафика Цикл
					Если СтрокаГрафика.НачалоРабочегоВремени = '00010101' И  СтрокаГрафика.КонецРабочегоВремени = '00010101' Тогда
						СтрокаГрафика.НачалоРабочегоВремени =  КонецДня(СтрокаГрафика.НачалоРабочегоВремени);
					КонецЕсли;
				КонецЦикла;
				
				КопияТаблицаГрафика.Сортировать("Дата ВОЗР, НачалоРабочегоВремени ВОЗР");
				ГрафикДатаНачалоПоГрафику = КопияТаблицаГрафика[0].Дата + Час(КопияТаблицаГрафика[0].НачалоРабочегоВремени)*60*60 + Минута(КопияТаблицаГрафика[0].НачалоРабочегоВремени)*60 + Секунда(КопияТаблицаГрафика[0].НачалоРабочегоВремени);
				Если СтрокаТаблицыГраниц.НачалоДня = '00010101' ИЛИ ГрафикДатаНачалоПоГрафику < СтрокаТаблицыГраниц.НачалоДня Тогда
					СтрокаТаблицыГраниц.НачалоДня = ГрафикДатаНачалоПоГрафику;	
				КонецЕсли;
				КопияТаблицаГрафика.Сортировать("Дата УБЫВ,КонецРабочегоВремени УБЫВ");
				ГрафикДатаКонецПоГрафику = КопияТаблицаГрафика[0].Дата + Час(КопияТаблицаГрафика[0].КонецРабочегоВремени)*60*60 + Минута(КопияТаблицаГрафика[0].КонецРабочегоВремени)*60 + Секунда(КопияТаблицаГрафика[0].КонецРабочегоВремени);
				Если СтрокаТаблицыГраниц.КонецДня = '00010101' ИЛИ ГрафикДатаКонецПоГрафику > СтрокаТаблицыГраниц.КонецДня Тогда
					СтрокаТаблицыГраниц.КонецДня = ГрафикДатаКонецПоГрафику;	
				КонецЕсли;
			КонецЦикла;
		
		ИначеЕсли ИспользованиеГрафиковРабот = 1 ИЛИ ИспользованиеГрафиковРабот = 3 Тогда
			Если ИспользованиеГрафиковРабот = 3 Тогда
				Если ЗначениеЗаполнено(ОтборПоПодразделению) Тогда
					ИспользуемыйГрафик = ОтборПоПодразделению.ГрафикРаботы;
				КонецЕсли;
			Иначе
				ИспользуемыйГрафик = БазовыйГрафик;
			КонецЕсли;
			
			ДанныеТаблицыГрафика = кпПолучитьГрафик(ИспользуемыйГрафик, НачалоДня(ДатаОтчета+60*60*24*(СчетчикДней)), КонецДня(ДатаОтчета+60*60*24*(СчетчикДней)));
			Если ДанныеТаблицыГрафика.Количество() > 0 Тогда
				КопияТаблицаБазовогоГрафика = ДанныеТаблицыГрафика.Скопировать();
				
				//исключим из рассмотрения строки, где время не заполнено,
				//для этого время начала установим равным концу дня
				Для Каждого СтрокаГрафика Из КопияТаблицаБазовогоГрафика Цикл
					Если СтрокаГрафика.НачалоРабочегоВремени = '00010101' И  СтрокаГрафика.КонецРабочегоВремени = '00010101' Тогда
						СтрокаГрафика.НачалоРабочегоВремени =  КонецДня(СтрокаГрафика.НачалоРабочегоВремени);
					КонецЕсли;
				КонецЦикла;
				
				КопияТаблицаБазовогоГрафика.Сортировать("Дата ВОЗР,НачалоРабочегоВремени ВОЗР");
				СтрокаТаблицыГраниц.НачалоДня = КопияТаблицаБазовогоГрафика[0].Дата + Час(КопияТаблицаБазовогоГрафика[0].НачалоРабочегоВремени)*60*60 + Минута(КопияТаблицаБазовогоГрафика[0].НачалоРабочегоВремени)*60 + Секунда(КопияТаблицаБазовогоГрафика[0].НачалоРабочегоВремени);
				КопияТаблицаБазовогоГрафика.Сортировать("Дата УБЫВ,КонецРабочегоВремени УБЫВ");
				СтрокаТаблицыГраниц.КонецДня = КопияТаблицаБазовогоГрафика[0].Дата + Час(КопияТаблицаБазовогоГрафика[0].КонецРабочегоВремени)*60*60 + Минута(КопияТаблицаБазовогоГрафика[0].КонецРабочегоВремени)*60 + Секунда(КопияТаблицаБазовогоГрафика[0].КонецРабочегоВремени);
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТаблицыГраниц.НачалоДня = '00010101' Тогда
			СтрокаТаблицыГраниц.НачалоДня = НачалоДня(ДатаОтчета+24*60*60*СчетчикДней);
		КонецЕсли;
		Если СтрокаТаблицыГраниц.КонецДня = '00010101' Тогда
			СтрокаТаблицыГраниц.КонецДня = КонецДня(ДатаОтчета+24*60*60*СчетчикДней);
		КонецЕсли;	
		
	КонецЦикла;
	
	//заново заполним временную таблицу с учетом изменений
	Запрос.Текст = "
	| УНИЧТОЖИТЬ Сотрудники;
	|ВЫБРАТЬ 
    |	ТаблицаРабочихМест.Ссылка,
    |	ТаблицаРабочихМест.ПометкаУдаления,
    |	ТаблицаРабочихМест.ГрафикРаботы
	|ПОМЕСТИТЬ Сотрудники
	|ИЗ &ТаблицаРабочихМест КАК ТаблицаРабочихМест";
	Запрос.УстановитьПараметр("ТаблицаРабочихМест",ТаблицаРабочихМест);
	Запрос.Выполнить();

КонецПроцедуры

//Позволяет проверить, пересекаются ли по времени работы из заявки в пределах указанной ячейки
// Возвращает Истина или Ложь в зависимости от того, есть ли хоть одно пересечение или нет
// Параметры: 
//	СписокЗаявок - Список значений - Ссылки на документы 
//	СмещениеСтроки - Число - Смещение строки в таблице смещений 
//	СмещениеКолонки - Число - Смещение колонки в таблице смещений 
Функция ПроверитьПересечениеДокументов(СписокЗаявок,СмещениеСтроки,СмещениеКолонки) 
	Отбор = Новый Структура();
	Отбор.Вставить("СмещениеКолонки",СмещениеКолонки);
	Отбор.Вставить("СмещениеСтроки",СмещениеСтроки);
	НайденныеСтроки = ТаблицаСмещений.НайтиСтроки(Отбор);
	ЕстьПересечение = Ложь;
	Для Индекс1 = 0 По НайденныеСтроки.Количество()-2 Цикл
		Для Индекс2 = Индекс1+1 По НайденныеСтроки.Количество()-1 Цикл
			ТестДрайв1 = НайденныеСтроки[Индекс1].ДокументСсылка;
			ТестДрайв2 = НайденныеСтроки[Индекс2].ДокументСсылка;
			Если (ТестДрайв1.ДатаНачала < ТестДрайв2.ДатаОкончания) И (ТестДрайв1.ДатаОкончания > ТестДрайв2.ДатаНачала) Тогда
		   		ЕстьПересечение = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Возврат ЕстьПересечение;	
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ КАЛЕНДАРЯ НА МЕСЯЦ

// Функция предназначена для формирования таблицы загрузки рабочих мест свернутой по дням
// Возвращает таблицу значений
Функция СформироватьТаблицуЗагрузкиРабочихМест() Экспорт
	Если РежимКалендаря = 1 Тогда
		Возврат СформироватьТаблицуЗагрузкиДляАвтомобилей(); 
	ИначеЕсли РежимКалендаря = 2 Тогда
		Возврат СформироватьТаблицуЗагрузкиДляСотрудников();
	ИначеЕсли РежимКалендаря = 3 Тогда
		Возврат СформироватьТаблицуЗагрузкиДляМоделей(); 
	КонецЕсли
КонецФункции

// Функция предназначена для формирования таблицы загрузки рабочих мест свернутой по дням
// Возвращает таблицу значений
// Параметры:
Функция СформироватьТаблицуЗагрузкиДляАвтомобилей() Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТАвтомобили;
	Запрос.Текст ="ВЫБРАТЬ
	              |	ЕСТЬNULL(ВыборкаОборотовДетали.КоличествоНормочасовОборот, 0) КАК КоличествоНормочасовОборот,
	              |	ВыборкаОборотовДетали.Период,
	              |	ВыборкаОборотовДетали.РабочееМесто
	              |ПОМЕСТИТЬ ДанныеЗагрузки
	              |ИЗ
	              |	(ВЫБРАТЬ
	              |		ГрафикРаботыРесурсов.Дата КАК Период,
	              |		ГрафикРаботыРесурсов.Ресурс1 КАК РабочееМесто,
	              |		СУММА(ЧАС(ГрафикРаботыРесурсов.Продолжительность) + МИНУТА(ГрафикРаботыРесурсов.Продолжительность) / 60 + СЕКУНДА(ГрафикРаботыРесурсов.Продолжительность) / 3600) КАК КоличествоНормочасовОборот
	              |	ИЗ
	              |		РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	              |	ГДЕ
	              |		ГрафикРаботыРесурсов.Ресурс1 В
	              |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |					Выб.Ссылка
	              |				ИЗ
	              |					АВТОМОБИЛИ КАК Выб)
	              |		И ГрафикРаботыРесурсов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	              |		И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ТестДрайв)
	              |	
	              |	СГРУППИРОВАТЬ ПО
	              |		ГрафикРаботыРесурсов.Дата,
	              |		ГрафикРаботыРесурсов.Ресурс1) КАК ВыборкаОборотовДетали
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	Показатели.Период КАК Период,
	              |	Показатели.РабочееМесто КАК РабочееМесто,
	              |	Показатели.РабочееМесто.Наименование КАК РабочееМестоНаименование,
	              |	0 КАК ОптМощностьНормочасов,
	              |	СУММА(Показатели.КоличествоНормочасовОборот) КАК КоличествоНормочасовОборот
	              |ИЗ
	              |	АВТОМОБИЛИ КАК АВТОМОБИЛИ
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	              |			ДанныеЗагрузки.Период КАК Период,
	              |			ДанныеЗагрузки.РабочееМесто КАК РабочееМесто,
	              |			0 КАК Поле1,
	              |			ДанныеЗагрузки.КоличествоНормочасовОборот КАК КоличествоНормочасовОборот
	              |		ИЗ
	              |			ДанныеЗагрузки КАК ДанныеЗагрузки) КАК Показатели
	              |		ПО АВТОМОБИЛИ.Ссылка = Показатели.РабочееМесто
	              |ГДЕ
	              |	Показатели.РабочееМесто.ПометкаУдаления = ЛОЖЬ
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	Показатели.Период,
	              |	Показатели.РабочееМесто,
	              |	Показатели.РабочееМесто.Наименование
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	Период,
	              |	РабочееМесто";
				  
	Запрос.УстановитьПараметр("НачалоПериода",НачалоМесяца(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(ДатаОтчета));
	ТаблицаЗагрузки = Запрос.Выполнить().Выгрузить();
	ТаблицаЗагрузки.Индексы.Добавить("Период");
	
	Возврат ТаблицаЗагрузки;
КонецФункции

// Функция предназначена для формирования таблицы загрузки мастеров свернутой по дням
// Возвращает таблицу значений
// Параметры:
Функция СформироватьТаблицуЗагрузкиДляСотрудников() Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТСотрудники;
	Запрос.Текст ="ВЫБРАТЬ
	              |	ТестДрайвОбороты.Период,
	              |	РабочиеМеста.РабочееМесто
	              |ПОМЕСТИТЬ ВыборкаСотрудники
	              |ИЗ
	              |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |		ТестДрайв.Дата КАК Период
	              |	ИЗ
	              |		РегистрСведений.ГрафикРаботыРесурсов КАК ТестДрайв
	              |	ГДЕ
	              |		ТестДрайв.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	              |		И ТестДрайв.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ТестДрайв)
	              |	
	              |	СГРУППИРОВАТЬ ПО
	              |		ТестДрайв.Дата,
	              |		ТестДрайв.Ресурс2) КАК ТестДрайвОбороты
	              |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	              |			Сотрудники.Ссылка КАК РабочееМесто
	              |		ИЗ
	              |			Сотрудники КАК Сотрудники
	              |		ГДЕ
	              |			Сотрудники.ПометкаУдаления = ЛОЖЬ) КАК РабочиеМеста
	              |		ПО ТестДрайвОбороты.Период <> РабочиеМеста.РабочееМесто
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ВыборкаСотрудники.Период КАК Период,
	              |	ВыборкаСотрудники.РабочееМесто КАК РабочееМесто,
	              |	ВыборкаСотрудники.РабочееМесто КАК РабочееМестоНаименование,
	              |	0 КАК ОптМощностьНормочасов,
	              |	ЕСТЬNULL(ВыборкаОборотовДетали.КоличествоНормочасовОборот, 0) КАК КоличествоНормочасовОборот
	              |ИЗ
	              |	ВыборкаСотрудники КАК ВыборкаСотрудники
	              |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	              |			ГрафикРаботыРесурсов.Дата КАК Период,
	              |			ГрафикРаботыРесурсов.Ресурс2 КАК РабочееМесто,
	              |			СУММА(ЧАС(ГрафикРаботыРесурсов.Продолжительность) + МИНУТА(ГрафикРаботыРесурсов.Продолжительность) / 60 + СЕКУНДА(ГрафикРаботыРесурсов.Продолжительность) / 3600) КАК КоличествоНормочасовОборот
	              |		ИЗ
	              |			РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	              |		ГДЕ
	              |			ГрафикРаботыРесурсов.Ресурс2 В
	              |					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |						Выб.РабочееМесто
	              |					ИЗ
	              |						ВыборкаСотрудники КАК Выб)
	              |			И ГрафикРаботыРесурсов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	              |			И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ТестДрайв)
	              |		
	              |		СГРУППИРОВАТЬ ПО
	              |			ГрафикРаботыРесурсов.Дата,
	              |			ГрафикРаботыРесурсов.Ресурс2) КАК ВыборкаОборотовДетали
	              |		ПО ВыборкаСотрудники.РабочееМесто = ВыборкаОборотовДетали.РабочееМесто
	              |			И ВыборкаСотрудники.Период = ВыборкаОборотовДетали.Период
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	Период,
	              |	РабочееМестоНаименование";
			  
	Запрос.УстановитьПараметр("НачалоПериода",НачалоМесяца(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(ДатаОтчета));
	ТаблицаЗагрузки = Запрос.Выполнить().Выгрузить();
	ТаблицаЗагрузки.Индексы.Добавить("Период");
	Возврат ТаблицаЗагрузки;
КонецФункции

// Функция предназначена для формирования таблицы загрузки рабочих мест свернутой по дням
// Возвращает таблицу значений
// Параметры:
Функция СформироватьТаблицуЗагрузкиДляМоделей() Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТАвтомобили;
	Запрос.Текст ="ВЫБРАТЬ
	              |	ЕСТЬNULL(ВыборкаОборотовДетали.КоличествоНормочасовОборот, 0) КАК КоличествоНормочасовОборот,
	              |	ВыборкаОборотовДетали.Период,
	              |	ВыборкаОборотовДетали.РабочееМесто
	              |ПОМЕСТИТЬ ДанныеЗагрузки
	              |ИЗ
	              |	(ВЫБРАТЬ
	              |		ГрафикРаботыРесурсов.Дата КАК Период,
	              |		ГрафикРаботыРесурсов.Ресурс1.Модель КАК РабочееМесто,
	              |		СУММА(ЧАС(ГрафикРаботыРесурсов.Продолжительность) + МИНУТА(ГрафикРаботыРесурсов.Продолжительность) / 60 + СЕКУНДА(ГрафикРаботыРесурсов.Продолжительность) / 3600) КАК КоличествоНормочасовОборот
	              |	ИЗ
	              |		РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	              |	ГДЕ
	              |		ГрафикРаботыРесурсов.Ресурс1 В
	              |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	              |					Выб.Модель
	              |				ИЗ
	              |					АВТОМОБИЛИ КАК Выб)
	              |		И ГрафикРаботыРесурсов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	              |		И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ТестДрайв)
	              |	
	              |	СГРУППИРОВАТЬ ПО
	              |		ГрафикРаботыРесурсов.Дата,
	              |		ГрафикРаботыРесурсов.Ресурс1,
	              |		ГрафикРаботыРесурсов.Ресурс1.Модель) КАК ВыборкаОборотовДетали
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	Показатели.Период КАК Период,
	              |	Показатели.РабочееМесто КАК РабочееМесто,
	              |	Показатели.РабочееМесто.Наименование КАК РабочееМестоНаименование,
	              |	0 КАК ОптМощностьНормочасов,
	              |	СУММА(Показатели.КоличествоНормочасовОборот) КАК КоличествоНормочасовОборот
	              |ИЗ
	              |	АВТОМОБИЛИ КАК АВТОМОБИЛИ
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	              |			ДанныеЗагрузки.Период КАК Период,
	              |			ДанныеЗагрузки.РабочееМесто КАК РабочееМесто,
	              |			0 КАК Поле1,
	              |			ДанныеЗагрузки.КоличествоНормочасовОборот КАК КоличествоНормочасовОборот
	              |		ИЗ
	              |			ДанныеЗагрузки КАК ДанныеЗагрузки) КАК Показатели
	              |		ПО АВТОМОБИЛИ.Модель = Показатели.РабочееМесто
	              |ГДЕ
	              |	Показатели.РабочееМесто.ПометкаУдаления = ЛОЖЬ
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	Показатели.Период,
	              |	Показатели.РабочееМесто,
	              |	Показатели.РабочееМесто.Наименование
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	Период,
	              |	РабочееМесто";
				  
	Запрос.УстановитьПараметр("НачалоПериода",НачалоМесяца(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(ДатаОтчета));
	ТаблицаЗагрузки = Запрос.Выполнить().Выгрузить();
	ТаблицаЗагрузки.Индексы.Добавить("Период");
	
	Возврат ТаблицаЗагрузки;
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ КАЛЕНДАРЯ НА ДЕНЬ

// Функция предназначена для формирования детальной таблицы загрузки рабочих мест
// Возвращает таблицу значений
//
// Параметры:
// РежимКалендаря - Число - Текущий режим просмотра календаря (1 - по автомобилям, 2 - по сотрудникам)
//
Функция СформироватьДетальнуюТаблицуЗагрузкиРабочихМест(ДокументОбъект) Экспорт
	Если РежимКалендаря = 1 Тогда
		Возврат СформироватьДетальнуюТаблицуЗагрузкиДляАвтомобилей(ДокументОбъект); 
	ИначеЕсли РежимКалендаря = 2 Тогда
		Возврат СформироватьДетальнуюТаблицуЗагрузкиДляСотрудников(ДокументОбъект); 
	ИначеЕсли РежимКалендаря = 3 Тогда
		СформироватьДетальнуюТаблицуЗагрузкиДляАвтомобилей(ДокументОбъект);
		Возврат СформироватьДетальнуюТаблицуЗагрузкиДляМоделей(ДокументОбъект); 
	КонецЕсли;
КонецФункции

// Функция предназначена для формирования детальной таблицы загрузки рабочих мест
// Возвращает таблицу значений
// Параметры:
Функция СформироватьДетальнуюТаблицуЗагрузкиДляАвтомобилей(ДокументОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТАвтомобили;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Автомобили.Ссылка КАК РабочееМесто,
	               |	Автомобили.Модель КАК Модель,
	               |	Автомобили.Ссылка.Наименование КАК РабочееМестоНаименование,
	               |	Автомобили.ГрафикРаботы КАК ГрафикРаботы,
	               |	ЕСТЬNULL(ГрафикРаботыРесурсов.НачалоВыполнения, &ПустаяДата) КАК НачалоВыполнения,
	               |	ЕСТЬNULL(ГрафикРаботыРесурсов.ОкончаниеВыполнения, &ПустаяДата) КАК ОкончаниеВыполнения,
	               |	ЕСТЬNULL(ГрафикРаботыРесурсов.Регистратор, &ПустаяСсылка) КАК Регистратор,
	               |	ЕСТЬNULL(ГрафикРаботыРесурсов.Продолжительность, 0) КАК Продолжительность,
	               |	ЕСТЬNULL(ГрафикРаботыРесурсов.Дата, &ПустаяДата) КАК Дата,
	               |	ВЫБОР
	               |		КОГДА ГрафикРаботыРесурсов.РабочееМесто ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ФлагНаличияЗаписей
	               |ИЗ
	               |	Автомобили КАК Автомобили
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ГрафикРаботыРесурсов.Ресурс1 КАК РабочееМесто,
	               |			ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.НачалоРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.НачалоРабочегоВремени)) КАК НачалоВыполнения,
	               |			ДОБАВИТЬКДАТЕ(ГрафикРаботыРесурсов.Дата, СЕКУНДА, ЧАС(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ГрафикРаботыРесурсов.КонецРабочегоВремени) * 60 + СЕКУНДА(ГрафикРаботыРесурсов.КонецРабочегоВремени)) КАК ОкончаниеВыполнения,
	               |			ГрафикРаботыРесурсов.Объект КАК Регистратор,
	               |			ГрафикРаботыРесурсов.Продолжительность КАК Продолжительность,
	               |			ГрафикРаботыРесурсов.Дата КАК Дата
	               |		ИЗ
	               |			РегистрСведений.ГрафикРаботыРесурсов КАК ГрафикРаботыРесурсов
	               |		ГДЕ
	               |			ГрафикРаботыРесурсов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |			И ГрафикРаботыРесурсов.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ТестДрайв)) КАК ГрафикРаботыРесурсов
	               |		ПО Автомобили.Ссылка = ГрафикРаботыРесурсов.РабочееМесто
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	Автомобили.Ссылка,
	               |	Автомобили.Модель,
	               |	Автомобили.Ссылка.Наименование,
	               |	Автомобили.ГрафикРаботы,
	               |	&ПустаяДата,
	               |	&ПустаяДата,
	               |	&ПустаяСсылка,
	               |	0,
	               |	&ПустаяДата,
	               |	ЛОЖЬ
	               |ИЗ
	               |	Автомобили КАК Автомобили
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	РабочееМестоНаименование,
	               |	НачалоВыполнения";
	
	Запрос.УстановитьПараметр("НачалоПериода",НачалоДня(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)));
	Запрос.УстановитьПараметр("ПустаяДата",Дата(1,1,1,0,0,0));
	Запрос.УстановитьПараметр("ПустаяСсылка",Документы.ТестДрайв.ПустаяСсылка());
	ДетальнаяТаблицаЗагрузкиРабочихМест = Запрос.Выполнить().Выгрузить();
	ДетальнаяТаблицаЗагрузкиРабочихМест.Индексы.Добавить("РабочееМесто");
	
	Если НЕ ДокументОбъект.Ссылка.Пустая() Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("Регистратор",ДокументОбъект.Ссылка);
		
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
            Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
			ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
		КонецЦикла;
	КонецЕсли;
	
	Если ДокументОбъект.ДатаНачала <> Дата(1,1,1,0,0,0) И ДокументОбъект.ДатаОкончания <> Дата(1,1,1,0,0,0) Тогда  
		//отображаться будут только строки планирования, которые попадают в границы календаря
		Если  ДокументОбъект.ДатаНачала <= КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)) И
			 ДокументОбъект.ДатаОкончания >= НачалоДня(ДатаОтчета) Тогда
				НоваяСтрока = ДетальнаяТаблицаЗагрузкиРабочихМест.Добавить();
				НоваяСтрока.НачалоВыполнения = ДокументОбъект.ДатаНачала;
				НоваяСтрока.ОкончаниеВыполнения = ДокументОбъект.ДатаОкончания;
				НоваяСтрока.РабочееМесто = ДокументОбъект.Автомобиль;
				Если ЗначениеЗаполнено(ДокументОбъект.Автомобиль) Тогда
					НоваяСтрока.Модель = ДокументОбъект.Автомобиль.Модель;
				КонецЕсли;
				НоваяСтрока.РабочееМестоНаименование = ?(ЗначениеЗаполнено(ДокументОбъект.Автомобиль),ДокументОбъект.Автомобиль.Наименование,"");
				НоваяСтрока.Регистратор = ДокументОбъект.Ссылка;	
		КонецЕсли;
	КонецЕсли;
		
	ДетальнаяТаблицаЗагрузкиРабочихМест.Сортировать("РабочееМестоНаименование,НачалоВыполнения");
	
	//удалим строки с незаполненным рабочим местом
	Отбор = Новый Структура();
	Отбор.Вставить("РабочееМесто",Справочники.Автомобили.ПустаяСсылка());
	Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
	Для Каждого Строка Из Строки Цикл
        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
		ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
	КонецЦикла;
	
	//в режиме использования графиков рабочих мест удалим рабочие места, для которых не задан график на дату 
	Если ИспользованиеГрафиковРабот = 2 Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("ФлагНаличияЗаписей",Ложь);
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
			ДанныеТаблицыГрафика = Новый ТаблицаЗначений;
			ДанныеТаблицыГрафика = кпПолучитьГрафик(Строка.ГрафикРаботы, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета)+60*60*24*(Продолжительность-1));
			Если ДанныеТаблицыГрафика.Количество() = 0 Тогда
		        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
				ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
			ИначеЕсли ОтображатьТолькоРаботающих Тогда
				ЕстьРабочееВремя = Ложь;
				Для Каждого СтрокаГрафика Из ДанныеТаблицыГрафика Цикл
					Если СтрокаГрафика.ВидДня = Перечисления.ВидДня.Рабочий ИЛИ СтрокаГрафика.ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
						ЕстьРабочееВремя = Истина;	
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если НЕ ЕстьРабочееВремя Тогда
			        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
					ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;	
	
КонецФункции

// Функция предназначена для формирования детальной таблицы загрузки мастеров
// Возвращает таблицу значений
// Параметры:
Функция СформироватьДетальнуюТаблицуЗагрузкиДляСотрудников(ДокументОбъект,Планирование=Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТСотрудники;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(Сотрудники.Ссылка, &ПустойМастер) КАК РабочееМесто,
	               |	Сотрудники.Ссылка.Наименование КАК РабочееМестоНаименование,
	               |	ЕСТЬNULL(ТестДрайв.НачалоВыполнения, &ПустаяДата) КАК НачалоВыполнения,
	               |	ЕСТЬNULL(ТестДрайв.ОкончаниеВыполнения, &ПустаяДата) КАК ОкончаниеВыполнения,
	               |	ЕСТЬNULL(ТестДрайв.Регистратор, &ПустаяСсылка) КАК Регистратор,
	               |	ЕСТЬNULL(ТестДрайв.Продолжительность, 0) КАК Продолжительность,
	               |	ЕСТЬNULL(ТестДрайв.Дата, &ПустаяДата) КАК Дата,
	               |	ВЫБОР
	               |		КОГДА ТестДрайв.РабочееМесто ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ФлагНаличияЗаписей
	               |ИЗ
	               |	Сотрудники КАК Сотрудники
	               |		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ТестДрайв.Ресурс2 КАК РабочееМесто,
	               |			ДОБАВИТЬКДАТЕ(ТестДрайв.Дата, СЕКУНДА, ЧАС(ТестДрайв.НачалоРабочегоВремени) * 60 * 60 + МИНУТА(ТестДрайв.НачалоРабочегоВремени) * 60 + СЕКУНДА(ТестДрайв.НачалоРабочегоВремени)) КАК НачалоВыполнения,
	               |			ДОБАВИТЬКДАТЕ(ТестДрайв.Дата, СЕКУНДА, ЧАС(ТестДрайв.КонецРабочегоВремени) * 60 * 60 + МИНУТА(ТестДрайв.КонецРабочегоВремени) * 60 + СЕКУНДА(ТестДрайв.КонецРабочегоВремени)) КАК ОкончаниеВыполнения,
	               |			ТестДрайв.Объект КАК Регистратор,
	               |			ТестДрайв.Продолжительность КАК Продолжительность,
	               |			ТестДрайв.Дата КАК Дата
	               |		ИЗ
	               |			РегистрСведений.ГрафикРаботыРесурсов КАК ТестДрайв
	               |		ГДЕ
	               |			ТестДрайв.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |			И ТестДрайв.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ТестДрайв)) КАК ТестДрайв
	               |		ПО Сотрудники.Ссылка = ТестДрайв.РабочееМесто
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	Сотрудники.Ссылка,
	               |	Сотрудники.Ссылка.Наименование,
	               |	&ПустаяДата,
	               |	&ПустаяДата,
	               |	&ПустаяСсылка,
	               |	0,
	               |	&ПустаяДата,
	               |	ЛОЖЬ
	               |ИЗ
	               |	Сотрудники КАК Сотрудники
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	РабочееМестоНаименование,
	               |	НачалоВыполнения";
	
	Запрос.УстановитьПараметр("НачалоПериода",НачалоДня(ДатаОтчета));
	Запрос.УстановитьПараметр("КонецПериода",КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)));
	Запрос.УстановитьПараметр("ПустаяДата",Дата(1,1,1,0,0,0));
 	Запрос.УстановитьПараметр("ПустойМастер",Справочники.Сотрудники.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСсылка",Документы.ТестДрайв.ПустаяСсылка());
	ДетальнаяТаблицаЗагрузкиРабочихМест = Запрос.Выполнить().Выгрузить();
	ДетальнаяТаблицаЗагрузкиРабочихМест.Индексы.Добавить("РабочееМесто");
	//ДетальнаяТаблицаЗагрузкиРабочихМест.Колонки.Добавить("ИдентификаторРаботы", Новый ОписаниеТипов("Строка"));
	
	Если НЕ ДокументОбъект.Ссылка.Пустая() Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("Регистратор",ДокументОбъект.Ссылка);
		
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
            Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
			ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
		КонецЦикла;
	КонецЕсли;
		
	
	Если ДокументОбъект.ДатаНачала <> Дата(1,1,1,0,0,0) И ДокументОбъект.ДатаОкончания <> Дата(1,1,1,0,0,0) Тогда  
		//отображаться будут только строки планирования, которые попадают в границы календаря
		Если ДокументОбъект.ДатаНачала >= НачалоДня(ДатаОтчета) И 
			 ДокументОбъект.ДатаНачала <= КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)) И
			 ДокументОбъект.ДатаОкончания >= НачалоДня(ДатаОтчета) И 
			 ДокументОбъект.ДатаОкончания <= КонецДня(ДатаОтчета+24*60*60*(Продолжительность-1)) Тогда
				НоваяСтрока = ДетальнаяТаблицаЗагрузкиРабочихМест.Добавить();
				НоваяСтрока.НачалоВыполнения = ДокументОбъект.ДатаНачала;
				НоваяСтрока.ОкончаниеВыполнения = ДокументОбъект.ДатаОкончания;
				НоваяСтрока.РабочееМесто = ДокументОбъект.Менеджер;
				НоваяСтрока.РабочееМестоНаименование = ?(ЗначениеЗаполнено(ДокументОбъект.Менеджер),ДокументОбъект.Менеджер.Наименование,"");
				НоваяСтрока.Регистратор = ДокументОбъект.Ссылка;	
		КонецЕсли;
	КонецЕсли;
		
	ДетальнаяТаблицаЗагрузкиРабочихМест.Сортировать("РабочееМестоНаименование,НачалоВыполнения");
	
	//удалим строки с незаполненным рабочим местом
	Отбор = Новый Структура();
	Отбор.Вставить("РабочееМесто",Справочники.Сотрудники.ПустаяСсылка());
	Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
	Для Каждого Строка Из Строки Цикл
        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
		ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
	КонецЦикла;
	
	//в режиме использования графиков рабочих мест удалим рабочие места, для которых не задан график на дату 
	Если ИспользованиеГрафиковРабот = 2 Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("ФлагНаличияЗаписей",Ложь);
		Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
		Для Каждого Строка Из Строки Цикл
			ДанныеТаблицыГрафика = Новый ТаблицаЗначений;
			ДанныеТаблицыГрафика = кпПолучитьГрафик(Строка.РабочееМесто.ГрафикРаботы, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета)+60*60*24*(Продолжительность-1));
			Если ДанныеТаблицыГрафика.Количество() = 0 Тогда
		        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
				ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
			ИначеЕсли ОтображатьТолькоРаботающих Тогда
				ЕстьРабочееВремя = Ложь;
				Для Каждого СтрокаГрафика Из ДанныеТаблицыГрафика Цикл
					Если СтрокаГрафика.ВидДня = Перечисления.ВидДня.Рабочий ИЛИ СтрокаГрафика.ВидДня = Перечисления.ВидДня.Предпраздничный Тогда
						ЕстьРабочееВремя = Истина;	
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если НЕ ЕстьРабочееВремя Тогда
			        Индекс = ДетальнаяТаблицаЗагрузкиРабочихМест.Индекс(Строка);
					ДетальнаяТаблицаЗагрузкиРабочихМест.Удалить(Индекс);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
 КонецФункции

 // Функция предназначена для формирования детальной таблицы загрузки рабочих мест
// Возвращает таблицу значений
// Параметры:
Функция СформироватьДетальнуюТаблицуЗагрузкиДляМоделей(ДокументОбъект) Экспорт
	//число моделей
	КопияДетальнойТаблицы = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
	КопияДетальнойТаблицы.Свернуть("Модель");
	ЧислоМоделей = КопияДетальнойТаблицы.Количество();
	КопияДетальнойТаблицы.ВыгрузитьКолонку("Модель");
	
   //|	ЕСТЬNULL(Сотрудники.Ссылка, &ПустойМастер) КАК РабочееМесто,
   //|	Сотрудники.Ссылка.Наименование КАК РабочееМестоНаименование,
   //|	ЕСТЬNULL(ТестДрайв.НачалоВыполнения, &ПустаяДата) КАК НачалоВыполнения,
   //|	ЕСТЬNULL(ТестДрайв.ОкончаниеВыполнения, &ПустаяДата) КАК ОкончаниеВыполнения,
   //|	ЕСТЬNULL(ТестДрайв.Регистратор, &ПустаяСсылка) КАК Регистратор,
   //|	ЕСТЬNULL(ТестДрайв.Продолжительность, 0) КАК Продолжительность,
   //|	ЕСТЬNULL(ТестДрайв.Дата, &ПустаяДата) КАК Дата,
КонецФункции

 
// Процедура предназначена для инициализации и формирования основных таблиц,
// использующихся при выводе календаря в режиме дня
// Параметры:
Процедура РассчитатьОсновныеПараметры(ДокументОбъект,ОбработкаОбъект) Экспорт
	
	ДлительностьИнтервала = ОбработкаОбъект.ИнтервалКалендаря*60;
	СформироватьДетальнуюТаблицуЗагрузкиРабочихМест(ДокументОбъект);
	
	ТаблицаРабочихИнтервалов = Новый ТаблицаЗначений;
	ТаблицаРабочихИнтервалов.Колонки.Добавить("ДатаИнтервала", Новый ОписаниеТипов("Дата"));
	ТаблицаРабочихИнтервалов.Колонки.Добавить("ПризнакРабочий", Новый ОписаниеТипов("Булево"));
	
	ОбщееКоличествоИнтервалов = Продолжительность*(24*60*60/ДлительностьИнтервала);
	Для Сч = 0 По ОбщееКоличествоИнтервалов-1 Цикл
		НоваяСтрока = ТаблицаРабочихИнтервалов.Добавить();	
		НоваяСтрока.ДатаИнтервала = НачалоДня(ДатаОтчета)+Сч*ДлительностьИнтервала;
		НоваяСтрока.ПризнакРабочий = Ложь;
	КонецЦикла;
	
	//определим начальную и конечную даты по существующим документам
	Для СчетчикДней = 0 По Продолжительность-1 Цикл	
		
		//заполним исходные данные
		ДатаНачалоПоГрафику =  ТаблицаГраницГрафика[СчетчикДней].НачалоДня;
		ДатаКонецПоГрафику =  ТаблицаГраницГрафика[СчетчикДней].КонецДня;
		ДатаНачалоПоДокументам = ДатаНачалоПоГрафику;
		ДатаКонецПоДокументам = ДатаКонецПоГрафику;
		ТекущаяДата = ДатаОтчета+24*60*60*СчетчикДней;
		
		//находим начальную дня
		КопияДетальнойТаблицы = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
		КопияДетальнойТаблицы.Сортировать("НачалоВыполнения ВОЗР");
		Для Каждого СтрокаТаблицы Из КопияДетальнойТаблицы Цикл
			//не рассматриваем строки с незаполненной датой
			Если СтрокаТаблицы.НачалоВыполнения = '00010101' Тогда
				Продолжить;
			КонецЕсли;
			//если работа началась еще в предыдущем дне, то дату начала устанавливаем начало дня
			Если СтрокаТаблицы.НачалоВыполнения < НачалоДня(ТекущаяДата) И 
				 СтрокаТаблицы.ОкончаниеВыполнения > НачалоДня(ТекущаяДата) Тогда
				ДатаНачалоПоДокументам = НачалоДня(ТекущаяДата);
				Продолжить;
			КонецЕсли;
			//если работа началась в этом дне...
			Если СтрокаТаблицы.НачалоВыполнения >= НачалоДня(ТекущаяДата) И 
				 СтрокаТаблицы.НачалоВыполнения <= КонецДня(ТекущаяДата) Тогда
				Если СтрокаТаблицы.НачалоВыполнения < ДатаНачалоПоДокументам Тогда
					ДатаНачалоПоДокументам = СтрокаТаблицы.НачалоВыполнения; 	 
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		//находим конечную дату
		КопияДетальнойТаблицы.Сортировать("ОкончаниеВыполнения УБЫВ");
		Для Каждого СтрокаТаблицы Из КопияДетальнойТаблицы Цикл
			//не рассматриваем строки с незаполненной датой
			Если СтрокаТаблицы.ОкончаниеВыполнения = '00010101' Тогда
				Продолжить;
			КонецЕсли;
			//если работа заканчивается в следующем дне, то дату конца устанавливаем конец дня
			Если СтрокаТаблицы.НачалоВыполнения < НачалоДня(ТекущаяДата+24*60*60) И 
				 СтрокаТаблицы.ОкончаниеВыполнения >= НачалоДня(ТекущаяДата+24*60*60) Тогда
				ДатаКонецПоДокументам = КонецДня(ТекущаяДата);
				Продолжить;
			КонецЕсли;
			//если работа заканчивается в этом дне...
			Если СтрокаТаблицы.ОкончаниеВыполнения >= НачалоДня(ТекущаяДата) И 
				 СтрокаТаблицы.ОкончаниеВыполнения <= КонецДня(ТекущаяДата) Тогда
				Если СтрокаТаблицы.ОкончаниеВыполнения > ДатаКонецПоДокументам Тогда
					ДатаКонецПоДокументам = СтрокаТаблицы.ОкончаниеВыполнения; 	 
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		//убедимся, что конец даты по документам не раньше даты отчета
		Если ДатаНачалоПоДокументам < ДатаНачалоПоГрафику  И ДатаКонецПоДокументам > НачалоДня(ДатаНачалоПоГрафику) Тогда
			//если начало даты по документам лежит в другом дне, то время начала отчета = начало дня
			Если НачалоДня(ДатаНачалоПоДокументам) < НачалоДня(ДатаНачалоПоГрафику) Тогда
				ДатаНачалоПоГрафику = НачалоДня(ДатаНачалоПоГрафику);
			Иначе			
				//если начало даты по документам лежит в этом же дне и оно раньше даты по графику, то время начала отчета = время начала по документам
				Если (ДатаНачалоПоГрафику-НачалоДня(ДатаНачалоПоГрафику)) > (ДатаНачалоПоДокументам-НачалоДня(ДатаНачалоПоДокументам)) Тогда
					ДатаНачалоПоГрафику = НачалоДня(ДатаНачалоПоГрафику) + Окр((Час(ДатаНачалоПоДокументам)*60*60+Минута(ДатаНачалоПоДокументам)*60+Секунда(ДатаНачалоПоДокументам))/ДлительностьИнтервала,0,РежимОкругления.Окр15как10)*ДлительностьИнтервала;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//аналогично корректируем время конца дня
		Если ДатаКонецПоДокументам > ДатаКонецПоГрафику  И КонецДня(ДатаКонецПоГрафику) > ДатаНачалоПоДокументам Тогда
			Если КонецДня(ДатаКонецПоДокументам) > КонецДня(ДатаКонецПоГрафику) Тогда
				ДатаКонецПоГрафику = КонецДня(ДатаКонецПоГрафику);
			Иначе			
				Если (ДатаКонецПоГрафику-НачалоДня(ДатаКонецПоГрафику)) < (ДатаКонецПоДокументам-НачалоДня(ДатаКонецПоДокументам)) Тогда
					ДатаКонецПоГрафику = НачалоДня(ДатаНачалоПоГрафику) + Окр((Час(ДатаКонецПоДокументам)*60*60+Минута(ДатаКонецПоДокументам)*60+Секунда(ДатаКонецПоДокументам))/ДлительностьИнтервала,0,РежимОкругления.Окр15как10)*ДлительностьИнтервала;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//заполняем Таблицу границ графика
		СтрокаТаблицыГраниц = ТаблицаГраницГрафика.Найти(НачалоДня(ТекущаяДата),"Дата");
		СтрокаТаблицыГраниц.НачалоДня = ДатаНачалоПоГрафику;		
		СтрокаТаблицыГраниц.КонецДня = ДатаКонецПоГрафику;		
		
		//заполним признак в таблице рабочих интервалов
		ЗаполнятьКакРабочий = Ложь;
		ОбщееКоличествоИнтервалов = 24*60*60/ДлительностьИнтервала;
		Для СчИнтервалов = 0 По ОбщееКоличествоИнтервалов-1 Цикл
			Если ТаблицаРабочихИнтервалов[СчИнтервалов+СчетчикДней*ОбщееКоличествоИнтервалов].ДатаИнтервала >= ТаблицаГраницГрафика[СчетчикДней].НачалоДня Тогда
				ЗаполнятьКакРабочий = Истина;	
			КонецЕсли;	
			Если ТаблицаРабочихИнтервалов[СчИнтервалов+СчетчикДней*ОбщееКоличествоИнтервалов].ДатаИнтервала >= ТаблицаГраницГрафика[СчетчикДней].КонецДня Тогда
				ЗаполнятьКакРабочий = Ложь;	
			КонецЕсли;	
			ТаблицаРабочихИнтервалов[СчИнтервалов+СчетчикДней*ОбщееКоличествоИнтервалов].ПризнакРабочий = ЗаполнятьКакРабочий;
		КонецЦикла;
		
	КонецЦикла;	
	
	//число рабочих мест
	КопияДетальнойТаблицы = ДетальнаяТаблицаЗагрузкиРабочихМест.Скопировать();
	Если РежимКалендаря = 3 Тогда
		КопияДетальнойТаблицы.Свернуть("Модель");
	Иначе
		КопияДетальнойТаблицы.Свернуть("РабочееМесто");
	КонецЕсли;
	ЧислоРабочихМест = КопияДетальнойТаблицы.Количество();
	
КонецПроцедуры

// Процедура предназначена для заполнения основных таблиц детализированного отчета
//
// Параметры:
// РежимКалендаря - Число - Текущий режим просмотра календаря (1 - по автомобилям, 2 - по сотрудникам)
//
Процедура СформироватьСлужебныеТаблицы(ДокументОбъект) Экспорт
	
	ТаблицаСмещений = Новый ТаблицаЗначений;
	ТаблицаСмещений.Колонки.Добавить("ДокументСсылка", Новый ОписаниеТипов("ДокументСсылка.ТестДрайв"));
	ТаблицаСмещений.Колонки.Добавить("СмещениеКолонки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	ТаблицаСмещений.Колонки.Добавить("СмещениеСтроки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	//ТаблицаСмещений.Колонки.Добавить("ИдентификаторРаботы", Новый ОписаниеТипов("Строка"));
	ТаблицаСмещений.Колонки.Добавить("Продолжительность",Новый ОписаниеТипов("Число"));
	ТаблицаСмещений.Колонки.Добавить("ТекущаяРабота",Новый ОписаниеТипов("Булево"));
	
	// Определим рабочую таблицу отчета и создадим колонки
	ТаблицаРасшифровок = Новый ТаблицаЗначений;
	Если РежимКалендаря = 1 Тогда
		ТаблицаРасшифровок.Колонки.Добавить("РабочееМесто",Новый ОписаниеТипов("СправочникСсылка.Автомобили"));
	ИначеЕсли РежимКалендаря = 2 Тогда
		ТаблицаРасшифровок.Колонки.Добавить("РабочееМесто",Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ИначеЕсли РежимКалендаря = 3 Тогда
		ТаблицаРасшифровок.Колонки.Добавить("РабочееМесто",Новый ОписаниеТипов("СправочникСсылка.Модели"));
	КонецЕсли;
		
	//формируем незаполненную таблицу расшифровок
	//и сразу заполним первую строку датой интервала в корректном формате
	СлужебнаяСтрока = ТаблицаРасшифровок.Добавить();
	НовыйИнтервал = НачалоДня(ДатаОтчета);
	СтарыйИнтервал = НовыйИнтервал-1;
	ОбщееКоличествоИнтервалов = Продолжительность*24*60*60/ДлительностьИнтервала;
	Для ИндексИнтервала = 0 По ОбщееКоличествоИнтервалов-1 Цикл
		//добавляем колонку, если начало нового дня
		Если НачалоДня(СтарыйИнтервал) <> НачалоДня(НовыйИнтервал) Тогда
			ИмяКолонки = "НоваяДата" + Формат(НовыйИнтервал,"ДФ=ММдд");		
			ТаблицаРасшифровок.Колонки.Добавить(ИмяКолонки,Новый ОписаниеТипов("СписокЗначений"));
			НовоеЗначениеЯчейки = Новый СписокЗначений;
			НовоеЗначениеЯчейки.Добавить(НовыйИнтервал);
			СлужебнаяСтрока[ИмяКолонки] = НовоеЗначениеЯчейки;     
		КонецЕсли;	
		//добавляем колонки в таблицу расшифровок, не  выходяшие за график
		СтрокаТаблицыГраниц = ТаблицаГраницГрафика.Найти(НачалоДня(НовыйИнтервал),"Дата");
		Если НовыйИнтервал >= СтрокаТаблицыГраниц.НачалоДня И НовыйИнтервал < СтрокаТаблицыГраниц.КонецДня Тогда
			ИмяКолонки = "Интервал" + Формат(НовыйИнтервал,"ДФ=ддЧЧмм");		
			ТаблицаРасшифровок.Колонки.Добавить(ИмяКолонки,Новый ОписаниеТипов("СписокЗначений"));
			НовоеЗначениеЯчейки = Новый СписокЗначений;
			НовоеЗначениеЯчейки.Добавить(НовыйИнтервал);
			СлужебнаяСтрока[ИмяКолонки] = НовоеЗначениеЯчейки;
		КонецЕсли;
		СтарыйИнтервал = НовыйИнтервал;			
		НовыйИнтервал = НовыйИнтервал + ДлительностьИнтервала;
	КонецЦикла;
	
	ЧислоКолонокТаблицыРасшифровок = ТаблицаРасшифровок.Колонки.Количество();
	
	Для Каждого СтрокаТаблицыЗагрузки Из ДетальнаяТаблицаЗагрузкиРабочихМест Цикл
		
		//Если СтрокаТаблицыЗагрузки.ИдентификаторРаботы = "" Тогда
		//	СтрокаТаблицыЗагрузки.ИдентификаторРаботы = Новый УникальныйИдентификатор;
		//КонецЕсли;
		
		// Получим строку для заполнения или создадим новую
		Если РежимКалендаря = 1 ИЛИ РежимКалендаря = 2 Тогда
			РабочееМесто = СтрокаТаблицыЗагрузки.РабочееМесто;
		ИначеЕсли РежимКалендаря = 3 Тогда
			РабочееМесто = СтрокаТаблицыЗагрузки.Модель;
		КонецЕсли;
		
		РезультатПоискаСтроки = ТаблицаРасшифровок.Найти(РабочееМесто, "РабочееМесто");
		Если РезультатПоискаСтроки <> Неопределено Тогда
			СтрокаТаблицыРасшифровок = РезультатПоискаСтроки;
		Иначе
			СтрокаТаблицыРасшифровок = ТаблицаРасшифровок.Добавить();
			СтрокаТаблицыРасшифровок.РабочееМесто = РабочееМесто;
		КонецЕсли;
		
		//Определим начальный и конечный интервалы
		
		СлужебнаяСтрока = ТаблицаРасшифровок[0];
		
		Для ИндексКолонки = 2 По ЧислоКолонокТаблицыРасшифровок-1 Цикл
			
			Если Лев(ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя,8)="НоваяДат" Тогда 
				Продолжить;
			КонецЕсли;	
			
			Если СтрокаТаблицыЗагрузки.НачалоВыполнения < СлужебнаяСтрока[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя][0].Значение+ДлительностьИнтервала
				И СтрокаТаблицыЗагрузки.ОкончаниеВыполнения > СлужебнаяСтрока[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя][0].Значение Тогда
				Если обЗначениеНеЗаполнено(СтрокаТаблицыЗагрузки.Регистратор) Тогда
					Представление = ПолучитьПредставлениеДокумента(ДокументОбъект,ФорматПредставленияДокумента);					
				Иначе
					Представление = ПолучитьПредставлениеДокумента(СтрокаТаблицыЗагрузки.Регистратор,ФорматПредставленияДокумента);					
				КонецЕсли;
				СтрокаТаблицыРасшифровок[ТаблицаРасшифровок.Колонки[ИндексКолонки].Имя].Добавить(СтрокаТаблицыЗагрузки.Регистратор,Представление);	
				
				// ТАБЛИЦА СМЕЩЕНИЙ
				
				НоваяСтрокаТаблицыСмещений = ТаблицаСмещений.Добавить();
				НоваяСтрокаТаблицыСмещений.ДокументСсылка = СтрокаТаблицыЗагрузки.Регистратор;
				//НоваяСтрокаТаблицыСмещений.ИдентификаторРаботы = СтрокаТаблицыЗагрузки.ИдентификаторРаботы;
				НоваяСтрокаТаблицыСмещений.СмещениеКолонки = ИндексКолонки;
				НоваяСтрокаТаблицыСмещений.СмещениеСтроки = ТаблицаРасшифровок.Индекс(СтрокаТаблицыРасшифровок);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	// ТАБЛИЦА ОФОРМЛЕНИЯ
	
	ТаблицаОформления = Новый ТаблицаЗначений;
	ТаблицаОформления.Колонки.Добавить("АдресЯчейки", Новый ОписаниеТипов("Строка"));
	ТаблицаОформления.Колонки.Добавить("СмещениеКолонки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	ТаблицаОформления.Колонки.Добавить("СмещениеСтроки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10)));
	ТаблицаОформления.Колонки.Добавить("ПредставлениеЗаявки");
	ТаблицаОформления.Колонки.Добавить("ЦветФона");
	ТаблицаОформления.Колонки.Добавить("ЦветРамки");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиВерхняя");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиНижняя");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиЛевая");
	ТаблицаОформления.Колонки.Добавить("ЛинияРамкиПравая");
	ТаблицаОформления.Колонки.Добавить("АктуальноеВремя", Новый ОписаниеТипов("Булево"));
	ТаблицаОформления.Колонки.Добавить("Ссылки");
	
	
	Для ТекущаяКолонка = 1 По ТаблицаРасшифровок.Количество()-1 Цикл
		
		// Определим, какой график использовать для строки
		Если ИспользованиеГрафиковРабот = 2 Тогда
			Если РежимКалендаря = 1 Тогда
				РабочееМесто = ТаблицаРасшифровок[ТекущаяКолонка]["РабочееМесто"];
				
				Отбор = Новый Структура();
				Отбор.Вставить("РабочееМесто",РабочееМесто);
				Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
				Если Строки.Количество()>0 Тогда
					ИспользуемыйГрафик = Строки[0].ГрафикРаботы;
				КонецЕсли;
				
				ТаблицаГрафикаСтроки =  кпПолучитьГрафик(ИспользуемыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
			ИначеЕсли РежимКалендаря = 2 Тогда
				
				ГрафикРабочегоМеста = ТаблицаРасшифровок[ТекущаяКолонка]["РабочееМесто"].ГрафикРаботы;
				Если ГрафикРабочегоМеста <> Неопределено Тогда
					ТаблицаГрафикаСтроки = кпПолучитьГрафик(ГрафикРабочегоМеста, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
				КонецЕсли;
				
			ИначеЕсли РежимКалендаря = 3 Тогда
				
				Отбор = Новый Структура();
				Отбор.Вставить("Модель",ТаблицаРасшифровок[ТекущаяКолонка]["РабочееМесто"]);
				Строки = ДетальнаяТаблицаЗагрузкиРабочихМест.НайтиСтроки(Отбор); 
				Для Каждого Строка Из Строки Цикл
					ИспользуемыйГрафик = Строка.ГрафикРаботы;
					Если ТаблицаГрафикаСтроки = Неопределено Тогда
						ТаблицаГрафикаСтроки =  кпПолучитьГрафик(ИспользуемыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
					Иначе
						ТекТаблицаГрафика =  кпПолучитьГрафик(ИспользуемыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
						Для Каждого ТекСтрока ИЗ ТекТаблицаГрафика Цикл
							СтрокаТаблицыГрафика = ТаблицаГрафикаСтроки.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаТаблицыГрафика,ТекСтрока);
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
		ИначеЕсли ИспользованиеГрафиковРабот = 1 Тогда
			ТаблицаГрафикаСтроки =  кпПолучитьГрафик(БазовыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
		ИначеЕсли ИспользованиеГрафиковРабот = 3 Тогда
			Если ЗначениеЗаполнено(ОтборПоПодразделению) Тогда
				ИспользуемыйГрафик = ОтборПоПодразделению.ГрафикРаботы;
				ТаблицаГрафикаСтроки =  кпПолучитьГрафик(ИспользуемыйГрафик, НачалоДня(ДатаОтчета), КонецДня(ДатаОтчета+60*60*24*(Продолжительность-1)));
			КонецЕсли;
		КонецЕсли;
		
		ЗаявкаПредыдущейЯчейки = Документы.ТестДрайв.ПустаяСсылка();
		
		Для ИндексИнтервала = 2 По ЧислоКолонокТаблицыРасшифровок-1 Цикл
			
			//пропускаем строчку с днём
			Если Лев(ТаблицаРасшифровок.Колонки[ИндексИнтервала].Имя,8)="НоваяДат" Тогда 
				Продолжить;
			КонецЕсли;
					
			НоваяСтрокаОформления = ТаблицаОформления.Добавить();
			НоваяСтрокаОформления.АдресЯчейки = "R" + Строка(ИндексИнтервала+ОтступСлева) + "C" + Строка(ТекущаяКолонка+ОтступСверху);
			НоваяСтрокаОформления.СмещениеКолонки = ИндексИнтервала;
			НоваяСтрокаОформления.СмещениеСтроки = ТекущаяКолонка;
			НоваяСтрокаОформления.ЦветРамки = ЦветаРаскраски.Получить("ЦветРамкиСтандарт");
			НоваяСтрокаОформления.ЛинияРамкиВерхняя = ЛинияРамкиВнутренняя;
			НоваяСтрокаОформления.ЛинияРамкиНижняя = ЛинияРамкиВнутренняя;
			НоваяСтрокаОформления.ЛинияРамкиЛевая = ЛинияРамкиСплошная;
			НоваяСтрокаОформления.ЛинияРамкиПравая = ЛинияРамкиСплошная;
			
			ЗначениеЗаявки = ТаблицаРасшифровок[ТекущаяКолонка][ИндексИнтервала];
			
			ОчищатьЯчейку = Ложь;
			Если ТипЗнч(ЗначениеЗаявки) = Тип("СписокЗначений") И ЗначениеЗаявки.Количество() > 0 Тогда
				
				НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветЗагрузкаНорм");
				Если ЗначениеЗаявки.Количество() > 1 И ПроверитьПересечениеДокументов(ЗначениеЗаявки,ТекущаяКолонка,ИндексИнтервала) Тогда
 					НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветЗагрузкаМакс");	
				КонецЕсли;
				
				ОчищатьЯчейку = НЕ (ЗначениеЗаявки.НайтиПоЗначению(ЗаявкаПредыдущейЯчейки) <> ЗначениеЗаявки.НайтиПоЗначению(ЗначениеЗаявки[0].Значение)) ;
				Если НЕ ОчищатьЯчейку Тогда       
					НоваяСтрокаОформления.ПредставлениеЗаявки = ? (ЗначениеЗаявки[0].Значение <> Неопределено, ПолучитьПредставлениеДокумента(ЗначениеЗаявки[0].Значение,ФорматПредставленияДокумента), "");
					НоваяСтрокаОформления.ЛинияРамкиВерхняя = ЛинияРамкиСплошная;	
					НоваяСтрокаОформления.ЛинияРамкиНижняя = ЛинияРамкиВнутренняя;	
				Иначе
					НоваяСтрокаОформления.ЛинияРамкиВерхняя = ЛинияРамкиВнутренняя;	
					НоваяСтрокаОформления.ЛинияРамкиНижняя = ЛинияРамкиВнутренняя;	
				КонецЕсли;
				НоваяСтрокаОформления.ЛинияРамкиНижняя = ЛинияРамкиВнутренняя;	
				ЗаявкаПредыдущейЯчейки = ЗначениеЗаявки[0].Значение;
				
			Иначе
				
				Если (ЗаявкаПредыдущейЯчейки <> Неопределено И ЗаявкаПредыдущейЯчейки <> Документы.ТестДрайв.ПустаяСсылка()) Тогда       
					НоваяСтрокаОформления.ЛинияРамкиВерхняя = ЛинияРамкиСплошная;	
				КонецЕсли;
					
				НоваяСтрокаОформления.ПредставлениеЗаявки = "";
				НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно");
				ЗаявкаПредыдущейЯчейки = Документы.ТестДрайв.ПустаяСсылка();
				
			КонецЕсли;
			
			//раскрасим ячейку в соответствии с графиком
			Если ИспользованиеГрафиковРабот > 0 И НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно") Тогда
				ДатаТекущегоИнтервала = ТаблицаРасшифровок[0][ИндексИнтервала][0].Значение;
				ИнтервалВнеГрафика = Истина;
				ЭтоРабота = Ложь;
				ЭтоОбед = Ложь;
				ЭтоПерерыв = Ложь;
				Если ТаблицаГрафикаСтроки <> Неопределено Тогда
					Для Сч = 0 По ТаблицаГрафикаСтроки.Количество()-1 Цикл
						СтрокаТаблицыГрафика = ТаблицаГрафикаСтроки[Сч];
						НачалоРабочегоВремени = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.НачалоРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.НачалоРабочегоВремени);
						КонецРабочегоВремени = СтрокаТаблицыГрафика.Дата + Час(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60*60+Минута(СтрокаТаблицыГрафика.КонецРабочегоВремени)*60+Секунда(СтрокаТаблицыГрафика.КонецРабочегоВремени);
						Если НачалоРабочегоВремени <= ДатаТекущегоИнтервала	И ДатаТекущегоИнтервала < КонецРабочегоВремени Тогда
							// если нашли интервал
							Если СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.Работа Тогда
								ЭтоРабота = Истина;	
							ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ОбеденныйПерерыв Тогда
								ЭтоОбед = Истина;	
							ИначеЕсли СтрокаТаблицыГрафика.ВидИнтервала = Справочники.ВидыИнтервалов.ТехническийПерерыв Тогда
								ЭтоПерерыв = Истина;	
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Если ЭтоРабота Тогда
					ИнтервалВнеГрафика = Ложь;
				ИначеЕсли ЭтоОбед Тогда
					НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветОбеденныйПерерыв");
					ИнтервалВнеГрафика = Ложь;
				ИначеЕсли ЭтоПерерыв Тогда
					НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветТехническийПерерыв");
					ИнтервалВнеГрафика = Ложь;
				КонецЕсли;
				
				// если дата интервала не входит в график, то установим признак нерабочего времени
				Если ИнтервалВнеГрафика Тогда
					НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветВнеГрафика");
				КонецЕсли;
				
				Если НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободно") Тогда
					Если ИндексИнтервала%2 = 0 Тогда
						НоваяСтрокаОформления.ЦветФона = ЦветаРаскраски.Получить("ЦветСвободноЧетнаяСтрока");
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			//нижнее отчеркивание календаря
			Если ИндексИнтервала = ЧислоКолонокТаблицыРасшифровок-1 Тогда       
				НоваяСтрокаОформления.ЛинияРамкиНижняя = ЛинияРамкиСплошная;	
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура предназначена для формирования строки представления документа "Тест-драйв"
//
// Параметры:
//  ДокументСсылка 		- ссылка на документ,
//  ФорматСтроки: 
//		   Г - гос. номер (представление автомобиля), 
//		   Н - номер документа, 
//         Д - дата документа, 
//         З - заказчик (наименование), 
//         П - первые 10 символов причины обращения, 
//         М - мастер (наименование)
// 		   Любой другой символ считается разделителем
Функция ПолучитьПредставлениеДокумента(ДокументСсылка, ФорматСтроки = "") Экспорт
	// представление автомобиля
	стрПредставление = "";
	Если ПустаяСтрока(ФорматСтроки) Тогда
		стрПредставление = Строка(ДокументСсылка)
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ДокументСсылка) Тогда
		стрПредставление = "";
	КонецЕсли;
	
	ФорматДляРазбора = ФорматСтроки;
	
	Пока СтрДлина(ФорматДляРазбора) > 0 Цикл
		
		ТекущийСимвол = Лев(ФорматДляРазбора,1);
		Если СтрДлина(ФорматДляРазбора) > 1 Тогда
			ФорматДляРазбора = Сред(ФорматДляРазбора,2,СтрДлина(ФорматДляРазбора)-1);
		Иначе
			ФорматДляРазбора = "";
		КонецЕсли;
		
		Если ТекущийСимвол = "А" Тогда	
			Фрагмент = ПолучитьПредставлениеРабочегоМеста(ДокументСсылка.Автомобиль);	
			стрПредставление = стрПредставление + Фрагмент;
			
		ИначеЕсли ТекущийСимвол = "Н" Тогда	
			Фрагмент = СокрЛП(ДокументСсылка.Номер);	
			стрПредставление = стрПредставление + Фрагмент;
			
		ИначеЕсли ТекущийСимвол = "Д" Тогда	
			Фрагмент = Формат(ДокументСсылка.Дата,"ДЛФ=Д");	
			стрПредставление = стрПредставление + Фрагмент;
			
		ИначеЕсли ТекущийСимвол = "К" Тогда	
			Фрагмент = СокрЛП(ДокументСсылка.Контрагент.Наименование);	
			стрПредставление = стрПредставление + Фрагмент;
			
		ИначеЕсли ТекущийСимвол = "М" Тогда	
			Фрагмент = СокрЛП(ДокументСсылка.Маршрут);	
			стрПредставление = стрПредставление + Фрагмент;
			
		ИначеЕсли ТекущийСимвол = "С" И ЗначениеЗаполнено(ДокументСсылка.Менеджер) Тогда	
			Фрагмент = СокрЛП(ПолучитьПредставлениеРабочегоМеста(ДокументСсылка.Менеджер));	
			стрПредставление = стрПредставление + Фрагмент;
			
		Иначе
			стрПредставление = стрПредставление + ТекущийСимвол;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат СокрЛП(стрПредставление);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ОтступСлева = 1;
ОтступСверху = 1;

// Цвета раскраски ячеек календаря
ЦветаРаскраски = Новый Соответствие;
ЦветаРаскраски.Вставить("ЦветТекущаяДата", Новый Цвет(255,160,122));
ЦветаРаскраски.Вставить("ЦветСлужебнаяЯчейка", Новый Цвет(230,225,220));     
ЦветаРаскраски.Вставить("ЦветОбеденныйПерерыв", Новый Цвет(255,255,180));
ЦветаРаскраски.Вставить("ЦветТехническийПерерыв", Новый Цвет(230,200,255));
ЦветаРаскраски.Вставить("ЦветВыделенияДокумента",  Новый Цвет(211,217,239)); 
ЦветаРаскраски.Вставить("ЦветВыделенияРаботы",  Новый Цвет(83,106,194));  
ЦветаРаскраски.Вставить("ЦветСвободно", Новый Цвет(255,255,255)); 
ЦветаРаскраски.Вставить("ЦветСвободноЧетнаяСтрока", Новый Цвет(250,248,237));     
ЦветаРаскраски.Вставить("ЦветЗагрузкаМин", Новый Цвет(225,255,210));
ЦветаРаскраски.Вставить("ЦветЗагрузкаНорм", Новый Цвет(190,255,170));
ЦветаРаскраски.Вставить("ЦветЗагрузкаМакс", Новый Цвет(255,180,140));
ЦветаРаскраски.Вставить("ЦветВнеГрафика", Новый Цвет(235,230,215));
ЦветаРаскраски.Вставить("ЦветВнеПериода", Новый Цвет(210,210,210));

// Линии рамки ячеек
ЛинияРамкиВыделенияДокумента = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
ЛинияРамкиВыделенияРаботы = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
ЛинияРамкиВнутренняя = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
ЛинияРамкиСплошная = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
ЛинияРамкиТочечная = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Точечная,1);
ЛинияРамкиДвойная = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Двойная,1);

ПредставленияАвтомобилей = Новый Соответствие;


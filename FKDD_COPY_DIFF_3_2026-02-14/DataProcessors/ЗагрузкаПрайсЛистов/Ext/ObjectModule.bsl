// Модуль справочника ПрайсЛистыКонтрагентов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем Рарус_Компонента;
Перем СопоставленныеПроизводители;
Перем СтатистикаЗагрузки;
Перем МассивДанных;
Перем ТЗПрайсЛиста;
Перем РегистрПрайсЛистов;

Перем СоответствиеВалют;
Перем СоответствиеЦен;
Перем СоответствиеКоличеств;

Перем ДатаЗаписиПорцииДанных;
Перем МаксПорцияЗаписи;
Перем СтрокаЗагружаемойСтраницы;

Перем ЗаголовкиВФайле;
Перем СмещениеКолонокПоГоризонтали;
Перем СмещениеКолонокПоВертикали;
Перем ТипФайла;

Процедура Состояние_(СтрокаСообщения)
	#Если Клиент Тогда
		Состояние(СтрокаСообщения);
	#КонецЕсли
КонецПроцедуры //Состояние_()

Процедура Предупреждение_(СтрокаСообщения, Таймаут=0, Заголовок="")
	Если ЗначениеЗаполнено(СтрокаСообщения) Тогда
		#Если Клиент Тогда
			Предупреждение(СтрокаСообщения, Таймаут, Заголовок);
		#Иначе
			ЗаписьЖурналаРегистрации("Ошибка при загрузке прайс-листа", УровеньЖурналаРегистрации.Ошибка,, СтрокаСообщения);
		#КонецЕсли
	КонецЕсли;
КонецПроцедуры //Предупреждение_()

Функция Вопрос_(ТекстВопроса, Кнопки="ДаНетОтмена", Таймаут=0, КнопкаПоУмолчанию="Отмена", Заголовок="Вопрос", КнопкаТаймаута=Неопределено)
	#Если Клиент Тогда
		Возврат "" + Вопрос(ТекстВопроса, РежимДиалогаВопрос[Кнопки], Таймаут);
	#КонецЕсли
	Возврат Неопределено;
КонецФункции //Вопрос_()

Процедура ОбработкаПрерыванияПользователя_()
	#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
	#КонецЕсли
КонецПроцедуры //ОбработкаПрерыванияПользователя_()

Функция MD5(КодируемаяСтрока)
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	
	Хеш.Добавить(КодируемаяСтрока);
	ХешСумма = Строка(Хеш.ХешСумма);
	
	Возврат СтрЗаменить(ХешСумма," ","");
КонецФункции //MD5()

Процедура РазобратьСрокПоставки(Знач ЗначениеПоля, лСрокПоставки, лСрокПоставкиМинимальный, лСрокПоставкиМаксимальный) Экспорт
	
	лСрокПоставки = ""; // Это то как написано в прайс-листе
	лСрокПоставкиМинимальный = Неопределено;
	лСрокПоставкиМаксимальный = Неопределено;
	
	Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
		Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
			лСрокПоставкиМинимальный = ЗначениеПоля;
			лСрокПоставкиМаксимальный = ЗначениеПоля;
			
		Иначе
			ВремПоле = СокрЛП(ВРег(ЗначениеПоля));
			Попытка
				лСрокПоставкиМинимальный = Число(ЗначениеПоля);
				лСрокПоставкиМаксимальный = лСрокПоставкиМинимальный;
			Исключение
				// На заказ, на складе, в наличии
				// 30-45 дней
				// 2-3 недели до Москвы 
				// поставка 7-8 недель
				// 5-6 weeks from yr PO
				// 90 Days
				// Not Available
				Если Найти(ВремПоле,"СКЛ") > 0 // На складе
					ИЛИ Найти(ВремПоле,"НАЛ") > 0 // в наличии
					ИЛИ Найти(ВремПоле,"STOCK") > 0 // in stock, on stock
					ИЛИ Найти(ВремПоле,"STORE") > 0 // store
					ИЛИ Найти(ВремПоле,"WAR") > 0 // warehouse
					Тогда
					лСрокПоставкиМинимальный = 0;
					лСрокПоставкиМаксимальный = 0;
					лСрокПоставки = СокрЛП(ЗначениеПоля); // Сохраним как было в прайсе
				Иначе
					ВремПоле = СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(ВремПоле,"ДО","-"),"ОТ",""),"ПОСТАВКА",""),"НА ЗАКАЗ","");
					ВремПоле = СтрЗаменить(ВремПоле," ","");
					Коэффициент = 1;
					Поз_= 0 ;
					Если Найти(ВремПоле,"МЕС") > 0 Тогда // месяцев
						Коэффициент = 30;
						Поз_ = Найти(ВремПоле,"М");
					ИначеЕсли Найти(ВремПоле,"MONTH") > 0 Тогда // месяцев
						Коэффициент = 30;
						Поз_ = Найти(ВремПоле,"M");
					ИначеЕсли Найти(ВремПоле,"НЕД") > 0 Тогда // Недель, неделя
						Коэффициент = 7;
						Поз_ = Найти(ВремПоле,"Н");
					ИначеЕсли Найти(ВремПоле,"WEEK") > 0 Тогда // week
						Коэффициент = 7;
						Поз_ = Найти(ВремПоле,"W");
					ИначеЕсли Найти(ВремПоле,"Д") > 0 Тогда // Дней, дн., д.
						Поз_ = Найти(ВремПоле,"Д");
					ИначеЕсли Найти(ВремПоле,"D") > 0 Тогда // Days
						Поз_ = Найти(ВремПоле,"D");
					КонецЕсли;
					Если Поз_>0 Тогда
						ВремПоле = ЛЕВ(ВремПоле,Поз_-1);
					КонецЕсли;
					Поз_ = Найти(ВремПоле,"-");
					Если Поз_ = 0 Тогда
						Поз_ = Найти(ВремПоле,"/");
					КонецЕсли;
					Если Поз_ > 0 Тогда
						Минимальный = ЛЕВ(ВремПоле,Поз_-1);
						Максимальный = СРЕД(ВремПоле,Поз_+1);
					Иначе
						Минимальный = ВремПоле;
						Максимальный = ВремПоле;
					КонецЕсли;
					Попытка
						лСрокПоставкиМинимальный = Число(Минимальный)*Коэффициент;
						лСрокПоставкиМаксимальный = Число(Максимальный)*Коэффициент;
					Исключение
						лСрокПоставкиМинимальный = Коэффициент;
						лСрокПоставкиМаксимальный = Коэффициент;
						лСрокПоставки = СокрЛП(ЗначениеПоля); // Сохраним как было в прайсе
					КонецПопытки; 
				КонецЕсли;
			КонецПопытки;
		КонецЕсли;
	Иначе
		
	КонецЕсли;
	
	
	Если лСрокПоставкиМинимальный = Неопределено Тогда
		// Здесь уже в нужных секундах или сутках?
		лСрокПоставкиМинимальный	= ПрайсЛистКонтрагента.СрокПоставкиМинимальный;
		лСрокПоставкиМаксимальный	= ПрайсЛистКонтрагента.СрокПоставкиМаксимальный;
	Иначе
		// а в прайс-листах у нас все хранится в днях
		лСрокПоставкиМинимальный	= лСрокПоставкиМинимальный * (24*60*60);
		лСрокПоставкиМаксимальный	= лСрокПоставкиМаксимальный * (24*60*60);
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(лСрокПоставки) Тогда
		// Попробуем присвоить как полагается, а не как в прайсе
		лСрокПоставки = Справочники.ПрайсЛистыКонтрагентов.ПредставлениеСрокаПоставкиИнтервал(лСрокПоставкиМинимальный, лСрокПоставкиМаксимальный);
	КонецЕсли;
	
КонецПроцедуры //РазобратьСрокПоставки()

Функция РазобратьВалюту(Знач ЗначениеПоля="", ВалютаПрайсЛиста = Неопределено) Экспорт
	
	//Проверим соответствие. Возможно в нем уже есть валюта
	ЭлементСоответствия = СоответствиеВалют.Получить(ЗначениеПоля);
	Если ЭлементСоответствия <> Неопределено Тогда
		 Возврат ЭлементСоответствия;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ЗначениеПоля) Тогда
		
		Если ВалютаПрайсЛиста = Неопределено Тогда
			ВалютаПрайсЛиста = ПрайсЛистКонтрагента.Валюта;
		КонецЕсли;
		Если ВалютаПрайсЛиста.Пустая() Тогда
			ВалютаПрайсЛиста = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		КонецЕсли;
		
		СоответствиеВалют.Вставить(ЗначениеПоля, ВалютаПрайсЛиста);
		Возврат ВалютаПрайсЛиста;
		
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
		КодВалюты = Формат(ЗначениеПоля, "ЧГ=0");
		
	Иначе
		КодВалюты = ВРег(СокрЛП(ЗначениеПоля));
		
		Валюта = Справочники.Валюты.НайтиПоКоду(СокрЛП(КодВалюты));
		Если Валюта.Пустая() Тогда
			Валюта = Справочники.Валюты.НайтиПоНаименованию(КодВалюты);
		КонецЕсли;
		Если Не Валюта.Пустая() Тогда
			СоответствиеВалют.Вставить(ЗначениеПоля, Валюта.Ссылка);
			Возврат Валюта.Ссылка;
		КонецЕсли;
		
		// Упрощенно возможные варианты
		// Доллар
		Если Найти(КодВалюты, "$") > 0
			ИЛИ ЛЕВ(КодВалюты, 1) = "U" // USD
			ИЛИ ЛЕВ(КодВалюты, 1) = "Д" // Доллар
			Тогда
			КодВалюты = "840";
			
		// Евро
		ИначеЕсли Найти(КодВалюты, "₤") > 0
			ИЛИ ЛЕВ(КодВалюты, 1) = "E" // EURO
			ИЛИ ЛЕВ(КодВалюты, 1) = "Е" // ЕВРО
			Тогда
			КодВалюты = "978";
			
		// Иена
		ИначеЕсли Найти(КодВалюты, "¥") > 0
			ИЛИ ЛЕВ(КодВалюты, 1) = "J" // JPY
			ИЛИ ЛЕВ(КодВалюты, 1) = "И" // ИЕНА
			ИЛИ ЛЕВ(КодВалюты, 1) = "Й" // ЙЕНА
			Тогда
			КодВалюты = "392";
			
		ИначеЕсли ЛЕВ(КодВалюты, 1) = "R" // RUR, RUB
			ИЛИ ЛЕВ(КодВалюты, 1) = "Р" // РУБ
			Тогда
			КодВалюты = "643";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Валюта = Справочники.Валюты.НайтиПоКоду(СокрЛП(КодВалюты));
	Если Валюта.Пустая() Тогда
		Валюта = Справочники.Валюты.НайтиПоНаименованию(КодВалюты);
	КонецЕсли;
	
	// Даже если не смогли распознать, поставим пустую валюту
	СоответствиеВалют.Вставить(ЗначениеПоля, Валюта);
	Возврат Валюта;
	
КонецФункции

Функция РазобратьЦену(Знач ЗначениеПоля, Валюта=Неопределено, ВалютаВКолонкеЦена=Ложь) Экспорт
	
	//Проверим соответствие. Возможно в нем уже есть
	ЭлементСоответствия = СоответствиеЦен.Получить(ЗначениеПоля);
	Если ЭлементСоответствия <> Неопределено Тогда
		Если ВалютаВКолонкеЦена Тогда
			Валюта = ЭлементСоответствия.Валюта;
		КонецЕсли;
		Возврат ЭлементСоответствия.Цена;
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
		Если ВалютаВКолонкеЦена Тогда
			Валюта = ПрайсЛистКонтрагента.Валюта;
		КонецЕсли;
		СтруктураЦены = Новый Структура("Цена, Валюта", ЗначениеПоля, Валюта);
		СоответствиеЦен.Вставить(ЗначениеПоля, СтруктураЦены);
		Возврат ЗначениеПоля;
	КонецЕсли;
	
	ЗначениеПоляОриг = ЗначениеПоля;
	
	// $20.1  $20,1  20,1 руб. 20.1 руб.
	
	ЗначениеПоля = СтрЗаменить(ЗначениеПоля, " ", "");
	ЗначениеПоля = СтрЗаменить(ЗначениеПоля, Символы.НПП, "");
	ЗначениеПоля = СтрЗаменить(ЗначениеПоля, Символы.Таб, "");
	ЗначениеПоля = СтрЗаменить(ЗначениеПоля, "'", "");
	
	Попытка
		
		ЗначениеПоля = СтрЗаменить(ЗначениеПоля, ".", ",");
		
		Цена = Число(ЗначениеПоля);
		Если ВалютаВКолонкеЦена Тогда
			Валюта = ПрайсЛистКонтрагента.Валюта;
		КонецЕсли;
		
		СтруктураЦены = Новый Структура("Цена, Валюта", Цена, Валюта);
		СоответствиеЦен.Вставить(ЗначениеПоляОриг, СтруктураЦены);
		
		Возврат Цена;
		
	Исключение
	КонецПопытки; 
	
	НоваяЦена = "";
	НоваяВалюта = "";
	
	Если Найти("0123456789", ЛЕВ(ЗначениеПоля, 1)) = 0 Тогда
		// Сначала Валюта
		НачаласьЦена = Ложь;
		
		Для Сч = 1 По СтрДлина(ЗначениеПоля) Цикл
			Если Найти(".,", Сред(ЗначениеПоля, Сч, 1)) > 0 Тогда
				Если НачаласьЦена Тогда
					Если Найти(НоваяЦена, ",") = 0 Тогда
						НоваяЦена = НоваяЦена + ",";
					КонецЕсли;
				Иначе
					Если Найти(НоваяВалюта, ".") = 0 Тогда
						НоваяВалюта = НоваяВалюта + ".";
					КонецЕсли;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			Если Найти("0123456789", Сред(ЗначениеПоля, Сч, 1)) > 0 Тогда
				Если НЕ НачаласьЦена Тогда
					НачаласьЦена = Истина;
				КонецЕсли;
				НоваяЦена = НоваяЦена + Сред(ЗначениеПоля, Сч, 1);
			Иначе
				Если НачаласьЦена Тогда
					Продолжить;
				КонецЕсли;
				НоваяВалюта = НоваяВалюта + Сред(ЗначениеПоля, Сч, 1);
			КонецЕсли;
			
		КонецЦикла;
	Иначе
		// Сначала Цена
		НачаласьВалюта = Ложь;
		Для Сч = 1 По СтрДлина(ЗначениеПоля) Цикл
			Если Найти(".,", Сред(ЗначениеПоля, Сч, 1)) > 0 Тогда
				Если НачаласьВалюта Тогда
					Если Найти(НоваяВалюта, ".") = 0 Тогда
						НоваяВалюта = НоваяВалюта + ".";
					КонецЕсли;
				Иначе
					Если Найти(НоваяЦена, ",") = 0 Тогда
						НоваяЦена = НоваяЦена + ",";
					КонецЕсли;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			Если Найти("0123456789", Сред(ЗначениеПоля, Сч, 1)) > 0 Тогда
				// Сливаем все цифры в НоваяЦена, если не началась Валюта
				Если НачаласьВалюта Тогда
					Продолжить;
				КонецЕсли;
				НоваяЦена = НоваяЦена + Сред(ЗначениеПоля, Сч, 1);
			Иначе
				// Сливаем все буквы в НоваяВалюта
				Если Не НачаласьВалюта Тогда
					НачаласьВалюта = Истина;
				КонецЕсли;
				НоваяВалюта = НоваяВалюта + Сред(ЗначениеПоля, Сч, 1);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		Цена = Число(НоваяЦена);
	Исключение
		Цена = 0;
	КонецПопытки;
	
	Если ВалютаВКолонкеЦена Тогда
		Если обЗначениеНеЗаполнено(НоваяВалюта) Тогда
			Валюта = ПрайсЛистКонтрагента.Валюта;
		Иначе
			Валюта = РазобратьВалюту(НоваяВалюта);
		КонецЕсли;
	КонецЕсли;
	
	СтруктураЦены = Новый Структура("Цена, Валюта", Цена, Валюта);
	СоответствиеЦен.Вставить(ЗначениеПоляОриг, СтруктураЦены);
	
	Возврат Цена;
	
КонецФункции

Функция ВыделитьЧислоИзСтроки(Стр)
	СтрокаЦифр = "";
	ПерваяЦифраНайдена = Ложь;
	//Стр = СтрЗаменить(Стр, ".", ",");
	Для Сч = 1 По СтрДлина(Стр) Цикл
		Если Найти("1234567890,", Сред(Стр, Сч, 1)) > 0 Тогда
			ПерваяЦифраНайдена = Истина;
			СтрокаЦифр = СтрокаЦифр + Сред(Стр, Сч, 1);
		Иначе
			Если ПерваяЦифраНайдена Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(СтрокаЦифр) Тогда
		Количество = Число(СтрокаЦифр);
		Возврат ?(Количество = 0, Ложь, Количество);
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

Функция РазобратьКоличество(Знач ЗначениеПоля) Экспорт
	//Проверим соответствие. Возможно в нем уже есть
	ЭлементСоответствия = СоответствиеКоличеств.Получить(ЗначениеПоля);
	Если ЭлементСоответствия <> Неопределено Тогда
		 Возврат ЭлементСоответствия;
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
		Если ЗначениеПоля = 0 Тогда
			СоответствиеКоличеств.Вставить(ЗначениеПоля, Ложь);
		Иначе
			СоответствиеКоличеств.Вставить(ЗначениеПоля, ЗначениеПоля);
		КонецЕсли;
	Иначе
		ВРегЗначениеПоля = ВРег(СокрЛП(ЗначениеПоля));
		Если обЗначениеНеЗаполнено(ЗначениеПоля) ИЛИ Найти(ВРегЗначениеПоля, "НЕТ") > 0 ИЛИ Найти(ВРегЗначениеПоля, "NO") > 0 Тогда
			// Считаем, что нет в наличии, если ничего не указано
			СоответствиеКоличеств.Вставить(ЗначениеПоля, Ложь);
		ИначеЕсли Найти("*+VЕД><YОЗБМВСS123456789", ЛЕВ(ВРегЗначениеПоля, 1)) > 0 Тогда
			//+,v,Есть,Да,Yes,>1,<5,Ост,Завались,Больше,Много,В наличии,Со склада,Склад,stock или цифра
			Попытка
				СоответствиеКоличеств.Вставить(ЗначениеПоля, Число(ЗначениеПоля));
			Исключение
				СоответствиеКоличеств.Вставить(ЗначениеПоля, ВыделитьЧислоИзСтроки(ЗначениеПоля));
			КонецПопытки;
		ИначеЕсли ЛЕВ(ВРегЗначениеПоля, 2) = "НА" Тогда
			// На складе
			СоответствиеКоличеств.Вставить(ЗначениеПоля, Истина);
		Иначе
			// 0, - и т.д.
			СоответствиеКоличеств.Вставить(ЗначениеПоля, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СоответствиеКоличеств[ЗначениеПоля];
	
КонецФункции //РазобратьКоличество()

Процедура УстановитьТипФайла(СтрокаПодключения)
	ТипФайла = "";
	ТипФайла = СтрокаПодключения;
	ПозицияТочки = Найти(ТипФайла, ".");
	Пока ПозицияТочки>0 Цикл
		ТипФайла = Сред(ТипФайла, ПозицияТочки+1);
		ПозицияТочки = Найти(ТипФайла, ".");
	КонецЦикла;
	ТипФайла = "."+ВРег(ТипФайла);
КонецПроцедуры //УстановитьТипФайла()

Процедура ПерекодироватьМассивДанных(МассивДанныхДляПерекодировки)
	Для СчКолонок = 0 По МассивДанныхДляПерекодировки.ВГраница() Цикл
		Для СчСтрок = 0 По МассивДанныхДляПерекодировки[СчКолонок].ВГраница() Цикл
			Если ТипЗнч(МассивДанныхДляПерекодировки[СчКолонок][СчСтрок]) = Тип("Строка") Тогда
				МассивДанныхДляПерекодировки[СчКолонок][СчСтрок] = Рарус_Компонента.ПерекодироватьСтроку(СокрЛП(МассивДанныхДляПерекодировки[СчКолонок][СчСтрок]), ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ConnectionClose(Connection)
	Попытка
		Connection.Close();
	Исключение
	КонецПопытки;
	Connection = Неопределено;
КонецПроцедуры

Функция EXCEL_УстановитьСвязь_ADO(ИмяФайла, ВернутьВсеЛисты=Ложь, КоличествоСтрок=0)
	
	Состояние_("Идет установка связи, ждите.....");
	
	//HDR = ?(HDR, "YES", "NO");
	HDR = "NO";
	
	ExtendedProperties = "Excel 8.0"; // xls
	Если ТипФайла = ".XLSB" Тогда
		ExtendedProperties = "Excel 12.0"; // xlsb
	ИначеЕсли ТипФайла = ".XLSX" Тогда
		ExtendedProperties = "Excel 12.0 Xml"; // xlsx
	ИначеЕсли ТипФайла = ".XLSM" Тогда
		ExtendedProperties = "Excel 12.0 Macro"; // xlsm
	КонецЕсли;
	
	Попытка
		//Excel 2007 - 2010
		Connection = Новый COMОбъект("ADODB.Connection");
		Connection.ConnectionString = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source=" + ИмяФайла + ";Extended Properties="""+ExtendedProperties+"; HDR="+HDR+";"";Persist Security Info=False;";
		Connection.Open();
	Исключение
		СтатистикаЗагрузки.Примечание = СтатистикаЗагрузки.Примечание + Символы.ПС + "Microsoft.ACE.OLEDB.12.0:" + Символы.ПС + ОписаниеОшибки();
		Connection = Неопределено;
	КонецПопытки;
	
	Если Connection = Неопределено Тогда
		Попытка
			//Excel 2000 - 2003
			Connection = Новый COMОбъект("ADODB.Connection");
			Connection.ConnectionString = "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" + ИмяФайла + "; Extended Properties="""+ExtendedProperties+"; HDR="+HDR+"; IMEX=1"";";
			Connection.Open();
		Исключение
			СтатистикаЗагрузки.Примечание = "Microsoft.Jet.OLEDB.4.0:" + Символы.ПС + ОписаниеОшибки();
			Connection = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Если Connection = Неопределено Тогда
		Попытка
			//Excel
			Connection = Новый COMОбъект("ADODB.Connection");
			Connection.ConnectionString = "Driver={Microsoft Excel Driver (*.xls, *.xlsx, *.xlsm, *.xlsb)};Dbq=" + ИмяФайла + ";";
			Connection.Open();
		Исключение
			СтатистикаЗагрузки.Примечание = СтатистикаЗагрузки.Примечание + Символы.ПС + "{Microsoft Excel Driver (*.xls, *.xlsx, *.xlsm, *.xlsb)}:" + Символы.ПС + ОписаниеОшибки();
			Connection = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Если Connection = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ADOXCatalog = Новый COMОбъект("ADOX.Catalog");
	ADOXCatalog.ActiveConnection = Connection;
	КоличествоЛистов = ADOXCatalog.Tables.Count;
	
	Результат = Новый Соответствие;
	ВсеЛисты = Новый Массив;
	МассивЛистов = Новый Массив;
	
	Для е = 1 по КоличествоЛистов Цикл
		ИмяЛиста = ADOXCatalog.Tables(е - 1).Name;
		Если Прав(ИмяЛиста, 1) = "$" Тогда
			ИмяЛиста = Лев(ИмяЛиста, СтрДлина(ИмяЛиста)-1);
			ВсеЛисты.Добавить(ИмяЛиста);
			Если ВернутьВсеЛисты ИЛИ ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.НайтиСтроки(Новый Структура("ИмяЛиста, Использовать", ИмяЛиста, Истина)).Количество() > 0 Тогда
				МассивЛистов.Добавить(ИмяЛиста);
			КонецЕсли;
		КонецЕсли;
		Если Прав(ИмяЛиста, 2) = "$'" Тогда
			ИмяЛиста = Сред(Лев(ИмяЛиста, СтрДлина(ИмяЛиста)-2), 2);
			ВсеЛисты.Добавить(ИмяЛиста);
			Если ВернутьВсеЛисты ИЛИ ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.НайтиСтроки(Новый Структура("ИмяЛиста, Использовать", ИмяЛиста, Истина)).Количество() > 0 Тогда
				МассивЛистов.Добавить(ИмяЛиста);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ВсеЛисты.Количество() > 0 И МассивЛистов.Количество() = 0 Тогда
		// Хотя бы один лист добавим, так как может быть ситуация, когда каждый раз разные названия листов в файле
		МассивЛистов.Добавить(ВсеЛисты[0]);
	КонецЕсли;
	
	ADOXCatalog = Неопределено;
	
	Если МассивЛистов.Количество() = 0 Тогда
		СтатистикаЗагрузки.Примечание = "В файле: " + ИмяФайла + " нет данных";
		ConnectionClose(Connection);
		Возврат Результат;
	КонецЕсли;
	
	ТребуетсяПерекодировка = Рарус_Компонента <> Неопределено И ЗначениеЗаполнено(ПрайсЛистКонтрагента.КодоваяСтраница) И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251";
	
	Для Каждого ИмяЛиста Из МассивЛистов Цикл
		
		МассивДанныхЛиста = Новый Массив;
		
		ТекстЗапроса = "SELECT"+?(КоличествоСтрок>0, " TOP "+Формат(КоличествоСтрок, "ЧГ=0"), "")+" * FROM [" + ИмяЛиста + "$]";
		
		RecordSet = Новый COMОбъект("ADODB.Recordset");
		Recordset.CursorType = 3;
		
		Попытка
			RecordSet.Open(ТекстЗапроса, Connection);
		Исключение
			Сообщить("Ошибка чтения листа [" + ИмяЛиста + "]: 
							|"+ОписаниеОшибки());
			Продолжить;
		КонецПопытки;
		
		//МассивДанныхЛиста = ПолучитьМассивMoveNext(RecordSet, ТребуетсяПерекодировка);
		МассивДанныхЛиста = ПолучитьМассивGetRows(RecordSet, ТребуетсяПерекодировка);
		
		Результат.Вставить(ИмяЛиста, МассивДанныхЛиста);
		
		RecordSet.Close();
		
	КонецЦикла;
	
	RecordSet = Неопределено;
	
	ConnectionClose(Connection);
	
	Состояние_("Данные из Excel получены");
	
	Возврат Результат;
	
КонецФункции //EXCEL_УстановитьСвязь_ADO()

Функция ПолучитьМассивGetRows(RecordSet, ТребуетсяПерекодировка)
	
	Если RecordSet.EOF() Тогда
		RecordSet.MoveFirst();
	КонецЕсли;
	
	КоличествоКолонок = RecordSet.Fields.Count;
	
	МассивДанныхЛиста = Новый Массив;
	Для к = 0 По КоличествоКолонок - 1 Цикл
		МассивДанныхЛиста.Добавить(Новый Массив);
	КонецЦикла;
	
	ПолныйМассивДанных = RecordSet.GetRows().Выгрузить();
	// Транспонируем массив данных
	Для Каждого МассивМассивов Из ПолныйМассивДанных Цикл
		Для СчКолонок = 0 По КоличествоКолонок-1 Цикл
			ЗначениеПоля = МассивМассивов[СчКолонок];
			Если ТребуетсяПерекодировка И ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
				ЗначениеПоля = СокрЛП(ЗначениеПоля);
				ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
			КонецЕсли;
			МассивДанныхЛиста[СчКолонок].Добавить(ЗначениеПоля);
		КонецЦикла; 
	КонецЦикла;
	
	Возврат МассивДанныхЛиста;
	
КонецФункции

Функция ПолучитьМассивMoveNext(RecordSet, ТребуетсяПерекодировка)
	
	Если RecordSet.EOF() Тогда
		RecordSet.MoveFirst();
	КонецЕсли;
	
	КоличествоКолонок = RecordSet.Fields.Count;
	
	МассивДанныхЛиста = Новый Массив;
	Для к = 0 По КоличествоКолонок - 1 Цикл
		МассивДанныхЛиста.Добавить(Новый Массив);
	КонецЦикла;
	
	Пока RecordSet.EOF() = 0 Цикл
		СчКолонок = 0;
		Для каждого Поле Из RecordSet.Fields Цикл
			ЗначениеПоля = Поле.Value;
			Если ТребуетсяПерекодировка И ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
				ЗначениеПоля = СокрЛП(ЗначениеПоля);
				ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
			КонецЕсли;
			МассивДанныхЛиста[СчКолонок].Добавить(ЗначениеПоля);
			СчКолонок = СчКолонок + 1;
		КонецЦикла; 
		RecordSet.MoveNext();
	КонецЦикла;
	
	Возврат МассивДанныхЛиста;
	
КонецФункции

Функция EXCEL_УстановитьСвязь_Application(ИмяФайла, ВернутьВсеЛисты=Ложь, КоличествоСтрок=0)
	
	Состояние_("Идет установка связи, ждите.....");
	
	Попытка
		BaseOLE = Новый COMОбъект("Excel.Application");
		ExcelФайл = BaseOLE.WorkBooks.Open(СокрЛП(ИмяФайла),, Истина,,,,,,,,,, Ложь);
		
		МассивЛистов = Новый Массив;
		КоличествоЛистов = ExcelФайл.Sheets.Count;
		Для е = 1 по КоличествоЛистов Цикл
			Если ВернутьВсеЛисты ИЛИ ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.НайтиСтроки(Новый Структура("ИмяЛиста, Использовать", ExcelФайл.Sheets(е).Name, Истина)).Количество() > 0 Тогда
				МассивЛистов.Добавить(ExcelФайл.Sheets(е));
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоЛистов > 0 И МассивЛистов.Количество() = 0 Тогда
			// Хотя бы один лист добавим, так как может быть ситуация, когда каждый раз разные названия листов в файле
			МассивЛистов.Добавить(ExcelФайл.Sheets(1));
		КонецЕсли;
		
		Состояние_("Соединение с Excel установлено");
	Исключение
		СтатистикаЗагрузки.Примечание = "Ошибка связи с Microsoft Excel:" + Символы.ПС + ОписаниеОшибки();
		Возврат Неопределено;
	КонецПопытки;
	
	// Сначала нужно с главной страницы загрузить!
	// Там проверим заголовки и установим смещение
	
	ТребуетсяПерекодировка = Рарус_Компонента <> Неопределено И ЗначениеЗаполнено(ПрайсЛистКонтрагента.КодоваяСтраница) И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251";
	
	Результат = Новый Соответствие;
	
	Для Каждого Sheet Из МассивЛистов Цикл
		
		Если Sheet.Name = ПрайсЛистКонтрагента.ИмяТаблицы Тогда
			
			// Весь лист (Диапазон) выгружаем в массив
			Состояние_("Получение данных со страницы [" + Sheet.Name + "]");
			
			Диапазон = Sheet.UsedRange;
			
			ВсегоКолонок = Диапазон.Columns.Count;
			ВсегоСтрок = Диапазон.Rows.Count;
			
			Если КоличествоСтрок > 0 И ВсегоСтрок > КоличествоСтрок Тогда
				// Требуется только часть данных
				МассивДанныхЛиста = Новый Массив;
				Для СчКолонок = 1 По ВсегоКолонок Цикл
					МассивДанныхЛиста.Добавить(Новый Массив);
					Для СчСтрок = 1 По КоличествоСтрок Цикл
						МассивДанныхЛиста[СчКолонок-1].Добавить(Диапазон.Item(СчСтрок, СчКолонок).Value);
					КонецЦикла;
				КонецЦикла;
			Иначе
				Попытка
					МассивДанныхЛиста = Диапазон.Value.Выгрузить();
				Исключение
					Сообщить("Ошибка получения данных со страницы [" + Sheet.Name + "] !");
					Продолжить;
				КонецПопытки;
			КонецЕсли;
			
			Если ТребуетсяПерекодировка Тогда
				ПерекодироватьМассивДанных(МассивДанныхЛиста);
			КонецЕсли;
			
			Результат.Вставить(Sheet.Name, МассивДанныхЛиста);
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Sheet Из МассивЛистов Цикл
		
		Если Sheet.Name = ПрайсЛистКонтрагента.ИмяТаблицы Тогда
			Продолжить;
		КонецЕсли;
		
		Состояние_("Получение данных со страницы [" + Sheet.Name + "]");
		
		Диапазон = Sheet.UsedRange;
		ВсегоКолонок = Диапазон.Columns.Count;
		ВсегоСтрок = Диапазон.Rows.Count;
		
		Если КоличествоСтрок > 0 И ВсегоСтрок > КоличествоСтрок Тогда
			// Требуется только часть данных
			МассивДанныхЛиста = Новый Массив;
			Для СчКолонок = 1 По ВсегоКолонок Цикл
				МассивДанныхЛиста.Добавить(Новый Массив);
				Для СчСтрок = 1 По КоличествоСтрок Цикл
					МассивДанныхЛиста[СчКолонок-1].Добавить(Диапазон.Item(СчСтрок, СчКолонок).Value);
				КонецЦикла;
			КонецЦикла;
		Иначе
			Попытка
				МассивДанныхЛиста = Диапазон.Value.Выгрузить();
			Исключение
				Сообщить("Ошибка получения данных со страницы [" + Sheet.Name + "] !");
				Продолжить;
			КонецПопытки;
		КонецЕсли;
		
		Если ТребуетсяПерекодировка Тогда
			ПерекодироватьМассивДанных(МассивДанныхЛиста);
		КонецЕсли;
		
		Результат.Вставить(Sheet.Name, МассивДанныхЛиста);
		
	КонецЦикла;
	
	Попытка
		BaseOLE.DisplayAlerts = 0;
		ExcelФайл.Close();
		BaseOLE.DisplayAlerts = 1;
		BaseOLE.Quit(); 
		BaseOLE = Неопределено;
	Исключение
		СтатистикаЗагрузки.Примечание = ?(ЗначениеЗаполнено(СтатистикаЗагрузки.Примечание), СтатистикаЗагрузки.Примечание + Символы.ПС, "") + "Ошибка: " + ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции //EXCEL_УстановитьСвязь_Application()

// процедура разбора строки символов на массив символов
//
// Параметры:
// СтрокаДляРазбора  - Строка - Строка для разбора
// СтрокаПодключения - Строка - Адрес загружаемого файла
//
// Возвращаемое значение:
// Массив - Массив символов строки
//
Функция РазобратьСтрокуTXT(Знач СтрокаДляРазбора, Знач СтрокаПодключения, Знач Разд = Неопределено)
	
	МассивСтрокиФайлаПрайсЛиста = Новый Массив();
	
	Если ВРег(Прав(СтрокаПодключения, 4)) = ".CSV" И ?(Разд = Неопределено, ПрайсЛистКонтрагента.Разделитель, Разд) = "T" Тогда
		Разделитель = Символы.Таб;
	Иначе
		Разделитель = ?(Разд = Неопределено, ПрайсЛистКонтрагента.Разделитель, Разд);
	КонецЕсли;
	
	Пока СтрДлина(СтрокаДляРазбора) > 0 Цикл
		ПозицияРазделителя = Найти(СтрокаДляРазбора,Разделитель);
		Если ПозицияРазделителя = 0 Тогда
			ПозицияРазделителя = СтрДлина(СтрокаДляРазбора)+1;
		КонецЕсли;
		СтрокаПараметр = Лев(СтрокаДляРазбора,ПозицияРазделителя-1);
		СтрокаДляРазбора = Сред(СтрокаДляРазбора,ПозицияРазделителя+1);
		// Остатки от формата Csv
		Если Лев(СтрокаПараметр, 1) = """" Тогда
			СтрокаПараметр = Сред(СтрокаПараметр, 2);
		КонецЕсли;
		Если Прав(СтрокаПараметр, 1) = """" Тогда
			СтрокаПараметр = Лев(СтрокаПараметр, СтрДлина(СтрокаПараметр)-1);
		КонецЕсли;
		МассивСтрокиФайлаПрайсЛиста.Добавить(СокрЛП(СтрокаПараметр));
	КонецЦикла; 
	
	Возврат МассивСтрокиФайлаПрайсЛиста;
	
КонецФункции // РазобратьСтрокуTXT()


Процедура ЗаполнитьСоответствиеДанныхXLS(СтрокаПодключения, СоответствиеОбразец, КоличествоСтрок)
	
	// Сначала самый быстрый метод
	СоответствиеОбразец = EXCEL_УстановитьСвязь_Application(СтрокаПодключения, Истина, КоличествоСтрок);
	
	Если СоответствиеОбразец = Неопределено тогда
		// Здесь помедленнее
		СоответствиеОбразец = EXCEL_УстановитьСвязь_ADO(СтрокаПодключения, Истина, КоличествоСтрок);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСоответствиеДанныхXLS()

Процедура ЗаполнитьСоответствиеДанныхODS(СтрокаПодключения, СоответствиеОбразец, КоличествоСтрок)
	
	СоответствиеОбразец = ПрочитатьОбразецOpenOffice(СтрокаПодключения, КоличествоСтрок);
	
КонецПроцедуры //ЗаполнитьМассивДанныхODS()

Функция ПрочитатьОбразецOpenOffice(СтрокаПодключения, КоличествоСтрок)
	
	ЗаголовкиВФайле = Новый СписокЗначений;
	
	// Получим объект листа файла
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		Params = MakePropertyValue(ServiceManager, "Hidden", Истина);
		Args = Новый COMSafeArray("VT_DISPATCH", 1);
		Args.SetValue(0, Params);
		OODoc = Desktop.loadComponentFromURL(ПреобразоватьВURL(СтрокаПодключения), "_blank", 0, Args);
		Sheets = OODoc.getSheets();
	Исключение
		СтатистикаЗагрузки.Примечание = "На компьютере должен быть установлен Open Office версии не ниже 2.0!";
		ServiceManager = Неопределено;
		Возврат Неопределено;
	КонецПопытки;
	
	Если ТипЗнч(Sheets) <> Тип("COMОбъект") Тогда
		СтатистикаЗагрузки.Примечание = "Open Office не загружен!";
		Возврат Неопределено;
	КонецЕсли;
	
	СоответствиеОбразец = Новый Соответствие;
	
	ТребуетсяПерекодировка = Рарус_Компонента <> Неопределено И ЗначениеЗаполнено(ПрайсЛистКонтрагента.КодоваяСтраница) И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251";
	
	Для СчЛистов = 0 По Sheets.getCount() - 1 Цикл
		Попытка
			Sheet = Sheets.getByIndex(СчЛистов);
		Исключение
			Продолжить;
		КонецПопытки;
		
		Если ТипЗнч(Sheet) <> Тип("COMОбъект") Тогда
			Продолжить;
		КонецЕсли;
		
		Cursor = Sheet.createCursor();
		Cursor.gotoStartOfUsedArea(0);
		Cursor.gotoEndOfUsedArea(-1);
		Addresses = Cursor.getRangeAddress();
		ВсегоКолонок = Addresses.EndColumn - Addresses.StartColumn + 1;
		ВсегоСтрок = Addresses.EndRow - Addresses.StartRow + 1;
		
		СтрокаНачало = Addresses.StartRow;
		СтрокаКонец = Мин(ВсегоСтрок, КоличествоСтрок);
		
		Диапазон = Sheet.getCellRangeByPosition(Addresses.StartColumn, СтрокаНачало, Addresses.EndColumn, СтрокаКонец);
		
		МассивКолонок = Новый Массив;
		
		Для Сч = 1 По ВсегоКолонок Цикл
			МассивКолонок.Добавить(Новый Массив);
		КонецЦикла; 
		
		Для СчСтрок = 0 По Диапазон.getRows().getCount()-1 Цикл
			Для Сч = 0 По ВсегоКолонок-1 Цикл
				Cell = Диапазон.getCellByPosition(Сч, СчСтрок);
				//ЗначениеПоля = СокрЛП(Cell.getText().String());
				ТипЯчейки = Cell.getType();
				Если ТипЯчейки = 1 Тогда
					ЗначениеПоля = Cell.getValue();
				ИначеЕсли ТипЯчейки = 2 Тогда
					ЗначениеПоля = СокрЛП(Cell.getString());
				ИначеЕсли ТипЯчейки = 3 Тогда
					ЗначениеПоля = Cell.getFormula();
				Иначе
					ЗначениеПоля = "";
				КонецЕсли;
				Если ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
					ЗначениеПоля = СокрЛП(ЗначениеПоля);
					Если ТребуетсяПерекодировка Тогда
						ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
					КонецЕсли;
				КонецЕсли;
				МассивКолонок[Сч].Добавить(ЗначениеПоля);
			КонецЦикла;
		КонецЦикла;
		
		СоответствиеОбразец.Вставить(Sheet.Name, МассивКолонок);
		
	КонецЦикла;
	
	Desktop.Terminate();
	ServiceManager = Неопределено;
	
	Возврат СоответствиеОбразец;
	
КонецФункции // ПрочитатьОбразецOpenOffice()

Функция ПрочитатьОбразецTXT(СтрокаПодключения, Разделитель, КоличествоСтрок)
	
	Состояние_("Получение образца данных");
	
	МассивДанныхОбразец = Новый Массив;
	
	ФайлОбменаОбъект =  Новый ЧтениеТекста(СтрокаПодключения);
	
	ОбрабатываемаяСтрока = 0;
	
	Пока ОбрабатываемаяСтрока < КоличествоСтрок Цикл
		
		СтрокаФайлаПрайсЛиста = ФайлОбменаОбъект.ПрочитатьСтроку();
		Если СтрокаФайлаПрайсЛиста = Неопределено Тогда
			Прервать;
		КонецЕсли; 
		
		ОбрабатываемаяСтрока = ОбрабатываемаяСтрока + 1;
		
		Сч = 0;
		Для Каждого ЗначениеПоля Из РазобратьСтрокуTXT(СокрЛП(СтрокаФайлаПрайсЛиста), СтрокаПодключения, Разделитель) Цикл
			Если МассивДанныхОбразец.ВГраница() < Сч Тогда
				МассивДанныхКолонки = Новый Массив;
				Для СчЦикла = 1 По ОбрабатываемаяСтрока-1 Цикл
					МассивДанныхКолонки.Добавить("");
				КонецЦикла;
				МассивДанныхОбразец.Добавить(МассивДанныхКолонки);
			КонецЕсли;
			Если Рарус_Компонента <> Неопределено Тогда
				Если ЗначениеЗаполнено(ПрайсЛистКонтрагента.КодоваяСтраница) И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251" Тогда
					ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
				КонецЕсли;
			КонецЕсли;
			МассивДанныхОбразец[Сч].Добавить(ЗначениеПоля);
			Сч = Сч + 1;
		КонецЦикла;
		
		Для СчЦикла = Сч По МассивДанныхОбразец.ВГраница() Цикл
			МассивДанныхОбразец[СчЦикла].Добавить("");
		КонецЦикла;
		
	КонецЦикла;
	
	ФайлОбменаОбъект.Закрыть();
	
	Возврат МассивДанныхОбразец;
	
КонецФункции // ПрочитатьОбразецTXT()

Функция ПолучитьОбразецДанныхИзФайла(СтрокаПодключения, КоличествоСтрок)
	
	СоответствиеОбразец = Неопределено; // Для каждой страницы будут свои массивы
	
	Если ТипФайла = ".ODS" ИЛИ ТипФайла = ".OTS" Тогда
		ЗаполнитьСоответствиеДанныхODS(СтрокаПодключения, СоответствиеОбразец, КоличествоСтрок);
		Если СоответствиеОбразец = Неопределено Тогда
			ЗаполнитьСоответствиеДанныхXLS(СтрокаПодключения, СоответствиеОбразец, КоличествоСтрок);
		КонецЕсли;
	Иначе
		ЗаполнитьСоответствиеДанныхXLS(СтрокаПодключения, СоответствиеОбразец, КоличествоСтрок);
		Если СоответствиеОбразец = Неопределено Тогда
			ЗаполнитьСоответствиеДанныхODS(СтрокаПодключения, СоответствиеОбразец, КоличествоСтрок);
		КонецЕсли;
	КонецЕсли;
	
	Если СоответствиеОбразец = Неопределено Тогда
		СтатистикаЗагрузки.Примечание = "Для загрузки файла требуется установить Microsoft Office или Open Office."+Символы.ПС+СтатистикаЗагрузки.Примечание;
	КонецЕсли;
	
	Возврат СоответствиеОбразец;
	
КонецФункции //ПолучитьОбразецДанныхИзФайла()

Функция ПрочитатьОбразецИзФайла(СтрокаПодключения, Разделитель, КоличествоСтрок = 20) Экспорт
	
	Если ПустаяСтрока(СтрокаПодключения) Тогда 
		Предупреждение_("Не указан файл для загрузки!");
		Возврат Неопределено;
	КонецЕсли;
	
	Файл = Новый Файл(СтрокаПодключения);
	Если Не Файл.Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьТипФайла(СтрокаПодключения);
	
	Если Найти(ТипФайла, ".XL") > 0 ИЛИ ТипФайла = ".ODS" ИЛИ ТипФайла = ".OTS" Тогда
		СоответствиеОбразец = ПолучитьОбразецДанныхИзФайла(СтрокаПодключения, КоличествоСтрок);
	ИначеЕсли ТипФайла = ".TXT" ИЛИ ТипФайла = ".CSV" Тогда
		СоответствиеОбразец = Новый Соответствие;
		СоответствиеОбразец.Вставить("Текст", ПрочитатьОбразецTXT(СтрокаПодключения, Разделитель, КоличествоСтрок));
	Иначе
		СоответствиеОбразец = Неопределено;
		СтатистикаЗагрузки.Примечание = "Недопустимый тип файла!";
	КонецЕсли;
	
	Если СоответствиеОбразец = Неопределено Тогда
		Предупреждение_(СтатистикаЗагрузки.Примечание);
	КонецЕсли;
	
	Возврат СоответствиеОбразец;
	
КонецФункции //ПрочитатьОбразецИзФайла()

Функция ПрочитатьОбразецИзИсточникаДанных(СтрокаПодключения, КоличествоСтрок) Экспорт
	
	Если ПустаяСтрока(СтрокаПодключения) Тогда
		Предупреждение_("Не указана строка подключения для загрузки!");
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		Catalog = Новый COMОбъект("ADOX.Catalog");
		Catalog.ActiveConnection = СтрокаПодключения;
	Исключение
		Предупреждение_("Ошибка подключения к источнику данных: 
						|"+ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	МассивТаблиц = Новый Массив;
	
	Попытка
		Для НомерТаблицы=0 По Catalog.Tables.Count-1 Цикл
			Table = Catalog.Tables.Item(НомерТаблицы);
			Если Врег(Table.Type)="TABLE" Тогда
				МассивТаблиц.Добавить(Table.Name);
			КонецЕсли;
		КонецЦикла;
	Исключение
		Предупреждение_("Ошибка чтения таблиц в источнике данных: 
						|"+ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	Состояние_("Инициализация ADODB.Connection ...");
	Попытка
		Connection = Новый COMОбъект("ADODB.Connection");
		Коннект = Connection.Open(СтрокаПодключения);
	Исключение
		Предупреждение_("Ошибка подключения к источнику данных: 
						|"+ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	ТребуетсяПерекодировка = Рарус_Компонента <> Неопределено И ЗначениеЗаполнено(ПрайсЛистКонтрагента.КодоваяСтраница) И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251";
	
	СоответствиеОбразец = Новый Соответствие;
	
	Для Каждого ИмяТаблицы Из МассивТаблиц Цикл
		
		МассивДанныхТаблицы = Новый Массив;
		
		Command = Новый  COMОбъект("ADODB.Command");
		Command.CommandText =
			"SELECT TOP " + Формат(КоличествоСтрок, "ЧГ=0") + " " + ИмяТаблицы + ".*
			|FROM " + ИмяТаблицы;
		Command.ActiveConnection = Connection;
		
		Попытка
			Recordset = Command.Execute();
		Исключение
			Сообщить("Ошибка чтения таблицы [" + ИмяТаблицы + "]: 
							|"+ОписаниеОшибки());
			Продолжить;
		КонецПопытки;
		
		КолПолей = Recordset.Fields.Count;
		
		Для Сч = 0 По КолПолей-1 Цикл
			ИмяКолонки = Recordset.Fields(Сч).Name;
			МассивКолонка = Новый Массив;
			МассивКолонка.Добавить(ИмяКолонки);
			МассивДанныхТаблицы.Добавить(МассивКолонка);
		КонецЦикла;
		
		Состояние_("Чтение образца данных из таблицы [" + ИмяТаблицы + "] ...");
		Пока Не Recordset.EOF Цикл
			
			Для Сч = 0 По КолПолей-1 Цикл
				ЗначениеПоля = Recordset.Fields(Сч).Value;
				Если ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
					ЗначениеПоля = СокрЛП(ЗначениеПоля);
					Если ТребуетсяПерекодировка Тогда
						ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
					КонецЕсли;
				КонецЕсли;
				МассивДанныхТаблицы[Сч].Добавить(ЗначениеПоля);
			КонецЦикла;
			
			Recordset.MoveNext();
		КонецЦикла;
		
		СоответствиеОбразец.Вставить(ИмяТаблицы, МассивДанныхТаблицы);
		
	КонецЦикла;
	
	Возврат СоответствиеОбразец;
	
КонецФункции //ПрочитатьОбразецИзИсточникаДанных() 


// Функция определяет, с помощью каких средств будет загружаться файл в формате XLS,XLSX
// доступна загрузка с использованием Microsoft Office либо Open Office
//
// Параметры:
// СтрокаПодключения - Строка - Адрес скачиваемого файла
//
// Возвращаемое значение:
//  Булево			- Истина, если файл прочитан, Ложь - иначе
//
Функция ПрочитатьXLS(СтрокаПодключения)
	
	// Сначала попробуем прочитать с помощью ADO или Excel
	РезультатЗагрузки = ПрочитатьEXCEL(СтрокаПодключения);
	
	Если РезультатЗагрузки = Неопределено Тогда // При загрузке возникли ошибки
		
		РезультатЗагрузки = Ложь;
		
		Попытка
			ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
			Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
			Params = MakePropertyValue(ServiceManager, "Hidden", Истина);
			Args = Новый COMSafeArray("VT_DISPATCH", 1);
			Args.SetValue(0, Params);
			OpenOfficeДоступен = Истина;
		Исключение
			OpenOfficeДоступен = Ложь;
		КонецПопытки;
		
		Если OpenOfficeДоступен Тогда
			ТекстВопроса = "На компьютере не установлен Microsoft Excel. Загрузить файл средствами Open Office?";
			ОтветНаВопрос = Вопрос_(ТекстВопроса, "ДаНет", 20, "Нет");
			Если ОтветНаВопрос = "Да" Тогда
				РезультатЗагрузки = (ПрочитатьOpenOffice(СтрокаПодключения) = Истина);
			КонецЕсли;
		Иначе
			СтатистикаЗагрузки.Примечание = "На компьютере не установлен Microsoft Office или Open Office." + Символы.ПС + СтатистикаЗагрузки.Примечание;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатЗагрузки;
	
КонецФункции // ПрочитатьXLS()

Функция ПрочитатьОО(СтрокаПодключения)
	
	РезультатЗагрузки = ПрочитатьOpenOffice(СтрокаПодключения);
	
	Если РезультатЗагрузки = Неопределено Тогда
		// При попытке чтения средствами OpenOffice произошла ошибка
		// Попробуем загрузить с помощью Excel
		РезультатЗагрузки = Ложь;
		ТекстВопроса = "На компьютере не установлен Open Office. Загрузить файл средствами Microsoft Excel?";
		ОтветНаВопрос = Вопрос_(ТекстВопроса, "ДаНет", 20, "Нет");
		Если ОтветНаВопрос = "Да" Тогда
			РезультатЗагрузки = ПрочитатьEXCEL(СтрокаПодключения);
			Если РезультатЗагрузки = Неопределено Тогда
				СтатистикаЗагрузки.Примечание = "На компьютере не установлен Microsoft Office или Open Office." + Символы.ПС + СтатистикаЗагрузки.Примечание;
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатЗагрузки;
	
КонецФункции // ПрочитатьОО()

Функция РазобратьМассивДанных(ПроверитьЗаголовки = Истина, УдалитьПервыеСтроки = Истина, ОбрезатьКонецДанных = Истина)
	
	// Очистим, если вруг там были данные
	ТЗПрайсЛиста.Очистить();
	
	ВсегоСтолбцов = МассивДанных.Количество();
	Если ВсегоСтолбцов = 0 Тогда
		СтатистикаЗагрузки.Примечание = "В файле нет колонок!";
		Возврат Ложь;
	КонецЕсли;
	ВсегоСтрок = МассивДанных[0].Количество();
	Если ВсегоСтрок = 0 Тогда
		СтатистикаЗагрузки.Примечание = "В файле нет строк с данными!";
		Возврат Ложь;
	КонецЕсли;
	
	//ПервыйЛист = (ЗаголовкиВФайле = Неопределено);
	
	Если ПроверитьЗаголовки И ПрайсЛистКонтрагента.ЗаголовкиКолонокВПрайсЛисте.Количество() > 0 И СтрокаЗагружаемойСтраницы.СтрокаЗаголовковКолонок > 0 Тогда // Будет с чем сравнить!!
		
		СтрокаЗаголовкиКолонок = СтрокаЗагружаемойСтраницы.СтрокаЗаголовковКолонок;
		
		СмещениеКолонокПоГоризонтали = Неопределено;
		СмещениеКолонокПоВертикали = Неопределено;
		
		Если СтрокаЗаголовкиКолонок > ВсегоСтрок Тогда
			СтатистикаЗагрузки.Примечание = "В файле нет строк с данными!";
			Возврат Ложь;
		КонецЕсли;
		
		МассивПервыхСтрок = Новый Массив;
		
		Для СчСтрок = 1 По СтрокаЗаголовкиКолонок Цикл
			
			ЗаголовкиВФайле = Новый СписокЗначений;
			
			Для Сч = 1 ПО ВсегоСтолбцов Цикл
				ЗначениеПоля = МассивДанных[Сч-1][СчСтрок-1];
				Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
					ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
				ИначеЕсли ТипЗнч(ЗначениеПоля) <> Тип("Строка") Тогда
					ЗначениеПоля = "" + ЗначениеПоля;
				КонецЕсли;
				Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
					ЗаголовкиВФайле.Добавить(Формат(Сч,"ЧГ=0"), ЗначениеПоля);
				Иначе
					ЗаголовкиВФайле.Добавить(Формат(Сч,"ЧГ=0"), Формат(Сч,"ЧГ=0"));
				КонецЕсли;
			КонецЦикла;
			
			МассивПервыхСтрок.Добавить(ЗаголовкиВФайле);
			
		КонецЦикла; 
		
		// Используемые колонки
		ИспользуемыеКолонки = Новый СписокЗначений;
		Для Каждого ПолеЗагрузки Из ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста Цикл
			Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла)
				ИЛИ Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
				// Поле не заполнено, но так видимо и надо
				Продолжить;
			КонецЕсли;
			ЗаголовокКолонки = ПрайсЛистКонтрагента.ЗаголовкиКолонокВПрайсЛисте.Найти(ПолеЗагрузки.ИмяПоляФайла, "ИмяКолонки");
			Если ЗаголовокКолонки = Неопределено ИЛИ ЗаголовокКолонки.ИмяКолонки = ЗаголовокКолонки.ПредставлениеКолонки Тогда
				// Заголовок или не сохранен или представлен числом, такую колонку пропустим
				Продолжить;
			КонецЕсли;
			Если ИспользуемыеКолонки.НайтиПоЗначению(ПолеЗагрузки.ИмяПоляФайла) = Неопределено Тогда
				ИспользуемыеКолонки.Добавить(ПолеЗагрузки.ИмяПоляФайла, ЗаголовокКолонки.ПредставлениеКолонки);
			КонецЕсли;
		КонецЦикла;
		
		СтруктураЗаголовковСовпадает = Истина;
		
		Для Каждого ЗаголовокКолонки Из ИспользуемыеКолонки Цикл
			
			// Найдем возможное смещение
			Для СчСтрок = 1 По СтрокаЗаголовкиКолонок Цикл
				
				ЗаголовкиВФайле = МассивПервыхСтрок[СчСтрок-1];
				
				Для Каждого ЗаголовокКолонкиФайла Из ЗаголовкиВФайле Цикл
					
					Если ЗаголовокКолонкиФайла.Представление = ЗаголовокКолонки.Представление Тогда
						// Попали на ячейку с нужным наименованием
						НомерКолонки = Число(ЗаголовокКолонки.Значение);
						НомерКолонкиФайла = Число(ЗаголовокКолонкиФайла.Значение);
						
						СмещениеТекКолонки = НомерКолонкиФайла - НомерКолонки;
						СмещениеТекКолонкиПоВертикали = СтрокаЗаголовкиКолонок - СчСтрок;
						
						Если СмещениеКолонокПоВертикали = Неопределено Тогда
							СмещениеКолонокПоВертикали = СмещениеТекКолонкиПоВертикали;
						Иначе
							Если СмещениеКолонокПоВертикали <> СмещениеТекКолонкиПоВертикали Тогда
								СмещениеКолонокПоВертикали = Неопределено;
								Прервать;
							КонецЕсли;
						КонецЕсли;
						
						Если СмещениеКолонокПоГоризонтали = Неопределено Тогда
							СмещениеКолонокПоГоризонтали = СмещениеТекКолонки;
						Иначе
							Если СмещениеКолонокПоГоризонтали <> СмещениеТекКолонки Тогда
								СмещениеКолонокПоГоризонтали = Неопределено;
								Прервать;
							КонецЕсли;
						КонецЕсли;
						
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
			
			Если СмещениеКолонокПоГоризонтали = Неопределено ИЛИ СмещениеКолонокПоВертикали = Неопределено Тогда
				СтруктураЗаголовковСовпадает = Ложь;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не СтруктураЗаголовковСовпадает Тогда
			СтатистикаЗагрузки.Примечание = "В загружаемом файле заголовки отличаются от ожидаемых!";
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбрезатьКонецДанных И СтрокаЗагружаемойСтраницы.СтрокаКонец > 0 Тогда 
		Если ВсегоСтрок > СтрокаЗагружаемойСтраницы.СтрокаКонец Тогда
			Для Каждого Массив Из МассивДанных Цикл
				Пока Массив.Количество() > СтрокаЗагружаемойСтраницы.СтрокаКонец Цикл
					Массив.Удалить(Массив.ВГраница());
				КонецЦикла; 
			КонецЦикла;
		Иначе
			Для Каждого Массив Из МассивДанных Цикл
				Массив.Очистить();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ВсегоСтрок = МассивДанных[0].Количество();
	
	Если ПроверитьЗаголовки Тогда
		// Смещение по вертикали учитываем только на первой странице
		СмещениеКолонокПоВертикали = ?(СмещениеКолонокПоВертикали = Неопределено, 0, СмещениеКолонокПоВертикали);
		КоличествоУдаляемыхСтрок = Макс(0, ?(СтрокаЗагружаемойСтраницы.СтрокаНачало > 0, СтрокаЗагружаемойСтраницы.СтрокаНачало - 1, 0) - СмещениеКолонокПоВертикали);
	Иначе
		КоличествоУдаляемыхСтрок = ?(СтрокаЗагружаемойСтраницы.СтрокаНачало > 0, СтрокаЗагружаемойСтраницы.СтрокаНачало - 1, 0);
	КонецЕсли;
	
	Если УдалитьПервыеСтроки И КоличествоУдаляемыхСтрок > 0 Тогда
		Для Каждого Массив Из МассивДанных Цикл
			Для Сч = 1 По Мин(КоличествоУдаляемыхСтрок, ВсегоСтрок) Цикл
				Массив.Удалить(0);
			КонецЦикла; 
		КонецЦикла;
	КонецЕсли;
	
	Состояние_("Загрузка данных в прайс-листы контрагентов");
	
	ИмяПоляВалюта = Неопределено;
	ИмяПоляЦена = Неопределено;
	
	СмещениеКолонокПоГоризонтали = ?(СмещениеКолонокПоГоризонтали = Неопределено, 0, СмещениеКолонокПоГоризонтали);
	
	СписокОстальныхПолей = Новый СписокЗначений;
	Для Сч = 0 По МассивДанных.ВГраница() Цикл
		//СписокОстальныхПолей.Добавить(Сч,Формат(Сч+1,"ЧГ=0"));
		СписокОстальныхПолей.Добавить(Сч, Формат(Сч + 1 - СмещениеКолонокПоГоризонтали, "ЧГ=0")); // Значение - Реальная колонка, Представление - Колонка из настроек
	КонецЦикла;
	
	Для Каждого ПолеЗагрузки Из ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста Цикл
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла)
			ИЛИ Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			// Поле не заполнено, но так видимо и надо
			Продолжить;
		КонецЕсли;
		ИмяЯчейки = Число(ПолеЗагрузки.ИмяПоляФайла) - 1 + СмещениеКолонокПоГоризонтали; // Ячейка из которой происходит загрузка
		Если ИмяЯчейки > МассивДанных.ВГраница() Тогда
			СтатистикаЗагрузки.Примечание = "Поле файла '" + ПолеЗагрузки.ИмяПоляФайла + "', выбранное для реквизита <" + ПолеЗагрузки.ИмяРеквизитаПрайсЛиста + ">,  больше, чем количество колонок в файле!";
			Возврат Ложь;
		КонецЕсли;
		Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Артикул"
			ИЛИ ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Производитель"
			ИЛИ ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Количество"
			ИЛИ ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "СрокПоставки"
			ИЛИ ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Наименование" Тогда
			ЭлементСписка = СписокОстальныхПолей.НайтиПоЗначению(ИмяЯчейки);
			Если ЭлементСписка <> Неопределено Тогда
				СписокОстальныхПолей.Удалить(ЭлементСписка);
			КонецЕсли;
		КонецЕсли;
		Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Цена" Тогда
			ИмяПоляЦена = ИмяЯчейки;
		КонецЕсли;
		Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Валюта" Тогда
			ИмяПоляВалюта = ИмяЯчейки;
		КонецЕсли;
	КонецЦикла;
	
	ВалютаВКолонкеЦена = ((ИмяПоляВалюта = ИмяПоляЦена) И ИмяПоляВалюта <> Неопределено) ИЛИ (ИмяПоляВалюта = Неопределено);
	
	// Особенные поля загрузки
	// КлючСтрокиПоставщика
	КлючевыеПоля = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Выгрузить(Новый Структура("Ключевое", Истина));
	КлючевыеПоля.Колонки.Добавить("ИмяЯчейки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	Для Каждого ПолеЗагрузки Из КлючевыеПоля Цикл
		Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			Продолжить;
		КонецЕсли;
		ПолеЗагрузки.ИмяЯчейки = Число(ПолеЗагрузки.ИмяПоляФайла) - 1 + СмещениеКолонокПоГоризонтали;
	КонецЦикла;
	
	// НаименованиеИностранное
	ПоляНаименование = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Выгрузить(Новый Структура("ИмяРеквизитаПрайсЛиста", "Наименование"));
	ПоляНаименование.Колонки.Добавить("ИмяЯчейки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	Сч = 0;
	Пока Сч < ПоляНаименование.Количество() Цикл
		ПолеЗагрузки = ПоляНаименование[Сч];
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла) Тогда
			ПоляНаименование.Удалить(ПолеЗагрузки);
			Продолжить;
		КонецЕсли; 
		Сч = Сч + 1;
		Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			Продолжить;
		КонецЕсли;
		ПолеЗагрузки.ИмяЯчейки = Число(ПолеЗагрузки.ИмяПоляФайла) - 1 + СмещениеКолонокПоГоризонтали;
	КонецЦикла;
	
	// НаименованиеИностранное
	ПоляНаименованиеИностранное = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Выгрузить(Новый Структура("ИмяРеквизитаПрайсЛиста","НаименованиеИностранное"));
	ПоляНаименованиеИностранное.Колонки.Добавить("ИмяЯчейки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	Сч = 0;
	Пока Сч < ПоляНаименованиеИностранное.Количество() Цикл
		ПолеЗагрузки = ПоляНаименованиеИностранное[Сч];
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла) Тогда
			ПоляНаименованиеИностранное.Удалить(ПолеЗагрузки);
			Продолжить;
		КонецЕсли; 
		Сч = Сч + 1;
		Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			Продолжить;
		КонецЕсли;
		ПолеЗагрузки.ИмяЯчейки = Число(ПолеЗагрузки.ИмяПоляФайла) - 1 + СмещениеКолонокПоГоризонтали;
	КонецЦикла;
	
	// Артикул
	ПолеАртикул = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Артикул", "ИмяРеквизитаПрайсЛиста");
	ИмяЯчейкиАртикул = Неопределено;
	Если ЗначениеЗаполнено(ПолеАртикул.ИмяПоляФайла) Тогда
		ИмяЯчейкиАртикул = Число(ПолеАртикул.ИмяПоляФайла) - 1 + СмещениеКолонокПоГоризонтали;
	КонецЕсли;
	
	ВсегоСтрок = МассивДанных[0].Количество();
	
	ЗапросКаталогаПредложений = Неопределено;
	МассивСч     = Новый Массив;
	МассивХеш    = Новый Массив;
	МассивАрт    = Новый Массив;
	МассивНаим   = Новый Массив;
	МассивНаимИн = Новый Массив;
	МассивОп     = Новый Массив;
	
	Если ПрайсЛистКонтрагента.ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ОстаткиДляКаталогаПредложений Тогда
		ТаблицаКлюч  = Новый ТаблицаЗначений;
		ТаблицаКлюч.Колонки.Добавить("Сч",  Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10,0,ДопустимыйЗнак.Неотрицательный)));
		ТаблицаКлюч.Колонки.Добавить("Хеш", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(32, ДопустимаяДлина.Фиксированная)));
		
		ЗапросКаталогаПредложений = Новый Запрос;
		ЗапросКаталогаПредложений.Текст = 
			"ВЫБРАТЬ
			|	Ключи.Сч КАК Сч,
			|	Ключи.Хеш КАК КлючСтрокиПоставщика
			|ПОМЕСТИТЬ втКлючи
			|ИЗ
			|	&Таб КАК Ключи
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Ключи.Сч КАК Сч,
			|	Ключи.КлючСтрокиПоставщика КАК Ключ,
			|	ПрайсЛистыКонтрагентов.Артикул,
			|	ПрайсЛистыКонтрагентов.Производитель,
			|	ПрайсЛистыКонтрагентов.Наименование,
			|	ПрайсЛистыКонтрагентов.НаименованиеИностранное,
			|	ПрайсЛистыКонтрагентов.Цена,
			|	ПрайсЛистыКонтрагентов.Валюта,
			|	ПрайсЛистыКонтрагентов.КратностьПоставок,
			|	ПрайсЛистыКонтрагентов.СрокПоставки,
			|	ПрайсЛистыКонтрагентов.СрокПоставкиМинимальный,
			|	ПрайсЛистыКонтрагентов.СрокПоставкиМаксимальный,
			|	ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте,
			|	ПрайсЛистыКонтрагентов.АртикулВПрайсЛисте,
			|	ПрайсЛистыКонтрагентов.СрокПоставкиВПрайсЛисте,
			|	ПрайсЛистыКонтрагентов.ЗапретПродажи,
			|	ПрайсЛистыКонтрагентов.ЗапретЗакупки,
			|	ПрайсЛистыКонтрагентов.ТегПозиции,
			|	ПрайсЛистыКонтрагентов.СнятаСПроизводства,
			|	ПрайсЛистыКонтрагентов.Вес,
			|	ПрайсЛистыКонтрагентов.Объем
			|ИЗ
			|	втКлючи КАК Ключи
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК ПрайсЛистыКонтрагентов
			|		ПО Ключи.КлючСтрокиПоставщика = ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика
			|ГДЕ
			|	ПрайсЛистыКонтрагентов.ПрайсЛист = &БазовыйПрайсЛист
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сч";
		ЗапросКаталогаПредложений.УстановитьПараметр("БазовыйПрайсЛист", ПрайсЛистКонтрагента.БазовыйПрайсЛист);
	КонецЕсли;
	
	Сч = 0;
	Пока Сч < ВсегоСтрок Цикл
		Сч = Сч + 1;
		
		КлючСтрокиПоставщика = "";
		ПустойКлюч = Истина;
		Для Каждого ПолеЗагрузки Из КлючевыеПоля Цикл
			ЗначениеПоля = МассивДанных[ПолеЗагрузки.ИмяЯчейки][Сч-1];
			Если ТипЗнч(ЗначениеПоля) = Тип("Число") И Не ПрайсЛистКонтрагента.НеФорматироватьКлючевыеПоля Тогда
				ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
			КонецЕсли;
			КлючСтрокиПоставщика = КлючСтрокиПоставщика + "_" + СокрЛП(ЗначениеПоля);
			Если Не ПустаяСтрока(ЗначениеПоля) Тогда
				ПустойКлюч = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		//Если ключ строки пустой, то пропускаем эту строку
		Если ПустойКлюч Тогда
			//Удалим из каждого массива пустой элемнт
			Для Каждого ЭлементМассива Из МассивДанных Цикл
				ЭлементМассива.Удалить(Сч-1);
			КонецЦикла;
			Сч = Сч - 1;
			ВсегоСтрок = ВсегоСтрок - 1;
			Продолжить;
		КонецЕсли;
		
		Хэш = НРег(MD5(КлючСтрокиПоставщика));
		НоваяПозицияПрайсЛиста = ТЗПрайсЛиста.Добавить();
		МассивСч.Добавить(Сч);
		МассивХеш.Добавить(Хэш);
		
		Если ИмяЯчейкиАртикул <> Неопределено Тогда
			Артикул = МассивДанных[ИмяЯчейкиАртикул][Сч-1];
			Если ТипЗнч(Артикул) = Тип("Число") Тогда
				Артикул = Формат(Артикул, "ЧГ=0");
			ИначеЕсли ТипЗнч(Артикул) <> Тип("Строка") Тогда
				// Пусть 1С сама преобразует в строку
				Артикул = "" + Артикул;
			КонецЕсли;
			МассивАрт.Добавить(Артикул);
		КонецЕсли;
		
		Если ПоляНаименование.Количество() > 0 Тогда
			ПолеНаименование = "";
			Для Каждого ПолеЗагрузки Из ПоляНаименование Цикл
				Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
					ЗначениеПоля = СтрЗаменить(ПолеЗагрузки.ИмяПоляФайла, """", "");
				Иначе
					ЗначениеПоля = МассивДанных[ПолеЗагрузки.ИмяЯчейки][Сч-1];
					Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
						ЗначениеПоля = Формат(Окр(ЗначениеПоля, 3), "ЧГ=0");
					Иначе
						ЗначениеПоля = СокрЛП(ЗначениеПоля);
					КонецЕсли;
				КонецЕсли;
				ПолеНаименование = ПолеНаименование + ЗначениеПоля;
			КонецЦикла;
			МассивНаим.Добавить(ПолеНаименование);
		КонецЕсли;
		
		Если ПоляНаименованиеИностранное.Количество() > 0 Тогда
			ПолеНаименованиеИностранное = "";
			Для Каждого ПолеЗагрузки Из ПоляНаименованиеИностранное Цикл
				Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
					ЗначениеПоля = СтрЗаменить(ПолеЗагрузки.ИмяПоляФайла, """", "");
				Иначе
					ЗначениеПоля = МассивДанных[ПолеЗагрузки.ИмяЯчейки][Сч-1];
					Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
						ЗначениеПоля = Формат(Окр(ЗначениеПоля, 3), "ЧГ=0");
					Иначе
						ЗначениеПоля = СокрЛП(ЗначениеПоля);
					КонецЕсли;
				КонецЕсли;
				ПолеНаименованиеИностранное = ПолеНаименованиеИностранное + ЗначениеПоля;
			КонецЦикла;
			МассивНаимИн.Добавить(ПолеНаименованиеИностранное);
		КонецЕсли;
		
		СписокЗначенийОстальныхПолей = Новый СписокЗначений;
		Для Каждого ОстальноеПоле Из СписокОстальныхПолей Цикл
			ЗначениеПоля = МассивДанных[ОстальноеПоле.Значение][Сч-1];
			Если ТипЗнч(ЗначениеПоля) = Тип("Число") И Не ПрайсЛистКонтрагента.НеФорматироватьКлючевыеПоля Тогда
				ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
			КонецЕсли;
			СписокЗначенийОстальныхПолей.Добавить(ОстальноеПоле.Представление, ЗначениеПоля);
		КонецЦикла;
		МассивОп.Добавить(Новый ХранилищеЗначения(СписокЗначенийОстальныхПолей));
		
	КонецЦикла;
	
	Если ЗапросКаталогаПредложений <> Неопределено Тогда
		ЗапросКаталогаПредложений.УстановитьПараметр("Таб", ТаблицаКлюч);
		КаталогПредложений = ЗапросКаталогаПредложений.Выполнить().Выгрузить();
		
		Для Каждого КолонкаКаталога Из КаталогПредложений.Колонки Цикл
			Если КолонкаКаталога.Имя = "Сч" ИЛИ КолонкаКаталога.Имя = "Ключ" Тогда
				Продолжить;
			КонецЕсли;
			ТЗПрайсЛиста.ЗагрузитьКолонку(КаталогПредложений.ВыгрузитьКолонку(КолонкаКаталога.Имя), КолонкаКаталога.Имя);
		КонецЦикла;
		КаталогПредложений.Очистить();
	КонецЕсли;
	
	Если МассивСч.Количество() > 0 Тогда
		ТЗПрайсЛиста.ЗагрузитьКолонку(МассивСч,  "КодПредложения");
		МассивСч.Очистить();
		ТЗПрайсЛиста.ЗагрузитьКолонку(МассивХеш, "КлючСтрокиПоставщика");
		МассивХеш.Очистить();
		Если МассивАрт.Количество() > 0 Тогда
			ТЗПрайсЛиста.ЗагрузитьКолонку(МассивАрт, "Артикул");
			ТЗПрайсЛиста.ЗагрузитьКолонку(МассивАрт, "АртикулВПрайсЛисте");
			МассивАрт.Очистить();
		КонецЕсли;
		Если МассивНаим.Количество() > 0 Тогда
			ТЗПрайсЛиста.ЗагрузитьКолонку(МассивНаим, "Наименование");
			МассивНаим.Очистить();
		КонецЕсли;
		Если МассивНаимИн.Количество() > 0 Тогда
			ТЗПрайсЛиста.ЗагрузитьКолонку(МассивНаимИн, "НаименованиеИностранное");
			МассивНаимИн.Очистить();
		КонецЕсли;
		Если МассивОп.Количество() > 0 Тогда
			ТЗПрайсЛиста.ЗагрузитьКолонку(МассивОп, "ОстальныеПоляВПрайсЛисте");
			МассивОп.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	Для каждого ПолеЗагрузки Из ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста Цикл
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла)
			ИЛИ Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0
			ИЛИ ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Артикул"
			ИЛИ ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "КлючСтрокиПоставщика"
			ИЛИ Найти(ПолеЗагрузки.ИмяРеквизитаПрайсЛиста, "Наименование") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяЯчейки = Число(ПолеЗагрузки.ИмяПоляФайла) - 1 + СмещениеКолонокПоГоризонтали;
		
		МассивЗначенийПолей = МассивДанных[ИмяЯчейки];

		Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Производитель" Тогда
		
			ТЗПрайсЛиста.ЗагрузитьКолонку(МассивЗначенийПолей, "ПроизводительВПрайсЛисте");
			
			ТЗ = ТЗПрайсЛиста.Скопировать(, "ПроизводительВПрайсЛисте");
			ТЗ.Свернуть("ПроизводительВПрайсЛисте");
			
			СоответствиеЗначенийПолей = Новый Соответствие;
			Для Каждого СтрокаТЗ Из ТЗ Цикл
				СоответствиеЗначенийПолей.Вставить(?(обЗначениеНеЗаполнено(СтрокаТЗ.ПроизводительВПрайсЛисте), "Значение не заполнено", СтрокаТЗ.ПроизводительВПрайсЛисте), РегистрыСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки.ПолучитьПроизводителя(ПрайсЛистКонтрагента, СтрокаТЗ.ПроизводительВПрайсЛисте, Истина, СопоставленныеПроизводители));
			КонецЦикла;
			
			Массив = Новый Массив;
			Для Каждого ЗначениеПоля Из МассивЗначенийПолей Цикл
				Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
					// Нужно отформатировать по умолчанию т.к. есть 555, а есть 5 825 !!
					ЗначениеПоля = "" + ЗначениеПоля;
				КонецЕсли;
				Массив.Добавить(СоответствиеЗначенийПолей[?(обЗначениеНеЗаполнено(ЗначениеПоля), "Значение не заполнено", ЗначениеПоля)]);
			КонецЦикла;
			
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив, "Производитель");
			
		ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Валюта" Тогда
			Если Не ВалютаВКолонкеЦена Тогда
				ТЗПрайсЛиста.Колонки.Добавить("ВалютаВПрайсЛисте", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(20)));
				
				ТЗ = ТЗПрайсЛиста.Скопировать(, "ВалютаВПрайсЛисте");
				ТЗ.ЗагрузитьКолонку(МассивЗначенийПолей, "ВалютаВПрайсЛисте");
				ТЗ.Свернуть("ВалютаВПрайсЛисте");
				СоответствиеЗначенийПолей = Новый Соответствие;
				Для Каждого СтрокаТЗ Из ТЗ Цикл
					СоответствиеЗначенийПолей.Вставить(?(обЗначениеНеЗаполнено(СтрокаТЗ.ВалютаВПрайсЛисте), "Значение не заполнено", СтрокаТЗ.ВалютаВПрайсЛисте),РазобратьВалюту(СтрокаТЗ.ВалютаВПрайсЛисте));
				КонецЦикла;
				Массив = Новый Массив;
				Для Каждого ЗначениеПоля Из МассивЗначенийПолей Цикл
					Массив.Добавить(СоответствиеЗначенийПолей[?(обЗначениеНеЗаполнено(ЗначениеПоля), "Значение не заполнено", ЗначениеПоля)]);
				КонецЦикла;
				ТЗПрайсЛиста.ЗагрузитьКолонку(Массив, "Валюта");
				ТЗПрайсЛиста.Колонки.Удалить("ВалютаВПрайсЛисте");
			КонецЕсли;
			
		ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "СрокПоставки" Тогда
		
			Попытка
				// Это поле есть только в ПЛ_ПрайсЛистыКонтрагентов
				ТЗПрайсЛиста.ЗагрузитьКолонку(МассивЗначенийПолей, "СрокПоставкиВПрайсЛисте");
			Исключение КонецПопытки;
			
			ТЗ = ТЗПрайсЛиста.Скопировать(, "СрокПоставки");
			ТЗ.ЗагрузитьКолонку(МассивЗначенийПолей, "СрокПоставки");
			ТЗ.Свернуть("СрокПоставки");
			СоответствиеЗначенийПолей = Новый Соответствие;
			Для Каждого СтрокаТЗ Из ТЗ Цикл
				СтруктураСрокПоставки = Новый Структура;
				СтруктураСрокПоставки.Вставить("СрокПоставки", "");
				СтруктураСрокПоставки.Вставить("СрокПоставкиМинимальный",  ПрайсЛистКонтрагента.СрокПоставкиМинимальный);
				СтруктураСрокПоставки.Вставить("СрокПоставкиМаксимальный", ПрайсЛистКонтрагента.СрокПоставкиМаксимальный);
				РазобратьСрокПоставки(СтрокаТЗ.СрокПоставки, СтруктураСрокПоставки.СрокПоставки, СтруктураСрокПоставки.СрокПоставкиМинимальный, СтруктураСрокПоставки.СрокПоставкиМаксимальный);
				СоответствиеЗначенийПолей.Вставить(?(обЗначениеНеЗаполнено(СтрокаТЗ.СрокПоставки), "Значение не заполнено", "" + СтрокаТЗ.СрокПоставки), СтруктураСрокПоставки);
			КонецЦикла;
			Массив = Новый Массив;
			Массив2 = Новый Массив;
			Массив3 = Новый Массив;
			Для Каждого ЗначениеПоля Из МассивЗначенийПолей Цикл
				Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
					ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
				КонецЕсли;
				СтруктураСрокПоставки = СоответствиеЗначенийПолей[?(обЗначениеНеЗаполнено(ЗначениеПоля), "Значение не заполнено", ЗначениеПоля)];
				Массив.Добавить(СтруктураСрокПоставки.СрокПоставки);
				Массив2.Добавить(СтруктураСрокПоставки.СрокПоставкиМинимальный);
				Массив3.Добавить(СтруктураСрокПоставки.СрокПоставкиМаксимальный);
			КонецЦикла;
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив, "СрокПоставки");
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив2, "СрокПоставкиМинимальный");
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив3, "СрокПоставкиМаксимальный");
			
		ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Цена" Тогда
		
			МассивВалют = Новый Массив;
			Массив = Новый Массив;
			Для Каждого ЗначениеПоля Из МассивЗначенийПолей Цикл
				Валюта = Неопределено;
				Цена = РазобратьЦену(ЗначениеПоля, Валюта, ВалютаВКолонкеЦена);
				Массив.Добавить(Цена);
				МассивВалют.Добавить(Валюта);
			КонецЦикла;
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив, "Цена");
			Если ВалютаВКолонкеЦена Тогда
				ТЗПрайсЛиста.ЗагрузитьКолонку(МассивВалют, "Валюта");
			КонецЕсли;
			
		ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Количество" Тогда
		
			Попытка
				// Это поле есть только в ПрайсЛистыКонтрагентовВременный
				ТЗПрайсЛиста.ЗагрузитьКолонку(МассивЗначенийПолей, "КоличествоВПрайсЛисте");
			Исключение КонецПопытки;
			
			Массив = Новый Массив;
			Для Каждого ЗначениеПоля Из МассивЗначенийПолей Цикл
				Массив.Добавить(РазобратьКоличество(ЗначениеПоля));
			КонецЦикла;
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив, "Количество");
		
		ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Вес" Тогда
		
			КоэффициентПересчетаВеса = ?(ПрайсЛистКонтрагента.КоэффициентПересчетаВеса = 0, 1, ПрайсЛистКонтрагента.КоэффициентПересчетаВеса);
			
			Массив = Новый Массив;
			Для Каждого ЗначениеПоля Из МассивЗначенийПолей Цикл
				Если ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
					Попытка
						ЗначениеПоля = Число(ЗначениеПоля);
					Исключение
						ЗначениеПоля = 0;
					КонецПопытки
				КонецЕсли;
				Массив.Добавить(ЗначениеПоля / КоэффициентПересчетаВеса);
			КонецЦикла;
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив, "Вес");
			
		Иначе
			Массив = Новый Массив;
			Для Каждого ЗначениеПоля Из МассивЗначенийПолей Цикл
				Если ТипЗнч(НоваяПозицияПрайсЛиста[ПолеЗагрузки.ИмяРеквизитаПрайсЛиста]) = Тип("Строка")
					И ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
					ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
				ИначеЕсли ТипЗнч(НоваяПозицияПрайсЛиста[ПолеЗагрузки.ИмяРеквизитаПрайсЛиста]) = Тип("Число")
					И ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
					Попытка
						ЗначениеПоля = Число(ЗначениеПоля);
					Исключение
						ЗначениеПоля = 0;
					КонецПопытки
				КонецЕсли;
				Массив.Добавить(ЗначениеПоля);
			КонецЦикла;
			ТЗПрайсЛиста.ЗагрузитьКолонку(Массив, ПолеЗагрузки.ИмяРеквизитаПрайсЛиста);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции //РазобратьМассивДанных()

Функция ПрочитатьEXCEL(ИмяОбрФайла)
	
	// Сначала самый быстрый метод
	СоответствиеЗагрузки = EXCEL_УстановитьСвязь_Application(ИмяОбрФайла);
	
	Если СоответствиеЗагрузки = Неопределено И Найти(ТипФайла, ".XL") > 0 Тогда
		// Здесь помедленнее
		СоответствиеЗагрузки = EXCEL_УстановитьСвязь_ADO(ИмяОбрФайла);
	КонецЕсли;
	
	Если СоответствиеЗагрузки = Неопределено тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтатистикаЗагрузки.Примечание = "";
	
	ГлавнаяСтраницаЗагружена = Ложь;
	ПерваяСтраница = Истина;
	
	Для Каждого КлючЗначение Из СоответствиеЗагрузки Цикл
		Если КлючЗначение.Ключ = ПрайсЛистКонтрагента.ИмяТаблицы Тогда
			ГлавнаяСтраницаЗагружена = Истина;
		КонецЕсли;
		Состояние_("Обработка данных со страницы [" + КлючЗначение.Ключ + "]");
		СтрокаЗагружаемойСтраницы = ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.Найти(КлючЗначение.Ключ, "ИмяЛиста");
		Если СтрокаЗагружаемойСтраницы = Неопределено Тогда
			// Бозьмем настройки главной страницы!
			СтрокаЗагружаемойСтраницы = ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.Найти(ПрайсЛистКонтрагента.ИмяТаблицы, "ИмяЛиста");
		КонецЕсли;
		МассивДанных = КлючЗначение.Значение;
		Если Не СохранитьМассивДанныхВРегистр(ПерваяСтраница, Истина, Истина) Тогда
			Возврат Ложь;
		КонецЕсли;
		ПерваяСтраница = Ложь;
	КонецЦикла;
	
	Если Не ГлавнаяСтраницаЗагружена Тогда
		СтатистикаЗагрузки.Примечание = СтатистикаЗагрузки.Примечание + " В файле нет страницы [" + ПрайсЛистКонтрагента.ИмяТаблицы + "]!";
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции //ЗагрузитьИзEXCEL()

// "open office" заполняет массив свойств
Функция MakePropertyValue(ServiceManager, Name, Value)
	Struct = ServiceManager.Bridge_GetStruct("com.sun.star.beans.PropertyValue");
	Struct.Name = Name;
	Struct.Value = Value;
	Возврат Struct;
КонецФункции //MakePropertyValue()

// "open office" Функция преобразует Windows имя файла в URL OpenOffice
Функция ПреобразоватьВURL(ИмяФайла)
	Возврат "file:///" + СтрЗаменить(ИмяФайла, "\", "/");
КонецФункции //ПреобразоватьВURL()

// функция чтения файла OpenOffice в прайс-лист
//
// Параметры:
// СтрокаПодключения - Строка - Адрес скачиваемого файла
//
Функция ПрочитатьOpenOffice(СтрокаПодключения)
	
	Если СтрокаПодключения = Неопределено Тогда
		ИмяОбрФайла = СокрЛП(ПрайсЛистКонтрагента.СтрокаПодключения);
	Иначе
		ИмяОбрФайла = СтрокаПодключения;
	КонецЕсли;
	
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		Params = MakePropertyValue(ServiceManager,"Hidden", Истина);
		Args = Новый COMSafeArray("VT_DISPATCH", 1);
		Args.SetValue(0, Params);
		OODoc = Desktop.loadComponentFromURL(ПреобразоватьВURL(ИмяОбрФайла), "_blank", 0, Args);
		Sheets = OODoc.getSheets();
	Исключение
		СтатистикаЗагрузки.Примечание = "На компьютере должен быть установлен 
		|Open Office версии не ниже 2.0";
		Возврат Неопределено;
	КонецПопытки;
	
	Если ТипЗнч(Sheets) <> Тип("COMОбъект") Тогда
		СтатистикаЗагрузки.Примечание = "Open Office не загружен";
		Возврат Неопределено;
	КонецЕсли;
	
	СтатистикаЗагрузки.Примечание = "";
	
	ВсеОК = Истина;
	
	КопияСтруктураФайлаПрайсЛиста = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.ВыгрузитьКолонки();
	КопияСтруктураФайлаПрайсЛиста.Колонки.Добавить("ИмяЯчейки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	
	КлючевыеПоля = Новый Структура;
	
	ИмяПоляВалюта = Неопределено;
	ИмяПоляЦена = Неопределено;
	
	Для каждого ПолеЗагрузки Из ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста Цикл
		
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла) ИЛИ Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Колонка_Источник = Число(ПолеЗагрузки.ИмяПоляФайла);
		НоваяСтрока = КопияСтруктураФайлаПрайсЛиста.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ПолеЗагрузки);
		НоваяСтрока.ИмяЯчейки = Колонка_Источник;
		
		Если ПолеЗагрузки.Ключевое Тогда
			КлючевыеПоля.Вставить(ПолеЗагрузки.ИмяРеквизитаПрайсЛиста + "Поиск", Колонка_Источник);
		КонецЕсли;
		
		Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Цена" Тогда
			ИмяПоляЦена = Колонка_Источник;
		КонецЕсли;
		Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Валюта" Тогда
			ИмяПоляВалюта = Колонка_Источник;
		КонецЕсли;
		
	КонецЦикла;
	
	ВалютаВКолонкеЦена = ((ИмяПоляВалюта = ИмяПоляЦена) И ИмяПоляВалюта <> Неопределено) ИЛИ ИмяПоляВалюта = Неопределено;
	
	ТаблицаСтраниц = ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.Выгрузить(Новый Структура("Использовать", Истина));
	
	Для Каждого ИспользуемаяСтраница Из ТаблицаСтраниц Цикл
		
		Попытка
			Sheet = Sheets.getByName(ИспользуемаяСтраница.ИмяЛиста);
		Исключение
			Продолжить;
		КонецПопытки;
		
		Cursor = Sheet.createCursor();
		Cursor.gotoStartOfUsedArea(0);
		Cursor.gotoEndOfUsedArea(-1);
		Addresses = Cursor.getRangeAddress();
		
		ВсегоКолонок = Addresses.EndColumn - Addresses.StartColumn + 1;
		ВсегоСтрок = Addresses.EndRow - Addresses.StartRow + 1;
		
		Если ИспользуемаяСтраница.СтрокаНачало > 0 Тогда
			Если ВсегоСтрок < ИспользуемаяСтраница.СтрокаНачало Тогда
				Продолжить;
			КонецЕсли;
			СтрокаНачало = Addresses.StartRow + ИспользуемаяСтраница.СтрокаНачало - 1;
		Иначе
			СтрокаНачало = Addresses.StartRow;
		КонецЕсли;
		
		Если ИспользуемаяСтраница.СтрокаКонец > 0 Тогда
			СтрокаКонец = Мин(ВсегоСтрок, ИспользуемаяСтраница.СтрокаКонец);
		Иначе
			СтрокаКонец = ВсегоСтрок;
		КонецЕсли;
		
		Диапазон = Sheet.getCellRangeByPosition(Addresses.StartColumn, СтрокаНачало, Addresses.EndColumn, СтрокаКонец);
		
		Для СчСтрок = 0 По Диапазон.getRows().getCount()-1 Цикл
			
			Состояние_("Обрабатывается строка " + СчСтрок);
			ОбработкаПрерыванияПользователя_();
			
			КлючСтрокиПоставщика = "";
			ПустойКлюч = Истина;
			
			Для Каждого ПолеЗагрузки Из КлючевыеПоля Цикл // Ключевые поля
				
				Cell = Диапазон.getCellByPosition(ПолеЗагрузки.Значение - 1, СчСтрок);
				//ЗначениеПоля = Cell.getText().String();
				ТипЯчейки = Cell.getType();
				Если ТипЯчейки = 1 Тогда
					ЗначениеПоля = Cell.getValue();
				ИначеЕсли ТипЯчейки = 2 Тогда
					ЗначениеПоля = СокрЛП(Cell.getString());
				ИначеЕсли ТипЯчейки = 3 Тогда
					ЗначениеПоля = Cell.getFormula();
				Иначе
					ЗначениеПоля = "";
				КонецЕсли;
				
				Если ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
					Если Рарус_Компонента <> Неопределено И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251" Тогда
						ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
					КонецЕсли;
					ЗначениеПоля = ВРег(ЗначениеПоля);
				ИначеЕсли ТипЗнч(ЗначениеПоля) = Тип("Число") И Не ПрайсЛистКонтрагента.НеФорматироватьКлючевыеПоля Тогда
					ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
					ПустойКлюч = Ложь;
				КонецЕсли;
				
				КлючСтрокиПоставщика = КлючСтрокиПоставщика + "_" + ЗначениеПоля;
				
			КонецЦикла;
			
			//Если ключ строки пустой, то пропускаем эту строку
			Если ПустойКлюч Тогда
				Продолжить;
			КонецЕсли;
			
			Хэш = НРег(MD5(КлючСтрокиПоставщика));
			
			НоваяПозицияПрайсЛиста = ТЗПрайсЛиста.Добавить();
			НоваяПозицияПрайсЛиста.ПрайсЛист = ПрайсЛистКонтрагента.Ссылка;
			НоваяПозицияПрайсЛиста.КлючСтрокиПоставщика = Хэш;
			НоваяПозицияПрайсЛиста.КодПредложения = СчСтрок + 1;
			
			Для каждого ПолеЗагрузки Из КопияСтруктураФайлаПрайсЛиста Цикл
				
				Cell = Диапазон.getCellByPosition(ПолеЗагрузки.ИмяЯчейки - 1, СчСтрок);
				
				ТипЯчейки = Cell.getType();
				Если ТипЯчейки = 1 Тогда
					ЗначениеПоля = Cell.getValue();
				ИначеЕсли ТипЯчейки = 2 Тогда
					ЗначениеПоля = СокрЛП(Cell.getString());
				ИначеЕсли ТипЯчейки = 3 Тогда
					ЗначениеПоля = Cell.getFormula();
				Иначе
					ЗначениеПоля = "";
				КонецЕсли;
				
				Если ТипЗнч(ЗначениеПоля) = Тип("Строка")
					И Рарус_Компонента <> Неопределено
					И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251" Тогда
					ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля, ПрайсЛистКонтрагента.КодоваяСтраница, "cpCP1251");
				КонецЕсли;
				
				ТипЗнчРеквизита = ТипЗнч(НоваяПозицияПрайсЛиста[ПолеЗагрузки.ИмяРеквизитаПрайсЛиста]);
				Если ТипЗнчРеквизита = Тип("Строка") И ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
					ЗначениеПоля = Формат(ЗначениеПоля, "ЧГ=0");
				ИначеЕсли ТипЗнчРеквизита = Тип("Число") И ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
					Попытка
						ЗначениеПоля = Число(ЗначениеПоля);
					Исключение
						ЗначениеПоля = 0;
					КонецПопытки;
				КонецЕсли;
				
				Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Производитель" Тогда
					
					НоваяПозицияПрайсЛиста.ПроизводительВПрайсЛисте = ЗначениеПоля;
					НоваяПозицияПрайсЛиста.Производитель = РегистрыСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки.ПолучитьПроизводителя(ПрайсЛистКонтрагента, ЗначениеПоля, Истина, СопоставленныеПроизводители);
					
				ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Артикул" Тогда
					
					НоваяПозицияПрайсЛиста.Артикул            = ЗначениеПоля;
					НоваяПозицияПрайсЛиста.АртикулВПрайсЛисте = ЗначениеПоля;
					
				ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Валюта" И Не ВалютаВКолонкеЦена Тогда
					
					НоваяПозицияПрайсЛиста.Валюта = РазобратьВалюту(ЗначениеПоля);
					
				ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Цена" Тогда
					
					Валюта = Неопределено;
					НоваяПозицияПрайсЛиста.Цена = РазобратьЦену(ЗначениеПоля, Валюта, ВалютаВКолонкеЦена);
					
				ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Количество" Тогда
					
					НоваяПозицияПрайсЛиста.Количество = РазобратьКоличество(ЗначениеПоля);
					НоваяПозицияПрайсЛиста.КоличествоВПрайсЛисте = ЗначениеПоля;
					
				ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "СрокПоставки" Тогда
					
					РазобратьСрокПоставки(ЗначениеПоля, НоваяПозицияПрайсЛиста.СрокПоставки, НоваяПозицияПрайсЛиста.СрокПоставкиМинимальный, НоваяПозицияПрайсЛиста.СрокПоставкиМаксимальный);
					НоваяПозицияПрайсЛиста.СрокПоставкиВПрайсЛисте = ЗначениеПоля;
					
				Иначе
					
					НоваяПозицияПрайсЛиста[ПолеЗагрузки.ИмяРеквизитаПрайсЛиста] = ЗначениеПоля;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Desktop.Terminate();
	ServiceManager = Неопределено;
	
	Если ПрайсЛистКонтрагента.ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ОстаткиДляКаталогаПредложений Тогда
		
		ЗапросКаталогаПредложений = Новый Запрос;
		ЗапросКаталогаПредложений.Текст = 
			"ВЫБРАТЬ
			|	ПрайсЛистыКонтрагентов.Артикул,
			|	ПрайсЛистыКонтрагентов.Производитель,
			|	ПрайсЛистыКонтрагентов.АртикулДляПоиска,
			|	ПрайсЛистыКонтрагентов.Наименование,
			|	ПрайсЛистыКонтрагентов.НаименованиеИностранное,
			|	ПрайсЛистыКонтрагентов.Цена,
			|	ПрайсЛистыКонтрагентов.Валюта,
			|	ПрайсЛистыКонтрагентов.КратностьПоставок,
			|	ПрайсЛистыКонтрагентов.СрокПоставки,
			|	ПрайсЛистыКонтрагентов.СрокПоставкиМинимальный,
			|	ПрайсЛистыКонтрагентов.СрокПоставкиМаксимальный,
			|	ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте,
			|	ПрайсЛистыКонтрагентов.АртикулВПрайсЛисте,
			|	ПрайсЛистыКонтрагентов.СрокПоставкиВПрайсЛисте,
			|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика,
			|	ПрайсЛистыКонтрагентов.ЗапретПродажи,
			|	ПрайсЛистыКонтрагентов.ЗапретЗакупки,
			|	ПрайсЛистыКонтрагентов.ТегПозиции,
			|	ПрайсЛистыКонтрагентов.СнятаСПроизводства,
			|	ПрайсЛистыКонтрагентов.Вес,
			|	ПрайсЛистыКонтрагентов.Объем
			|ИЗ
			|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК ПрайсЛистыКонтрагентов
			|ГДЕ
			|	ПрайсЛистыКонтрагентов.ПрайсЛист = &БазовыйПрайсЛист
			|	И ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика В(&КлючСтрокиПоставщика)";
			
		ЗапросКаталогаПредложений.УстановитьПараметр("БазовыйПрайсЛист", ПрайсЛистКонтрагента.БазовыйПрайсЛист);
		ЗапросКаталогаПредложений.УстановитьПараметр("КлючСтрокиПоставщика", ТЗПрайсЛиста.ВыгрузитьКолонку("КлючСтрокиПоставщика"));
		
		СтруктураСтрокиКаталога = Новый Структура("Артикул, Производитель, АртикулДляПоиска, Наименование, НаименованиеИностранное, Цена, Валюта, КратностьПоставок, СрокПоставки, СрокПоставкиМинимальный, СрокПоставкиМаксимальный,ПроизводительВПрайсЛисте, АртикулВПрайсЛисте, СрокПоставкиВПрайсЛисте, ЗапретПродажи, ЗапретЗакупки, ТегПозиции, СнятаСПроизводства, Вес, Объем");
		
		ВыборкаКаталогаПредложений = ЗапросКаталогаПредложений.Выполнить().Выбрать();
		Пока ВыборкаКаталогаПредложений.Следующий() Цикл
			МассивСтрок = ТЗПрайсЛиста.НайтиСтроки(Новый Структура("КлючСтрокиПоставщика", ВыборкаКаталогаПредложений.КлючСтрокиПоставщика));
			Для Каждого СтрокаТЗПрайсЛиста Из МассивСтрок Цикл
				// Надо заполнить только незаполненные поля!!
				Для Каждого КлючИЗначение Из СтруктураСтрокиКаталога Цикл
					ЗначениеТЗПрайсЛиста = СтрокаТЗПрайсЛиста[КлючИЗначение.Ключ];
					Если ТипЗнч(ЗначениеТЗПрайсЛиста) = Тип("Булево")Тогда
						//Проверим, установлено ли соответствие для значения в формате "Булево"
						НайденнаяСтрока = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти(КлючИЗначение.Ключ, "ИмяРеквизитаПрайсЛиста"); 
						Если НайденнаяСтрока <> Неопределено И НЕ ПустаяСтрока(НайденнаяСтрока.ИмяПоляФайла)Тогда
							Продолжить;
						КонецЕсли;
					Иначе
						Если ЗначениеЗаполнено(СтрокаТЗПрайсЛиста[КлючИЗначение.Ключ]) Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					СтрокаТЗПрайсЛиста[КлючИЗначение.Ключ] = ВыборкаКаталогаПредложений[КлючИЗначение.Ключ];
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ВсеОК;
	
КонецФункции //ПрочитатьOpenOffice()

Функция ОбработатьТЗПрайсЛиста()
	// сначала удалим неуникальные строки
	ТЗПрайсЛиста.Сортировать("КлючСтрокиПоставщика");
	
	КоэффициентПересчетаВеса = ?(ПрайсЛистКонтрагента.КоэффициентПересчетаВеса = 0, 1, ПрайсЛистКонтрагента.КоэффициентПересчетаВеса);
	
	ТекСтрока = Неопределено;
	Для Каждого СтрокаТЗ Из ТЗПрайсЛиста Цикл
		Если НЕ ЗначениеЗаполнено(ТекСтрока)
			ИЛИ ТекСтрока.КлючСтрокиПоставщика <> СтрокаТЗ.КлючСтрокиПоставщика Тогда
			ТекСтрока = СтрокаТЗ;
			Продолжить;
		КонецЕсли;
		ТекСтрока.ЕстьДубли = Истина;
		Если (СтрокаТЗ.Количество = Истина И ТипЗнч(СтрокаТЗ.Количество) <> Тип("Число")) ИЛИ (ТекСтрока.Количество = Истина И ТипЗнч(ТекСтрока.Количество) <> Тип("Число")) Тогда
			ТекСтрока.Количество = Истина;
		ИначеЕсли (ТекСтрока.Количество = Ложь И ТипЗнч(ТекСтрока.Количество) <> Тип("Число")) ИЛИ ТекСтрока.Количество = Неопределено Тогда
			ТекСтрока.Количество = СтрокаТЗ.Количество;
		ИначеЕсли ТипЗнч(СтрокаТЗ.Количество) = Тип("Число") Тогда
			ТекСтрока.Количество = ТекСтрока.Количество + СтрокаТЗ.Количество;
		КонецЕсли;
		СтрокаТЗ.КлючСтрокиПоставщика = "";
		ТекСтрока.Вес = ТекСтрока.Вес / КоэффициентПересчетаВеса;
	КонецЦикла;
	
	НайденныеСтроки = ТЗПрайсЛиста.НайтиСтроки(Новый Структура("КлючСтрокиПоставщика", ""));
	Для Каждого НайденнаяСтрока ИЗ НайденныеСтроки Цикл
		ТЗПрайсЛиста.Удалить(НайденнаяСтрока);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПравилаЗагрузки.НазначениеПравила КАК НазначениеПравила,
		|	ПравилаЗагрузки.ОбъектПравила КАК ОбъектПравила,
		|	ПравилаЗагрузки.ВидПравила КАК ВидПравила,
		|	ПравилаЗагрузки.ИмяРеквизитаПрайсЛиста,
		|	ПравилаЗагрузки.Значение,
		|	ПравилаЗагрузки.ПорядокПрименения КАК ПорядокПрименения,
		|	ВЫБОР
		|		КОГДА ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки)
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК ПорядокНазначения
		|ИЗ
		|	РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
		|ГДЕ
		|	ПравилаЗагрузки.ПрайсЛист = &ПрайсЛист
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПорядокНазначения,
		|	ОбъектПравила,
		|	ВидПравила,
		|	ПорядокПрименения
		|ИТОГИ ПО
		|	НазначениеПравила,
		|	ОбъектПравила,
		|	ВидПравила";
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистКонтрагента.Ссылка);
	
	// Общие для всех прайс-листов правила распространяются только на преобразования артикулов по шаблону
	// и бывают при НазначенииПравила = 3 // Для сопоставленного производителя
	
	ВыборкаНазначенийПравил = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНазначенийПравил.Следующий() Цикл
		Если ВыборкаНазначенийПравил.НазначениеПравила = Перечисления.НазначениеПравилЗагрузки.КлючСтроки Тогда
			
			ВыборкаКлючСтрокиПоставщика = ВыборкаНазначенийПравил.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаКлючСтрокиПоставщика.Следующий() Цикл
				
				НайденнаяСтрока = ТЗПрайсЛиста.Найти(ВыборкаКлючСтрокиПоставщика.ОбъектПравила, "КлючСтрокиПоставщика");
				Если НайденнаяСтрока = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ВыборкаВидаПравила = ВыборкаКлючСтрокиПоставщика.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаВидаПравила.Следующий() Цикл
					Если ВыборкаВидаПравила.ВидПравила = Перечисления.ВидыПравилЗагрузки.НеЗагружать Тогда
						НайденнаяСтрока.ЗапретЗагрузки = Истина;
						Прервать;
					КонецЕсли;
					
					ВыборкаДетальныхЗаписей = ВыборкаВидаПравила.Выбрать();
					Пока ВыборкаДетальныхЗаписей.Следующий() Цикл 
						Если ВыборкаДетальныхЗаписей.ВидПравила = Перечисления.ВидыПравилЗагрузки.ПрисвоитьЗначение Тогда
							Если ВыборкаДетальныхЗаписей.ИмяРеквизитаПрайсЛиста = "Номенклатура" Тогда
								Продолжить;
							КонецЕсли;
							НайденнаяСтрока[ВыборкаДетальныхЗаписей.ИмяРеквизитаПрайсЛиста] = ВыборкаДетальныхЗаписей.Значение;
						КонецЕсли
					КонецЦикла;
				КонецЦикла;
				
			КонецЦикла;
			
		ИначеЕсли ВыборкаНазначенийПравил.НазначениеПравила = Перечисления.НазначениеПравилЗагрузки.ПроизводительВПрайсЛисте Тогда
			ВыборкаОбъектовНазначенияПравил = ВыборкаНазначенийПравил.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			ПроизводителиВПрайсЛисте = ТЗПрайсЛиста.Скопировать(, "ПроизводительВПрайсЛисте");
			ПроизводителиВПрайсЛисте.Свернуть("ПроизводительВПрайсЛисте");
			
			Для Каждого СтрПроизводительВПрайсЛисте Из ПроизводителиВПрайсЛисте Цикл
				ПроизводительВПрайсЛисте = СтрПроизводительВПрайсЛисте.ПроизводительВПрайсЛисте;
				
				НайденнаяСтрока = ТЗПрайсЛиста.Найти(ПроизводительВПрайсЛисте, "ПроизводительВПрайсЛисте");
				Если НайденнаяСтрока = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ВыборкаОбъектовНазначенияПравил.Сбросить();
				
				Пока ВыборкаОбъектовНазначенияПравил.НайтиСледующий(Новый Структура("ОбъектПравила",ПроизводительВПрайсЛисте)) Цикл
					
					ВыборкаВидаПравила = ВыборкаОбъектовНазначенияПравил.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаВидаПравила.Следующий() Цикл 
						ВидПравила = ВыборкаВидаПравила.ВидПравила;
						Если ВидПравила = Перечисления.ВидыПравилЗагрузки.НеЗагружать Тогда  //применяется ко всей строке прайс-листа
							
							ВыборкаДетальныхЗаписей = ВыборкаВидаПравила.Выбрать();
							Пока ВыборкаДетальныхЗаписей.Следующий() Цикл
								НайденныеСтроки = ТЗПрайсЛиста.НайтиСтроки(Новый Структура("ПроизводительВПрайсЛисте, Производитель", ПроизводительВПрайсЛисте, ВыборкаДетальныхЗаписей.Значение));
								Для Каждого НайденнаяСтрока ИЗ НайденныеСтроки Цикл
									ТЗПрайсЛиста.Удалить(НайденнаяСтрока);
								КонецЦикла;
							КонецЦикла;
							
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
			
		ИначеЕсли ВыборкаНазначенийПравил.НазначениеПравила = Перечисления.НазначениеПравилЗагрузки.Производитель Тогда // 3 - Сопоставленный производитель = ОбъектПравила (СправочникСсылка.Производители)
			
			ВыборкаОбъектовНазначенияПравил = ВыборкаНазначенийПравил.Выбрать();
			
			Производители = ТЗПрайсЛиста.Скопировать(,"Производитель");
			Производители.Свернуть("Производитель");
			
			Для Каждого СтрокаПроизводитель Из Производители Цикл
				
				НайденныеСтроки = ТЗПрайсЛиста.НайтиСтроки(Новый Структура("Производитель", СтрокаПроизводитель.Производитель));
				Если НайденныеСтроки.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ВыборкаОбъектовНазначенияПравил.Сбросить();
				
				Пока ВыборкаОбъектовНазначенияПравил.НайтиСледующий(Новый Структура("ОбъектПравила", СтрокаПроизводитель.Производитель)) Цикл
					
					ВидПравила = ВыборкаОбъектовНазначенияПравил.ВидПравила;
					
					Если ВидПравила = Перечисления.ВидыПравилЗагрузки.НеЗагружать Тогда  //удалим строки из ТЗ
						Для Каждого НайденнаяСтрока ИЗ НайденныеСтроки Цикл
							ТЗПрайсЛиста.Удалить(НайденнаяСтрока);
						КонецЦикла;
						
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ПолеПроизводитель = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Производитель","ИмяРеквизитаПрайсЛиста");
	Если ПолеПроизводитель <> Неопределено И обЗначениеНеЗаполнено(ПолеПроизводитель.ИмяПоляФайла) Тогда
		// У нас ранее могли сопоставить производителя по ключевому полю или полю ПроизводительВПрайсЛисте
		// А всем остальным записям присвоим производителя, указанного в самом прайс-листе!
		НайденныеСтроки = ТЗПрайсЛиста.НайтиСтроки(Новый Структура("Производитель", Справочники.Производители.ПустаяСсылка()));
		Для Каждого НайденнаяСтрока ИЗ НайденныеСтроки Цикл
			НайденнаяСтрока.Производитель = ПрайсЛистКонтрагента.Производитель;
			НайденнаяСтрока.ПроизводительВПрайсЛисте = ПрайсЛистКонтрагента.Производитель.Наименование;
		КонецЦикла;
	КонецЕсли;
	
	Если ПрайсЛистКонтрагента.ВидПрайсЛиста <> Перечисления.ВидыПрайсЛистов.ОстаткиДляКаталогаПредложений Тогда
		ПолеВалюта = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Валюта", "ИмяРеквизитаПрайсЛиста");
		Если ПолеВалюта <> Неопределено И обЗначениеНеЗаполнено(ПолеВалюта.ИмяПоляФайла) Тогда
			ТЗПрайсЛиста.ЗаполнитьЗначения(ПрайсЛистКонтрагента.Валюта, "Валюта");
		КонецЕсли;
		ПолеСрокПоставки = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("СрокПоставки", "ИмяРеквизитаПрайсЛиста");
		Если ПолеСрокПоставки = Неопределено ИЛИ обЗначениеНеЗаполнено(ПолеСрокПоставки.ИмяПоляФайла) Тогда
			ТЗПрайсЛиста.ЗаполнитьЗначения(ПрайсЛистКонтрагента.СрокПоставкиМинимальный, "СрокПоставкиМинимальный");
			ТЗПрайсЛиста.ЗаполнитьЗначения(ПрайсЛистКонтрагента.СрокПоставкиМаксимальный, "СрокПоставкиМаксимальный");
			ТЗПрайсЛиста.ЗаполнитьЗначения(Справочники.ПрайсЛистыКонтрагентов.ПредставлениеСрокаПоставкиИнтервал(ПрайсЛистКонтрагента.СрокПоставкиМинимальный, ПрайсЛистКонтрагента.СрокПоставкиМаксимальный), "СрокПоставки");
		КонецЕсли;
	Иначе
		ПолеЦена = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Цена", "ИмяРеквизитаПрайсЛиста");
		Если ПолеЦена <> Неопределено И обЗначениеНеЗаполнено(ПолеЦена.ИмяПоляФайла) Тогда
			// Мы грузили цену из самого прайс-листа!!
			// Тогда и валюту загрузим из настроек этого же прайс-листа
			// Перекроем возможно загруженные значения из базового каталога
			ПолеВалюта = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Валюта", "ИмяРеквизитаПрайсЛиста");
			Если ПолеВалюта <> Неопределено И обЗначениеНеЗаполнено(ПолеВалюта.ИмяПоляФайла) Тогда
				ТЗПрайсЛиста.ЗаполнитьЗначения(ПрайсЛистКонтрагента.Валюта, "Валюта");
			КонецЕсли;
		КонецЕсли;
		ПолеСрокПоставкиКаталога = ПрайсЛистКонтрагента.БазовыйПрайсЛист.СтруктураФайлаПрайсЛиста.Найти("СрокПоставки", "ИмяРеквизитаПрайсЛиста");
		Если ПолеСрокПоставкиКаталога = Неопределено ИЛИ обЗначениеНеЗаполнено(ПолеСрокПоставкиКаталога.ИмяПоляФайла) Тогда
			// В базовом каталоге был не определен срок поставки
			// Тогда срок поставки возьмем из настроек прайс-листа
			// Перекроем возможно загруженные значения из каталога
			ПолеСрокПоставки = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("СрокПоставки", "ИмяРеквизитаПрайсЛиста");
			Если ПолеСрокПоставки = Неопределено ИЛИ обЗначениеНеЗаполнено(ПолеСрокПоставки.ИмяПоляФайла) Тогда
				ТЗПрайсЛиста.ЗаполнитьЗначения(ПрайсЛистКонтрагента.СрокПоставкиМинимальный, "СрокПоставкиМинимальный");
				ТЗПрайсЛиста.ЗаполнитьЗначения(ПрайсЛистКонтрагента.СрокПоставкиМаксимальный, "СрокПоставкиМаксимальный");
				ТЗПрайсЛиста.ЗаполнитьЗначения(Справочники.ПрайсЛистыКонтрагентов.ПредставлениеСрокаПоставкиИнтервал(ПрайсЛистКонтрагента.СрокПоставкиМинимальный, ПрайсЛистКонтрагента.СрокПоставкиМаксимальный), "СрокПоставки");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПолеКоличество = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Найти("Количество","ИмяРеквизитаПрайсЛиста");
	Если ПолеКоличество <> Неопределено И обЗначениеНеЗаполнено(ПолеКоличество.ИмяПоляФайла) Тогда
		ТЗПрайсЛиста.ЗаполнитьЗначения(Истина, "Количество");
	КонецЕсли;
	
	ТЗПрайсЛиста.ЗаполнитьЗначения(ПрайсЛистКонтрагента.Ссылка, "ПрайсЛист");
	
	// Напоследок заполним АртикулДляПоиска
	Для Каждого СтрокаПрайса ИЗ ТЗПрайсЛиста Цикл
		СтрокаПрайса.АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(СтрокаПрайса.Артикул, СтрокаПрайса.Производитель);
	КонецЦикла;
	
	Если ТЗПрайсЛиста.Количество() > МаксПорцияЗаписи Тогда
		// В несколько приемов
		Сч = 0;
		КопияТЗПрайсЛиста = ТЗПрайсЛиста.СкопироватьКолонки();
		ДатаЗаписиПорцииДанных = ТекущаяДата();
		
		Для Каждого СтрокаПрайсЛиста ИЗ ТЗПрайсЛиста Цикл
			КопияСтроки = КопияТЗПрайсЛиста.Добавить();
			ЗаполнитьЗначенияСвойств(КопияСтроки, СтрокаПрайсЛиста);
			Сч = Сч + 1;
			Если Сч >= МаксПорцияЗаписи Тогда
				КопияТЗПрайсЛиста.ЗаполнитьЗначения(ДатаЗаписиПорцииДанных, "ДатаЗаписи");
				РегистрПрайсЛистов.Отбор.ДатаЗаписи.Значение = ДатаЗаписиПорцииДанных;
				РегистрПрайсЛистов.Отбор.ДатаЗаписи.Использование = Истина;
				РегистрПрайсЛистов.Загрузить(КопияТЗПрайсЛиста);
				Попытка
					РегистрПрайсЛистов.Записать();
				Исключение
					СтатистикаЗагрузки.Примечание = "Ошибка записи части прайс-листа <"+ПрайсЛистКонтрагента.Наименование+"> во временное хранилище: "+ОписаниеОшибки();
					Возврат Ложь;
				КонецПопытки; 
				Сч = 0;
				КопияТЗПрайсЛиста = ТЗПрайсЛиста.СкопироватьКолонки();
				Если ТекущаяДата() <= ДатаЗаписиПорцииДанных Тогда
					ДатаЗаписиПорцииДанных = ДатаЗаписиПорцииДанных + 1;
				Иначе
					ДатаЗаписиПорцииДанных = ТекущаяДата();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если Сч > 0 Тогда
			Если ТекущаяДата() <= ДатаЗаписиПорцииДанных Тогда
				ДатаЗаписиПорцииДанных = ДатаЗаписиПорцииДанных + 1;
			Иначе
				ДатаЗаписиПорцииДанных = ТекущаяДата();
			КонецЕсли;
			КопияТЗПрайсЛиста.ЗаполнитьЗначения(ДатаЗаписиПорцииДанных, "ДатаЗаписи");
			РегистрПрайсЛистов.Отбор.ДатаЗаписи.Значение = ДатаЗаписиПорцииДанных;
			РегистрПрайсЛистов.Отбор.ДатаЗаписи.Использование = Истина;
			РегистрПрайсЛистов.Загрузить(КопияТЗПрайсЛиста);
			Попытка
				РегистрПрайсЛистов.Записать();
			Исключение
				СтатистикаЗагрузки.Примечание = "Ошибка записи части прайс-листа во временное хранилище: "+ОписаниеОшибки();
				Возврат Ложь;
			КонецПопытки; 
		КонецЕсли;
		
	Иначе
		Если ДатаЗаписиПорцииДанных <> Неопределено 
			И ТекущаяДата() <= ДатаЗаписиПорцииДанных Тогда
			ДатаЗаписиПорцииДанных = ДатаЗаписиПорцииДанных + 1;
		Иначе
			ДатаЗаписиПорцииДанных = ТекущаяДата();
		КонецЕсли;
		ТЗПрайсЛиста.ЗаполнитьЗначения(ДатаЗаписиПорцииДанных, "ДатаЗаписи");
		РегистрПрайсЛистов.Отбор.ДатаЗаписи.Значение = ДатаЗаписиПорцииДанных;
		РегистрПрайсЛистов.Отбор.ДатаЗаписи.Использование = Истина;
				
		РегистрПрайсЛистов.Загрузить(ТЗПрайсЛиста);
		Попытка
			РегистрПрайсЛистов.Записать();
		Исключение
			СтатистикаЗагрузки.Примечание = "Ошибка записи части прайс-листа во временное хранилище: "+ОписаниеОшибки();
			Возврат Ложь;
		КонецПопытки; 
		
	КонецЕсли;
	
	ТЗПрайсЛиста.Очистить();
	
	Возврат Истина;
КонецФункции //ОбработатьТЗПрайсЛиста()

Функция СохранитьМассивДанныхВРегистр(ПервыйЗаход, УдалитьПервыеСтроки, ОбрезатьКонецДанных = Ложь)
	// На первом заходе надо проверить строку заголовков и удалить лишние строки
	Результат = РазобратьМассивДанных(ПервыйЗаход, УдалитьПервыеСтроки, ОбрезатьКонецДанных);
	
	// Почистим за собой массивы
	Для Каждого ВремМассив Из МассивДанных Цикл
		ВремМассив.Очистить();
	КонецЦикла;
	
	Если Не Результат Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Сохраним данные во временный регистр
	Если Не ОбработатьТЗПрайсЛиста() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции //СохранитьМассивДанныхВРегистр()

// функция чтения текстового файла в прайс-лист
//
// Параметры:
// СтрокаПодключения - Строка - Адрес загружаемого файла
//
Функция ПрочитатьTXT(СтрокаПодключения)
	ВсеОК = Истина;

	Если обЗначениеНеЗаполнено(СтрокаПодключения) Тогда
		ИмяОбрФайла = СокрЛП(ПрайсЛистКонтрагента.СтрокаПодключения);
	Иначе
		ИмяОбрФайла = СтрокаПодключения;
	КонецЕсли;
	
	Файл = Новый Файл(ИмяОбрФайла);
	Папка    = Файл.Путь;
	ИмяФайла = Файл.Имя;
	
	Если ПрайсЛистКонтрагента.Разделитель = "T" ИЛИ ПрайсЛистКонтрагента.Разделитель = Символы.Таб Тогда
		Разделитель = "TabDelimited";
	ИначеЕсли ЗначениеЗаполнено(ПрайсЛистКонтрагента.Разделитель) Тогда
		Разделитель = "Delimited(" + ПрайсЛистКонтрагента.Разделитель + ")";
	Иначе
		Разделитель = "Delimited(;)";
	КонецЕсли;
	
	Текст = "[" + ИмяФайла + "]
	|ColNameHeader=False
	|Format=" + Разделитель + "
	|TextDelimiter=none
	|CharacterSet=ANSI
	|";
	Для н = 1 По ПрайсЛистКонтрагента.ЗаголовкиКолонокВПрайсЛисте.Количество() Цикл
		//Текст = Текст + "Col" + н + "=""" + ПрайсЛистКонтрагента.ЗаголовкиКолонокВПрайсЛисте[н-1].ПредставлениеКолонки + """ Text" + Символы.ПС;
		Текст = Текст + "Col" + н + "=Field" + н + " Text" + Символы.ПС;
	КонецЦикла;
	
	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.УстановитьТекст(Текст);
	ТекстДок.Записать(Папка + "Schema.ini", КодировкаТекста.ANSI);
	
	RecordSet = Новый COMОбъект("ADODB.Recordset");
	
	strQuery = "SELECT * FROM [" + ИмяФайла + "]";
	adOpenStatic = 3;
	adLockOptimistic = 3;
	adCmdText = 1;
	
	МассивДанных = Новый Массив;
	
	Попытка
		strConn = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source=" + Папка + ";Extended Properties=""text;""";
		RecordSet.Open(strQuery, strConn, adOpenStatic, adLockOptimistic, adCmdText);
		МассивДанных = ПолучитьМассивGetRows(RecordSet, Ложь);
	Исключение КонецПопытки;
	
	Если МассивДанных.Количество() = 0 Тогда
		Попытка
			strConn = "Provider=Microsoft.Jet.OLEDB.4.0;Data Source=" + Папка + ";Extended Properties=""text;""";
			RecordSet.Open(strQuery, strConn, adOpenStatic, adLockOptimistic, adCmdText);
			МассивДанных = ПолучитьМассивGetRows(RecordSet, Ложь);
		Исключение КонецПопытки;
	КонецЕсли;
	
	УдалитьФайлы(Папка + "Schema.ini");
	
	СтрокаЗагружаемойСтраницы = ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста[0];
	
	Если МассивДанных.Количество() > 0 Тогда
		// Средствами ADO получилось прочитать
		Если Не СохранитьМассивДанныхВРегистр(Истина, Истина) Тогда
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
	КонецЕсли;
	
	// Попробуем старым методом
	МассивДанных = Новый Массив;
	
	ФайлОбменаОбъект =  Новый ЧтениеТекста(ИмяОбрФайла);
	ОбрабатываемаяСтрока = 0;
	
	КвантЧтения = 50000; // Особенность загрузки больших прайс-листов
	ПервыйЗаход = Истина;
	
	Пока обЗначениеНеЗаполнено(СтрокаЗагружаемойСтраницы.СтрокаКонец) ИЛИ ОбрабатываемаяСтрока < СтрокаЗагружаемойСтраницы.СтрокаКонец Цикл
		ОбработкаПрерыванияПользователя_();
		
		СтрокаФайлаПрайсЛиста = ФайлОбменаОбъект.ПрочитатьСтроку();
		Если СтрокаФайлаПрайсЛиста = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ОбрабатываемаяСтрока = ОбрабатываемаяСтрока + 1;
		Состояние_("Обрабатывается строка " + ОбрабатываемаяСтрока);
		
		Если ОбрабатываемаяСтрока%КвантЧтения = 0 Тогда
			// Прочитали нужное количество строк (файл получился реально большой)
			// Сохраним строки во временный регистр
			Если МассивДанных.Количество() > 0 Тогда
				// Есть что сохранять
				Если Не СохранитьМассивДанныхВРегистр(ПервыйЗаход, ПервыйЗаход) Тогда // Разберем данные и сохраним массивы данных
					Возврат Ложь;
				КонецЕсли;
				// Начнем новую порцию данных
				МассивДанных = Новый Массив;
				ПервыйЗаход = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаФайлаПрайсЛиста = СокрЛП(СтрокаФайлаПрайсЛиста);
		МассивСтрокиФайлаПрайсЛиста = РазобратьСтрокуTXT(СтрокаФайлаПрайсЛиста, СтрокаПодключения);
		
		Если МассивСтрокиФайлаПрайсЛиста.Количество() = 0 Тогда
			// Пропускаем пустые строки
			Продолжить;
		КонецЕсли;
		
		Пока МассивДанных.Количество() < МассивСтрокиФайлаПрайсЛиста.Количество() Цикл
			МассивЭлементовКолонки = Новый Массив;
			Если МассивДанных.Количество() > 0 Тогда
				Для Каждого ЭлементПервойКолонки Из МассивДанных[0] Цикл
					МассивЭлементовКолонки.Добавить("");
				КонецЦикла;
			КонецЕсли;
			МассивДанных.Добавить(МассивЭлементовКолонки);
		КонецЦикла;
		
		// Надо дополнить и МассивСтрокиФайлаПрайсЛиста недостающими колонками!!
		Пока МассивДанных.Количество() > МассивСтрокиФайлаПрайсЛиста.Количество() Цикл
			МассивСтрокиФайлаПрайсЛиста.Добавить("");
		КонецЦикла;
		
		СчКолонок = 0;
		Для Каждого ЭлементМассиваСтроки Из МассивСтрокиФайлаПрайсЛиста Цикл
			МассивДанных[СчКолонок].Добавить(ЭлементМассиваСтроки);
			СчКолонок = СчКолонок + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	Если МассивДанных.Количество() > 0 Тогда
		// Есть что сохранять
		Если Не СохранитьМассивДанныхВРегистр(ПервыйЗаход, ПервыйЗаход) Тогда // разберем данные и сохраним массивы данных
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПрочитатьTXT()

// Функция возвращает количество записей ADO
Функция ПолучитьКоличествоЗаписейADO() Экспорт
	Состояние_("Инициализация ADODB.Connection ...");
	Попытка
		Приложение = Новый COMОбъект("ADODB.Connection");
		Коннект = Приложение.Open(ПрайсЛистКонтрагента.СтрокаПодключения);
	Исключение
		СтатистикаЗагрузки.ОшибкиЗагрузки = "Ошибка подключения к источнику данных: "+ОписаниеОшибки();
		Возврат -1;
	КонецПопытки;
	Recordset = Новый COMОбъект("ADODB.Recordset");
	Recordset.ActiveConnection = Приложение;
	Recordset.CursorType = 3;
	Recordset.Open(СокрЛП(ПрайсЛистКонтрагента.ИмяТаблицы));
	КоличествоЗаписей = Recordset.RecordCount;
	Recordset.Close();
	Возврат КоличествоЗаписей;
КонецФункции // ПолучитьКоличествоЗаписейADO()

// функция чтения файла в прайс-лист
//
// Параметры:
// ТЗПрайсЛиста - ТаблицаЗначений - Таблица значений прайс-листа
//
Функция ПрочитатьADO() Экспорт
	ВсеОК = Истина;
	
	Состояние_("Инициализация ADODB.Connection ...");
	Попытка
		Приложение = Новый COMОбъект("ADODB.Connection");
		Коннект = Приложение.Open(ПрайсЛистКонтрагента.СтрокаПодключения);
	Исключение
		СтатистикаЗагрузки.Примечание = "Ошибка подключения к источнику данных: "+ОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;
	
	Recordset = Новый COMОбъект("ADODB.Recordset");
	Recordset.ActiveConnection = Приложение;
	Recordset.CursorType = 3;
	Recordset.Open(СокрЛП(ПрайсЛистКонтрагента.ИмяТаблицы));
	
	КоличествоЗаписей = Recordset.RecordCount;
	КолПолей = Recordset.Fields.Count;
	
	СтрокаЗагружаемойСтраницы = ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.Найти(ПрайсЛистКонтрагента.ИмяТаблицы, "ИмяЛиста");
	
	КоличествоЗаписей = Мин(КоличествоЗаписей, ?(СтрокаЗагружаемойСтраницы.СтрокаКонец > 0, СтрокаЗагружаемойСтраницы.СтрокаКонец, КоличествоЗаписей));
	
	#Если Клиент Тогда
	ФормаИндикатора = ПолучитьОбщуюФорму("Индикация");
	ФормаИндикатора.СтрокаСостояния = "Загрузка прайс-листа";
	ФормаИндикатора.Открыть();
	ФормаИндикатора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = КоличествоЗаписей;
	ФормаИндикатора.ЭлементыФормы.Индикатор.Шаг = КоличествоЗаписей/100;
	#КонецЕсли
	
	СтруктураФайлаПрайсЛиста = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Выгрузить();
	СчПолей = 0;
	Пока СчПолей < СтруктураФайлаПрайсЛиста.Количество() Цикл
		ПолеЗагрузки = СтруктураФайлаПрайсЛиста[СчПолей];
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла) Тогда
			СтруктураФайлаПрайсЛиста.Удалить(ПолеЗагрузки);
			Продолжить;
		КонецЕсли;
		СчПолей = СчПолей + 1;
	КонецЦикла;
	
	СтруктураКлючевыхПолей = ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста.Выгрузить(Новый Структура("Ключевое", Истина));
	СчПолей = 0;
	Пока СчПолей < СтруктураКлючевыхПолей.Количество() Цикл
		ПолеЗагрузки = СтруктураКлючевыхПолей[СчПолей];
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла) ИЛИ Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			СтруктураКлючевыхПолей.Удалить(ПолеЗагрузки);
			Продолжить;
		КонецЕсли; 
		СчПолей = СчПолей + 1;
	КонецЦикла;
	
	Сч = 0;
	Состояние_("Загрузка прайс-листа ...");
	Пока НЕ Recordset.EOF Цикл
		ОбработкаПрерыванияПользователя_();
		Сч = Сч + 1;
		
		#Если Клиент Тогда
		Если (Сч % 100) = 0 Тогда
			ФормаИндикатора.ЭлементыФормы.Индикатор.Значение = Сч;
		КонецЕсли;
		#КонецЕсли
		
		Если СтрокаЗагружаемойСтраницы.СтрокаНачало > 0 И Сч < СтрокаЗагружаемойСтраницы.СтрокаНачало Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаЗагружаемойСтраницы.СтрокаКонец > 0 И Сч > СтрокаЗагружаемойСтраницы.СтрокаКонец Тогда
			Прервать;
		КонецЕсли;
		
		КлючСтрокиПоставщика = "";
		Производитель = Неопределено;
		
		Для каждого ПолеЗагрузки Из СтруктураКлючевыхПолей Цикл
			Попытка 
				ЗначениеПоля = Recordset.Fields(ПолеЗагрузки.ИмяПоляФайла).Value;
				Если ТипЗнч(ЗначениеПоля) = Тип("Число") И Не ПрайсЛистКонтрагента.НеФорматироватьКлючевыеПоля Тогда
					ЗначениеПоля = Формат(ЗначениеПоля,"ЧГ=0");
				КонецЕсли;
				КлючСтрокиПоставщика = КлючСтрокиПоставщика + "_" + СокрЛП(ЗначениеПоля);
			Исключение
				СтатистикаЗагрузки.Примечание = "Поле таблицы '"+ПолеЗагрузки.ИмяПоляФайла+"', выбранное для реквизита <"+ПолеЗагрузки.ИмяРеквизитаПрайсЛиста+">,  отсутствует в источнике данных!";
				//Предупреждение_("Поле таблицы '"+ПолеЗагрузки.ИмяПоляФайла+"', выбранное для реквизита <"+ПолеЗагрузки.ИмяРеквизитаПрайсЛиста+">,  отсутствует в источнике данных!");
				#Если Клиент Тогда
				Попытка ФормаИндикатора.Закрыть(); Исключение КонецПопытки;
				#КонецЕсли
				Возврат Ложь;
			КонецПопытки;
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(КлючСтрокиПоставщика) Тогда
			Продолжить;
		КонецЕсли;
		
		Хэш = НРег(MD5(КлючСтрокиПоставщика));
		
		НоваяПозицияПрайсЛиста = ТЗПрайсЛиста.Добавить();
		НоваяПозицияПрайсЛиста.КодПредложения = Сч;
		НоваяПозицияПрайсЛиста.ПрайсЛист  = ПрайсЛистКонтрагента.Ссылка;
		НоваяПозицияПрайсЛиста.КлючСтрокиПоставщика = Хэш;
		
		ПолеНаименование = "";
		ПолеНаименованиеИностранное = "";
		
		Для каждого ПолеЗагрузки Из СтруктураФайлаПрайсЛиста Цикл
			Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
				Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Наименование" Тогда
					ПолеНаименование = ПолеНаименование + СтрЗаменить(ПолеЗагрузки.ИмяПоляФайла, """", "");
				ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "НаименованиеИностранное" Тогда
					ПолеНаименованиеИностранное = ПолеНаименованиеИностранное + СтрЗаменить(ПолеЗагрузки.ИмяПоляФайла, """", "");
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			Попытка
				ЗначениеПоля = Recordset.Fields(ПолеЗагрузки.ИмяПоляФайла).Value;
			Исключение
				СтатистикаЗагрузки.Примечание = "Поле таблицы '"+ПолеЗагрузки.ИмяПоляФайла+"', выбранное для реквизита <"+ПолеЗагрузки.ИмяРеквизитаПрайсЛиста+">,  отсутствует в источнике данных!";
				//Предупреждение_("Поле таблицы '"+ПолеЗагрузки.ИмяПоляФайла+"', выбранное для реквизита <"+ПолеЗагрузки.ИмяРеквизитаПрайсЛиста+">,  отсутствует в источнике данных!");
				#Если Клиент Тогда
				Попытка ФормаИндикатора.Закрыть(); Исключение КонецПопытки;
				#КонецЕсли
				Возврат Ложь;
			КонецПопытки;
			Если Рарус_Компонента <> Неопределено Тогда
				Если ТипЗнч(ЗначениеПоля) = Тип("Строка") И ПрайсЛистКонтрагента.КодоваяСтраница <> "cpCP1251" Тогда
					ЗначениеПоля = Рарус_Компонента.ПерекодироватьСтроку(ЗначениеПоля,ПрайсЛистКонтрагента.КодоваяСтраница,"cpCP1251");
				КонецЕсли;
			КонецЕсли;
			ТипЗнчРеквизита = ТипЗнч(НоваяПозицияПрайсЛиста[ПолеЗагрузки.ИмяРеквизитаПрайсЛиста]);
			Если ТипЗнчРеквизита = Тип("Строка") И ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
				ЗначениеПоля = Формат(ЗначениеПоля,"ЧГ=0");
			ИначеЕсли ТипЗнчРеквизита = Тип("Число") И ТипЗнч(ЗначениеПоля) = Тип("Строка") Тогда
				Попытка
					ЗначениеПоля = Число(ЗначениеПоля);
				Исключение
					ЗначениеПоля = 0;
				КонецПопытки;
			КонецЕсли;
			
			Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Наименование" Тогда
				ПолеНаименование = ПолеНаименование + ЗначениеПоля;
				Продолжить;
			ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "НаименованиеИностранное" Тогда
				ПолеНаименованиеИностранное = ПолеНаименованиеИностранное + ЗначениеПоля;
				Продолжить;
			КонецЕсли;
			
			Если ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Производитель" Тогда
				НоваяПозицияПрайсЛиста.ПроизводительВПрайсЛисте = ЗначениеПоля;
				НоваяПозицияПрайсЛиста.Производитель = РегистрыСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки.ПолучитьПроизводителя(ПрайсЛистКонтрагента, ЗначениеПоля, Истина, СопоставленныеПроизводители);
			ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Артикул" Тогда
				НоваяПозицияПрайсЛиста.Артикул = ЗначениеПоля;
				НоваяПозицияПрайсЛиста.АртикулВПрайсЛисте = ЗначениеПоля;
			ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Валюта" Тогда
				НоваяПозицияПрайсЛиста.Валюта = РазобратьВалюту(ЗначениеПоля);
			ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Цена" Тогда
				НоваяПозицияПрайсЛиста.Цена = РазобратьЦену(ЗначениеПоля);
			ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "Количество" Тогда
				НоваяПозицияПрайсЛиста.Количество = РазобратьКоличество(ЗначениеПоля);
				НоваяПозицияПрайсЛиста.КоличествоВПрайсЛисте = ЗначениеПоля;
			ИначеЕсли ПолеЗагрузки.ИмяРеквизитаПрайсЛиста = "СрокПоставки" Тогда
				РазобратьСрокПоставки(ЗначениеПоля, НоваяПозицияПрайсЛиста.СрокПоставки, НоваяПозицияПрайсЛиста.СрокПоставкиМинимальный, НоваяПозицияПрайсЛиста.СрокПоставкиМаксимальный);
				НоваяПозицияПрайсЛиста.СрокПоставкиВПрайсЛисте = ЗначениеПоля;
			Иначе
				НоваяПозицияПрайсЛиста[ПолеЗагрузки.ИмяРеквизитаПрайсЛиста] = ЗначениеПоля;
			КонецЕсли;
		КонецЦикла;
		
		НоваяПозицияПрайсЛиста.Наименование = ПолеНаименование;
		НоваяПозицияПрайсЛиста.НаименованиеИностранное = ПолеНаименованиеИностранное;
		//НоваяПозицияПрайсЛиста.АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(НоваяПозицияПрайсЛиста.Артикул, НоваяПозицияПрайсЛиста.Производитель);
		
		Recordset.MoveNext();
	КонецЦикла;
	
	Recordset.Close();
	
	#Если Клиент Тогда
	Попытка ФормаИндикатора.Закрыть(); Исключение КонецПопытки;
	#КонецЕсли
	
	Возврат ВсеОК;
КонецФункции // ПрочитатьADO()

// Процедура очистки прайс-листа
Функция ОчиститьПрайсЛист() Экспорт
	Если НЕ ОчиститьРегистрПрайсЛиста("ПрайсЛистыКонтрагентовВременный", Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;
	Если НЕ ОчиститьРегистрПрайсЛиста("ПрайсЛистыКонтрагентов", Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции // ОчиститьПрайсЛист()

Функция ОчиститьРегистрПрайсЛиста(ИмяРегистра, ДатаЗаписиПорцииДанных)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрайсЛистыКонтрагентов.ДатаЗаписи
		|ИЗ
		|	РегистрСведений."+ИмяРегистра+" КАК ПрайсЛистыКонтрагентов
		|ГДЕ
		|	ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист
		|	"+?(ДатаЗаписиПорцииДанных = Неопределено,"","И ПрайсЛистыКонтрагентов.ДатаЗаписи = &ДатаЗаписи")+"
		|";
	Запрос.УстановитьПараметр("ПрайсЛист" , ПрайсЛистКонтрагента.Ссылка);
	Запрос.УстановитьПараметр("ДатаЗаписи", ДатаЗаписиПорцииДанных);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		//Заблокируем Данные записи регистра "ИмяРегистра"
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений." + ИмяРегистра);
		ЭлементБлокировки.УстановитьЗначение("ПрайсЛист" , ПрайсЛистКонтрагента.Ссылка);
		ЭлементБлокировки.УстановитьЗначение("ДатаЗаписи", Выборка.ДатаЗаписи);
		
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		//Очистим набор записей
		РегистрПрайсЛистов = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
		РегистрПрайсЛистов.Отбор.ПрайсЛист.Значение = ПрайсЛистКонтрагента.Ссылка;
		РегистрПрайсЛистов.Отбор.ПрайсЛист.Использование = Истина;
		
		РегистрПрайсЛистов.Отбор.ДатаЗаписи.Значение = Выборка.ДатаЗаписи;
		РегистрПрайсЛистов.Отбор.ДатаЗаписи.Использование = Истина;
		
		РегистрПрайсЛистов.Очистить();
		
		РегистрПрайсЛистов.ОбменДанными.Загрузка = Истина;
		
		Попытка
			РегистрПрайсЛистов.Записать();
		Исключение
			СтрокаОшибки = "Ошибка очистки регистра "+ИмяРегистра+": "+ОписаниеОшибки();
			СтатистикаЗагрузки.Примечание = СтрокаОшибки;
			ОтменитьТранзакцию();
			Возврат Ложь;
		КонецПопытки; 
		ЗафиксироватьТранзакцию();
	КонецЦикла;
	Возврат Истина;
КонецФункции //ОчиститьРегистрПрайсЛиста()

Функция ПроверитьКорректностьКолонок()
	ЕстьОшибки = Ложь;
	Для каждого ПолеЗагрузки Из ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста Цикл
		Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			Продолжить;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ПолеЗагрузки.ИмяПоляФайла) Тогда
			Если ПолеЗагрузки.Ключевое Тогда
				ЕстьОшибки = Истина;
				СтатистикаЗагрузки.Примечание = ?(НЕ ПустаяСтрока(СтатистикаЗагрузки.Примечание), СтатистикаЗагрузки.Примечание + Символы.ПС, "") + "Не указано Поле для реквизита <"+ПолеЗагрузки.ИмяРеквизитаПрайсЛиста+">, заданного как Ключ строки!";
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		//колонка может идентифицироваться только номером!
		Колонка_Источник = ПолеЗагрузки.ИмяПоляФайла;
		//попытаемся преобразовать в число
		Попытка
			Колонка_Источник = Число(Колонка_Источник);
		Исключение
			ЕстьОшибки = Истина;
			СтатистикаЗагрузки.Примечание = ?(НЕ ПустаяСтрока(СтатистикаЗагрузки.Примечание), СтатистикаЗагрузки.Примечание + Символы.ПС, "") + "Поле файла '"+Колонка_Источник+"', выбранное для реквизита <"+ПолеЗагрузки.ИмяРеквизитаПрайсЛиста+">,  невозможно преобразовать в число!";
		КонецПопытки;
	КонецЦикла;	
	Возврат Не ЕстьОшибки;
КонецФункции //ПроверитьКорректностьКолонок()

Функция ПроверитьКорректностьСтруктурыПрайса()
	
	ЕстьОшибки = Ложь;
	
	МассивСтрок = ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.НайтиСтроки(Новый Структура("ИмяЛиста, Использовать", ПрайсЛистКонтрагента.ИмяТаблицы, Истина));
	Если МассивСтрок.Количество() = 0 Тогда
		ЕстьОшибки = Истина;
		СтатистикаЗагрузки.Примечание = ?(НЕ ПустаяСтрока(СтатистикаЗагрузки.Примечание), СтатистикаЗагрузки.Примечание + Символы.ПС, "")+ "Не указана главная страница данных!";
	КонецЕсли;
	
	МассивСтрок = ПрайсЛистКонтрагента.СтруктураСтраницПрайсЛиста.НайтиСтроки(Новый Структура("Использовать", Истина));
	Если МассивСтрок.Количество() = 0 Тогда
		ЕстьОшибки = Истина;
		СтатистикаЗагрузки.Примечание = ?(НЕ ПустаяСтрока(СтатистикаЗагрузки.Примечание), СтатистикаЗагрузки.Примечание + Символы.ПС, "")+ "Не указаны листы, с которых производится загрузка данных прайс-листа!";
	КонецЕсли;
	
	//Для Каждого Строка ИЗ МассивСтрок Цикл
	//	Если Строка.СтрокаЗаголовковКолонок = 0 Тогда
	//		ЕстьОшибки = Истина;
	//		СтатистикаЗагрузки.Примечание = ?(НЕ ПустаяСтрока(СтатистикаЗагрузки.Примечание), СтатистикаЗагрузки.Примечание + Символы.ПС, "") + "У листа """ + Строка.ИмяЛиста + """ не указан номер строки заголовков!";
	//	КонецЕсли;
	//	Если Строка.СтрокаНачало = 0 Тогда
	//		// Обязательно должна быть
	//		ЕстьОшибки = Истина;
	//		СтатистикаЗагрузки.Примечание = ?(НЕ ПустаяСтрока(СтатистикаЗагрузки.Примечание), СтатистикаЗагрузки.Примечание + Символы.ПС, "") + "У листа """ + Строка.ИмяЛиста + """ не указан номер строки начала данных!";
	//	КонецЕсли;
	//КонецЦикла;
	
	Возврат Не ЕстьОшибки;
	
КонецФункции //ПроверитьКорректностьСтруктурыПрайса()

Функция ПроверитьФайл(СтрокаПодключения)
	
	Если обЗначениеНеЗаполнено(СтрокаПодключения) Тогда
		СтатистикаЗагрузки.Примечание = "Не указана строка подключения!";
		Возврат Ложь;
	КонецЕсли;
		
	Если ПрайсЛистКонтрагента.ФайлИсточникДанных Тогда 
		
		//Проверим наличие файла
		Файл = Новый Файл(СтрокаПодключения);
		Если НЕ Файл.Существует() ИЛИ НЕ Файл.ЭтоФайл() Тогда
			СтатистикаЗагрузки.Примечание = "Файл прайс-листа <"+СтрокаПодключения+"> не найден.";
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ПрайсЛистКонтрагента.ИмяТаблицы) Тогда
		СтатистикаЗагрузки.Примечание = "Не указана таблица/главная страница данных!";
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьЗаполненныеПоля = Ложь;
	ЕстьКлючевыеПоля = Ложь;
	
	Для Каждого ПолеЗагрузки Из ПрайсЛистКонтрагента.СтруктураФайлаПрайсЛиста Цикл
		Если Найти(ПолеЗагрузки.ИмяПоляФайла, """") > 0 Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолеЗагрузки.ИмяПоляФайла) Тогда
			ЕстьЗаполненныеПоля = Истина;
			Если ПолеЗагрузки.Ключевое Тогда
				ЕстьКлючевыеПоля = Истина;
			КонецЕсли;
		Иначе
			Если ПолеЗагрузки.Ключевое Тогда
				// Обязательно к заполнению
				СтатистикаЗагрузки.Примечание = "Не указано поле файла для реквизита <"+ПолеЗагрузки.ИмяРеквизитаПрайсЛиста+">!";
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьЗаполненныеПоля Тогда
		// если поля не заполнены продолжать не имеет смысла
		СтатистикаЗагрузки.Примечание = "Ни одно поле таблицы не заполнено";
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЕстьКлючевыеПоля Тогда
		СтатистикаЗагрузки.Примечание = "Не задано ни одного 'Ключевого' поля";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции //ПроверитьФайл()

// процедура загрузки прайс-листа
//
// Параметры:
// СтрокаПодключения - Строка - Адрес загружаемого файла
//
Функция ЗагрузитьПрайсЛист(СтрокаПодключения)
	
	РезультатПроверкиФайла = ПроверитьФайл(СтрокаПодключения);
	
	Если РезультатПроверкиФайла = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДатаПрайсЛиста = ТекущаяДата();
	ИмяФайлаЗагрузки = "";
	ТипФайла = "";
	
	Если ПрайсЛистКонтрагента.ФайлИсточникДанных Тогда
		
		Файл = Новый Файл(СтрокаПодключения);
		ИмяФайлаЗагрузки = Файл.Имя;
		ТипФайла = ВРег(Файл.Расширение);
		ДатаПрайсЛиста = Файл.ПолучитьВремяИзменения();
		
		Если Найти(ТипФайла, ".XL") > 0 
			ИЛИ ТипФайла = ".TXT" 
			ИЛИ ТипФайла = ".CSV" 
			ИЛИ ТипФайла = ".ODS" 
			ИЛИ ТипФайла = ".OTS" Тогда
			
			Если Не ПроверитьКорректностьКолонок() ИЛИ НЕ ПроверитьКорректностьСтруктурыПрайса()Тогда
				Возврат Ложь;
			КонецЕсли;
			
		Иначе
			
			СтатистикаЗагрузки.Примечание = "Недопустимый тип файла!";
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтатистикаЗагрузки.ДатаФайла = ДатаПрайсЛиста;
	СтатистикаЗагрузки.СтрокаПодключения = СтрокаПодключения;
	
	//Получим набор записей регистра сведений
	РегистрПрайсЛистов = РегистрыСведений.ПрайсЛистыКонтрагентовВременный.СоздатьНаборЗаписей();
	РегистрПрайсЛистов.Отбор.ПрайсЛист.Значение = ПрайсЛистКонтрагента.Ссылка;
	РегистрПрайсЛистов.Отбор.ПрайсЛист.Использование = Истина;
	
	// Временно работаем со внутренним хранилищем!!
	РегистрПрайсЛистовОсновной = РегистрыСведений.ПрайсЛистыКонтрагентов.СоздатьНаборЗаписей();
	РегистрПрайсЛистовОсновной.Отбор.ПрайсЛист.Значение = ПрайсЛистКонтрагента.Ссылка;
	РегистрПрайсЛистовОсновной.Отбор.ПрайсЛист.Использование = Истина;
	
	РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(СтатистикаЗагрузки, ПрайсЛистКонтрагента);
	
	Состояние_("Удаление старых данных ...");
	
	// Это загрузка регулярного прайс-листа
	Если Не ОчиститьРегистрПрайсЛиста("ПрайсЛистыКонтрагентовВременный", Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(СтатистикаЗагрузки, ПрайсЛистКонтрагента);
	
	// Здесь у нас всегда чистая таблица
	ТЗПрайсЛиста = РегистрПрайсЛистов.Выгрузить();
	ТЗПрайсЛиста.Индексы.Добавить("КлючСтрокиПоставщика");
	ТЗПрайсЛиста.Индексы.Добавить("ПроизводительВПрайсЛисте");
	ТЗПрайсЛиста.Индексы.Добавить("Производитель");
	
	Если ПрайсЛистКонтрагента.ФайлИсточникДанных Тогда
		
		Если Найти(ТипФайла,".XL") > 0 Тогда
			РезультатЗагрузки = ПрочитатьXLS(СтрокаПодключения);
		ИначеЕсли ТипФайла = ".ODS" ИЛИ ТипФайла = ".OTS" Тогда
			РезультатЗагрузки = ПрочитатьОО(СтрокаПодключения);
			Если Не ОбработатьТЗПрайсЛиста() Тогда
				Возврат Ложь;
			КонецЕсли;
		ИначеЕсли ТипФайла = ".TXT" ИЛИ ТипФайла = ".CSV" Тогда
			РезультатЗагрузки = ПрочитатьTXT(СтрокаПодключения);
		КонецЕсли;
		
	Иначе //Грузим из ADO
		
		РезультатЗагрузки = ПрочитатьADO();
		Если Не ОбработатьТЗПрайсЛиста() Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
		
	Если НЕ РезультатЗагрузки = Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(СтатистикаЗагрузки, ПрайсЛистКонтрагента);
	
	ТЗПрайсЛиста.Очистить();
	РегистрПрайсЛистов = Неопределено;
	
	Если ПрайсЛистКонтрагента.ВидПрайсЛиста <> Перечисления.ВидыПрайсЛистов.КаталогПредложений Тогда
		
		РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(СтатистикаЗагрузки, ПрайсЛистКонтрагента);
		
		РегистрыСведений.ПрайсЛистыКонтрагентов.ОбновитьЗаписиВОсновномРегистре(ПрайсЛистКонтрагента, МаксПорцияЗаписи);
		
		//ОТРАБОТАТЬ СИТУАЦИЮ КОГДА НЕ УДАЛОСЬ ЗАПИСАТЬ ДАННЫЕ В ПОСТОЯННЫЙ РЕГИСТР
		
	КонецЕсли;
	
	Возврат Истина;

КонецФункции // ЗагрузитьПрайсЛист()

// процедура загрузки прайс-листа
//
// Параметры:
// СтрокаПодключения - Строка - Полный адрес прайс-листа
//
Функция ЗагрузитьПрайсЛистДляРазбора(СтрокаПодключения = Неопределено, КлючЗадания = Неопределено) Экспорт
	
	Если ПрайсЛистКонтрагента.ЭтоГруппа Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ПрайсЛистКонтрагента.ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ВебПрайсЛист ИЛИ (Не ПрайсЛистКонтрагента.ФайлИсточникДанных И Не ПрайсЛистКонтрагента.ХранитьДанныеЛокально) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СопоставленныеПроизводители = РегистрыСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки.ПолучитьТаблицуСопоставленныхПроизводителей(ПрайсЛистКонтрагента);
	
	Если обЗначениеНеЗаполнено(СтрокаПодключения) Тогда
		СтрокаПодключения = СокрЛП(ПрайсЛистКонтрагента.СтрокаПодключения);
	КонецЕсли;

	СтатистикаЗагрузки.СтрокаПодключения = СтрокаПодключения;
	СтатистикаЗагрузки.Статус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Загружается;
	Если КлючЗадания <> Неопределено Тогда
		 СтатистикаЗагрузки.ИдентификаторФоновогоЗадания = КлючЗадания;
	КонецЕсли;
	РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(СтатистикаЗагрузки, ПрайсЛистКонтрагента);
	
	РезультатЗагрузки = ЗагрузитьПрайсЛист(СтрокаПодключения);
	
	Если РезультатЗагрузки <> Неопределено Тогда
		Если РезультатЗагрузки Тогда
			СтатистикаЗагрузки.Статус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Завершена;
		Иначе
			СтатистикаЗагрузки.Статус = Перечисления.СтатусыЗагрузкиПрайсЛистов.Ошибка;
		КонецЕсли;
		РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(СтатистикаЗагрузки, ПрайсЛистКонтрагента);
		
	Иначе
		СтатистикаЗагрузки.Примечание = "Загрузка не требуется";
		РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(СтатистикаЗагрузки, ПрайсЛистКонтрагента);
	КонецЕсли;
	
	Возврат РезультатЗагрузки;
	
КонецФункции // ЗагрузитьПрайсЛистДляРазбора()



////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Рарус_Компонента = Неопределено;

#Если Клиент Тогда
	Рарус_Компонента = глРарус_Компонента;
#КонецЕсли

СоответствиеВалют     = Новый Соответствие;
СоответствиеЦен       = Новый Соответствие;
СоответствиеКоличеств = Новый Соответствие;
МаксПорцияЗаписи      = 2000;

СтатистикаЗагрузки = РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СформироватьСтруктуруЖурналаЗагрузки();

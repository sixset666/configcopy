// НАЗНАЧЕНИЕ РЕКВИЗИТОВ ОБРАБОТКИ:
//
// РЕКВИЗИТЫ ОПРЕДЕЛЯЮЩИЕ ИМЕНА РЕКВИЗИТОВ ДОКУМЕНТА, ОТВЕЧАЮЩИХ ЗА СКИДКИ.
// (используются, если необходимо задать альтернативные имена реквизитов документа).
// ИмяТабличнойЧасти                 - имя табличной части, для которой применяются скидки.
// По умолчанию имеет значение "Товары".
// ИмяРеквизитаПодразделениеКомпании - имя реквизита документа,
// в котором хранится подразделение компании. По умолчанию - "ПодразделениеКомпании".
// ИмяРеквизитаСкладКомпании         - имя реквизита документа,
// в котором хранится склад компании. По умолчанию - "СкладКомпании".
// ИмяРеквизитаКарточка              - имя реквизита документа, в котором хранится карточка.
// По умолчанию - "Карточка".
// ИмяРеквизитаСкидкаНаценка         - имя реквизита документа,
// в котором хранится скидка. По умолчанию - "СкидкаНаценка".
// ИмяРеквизитаЗначениеСкидкиНаценки - имя реквизита документа,
// в котором хранится значение подобранной "шапочной" скидки. По умолчанию - "ЗначениеСкидкиНаценки".
// ИмяРеквизитаСуммаСкидкиНаценки    - имя реквизита документа,
// в котором хранится итоговая сумма скидки ("Шапочная" плюс сумма всех товарных скидок).
//По умолчанию - "СуммаСкидкиНаценки".

// РЕКВИЗИТЫ ОТВЕЧАЮЩИЕ ЗА ПЕРЕСЧЕТ КЭША. (Если новое и текущее значения отличаются,
// то выполняется пересчет кэшей).
// ПодразделениеКомпании                - новое значение подразделения компании.
// ПодразделениеКомпанииСтароеЗначение  - текущее значение подразделения компании.
// СкладКомпании                        - новое значение склада.
// СкладКомпанииСтароеЗначение          - текущее значение склада.
// СкидкаНаценка                        - новая "шапочная" скидки. (только если тип скидки - ручная)
// СкидкаНаценкаСтароеЗначение          - текущее значение "шапочной" скидки.
// Дата                                 - новая дата документа.
// ДатаСтароеЗначение                   - текущее значение даты.
// Карточка                             - новая дисконтная карточка.
// КарточкаСтароеЗначение               - текущая дисконтная карточка.
// СкладСтроки                          - склад текущей строки. Имеет смысл, если в табличной
//                                        части можно указывать различные склады для отгрузки.
//                                        Если текущий склад строки не равен данному реквизиту
//                                        и реквизиту "СкладКомпании", то выполняется пересчет
//                                        кэша товарных скидок.

// БазаДляРасчетаСкидки  - содержит сумму документа (итог по колонке "Сумма").
// Используется для определения базы для расчета, и порога применения скидки.

// РЕКВИЗИТЫ ДЛЯ ОПРЕДЕЛЕНИЯ НЕОБХОДИМОСТИ ПЕРЕСЧЕТА ТАБЛИЧНОЙ ЧАСТИ ДОКУМЕНТА
// (Если найденная новая скидка, отличается по типу или значению от
// текущей в документе, то необходимо выполнить пересчет всей табличной части документа).
//
// ТекущаяСкидкаНаценка  - текущая скидка/наценка в документе.
// ТекущееЗначениеСкидки - текущее значение скидки
// ТекущаяСуммаСкидки    - текущая сумма скидки.

// СпособВыбораСкидки      - содержит значение права "СпособВыбораСкидки".
// Для уменьшения количества обращений к правам. 

// РЕКВИЗИТЫ ДЛЯ ХРАНЕНИЯ ИНФОРМАЦИИ О ТЕКУЩЕЙ ПОДОБРАННОЙ ТОВАРНОЙ СКИДКЕ.
// ТекущаяСкидкаСтроки          - тип подобранной товарной скидки.
// ТекущийПроцентСкидкиСтроки   - процент товарной скидки.
// ТекущаяСуммаСкидкиСтроки     - рассчитанная сумма товарной скидки для текущей строки. 
// НеРассчитыватьСтрочныеСкидки - признак отказа от подбора строчных скидок.
// ВНИМАНИЕ!!! Если в документе используются ТОЛЬКО шапочные скидки,
// НеРассчитыватьАвтоматическиеСкидки - признак отказа от подбора автоматических скидок.
// ВНИМАНИЕ!!! Если в документе используются ТОЛЬКО ручные скидки,
// то необходимо в модуле соответствующего документа при инициализации 
// обработки присваивать данному реквизиту ИСТИНА, иначе будут ошибки.
// Так сделано, чтобы избежать дополнительных проверок на необходимость расчета товарных скидок.

// КэшИнициализирован - признак, что кэш "шапочных" скидок проинициализирован.

Перем КоэффициентПересчета; // Коэффициент пересчета из валюты накопительных сумм в регламентированную.


//////////////////////////////////////////////////////////////////////////////////
// ЛОКАЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//////////////////////////////////////////////////////////////////////////////////   

// Получение суммы табличной части документа без скидки
// Параметры
//  ЭтотОбъект	– ДокументОбъект – документ, сумму которого необходимо получить
//  Товары		– ТабличнаяЧасть – табличная часть документа, сумму по которой необходимо получить
// Возвращаемое значение:
//   Число		– Сумма документа без скидки
Функция ПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, Знач Товары = Неопределено, МассивСтрокСВытеснением = Неопределено, ИмяТЧ ="")
	
	СуммаДокументаБезСкидки = орПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, ИмяТЧ, МассивСтрокСВытеснением);
	Если СуммаДокументаБезСкидки <> Неопределено Тогда
		Возврат СуммаДокументаБезСкидки;
	КонецЕсли; 
	
	// Получим табличную часть документа
	Попытка
		Товары = ?(Товары = Неопределено, ЭтотОбъект.Товары, Товары);
	Исключение КонецПопытки;
	
	// Если так и не удалось получить табличную часть или она не содержит ни одной строки, то сумма документа равна "0"
	Если (Товары = Неопределено)ИЛИ(Товары.Количество() = 0) Тогда
		Возврат 0;
	КонецЕсли;
	
	ЕстьВытеснение = Ложь;
	Если ТипЗнч(МассивСтрокСВытеснением) = Тип("Массив") Тогда
		ЕстьВытеснение = Истина;
	КонецЕсли;
	
	// Определим включают ли в себя НДС цены документа
	ЦенаВключаетНДС = ИСТИНА;
	Попытка
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
	Исключение
	КонецПопытки;
	
	СуммаДокументаБезСкидки = 0;
	Попытка
		Если ЕстьВытеснение Тогда
			КоличествоСтрок = Товары.Количество()-1;
			Для Инд = 0 По КоличествоСтрок Цикл
				Если НЕ МассивСтрокСВытеснением.Найти(Инд) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ТекСтрока = Товары[Инд];
				Если ЦенаВключаетНДС Тогда
					// Получим сумму документу без учета НДС и скидки
					СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ТекСтрока.Сумма;
				Иначе
					//Если цена не включает НДС, то получим суммарную НДС по всем строкам
					СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + ТекСтрока.Сумма + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
				КонецЕсли;
			КонецЦикла;
		Иначе
			// Получим сумму документу без учета НДС и скидки
			СуммаДокументаБезСкидки = Товары.Итог("Сумма");
			//Если цена не включает НДС, то получим суммарную НДС по всем строкам
			Если НЕ ЦенаВключаетНДС Тогда
				Попытка
					Для Каждого ТекСтрока Из Товары Цикл
						СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
					КонецЦикла;
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Возврат СуммаДокументаБезСкидки;
	
КонецФункции // ПолучитьСуммуДокументаБезСкидки()

// Процедура заполняет кэш скидок (табличная часть "Кэш скидок") подходящими под критерии поиска 
// строками регистра сведений "СкидкиШапки".
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура ОбновитьКэшСкидок()
		
	// 1. Очищаем кэш.
	КэшСкидок.Очистить();
		  		
	// 3. Создадим объект запроса
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Дата",                 Дата); 
	Запрос.УстановитьПараметр("ДеньНедели",	          ДеньНедели(Дата)); 
	Запрос.УстановитьПараметр("Время",			      '00010101' + (Дата - НачалоДня(Дата)));
	Запрос.УстановитьПараметр("КоэффициентПересчета", КоэффициентПересчета); 
		         
	// Список иерархических параметров
	Иерархические = Новый Структура();
	Иерархические.Вставить("ПодразделениеКомпании",	ПодразделениеКомпании);	
	Иерархические.Вставить("Карточка",	            Карточка);
	Иерархические.Вставить("СкладКомпании",	        СкладКомпании);
		
	
	// Установим иерархические параметры
	// Получать всех родителей будем через обращение к реквизиту "Родитель". Как показали тесты,
	// этот вариант является более быстродейственным по сравнению с вариантом, при котором формируется
	// текст вложенного запроса (состоящего из объединения нескольких подзапросов к Справочник.Родитель.Родитель...)
	// а также более эффективен, чем получение списка всех родителей через запрос с итогом по иерархии.
	Для Каждого ТекПараметр Из Иерархические Цикл
		Элемент = ТекПараметр.Значение;
		МассивЗначений = Новый Массив();		
		Пока НЕ Элемент.Пустая() Цикл  			
			МассивЗначений.Добавить(Элемент);
			Элемент = Элемент.Родитель;
		КонецЦикла;
		
		// Если пользователь задал какую-нибудь конкретную карточку (или склад), то поиск подходящей скидки будет
		// произведен не только среди записей с данной карточкой (складом), но и среди записей, у которых
		// карта (склад) не задана.
		Если ТекПараметр.Ключ="Карточка" Тогда
			МассивЗначений.Добавить(Справочники.Карточки.ПустаяСсылка());		
		ИначеЕсли ТекПараметр.Ключ="СкладКомпании" Тогда	
			МассивЗначений.Добавить(Справочники.СкладыКомпании.ПустаяСсылка());	
		КонецЕсли;
		
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, МассивЗначений);
	КонецЦикла;
	
	СтрокаДопУсловия = "";
	// Если задана конкретная скидка, значит получим только ее значения.
	// Иначе получаем только автоматические скидки.
	Если СкидкаНаценка.Пустая() ИЛИ (Не СкидкаНаценка.РучнаяСкидка) Тогда		
		СтрокаДопУсловия = СтрокаДопУсловия + " И Не РучнаяСкидка";	
	Иначе	
		СтрокаДопУсловия = СтрокаДопУсловия + " И Скидка = &Скидка";
		Запрос.УстановитьПараметр("Скидка", СкидкаНаценка);		
	КонецЕсли;	
	
	// Сформируем текст запроса
	Запрос.Текст = "ВЫБРАТЬ
	| Скидка,
	| СпособВычисления,
	| Скидка.ВидСвойства КАк ВидСвойства,
	| Свойство,
	| ЗначениеСкидки,
	| ОтСуммыЧека,
	| ЭтоПодарок,
	| ВидПрайсЛистаПодарка,
	| МаксимальнаяСуммаПодарка,
	| ФлагВытеснения
	|ИЗ
	|	РегистрСведений.СкидкиШапки.СрезПоследних(
	|		&Дата,		
	|		(&Время МЕЖДУ НачВремя И КонВремя)
	|		И ПОДСТРОКА(ДниНедели, &ДеньНедели, 1) = ""1""
	|		И Подразделение В (&ПодразделениеКомпании)
	|		"+?(НеРассчитыватьАвтоматическиеСкидки,"И РучнаяСкидка=ИСТИНА","")+"
	|		И ДисконтнаяКарта В (&Карточка)
	|		И ЗалОбслуживания В (&СкладКомпании)
	|		И "+?(СкидкаНаРаботы,"СкидкаНаРаботы=ИСТИНА","СкидкаНаТовары=ИСТИНА")+"
	|		//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ) КАК СкидкиШапкиСрезПоследних";
			
	// Установим дополнительные фильтры
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ", СтрокаДопУсловия);
	
	// Если требуется получить скидку по конкретной карточке, то необходимо также сделать подзапрос
	// и к накопленным суммам на этой карточке.
    Если Не Карточка.Пустая() Тогда
        Запрос.Текст = Запрос.Текст + "
        |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	(ВЫБРАТЬ
        |			ЕСТЬNULL(СУММА(НакоплениеСумм.Сумма), 0) * &КоэффициентПересчета КАК Сумма
        |		ИЗ
        |			РегистрСведений.НакоплениеСумм КАК НакоплениеСумм
        |		ГДЕ
        |			НакоплениеСумм.ПериодНакопления <= &Дата
        |			И НакоплениеСумм.Карточка = &КарточкаЗначение) КАК СуммыНакопления        
		|ПО
		|	ИСТИНА
        |ГДЕ
        |	СкидкиШапкиСрезПоследних.Действует И
        |	(СкидкиШапкиСрезПоследних.ДисконтнаяКарта = ЗНАЧЕНИЕ(Справочник.Карточки.ПустаяСсылка) ИЛИ СкидкиШапкиСрезПоследних.ОтСуммыНакопленияНаКарте <= ЕСТЬNULL(СуммыНакопления.Сумма, 0))";
        Запрос.УстановитьПараметр("КарточкаЗначение", Карточка);
    Иначе
        // Необходимо отсечь скидки, действие которых уже завершено. 
        // Данное условие нельзя помещать в параметры виртуальной таблицы, т.к. запрос будет работать неверно.
        Запрос.Текст = Запрос.Текст + "
        | ГДЕ 
        |	СкидкиШапкиСрезПоследних.Действует";
    КонецЕсли;  
	
	// Заполняем кэш.
	КэшСкидок.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры // ОбновитьКэшСкидок() 

// Процедура заполняет кэш строчных скидок (табличная часть "Кэш строчных скидок") подходящими под критерии поиска 
// строками регистра сведений "СкидкиСтроки".
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура ОбновитьКэшСтрочныхСкидок()
		
	// 1. Очищаем кэш.
	КэшСтрочныхСкидок.Очистить();
		  		
	// 3. Создадим объект запроса
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Дата",                 Дата); 
	Запрос.УстановитьПараметр("ДеньНедели",	          ДеньНедели(Дата)); 
	Запрос.УстановитьПараметр("Время",			      '00010101' + (Дата - НачалоДня(Дата)));
	Запрос.УстановитьПараметр("КоэффициентПересчета", КоэффициентПересчета); 
		         
	// Список иерархических параметров
	Иерархические = Новый Структура();
	Иерархические.Вставить("ПодразделениеКомпании",	ПодразделениеКомпании);	
	Иерархические.Вставить("Карточка",	            Карточка);
	Иерархические.Вставить("СкладКомпании",	        СкладКомпании);
		
	
	// Установим иерархические параметры
	// Получать всех родителей будем через обращение к реквизиту "Родитель". Как показали тесты,
	// этот вариант является более быстродейственным по сравнению с вариантом, при котором формируется
	// текст вложенного запроса (состоящего из объединения нескольких подзапросов к Справочник.Родитель.Родитель...)
	// а также более эффективен, чем получение списка всех родителей через запрос с итогом по иерархии.
	Для Каждого ТекПараметр Из Иерархические Цикл
		Элемент = ТекПараметр.Значение;
		МассивЗначений = Новый Массив();		
		Пока НЕ Элемент.Пустая() Цикл  			
			МассивЗначений.Добавить(Элемент);
			Элемент = Элемент.Родитель;
		КонецЦикла;
		
		// Если пользователь задал какую-нибудь конкретную карточку (или склад), то поиск подходящей скидки будет
		// произведен не только среди записей с данной карточкой (складом), но и среди записей, у которых
		// карта (склад) не задана.
		Если ТекПараметр.Ключ="Карточка" Тогда
			МассивЗначений.Добавить(Справочники.Карточки.ПустаяСсылка());		
		ИначеЕсли ТекПараметр.Ключ="СкладКомпании" Тогда
			МассивЗначений.Добавить(СкладСтроки);
			МассивЗначений.Добавить(Справочники.СкладыКомпании.ПустаяСсылка());	
		КонецЕсли;
		
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, МассивЗначений);
	КонецЦикла;
	
	СтрокаДопУсловия = "";
	Если СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.АвтоматическиеСкидки Тогда   					
		СтрокаДопУсловия = СтрокаДопУсловия + " И Не РучнаяСкидка";	
	ИначеЕсли (СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидки) Или (СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов) Тогда
		СтрокаДопУсловия = СтрокаДопУсловия + " И РучнаяСкидка";	
	КонецЕсли;
			
	// Сформируем текст запроса
	Запрос.Текст = "ВЫБРАТЬ
	| РучнаяСкидка,
	| Объект,
	| Скидка, 	
	| Скидка.ВидСвойства КАК ВидСвойства,
	| Свойство,
	| СпособВычисления,
	| ЗначениеСкидки,
	| ОтСуммыСтроки,
	| ОтКоличества,
	| Правило,
	| ЭтоПодарок,
	| ВидПрайсЛистаПодарка,
	| МаксимальнаяСуммаПодарка,
	| ФлагВытеснения
	|ИЗ
	|	РегистрСведений.СкидкиСтроки.СрезПоследних(
	|		&Дата,		
	|		(&Время МЕЖДУ НачВремя И КонВремя)
	|		И ПОДСТРОКА(ДниНедели, &ДеньНедели, 1) = ""1""
	|		И Подразделение В (&ПодразделениеКомпании)
	|		"+?(НеРассчитыватьАвтоматическиеСкидки,"И РучнаяСкидка=ИСТИНА","")+"
	|		И ДисконтнаяКарта В (&Карточка)
	|		И ЗалОбслуживания В (&СкладКомпании)
	|		И "+?(СкидкаНаРаботы,"СкидкаНаРаботы=ИСТИНА","СкидкаНаТовары=ИСТИНА")+"
	|		//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ) КАК СкидкиСтрокиСрезПоследних";
			
	// Установим дополнительные фильтры
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ", СтрокаДопУсловия);
			
	// Если требуется получить скидку по конкретной карточке, то необходимо также сделать подзапрос
	// и к накопленным суммам на этой карточке.
    Если Не Карточка.Пустая() Тогда
        Запрос.Текст = Запрос.Текст + "
        |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	(ВЫБРАТЬ
        |			ЕСТЬNULL(СУММА(НакоплениеСумм.Сумма), 0) * &КоэффициентПересчета КАК Сумма
        |		ИЗ
        |			РегистрСведений.НакоплениеСумм КАК НакоплениеСумм
        |		ГДЕ
        |			НакоплениеСумм.ПериодНакопления <= &Дата
        |			И НакоплениеСумм.Карточка = &КарточкаЗначение) КАК СуммыНакопления        
		|ПО
		|	ИСТИНА
        |ГДЕ
        |	СкидкиСтрокиСрезПоследних.Действует И
        |	(СкидкиСтрокиСрезПоследних.ДисконтнаяКарта = ЗНАЧЕНИЕ(Справочник.Карточки.ПустаяСсылка) ИЛИ СкидкиСтрокиСрезПоследних.ОтСуммыНакопленияНаКарте <= ЕСТЬNULL(СуммыНакопления.Сумма, 0))";
        Запрос.УстановитьПараметр("КарточкаЗначение", Карточка);
    Иначе
        // Необходимо отсечь скидки, действие которых уже завершено. 
        // Данное условие нельзя помещать в параметры виртуальной таблицы, т.к. запрос будет работать неверно.
        Запрос.Текст = Запрос.Текст + "
        | ГДЕ 
        |	СкидкиСтрокиСрезПоследних.Действует";
    КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст + "
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоПодарок		УБЫВ,
	|	ФлагВытеснения	УБЫВ
	|";
	
	// Заполняем кэш.
	КэшСтрочныхСкидок.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры // ОбновитьКэшСтрочныхСкидок() 


//////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//////////////////////////////////////////////////////////////////////////////////

// Функция осуществляет поиск подходящей скидки для документа. 
//
// Параметры:
//
// Возвращаемое значение:
//   Булево - Истина: Необходим пересчет табличной части в документе; Ложь: Подобранная скидка не отличается от текущей.
// 
Функция ПодобратьСкидку(ДокументОбъект, ИзПодбораСтрочнойСкидки = Ложь) Экспорт
			
	// 1. Вызываем отраслевую функцию.
	РезультатРаботы = орПолучитьСкидкуШапки(ДокументОбъект, ЭтотОбъект);
	Если РезультатРаботы<>Неопределено Тогда
		Возврат РезультатРаботы;
	КонецЕсли;
		
	Если Не КэшИнициализирован Тогда
		// Запоминаем текущее значение права.
		СпособВыбораСкидки = обПраво("СпособВыбораСкидки", ДокументОбъект.Права,,ДокументОбъект);
		КэшИнициализирован = Истина;
	КонецЕсли;
	
	Если СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.СкидкиЗапрещены Тогда
		// Сразу возвращаемся. Пересчет ТЧ не делаем.
		Возврат Ложь;  		
	КонецЕсли;
	
	// 2. Инициализация "кэшеобразующих" реквизитов обработки.
	ПодразделениеКомпании = ДокументОбъект[ИмяРеквизитаПодразделениеКомпании];
	СкидкаНаценка         = ДокументОбъект[ИмяРеквизитаСкидкаНаценка];
	Дата                  = ДокументОбъект.Дата;
	
	// Склада и карточки может и не быть.
	Попытка 
		Если ТипЗнч(ДокументОбъект[ИмяРеквизитаСкладКомпании]) = Тип("СправочникСсылка.Цеха") Тогда
			СкладКомпании = ДокументОбъект[ИмяРеквизитаСкладКомпании].СкладКомпании;
		Иначе
			СкладКомпании = ДокументОбъект[ИмяРеквизитаСкладКомпании];
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Попытка
		Карточка = ДокументОбъект[ИмяРеквизитаКарточка];
	Исключение КонецПопытки;
	
	ОбновитьКэшСтрочныхСкидок = Истина;
	ОбновитьКэшСкидок         = Истина;
		
	// 4. Определим, нужно ли выполнять пересчет кэша, как для шапочных скидок, так и для товарных (строчных).	
	Если (ПодразделениеКомпании<>ПодразделениеКомпанииСтароеЗначение) Или
		(СкладКомпании<>СкладКомпанииСтароеЗначение) Или		
		(Дата<>ДатаСтароеЗначение) Или
		(Карточка<>КарточкаСтароеЗначение) Тогда
		
		ОбновитьКэшСкидок         = Истина; 
		ОбновитьКэшСтрочныхСкидок = Истина;

	ИначеЕсли (СкидкаНаценка<>СкидкаНаценкаСтароеЗначение) Тогда  		
		ОбновитьКэшСкидок         = Истина;
		ОбновитьКэшСтрочныхСкидок = Ложь;
	Иначе
		ОбновитьКэшСкидок         = Ложь;
		ОбновитьКэшСтрочныхСкидок = Ложь;   		
	КонецЕсли;
		
	// Один из "кэшеобразующих" реквизитов изменился, значит сохраняем новое значение реквизита.
	Если ОбновитьКэшСкидок Или ОбновитьКэшСтрочныхСкидок Тогда
		ПодразделениеКомпанииСтароеЗначение = ПодразделениеКомпании;
		СкладКомпанииСтароеЗначение         = СкладКомпании;
		СкидкаНаценкаСтароеЗначение         = СкидкаНаценка;
		ДатаСтароеЗначение                  = Дата;
		КарточкаСтароеЗначение              = Карточка;
	КонецЕсли;    	
	
	// 5. Если прав у пользователя хватает только на ручные скидки,
	// но при этом скидка не выбрана, то кэш будет пустой и скидка не подберется.
	Если (СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидки) Или (СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов) Тогда
		// Обязательно должна быть задана ручная скидка в "кэшеобразующем" реквизите.
		Если СкидкаНаценка.Пустая() Тогда
			ОбновитьКэшСкидок = Ложь;
			КэшСкидок.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	// Признак пересчета табличной части документа, после подбора скидки.
	ТребуетсяПересчет = Ложь;
	
	// 6.1 Пересчитываем кэш, если нужно.
	Если ОбновитьКэшСкидок Тогда				
		ОбновитьКэшСкидок();			 
	КонецЕсли;
	
	// 6.2 Пересчитываем кэш строчных скидок.
	Если ОбновитьКэшСтрочныхСкидок И (Не НеРассчитыватьСтрочныеСкидки) Тогда		
		ОбновитьКэшСтрочныхСкидок();
		ТребуетсяПересчет = Истина; // Если изменились реквизиты, отвечающие за кэш строчных скидок, то придется пересчитать ТЧ.
	КонецЕсли; 	
	
	// 7. Получаем текущий тип скидки и текущее значение скидки, а также базу для расчета скидки.
	ТекущаяСкидкаНаценка  = ДокументОбъект[ИмяРеквизитаСкидкаНаценка];
	ТекущееЗначениеСкидки = ДокументОбъект[ИмяРеквизитаЗначениеСкидкиНаценки];
	
	//Если скидку принудительно отменили, то новую подбирать не надо
	НеЗаменятьПустуюСкидку=Ложь;
	Если обЗначениеНеЗаполнено(ТекущаяСкидкаНаценка) Тогда
		Если НЕ ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаменятьПустуюСкидку",НеЗаменятьПустуюСкидку) Тогда
			НеЗаменятьПустуюСкидку=Ложь;
		КонецЕсли; 
	КонецЕсли;
	Если НеЗаменятьПустуюСкидку Тогда
		ТекущееЗначениеСкидки = 0;
		ТекущаяСуммаСкидки    = 0; 
		ТребуетсяПересчет     = Истина;
		Возврат ТребуетсяПересчет;
	КонецЕсли; 
	
	БазаДляРасчетаСкидкиСтароеЗначение = СуммаДляРасчетаШапочнойСкидки;
	
	Попытка
		БазаДляРасчетаСкидкиТЧ		= ДокументОбъект.ПолучитьБазуДляРасчетаСкидки(ЭтотОбъект);
		Если БазаДляРасчетаСкидкиТЧ = Неопределено Тогда
			БазаДляРасчетаСкидки	= ПолучитьСуммуДокументаБезСкидки(ДокументОбъект, ДокументОбъект[ИмяТабличнойЧасти],,ИмяТабличнойЧасти);
		Иначе
			БазаДляРасчетаСкидки	= БазаДляРасчетаСкидкиТЧ;
		КонецЕсли;
	Исключение
		БазаДляРасчетаСкидки		= ПолучитьСуммуДокументаБезСкидки(ДокументОбъект, ДокументОбъект[ИмяТабличнойЧасти],,ИмяТабличнойЧасти);
	КонецПопытки;
	
	Если НеРассчитыватьСтрочныеСкидки Тогда
		СуммаДляРасчетаШапочнойСкидки = БазаДляРасчетаСкидки;
	Иначе
		МассивСтрокСВытеснением = Новый Массив;
		Попытка
			ТабличнаяЧасть = ДокументОбъект[ИмяТабличнойЧасти];
			Для Каждого ТекСтрока Из ТабличнаяЧасть Цикл
				Если ВСтрокеВытесняющаяСкидка(ДокументОбъект, ТекСтрока) Тогда
					МассивСтрокСВытеснением.Добавить(ТабличнаяЧасть.Индекс(ТекСтрока));
				КонецЕсли;
			КонецЦикла;
		Исключение
		КонецПопытки;
		
		Если МассивСтрокСВытеснением.Количество() = 0 Тогда
			СуммаДляРасчетаШапочнойСкидки = БазаДляРасчетаСкидки;
		Иначе
			Попытка
				БазаДляРасчетаСкидкиТЧ      = ДокументОбъект.ПолучитьБазуДляРасчетаСкидки(ЭтотОбъект);
				Если БазаДляРасчетаСкидкиТЧ = Неопределено Тогда
					СуммаДляРасчетаШапочнойСкидки = ПолучитьСуммуДокументаБезСкидки(ДокументОбъект, ДокументОбъект[ИмяТабличнойЧасти], МассивСтрокСВытеснением,ИмяТабличнойЧасти);
				Иначе
					СуммаДляРасчетаШапочнойСкидки = БазаДляРасчетаСкидкиТЧ;
				КонецЕсли;
			Исключение
				СуммаДляРасчетаШапочнойСкидки     = ПолучитьСуммуДокументаБезСкидки(ДокументОбъект, ДокументОбъект[ИмяТабличнойЧасти], МассивСтрокСВытеснением,ИмяТабличнойЧасти);
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;

	БазаДляРасчетаСкидкиИзменилась  = (БазаДляРасчетаСкидкиСтароеЗначение<>СуммаДляРасчетаШапочнойСкидки);
	ТекущаяСкидкаРучная             = ТекущаяСкидкаНаценка.РучнаяСкидка;
	
	// 8. Если кэш пуст... 
	Если КэшСкидок.Количество()=0 Тогда
		Если Не ТекущаяСкидкаРучная Тогда
			ТекущаяСкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
		КонецЕсли;			
		Если ТекущееЗначениеСкидки<>0 Тогда
			ТребуетсяПересчет     = Истина;
			ТекущееЗначениеСкидки = 0;
			ТекущаяСуммаСкидки    = 0; 
		КонецЕсли;
		
		Возврат ТребуетсяПересчет; 
	КонецЕсли;
                       	
	// 9. Поиск подходящей скидки. Если скидок в кэше несколько, то подберем скидку дающую наибольшую сумму скидки.
	// Не забываем отсеять скидки, сумма чека которых превышает текущую базу для расчета (сумму документа).
	КоэффициентПересчетаВВалютуДокумента = ?((ДокументОбъект.КурсДокумента=0) ИЛИ (ДокументОбъект.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()), 1, ДокументОбъект.КурсДокумента);
	
	Попытка
		ЭтотНовыйДокумент = ДокументОбъект.ЭтоНовый();
	Исключение
		ЭтотНовыйДокумент = Истина;
	КонецПопытки;
	СсылкаДокумент = ДокументОбъект.Ссылка;
	СтруктураВыборки = Новый Структура("Объект,Свойство",Неопределено,Неопределено);
	
	Для Каждого ТекСтрокаКэша Из КэшСкидок Цикл
		ОтказПоНесовпадениюСвойства = Ложь;
		Если ЗначениеЗаполнено(ТекСтрокаКэша.ВидСвойства) И ЗначениеЗаполнено(ТекСтрокаКэша.Свойство) Тогда
			Если ЭтотНовыйДокумент Тогда
				ОтказПоНесовпадениюСвойства = Истина;
			Иначе
				СтруктураВыборки.Объект = СсылкаДокумент;
				СтруктураВыборки.Свойство = ТекСтрокаКэша.ВидСвойства;
				СтрСвойств = РегистрыСведений.ЗначенияСвойствОбъектов.Получить(СтруктураВыборки);
				Если СтрСвойств.Значение <> ТекСтрокаКэша.Свойство Тогда
					ОтказПоНесовпадениюСвойства = Истина
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ОтСуммыЧекаВВалютеДокумента = ТекСтрокаКэша.ОтСуммыЧека / КоэффициентПересчетаВВалютуДокумента;
		
		Если ОтСуммыЧекаВВалютеДокумента>БазаДляРасчетаСкидки Тогда // Сумма документа недостаточна для скидки
			ТекСтрокаКэша.РассчитаннаяСуммаСкидки = 0;	
			ТекСтрокаКэша.НеПодходит             = Истина;
		ИначеЕсли ОтказПоНесовпадениюСвойства Тогда
			ТекСтрокаКэша.РассчитаннаяСуммаСкидки = 0;
			ТекСтрокаКэша.НеПодходит             = Истина;
		Иначе
			ТекСтрокаКэша.НеПодходит = Ложь;
			// Раcсчитываем сумму скидки.
			Если ТекСтрокаКэша.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
				ТекСтрокаКэша.РассчитаннаяСуммаСкидки = СуммаДляРасчетаШапочнойСкидки * ТекСтрокаКэша.ЗначениеСкидки / 100;
			Иначе
				// Если скидка абсолютная, то ее значение - это сумма в валюте рег. учета.
				// Выполним пересчет в валюту документа.
				ТекСтрокаКэша.РассчитаннаяСуммаСкидки = ТекСтрокаКэша.ЗначениеСкидки / КоэффициентПересчетаВВалютуДокумента;
				                               
				// Если сумма абсолютной скидки превышает значение базы для расчета скидки (т.е больше суммы документа), 
				// то итоговая сумма уйдет в минус, поэтому такая скидка не подойдет.
				Если ТекСтрокаКэша.РассчитаннаяСуммаСкидки>СуммаДляРасчетаШапочнойСкидки Тогда
					ТекСтрокаКэша.НеПодходит = Истина;					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
	// Сортируем по убыванию.
	КэшСкидок.Сортировать("НеПодходит ВОЗР, ЭтоПодарок УБЫВ, ФлагВытеснения УБЫВ, РассчитаннаяСуммаСкидки УБЫВ, ЗначениеСкидки УБЫВ");
	
	Если Не КэшСкидок[0].НеПодходит Тогда
		// Подходящая скидка найдена, сразу заполним итоговую сумму скидки на документ.
		НайденноеЗначениеСкидки = ?(КэшСкидок[0].СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная, КэшСкидок[0].РассчитаннаяСуммаСкидки, КэшСкидок[0].ЗначениеСкидки);
		ТекущаяСуммаСкидки = КэшСкидок[0].РассчитаннаяСуммаСкидки;
		Если (ТекущаяСкидкаНаценка<>КэшСкидок[0].Скидка) ИЛИ (ТекущееЗначениеСкидки<>НайденноеЗначениеСкидки) Тогда
			ТекущаяСкидкаНаценка  = КэшСкидок[0].Скидка;
			ТекущееЗначениеСкидки = НайденноеЗначениеСкидки;
			ТребуетсяПересчет     = Истина;
			
		ИначеЕсли БазаДляРасчетаСкидкиИзменилась И (ТекущаяСкидкаНаценка.СпособВычисления=Перечисления.СкидкиСпособВычисления.Абсолютная) Тогда
			// Если тип и значение скидки не изменились, но произошло изменение суммовой базы для расчета абсолютной скидки, то
			// придется пересчитать табличную часть.
			ТребуетсяПересчет = Истина;
		КонецЕсли;			
	Иначе 
		Если Не ТекущаяСкидкаРучная Тогда
			ТекущаяСкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
		КонецЕсли;			
		Если ТекущееЗначениеСкидки<>0 Тогда
			ТребуетсяПересчет     = Истина;
			ТекущееЗначениеСкидки = 0;
			ТекущаяСуммаСкидки    = 0; 
		КонецЕсли;			
	КонецЕсли;
	
	ТекущаяСуммаСкидки=дкОкруглитьСуммуСкидки(ТекущаяСуммаСкидки,ТекущаяСкидкаНаценка);
	
	Возврат ТребуетсяПересчет;

КонецФункции // ПолучитьСкидкуШапки() 
 
// Функция перерассчитывает проценты скидок в табличной части документа.
//
// Параметры:
//  ДокументОбъект – "ДокументОбъект" - Произвольный документ, для которого производится поиск подходящей скидки.
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура ПрименитьСкидку(ДокументОбъект) Экспорт
	// подготовим данные для сравнения
	РедактируемПроценты = обПраво("СпособВыбораСкидки",ДокументОбъект.Права,,ДокументОбъект) = Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов;
	Попытка
		СкидкаШапкиДок = ДокументОбъект.СкидкаНаценка;
	Исключение
		СкидкаШапкиДок = Неопределено;
	КонецПопытки;
	// 1. Вызываем отраслевую функцию.
	Результат = орПрименитьСкидкуШапки(ДокументОбъект, ЭтотОбъект);
	Если Результат<>Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// 2. Пытаемся получить табличную часть.
	Попытка
		ТабличнаяЧасть = ДокументОбъект[ИмяТабличнойЧасти];
	Исключение
		Возврат;
	КонецПопытки;
	
	Если ТабличнаяЧасть.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаОтносительная = ТекущаяСкидкаНаценка.Пустая() Или (ТекущаяСкидкаНаценка.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная);
	СкидкаШапкиВытесняющая = ТекущаяСкидкаНаценка.ФлагВытеснения;
		
	// 3. Выполняем расчет суммы скидки для каждой строки табличной части.
	Если СкидкаОтносительная Тогда
		// Скидки более 100% недопустимы.
		ПроцентСкидки = Мин(ТекущееЗначениеСкидки, 100); 		
	Иначе
		// Скидки более 100% недопустимы.
		КоэффициентСкидки = ?(СуммаДляРасчетаШапочнойСкидки=0, 0, ТекущаяСуммаСкидки / СуммаДляРасчетаШапочнойСкидки);
		КоэффициентСкидки = ?(КоэффициентСкидки>1, 1, КоэффициентСкидки);
	КонецЕсли;
	
	// Проходимся по табличной части. За одно найдем строку с максимальной суммой.
	СтрокаСМаксимальнойСуммой = ТабличнаяЧасть[0];
	ДопПараметры = Новый Структура;
	Если ИмяТабличнойЧасти = "Работы" Тогда
		Попытка
			ДопПараметры.Вставить("ТипЦен", ДокументОбъект.ТипЦенРабот);
		Исключение
		КонецПопытки;
	КонецЕсли;
	СуммаСтрокиСМаксимальнойСуммой = дкПолучитьСуммуСтрокиБезСкидки(ДокументОбъект,СтрокаСМаксимальнойСуммой, ДопПараметры);
	//СуммаВытеснения = 0;
	Для Каждого ТекСтрока Из ТабличнаяЧасть Цикл
		Сумма = дкПолучитьСуммуСтрокиБезСкидки(ДокументОбъект,ТекСтрока,ДопПараметры);
		Если СкидкаОтносительная Тогда
			Если НЕ (РедактируемПроценты И НЕ обЗначениеНеЗаполнено(ТекСтрока.ПроцентСкидки) И (СкидкаШапкиДок = ТекущаяСкидкаНаценка))Тогда
				ТекСтрока.ПроцентСкидки = ПроцентСкидки;
			КонецЕсли;
			Если ТекущаяСкидкаНаценка.Пустая() И РедактируемПроценты Тогда
				ТекСтрока.ПроцентСкидки = 0;
			КонецЕсли;
			ТекСтрока.СуммаСкидки	 = ТекСтрока.ПроцентСкидки / 100 * Сумма;
			ТекСтрока.СуммаСкидки	 = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,ТекущаяСкидкаНаценка);
		Иначе
			Если ТекущаяСкидкаНаценка.Пустая() И РедактируемПроценты Тогда
				ТекСтрока.СуммаСкидки = 0;
			Иначе
				ТекСтрока.СуммаСкидки = КоэффициентСкидки * Сумма;
				ТекСтрока.СуммаСкидки = дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,ТекущаяСкидкаНаценка);
			КонецЕсли;
			ТекСтрока.ПроцентСкидки	 = ?(ТекСтрока.СуммаСкидки=0, 0, КоэффициентСкидки * 100);			
		КонецЕсли;
		
		СкидкаНеВытесняется = Истина;
		//СуммаСкидкиСтрокиВрем = ТекСтрока.СуммаСкидки;
		// Теперь перессчитываем строчные скидки.
		Если НЕ НеРассчитыватьСтрочныеСкидки Тогда
			
			ПодобратьСтрочнуюСкидку(ДокументОбъект, ТекСтрока);
			Установить = (НЕ обЗначениеНеЗаполнено(ТекущаяСкидкаСтроки)) И ((НЕ СкидкаШапкиВытесняющая) ИЛИ ТекущаяСкидкаСтроки.ФлагВытеснения ИЛИ ТекущаяСкидкаСтроки.ЭтоПодарок);
			Если Установить Тогда
				ТекСтрока.СкидкаНаТовар       = ТекущаяСкидкаСтроки;
				ТекСтрока.ПроцентСкидкиСтроки = ТекущийПроцентСкидкиСтроки;
				ТекСтрока.СуммаСкидкиСтроки   = ТекущаяСуммаСкидкиСтроки;
			Иначе
				ТекСтрока.СкидкаНаТовар		  = ?(ТекущаяСкидкаСтроки.РучнаяСкидка, ТекущаяСкидкаСтроки, Справочники.ТипыСкидок.ПустаяСсылка());
				ТекСтрока.СуммаСкидкиСтроки	  = 0;
				ТекСтрока.ПроцентСкидкиСтроки = 0;
			КонецЕсли;
			
			Если ТекущаяСкидкаСтроки.ФлагВытеснения Тогда
				СкидкаНеВытесняется = Ложь;
				//СуммаВытеснения = СуммаВытеснения + СуммаСкидкиСтрокиВрем;
				Если РедактируемПроценты Тогда
					ТекСтрока.ПроцентСкидки = 0;
					ТекСтрока.СуммаСкидки = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если СкидкаНеВытесняется И Сумма>СуммаСтрокиСМаксимальнойСуммой Тогда
			// Ищем строку с максимальной суммой.
			СтрокаСМаксимальнойСуммой = ТекСтрока;
			СуммаСтрокиСМаксимальнойСуммой = Сумма;
		КонецЕсли;
		
		// Рассчитываем сумму НДС и сумму всего.
		ДокументОбъект.ОбработкаРеквизита(ИмяТабличнойЧасти + ".СтавкаНДС", ТекСтрока);
	КонецЦикла;
	
	// 4. Убираем копейки от округления. 
	Если РедактируемПроценты Тогда
		ОстатокСкидки = 0;
	Иначе
		//ived Была ошибка исправляемая данным кодом исправили сейчас не воспроизводится
		//ОстатокСкидки = ТекущаяСуммаСкидки - ТабличнаяЧасть.Итог("СуммаСкидки") - СуммаВытеснения;
		ОстатокСкидки = ТекущаяСуммаСкидки - ТабличнаяЧасть.Итог("СуммаСкидки");
	КонецЕсли;
	Если ОстатокСкидки<>0 Тогда
		СтрокаСМаксимальнойСуммой.СуммаСкидки   = СтрокаСМаксимальнойСуммой.СуммаСкидки + ОстатокСкидки; 		
		СтрокаСМаксимальнойСуммой.ПроцентСкидки	= ?(СуммаСтрокиСМаксимальнойСуммой=0, 0, 100 * СтрокаСМаксимальнойСуммой.СуммаСкидки / СуммаСтрокиСМаксимальнойСуммой);
		
		ДокументОбъект.ОбработкаРеквизита(ИмяТабличнойЧасти + ".СтавкаНДС", СтрокаСМаксимальнойСуммой);				
	КонецЕсли;
	
КонецПроцедуры // ПрименитьСкидкуШапки() 

// Функция осуществляет подбор подходящей скидки для тещей строки и заполняет соответствующие реквизиты обработки.
//
// Параметры:
//  ТекСтрока – "СтрокаТабличнойЧасти" - Строка ТЧ, для которой необходимо подобрать строчную скидку.
//
// Возвращаемое значение:
//   Истина - если скидка подобрана. 
// 
Функция ПодобратьСтрочнуюСкидку(ДокументОбъект, ТекСтрокаТЧ) Экспорт
	
	// Проверим, подходит ли текущий КЭШ для поиска.
	// Если в табличной части указывается склад (место размещения) отличный от склада шапки, то
	// необходимо сделать проверку на актуальность кэша. Кэш строчных скидок заполняется сразу
	// и по складу шапки, и по складу строки.
	// Для этого у обработки есть реквизит "СкладСтроки". Если склад текущей строки табличной
	// части отличается и от реквизита обработки
	// "СкладКомпании", и от реквизита "СкладСтроки", то значит кэш не актуален и необходим
	// его пересчет с учетом текущего склада строки.
	// Расчет сделан на то, что редко встречаются ситуации,
	// когда в табличной части документа во всех строках будут различные склады.
	// Чаще бывает, то лишь одна строка отличается.
	РедактируемПроценты = обПраво("СпособВыбораСкидки",ДокументОбъект.Права,,ДокументОбъект) = Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов;
	// подготовим данные для подмены значений
	ПроцентСкидкиСтрДок = ТекСтрокаТЧ.ПроцентСкидкиСтроки;
	СуммаСкидкиСтрДок   = ТекСтрокаТЧ.СуммаСкидкиСтроки;
	СкидкаСкидкиСтрДок  = ТекСтрокаТЧ.СкидкаНаТовар;

	ТекущийСкладСтроки = Справочники.СкладыКомпании.ПустаяСсылка();
	Попытка
		ТекущийСкладСтроки = ТекСтрокаТЧ.МестоРазмещения;
	Исключение
		Попытка
			ТекущийСкладСтроки = ТекСтрокаТЧ.СкладКомпании;
		Исключение
		КонецПопытки;
	КонецПопытки;
	
	Если КэшИнициализирован И Не ТекущийСкладСтроки.Пустая() И (ТекущийСкладСтроки<>СкладКомпании) И (ТекущийСкладСтроки<>СкладСтроки) Тогда
		СкладСтроки = ТекущийСкладСтроки;
		ОбновитьКэшСтрочныхСкидок();
	ИначеЕсли Не КэшИнициализирован Тогда
		// Оба кэша пусты. Тогда пытаемся получить "шапочную скидку", которая выполнит заполнение кэша (обоих).
		ПодобратьСкидку(ДокументОбъект, Истина); 		
	КонецЕсли; 	
	
	// Выясним, может пользователь выбрал в строке скидку ручками.
	ТекущаяСкидкаРучная = ТекСтрокаТЧ.СкидкаНаТовар.РучнаяСкидка;
	
	НеЗаменятьПустуюСкидку=Ложь;
	Если обЗначениеНеЗаполнено(ТекСтрокаТЧ.СкидкаНаТовар) Тогда
		Если НЕ ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаменятьПустуюСкидку",НеЗаменятьПустуюСкидку) Тогда
			НеЗаменятьПустуюСкидку=Ложь;
		КонецЕсли; 
	КонецЕсли;
	Если НеЗаменятьПустуюСкидку Тогда
		ТекущаяСкидкаСтроки        = Справочники.ТипыСкидок.ПустаяСсылка();
		ТекущийПроцентСкидкиСтроки = 0;
		ТекущаяСуммаСкидкиСтроки   = 0;
		Возврат Ложь;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура;
	Если ТекущаяСкидкаРучная Тогда
		// Искать будем только среди строк с текущим типом скидки.
		СтруктураПоиска.Вставить("Скидка", ТекСтрокаТЧ.СкидкаНаТовар);
	Иначе
		// Ищем среди строк с автоматическими типами скидок.
		СтруктураПоиска.Вставить("РучнаяСкидка", Ложь);
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	Если ИмяТабличнойЧасти = "Работы" Тогда
		Попытка
			ДопПараметры.Вставить("ТипЦен", ДокументОбъект.ТипЦенРабот);
		Исключение
		КонецПопытки;
	КонецЕсли;

	МассивСтрокПретендентов = КэшСтрочныхСкидок.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрокПретендентов.Количество()=0 Тогда
		Если НЕ РедактируемПроценты Тогда
			ТекущаяСкидкаСтроки        = ?(ТекущаяСкидкаРучная, ТекСтрокаТЧ.СкидкаНаТовар, Справочники.ТипыСкидок.ПустаяСсылка());
			ТекущийПроцентСкидкиСтроки = 0;
			ТекущаяСуммаСкидкиСтроки   = 0;
			Возврат Ложь; // Сбрасываем скидку.
		Иначе
			Если НЕ обЗначениеНеЗаполнено(ТекСтрокаТЧ.СкидкаНаТовар) Тогда
				Сумма = дкПолучитьСуммуСтрокиБезСкидки(ДокументОбъект,ТекСтрокаТЧ,ДопПараметры);
				Если ТекСтрокаТЧ.СкидкаНаТовар.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					СуммаСкидкиСтрДок = Сумма * ПроцентСкидкиСтрДок/100;
				Иначе
					ПроцентСкидкиСтрДок = (СуммаСкидкиСтрДок / Сумма)*100;
				КонецЕсли;
			КонецЕсли;
			ТекущаяСкидкаСтроки        = ?(ТекущаяСкидкаРучная, ТекСтрокаТЧ.СкидкаНаТовар, Справочники.ТипыСкидок.ПустаяСсылка());
			ТекущийПроцентСкидкиСтроки = ?(обЗначениеНеЗаполнено(ТекущаяСкидкаСтроки),0,ПроцентСкидкиСтрДок);
			ТекущаяСуммаСкидкиСтроки   = ?(обЗначениеНеЗаполнено(ТекущаяСкидкаСтроки),0,СуммаСкидкиСтрДок);
			Если ТекущаяСкидкаСтроки.ФлагВытеснения Тогда
				ТекСтрокаТЧ.ПроцентСкидки = 0;
				ТекСтрокаТЧ.СуммаСкидки = 0;
			Иначе
				Если обЗначениеНеЗаполнено(ТекСтрокаТЧ.ПроцентСкидки)
					И ТекущаяСкидкаНаценка.СпособВычисления = Перечисления.СкидкиСпособВычисления.Относительная Тогда
					
					ТекСтрокаТЧ.ПроцентСкидки = ТекущееЗначениеСкидки;
					ДокументОбъект.ОбработкаРеквизита("Товары.ПроцентСкидки", ТекСтрокаТЧ);
					
				КонецЕсли;
			КонецЕсли;
			ТекущаяСуммаСкидкиСтроки=дкОкруглитьСуммуСкидки(ТекущаяСуммаСкидкиСтроки,ТекущаяСкидкаСтроки);
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Данная переменная будет содержать строку кэша, которая дает наибольшую сумму скидки.
	ПодходящаяСкидка = Неопределено;		
	КоэффициентПересчетаВВалютуДокумента = ?((ДокументОбъект.КурсДокумента=0) ИЛИ (ДокументОбъект.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()), 1, ДокументОбъект.КурсДокумента);
	
	Сумма = дкПолучитьСуммуСтрокиБезСкидки(ДокументОбъект,ТекСтрокаТЧ,ДопПараметры);
	
	Попытка
		//Номенклатура= ?(ТипЗнч(ТекСтрокаТЧ.Номенклатура) = Тип("СправочникСсылка.Автоработы"), ТекСтрокаТЧ.Номенклатура.Номенклатура, ТекСтрокаТЧ.Номенклатура);
		Номенклатура = ТекСтрокаТЧ.Номенклатура;
		Коэффициент  = ?(ТекСтрокаТЧ.Коэффициент=0, 1, ТекСтрокаТЧ.Коэффициент);
		Попытка 
			КоличествоВСтроке = ТекСтрокаТЧ.КоличествоБазовое;
		Исключение
			КоличествоВСтроке = ТекСтрокаТЧ.Количество * Коэффициент
		КонецПопытки;
		РеальноеКоличество = ТекСтрокаТЧ.Количество * Коэффициент;
	Исключение
		Попытка
			//Номенклатура=ТекСтрокаТЧ.Работа.Номенклатура;
			Номенклатура = ТекСтрокаТЧ.Работа;
			Коэффициент = 1;
			КоличествоВСтроке = ТекСтрокаТЧ.Количество;
			РеальноеКоличество = ТекСтрокаТЧ.Количество;
		Исключение
			Попытка
				Номенклатура=Справочники.Номенклатура.ПустаяСсылка();
				Коэффициент = ТекСтрокаТЧ.Коэффициент;
				Попытка 
					КоличествоВСтроке = ТекСтрокаТЧ.КоличествоБазовое;
				Исключение
					КоличествоВСтроке = ТекСтрокаТЧ.Количество * Коэффициент
				КонецПопытки;
				РеальноеКоличество = ТекСтрокаТЧ.Количество * Коэффициент;
			Исключение
			КонецПопытки; 
		КонецПопытки; 
	КонецПопытки;
	
	СписокГруппНоменклатуры=Новый СписокЗначений;
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		СписокГруппНоменклатуры.Добавить(Номенклатура);
		НоменклатураРодитель=Номенклатура.Родитель;
		Пока Не НоменклатураРодитель.Пустая() Цикл
			СписокГруппНоменклатуры.Добавить(НоменклатураРодитель);
			НоменклатураРодитель=НоменклатураРодитель.Родитель;
		КонецЦикла; 
		//ТипНоменклатуры=Номенклатура.ТипНоменклатуры;
		//ТипНоменклатуры = ?(ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Автоработы"), Номенклатура.Номенклатура.ТипНоменклатуры, Номенклатура.ТипНоменклатуры);
		Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Автоработы") Тогда
			
			СписокГруппНоменклатуры.Добавить(Номенклатура.Номенклатура);
			НоменклатураРодитель = Номенклатура.Номенклатура.Родитель;
			Пока Не НоменклатураРодитель.Пустая() Цикл
				СписокГруппНоменклатуры.Добавить(НоменклатураРодитель);
				НоменклатураРодитель = НоменклатураРодитель.Родитель;
			КонецЦикла;
			
			ТипНоменклатуры = Номенклатура.Номенклатура.ТипНоменклатуры;
		Иначе
			ТипНоменклатуры = Номенклатура.ТипНоменклатуры;
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		ЭтотНовыйДокумент = ДокументОбъект.ЭтоНовый();
	Исключение
		ЭтотНовыйДокумент = Истина;
	КонецПопытки;
	
	СсылкаДокумент = ДокументОбъект.Ссылка;
	СтруктураВыборки = Новый Структура("Объект,Свойство",Неопределено,Неопределено);
	Для Каждого ТекСтрокаПретендент Из МассивСтрокПретендентов Цикл
		// Заменим значения
		ЗначениеСкидкиДок = ТекСтрокаПретендент.ЗначениеСкидки;
		ЗначениеСкидкиПодх = 0;
		Если РедактируемПроценты И (СкидкаСкидкиСтрДок = ТекСтрокаПретендент.Скидка) Тогда
			Если (СкидкаСкидкиСтрДок.СпособВычисления = Перечисления.СкидкиСпособВычисления.Относительная) Тогда
				Если НЕ обЗначениеНеЗаполнено(ПроцентСкидкиСтрДок) Тогда
					ЗначениеСкидкиДок = ПроцентСкидкиСтрДок;
				КонецЕсли;
			Иначе
				Если НЕ обЗначениеНеЗаполнено(СуммаСкидкиСтрДок) Тогда
					ЗначениеСкидкиДок = СуммаСкидкиСтрДок;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		// Проверяем условия.
		Если СписокГруппНоменклатуры.НайтиПоЗначению(ТекСтрокаПретендент.Объект)=Неопределено И ТекСтрокаПретендент.Объект<>ТипНоменклатуры Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекСтрокаПретендент.ВидСвойства) И ЗначениеЗаполнено(ТекСтрокаПретендент.Свойство) Тогда
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				СтруктураВыборки.Объект   = Номенклатура;
				СтруктураВыборки.Свойство = ТекСтрокаПретендент.ВидСвойства;
				СтрСвойств = РегистрыСведений.ЗначенияСвойствОбъектов.Получить(СтруктураВыборки);
				Если СтрСвойств.Значение <> ТекСтрокаПретендент.Свойство Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ОтСуммыСтрокиВВалютеДокумента = ТекСтрокаПретендент.ОтСуммыСтроки / КоэффициентПересчетаВВалютуДокумента;
		Если ОтСуммыСтрокиВВалютеДокумента>Сумма Тогда
			Продолжить;
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(ТекСтрокаПретендент.Правило) ИЛИ
			 ТекСтрокаПретендент.Правило=Перечисления.ПравилаРасчетаСкидкиПоКоличеству.НеИспользуется Тогда
			Если ТекСтрокаПретендент.ОтКоличества>КоличествоВСтроке Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Условия пройдены. Значит можно раcсчитать сумму скидки для текущей строки.
		Если ТекСтрокаПретендент.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
			ТекСтрокаПретендент.РассчитаннаяСуммаСкидки = Сумма * ?(НЕ РедактируемПроценты,ТекСтрокаПретендент.ЗначениеСкидки, ЗначениеСкидкиДок) / 100;		
		Иначе			
			ТекСтрокаПретендент.РассчитаннаяСуммаСкидки = ?(НЕ РедактируемПроценты,ТекСтрокаПретендент.ЗначениеСкидки, ЗначениеСкидкиДок) / КоэффициентПересчетаВВалютуДокумента;
			// Если сумма абсолютной строчной скидки превышает значение суммы строки, то такую скидку не учитываем.
			Если ТекСтрокаПретендент.РассчитаннаяСуммаСкидки>Сумма Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;	
		
		Если ПодходящаяСкидка=Неопределено ИЛИ
			 ПодходящаяСкидка.ФлагВытеснения < ТекСтрокаПретендент.ФлагВытеснения ИЛИ
			 (ПодходящаяСкидка.ФлагВытеснения = ТекСтрокаПретендент.ФлагВытеснения И
			 ПодходящаяСкидка.РассчитаннаяСуммаСкидки<ТекСтрокаПретендент.РассчитаннаяСуммаСкидки) Тогда
			 
			ПодходящаяСкидка = ТекСтрокаПретендент;
			ЗначениеСкидкиПодх = ?(РедактируемПроценты, ЗначениеСкидкиДок, ТекСтрокаПретендент.ЗначениеСкидки);
		КонецЕсли;
	КонецЦикла;
	
	Если ПодходящаяСкидка<>Неопределено Тогда
		Если ПодходящаяСкидка.Скидка.ФлагВытеснения=Ложь И ТекущаяСкидкаНаценка.ФлагВытеснения=Истина Тогда
			ТекущаяСкидкаСтроки			= Справочники.ТипыСкидок.ПустаяСсылка();
			ТекущаяСуммаСкидкиСтроки	= 0;
			ТекущийПроцентСкидкиСтроки	= 0;
		Иначе
			// Есть подходящая скидка, заполняем соответствующие реквизиты обработки.
			ТекущаяСкидкаСтроки      = ПодходящаяСкидка.Скидка;
			ТекущаяСуммаСкидкиСтроки = ПодходящаяСкидка.РассчитаннаяСуммаСкидки;
			
			// Теперь учтем правило расчета количества.
			Если (ПодходящаяСкидка.ОтКоличества<>0) И (ПодходящаяСкидка.Правило<>Перечисления.ПравилаРасчетаСкидкиПоКоличеству.НеИспользуется) Тогда 
				КоличествоОграничение = ПодходящаяСкидка.ОтКоличества;
				
				Если ПодходящаяСкидка.Правило=Перечисления.ПравилаРасчетаСкидкиПоКоличеству.Больше Тогда
					// Скидка распространяется только на количество товара превышающее значение "КоличествоОграничение"
					КоличествоСоСкидкой = Макс(РеальноеКоличество - КоличествоОграничение, 0);
					
				ИначеЕсли ПодходящаяСкидка.Правило=Перечисления.ПравилаРасчетаСкидкиПоКоличеству.Меньше Тогда
					// Скидка распространяется только на количество товара равное значению "КоличествоОграничение".
					КоличествоСоСкидкой = Мин(РеальноеКоличество, КоличествоОграничение);
					
				ИначеЕсли ПодходящаяСкидка.Правило=Перечисления.ПравилаРасчетаСкидкиПоКоличеству.Упаковка Тогда
					// Скидка распространяется только на количество товара кратное "упаковке".
					Попытка
						РеальноеКоличество  = ТекСтрокаТЧ.КоличествоБазовое;
						КоличествоСоСкидкой = Цел(РеальноеКоличество / Коэффициент) * Коэффициент;
					Исключение
						КоличествоСоСкидкой = РеальноеКоличество;
					КонецПопытки;	
				КонецЕсли;
				
				// Пропорцией вычисляем сумму скидки, приходящуюся на "КоличествоСоСкидкой";
				ТекущаяСуммаСкидкиСтроки = ТекущаяСуммаСкидкиСтроки * КоличествоСоСкидкой / РеальноеКоличество; 
			КонецЕсли;
			
			Если ТекущаяСуммаСкидкиСтроки=0 Тогда 
				// Если сумма скидки строки равна нулю, то обнулим и процент скидки.
				ТекущаяСкидкаСтроки        = ?(ТекущаяСкидкаРучная, ТекСтрокаТЧ.СкидкаНаТовар, Справочники.ТипыСкидок.ПустаяСсылка());
				ТекущийПроцентСкидкиСтроки = 0; 			
			ИначеЕсли ПодходящаяСкидка.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
				ТекущийПроцентСкидкиСтроки = ?(обЗначениеНеЗаполнено(ЗначениеСкидкиПодх),ПодходящаяСкидка.ЗначениеСкидки,ЗначениеСкидкиПодх);
			Иначе			
				ТекущийПроцентСкидкиСтроки = ?(Сумма=0, 0, ТекущаяСуммаСкидкиСтроки / Сумма) * 100; 
			КонецЕсли;
			
			Если ПодходящаяСкидка.Скидка.ФлагВытеснения=Истина Тогда
				ТекСтрокаТЧ.ПроцентСкидки	= 0;
				ТекСтрокаТЧ.СуммаСкидки		= 0;
			КонецЕсли; 
		КонецЕсли; 
		
		ТекущаяСуммаСкидкиСтроки=дкОкруглитьСуммуСкидки(ТекущаяСуммаСкидкиСтроки,ТекущаяСкидкаСтроки);
		
		Возврат Истина;
	Иначе
		Если НЕ РедактируемПроценты Тогда
			ТекущаяСкидкаСтроки        = ?(ТекущаяСкидкаРучная, ТекСтрокаТЧ.СкидкаНаТовар, Справочники.ТипыСкидок.ПустаяСсылка());
			ТекущийПроцентСкидкиСтроки = 0;
			ТекущаяСуммаСкидкиСтроки   = 0;
			Возврат Ложь;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(ТекСтрокаТЧ.СкидкаНаТовар) Тогда
				Сумма = дкПолучитьСуммуСтрокиБезСкидки(ДокументОбъект,ТекСтрокаТЧ,ДопПараметры);
				Если ТекСтрокаТЧ.СкидкаНаТовар.СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная Тогда
					СуммаСкидкиСтрДок = Сумма * ПроцентСкидкиСтрДок/100;
				Иначе
					ПроцентСкидкиСтрДок = (СуммаСкидкиСтрДок / Сумма)*100;
				КонецЕсли;
			КонецЕсли;
			Если ТекСтрокаТЧ.СкидкаНаТовар.ФлагВытеснения Тогда
				ТекущаяСкидкаСтроки        = ?(ТекущаяСкидкаРучная, ТекСтрокаТЧ.СкидкаНаТовар, Справочники.ТипыСкидок.ПустаяСсылка());
				ТекущийПроцентСкидкиСтроки = ?(обЗначениеНеЗаполнено(ТекущаяСкидкаСтроки),0,ПроцентСкидкиСтрДок);
				ТекущаяСуммаСкидкиСтроки   = ?(обЗначениеНеЗаполнено(ТекущаяСкидкаСтроки),0,СуммаСкидкиСтрДок);
				ТекСтрокаТЧ.ПроцентСкидки = 0;
				ТекСтрокаТЧ.СуммаСкидки = 0;
			//ИначеЕсли ТекСтрокаТЧ.СкидкаНаТовар.ФлагВытеснения=Ложь И ТекущаяСкидкаНаценка.ФлагВытеснения=Истина Тогда
			//	ТекущаяСкидкаСтроки			= Справочники.ТипыСкидок.ПустаяСсылка();
			//	ТекущаяСуммаСкидкиСтроки	= 0;
			//	ТекущийПроцентСкидкиСтроки	= 0;
			Иначе
				ТекущаяСкидкаСтроки        = ?(ТекущаяСкидкаРучная, ТекСтрокаТЧ.СкидкаНаТовар, Справочники.ТипыСкидок.ПустаяСсылка());
				ТекущийПроцентСкидкиСтроки = ?(обЗначениеНеЗаполнено(ТекущаяСкидкаСтроки),0,ПроцентСкидкиСтрДок);
				ТекущаяСуммаСкидкиСтроки   = ?(обЗначениеНеЗаполнено(ТекущаяСкидкаСтроки),0,СуммаСкидкиСтрДок);
				Если обЗначениеНеЗаполнено(ТекСтрокаТЧ.ПроцентСкидки) Тогда
					ТекСтрокаТЧ.ПроцентСкидки = ТекущееЗначениеСкидки;
				КонецЕсли;
				ТекущаяСуммаСкидкиСтроки=дкОкруглитьСуммуСкидки(ТекущаяСуммаСкидкиСтроки,ТекущаяСкидкаСтроки);
				Возврат Истина;
			КонецЕсли;
			ТекущаяСуммаСкидкиСтроки=дкОкруглитьСуммуСкидки(ТекущаяСуммаСкидкиСтроки,ТекущаяСкидкаСтроки);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции // ПодобратьСтрочнуюСкидку() 

// Функция осуществляет подбор подходящей скидки для тещей строки и заполняет соответствующие реквизиты обработки.
//
// Параметры:
//  ТекСтрока – "СтрокаТабличнойЧасти" - Строка ТЧ, для которой необходимо подобрать строчную скидку.
//
// Возвращаемое значение:
//   Истина - если скидка подобрана. 
// 
Функция ВСтрокеВытесняющаяСкидка(ДокументОбъект, ТекСтрокаТЧ) Экспорт
	
	// Проверим, подходит ли текущий КЭШ для поиска.
	// Если в табличной части указывается склад (место размещения) отличный от склада шапки, то
	// необходимо сделать проверку на актуальность кэша. Кэш строчных скидок заполняется сразу
	// и по складу шапки, и по складу строки.
	// Для этого у обработки есть реквизит "СкладСтроки". Если склад текущей строки табличной
	// части отличается и от реквизита обработки
	// "СкладКомпании", и от реквизита "СкладСтроки", то значит кэш не актуален и необходим
	// его пересчет с учетом текущего склада строки.
	// Расчет сделан на то, что редко встречаются ситуации,
	// когда в табличной части документа во всех строках будут различные склады.
	// Чаще бывает, то лишь одна строка отличается. 
	ТекущийСкладСтроки = Справочники.СкладыКомпании.ПустаяСсылка();
	Попытка
		ТекущийСкладСтроки = ТекСтрокаТЧ.МестоРазмещения;
	Исключение
		Попытка
			ТекущийСкладСтроки = ТекСтрокаТЧ.СкладКомпании;
		Исключение
		КонецПопытки;
	КонецПопытки;
	
	Если Не ТекущийСкладСтроки.Пустая() И (ТекущийСкладСтроки<>СкладКомпании) И (ТекущийСкладСтроки<>СкладСтроки) Тогда
		СкладСтроки = ТекущийСкладСтроки;
		ОбновитьКэшСтрочныхСкидок();
		
	ИначеЕсли Не КэшИнициализирован Тогда
		// Оба кэша пусты. Тогда пытаемся получить "шапочную скидку", которая выполнит заполнение кэша (обоих).
		ПодобратьСкидку(ДокументОбъект, Истина);
	КонецЕсли;
	
	// Выясним, может пользователь выбрал в строке скидку ручками.
	ТекущаяСкидкаРучная = ТекСтрокаТЧ.СкидкаНаТовар.РучнаяСкидка;
	
	НеЗаменятьПустуюСкидку=Ложь;
	Если обЗначениеНеЗаполнено(ТекСтрокаТЧ.СкидкаНаТовар) Тогда
		Если НЕ ДокументОбъект.ДополнительныеСвойства.Свойство("НеЗаменятьПустуюСкидку",НеЗаменятьПустуюСкидку) Тогда
			НеЗаменятьПустуюСкидку=Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НеЗаменятьПустуюСкидку Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ФлагВытеснения", Истина);
	Если ТекущаяСкидкаРучная Тогда
		// Искать будем только среди строк с текущим типом скидки.
		СтруктураПоиска.Вставить("Скидка", ТекСтрокаТЧ.СкидкаНаТовар);
	Иначе
		// Ищем среди строк с автоматическими типами скидок.
		СтруктураПоиска.Вставить("РучнаяСкидка", Ложь);
	КонецЕсли;
	
	МассивСтрокПретендентов = КэшСтрочныхСкидок.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрокПретендентов.Количество()=0 Тогда
		Возврат Ложь; // Сбрасываем скидку.
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	Если ИмяТабличнойЧасти = "Работы" Тогда
		Попытка
			ДопПараметры.Вставить("ТипЦен", ДокументОбъект.ТипЦенРабот);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	КоэффициентПересчетаВВалютуДокумента = ?((ДокументОбъект.КурсДокумента=0) ИЛИ (ДокументОбъект.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()), 1, ДокументОбъект.КурсДокумента);
	Сумма = дкПолучитьСуммуСтрокиБезСкидки(ДокументОбъект,ТекСтрокаТЧ,ДопПараметры);
	
	Попытка
		//Номенклатура= ?(ТипЗнч(ТекСтрокаТЧ.Номенклатура) = Тип("СправочникСсылка.Автоработы"), ТекСтрокаТЧ.Номенклатура.Номенклатура, ТекСтрокаТЧ.Номенклатура);
		Номенклатура = ТекСтрокаТЧ.Номенклатура;
		Коэффициент  = ?(ТекСтрокаТЧ.Коэффициент=0, 1, ТекСтрокаТЧ.Коэффициент);
		Попытка 
			КоличествоВСтроке = ТекСтрокаТЧ.КоличествоБазовое;
		Исключение
			КоличествоВСтроке = ТекСтрокаТЧ.Количество * Коэффициент
		КонецПопытки;
		РеальноеКоличество = ТекСтрокаТЧ.Количество * Коэффициент;
	Исключение
		Попытка
			//Номенклатура=ТекСтрокаТЧ.Работа.Номенклатура;
			Номенклатура = ТекСтрокаТЧ.Работа;
			Коэффициент = 1;
			КоличествоВСтроке = ТекСтрокаТЧ.Количество;
			РеальноеКоличество = ТекСтрокаТЧ.Количество;
		Исключение
			Попытка
				Номенклатура=Справочники.Номенклатура.ПустаяСсылка();
				Коэффициент = ТекСтрокаТЧ.Коэффициент;
				Попытка 
					КоличествоВСтроке = ТекСтрокаТЧ.КоличествоБазовое;
				Исключение
					КоличествоВСтроке = ТекСтрокаТЧ.Количество * Коэффициент
				КонецПопытки;
				РеальноеКоличество = ТекСтрокаТЧ.Количество * Коэффициент;
			Исключение
			КонецПопытки; 
		КонецПопытки; 
	КонецПопытки;
	
	СписокГруппНоменклатуры=Новый СписокЗначений;
	Если НЕ обЗначениеНеЗаполнено(Номенклатура) Тогда
		СписокГруппНоменклатуры.Добавить(Номенклатура);
		НоменклатураРодитель=Номенклатура.Родитель;
		Пока Не НоменклатураРодитель.Пустая() Цикл
			СписокГруппНоменклатуры.Добавить(НоменклатураРодитель);
			НоменклатураРодитель=НоменклатураРодитель.Родитель;
		КонецЦикла;
		//ТипНоменклатуры=Номенклатура.ТипНоменклатуры;
		//ТипНоменклатуры = ?(ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Автоработы"), Номенклатура.Номенклатура.ТипНоменклатуры, Номенклатура.ТипНоменклатуры);
		Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Автоработы") Тогда
			
			СписокГруппНоменклатуры.Добавить(Номенклатура.Номенклатура);
			НоменклатураРодитель = Номенклатура.Номенклатура.Родитель;
			Пока Не НоменклатураРодитель.Пустая() Цикл
				СписокГруппНоменклатуры.Добавить(НоменклатураРодитель);
				НоменклатураРодитель = НоменклатураРодитель.Родитель;
			КонецЦикла;
			
			ТипНоменклатуры = Номенклатура.Номенклатура.ТипНоменклатуры;
		Иначе
			ТипНоменклатуры = Номенклатура.ТипНоменклатуры;
		КонецЕсли;
	КонецЕсли;
		
	Попытка
		ЭтотНовыйДокумент = ДокументОбъект.ЭтоНовый();
	Исключение
		ЭтотНовыйДокумент = Истина;
	КонецПопытки;
	
	СсылкаДокумент = ДокументОбъект.Ссылка;
	СтруктураВыборки = Новый Структура("Объект,Свойство",Неопределено,Неопределено);
	Для Каждого ТекСтрокаПретендент Из МассивСтрокПретендентов Цикл  	
		// Проверяем условия.
		Если СписокГруппНоменклатуры.НайтиПоЗначению(ТекСтрокаПретендент.Объект)=Неопределено И ТекСтрокаПретендент.Объект<>ТипНоменклатуры Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекСтрокаПретендент.ВидСвойства) И ЗначениеЗаполнено(ТекСтрокаПретендент.Свойство) Тогда
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				СтруктураВыборки.Объект   = Номенклатура;
				СтруктураВыборки.Свойство = ТекСтрокаПретендент.ВидСвойства;
				СтрСвойств = РегистрыСведений.ЗначенияСвойствОбъектов.Получить(СтруктураВыборки);
				Если СтрСвойств.Значение <> ТекСтрокаПретендент.Свойство Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ОтСуммыСтрокиВВалютеДокумента = ТекСтрокаПретендент.ОтСуммыСтроки / КоэффициентПересчетаВВалютуДокумента;
		Если ОтСуммыСтрокиВВалютеДокумента>Сумма Тогда
			Продолжить;
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(ТекСтрокаПретендент.Правило) ИЛИ
			 ТекСтрокаПретендент.Правило=Перечисления.ПравилаРасчетаСкидкиПоКоличеству.НеИспользуется Тогда
			Если ТекСтрокаПретендент.ОтКоличества>КоличествоВСтроке Тогда
				Продолжить;
			КонецЕсли;
		Иначе
			Если ТекСтрокаПретендент.ОтКоличества>=КоличествоВСтроке Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли; 
		
		// Условия пройдены. Значит можно раcсчитать сумму скидки для текущей строки.
		Если ТекСтрокаПретендент.СпособВычисления=Перечисления.СкидкиСпособВычисления.Абсолютная И 
			(ТекСтрокаПретендент.ЗначениеСкидки / КоэффициентПересчетаВВалютуДокумента)>Сумма Тогда
			// Если сумма абсолютной строчной скидки превышает значение суммы строки, то такую скидку не учитываем.
			Продолжить;
		КонецЕсли;
		
		Возврат Истина;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции // ЭтоВытесняющаяСкидка() 


//////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ
//////////////////////////////////////////////////////////////////////////////////

КоэффициентПересчета = обПересчет(1, Константы.ВалютаНакопительныхСумм.Получить(), Дата, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), 1);

ИмяРеквизитаПодразделениеКомпании = "ПодразделениеКомпании";
ИмяРеквизитаСкладКомпании         = "СкладКомпании";
ИмяРеквизитаСкидкаНаценка         = "СкидкаНаценка";
ИмяРеквизитаКарточка              = "Карточка";
ИмяРеквизитаЗначениеСкидкиНаценки = "ЗначениеСкидкиНаценки";
ИмяРеквизитаСуммаСкидкиНаценки    = "СуммаСкидкиНаценки";
ИмяТабличнойЧасти                 = "Товары";
КэшИнициализирован                = Ложь;  



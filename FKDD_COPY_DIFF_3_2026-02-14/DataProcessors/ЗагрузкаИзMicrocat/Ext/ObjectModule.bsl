////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем РезультатЗагрузки Экспорт; // Результат загрузки
Перем ОписаниеОшибок Экспорт;    // Строка описания ошибок загрузки
Перем Права Экспорт;             // Права текущего пользователя
Перем ДобавлениеВДокумент Экспорт; // Признак добавления в документ
Перем ТабличнаяЧастьДокумента Экспорт; // Ссылка на табличную часть документа

// Файлы Микрокат MMP
Перем ИмяФайлаИмпорта Экспорт; // Имя к файлу импорта
Перем ИмяФайлаЭкспорта Экспорт; // Имя к файлу экспорта
Перем КаталогФайлов Экспорт; // Путь к каталогу с файлами
// На основе настроек установим каталог пользователя
Перем ПутьКФайлуИмпорта Экспорт; // Путь к файлу импорта
Перем ВремяОжиданияОтвета Экспорт; // Время ожидания ответа

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает обязательные реквизиты
//
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	ОбязательныеРеквизиты = Новый Структура;
	ОбязательныеРеквизиты.Вставить("ТипНоменклатуры");
	ОбязательныеРеквизиты.Вставить("ВидНоменклатуры");
	ОбязательныеРеквизиты.Вставить("БазоваяЕдиницаИзмерения");
	ОбязательныеРеквизиты.Вставить("ВалютаУчета");
	ОбязательныеРеквизиты.Вставить("СтавкаНДС");
	Возврат ОбязательныеРеквизиты;	
КонецФункции

// 0 - загрузка текстового файла
// 1 - загрузка в справочник
Функция ПроверитьКорректность(Режим=0) Экспорт
	Рез = Истина;
	
	Если Режим = 0 Тогда
	
		Если ФорматСтроки.Количество() = 0 Тогда
			дкДобавитьОшибку(ОписаниеОшибок,"В настройках не указан порядок размещения полей данных в строке файла!");
			Рез = Ложь;
			
		ИначеЕсли ПоляФиксированнойШирины Тогда
			НулевыеСтроки = ФорматСтроки.НайтиСтроки(Новый Структура("Ширина", 0));
			Если НулевыеСтроки.Количество() > 0 Тогда
				Рез = Ложь;
				Для Каждого СтрокаФормата Из НулевыеСтроки Цикл
					дкДобавитьОшибку(ОписаниеОшибок,"В настройках не указана ширина поля [" + СтрокаФормата.Значение + "] !");	
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	
	ИначеЕсли Режим = 1 Тогда
		
		ОбязательныеРеквизиты = ПолучитьОбязательныеРеквизиты();
		ЭтотОбъектМетаданные = ЭтотОбъект.Метаданные();
		Для Каждого Реквизит Из ОбязательныеРеквизиты Цикл
			Если обЗначениеНеЗаполнено(ЭтотОбъект[Реквизит.Ключ]) Тогда
				Рез = Ложь;
				дкДобавитьОшибку(ОписаниеОшибок,"В настройках не заполнен реквизит [" + обПолучитьСинонимРеквизита(ЭтотОбъектМетаданные,Реквизит.Ключ) + "] !");
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

// Процедура чтения строки с полями фиксированной длины
Функция ПрочитатьСтрокуСПолямиФиксированнойШирины(ИсходнаяСтрока)
	
	МассивСтрокиФайла = Новый Массив();
		
	НомерФорматнойСтроки = 0;
	Пока НомерФорматнойСтроки <= ФорматСтроки.Количество()-1 Цикл
		НаименованиеЗначения = ФорматСтроки[НомерФорматнойСтроки].Значение;
		ШиринаЗначения = ФорматСтроки[НомерФорматнойСтроки].Ширина;
		Значение = СОКРЛП(Сред(ИсходнаяСтрока,1,ШиринаЗначения));
		МассивСтрокиФайла.Добавить(Значение);	
		ИсходнаяСтрока = Прав(ИсходнаяСтрока,СтрДлина(ИсходнаяСтрока)-ШиринаЗначения);
		НомерФорматнойСтроки = НомерФорматнойСтроки + 1;	
	КонецЦикла;
	
	Возврат МассивСтрокиФайла;
	
КонецФункции

// Процедура чтения строки с разделителями
Функция ПрочитатьСтрокуСРазделителями(ИсходнаяСтрока)
	
	
	МассивСтрокиФайла=Новый Массив();
	
	Пока СтрДлина(ИсходнаяСтрока)>0 Цикл
		ПозицияРазделителя=Найти(ИсходнаяСтрока,Разделитель);
		Если ПозицияРазделителя=0 Тогда
			ПозицияРазделителя=СтрДлина(ИсходнаяСтрока)+1;
		КонецЕсли; 	
		Значение=СОКРЛП(Лев(ИсходнаяСтрока,ПозицияРазделителя-1));
		ИсходнаяСтрока=Сред(ИсходнаяСтрока,ПозицияРазделителя+1);
		МассивСтрокиФайла.Добавить(Значение);
	КонецЦикла; 
	
	Возврат МассивСтрокиФайла;
	
КонецФункции

// Загрузка файла обмена
Процедура ЗагрузитьФайл(ПутьКФайлу,ЭтаФорма=Неопределено) Экспорт
	
	#Если Клиент Тогда
		Если ЭтаФорма <> Неопределено Тогда	
			//Проверим наличие файла
			Файл=Новый Файл(ПутьКФайлу);
			Если НЕ Файл.Существует() Тогда
				дкДобавитьОшибку(ОписаниеОшибок,"Файл обмена с автокаталогом Microcat <"+ПутьКФайлу+"> не найден.");
				ЭтаФорма.ЭлементыФормы.ПолеВводаФайл.ЦветТекста=Новый Цвет(128,0,0);
				ЭтаФорма.Обновить();
				Возврат;
			Иначе
				ЭтаФорма.ЭлементыФормы.ПолеВводаФайл.ЦветТекста=Новый Цвет(0,0,0);
				ЭтаФорма.Обновить();
			КонецЕсли;
			
			ЭтаФорма.ЭлементыФормы.Индикатор.Значение=0;
			ЭтаФорма.ЭлементыФормы.Индикатор.МаксимальноеЗначение=Файл.Размер();
			ЭтаФорма.ЭлементыФормы.Индикатор.Видимость=Истина;
		КонецЕсли;
		
	#КонецЕсли
	
	Товары = ЭтаФорма.Товары.Скопировать();
	Товары.Очистить();
	
	ЧтениеТекста = Новый ЧтениеТекста(СокрЛП(ПутьКФайлу));
	СтрокаФайла = ЧтениеТекста.ПрочитатьСтроку();
	НомерСтроки = 1;
	
	
	Пока СтрокаФайла<>Неопределено Цикл
		
		#Если Клиент Тогда
			Если ЭтаФорма <> Неопределено Тогда
				ЭтаФорма.ЭлементыФормы.Индикатор.Значение=ЭтаФорма.ЭлементыФормы.Индикатор.Значение+СтрДлина(СтрокаФайла)+2;
			КонецЕсли;
		#КонецЕсли
		// при использовании колонтитулов будем обрабатывать 
		// только строки из заданного диапазона
		Если ИспользованиеКолонтитулов Тогда
			Если СтрокаС<>0 И НомерСтроки<СтрокаС Тогда
				СтрокаФайла = ЧтениеТекста.ПрочитатьСтроку();
				НомерСтроки = НомерСтроки + 1;
				Продолжить;
			КонецЕсли;     
			Если СтрокаПо<>0 И НомерСтроки>СтрокаПо Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
		Если ПоляФиксированнойШирины Тогда
			МассивСтрокиФайла = ПрочитатьСтрокуСПолямиФиксированнойШирины(СтрокаФайла);
		Иначе
			МассивСтрокиФайла = ПрочитатьСтрокуСРазделителями(СтрокаФайла);
		КонецЕсли;
		
		НоваяСтрока = Товары.Добавить();
		Для Каждого СтрокаФормата Из ФорматСтроки.Выгрузить() Цикл
			НоваяСтрока[СтрокаФормата.Значение] = МассивСтрокиФайла[СтрокаФормата.НомерСтроки-1];	
		КонецЦикла;
		
		СтрокаФайла = ЧтениеТекста.ПрочитатьСтроку();
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	Товары.Свернуть("Номер,Наименование,Номенклатура,СвободныйОстаток,Резерв,Остаток","Количество");
	
	#Если Клиент Тогда
		Если ЭтаФорма <> Неопределено Тогда
			ЭтаФорма.ЭлементыФормы.Индикатор.Видимость=Ложь;
		КонецЕсли;
	#КонецЕсли
	
	ТаблицаОстатков = ПолучитьТаблицуОстатков(Товары);
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номер", СтрокаТоваров.Номер);
		СтрокиОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоиска);
		Если СтрокиОстатков.Количество() > 0 Тогда
			СтрокаТоваров.Номенклатура = СтрокиОстатков[0].Номенклатура;
			СтрокаТоваров.СвободныйОстаток = СтрокиОстатков[0].КоличествоОстаток - СтрокиОстатков[0].РезервОстаток;
			СтрокаТоваров.Резерв = СтрокиОстатков[0].РезервОстаток;
			СтрокаТоваров.Остаток = СтрокиОстатков[0].КоличествоОстаток;
		КонецЕсли;
	КонецЦикла;
	
	РезультатЗагрузки = Товары;
	
	#Если Клиент Тогда
		Если ЭтаФорма <> Неопределено Тогда
			ЭтаФорма.Товары.Очистить();
			Для Каждого СтрокаТаблицы Из РезультатЗагрузки Цикл
				НоваяСтрока = ЭтаФорма.Товары.Добавить();
				Для Каждого Колонка Из РезультатЗагрузки.Колонки Цикл
					НоваяСтрока[Колонка.Имя] = СтрокаТаблицы[Колонка.Имя];	
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	#КонецЕсли

	
	ЧтениеТекста = Неопределено;
 
КонецПроцедуры

// Процедура создания элемента справочника
Функция СоздатьЭлементСправочникаНоменклатуры(Наименование,Артикул,ПроизводительМодели)
	
	//Определим какой производитель будет подставляться в создаваемую номенклатуру
	Если ИспользоватьПроизводителяМодели Тогда
		ПроизводительНом = ?(ПроизводительМодели = Неопределено,Справочники.Производители.ПустаяСсылка(),ПроизводительМодели);
	Иначе
		ПроизводительНом = Производитель;
	КонецЕсли;
	// если каталожные номера уникальны, то сначала поищем существующий элемент
	Если обПраво("НомерПоКаталогуУникальный",Права) Тогда
		//СуществующаяСсылка = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",Артикул);
		АртикулДляПоиска   = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул,ПроизводительНом);                   
		СуществующаяСсылка = Справочники.Номенклатура.НайтиНоменклатуру(АртикулДляПоиска,ПроизводительНом);
		Если НЕ СуществующаяСсылка.Пустая() Тогда
			Возврат СуществующаяСсылка;	
		КонецЕсли;
	КонецЕсли;
	
	НовыйЭлемент=Справочники.Номенклатура.СоздатьЭлемент();
	Если НЕ Родитель.Пустая() И Родитель.ЭтоГруппа Тогда
		НовыйЭлемент.Родитель=Родитель;
	КонецЕсли;
	НовыйЭлемент.УстановитьНовыйКод();
	НовыйЭлемент.Наименование=Наименование;
	НовыйЭлемент.НаименованиеИностранное=Наименование;
	НовыйЭлемент.НаименованиеПолное=Наименование;
	НовыйЭлемент.Артикул=Артикул;
	НовыйЭлемент.ОбработкаРеквизита("Артикул");
	НовыйЭлемент.ТипНоменклатуры=ТипНоменклатуры;
	НовыйЭлемент.ВидНоменклатуры=ВидНоменклатуры;
	НовыйЭлемент.БазоваяЕдиницаИзмерения=БазоваяЕдиницаИзмерения;
	НовыйЭлемент.СтавкаНДС=СтавкаНДС;
	НовыйЭлемент.ВалютаУчета=ВалютаУчета;
	НовыйЭлемент.СтранаПроисхождения=Страна;
	//НовыйЭлемент.Производитель=Производитель;
	НовыйЭлемент.Производитель = ПроизводительНом;
	Попытка
		НовыйЭлемент.Записать();
		Сообщить("Записан справочник номенклатуры <"+спПолучитьНаименование(НовыйЭлемент)+">");
		Попытка	// Возможна ошибка при записи основной единицы измерения (если не задана базовая)
			НовыйЭлемент.ОсновнаяЕдиницаИзмерения = НовыйЭлемент.ПолучитьЕдиницуИзмеренияПоБазовой();
			НовыйЭлемент.Записать();
		Исключение
			Сообщить("Ошибка записи основной единицы измерения для номенклатуры <"+спПолучитьНаименование(НовыйЭлемент)+">");
		КонецПопытки;
		
		Если ТипНоменклатуры.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Шины Тогда
			ЗначенияСвойствОбъекта=Обработки.ЗначенияСвойствОбъекта.Создать();
			ЗначенияСвойствОбъекта.НазначениеСвойств    = Неопределено;
			ЗначенияСвойствОбъекта.ОбъектОтбораЗначений = НовыйЭлемент.Ссылка;
			ЗначенияСвойствОбъекта.ПрочитатьЗаполнитьСвойстваИЗначения();
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныВысота,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаВысота;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныШирина,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаШирина;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныРадиус,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаРадиус;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныПрофиль,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаПрофиль;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныНазначение,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаНазначение;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныКонструкция,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаКонструкция;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСезонность,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаСезонность;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексНагрузки,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаИндексНагрузки;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексСкорости,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаИндексСкорости;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСпособГерметизации,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаСпособГерметизации;
			КонецЕсли; 
			Если НЕ ЗначенияСвойствОбъекта.ЗаписатьВсеЗначенияСвойств() Тогда
				дкДобавитьОшибку(ОписаниеОшибок,"["+ Артикул +"] При записи свойств номенклатуры произошла ошибка.");
			КонецЕсли;
		ИначеЕсли ТипНоменклатуры.ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Диски Тогда
			ЗначенияСвойствОбъекта=Обработки.ЗначенияСвойствОбъекта.Создать();
			ЗначенияСвойствОбъекта.НазначениеСвойств    = Неопределено;
			ЗначенияСвойствОбъекта.ОбъектОтбораЗначений = НовыйЭлемент.Ссылка;
			ЗначенияСвойствОбъекта.ПрочитатьЗаполнитьСвойстваИЗначения();
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиШирина,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ДискШирина;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ШиныРадиус,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ШинаРадиус;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиКоличествоОтверстий,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ДискКоличествоОтверстий;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиРасстояниеМеждуОтверстиями,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ДискРасстояниеМеждуОтверстиями;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиВылет,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ДискВылет;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиДиаметрСтупицы,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ДискДиаметрСтупицы;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиМодель,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ДискМодель;
			КонецЕсли; 
			Свойство=ЗначенияСвойствОбъекта.СвойстваИЗначения.Найти(ПланыВидовХарактеристик.СвойстваОбъектов.ДискиТип,"Свойство");
			Если Свойство<>Неопределено Тогда
				Свойство.Значение=ДискТип;
			КонецЕсли; 
			Если НЕ ЗначенияСвойствОбъекта.ЗаписатьВсеЗначенияСвойств() Тогда
				дкДобавитьОшибку(ОписаниеОшибок,"["+ Артикул +"] При записи свойств номенклатуры произошла ошибка.");
			КонецЕсли;
		КонецЕсли;
	Исключение
		дкДобавитьОшибку(ОписаниеОшибок,"["+ Артикул +"] Ошибка записи элемента справочника номенклатуры """+Наименование+""".");
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецПопытки;
	Возврат НовыйЭлемент.Ссылка;
КонецФункции

// Добавляет строку в табличную часть
Процедура ДобавитьСтрокуВТабличнуюЧасть(ЭтаФорма,СтрокаТаблицы)
	СтруктураОтбора = Новый Структура("Номенклатура", СтрокаТаблицы.Номенклатура);
	СтрокиТоваров = ТабличнаяЧастьДокумента.НайтиСтроки(СтруктураОтбора);
	Если СтрокиТоваров.Количество() > 0 Тогда
		СтрокаДокумента = СтрокиТоваров[0];
		ЭтоНоваяСтрока = Ложь;
	Иначе
		СтрокаДокумента = ТабличнаяЧастьДокумента.Добавить();
		СтрокаДокумента.Номенклатура = СтрокаТаблицы.Номенклатура;
		ЭтаФорма.ОбработкаРеквизита("Товары.Номенклатура",СтрокаДокумента,ЭтаФорма);
		ЭтоНоваяСтрока = Истина;
	КонецЕсли;
	СтрокаДокумента.Количество = ?(ЭтоНоваяСтрока,СтрокаТаблицы.Количество,СтрокаДокумента.Количество + СтрокаТаблицы.Количество);
	ЭтаФорма.ОбработкаРеквизита("Товары.Количество",СтрокаДокумента,ЭтаФорма);
КонецПроцедуры

// Процедура загрузки номенклатуры в справочник
Функция ЗагрузитьНоменклатуруВСправочник(ЭтаФорма=Неопределено,ПроизводительМодели = Неопределено) Экспорт
	Если РезультатЗагрузки = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Для Каждого СтрокаТаблицы Из РезультатЗагрузки Цикл
		Если НЕ СтрокаТаблицы.Пометка Тогда
			Продолжить;	
		Иначе	
			Если СтрокаТаблицы.Номенклатура.Пустая() Тогда
				//СтрокаТаблицы.Номенклатура = СоздатьЭлементСправочникаНоменклатуры(СтрокаТаблицы.Наименование,СтрокаТаблицы.Номер);	
				СтрокаТаблицы.Номенклатура = СоздатьЭлементСправочникаНоменклатуры(СтрокаТаблицы.Наименование,СтрокаТаблицы.Номер,ПроизводительМодели);	
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаОстатков = ПолучитьТаблицуОстатков(РезультатЗагрузки);
	Для Каждого СтрокаТоваров Из РезультатЗагрузки Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", СтрокаТоваров.Номенклатура);
		СтрокиОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоиска);
		Если СтрокиОстатков.Количество() > 0 Тогда
			СтрокаТоваров.СвободныйОстаток = СтрокиОстатков[0].КоличествоОстаток - СтрокиОстатков[0].РезервОстаток;
			СтрокаТоваров.Резерв = СтрокиОстатков[0].РезервОстаток;
			СтрокаТоваров.Остаток = СтрокиОстатков[0].КоличествоОстаток;
		КонецЕсли;
	КонецЦикла;
	
	#Если Клиент Тогда
	Если ЭтаФорма <> Неопределено Тогда
		ЭтаФорма.Товары = РезультатЗагрузки;			
	КонецЕсли;
	#КонецЕсли

	Возврат Истина;
КонецФункции

// Загрузка сотава ТЧ "Номенклатура" в документ
Функция ДобавитьВДокумент(ЭтаФорма=Неопределено) Экспорт
	Если РезультатЗагрузки = Неопределено ИЛИ НЕ ДобавлениеВДокумент Тогда
		Возврат Ложь;
	КонецЕсли;
	Для Каждого СтрокаТаблицы Из РезультатЗагрузки Цикл
		Если НЕ СтрокаТаблицы.Пометка Тогда
			Продолжить;	
		ИначеЕсли НЕ СтрокаТаблицы.Номенклатура.Пустая() Тогда
			ДобавитьСтрокуВТабличнуюЧасть(ЭтаФорма.ВладелецФормы,СтрокаТаблицы);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

// Загрузка сотава ТЧ "Автоработы" в документ
Процедура ДобавитьАвтоработуВДокумент(Автоработы,ВладелецФормы) Экспорт
	Если ВладелецФормы.Метаданные().ТабличныеЧасти.Найти("Работы")<>Неопределено Тогда
		ОбработкаПодборРабот=Обработки.ПодборРабот.Создать();
		ИмяКолонкиРабота="Работа";
		ТабличноеПоле="Работы";
		ФормаДокумента=ВладелецФормы;
		СтруктураСтрокиРабота = Новый Структура();
		Для Каждого Работа Из Автоработы Цикл
			СтруктураСтрокиРабота.Вставить("Работа",Работа.Авторабота);
			СтруктураСтрокиРабота.Вставить("КоличествоРабот",Работа.Количество);
			ОбработкаПодборРабот.ДобавитьРаботуВДокумент(СтруктураСтрокиРабота,ИмяКолонкиРабота,ТабличноеПоле,ФормаДокумента,Ложь);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Удаляет файл импорта(принятое сообщение от Микрокат)
Процедура УдалитьФайлИмпорта() Экспорт
	
	// Позаботимся о файле импорта, и вообще каждый сам должен заботится о файлах,
	// потому как о файле экспорта Микрокат сам заботится :)
	Попытка
		УдалитьФайлы(КаталогФайлов+ИмяФайлаИмпорта);
	Исключение
		// Возможно установлен атрибут "только чтение"(пока только предположения)... Бог его знает что ещё может быть...
		Сообщить(ОписаниеОшибки());		
	КонецПопытки;
	
КонецПроцедуры // УдалитьФайлИмпорта()

// Возвращает таблицу остатков для номенклатуры
Функция ПолучитьТаблицуОстатков(Артикул) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ТабНоменклатура.Ссылка.Артикул КАК Номер,
	|	ТабНоменклатура.Ссылка КАК Номенклатура,
	|	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0)) КАК КоличествоОстаток,
	|	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0)) КАК РезервОстаток
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Номенклатура.Ссылка КАК Ссылка
	|	ИЗ
	|		Справочник.Номенклатура КАК Номенклатура
	|	ГДЕ
	|		Номенклатура.Артикул В(&СписокНомеров)) КАК ТабНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Период, " + ?(СкладКомпании.Пустая(), "","СкладКомпании = &СкладКомпании") + ") КАК ОстаткиТоваровКомпанииОстатки
	|		ПО ТабНоменклатура.Ссылка = ОстаткиТоваровКомпанииОстатки.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабНоменклатура.Ссылка.Артикул,
	|	ТабНоменклатура.Ссылка");
				
	Запрос.УстановитьПараметр("СписокНомеров", Артикул);
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	Запрос.УстановитьПараметр("Период",ТекущаяДата());
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьТаблицуОстатков()

// Поиск контрагента по коду, либо по наименованию
Функция ПоискКонтрагента(ТипЗапросаМикрокат, ТекстЗапросаМикрокат) Экспорт
	// ТипЗапросаМикрокат - Либо значение кода, либо значение наименования контрагента	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты."+?(ТипЗапросаМикрокат="1","Наименование ПОДОБНО &ТекстЗапроса","Код=&ТекстЗапроса");
	Запрос.УстановитьПараметр("ТекстЗапроса",ТекстЗапросаМикрокат);
	ТЗРезультатЗапроса=Запрос.Выполнить().Выгрузить();
	
	Возврат ТЗРезультатЗапроса;
	
КонецФункции // ПоискКонтрагента()

// Возвращает список автомобилей контрагента
Функция ПолучитьАвтомобилиКонтрагента(Контрагент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	АвтомобилиСрезПоследних.Автомобиль.Ссылка КАК Автомобиль
	|ИЗ
	|	РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)) КАК АвтомобилиСрезПоследних
	|ГДЕ
	|	АвтомобилиСрезПоследних.Значение = &Хозяин";
	Запрос.УстановитьПараметр("Хозяин",Контрагент);
	РезультатЗапроса=Запрос.Выполнить().Выгрузить();
	Возврат РезультатЗапроса;
	
КонецФункции // ПолучитьАвтомобилиКонтрагента()

// Возвращает найденное транспортное средство по VIN, либо гаражному номеру
Функция ПоискТранспортногоСредства(VIN=Неопределено,НомерГаражный=Неопределено) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	Автомобили.Ссылка КАК Автомобиль
	|ИЗ
	|	Справочник.Автомобили КАК Автомобили
	|ГДЕ
	|	Автомобили.VIN = &VIN
	|	ИЛИ Автомобили.НомерГаражный = &НомерГаражный";
	Запрос.УстановитьПараметр("VIN",VIN);
	Запрос.УстановитьПараметр("НомерГаражный",НомерГаражный);
	РезультатЗапроса=Запрос.Выполнить().Выгрузить();
	Возврат РезультатЗапроса;
	
КонецФункции // ПоискТранспортногоСредства()

// Приводит строку артикула к строке с требуемой маской
Функция ПолучитьПредставлениеАртикула(Артикул) Экспорт 
	
	// Из микрокат передается артикул в виде строки без разделителя "-". Строка всегда состоит из 10 символов.
	// Артикул до: 0911160075
	// Нужно его привести к нормальному виду по аналогии с микрокат
	// Артикул после: 09111-60075
	Если СтрДлина(Артикул)=10 Тогда
		Возврат Лев(Артикул,5)+"-"+Прав(Артикул,5);
	Иначе
		// Никак не представляем
		Возврат Артикул;
	КонецЕсли;
	
КонецФункции // ПолучитьПредставлениеАртикула()

// Устанавливает новый номер транзакции
Функция УстановитьНовыйНомерТранзакции() Экспорт
	
	ГСЧ=Новый ГенераторСлучайныхЧисел(ТекущаяДата()-'00010101');
	Возврат СтрЗаменить(Строка(ГСЧ.СлучайноеЧисло(150000,190000))," ","");
	
КонецФункции // УстановитьНовыйНомерТранзакции()

// Создание элемента справочника "Контрагенты"
Функция СоздатьЭлементСправочникаКонтрагенты(Наименование) Экспорт
	// НА БУДУЩЕЕ ОБСУДИТЬ С YABS
	
	//НовыйЭлемент =Справочники.Контрагенты.СоздатьЭлемент();
	////ТипКонтрагента = НеНайденныеКонтрагенты[Индекс].Значение["ТИПКОНТРАГЕНТА"];
	////Создадим контрагента
	//НовыйЭлемент.УстановитьНовыйКод();
	//Если НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"1"]=Неопределено Тогда
	//	НовыйЭлемент.Наименование=СтрокаКонтрагент;
	//	НовыйЭлемент.НаименованиеПолное=СтрокаКонтрагент;
	//Иначе
	//	НовыйЭлемент.Наименование=НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"1"];
	//	НовыйЭлемент.НаименованиеПолное=НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"1"];
	//КонецЕсли;
	//НовыйЭлемент.ИНН=НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"ИНН"];
	//НовыйЭлемент.КПП = НеНайденныеКонтрагенты[Индекс].Значение[ТипКонтрагента+"КПП"];
	//Если ТипКонтрагента="ПЛАТЕЛЬЩИК" Тогда
	//	НовыйЭлемент.ВидКонтрагента=Перечисления.ВидыКонтрагентов.Покупатель;
	//ИначеЕсли ТипКонтрагента="ПОЛУЧАТЕЛЬ" Тогда
	//	НовыйЭлемент.ВидКонтрагента=Перечисления.ВидыКонтрагентов.Поставщик;
	//Иначе
	//	НовыйЭлемент.ВидКонтрагента=Перечисления.ВидыКонтрагентов.Прочее;
	//КонецЕсли; 
	//НовыйЭлемент.ФормаСобственности=Перечисления.ФормыСобственности.Прочее;
	//Попытка
	//	НовыйЭлемент.Записать();
	//Исключение
	//	Сообщить("Ошибка при записи контрагента: "+ОписаниеОшибки());
	//КонецПопытки;
КонецФункции // СоздатьЭлементСправочникаКонтрагенты()

// Возвращает структуру прдеставления ServiceMenu, для постройки иерархии справочника "Автоработы"
Функция ПолучитьСтруктуруПредставленияServiceMenu(Знач СтрокаMenu) Экспорт
	
	СтруктураРаботы=Новый Структура;
	
	Позиция=Найти(СтрокаMenu,"•");
	СтрНомер=Лев(СтрокаMenu,Позиция);
	СтрСокр=Прав(СтрокаMenu,СтрДлина(СтрокаMenu)-Позиция);
	Если Найти(СтрСокр,"Repair/Replace")>0 Тогда
		Позиция=Найти(СтрСокр,", Repair/Replace.");
		СтрИмя=Лев(СтрСокр,Позиция-1);
		СтруктураРаботы.Вставить("а0","Repair/Replace");
		СтруктураРаботы.Вставить("а1",СтрИмя);
	Иначе
		СтруктураРаботы.Вставить("а0","Service");
		СтруктураРаботы.Вставить("а1",СтрСокр);
	КонецЕсли;
	
	//// CLUTCH: CLUTCH COMPLETE, REMOVE & REPLACE, 5 DR HATCHBACK; 1ZR-FE; MP INJ  PETROL,
	////(-) MULTI MODE MANUAL TRANS, С:01.06.2006 ПО:30.04.2008(не понятно последнее поле с периодом...)
	//Позиция=Найти(СтрокаMenu,":");
	//СтруктураРаботы.Вставить("Заголовок",Лев(СтрокаMenu,Позиция-1));
	//
	//СтрокаMenu=СокрЛП(Прав(СтрокаMenu,СтрДлина(СтрокаMenu)-(Позиция)));
	//
	//Флаг=Ложь; // Признак обрезанной строки с разделителями ","
	//м=0;
	//Пока Найти(СтрокаMenu,";")>0 ИЛИ Найти(СтрокаMenu,",")>0 Цикл
	//	СтруктураСоставаРаботы=Новый Структура;
	//	Позиция=Найти(СтрокаMenu,";");
	//	Строка=Лев(СтрокаMenu,Позиция-1);
	//	Если Найти(СтрокаMenu,";")=0 Тогда
	//		Строка=СтрокаMenu;
	//		Флаг=Истина;
	//	КонецЕсли;
	//	СтрокаMenu=СокрЛП(Прав(СтрокаMenu,СтрДлина(СтрокаMenu)-(Позиция)));
	//	н=0;
	//	Пока Найти(Строка,",")>0 Цикл
	//		Позиция=Найти(Строка,",");
	//		СтруктураСоставаРаботы.Вставить("а"+н,СокрЛП(Лев(Строка,Позиция-1)));
	//		Строка=СокрЛП(Прав(Строка,СтрДлина(Строка)-(Позиция)));
	//		н=н+1;
	//	КонецЦикла;
	//	СтруктураСоставаРаботы.Вставить("а"+н,СокрЛП(Строка));
	//	СтруктураРаботы.Вставить("Структура"+м,СтруктураСоставаРаботы);
	//	м=м+1;
	//	Если Флаг Тогда
	//		Прервать;
	//	КонецЕсли;
	//КонецЦикла;
	
	Возврат СтруктураРаботы;

КонецФункции // ПолучитьСтруктуруПредставленияServiceMenu()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// Получим структуру настроек пользователя
Параметры=пвПривилегированныйМодуль.пвПолучитьПараметрыПользователяMicroCat(ПараметрыСеанса.Пользователь);

Если Параметры<>Неопределено Тогда
	СтруктураПараметровПользователя=Параметры.Получить();
	// Установим настройки пользователя из полученной структуры	
	ИмяФайлаИмпорта=СтруктураПараметровПользователя.ИмяФайлаИмпорта;
	ИмяФайлаЭкспорта=СтруктураПараметровПользователя.ИмяФайлаЭкспорта;
	КаталогФайлов=СтруктураПараметровПользователя.Каталог;
	// На основе настроек установим каталог пользователя
	ПутьКФайлуИмпорта=КаталогФайлов+ИмяФайлаИмпорта;
	ВремяОжиданияОтвета=СтруктураПараметровПользователя.Таймаут;
	
	// Вдруг там 0, тогда по умолчанию установим "ВремяОжидания" равной 10 секундам
	Если ВремяОжиданияОтвета=0 Тогда
		ВремяОжиданияОтвета=10;
	КонецЕсли;
	
КонецЕсли;

РезультатЗагрузки = Неопределено;	
ДобавлениеВДокумент = Ложь;
Права = Неопределено;
ТабличнаяЧастьДокумента = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
ИспользоватьПроизводителяМодели = Истина;

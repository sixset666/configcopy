////////////////////////////////////////////////////////////////////////////////
// ООО "1С-Рарус Альфа-Авто". Департамент разработки. 2011-2016 год.
//------------------------------------------------------------------------------
// Типовое решение "Альфа-Авто: Автосалон+Автосервис+Автозапчасти ПРОФ, редакция 5.1"
//------------------------------------------------------------------------------
// Модуль приложения

//------------------------------------------------------------------------------
// Размерность числовых типов конфигурации:
// Реквизиты типа Сумма       - 15.2
// Реквизиты типа Количество  - 15.3
// Реквизиты типа Коэффициент - 10.3
// Реквизиты типа Курс        - 10.4
// Реквизиты типа Процент     -  5.2

////////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ ПАРАМЕТРОВ СЕАНСА

// РежимРаботы - Строковая числовая маска [123]
// 1 символ: 0-Системное оборудование включается при старте,
//			 1-Включается только по запросу;
// 2 символ: 0-При запуске компонента должна была стать сервером,
//			 1-компонента не должна была становиться сервером,
//			 2-оборудование не используется;
// 3 символ: 0-Текущая сессия не является серверной и не может предоставлять оборудование,
//			 1-сессия является серверной (т.е. может предоставлять доступ к оборудованию);

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Переменные окружения пользователя, используются для авторизации и разделения доступа
// Заполняются из обработки "ПриНачалеРаботыСистемы" при старте и обновлении прав
Перем глПрава					Экспорт; // Соответствие со значениями прав текущего пользователя
Перем глИзбранное				Экспорт; // ДеревоЗначений с данными Избранного
Перем глДеревоОткрытыхФормАРМ 	Экспорт; // Хранит дерево открытых форм для панели АРМ

// компонента защиты
Перем глРарус_Компонента		Экспорт; // Объект компоненты защиты, оборудования и сервисных функций

// оборудование
Перем глТорговоеОборудование	Экспорт; // Класс. Торговое оборудование.
Перем глЗавершениеРаботыСистемы	Экспорт; // Флаг окончания работы

// свойства справочников, документов, оповещение
Перем глСвойстваОбъектов 		Экспорт; // нужна для того, что бы всегда держать только один экземпляр окна свойств

// свойства справочников
Перем глПанельСвойствОбъекта	Экспорт; //нужна для того, что бы всегда держать только один экземпляр окна свойств

// свойства объектов
Перем глФормаПанелиСвойств		Экспорт; // Форма панели свойств объекта

Перем глОповещениеАктивации		Экспорт; // булево, определяет наличие открытых оповещаемых форм для включения оповещений при активации строк списков

// работа с фильтрами
Перем глСтруктураОтбора 		Экспорт; //нужна для того, что бы сохранять текущую структуру отборов

// работа с электронной почтой
Перем глУчетныеЗаписиАвтоматическогоТранспортаПисем Экспорт; // таблица значений, хранящая учетные записи для автоматического получения и доставки электронной почты

// работа со значимыми событиями в файловом режиме.
Перем глОчередьЗначимыхСобытий  Экспорт; // Список значений наступивших значимых событий. Используется для передачи информации в процедуру-обработчик "глОбработатьЗначимоеСобытие"; 

Перем глОбработкаОбмена Экспорт; // обработка обмена в которой ведется лог-файл формирования/получения сообщений обмена
Перем глФронтКассира Экспорт; // Форма текущего открытого фронта кассира

Перем глКонтекстАРМ Экспорт; // Структура содержит констекст пользователя при работе с АРМ-ми
Перем глОткрытаяФормаФункциональнойПанели Экспорт;

// {{{ SMS Коммуникатор}}}  НАЧАЛО

//Глобальные переменные для SMS
Перем глСписокПодписок Экспорт;				//подписки на СМС
Перем глСписокОтработанныхПодписок Экспорт;	//отработанные подписки на СМС
Перем глСписокТелефонов Экспорт;			//структура (Оборудование|СЗ номеров)
Перем глТекущаяКомпонента Экспорт;			//переменная для загрузки нескольких компонент
Перем глОсновнаяСессия Экспорт;				//признак основной сессии
// {{{ SMS Коммуникатор}}}  КОНЕЦ

// {{{ Софт фон проф}}} Начало
//Префиксы и коды
Перем глПрефиксВыходаВГород Экспорт;           				// Переменная для простоты. Префикс выхода в город. Значение в настройки пользователя.
Перем глПрефиксВыходаНаМежгород Экспорт;       				// Переменная для простоты. Префикс выхода на межгород. Значение в настройки пользователя.
Перем глПрефиксВыходаНаМеждународную Экспорт;     			// Переменная для простоты. Префикс выхода на международную линию.
Перем глКодГорода Экспорт;                     				// Переменная для простоты. Код нашего города. Значение в настройки пользователя.
Перем глКодСтраны Экспорт;                     				// Переменная для простоты. Код нашей страны. Значение в настройки пользователя.

Перем глКоличествоХранимыхЦифрТелефона Экспорт;     		// Число, Это то количество символов, которое будет обрезаться справа при поиске и сохранении в базе данных.
Перем глОбработаноВнешнееСобытие Экспорт;			        // Булево, предназначена для того, что бы не обрабатывать внешнее событие в модуле, если оно было обработано в форме

Перем РарусСофтФонПроф Экспорт;                         	// РАРУС обработка управления звонками, проф
Перем РарусСофтФонКомпонента Экспорт;                   	// РАРУС компонента, для работы с компонентой управления звонками
Перем глИспользоватьСофтФонПроф Экспорт;                    // Признак использования СофтФона, Проф как CallCenter для всей конфигурации
Перем глИспользоватьСофтФонПрофТекущийПользователь Экспорт; // Признак использовать СофтФон, Проф для текущего пользолвателя

Перем РарусCLON Экспорт;									// Переменая для работы с обработкой записи разговоров CLON
Перем ObjCLON Экспорт; 										// Внешняя компонента для работы с сервером системы записи разговоров CLON

//Временные переменные
Перем глТекущийПользователь Экспорт;						// Текущий пользователь
//Настройки софт фона
Перем глМаксимальнаяДлинаВнутреннегоНомера Экспорт;     	// Максимальная длина номера телефона, используется для определения
															// внутреннего номера телефона при исходящем звонке, если он явно не определен
// {{{ Софт фон проф}}} Конец

// +СофтФон
Перем сфпПанельУправления Экспорт;			 // COM-объект панели управления СофтФон
Перем сфпObjCLON Экспорт;					 // COM-объект компоненты CLON
Перем сфпСтруктураЗвонков Экспорт;			 // Структура активных звонков
Перем сфпДанныеЗаполнения Экспорт;			 // Структура данных для заполнения телефонного звонка
Перем сфпСоответствиеЛинийИСтатусов Экспорт; // Соответствие, хранящее номера внутр. телефонов и статусов (состояний по ним)
Перем сфпГлобальныеПараметры Экспорт;		 // Структура - хранилище глобальных переменных
// -СофтФон

Перем глАвтокаталог Экспорт; // COM-Объект подключения автокаталога

Перем глСписокПринтеров Экспорт; // Список принтеров для печати

#Область ОбработчикиСобытий

// Обработка предопределенного события
// Инициализирует рабочие переменные сеанса
// Авторизует пользователя, проверяет факт первого запуска, обновляет ИБ
// Выполняет дополнительные действия в соответствии с пользовательскими настройками
Процедура ПриНачалеРаботыСистемы()
	
	ОбработчикПродолжения = Новый ОписаниеОповещения("ПослеСтартаСистемыЗащитыОбычноеПриложение", ЛицензированиеКлиентСобытия);
	ЛицензированиеКлиентСобытия.ПриНачалеРаботыСистемыОбычноеПриложение(ОбработчикПродолжения);
	СохранитьЗначение("ПолучениеПисемПриСтартеСистемы", Истина);
	
	//БИЗТЕХ КОВАЛЬ 29.01.2025 ++
	Если Не ОбПраво("МожноВходитьБолееОдногоРаза") Тогда
		ТекСоединения = ПолучитьСоединенияИнформационнойБазы(); 
		ТекПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
		Если ТекСоединения <> Неопределено Тогда
			УжеЕстьСоединение = Ложь;
			Для Каждого Соединение из ТекСоединения Цикл
				Если Соединение.Пользователь.УникальныйИдентификатор = ТекПользователь.УникальныйИдентификатор И Соединение.ИмяПриложения = "1CV8" Тогда
					Если УжеЕстьСоединение Тогда
						Предупреждение("Нельзя входить более одного раза", 5);     
						ЗавершитьРаботуСистемы(Ложь);
					Иначе
						УжеЕстьСоединение = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	//БИЗТЕХ КОВАЛЬ 29.01.2025 --
КонецПроцедуры

// Обработка предопределенного события
// Сохраняет рабочие переменные сеанса
// Обнуляет системные объекты - компоненты, соединения и т.п
Процедура ПриЗавершенииРаботыСистемы()
	ОписаниеОшибки = "";
	ЛицензированиеСервер.ЗавершениеРаботыСистемыЛицензирования(ОписаниеОшибки);
	
	Попытка // отключаем торговое оборудование
		Если глТорговоеОборудование<>НЕОПРЕДЕЛЕНО Тогда
			глТорговоеОборудование.ОтключитьТорговоеОборудование();
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Попытка
		Если (глРарус_Компонента <> НЕОПРЕДЕЛЕНО) И НЕ обПраво("РазрешитьРаботуССистемойПослеВыхода",глПрава) Тогда
			глРарус_Компонента.ЗавершитьРаботу();
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	глТорговоеОборудование = НЕОПРЕДЕЛЕНО;
	глРарус_Компонента = НЕОПРЕДЕЛЕНО;
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

// процедура вызывается обработчиком ожидания через время указанное в настройке "ПериодОбновленияДанныхОборудования"
// вызывается только в том случае, если к компьютеру подключено "автообновляемое" оборудование.
Процедура ОбновитьДанныеОборудования() Экспорт
	глТорговоеОборудование.ОбновитьДанныеАвтообновляемогоОборудования();
КонецПроцедуры

// ОбработкаПланировщикаЗадач разбита на 2 процедуры, что бы можно было вызывать как из текста так и таймером
// Режим. Перечисление.ПериодыЗапускаЗадачПланировщика. Если значение не указано, то процедура вызвана таймером
Процедура ОбработкаПланировщикаЗадач(Режим=Неопределено) Экспорт
	// выберем задачи текущего пользователя
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ЗадачиПланировщика.ВидЗадачи,
	|	ЗадачиПланировщика.Объект,
	|	ЗадачиПланировщика.ПериодЗапускаЗадачи,
	|	ЗадачиПланировщика.МоментЗапускаЗадачи,
	|	ЗадачиПланировщика.ДействиеПриОшибке,
	|	ЗадачиПланировщика.Периодическая,
	|	ЗадачиПланировщика.ДатаИВремяОднократногоЗапуска,
	|	ЗадачиПланировщика.УдалятьЗадачуПослеЗапуска
	|ИЗ
	|	РегистрСведений.ЗадачиПланировщика КАК ЗадачиПланировщика
	|
	|ГДЕ
	|	ЗадачиПланировщика.Пользователь = &Пользователь
	|	"+?(Режим<>Неопределено,"И ЗадачиПланировщика.ПериодЗапускаЗадачи=&ПериодЗапускаЗадачи",""));
	Запрос.УстановитьПараметр("Пользователь",ПараметрыСеанса.Пользователь);
	Запрос.УстановитьПараметр("ПериодЗапускаЗадачи",Режим);
	РезультатЗадачи=Запрос.Выполнить();
	// если нет ничего, то ничего и не делаем
	Если РезультатЗадачи.Пустой() Тогда Возврат; КонецЕсли;
	// задачи есть. выполняем
	ВыборкаЗадачи=РезультатЗадачи.Выбрать();
	Пока ВыборкаЗадачи.Следующий() Цикл
		// проверим пришло ли время
		ВремяПришло=(Режим<>Неопределено);
		
		Если ВыборкаЗадачи.Периодическая Тогда
		
			Если ВыборкаЗадачи.ПериодЗапускаЗадачи=Перечисления.ПериодыЗапускаЗадачПланировщика.Ежедневно Тогда
				Секунд=ВыборкаЗадачи.МоментЗапускаЗадачи-НачалоДня(ВыборкаЗадачи.МоментЗапускаЗадачи);
				СейчасСекунд=ТекущаяДата()-НачалоДня(ТекущаяДата());
				Интервал=СейчасСекунд-Секунд;
				ВремяПришло=(Интервал>0 И Интервал<=10);
			КонецЕсли;
			
		Иначе
			
			Если НачалоДня(ТекущаяДата())=НачалоДня(ВыборкаЗадачи.ДатаИВремяОднократногоЗапуска) Тогда
				// отработаем проверку времени запуска одноразовой задачи
				Секунд=ВыборкаЗадачи.ДатаИВремяОднократногоЗапуска-НачалоДня(ВыборкаЗадачи.ДатаИВремяОднократногоЗапуска);
				СейчасСекунд=ТекущаяДата()-НачалоДня(ТекущаяДата());
				Интервал=СейчасСекунд-Секунд;
				ВремяПришло=(Интервал>0 И Интервал<=10);
			Иначе
				ВремяПришло=Ложь;
			КонецЕсли;
			
		КонецЕсли;
		// если время еще не пришло, то уйдем
		Если НЕ ВремяПришло Тогда Продолжить; КонецЕсли;
		КоличествоПопыток=10; // количество попыток выполнения действия
		ЕстьОшибка=Ложь;
		
	~Обратно:
		// избежим зацикливания
		КоличествоПопыток=КоличествоПопыток-1; // попытка прошла
		// попытки закончились
		Если КоличествоПопыток=0 Тогда 
			Сообщить("Задачу "+СокрЛП(ВыборкаЗадачи.ВидЗадачи)+" "+СокрЛП(ВыборкаЗадачи.Объект)+" выполнить не удалось!",СтатусСообщения.ОченьВажное);
			Сообщить("Было предпринято "+СокрЛП(КоличествоПопыток)+" попыток.",СтатусСообщения.Информация);
			Продолжить; 
		КонецЕсли;
		
		// смотрим что за задача
		Если ВыборкаЗадачи.ВидЗадачи=Перечисления.ВидЗадачПланировщика.ВосстановлениеПоследовательности Тогда
			//Попытка
			//	Последовательности[ВыборкаЗадачи.Объект].Восстановить(ТекущаяДата());
			//Исключение
			//	ЕстьОшибка=Истина;
			//КонецПопытки;
		ИначеЕсли ВыборкаЗадачи.ВидЗадачи=Перечисления.ВидЗадачПланировщика.ЗавершениеРаботыСистемы Тогда
			// завершаем
			ЗавершитьРаботуСистемы(Ложь);
		ИначеЕсли ВыборкаЗадачи.ВидЗадачи=Перечисления.ВидЗадачПланировщика.ЗапускПроцедуры Тогда
			// пытаемся выполнить процедуру
			Попытка
				Выполнить(ВыборкаЗадачи.Объект);
			Исключение
				ЕстьОшибка=Истина;
			КонецПопытки;
		ИначеЕсли ВыборкаЗадачи.ВидЗадачи=Перечисления.ВидЗадачПланировщика.ОткрытиеФормы Тогда
			ФормаОткрытия=Неопределено;
			Если Найти(ВыборкаЗадачи.Объект,"ОБЩАЯФОРМА.")>0 Тогда
				// имеем общую форму
				Попытка ФормаОткрытия=ПолучитьОбщуюФорму(СтрЗаменить(ВыборкаЗадачи.Объект,"ОБЩАЯФОРМА.","")); Исключение КонецПопытки;
			Иначе
				Об=Неопределено;
				// попытаемся создать такой объект
				Попытка
					Об=Новый(Вычислить(ВыборкаЗадачи.Объект));
				Исключение
					// тогда подумаем, что у нас внешняя обработка
					Попытка Об=ВнешниеОбработки.Создать(ВыборкаЗадачи.Объект); Исключение КонецПопытки;
				КонецПопытки;
				// если есть что-то то открываем
				Если Об<>Неопределено Тогда
					Попытка
						ФормаОткрытия=Об.ПолучитьФормуСписка();
					Исключение
						Попытка
							ФормаОткрытия=Об.ПолучитьФорму();
						Исключение
							ЕстьОшибка=Истина;
						КонецПопытки;
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;
			Если ФормаОткрытия<>Неопределено Тогда
				
				ЭтоФормаОтчета = Ложь;
				Если ТипЗнч(ФормаОткрытия) = Тип("ФормаКлиентскогоПриложения") И ФормаОткрытия.ИмяФормы = "ОбщаяФорма.Отчет_Тонкий_Клиент" Тогда
					ЭтоФормаОтчета = Истина;
				КонецЕсли;
				
				Если ЭтоФормаОтчета Тогда
					Отчет = Об.Создать();
					Отчет.ЗаполнитьНачальныеНастройки();
					
					МетаданныеОтчета  = Отчет.Метаданные();
					ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
					
					СтруктураПараметров = Новый Структура();
					СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
					СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
					
					ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);				
				Иначе
					ФормаОткрытия.Открыть();					
				КонецЕсли;				
			КонецЕсли;
		КонецЕсли;
		
		// ошибка, посмотрим что делаем по ошибке
		Если ЕстьОшибка Тогда
			Если ВыборкаЗадачи.ДействиеПриОшибке=Перечисления.ВидыДействийПриОшибкеВыполненияЗадачи.ЗавершитьРаботуСистемы Тогда
				ЗавершитьРаботуСистемы(Ложь);
			ИначеЕсли ВыборкаЗадачи.ДействиеПриОшибке=Перечисления.ВидыДействийПриОшибкеВыполненияЗадачи.ПовторятьПопытку Тогда
				Перейти ~Обратно;
			Иначе
				Продолжить;
			КонецЕсли;
		Иначе
			// Если задача исполнилась успешно - и при этом она одноразовая, и
			// у неё взведён флаг удаления после исполнения - то удалим её
			Если (НЕ ВыборкаЗадачи.Периодическая) И (ВыборкаЗадачи.УдалятьЗадачуПослеЗапуска) Тогда
				ОтборПланировщика=Новый Структура;
				ОтборПланировщика.Вставить("Пользователь", ПараметрыСеанса.Пользователь);

				Выборка=РегистрыСведений.ЗадачиПланировщика.Выбрать(ОтборПланировщика);
				Пока Выборка.Следующий() Цикл
					Если
						(Выборка.ВидЗадачи=ВыборкаЗадачи.ВидЗадачи) И
						(Выборка.Объект=ВыборкаЗадачи.Объект) И
						(Выборка.ПериодЗапускаЗадачи=ВыборкаЗадачи.ПериодЗапускаЗадачи) И
						(Выборка.МоментЗапускаЗадачи=ВыборкаЗадачи.МоментЗапускаЗадачи) И
						(Выборка.ДействиеПриОшибке=ВыборкаЗадачи.ДействиеПриОшибке) И
						(Выборка.Периодическая=ВыборкаЗадачи.Периодическая) И
						(Выборка.ДатаИВремяОднократногоЗапуска=ВыборкаЗадачи.ДатаИВремяОднократногоЗапуска) И
						(Выборка.УдалятьЗадачуПослеЗапуска=ВыборкаЗадачи.УдалятьЗадачуПослеЗапуска)
					Тогда		
						Запись=Выборка.ПолучитьМенеджерЗаписи();
						Запись.Удалить();
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// вызывается каждые 10 сек. проверяется наличие задач у пользователя и если
// подошел срок их исполнения, то задачи выполняются
//
// Динамически подключаемый обработчик изменения данных
//
Процедура глОбработкаПланировщикаЗадач() Экспорт
	ОбработкаПланировщикаЗадач();
КонецПроцедуры
	
// Процедура проверяет необходимость подключения напоминаний
//
// Параметры:
//  Нет.
//
Процедура глПроверитьПодключениеНапоминания() Экспорт
	Если Константы.ИнтервалПроверкиНапоминаний.Получить() > 0 Тогда
		ПодключитьОбработчикОжидания("опОбработкаОповещений", 60 * Константы.ИнтервалПроверкиНапоминаний.Получить());
	Иначе 
		ОтключитьОбработчикОжидания("опОбработкаОповещений");
	КонецЕсли; 
КонецПроцедуры // ПроверитьПодключениеНапоминания()

// Процедура осуществляет проверку на необходимость транспорта писем по учетным записям,
// с заданным интервалом.
//
// Динамически подключаемый обработчик изменения данных
//
Процедура глАвтоматическаяПроверкаПисем() Экспорт
    ДатаНачалаПроверки = ТекущаяДата();
	ОтключитьОбработчикОжидания("глАвтоматическаяПроверкаПисем");
	ПолучениеПисемПриСтартеСистемы = ВосстановитьЗначение("ПолучениеПисемПриСтартеСистемы");
	Если ПолучениеПисемПриСтартеСистемы = Неопределено Тогда
		ПолучениеПисемПриСтартеСистемы = Ложь;
	КонецЕсли;
	МассивОбработанныхСтрок = Новый Массив;
	Для каждого СтрокаТЧ Из глУчетныеЗаписиАвтоматическогоТранспортаПисем Цикл
		Если СтрокаТЧ.ВремяПоследнегоПолучения + СтрокаТЧ.ИнтервалОбновления * 60 > ТекущаяДата() Тогда
			Продолжить;
		КонецЕсли;
		МассивОбработанныхСтрок.Добавить(СтрокаТЧ);
	КонецЦикла;
	ТекстНапоминания = "";
	Если МассивОбработанныхСтрок.Количество() > 0 Тогда
		КоличествоПолученныхПисемВсего = 0;
		Для каждого СтрокаТЧ Из МассивОбработанныхСтрок Цикл
			Если ПолучениеПисемПриСтартеСистемы И НЕ СтрокаТЧ.УчетнаяЗапись.ПриниматьПисьмаПриСтартеСистемы Тогда
				Продолжить;
			КонецЕсли;
			КоличествоПолученныхПисем = 0;
			Если СтрокаТЧ.Действие = Перечисления.ВидыДействийАвтоПолученияОтправкиЭлектронныхПисем.Получение Тогда
				КоличествоПолученныхПисем = эпПолучитьЭлектронныеПисьма(СтрокаТЧ.УчетнаяЗапись, Истина, ПолучениеПисемПриСтартеСистемы);
			ИначеЕсли СтрокаТЧ.Действие = Перечисления.ВидыДействийАвтоПолученияОтправкиЭлектронныхПисем.Отправка Тогда
				эпОтправитьЭлектронныеПисьма(СтрокаТЧ.УчетнаяЗапись);
			Иначе
				эпОтправитьЭлектронныеПисьма(СтрокаТЧ.УчетнаяЗапись);
				КоличествоПолученныхПисем = эпПолучитьЭлектронныеПисьма(СтрокаТЧ.УчетнаяЗапись, Истина, ПолучениеПисемПриСтартеСистемы);
			КонецЕсли; 
			СтрокаТЧ.ВремяПоследнегоПолучения = ТекущаяДата();
			
			КоличествоПолученныхПисемВсего = КоличествоПолученныхПисемВсего + КоличествоПолученныхПисем;
			Если (КоличествоПолученныхПисем > 0) И (СтрокаТЧ.УчетнаяЗапись.ОповещатьОбАвтополученииПочты) Тогда
				Если Не ТекстНапоминания = "" Тогда
					ТекстНапоминания = ТекстНапоминания + Символы.ПС;
				КонецЕсли;
				ОкончаниеТекста = " новых писем";
				ПоследниеДвеЦифры = КоличествоПолученныхПисем % 100;
				ПоследняяЦифра = КоличествоПолученныхПисем % 10;
				Если (ПоследниеДвеЦифры < 5) Или (ПоследниеДвеЦифры > 20) Тогда
					Если ПоследняяЦифра = 1 Тогда
						ОкончаниеТекста = " новое письмо";
					Иначе
						Если ПоследняяЦифра < 5 Тогда
							ОкончаниеТекста = " новых письма";
						КонецЕсли;			
					КонецЕсли;	
				КонецЕсли;
				ТекстНапоминания = ТекстНапоминания + "Учетная запись """ + СтрокаТЧ.УчетнаяЗапись + """: " + КоличествоПолученныхПисем + ОкончаниеТекста;
			КонецЕсли;
		КонецЦикла;
		Если Не ТекстНапоминания = "" Тогда
			НовоеНапоминание = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
			
			// заполним измерения
			НовоеНапоминание.Пользователь = ПараметрыСеанса.Пользователь;
			НовоеНапоминание.Автор = ПараметрыСеанса.Пользователь;
			НовоеНапоминание.Завершено = Ложь;
			НовоеНапоминание.РеальнаяДатаОповещения = ТекущаяДата();
			НовоеНапоминание.Объект = Неопределено;
			
			// заполним ресурсы
			НовоеНапоминание.ДатаАктуальности = '00010101';
			НовоеНапоминание.ДатаНачала = ТекущаяДата();
			НовоеНапоминание.ДатаОповещения = ТекущаяДата();
			НовоеНапоминание.Описание = ТекстНапоминания;
			НовоеНапоминание.Редактирование = Ложь;
			НовоеНапоминание.СрокДоНачала = 0;
			НовоеНапоминание.Тема = "Получены новые письма";
			НовоеНапоминание.ТипПериода = "00";
			НовоеНапоминание.УдалитьПоИстеченииСрока = Истина;
			
			НовоеНапоминание.Записать(Ложь);
		КонецЕсли;
		Если КоличествоПолученныхПисемВсего > 0 Тогда
			Оповестить("ОбновитьДеревоГрупп", "Пересчитать");
		КонецЕсли;	
	КонецЕсли; 
	
	СохранитьЗначение("ПолучениеПисемПриСтартеСистемы", Ложь);
	ПодключитьОбработчикОжидания("глАвтоматическаяПроверкаПисем", 60);
	
КонецПроцедуры

// Процедура-обработчик значимых событий.
// Используется только в файловом режиме работы программы.
Процедура глОбработатьЗначимоеСобытие() Экспорт
	Если глОчередьЗначимыхСобытий=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для каждого ТекЗначимоеСобытие Из глОчередьЗначимыхСобытий Цикл
		сбСобытия.ОбработатьЗначимоеСобытие(ТекЗначимоеСобытие.Источник, ТекЗначимоеСобытие.ЗначимыеСобытия);
	КонецЦикла;
	глОчередьЗначимыхСобытий = Неопределено;
КонецПроцедуры // глОбработатьЗначимоеСобытие()

// Процедура вызывается периодически для обработки текущих регламентных заданий
Процедура глОбработкаПланировщикаЗаданий() Экспорт
	// Любые циклические обработчики должны проверять наличие блокировки на базе
	// и если таковой режим обнаружен немедленно завершать работу чтобы не мешать регламентным операциям
	РежимБлокировки = ПолучитьБлокировкуУстановкиСоединений();
	// Считаем параметры блокировки
	Если РежимБлокировки.Установлена Тогда
		ВремяНачалаБлокировки = РежимБлокировки.Начало;
		Если НЕ ЗначениеЗаполнено(ВремяНачалаБлокировки) ИЛИ (ТекущаяДата() >= ВремяНачалаБлокировки) Тогда
			// Вычеркнем себя из списка выполняющих регламентные операции
			УдалитьДанныеСеансаОбработчикаЗаданий(ПараметрыСеанса.Пользователь,НомерСоединенияИнформационнойБазы(),2);
			// Все завершаем наш сеанс работы и выходим
			ЗавершитьРаботуСистемы(Ложь, Истина);  // Если это был Робот и делал что-то полезное пусть перезапустится и продолжит когда опять будет можно
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ВыполнитьОбработкуЗаданий();
	
	Интервал = Константы.ИнтервалПроверкиЗаданий.Получить();
	Интервал = ?(Интервал < 5, 5 , Интервал); // Не буду я с таким маленьким интервалом регламентные задания выполнять 
	НомерСоединения = НомерСоединенияИнформационнойБазы();
	ПодключитьОбработчикОжидания("глОбработкаПланировщикаЗаданий", Интервал);
КонецПроцедуры

// Процедура вызывается периодически для проверки обновления прав и настроек пользователя
Процедура глОбновитьПраваИНастройки() Экспорт
	
	// проверим, что у нас в режиме работы пользователя
	СтруктураОтбора = Новый Структура("Пользователь,НомерСоединения,Режим",
		ПараметрыСеанса.Пользователь,НомерСоединенияИнформационнойБазы(),3);
	Ресурсы = РегистрыСведений.РежимыРаботыПользователей.Получить(СтруктураОтбора);	
	Если ЗначениеЗаполнено(Ресурсы.Начало) Тогда
		глПрава = Неопределено;
		// Обновим текущий набор прав
		ТекПользователь = ПараметрыСеанса.Пользователь;
		глПрава = обПолучитьПраваИНастройкиПользователя(ТекПользователь);
		
		// Помощник продаж +
		атСервер.ИнициализироватьКодСостояния();
		// Помощник продаж -
		
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		Сообщить(ТекДата + "Права текущего пользователя обновлены !",СтатусСообщения.Информация);
		// Поскольку на экране могут быть открыты формы объектов, скажем им, что надо бы обновить кэш прав..
		Оповестить("ОбновитьПрава",ТекПользователь);
		
		// оповестили один раз и хватит - ждем следующего обновления со стороны другого пользователя
		// здесь мы номер соединения знаем явно
		ТекНомерСоединения = НомерСоединенияИнформационнойБазы();
		УдалитьДанныеСеансаОбработчикаЗаданий(ТекПользователь,ТекНомерСоединения,3);
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработка предопределенного события
// Производит инициализацию системы до отрисовки окна 1С:Предприятия
Процедура ПередНачаломРаботыСистемы(Отказ)
	//Обработчик зарезервирован на будущее
КонецПроцедуры // ПередНачаломРаботыСистемы()

// Обработка предопределенного события
// Выдает запрос на завершение работы
Процедура ПередЗавершениемРаботыСистемы(Отказ)

	Если НЕ глЗавершениеРаботыСистемы Тогда
		// Запрос на завершение работы системы у пользователя
		Если (обПраво("РаботаСФронтомКассира",глПрава)<>Перечисления.РаботаСФронтомКассира.ТолькоСФронтом) И
			 (НЕ обПраво("УРВ_РаботатьТолькоСАРМомСотрудникаЦеха",глПрава)) Тогда
			Если обПраво("ПоказыватьЗапросНаЗавершениеРаботы",глПрава) Тогда
				Если Вопрос("Завершить работу программы?",РежимДиалогаВопрос.ДаНет,5,КодВозвратаДиалога.Да) = КодВозвратаДиалога.Нет Тогда
					Отказ = Истина; // Пользователь передумал и отменил выход из программы
					Возврат;
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	
	глЗавершениеРаботыСистемы = Истина;
	
	ОбработкаПланировщикаЗадач(Перечисления.ПериодыЗапускаЗадачПланировщика.ПередЗавершениемРаботыСистемы);
	
	//+СофтФон
	сфОтключитьОбработчикПланировщикаЗаданий();
	Если НЕ Отказ Тогда
		// Завершим работу СофтФона перед выходом из предприятия
		ТекАТС = Константы.CRM_ИспользуемыйСофтФон.Получить();
		Если ТекАТС = Перечисления.CRM_ИспользуемыйСофтФон.СофтФонПроф Тогда
			Если НЕ (РарусСофтФонПроф = Неопределено) Тогда
				РарусСофтФонПроф.ЗавершитьСофтФон();
			КонецЕсли;
		ИначеЕсли ТекАТС = Перечисления.CRM_ИспользуемыйСофтФон.СофтФон3 Тогда
			сфпСофтФонПроКлиент.сфпОтключитьСофтФон(Отказ);
		КонецЕсли;
	КонецЕсли;
	//-СофтФон

КонецПроцедуры // ПередЗавершениемРаботыСистемы()

// Обработка предопределенного события
// Выполняет действия для всех видов внешних событий
Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные)
	Если Источник = "Сканер" Тогда
		
	ИначеЕсли Источник="Синхронизатор" Тогда
		глТорговоеОборудование.ОбработкаВнешнегоСобытия(Источник, Событие, Данные);
		
	ИначеЕсли Лев(Источник,7)="Задача_" Тогда // изменилось состояние отправленной на другой компьютер задачи
		глТорговоеОборудование.ОбработкаВнешнегоСобытия(Источник, Событие, Данные);
		
	ИначеЕсли Источник = "КОМПОНЕНТА" Тогда
		опОбработкаВнешнегоСобытия(Источник, Событие, Данные);
	ИначеЕсли Источник = "RTUP" Тогда
		CRMОбработкаВнешнегоСобытия(Источник, Событие, Данные, РарусСофтФонПроф, Неопределено, Неопределено, Неопределено);
		глОбработаноВнешнееСобытие = Ложь;
	КонецЕсли; 
	
	// +СофтФон
	ТекАТС = Константы.CRM_ИспользуемыйСофтФон.Получить();
	Если ТекАТС = Перечисления.CRM_ИспользуемыйСофтФон.СофтФон3 Тогда
		сфпСофтФонПроКлиент.сфпВнешнееСобытияСофтфона(Источник, Событие, Данные);
	КонецЕсли;	
	// -СофтФон
	
КонецПроцедуры // ОбработкаВнешнегоСобытия()

// Процедура проверяет, есть ли регистры с выключенными итогами,
// если необходимо включает их использование.
Процедура ОпределитьНеобходимостьВключенияИтогов()
	
	СписокРегистровКВключениюИтогов = Новый СписокЗначений;
	Для Каждого Регистр ИЗ РегистрыНакопления Цикл                               
		МетаданныеРегистра = Метаданные.НайтиПоТипу(Тип(Регистр));
		Если МетаданныеРегистра.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			Если НЕ Регистр.ПолучитьИспользованиеИтогов() Тогда
				СписокРегистровКВключениюИтогов.Добавить(Регистр, "Регистр накопления "+МетаданныеРегистра.Синоним);
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;	
	
	Если СписокРегистровКВключениюИтогов.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Вопрос("Обнаружено отключение использования итогов по ряду регистров." + Символы.ПС 
				+ "Для избежания ошибок рекомендуется включить"+Символы.ПС
				+ "использование итогов регистров.Выполнить это сейчас?", 
			РежимДиалогаВопрос.ДаНет, 25 , КодВозвратаДиалога.Да) <> КодВозвратаДиалога.Да Тогда
			Возврат;
	КонецЕсли;

	Для Каждого РегистрИзСписка Из СписокРегистровКВключениюИтогов Цикл
		РегистрИзСписка.Значение.УстановитьИспользованиеИтогов(Истина);		
		МетаданныеРегистра = Метаданные.НайтиПоТипу(ТипЗнч(РегистрИзСписка.Значение));
		Если Метаданные.РегистрыНакопления.Содержит(МетаданныеРегистра) Тогда
			ТипРегистра = " накопления ";
		КонецЕсли;
		Сообщить("Включено использование итогов регистра"+ТипРегистра+""""+МетаданныеРегистра.Синоним+"""");
	КонецЦикла;
 
КонецПроцедуры

//Функция для отправки SMS
//Входные параметры:
//		Получатели - либо текст с одним номером, либо список значений с номерами
//		Сообщение - текст сообщения
//		Отправитель - строка с номером отправителя из оборудования
//		Оборудование - ссылка на элемент справочника оборудование
//		Транслит - флаг транслитировать текст или нет тип Булево
//		Отчет - Флаг запроса отчета о доставке тип Булево
//		СоздаватьСобытие - ссылка на значение перечисления ВариантыОтветов.
//			Если значение НЕТ то при отправке событие не создается, 
//			если ДА - SMS отправляется и создается событие,
//			СПРОСИТЬ - SMS не отправляется, просто открывается документ Событие
//		СтрокаОшибкиФункции - Описание ошибки в случае неудачной отправки
//Возвращает список значений в котором в представлении указан номер получателя, в значении указан гуид SMS.
//В случае если при отправке возникает ошибка то описание ошибки передается в переменной СтрокаОшибкиФункции.
Функция глОтправитьSMS(Получатели,Сообщение,Отправитель=Неопределено,Оборудование=Неопределено,Транслит=Неопределено,Отчет=Неопределено,СоздаватьСобытие = Неопределено,СтрокаОшибкиФункции = "") Экспорт
	Права = глПрава;
	ОбрТорговоеОборудование = глТорговоеОборудование;
	СтрокаОшибки = "";
	Возврат кмОтправитьSMS(Получатели,Сообщение,ОбрТорговоеОборудование,Права,Отправитель,Оборудование,Транслит,Отчет,СоздаватьСобытие,СтрокаОшибки);
КонецФункции

//Процедура открытия софт-фона
Процедура глОткрытьСофтФонИзМеню() Экспорт
	РезультатПодключения = Ложь;
	
	Если НЕ глИспользоватьСофтФонПроф Тогда
		Предупреждение("СофтФон не используется.");
		Возврат;
	КонецЕсли;
	
	Если НЕ РарусСофтФонПроф = Неопределено И НЕ глРарус_Компонента = Неопределено Тогда
		РезультатПодключения = РарусСофтФонПроф.ПодключитьСофтФон();
	КонецЕсли;
КонецПроцедуры

Процедура ЗапуститьРоботаРеглЗаданий() Экспорт 
	
	РоботРеглЗаданий = Константы.смсРоботРегламентныхЗаданий.Получить();
	Если НЕ РоботРеглЗаданий = Справочники.Пользователи.ПустаяСсылка() Тогда
		Если ПараметрыСеанса.Пользователь = РоботРеглЗаданий Тогда
			ПодключитьОбработчикОжидания("ОбработкаЗаданий", 60);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаданий() Экспорт
	ВыполнитьОбработкуЗаданий();
КонецПроцедуры

//+СофтФон

// Процедура предназначена для открытия СофтФона из меню
Процедура ОткрытьСофтФонИзМеню() Экспорт
	РезультатПодключения = Ложь;
	// Если не выбран ни один из СофтФонов, попросим пользователя указать необходимый
	Если НЕ ЗначениеЗаполнено (Константы.CRM_ИспользуемыйСофтФон.Получить()) Тогда
		Предупреждение("Не выбран используемый КоллЦентр!!! Для того, что бы выбрать"+Символы.ПС+
					   "необходимый КоллЦентр, необходимо открыть форму настройки."+Символы.ПС+
					   "<Сервис>-<Настройка параметров учета> выбрать закладку <СофтФон>"+Символы.ПС+
					   "и указать используемый в системе КоллЦентр."+Символы.ПС+
					   "Так же указать сервер синхронизации. Остальные параметры можно"+Символы.ПС+
					   "заполнить в другое время.",30, "КоллЦентр, не выбран.");
		Возврат;
	КонецЕсли;
	ФлагИспользованияПроф = Константы.CRM_ИспользуемыйСофтФон.Получить();
	Если ФлагИспользованияПроф = Перечисления.CRM_ИспользуемыйСофтФон.СофтФонПроф Тогда
		// Подключаем внешний обработчик для работы телефонии
		Если НЕ РарусСофтФонПроф = Неопределено И НЕ РарусСофтФонКомпонента = Неопределено Тогда
			РезультатПодключения = РарусСофтФонПроф.ПодключитьСофтФон();
		Иначе
			CRMОткрытьСообщениеОшибкиПодключения("СофтФон");
		КонецЕсли;
	ИначеЕсли ФлагИспользованияПроф = Перечисления.CRM_ИспользуемыйСофтФон.СофтФон3 Тогда
		сфпСофтФонПроКлиент.сфпПодключитьСофтФон();
	КонецЕсли;
КонецПроцедуры

//-СофтФон


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

глОповещениеАктивации = Ложь;	// изначально формы списков не вызывают оповещения при активации строк
глАвтокаталог = Неопределено;
глЗавершениеРаботыСистемы = Ложь;
глСписокПринтеров = Неопределено;
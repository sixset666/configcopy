// Предназначена для получения имени элемента перечисления по значению.
//
// Параметры:
//  Элемент перечисления.
//
// Возвращаемое значение:
//  Строка - имя элемента перечисления в метаданных.
//
Функция увкПолучитьИмяЭлементаПеречисленияПоЗначению(ЗначениеПеречисления) Экспорт
	
	ИмяЭлемента = Строка(ЗначениеПеречисления);
	Для каждого ЭлементПеречисления Из Метаданные.Перечисления[ЗначениеПеречисления.Метаданные().Имя].ЗначенияПеречисления Цикл
		Если ЭлементПеречисления.Синоним = Строка(ЗначениеПеречисления) Тогда
			ИмяЭлемента = ЭлементПеречисления.Имя;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ИмяЭлемента;
	
КонецФункции // ПолучитьПериодичностьДляЗапросаПоЗначениюПеречисления()

// Процедура открывает форму справочника ВидыКонтактнойИнформации для выбора.
//
// Параметры:
//  РеджимВыбора - булево, задает режим выбора для открываемой формы.
//  ВладелецФормы - задает владельца для открываемой формы.
//  ЗначениеОтбораПоТипу - задает значение отбора по типу конт.инф.
//  ЗначениеОтбораПоВидуОбъектаКИ - задает значение отбора по виду объекта конт.инф.
Процедура увкОткрытьФормуВыбораВидаКИ(РежимВыбора, ВладелецФормы, ЗначениеОтбораПоТипу = Неопределено) Экспорт

	ВыбФорма = Справочники.ВидыКонтактнойИнформации.ПолучитьФорму("ФормаСписка", ВладелецФормы);
	ВыбФорма.РежимВыбора = РежимВыбора;
	
	Если ЗначениеОтбораПоТипу <> Неопределено Тогда
		ВыбФорма.Отбор.Тип.Значение = ЗначениеОтбораПоТипу;
		ВыбФорма.Отбор.Тип.Использование = Истина;
		ВыбФорма.ЭлементыФормы.Список.НастройкаОтбора.Тип.Доступность = Ложь;
	КонецЕсли; 
	
	Если ТипЗнч(ВладелецФормы.Значение) = Тип("СправочникСсылка.ВидыКонтактнойИнформации")
	   И ЗначениеЗаполнено(ВладелецФормы.Значение) Тогда
		ВыбФорма.ПараметрТекущаяСтрока = ВладелецФормы.Значение;
	КонецЕсли; 
	
	ВыбФорма.Открыть();
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА АНКЕТИРОВАНИЕ
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Функция возвращает выборку по вопросам анкеты
//
Функция увкВыполнитьЗапросПоАнкете(Анкета)
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("Ссылка",	Анкета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТиповыеАнкетыВопросыАнкеты.НомерСтроки КАК НомерСтроки,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос КАК Вопрос,
	|	ТиповыеАнкетыВопросыАнкеты.Раздел КАК Раздел,
	|	ТиповыеАнкетыВопросыАнкеты.Раздел.Код КАК КодРаздела,
	|	"". "" + ТиповыеАнкетыВопросыАнкеты.Раздел.Наименование КАК РазделНаименование,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы КАК КолонкаТаблицы,
	|	ВариантыОтветовОпросов.Наименование КАК ВариантыОтветовНаименование,
	|	ВариантыОтветовОпросовТабличные.Наименование КАК КолонкаТаблицыВариантыОтветовНаименование,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Наименование КАК Наименование,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ТипОтветаНаВопрос КАК ТипОтветаНаВопрос,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Длина КАК Длина,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Код КАК Код,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Родитель КАК Родитель,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ПолнаяФормулировка КАК ПолнаяФормулировка,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ТипЗначения КАК ТипЗначения,
	|	ВЫБОР
	|		КОГДА ТиповыеАнкетыВопросыАнкеты.Обязательный В (ЗНАЧЕНИЕ(Перечисление.ОбязательностьЗаполненияОтветаНаВопрос.НеОбязателенКЗаполнению), ЗНАЧЕНИЕ(Перечисление.ОбязательностьЗаполненияОтветаНаВопрос.ПустаяСсылка))
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОбязателенДляЗаполнения,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.КоличествоСтрокТаблицы КАК СтрокВТаблице,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Предопределенный КАК Предопределенный,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ВидКонтактнойИнформации КАК ВидКонтактнойИнформации,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ВидКонтактнойИнформации.Тип КАК ТипКонтактнойИнформации,
	//|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ВидКонтактнойИнформации.ВидОбъектаКонтактнойИнформации КАК ВидОбъектаКонтактнойИнформации,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ТипЗначения,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ТипОтветаНаВопрос,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Длина,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ПолнаяФормулировка,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Код КАК КолонкаТаблицыКод,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Родитель,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Наименование,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.КоличествоСтрокТаблицы,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Предопределенный,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ВидКонтактнойИнформации,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ВидКонтактнойИнформации.Тип КАК КолонкаТаблицыТипКонтактнойИнформации,
	//|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ВидКонтактнойИнформации.ВидОбъектаКонтактнойИнформации КАК КолонкаТаблицыВидОбъектаКонтактнойИнформации,
	|	ВопросыДляАнкетированияКолонкиТаблицы.НомерСтроки КАК КолонкаТаблицыНомерСтроки,
	|	ВариантыОтветовОпросовТабличные.Код КАК КолонкаТаблицыВариантыОтветовКод,
	|	ВариантыОтветовОпросовТабличные.Ссылка КАК КолонкаТаблицыВариантыОтветовСсылка,
	|	ВариантыОтветовОпросовТабличные.ТребуетРазвернутыйОтвет КАК КолонкаТаблицыВариантыОтветовТребуетРазвернутыйОтвет,
	|	ЛОЖЬ КАК КолонкаТаблицыОбязателенДляЗаполнения,
	|	ВариантыОтветовОпросов.Код КАК ВариантыОтветовКод,
	|	ВариантыОтветовОпросов.Ссылка КАК ВариантыОтветовСсылка,
	|	ВариантыОтветовОпросов.ТребуетРазвернутыйОтвет КАК ВариантыОтветовТребуетРазвернутыйОтвет
	|ИЗ
	|	Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВопросыДляАнкетирования.КолонкиТаблицы КАК ВопросыДляАнкетированияКолонкиТаблицы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросовТабличные
	|			ПО ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы = ВариантыОтветовОпросовТабличные.Владелец
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ВопросыДляАнкетированияКолонкиТаблицы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросов
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ВариантыОтветовОпросов.Владелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВопросыДляАнкетирования КАК ВопросыДляАнкетирования
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ВопросыДляАнкетирования.Ссылка
	|ГДЕ
	|	ТиповыеАнкетыВопросыАнкеты.Ссылка = &Ссылка
	|	И ТиповыеАнкетыВопросыАнкеты.Вопрос <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодРаздела,
	|	НомерСтроки,
	|	ВариантыОтветовКод,
	|	КолонкаТаблицыНомерСтроки,
	|	КолонкаТаблицыВариантыОтветовКод
	|ИТОГИ
	|	МАКСИМУМ(НомерСтроки),
	|	МАКСИМУМ(КодРаздела),
	|	МАКСИМУМ(ОбязателенДляЗаполнения),
	|	МАКСИМУМ(КолонкаТаблицыНомерСтроки),
	|	МАКСИМУМ(КолонкаТаблицыВариантыОтветовКод),
	|	МАКСИМУМ(ВариантыОтветовКод)
	|ПО
	|	Раздел,
	|	Вопрос,
	|	КолонкаТаблицы";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // ВыполнитьЗапросПоАнкете()

Функция увкВыполнитьЗапросПоОпросу(ДокументОпрос)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Опрос",	ДокументОпрос.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОпросВопросы.Вопрос КАК Вопрос,
	|	ОпросВопросы.Ответ,
	|	ОпросВопросы.ТиповойОтвет КАК ТиповойОтвет,
	|	ОпросВопросы.Вопрос КАК КолонкаТаблицы,
	|	NULL КАК НомерСтрокиВТаблице,
	|	ОпросВопросы.Вопрос КАК ВопросКоличество
	|ИЗ
	|	Документ.Опрос.Вопросы КАК ОпросВопросы
	|ГДЕ
	|	ОпросВопросы.Ссылка.Ссылка = &Опрос
	|	И (НЕ ОпросВопросы.Вопрос.ТипОтветаНаВопрос В (ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.Табличный), ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОпросСоставнойОтвет.ВопросВладелец,
	|	ОпросСоставнойОтвет.Ответ,
	|	ОпросСоставнойОтвет.ТиповойОтвет,
	|	ОпросСоставнойОтвет.Вопрос,
	|	ОпросСоставнойОтвет.НомерСтрокиВТаблице,
	|	ОпросСоставнойОтвет.ВопросВладелец
	|ИЗ
	|	Документ.Опрос.СоставнойОтвет КАК ОпросСоставнойОтвет
	|ГДЕ
	|	ОпросСоставнойОтвет.Ссылка.Ссылка = &Опрос
	|ИТОГИ
	|	КОЛИЧЕСТВО(ВопросКоличество)
	|ПО
	|	Вопрос";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция увкВыполнитьЗапросПоДанным(ОпрашиваемоеЛицо, ТиповаяАнкета)
	
	Если ТипЗнч(ОпрашиваемоеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
	
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Ссылка",	ОпрашиваемоеЛицо);
		Запрос.УстановитьПараметр("Анкета",	ТиповаяАнкета);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВопросыДляАнкетирования.Ссылка КАК Вопрос,
		|	NULL КАК КолонкаТаблицы,
		|	NULL КАК НомерСтрокиВТаблице,
		|	"""" КАК Ответ,
		|	ВЫБОР
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Фамилия)
		|			ТОГДА ФИОФизЛицСрезПоследних.Фамилия
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Имя)
		|			ТОГДА ФИОФизЛицСрезПоследних.Имя
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Отчество)
		|			ТОГДА ФИОФизЛицСрезПоследних.Отчество
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ДатаРождения)
		|			ТОГДА ФизическиеЛица.ДатаРождения		
		|	КОНЕЦ КАК ТиповойОтвет
		|ИЗ
		|	ПланВидовХарактеристик.ВопросыДляАнкетирования КАК ВопросыДляАнкетирования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &Ссылка) КАК ФИОФизЛицСрезПоследних
		|			ПО ФИОФизЛицСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка
		|		ПО (ФизическиеЛица.Ссылка = &Ссылка)
		|			И ((НЕ ВопросыДляАнкетирования.ТипОтветаНаВопрос В (ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.Табличный), ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация))))
		|			И ((НЕ ВопросыДляАнкетирования.ЭтоГруппа))
		|			И (ВопросыДляАнкетирования.Ссылка В
		|				(ВЫБРАТЬ
		|					ТиповыеАнкетыВопросыАнкеты.Вопрос
		|				ИЗ
		|					Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
		|				ГДЕ
		|					ТиповыеАнкетыВопросыАнкеты.Ссылка = &Анкета))
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВопросыДляАнкетирования.Ссылка,
		|	NULL,
		|	NULL,
		|	"""",
		|	ВЫБОР
		|		КОГДА КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
		|			ТОГДА КонтактнаяИнформация.Поле1 + ""¤"" + КонтактнаяИнформация.Поле2 + ""¤"" + КонтактнаяИнформация.Поле3 + ""¤"" + КонтактнаяИнформация.Поле4
		|		КОГДА КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
		|			ТОГДА КонтактнаяИнформация.Поле1 + ""¤"" + КонтактнаяИнформация.Поле2 + ""¤"" + КонтактнаяИнформация.Поле3 + ""¤"" + КонтактнаяИнформация.Поле4 + ""¤"" + КонтактнаяИнформация.Поле5 + ""¤"" + КонтактнаяИнформация.Поле6 + ""¤"" + КонтактнаяИнформация.Поле7 + ""¤"" + КонтактнаяИнформация.Поле8 + ""¤"" + КонтактнаяИнформация.Поле9
		|	КОНЕЦ
		|ИЗ
		|	ПланВидовХарактеристик.ВопросыДляАнкетирования КАК ВопросыДляАнкетирования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|		ПО ВопросыДляАнкетирования.ВидКонтактнойИнформации = КонтактнаяИнформация.Вид
		|			И ((НЕ ВопросыДляАнкетирования.ЭтоГруппа))
		|			И (ВопросыДляАнкетирования.Ссылка В
		|				(ВЫБРАТЬ
		|					ТиповыеАнкетыВопросыАнкеты.Вопрос
		|				ИЗ
		|					Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
		|				ГДЕ
		|					ТиповыеАнкетыВопросыАнкеты.Ссылка = &Анкета))
		|			И (КонтактнаяИнформация.Объект = &Ссылка)
		|			И ВопросыДляАнкетирования.ВидКонтактнойИнформации.Тип = КонтактнаяИнформация.Тип
		|
		|УПОРЯДОЧИТЬ ПО
		|	Вопрос,
		|	КолонкаТаблицы,
		|	НомерСтрокиВТаблице";
		
		Возврат Запрос.Выполнить().Выбрать();
	
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА УДАЛЕННОЕ АНКЕТИРОВАНИЕ
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Функция формирует XSL файл
//
Функция увкСформироватьXSL(ИмяФайлаАнкеты, КаталогАнкеты, КаталогОпроса, ДобавлятьКнопкуЗаписи, ДобавлятьКнопкуОтправки, ЭлектронныйАдресОтветов) Экспорт
	
	ИмяФайлаПреобразования = "style.xsl";
	
	XSLМакет = ПолучитьОбщийМакет("МакетТиповойАнкеты");
	
	Если Не ПустаяСтрока(КаталогАнкеты) тогда
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("КнопкаОтправить2");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		СтрокаПреобразования = СтрЗаменить(СтрокаПреобразования, "//comment11;", "LoadPath = '"+СтрЗаменить(КаталогАнкеты+ИмяФайлаАнкеты, "\", "\\")+"';");
		СтрокаПреобразования = СтрЗаменить(СтрокаПреобразования, "//comment22;", "FName	= '" + СтрЗаменить(КаталогОпроса, "\", "\\")+"opros.xml';");
		
	ИначеЕсли Не ДобавлятьКнопкуОтправки И ДобавлятьКнопкуЗаписи тогда
		// когда открываем анкету просто для просмотра то она уже точно не адресная
		// в нее помещаем только кнопку "Сохранить в файл"
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("КнопкаСохранить");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		
	ИначеЕсли ДобавлятьКнопкуОтправки И ДобавлятьКнопкуЗаписи тогда
		// когда открываем адресную анкету просто для просмотра 
		// в нее помещаем обе кнопки
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("Кнопки");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		
	ИначеЕсли Не ДобавлятьКнопкуЗаписи тогда
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("КнопкаСохранить");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		
	КонецЕсли;
	СтрокаПреобразования = СтрЗаменить(СтрокаПреобразования, "###@###", ЭлектронныйАдресОтветов);
	ТекстXSL.УстановитьТекст(СтрокаПреобразования);
	
	Возврат ТекстXSL;
	
КонецФункции

Процедура увкДобавитьПоле(ОбъектXML, Имя, Значение)
	
	ОбъектXML.ЗаписатьАтрибут(Имя, XMLСтрока(Значение));
	
КонецПроцедуры // ДобавитьПоле()

Функция увкПолучитьОтветы(Выборка, Тип, ФлагКолонки = Ложь)
	
	Если НЕ ФлагКолонки Тогда
		Вопрос	= Выборка.Вопрос;
		Префикс	= "";
		
	Иначе
		Вопрос	= Выборка.КолонкаТаблицы;
		Префикс	= "КолонкаТаблицы";
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Вопрос", Вопрос);
	
	ТипЗначения = Выборка[Префикс + "ТипЗначения"].Типы().Получить(0);
	
	Если (ТипЗначения = Тип("Булево")) Тогда
		
		ВыборкаЭлементов = Новый ТаблицаЗначений();
		ВыборкаЭлементов.Колонки.Добавить("Код");
		ВыборкаЭлементов.Колонки.Добавить("Ссылка");
		ВыборкаЭлементов.Колонки.Добавить("Наименование");
		ВыборкаЭлементов.Колонки.Добавить("Комментарий");
		ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
		ВыборкаЭлементов.Колонки.Добавить("ГУИД");
		
		СтрокаТаблицы = ВыборкаЭлементов.Добавить();
		СтрокаТаблицы.Код						= ЗначениеВСтрокуВнутр(Ложь);
		СтрокаТаблицы.Ссылка					= Ложь;
		СтрокаТаблицы.Наименование				= Строка("Нет");
		СтрокаТаблицы.Комментарий				= "";
		СтрокаТаблицы.ТребуетРазвернутыйОтвет	= Ложь;
		СтрокаТаблицы.ГУИД = СтрокаТаблицы.Код;
		
		СтрокаТаблицы = ВыборкаЭлементов.Добавить();
		СтрокаТаблицы.Код						= ЗначениеВСтрокуВнутр(Истина);
		СтрокаТаблицы.Ссылка					= Истина;
		СтрокаТаблицы.Наименование				= Строка("Да");
		СтрокаТаблицы.Комментарий				= "";
		СтрокаТаблицы.ТребуетРазвернутыйОтвет	= Ложь;
		СтрокаТаблицы.ГУИД = СтрокаТаблицы.Код;
		
	ИначеЕсли (ТипЗначения = Тип("Строка")) ИЛИ
		(ТипЗначения = Тип("Число")) ИЛИ
		(ТипЗначения = Тип("Дата")) Тогда
		
		ВыборкаЭлементов = Новый ТаблицаЗначений();
		ВыборкаЭлементов.Колонки.Добавить("Код");
		ВыборкаЭлементов.Колонки.Добавить("Ссылка");
		ВыборкаЭлементов.Колонки.Добавить("Наименование");
		ВыборкаЭлементов.Колонки.Добавить("Комментарий");
		ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
		
	ИначеЕсли (ТипЗначения = Тип("СправочникСсылка.ВариантыОтветовОпросов")) Тогда
		
		ВыборкаЭлементов = Новый ТаблицаЗначений;
		ВыборкаЭлементов.Колонки.Добавить("Код");
		ВыборкаЭлементов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов"));
		ВыборкаЭлементов.Колонки.Добавить("Наименование");
		ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
		
		ВариантыОтветов = Выборка.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока ВариантыОтветов.Следующий() Цикл
			НоваяСтрока = ВыборкаЭлементов.Добавить();
			НоваяСтрока.Код						= ВариантыОтветов[Префикс + "ВариантыОтветовКод"];
			НоваяСтрока.Ссылка					= ВариантыОтветов[Префикс + "ВариантыОтветовСсылка"];
			НоваяСтрока.Наименование			= ВариантыОтветов[Префикс + "ВариантыОтветовНаименование"];
			НоваяСтрока.ТребуетРазвернутыйОтвет	= ВариантыОтветов[Префикс + "ВариантыОтветовТребуетРазвернутыйОтвет"];
		КонецЦикла;
		
		Если ВыборкаЭлементов.Количество() > 0 И НЕ ВыборкаЭлементов[0].Ссылка.Пустая() Тогда
			ПустаяСтрокаТаблицы			= ВыборкаЭлементов.Вставить(0);
			ПустаяСтрокаТаблицы.Ссылка	= Справочники.ВариантыОтветовОпросов.ПустаяСсылка();
		КонецЕсли;
		
		ВыборкаЭлементов.Колонки.Добавить("ГУИД");
		Для Каждого СтрокаТаблицы Из ВыборкаЭлементов Цикл
			СтрокаТаблицы.ГУИД = СтрокаТаблицы.Ссылка.УникальныйИдентификатор();
		КонецЦикла;
		
	Иначе
		Объект = Метаданные.НайтиПоТипу(ТипЗначения);
		
		Попытка  // Объект - перечисление
			Перечисление = Истина;
			Значения = Объект.ЗначенияПеречисления;
		Исключение // объект - справочник
			Перечисление = Ложь;
		КонецПопытки;
		
		Если Перечисление Тогда
			
			ВыборкаЭлементов = Новый ТаблицаЗначений();
			ВыборкаЭлементов.Колонки.Добавить("Код");
			ВыборкаЭлементов.Колонки.Добавить("Ссылка");
			ВыборкаЭлементов.Колонки.Добавить("Наименование");
			ВыборкаЭлементов.Колонки.Добавить("Комментарий");
			ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
			
			Менеджер = Перечисления[Объект.Имя];
			Для Индекс = 0 По Менеджер.Количество() - 1 Цикл
				СтрокаТаблицы = ВыборкаЭлементов.Добавить();
				Значение = Менеджер.Получить(Индекс);
				СтрокаТаблицы.Код						= ЗначениеВСтрокуВнутр(Значение);
				СтрокаТаблицы.Ссылка					= Значение;
				СтрокаТаблицы.Наименование				= Строка(Значение);
				СтрокаТаблицы.Комментарий				= Значение.Метаданные().Комментарий;
				СтрокаТаблицы.ТребуетРазвернутыйОтвет	= Ложь;
			КонецЦикла;
			ПустаяСтрокаТаблицы = ВыборкаЭлементов.Вставить(0);
			ПустаяСтрокаТаблицы.Ссылка = Менеджер.ПустаяСсылка();
			
		Иначе
			Запрос.Текст =
			"ВЫБРАТЬ
			|	"+Объект.Имя+".Ссылка       КАК Ссылка,
			|	"+Объект.Имя+".Наименование КАК Наименование,
			|	Ложь КАК ТребуетРазвернутыйОтвет
			|ИЗ
			|	Справочник."+Объект.Имя+" КАК "+Объект.Имя+"
			|";
			
			ВыборкаЭлементов =  Запрос.Выполнить().Выгрузить();
			ВыборкаЭлементов.Колонки.Добавить("Код");
			ВыборкаЭлементов.ЗаполнитьЗначения(NULL, "Код");
			
			Если ВыборкаЭлементов.Количество() > 0 тогда
				ПустойЭлемент = ВыборкаЭлементов.Вставить(0);
				ПустойЭлемент.Ссылка = Справочники[Объект.Имя].ПустаяСсылка();
				
			Иначе
				Сообщить("Справочник """+Объект.Синоним+""" не имеет ни одного элемента. Рекомендуется заполнить его и сформировать анкету еще раз.");
				
			КонецЕсли;
			
		КонецЕсли;
		
		ВыборкаЭлементов.Колонки.Добавить("ГУИД");
		Для Каждого СтрокаТаблицы Из ВыборкаЭлементов Цикл
			Если СтрокаТаблицы.Код = NULL Тогда
				СтрокаТаблицы.ГУИД = СтрокаТаблицы.Ссылка.УникальныйИдентификатор();
				
			Иначе
				СтрокаТаблицы.ГУИД = СтрокаТаблицы.Код;
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ВыборкаЭлементов;
	
КонецФункции // ПолучитьОтветы()

Функция увкПолучитьТипЗначенияОтвета(Выборка, ФлагКолонки)
	
	Если НЕ ФлагКолонки тогда
		Вопрос	= Выборка.Вопрос;
		Префикс	= "";
		
	Иначе
		Вопрос	= Выборка.КолонкаТаблицы;
		Префикс	= "КолонкаТаблицы";
		
	КонецЕсли;
	
	Параметры = Новый Структура("Представление, Тип, Длина, СпособОтвета, СтрокВТаблице, Ответы");
	
	ТипЗначения = Выборка[Префикс + "ТипЗначения"];
	ДлинаТипа   = Выборка[Префикс + "Длина"];
	
	Если (ТипЗначения.СодержитТип(Тип("Булево"))) Тогда
		Параметры.Тип = Строка(ТипЗначения);
		
	ИначеЕсли Выборка[Префикс + "ТипОтветаНаВопрос"] = Перечисления.ТипыОтветаНаВопросАнкеты.Текст тогда
		Параметры.Тип 			= "Строка";
		Параметры.Длина = Строка(ДлинаТипа);
		
	ИначеЕсли (ТипЗначения.СодержитТип(Тип("Строка"))) ИЛИ
		(ТипЗначения.СодержитТип(Тип("Число"))) ИЛИ
		(ТипЗначения.СодержитТип(Тип("Дата"))) Тогда
		
		Параметры.Тип = Строка(ТипЗначения);
		Параметры.Длина = Строка(ДлинаТипа);
		
	Иначе
		Тип = "Строка";
		
	КонецЕсли;
	
	Ответы = увкПолучитьОтветы(Выборка, Тип, ФлагКолонки);
	
	ЕстьОтветы = Ответы.Количество() > 0;
	
	Если Выборка[Префикс + "ТипОтветаНаВопрос"]  = Перечисления.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация Тогда
		СтрокаТипКИ = увкПолучитьИмяЭлементаПеречисленияПоЗначению(Выборка[Префикс + "ТипКонтактнойИнформации"]);
		//СтрокаВидКИ = увкПолучитьИмяЭлементаПеречисленияПоЗначению(Выборка[Префикс + "ВидОбъектаКонтактнойИнформации"]);
		
		//Параметры.Тип			= ""+СтрокаТипКИ+"¤"+СтрокаВидКИ;
		Параметры.Тип			= ""+СтрокаТипКИ;
		Параметры.СтрокВТаблице	= 0; // вопрос не табличный, значит строк - 0!
		Параметры.Представление	= "ПолеВвода";
		Параметры.СпособОтвета	= "ВСвободнойФорме";
		
	ИначеЕсли Выборка[Префикс + "ТипОтветаНаВопрос"] = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный Тогда
		Параметры.Тип			= "Таблица";
		Параметры.СтрокВТаблице	= Выборка.СтрокВТаблице; // вложенные табличные вопросы быть не могут, поэтому можно обойтись без префикса
		Параметры.Представление	= "Таблица";
		Параметры.СпособОтвета	= "";
		
	Иначе
		Параметры.СтрокВТаблице = 0;
		
		Если ЕстьОтветы Тогда
			
			Если Выборка[Префикс + "ТипОтветаНаВопрос"] <> Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета Тогда
				Если Ответы.Количество() > 5 Тогда
					Параметры.Тип				= Тип;
					Если Ответы.Найти(Истина, "ТребуетРазвернутыйОтвет") = Неопределено Тогда
						Параметры.Представление	= "СписокВыбора";
						
					Иначе
						// если есть хоть один предопределенный	ответ
						Параметры.Представление	= "Переключатель";
						
					КонецЕсли;
					Параметры.СпособОтвета		= "ВыборОдногоВарианта";
					
				Иначе
					Параметры.Тип				= Тип;
					Если ФлагКолонки Тогда
						Параметры.Представление	= "СписокВыбора";
						
					Иначе
						Параметры.Представление	= "Переключатель";
						
					КонецЕсли;
					Параметры.СпособОтвета		= "ВыборОдногоВарианта";
				КонецЕсли;
			Иначе
				Параметры.Тип			= Тип;
				Параметры.Представление	= "Флажок";
				Параметры.СпособОтвета	= "ВыборНесколькихВариантов";
				
			КонецЕсли;
			
		Иначе
			
			Если (ТипЗначения.СодержитТип(Тип("Булево"))) Тогда
				Параметры.Тип				= Строка(Выборка[Префикс + "ТипЗначения"]);
				Параметры.Представление		= "СписокВыбора";
				Параметры.СпособОтвета		= "ВыборОдногоВарианта";
				
			ИначеЕсли (ТипЗначения.СодержитТип(Тип("Строка"))) ИЛИ
				(ТипЗначения.СодержитТип(Тип("Число"))) ИЛИ
				(ТипЗначения.СодержитТип(Тип("Дата"))) Тогда
				
				Параметры.Тип				= Строка(Выборка[Префикс + "ТипЗначения"]);
				
				Если Выборка[Префикс + "ТипОтветаНаВопрос"] = Перечисления.ТипыОтветаНаВопросАнкеты.Текст тогда
					// не все строковые поля одинаковые. Бывает еще и текст
					Параметры.Представление	= "Текст";
					
				Иначе
					Параметры.Представление	= "ПолеВвода";
					
				КонецЕсли;
				
				Параметры.СпособОтвета		= "ВСвободнойФорме";
				
			Иначе
				Параметры.Тип				= "Строка";
				Параметры.Представление		= "ПолеВвода";
				Параметры.СпособОтвета		= "ВСвободнойФорме";
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Параметры.Ответы = Ответы;
	
	Возврат Параметры;
	
КонецФункции // ПолучитьТипЗначенияОтвета()

Процедура увкЗаписатьВопрос(ОбъектXML, Выборка, ФлагКолонки)
	
	Если НЕ ФлагКолонки тогда
		Вопрос	= Выборка.Вопрос;
		Префикс	= "";
		
	Иначе
		Вопрос	= Выборка.КолонкаТаблицы;
		Префикс	= "КолонкаТаблицы";
		
	КонецЕсли;
	
	Параметры	= увкПолучитьТипЗначенияОтвета(Выборка, ФлагКолонки);
	
	Если Выборка.ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный и НЕ ФлагКолонки тогда
		ТабличныйВопрос = Выборка.Вопрос;
		// значит имеем дело с табличным вопросом
		Параметры = увкПолучитьТипЗначенияОтвета(Выборка, Ложь);
		
		ОбъектXML.ЗаписатьНачалоЭлемента("Вопрос");
			
			увкДобавитьПоле(ОбъектXML,"Ид",						ТабличныйВопрос.УникальныйИдентификатор());
			увкДобавитьПоле(ОбъектXML,"Наименование",				?(ПустаяСтрока(Выборка.ПолнаяФормулировка), Выборка.Наименование, Выборка.ПолнаяФормулировка));
			увкДобавитьПоле(ОбъектXML,"Комментарий",				Выборка.Наименование);
			увкДобавитьПоле(ОбъектXML,"Обязательный",				Выборка.ОбязателенДляЗаполнения);
			увкДобавитьПоле(ОбъектXML,"СпособОтвета",				Параметры.СпособОтвета);
			увкДобавитьПоле(ОбъектXML,"ТипЗначенийОтвета",			Параметры.Тип);
			увкДобавитьПоле(ОбъектXML,"Представление",				Параметры.Представление);
			увкДобавитьПоле(ОбъектXML,"КоличествоСтрокТаблицы",	Параметры.СтрокВТаблице);
			
			КолонкиТаблицы = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока КолонкиТаблицы.Следующий() Цикл
				увкЗаписатьВопрос(ОбъектXML, КолонкиТаблицы, Истина);
			КонецЦикла;
			
		ОбъектXML.ЗаписатьКонецЭлемента(); // Вопрос
		
	Иначе
		Если НЕ ФлагКолонки тогда
			ОбъектXML.ЗаписатьНачалоЭлемента("Вопрос");
		Иначе
			ОбъектXML.ЗаписатьНачалоЭлемента("КолонкаТаблицы");
		КонецЕсли;
		
			увкДобавитьПоле(ОбъектXML,"Ид",						Вопрос.УникальныйИдентификатор());
			увкДобавитьПоле(ОбъектXML,"Наименование",				?(ПустаяСтрока(Выборка[Префикс + "ПолнаяФормулировка"]), Выборка[Префикс + "Наименование"], Выборка[Префикс + "ПолнаяФормулировка"]));
			увкДобавитьПоле(ОбъектXML,"Комментарий",				Выборка[Префикс + "Наименование"]);
			увкДобавитьПоле(ОбъектXML,"Обязательный",				Выборка[Префикс + "ОбязателенДляЗаполнения"]);
			увкДобавитьПоле(ОбъектXML,"СпособОтвета",				Параметры.СпособОтвета);
			увкДобавитьПоле(ОбъектXML,"ТипЗначенийОтвета",			Параметры.Тип);
			увкДобавитьПоле(ОбъектXML,"Длина",						?(Параметры.Длина = "0", "60", Параметры.Длина));
			увкДобавитьПоле(ОбъектXML,"Представление",				Параметры.Представление);
			увкДобавитьПоле(ОбъектXML,"КоличествоСтрокТаблицы",	Параметры.СтрокВТаблице);
			Ответы = Параметры.Ответы;
			
			Для Каждого СтрокаТаблицы Из Ответы Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Ссылка) и Параметры.Представление <> "СписокВыбора" тогда
					// пустые ссылки будем записывать только выпадающие списки
					Продолжить;
				КонецЕсли;
				
				ОбъектXML.ЗаписатьНачалоЭлемента("ВариантОтвета");
				
					увкДобавитьПоле(ОбъектXML,"Ид",						СтрокаТаблицы.ГУИД);
					увкДобавитьПоле(ОбъектXML,"Наименование",				СтрокаТаблицы.Наименование);
					увкДобавитьПоле(ОбъектXML,"Комментарий",				"");
					увкДобавитьПоле(ОбъектXML,"ТребуетРазвернутыйОтвет",	СтрокаТаблицы.ТребуетРазвернутыйОтвет);
				
				ОбъектXML.ЗаписатьКонецЭлемента(); // ВариантОтвета
			КонецЦикла;
		
		ОбъектXML.ЗаписатьКонецЭлемента(); // Вопрос ИЛИ КолонкаТаблицы
		
	КонецЕсли;
	
КонецПроцедуры // ЗаписатьВопрос()

Процедура увкЗаписатьРаздел(ОбъектXML, РазделДляЗаписи, РазделНаименование)
	
	ОбъектXML.ЗаписатьНачалоЭлемента("Группа");
	Если РазделДляЗаписи = NULL тогда
		увкДобавитьПоле(ОбъектXML,"Ид",			"");
		увкДобавитьПоле(ОбъектXML,"Комментарий",	"");
		
	Иначе
		увкДобавитьПоле(ОбъектXML,"Ид",			РазделДляЗаписи.Ссылка.УникальныйИдентификатор());
		увкДобавитьПоле(ОбъектXML,"Комментарий",	РазделДляЗаписи.Комментарий);
		
	КонецЕсли;
	увкДобавитьПоле(ОбъектXML,"Наименование", РазделНаименование);
	
КонецПроцедуры // ЗаписатьРаздел()

Процедура увкДобавитьОтвет(ОбъектXML, Вопрос, ТипОтветаНаВопрос, ВыборкаОтветовИзОпроса, ТабличныйВопрос = Неопределено, НомерСтрокиВТаблице = Неопределено)
	
	Если ТабличныйВопрос = Неопределено Тогда
		ВопросПоиска = Новый Структура("Вопрос");
		ВопросПоиска.Вопрос				= Вопрос;				// выберем ответы по указанному вопросу
		
	Иначе
		ВопросПоиска = Новый Структура("Вопрос, КолонкаТаблицы, НомерСтрокиВТаблице");
		ВопросПоиска.Вопрос				= ТабличныйВопрос;		// выберем ответы по указанному табличному вопросу
		ВопросПоиска.КолонкаТаблицы		= Вопрос;				// выберем ответы по указанной колонке
		ВопросПоиска.НомерСтрокиВТаблице= НомерСтрокиВТаблице;	// НомерСтрокиВТаблице
		
	КонецЕсли;
	
	Если ВыборкаОтветовИзОпроса <> Неопределено Тогда
		ВыборкаОтветовИзОпроса.Сбросить();
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента("Вопрос");
	
		увкДобавитьПоле(ОбъектXML,"Ид", Вопрос.УникальныйИдентификатор());
		Если (Вопрос.Предопределенный И 
			(Вопрос.ТипЗначения.СодержитТип(Тип("Строка")) ИЛИ
			Вопрос.ТипЗначения.СодержитТип(Тип("Дата")) ИЛИ 
			Вопрос.ТипЗначения.СодержитТип(Тип("Число")) ) ) ИЛИ
			(НЕ Вопрос.Предопределенный И
			(ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Строка ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Число ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Дата ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Текст)) Тогда
			
			ОбъектXML.ЗаписатьНачалоЭлемента("Значение");
			
				Если ВыборкаОтветовИзОпроса <> Неопределено Тогда
					Пока ВыборкаОтветовИзОпроса.НайтиСледующий(ВопросПоиска) Цикл
						Если Вопрос.ТипЗначения.СодержитТип(Тип("Дата")) ИЛИ
							Вопрос.ТипЗначения.СодержитТип(Тип("Число")) Тогда
							Если ЗначениеЗаполнено(ВыборкаОтветовИзОпроса.ТиповойОтвет) Тогда
								ОбъектXML.ЗаписатьТекст(XMLСтрока(ВыборкаОтветовИзОпроса.ТиповойОтвет));
								
							Иначе
								ОбъектXML.ЗаписатьТекст(" ");
								
							КонецЕсли;
							
						Иначе
							ОбъектXML.ЗаписатьТекст(Строка(ВыборкаОтветовИзОпроса.ТиповойОтвет));
							
						КонецЕсли;
					КонецЦикла;
					
				Иначе
					ОбъектXML.ЗаписатьТекст(" ");
					
				КонецЕсли;
			
			ОбъектXML.ЗаписатьКонецЭлемента(); // Значение
			
		ИначеЕсли ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.ОдинИзВариантовОтвета ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Булево ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета ИЛИ
			Вопрос.Предопределенный Тогда
			
			Если ВыборкаОтветовИзОпроса <> Неопределено Тогда
				
				Пока ВыборкаОтветовИзОпроса.НайтиСледующий(ВопросПоиска) Цикл
					ОбъектXML.ЗаписатьНачалоЭлемента("ВариантОтвета");
					
						Если ПустаяСтрока(ВыборкаОтветовИзОпроса.ТиповойОтвет) тогда
							увкДобавитьПоле(ОбъектXML,"Ид", "00000000-0000-0000-0000-000000000000");
							
						Иначе
							Попытка
								увкДобавитьПоле(ОбъектXML,"Ид", ВыборкаОтветовИзОпроса.ТиповойОтвет.УникальныйИдентификатор());
									
							Исключение
								увкДобавитьПоле(ОбъектXML,"Ид", ЗначениеВСтрокуВнутр(ВыборкаОтветовИзОпроса.ТиповойОтвет));
									
							КонецПопытки
							
						КонецЕсли;
						
						Если ПустаяСтрока(ВыборкаОтветовИзОпроса.Ответ) Тогда
							увкДобавитьПоле(ОбъектXML,"РазвернутыйОтвет", " ");
							
						Иначе
							увкДобавитьПоле(ОбъектXML,"РазвернутыйОтвет", ВыборкаОтветовИзОпроса.Ответ);
							
						КонецЕсли;
					
					ОбъектXML.ЗаписатьКонецЭлемента(); // ВариантОтвета
				КонецЦикла;
				
			Иначе
				ОбъектXML.ЗаписатьНачалоЭлемента("ВариантОтвета");
				
					увкДобавитьПоле(ОбъектXML,"Ид",				"00000000-0000-0000-0000-000000000000");
					увкДобавитьПоле(ОбъектXML,"РазвернутыйОтвет",	" ");
				
				ОбъектXML.ЗаписатьКонецЭлемента(); // ВариантОтвета
				
			КонецЕсли;
			
		КонецЕсли;
	
	ОбъектXML.ЗаписатьКонецЭлемента(); // Вопрос
	
КонецПроцедуры // ДобавитьОтвет

Процедура увкОбработкаЗапроса(Выборка, ОбъектXML);
	
	Разделы = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Разделы.Следующий() Цикл
		Вопросы = Разделы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если Разделы.КодРаздела = NULL Тогда
			РазделНаименование	= "";
			
		Иначе
			КодРаздела			= Строка(Число(Разделы.КодРаздела));
			РазделНаименование	= КодРаздела + Разделы.РазделНаименование;
			
		КонецЕсли;
		
		увкЗаписатьРаздел(ОбъектXML, Разделы.Раздел, РазделНаименование);
		
		// теперь обходим вопросы
		Пока Вопросы.Следующий() Цикл
			увкЗаписатьВопрос(ОбъектXML, Вопросы, Ложь);
		КонецЦикла;
		
		ОбъектXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	
КонецПроцедуры // ОбработкаЗапроса()

Функция увкВыгрузитьАнкетуВXML(Анкета, ИмяФайла = "", ОпрашиваемоеЛицо, ДокРассылки, ДокОпрос = Неопределено) Экспорт
	
	Если Анкета = Неопределено ИЛИ Анкета.Пустая() Тогда
		Возврат ""; // Нечего выгружать!
	КонецЕсли;
	
	Выборка		= увкВыполнитьЗапросПоАнкете(Анкета);
	XML			= Новый ЗаписьXML();
	XML.Отступ	= Истина;
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		XML.УстановитьСтроку("UTF-8"); //16
		
	Иначе
		XML.ОткрытьФайл(ИмяФайла, "UTF-8");
		
	КонецЕсли;
	
	XML.ЗаписатьОбъявлениеXML();
	
	XML.ЗаписатьИнструкциюОбработки("xml-stylesheet","type=""text/xsl"" href=""style.xsl""");
	
	XML.ЗаписатьНачалоЭлемента("Заголовок");
	
		увкДобавитьПоле(XML,"ДатаФормирования",	XMLСтрока(ТекущаяДата()));
		увкДобавитьПоле(XML,"ВерсияФормата",		"1.1");
		увкДобавитьПоле(XML,"ИдОпрашиваемого",		ЗначениеВСтрокуВнутр(ОпрашиваемоеЛицо));
		
		XML.ЗаписатьНачалоЭлемента("Анкета");
		
			увкДобавитьПоле(XML,"Ид",					Анкета.УникальныйИдентификатор());
			увкДобавитьПоле(XML,"Наименование",		Анкета.НаименованиеАнкеты);
			увкДобавитьПоле(XML,"Комментарий",			Анкета.Вступление);
			Если ДокРассылки <> Неопределено Тогда
				увкДобавитьПоле(XML,"ИдРассылки",		ДокРассылки.УникальныйИдентификатор());
			КонецЕсли;
			увкОбработкаЗапроса(Выборка, XML);
		
		XML.ЗаписатьКонецЭлемента(); // Анкета
		
		XML.ЗаписатьНачалоЭлемента("РезультатОпроса");
		
			увкДобавитьПоле(XML,"ДатаЗаполненияАнкеты",	XMLСтрока(?(ЗначениеЗаполнено(ДокОпрос), ДокОпрос.Дата, "")));
			увкДобавитьПоле(XML,"ИдАнкеты",				Анкета.УникальныйИдентификатор());
			увкДобавитьПоле(XML,"Наименование",			Анкета.НаименованиеАнкеты);
			Если ДокРассылки <> Неопределено Тогда
				увкДобавитьПоле(XML,"ИдРассылки",			ДокРассылки.УникальныйИдентификатор());
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДокОпрос) Тогда
				
				ВыборкаОтветовИзОпроса = увкВыполнитьЗапросПоОпросу(ДокОпрос).Выбрать();
				
			Иначе
				Если Анкета.ЗаполнятьАнкетуДанными Тогда
					ВыборкаОтветовИзОпроса = увкВыполнитьЗапросПоДанным(ОпрашиваемоеЛицо, Анкета);
					
				Иначе
					ВыборкаОтветовИзОпроса = Неопределено;
					
				КонецЕсли;
				
			КонецЕсли;
			
			ВыборкаВопросовДляОтветов = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Вопрос");
			Пока ВыборкаВопросовДляОтветов.Следующий() Цикл
				
				Если ВыборкаВопросовДляОтветов.ТипОтветаНаВопрос <> Перечисления.ТипыОтветаНаВопросАнкеты.Табличный Тогда
					увкДобавитьОтвет(XML, ВыборкаВопросовДляОтветов.Вопрос, ВыборкаВопросовДляОтветов.ТипОтветаНаВопрос, ВыборкаОтветовИзОпроса);
					
				Иначе
					XML.ЗаписатьНачалоЭлемента("Вопрос");
					
						увкДобавитьПоле(XML,"Ид", ВыборкаВопросовДляОтветов.Вопрос.УникальныйИдентификатор());
						Для НомерСтрокиТаблицы = 1 По ВыборкаВопросовДляОтветов.СтрокВТаблице Цикл
							
							XML.ЗаписатьНачалоЭлемента("СтрокаТаблицы");
							
								увкДобавитьПоле(XML,"НомерСтроки", НомерСтрокиТаблицы);
								ВыборкаКолонокДляОтветов = ВыборкаВопросовДляОтветов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КолонкаТаблицы");
								Пока ВыборкаКолонокДляОтветов.Следующий() Цикл
									увкДобавитьОтвет(XML, ВыборкаКолонокДляОтветов.КолонкаТаблицы, ВыборкаКолонокДляОтветов.КолонкаТаблицыТипОтветаНаВопрос, ВыборкаОтветовИзОпроса, ВыборкаВопросовДляОтветов.Вопрос, НомерСтрокиТаблицы);
								КонецЦикла;
							
							XML.ЗаписатьКонецЭлемента(); // СтрокаТаблицы
							
						КонецЦикла;
					
					XML.ЗаписатьКонецЭлемента(); // Вопрос
					
				КонецЕсли;
			КонецЦикла;
		
		XML.ЗаписатьКонецЭлемента(); // РезультатОпроса
	
	XML.ЗаписатьКонецЭлемента(); // Заголовок
	
	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		XML.Закрыть();
		
	Иначе
		Возврат (XML.Закрыть());
		
	КонецЕсли;
	
КонецФункции // ВыгрузитьАнкетуВXML()

Функция увкПолучитьИмяФайлаАнкеты(ТиповаяАнкета) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Анкета",	ТиповаяАнкета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТиповыеАнкеты.Код
	|ИЗ
	|	Справочник.ТиповыеАнкеты КАК ТиповыеАнкеты
	|ГДЕ
	|	ТиповыеАнкеты.Ссылка = &Анкета";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	//Возврат "anketa"+Число(Выборка.Код)+".xml";
	Возврат "anketa" + Выборка.Код + ".xml";
	
КонецФункции // ПолучитьИмяФайлаАнкеты()

Функция увкСформироватьВложение(ТиповаяАнкета, ОпрашиваемоеЛицо = Неопределено, ЭлектронныйАдресОтветов = "", ДокРассылки = Неопределено, ДокОпрос = Неопределено, ИмяФайла = "", ФормироватьXSL = Ложь, КаталогАнкеты = "", КаталогОпроса = "") Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Анкета",	ТиповаяАнкета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоВопросовВРазделе,
	|	ТиповыеАнкетыВопросыАнкеты.Раздел,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос
	|ИЗ
	|	Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
	|ГДЕ
	|	ТиповыеАнкетыВопросыАнкеты.Ссылка = &Анкета
	|
	|СГРУППИРОВАТЬ ПО
	|	ТиповыеАнкетыВопросыАнкеты.Раздел,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(*) > 1";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщить("Вопрос """+Выборка.Вопрос + """ встречается в разделе """ + Выборка.Раздел + """ более одного раза!");
	КонецЦикла;
	
	Если НЕ Выборка.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(ИмяФайла) тогда
		ИмяФайла = увкПолучитьИмяФайлаАнкеты(ТиповаяАнкета);
	КонецЕсли;
	
	ИмяФайлаПреобразования = "style.xsl";
	ФайлАнкеты			= Новый Файл(ИмяФайла);
	ТекстXSL			= Новый ТекстовыйДокумент;
	Преобразование		= Новый ПреобразованиеXSL;
	
	Если ФормироватьXSL Тогда
		ТекстXSL = увкСформироватьXSL(ИмяФайла, КаталогАнкеты, КаталогОпроса, ДокОпрос = Неопределено, ЭлектронныйАдресОтветов <> "e-mail ответов", ЭлектронныйАдресОтветов);
		ТекстXSL.Записать(КаталогВременныхФайлов()+ИмяФайлаПреобразования);
	КонецЕсли;
	
	увкВыгрузитьАнкетуВXML(ТиповаяАнкета, КаталогВременныхФайлов() + ИмяФайла, ОпрашиваемоеЛицо, ДокРассылки, ДокОпрос);
	
	Возврат Истина;
	
КонецФункции // СформироватьВложение()

#Если Клиент Тогда

Процедура увкОткрытьВложение(ТиповаяАнкета) Экспорт
	
	Если увкСформироватьВложение(ТиповаяАнкета, , "e-mail ответов", , , , Истина) тогда
		ИмяФайла = увкПолучитьИмяФайлаАнкеты(ТиповаяАнкета);
		ЗапуститьПриложение("explorer " + КаталогВременныхФайлов() + ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры // ОткрытьВложение()

// Процедура выполняет сворачивание/разворачивание элементов форм
//
// Параметры:
//  Видимость - Булево, признак "развернутости" элемент.
//  ГлавныйЭлемент - Элемент формы который сворачиваем.
//  Главный разделить - Разделитель.
//  Панель формы - панель или главная панель формы.
//  Направление - Строка, направление сворачивания.
//  Пропорционально - Булево - Истина, если разделитель привязывается пропорционально
//
Процедура увкУправлениеВидимостиЭлементов(Видимость, ГлавныйЭлемент, ГлавныйРазделитель, ПанельФормы, Направление, Пропорционально = Истина) Экспорт
	Если ВРег(Направление) =  "ЛЕВО" Тогда
		Если Видимость Тогда
			// откроем 
			ГлавныйРазделитель.Свертка=РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка=РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Лево);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Право,ГлавныйРазделитель,ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.Ширина = 4;
		Иначе	
			// скроем 
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйЭлемент,ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право);
			ГлавныйЭлемент.Свертка=РежимСверткиЭлементаУправления.Лево;
			ГлавныйРазделитель.Свертка=РежимСверткиЭлементаУправления.Лево;
		КонецЕсли;
	ИначеЕсли ВРег(Направление) = "ПРАВО" Тогда
		
		Если Видимость Тогда
			// Показать
			ГлавныйРазделитель.Свертка = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка = РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право,  ПанельФормы, ГраницаЭлементаУправления.Лево);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.Ширина = 4;
		Иначе 
			//скрыть 
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ГлавныйЭлемент, ГраницаЭлементаУправления.Лево);
			ГлавныйЭлемент.Свертка = РежимСверткиЭлементаУправления.Право;
			ГлавныйРазделитель.Свертка = РежимСверткиЭлементаУправления.Право;
		КонецЕсли;	
		
	ИначеЕсли ВРег(Направление) = "НИЗ" Тогда
		
		Если Видимость Тогда
			// Откроем
			ГлавныйРазделитель.Свертка  = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка		= РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Верх);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Низ);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			ГлавныйРазделитель.Высота 	= 4;
		Иначе // Скроем 
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх);
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Верх);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ГлавныйЭлемент,ГраницаЭлементаУправления.Верх);
			ГлавныйЭлемент.Свертка 		= РежимСверткиЭлементаУправления.Низ;		
			ГлавныйРазделитель.Свертка	= РежимСверткиЭлементаУправления.Низ;		
		КонецЕсли;
		
	ИначеЕсли ВРег(Направление) = "ВЕРХ" Тогда
		Если Видимость Тогда
			// Откроем
			ГлавныйРазделитель.Свертка  = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка		= РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Верх);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Низ);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ГлавныйРазделитель,ГраницаЭлементаУправления.Верх);
			ГлавныйРазделитель.Высота 	= 4;
		Иначе // Скроем 
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ);
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Низ);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйЭлемент, ГраницаЭлементаУправления.Низ);
			ГлавныйЭлемент.Свертка 		= РежимСверткиЭлементаУправления.Верх;		
			ГлавныйРазделитель.Свертка	= РежимСверткиЭлементаУправления.Верх;		
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры	// обУправлениеВидимостиЭлементов()

#КонецЕсли

// Проверяет - действует ли данное значимое событие для респондента, переданного в качестве параметра
//
// Параметры
//  <Объект>  - объект, запись которого вызывает значимое событие, содержащий ссылку на респондента <Контрагент или КонтактноеЛицо>
//  <ЗначимоеСобытие>  - <Значимое событие, которое срабатывает
//  <ВладелецРеквизита> - где искать реквизит "Контрагент" : Тип Строка, содержит "шапка", "свойство" или имя табличной части
//  <ИмяРеквизита> - имя реквизита содержащего значение респондента
// Возвращаемое значение:
//   Булево   - Истина - значит событие распространяется на респондента, Ложь - событие не для него
//
Функция увкЗначимоеСобытиеДействуетДляРеспондента(ЗначимоеСобытие,Респондент) Экспорт
	Результат = Ложь;
	КатегорияРеспондента = Респондент.КатегорияРеспондента;
	СтруктураОтбора = Новый Структура();
	Если КатегорияРеспондента = Справочники.КатегорииРеспондентов.ВнеКатегорий ИЛИ обЗначениеНеЗаполнено(КатегорияРеспондента) Тогда
		СтруктураОтбора.Вставить("Объект",Респондент);
	Иначе
		СтруктураОтбора.Вставить("Объект",КатегорияРеспондента);
	КонецЕсли;
	ШаблонВзаимодействия = РегистрыСведений.НастройкаВзаимодействия.Получить(СтруктураОтбора).ШаблонВзаимодействия;
	
	СтрокаШаблона = ШаблонВзаимодействия.ДействияПоШаблону.Найти(ЗначимоеСобытие, "ЗначимоеСобытие");
	Если СтрокаШаблона <>Неопределено Тогда
		Результат = Истина;
	КонецЕсли;		
	
	Возврат Результат;
	
КонецФункции // СобытиеДействуетДляРеспондента()

// Проверяет - действует ли данное значимое событие для категории респондента, указанного в объекте
//	для этого проверяем - имеется ли для категории респондента, запись в регистре сведений  "НастройкаВзаимодействия" с шаблоном,
//	в который входит данное значимое событие
//
// Параметры
//  <Объект>  - объект, запись которого вызывает значимое событие, содержащий ссылку на респондента <Контрагент или КонтактноеЛицо>
//  <ЗначимоеСобытие>  - <Значимое событие, которое срабатывает
//  <ВладелецРеквизита> - где искать реквизит "Контрагент" : Тип Строка, содержит "шапка", "свойство" или имя табличной части
//  <ИмяРеквизита> - имя реквизита содержащего значение респондента
// Возвращаемое значение:
//   Булево   - Истина - значит событие распространяется на респондента, Ложь - событие не для него
//
Функция увкЗначимоеСобытиеПроверкаДействия(ЗначимоеСобытие,Объект,ВладелецРеквизита="ШАПКА",ИмяРеквизита="Контрагент") Экспорт
	Результат = Ложь;
	Если ВРЕГ(ВладелецРеквизита) = "ШАПКА" Тогда //это самый простой случай, просто получаем контрагента
		Попытка
			Респондент = Объект[ИмяРеквизита];
			Результат = увкЗначимоеСобытиеДействуетДляРеспондента(ЗначимоеСобытие,Респондент);
		Исключение
			#Если Клиент Тогда
				Сообщить("В функцию проверки значимого события передано не верное имя реквизита респондента <" + ИмяРеквизита + ">");	
			#КонецЕсли
		КонецПопытки;
	ИначеЕсли ВРЕГ(ВладелецРеквизита) = "СВОЙСТВО" Тогда // получим респондента из свойства
		Попытка
			Респондент = обПолучитьЗначениеСвойства(Объект,ПланыВидовХарактеристик.СвойстваОбъектов[ИмяРеквизита]);
			Результат = увкЗначимоеСобытиеДействуетДляРеспондента(ЗначимоеСобытие,Респондент);
		Исключение
			#Если Клиент Тогда
				Сообщить("В функцию проверки значимого события передано не верное имя свойства <" + ИмяРеквизита + ">");	
			#КонецЕсли
		КонецПопытки;
	ИначеЕсли Объект.Метаданные().ТабличныеЧасти.Найти(ВладелецРеквизита) <> Неопределено Тогда // получим респондента в строке табличной части
		Попытка
			Для Каждого СтрокаТЧ Из Объект[ВладелецРеквизита] Цикл // будем выполнять есл изначимое событие актуально хотя бы для одного из респондентов
				Респондент = СтрокаТЧ[ИмяРеквизита];
				Результат = увкЗначимоеСобытиеДействуетДляРеспондента(ЗначимоеСобытие,Респондент);
				Если Результат Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Исключение
			#Если Клиент Тогда
				Сообщить("В функцию проверки значимого события передано не верное имя свойства <" + ИмяРеквизита + ">");	
			#КонецЕсли
		КонецПопытки;
	Иначе
		#Если Клиент Тогда
			Сообщить("В функцию проверки значимого события передано не верное имя владельца реквизита <" + ВладелецРеквизита + ">");	
		#КонецЕсли
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции // СобытиеДействует()

// Проверяет - срабатывало ли данное значимое событие для объекта
//необходимо для того чтобы исключить дублирование напоминаний, "Событий" и другой реакции на значимые события
//как проверять - надо подумать
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция увкБылоЛиСобытиеДляОбъекта() Экспорт
КонецФункции // БылоЛиСобытиеДляОбъекта()
 
// Процедура выводит на печать табличный документ.
// Открывает форму Печати документов, хаполняет печатный документ формы.
//
// Параметры:
//  ПечДокумент - ТабличныйДокумент
//  КоличествоЭкземпляров - число
//  НаПринтер - булево
//  Заголовок - строка, заголовок формы печати
//  Ссылка - Ссылка объекта печати
//  ИсточникМакета - не используется
//
Процедура увкНапечататьДокумент(ПечДокумент, КоличествоЭкземпляров = 1, НаПринтер = Ложь, Заголовок = "", Ссылка = Неопределено, ИсточникМакета = Неопределено) Экспорт

	Если ПечДокумент = Неопределено тогда
		Возврат;
	КонецЕсли;

	// Получить необходимое количество копий
	Если КоличествоЭкземпляров > 0 Тогда
		ПечДокумент.КоличествоЭкземпляров = КоличествоЭкземпляров;
	КонецЕсли;

	Если НЕ ПечДокумент.АвтоМасштаб
	   И НЕ ЗначениеЗаполнено(ПечДокумент.ИмяПринтера) Тогда
		ПечДокумент.АвтоМасштаб = Истина;
	КонецЕсли;

	Если ТипЗнч(НаПринтер) = Тип("Булево") Тогда
		Если НаПринтер Тогда
			ПечДокумент.Вывод = ИспользованиеВывода.Разрешить;
			ПечДокумент.Напечатать();
		Иначе
			ФормаПечати = ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",, Новый УникальныйИдентификатор);
			ФормаПечати.Объект     			= Ссылка;
			ФормаПечати.ОбъектПредставление = Заголовок;
			ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ПечДокумент);
			ФормаПечати.Открыть();
		КонецЕсли;
	ИначеЕсли ТипЗнч(НаПринтер) = Тип("Массив") Тогда
		ПечДокумент.Вывод = ИспользованиеВывода.Разрешить;
		ПечДокумент.Напечатать();

		НаПринтер.Добавить(ПечДокумент);
	КонецЕсли;

КонецПроцедуры // НапечататьДокумент()

// Предназначена для получения пустого значения заданного типа:
// примитивного, или ссылочного.
//
// Параметры:
//  ЗаданныйТип   - тип, пустое значение которого нужно получить
//
Функция увкПустоеЗначениеТипа(ЗаданныйТип) Экспорт
	
	Если ЗаданныйТип = Тип("Число") Тогда
		Возврат 0;
	ИначеЕсли ЗаданныйТип = Тип("Строка") Тогда
		Возврат "";
	ИначеЕсли ЗаданныйТип = Тип("Дата") Тогда
		Возврат '00010101000000';
	ИначеЕсли ЗаданныйТип = Тип("Булево") Тогда
		Возврат Ложь;
	Иначе
		Возврат Новый (ЗаданныйТип);
	КонецЕсли;
	
КонецФункции // ПустоеЗначениеТипа();

//возвращает список. содержащий макро-текст, предназначенный для замены
//в зависимости от назначения формируется свой список
//
Функция увкПолучитьСписокМакро(Порядок = "Возр") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Действия.Действие КАК Действие,
	               |	Действия.ПредставлениеДействия КАК Представление
	               |ИЗ
	               |	Справочник.Действия КАК Действия
	               |ГДЕ
	               |	Действия.Автотекст = ИСТИНА
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Представление " + Порядок + "
	               |";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выгрузить();

	Возврат Выборка;
	
КонецФункции

Функция увкПреобразоватьТекстИзХТМЛФорматаВПростой(ТекстВФорматеХТМЛ) Экспорт

	НовыйHTMLДокумент = Новый COMОбъект("HtmlFile");
	НовыйHTMLДокумент.open("text/html");
	НовыйHTMLДокумент.write(ТекстВФорматеХТМЛ);
	НовыйHTMLДокумент.close();
	
	Возврат СтрЗаменить(НовыйHTMLДокумент.all.item(0).innerText, Символ(13), "");

КонецФункции

// Функция предназначена для преобразования обычного текста в HTML формат
// Параметры:
//	Текст     - Строка - текст, преобразуемый в HTML-формат;
//	Кодировка - Строка - кодировка письма; 
//
Функция увкПреобразоватьТекстВHTML(Текст, Знач Кодировка) Экспорт
	
	Результат = увкПреобразоватьФрагментТекстаВHTML(Текст);
	
	Если Не ЗначениеЗаполнено(Кодировка) Тогда
		Кодировка = "utf-8";
	КонецЕсли;
	
	ФорматХТМЛ = "<HTML><HEAD>
	|<META http-equiv=Content-Type content=" + """" + "text/html; charset=" + Кодировка + """" + ">
	|<META content=" + """" + "MSHTML 6.00.2800.1400" + """" + " name=GENERATOR></HEAD>
	|<BODY>" + Результат + "</BODY></HTML>";
	
	Возврат ФорматХТМЛ;

КонецФункции

Функция увкПолучитьТелоHTMLДокумента(Текст) Экспорт
	ВремТекст = ВРег(Текст);
	ПозицияНачальногоТега = Найти(Текст,"<BODY>");	
	ПозицияКонечногоТега = Найти(Текст,"</BODY>");
	Результат = "";
	Если ПозицияНачальногоТега <> 0 И ПозицияКонечногоТега <> 0 Тогда
		Результат = Сред(Текст,ПозицияНачальногоТега+6,ПозицияКонечногоТега-ПозицияНачальногоТега-6);
	КонецЕсли;
	Возврат Результат;
КонецФункции

//Функция предназначена для преобразования фрагмента обычного текста в HTML формат
//
Функция увкПреобразоватьФрагментТекстаВHTML(Текст) Экспорт
	Результат = "";
	Результат = СтрЗаменить(Текст, "<" , "&lt");
	Результат = СтрЗаменить(Результат, ">" , "&gt");
	Результат = СтрЗаменить(Результат, Символы.ПС, "<BR>");
	Результат = "<P>" + Результат + "</P>";
	Возврат Результат;
КонецФункции

// Функция возвращает телефон контрагента
//
Функция увкПолучитьНомерТелефона(КонтрагентСсылка) Экспорт
	Результат	= "";
	
	Если ТипЗнч(КонтрагентСсылка)=Тип("СправочникСсылка.Контрагенты") И НЕ обЗначениеНеЗаполнено(КонтрагентСсылка) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.Представление КАК Телефон
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект = &Контрагент
		|	И КонтактнаяИнформация.Тип = &Тип
		|
		|УПОРЯДОЧИТЬ ПО
		|	КонтактнаяИнформация.ЗначениеПоУмолчанию УБЫВ";
		Запрос.УстановитьПараметр("Контрагент",КонтрагентСсылка);
		Запрос.УстановитьПараметр("Тип",Перечисления.ТипыКонтактнойИнформации.Телефон);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Результат = Выборка.Телефон;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции


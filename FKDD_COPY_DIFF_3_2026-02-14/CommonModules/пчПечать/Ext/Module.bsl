#Если ТолстыйКлиентОбычноеПриложение Тогда


//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ОФИСНЫМИ ПАКЕТАМИ
//
//	Модуль содержит интерфейсные функции (API), используемые при создании
//	печатных форм основанных на офисных документах. Поддерживается два офисных 
//  пакета MS Office (шаблоны MS Word) и Open Office (шаблоны OO Writer).

// Создает соединение с выходной печатной формой.
// Необходимо вызвать перед любыми действиями над формой.
// Параметры:
// ТипДокумента - строка - тип печатной формы либо "MS" либо "OO"
//
// Возвращаемое значение:
// СсылкаПечатнаяФорма
//
Функция пчИнициализироватьПечатнуюФорму(знач ТипДокумента, знач НастройкиСтраницыМакета = Неопределено) Экспорт
	
	Если ВРег(ТипДокумента) = "MS" Тогда
		ПечатнаяФорма = пчИнициализироватьПечатнуюФормуMSWord(НастройкиСтраницыМакета);
		ПечатнаяФорма.Вставить("Тип", "MS");
		ПечатнаяФорма.Вставить("ПоследняяВыведеннаяОбласть", Неопределено);
		Возврат ПечатнаяФорма;
	ИначеЕсли ВРег(ТипДокумента) = "OO" Тогда
		ПечатнаяФорма = пчИнициализироватьПечатнуюФормуOOWriter();
		ПечатнаяФорма.Вставить("Тип", "OO");
		ПечатнаяФорма.Вставить("ПоследняяВыведеннаяОбласть", Неопределено);
		Возврат ПечатнаяФорма;
	КонецЕсли;
	
КонецФункции

// Создает соединение с макетом. В дальнейшем это соединение используется
// при получении из него областей (тегов и таблиц).
//
// Параметры:
//  ДвоичныеДанныеМакета - ДвоичныеДанные - двоичные данные макета
//  ТипШаблона - тип макета, либо "OO", либо "OO"
// Возвращаемое значение:
//  СсылкаМакет
//
Функция пчИнициализироватьМакет(знач ДвоичныеДанныеМакета, знач ТипМакета) Экспорт

	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	
	Если ВРег(ТипМакета) = "MS" Тогда
		Макет = пчПолучитьМакетMSWord(ДвоичныеДанныеМакета, ИмяВременногоФайла);
		Макет.Вставить("Тип", "MS");
		Возврат Макет;
	ИначеЕсли ВРег(ТипМакета) = "OO" Тогда
		Макет = пчПолучитьМакетOOWriter(ДвоичныеДанныеМакета, ИмяВременногоФайла);
		Макет.Вставить("Тип", "OO");
		Макет.Вставить("НастройкиСтраницыМакета", Неопределено);
		Возврат Макет;
	КонецЕсли;
	
КонецФункции

// Освобождает ссылки в созданном интерфейсе связи с офисным приложением.
// Необходимо вызывать каждый раз после завершения формирования макета и выводе
// печатной формы пользователю.
// Параметры:
// Handler - СсылкаПечатнаяФорма, СсылкаМакет
// ЗакрытьПриложение - булево - признак, требуется ли закрыть приложение.
//					Соединение с макетом требуется закрывать с закрытием приложения.
//					ПечатнуюФорму не требуется закрывать.
//
Процедура пчОчиститьСсылки(Handler, знач ЗакрытьПриложение = Истина) Экспорт
	
	Если Handler <> Неопределено Тогда
		Если Handler.Тип = "MS" Тогда
			пчЗакрытьСоединениеMSWord(Handler, ЗакрытьПриложение);
		Иначе
			пчЗакрытьСоединениеOOWriter(Handler, ЗакрытьПриложение);
		КонецЕсли;
		Handler = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Показывает сформированный документ пользователю.
// Фактически устанавливает ему признак видимости.
// Параметры
//  Handler - СсылкаПечатнаяФорма
//
Процедура пчПоказатьДокумент(знач Handler) Экспорт
	
	Если Handler.Тип = "MS" Тогда
		пчПоказатьДокументMSWord(Handler);
	ИначеЕсли Handler.Тип = "OO" Тогда
		пчПоказатьДокументOOWriter(Handler);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет к параметру НаборОбластей новую запись об области
// Параметры
// НаборОбластей - массив - набор областей (массив структур)
// ИмяОбласти - строка - имя добавляемой области
// ТипОбласти - строка - тип области:
//			ВерхнийКолонтитул
//			НижнийКолонтитул
//			Общая
//			СтрокаТаблицы
//			Список
//
Процедура пчДобавитьОписаниеОбласти(НаборОбластей, знач ИмяОбласти, знач ТипОбласти) Экспорт
	
	НоваяОбласть = Новый Структура;
	
	НоваяОбласть.Вставить("ИмяОбласти", ИмяОбласти);
	НоваяОбласть.Вставить("ТипОбласти", ТипОбласти);
	
	НаборОбластей.Вставить(ИмяОбласти, НоваяОбласть);
	
КонецПроцедуры

// Получает область из макета.
// Параметры
// СсылкаМакет - СсылкаМакет - ссылка на макет
// ОписаниеОбласти - ОписаниеОбласти - описание области
//
// Возвращаемое значение
// Область - область из макета
//
Функция пчПолучитьОбласть(знач СсылкаМакет, знач ОписаниеОбласти) Экспорт
	
	Область = Неопределено;
	Если СсылкаМакет.Тип = "MS" Тогда
		
		Если		ОписаниеОбласти.ТипОбласти = "ВерхнийКолонтитул" Тогда
			Область = пчПолучитьОбластьВерхнегоКолонтитулаMSWord(СсылкаМакет);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "НижнийКолонтитул" Тогда
			Область = пчПолучитьОбластьНижнегоКолонтитулаMSWord(СсылкаМакет);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Общая" Тогда
			Область = пчПолучитьОбластьМакетаMSWord(СсылкаМакет, ОписаниеОбласти.ИмяОбласти, 1, 0);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы" Тогда
			Область = пчПолучитьОбластьМакетаMSWord(СсылкаМакет, ОписаниеОбласти.ИмяОбласти);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Список" Тогда
			Область = пчПолучитьОбластьМакетаMSWord(СсылкаМакет, ОписаниеОбласти.ИмяОбласти, 1, 0);
		Иначе
			ВызватьИсключение "Тип области не указан или указан не корректно: " + Строка(ОписаниеОбласти.ТипОбласти);
		КонецЕсли;
		
		Если Область <> Неопределено Тогда
			Область.Вставить("ОписаниеОбласти", ОписаниеОбласти);
		КонецЕсли;
	ИначеЕсли СсылкаМакет.Тип = "OO" Тогда
		
		Если		ОписаниеОбласти.ТипОбласти = "ВерхнийКолонтитул" Тогда
			Область = пчПолучитьОбластьВерхнегоКолонтитулаOOWriter(СсылкаМакет);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "НижнийКолонтитул" Тогда
			Область = пчПолучитьОбластьНижнегоКолонтитулаOOWriter(СсылкаМакет);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Общая"
				ИЛИ ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы"
				ИЛИ ОписаниеОбласти.ТипОбласти = "Список" Тогда
			Область = пчПолучитьОбластьМакетаOOWriter(СсылкаМакет, ОписаниеОбласти.ИмяОбласти);
		Иначе
			ВызватьИсключение "Тип области не указан или указан не корректно: " + Строка(ОписаниеОбласти.ТипОбласти);
		КонецЕсли;
		
		Если Область <> Неопределено Тогда
			Область.Вставить("ОписаниеОбласти", ОписаниеОбласти);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Область;
	
КонецФункции

// Присоединяет область в печатную форму из макета.
// Применяется при одиночном выводе области.
//
// Параметры
// ПечатнаяФорма - СсылкаПечатнаяФорма - ссылка на печатную форму
// ОбластьМакета - Область - область из макета
// ПереходНаСледующуюСтроку - булево, требуется ли вставлять разрыв после вывода области
//
Процедура пчПрисоединитьОбласть(знач ПечатнаяФорма,
							  знач ОбластьМакета,
							  знач ПереходНаСледующуюСтроку = Истина,
							  знач ПрисоединитьСтрокуТаблицы = Ложь) Экспорт
							  
	Если ОбластьМакета = Неопределено Тогда
		Возврат;						  
	КонецЕсли; 
								  
	Попытка
		ОписаниеОбласти = ОбластьМакета.ОписаниеОбласти;
		
		Если ПечатнаяФорма.Тип = "MS" Тогда
			
			ВыведеннаяОбласть = Неопределено;
			
			Если		ОписаниеОбласти.ТипОбласти = "ВерхнийКолонтитул" Тогда
				пчДобавитьВерхнийКолонтитулMSWord(ПечатнаяФорма, ОбластьМакета);
			ИначеЕсли	ОписаниеОбласти.ТипОбласти = "НижнийКолонтитул" Тогда
				пчДобавитьНижнийКолонтитулMSWord(ПечатнаяФорма, ОбластьМакета);
			ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Общая" Тогда
				ВыведеннаяОбласть = пчПрисоединитьОбластьMSWord(ПечатнаяФорма, ОбластьМакета, ПереходНаСледующуюСтроку);
			ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Список" Тогда
				ВыведеннаяОбласть = пчПрисоединитьОбластьMSWord(ПечатнаяФорма, ОбластьМакета, ПереходНаСледующуюСтроку);
			ИначеЕсли	ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы" Тогда
				Если ПечатнаяФорма.ПоследняяВыведеннаяОбласть <> Неопределено
				   И ПечатнаяФорма.ПоследняяВыведеннаяОбласть.ТипОбласти = "СтрокаТаблицы"
				   И НЕ ПечатнаяФорма.ПоследняяВыведеннаяОбласть.ПереходНаСледующуюСтроку Тогда
					ВыведеннаяОбласть = пчПрисоединитьОбластьMSWord(ПечатнаяФорма, ОбластьМакета, ПереходНаСледующуюСтроку, Истина);
				Иначе
					ВыведеннаяОбласть = пчПрисоединитьОбластьMSWord(ПечатнаяФорма, ОбластьМакета, ПереходНаСледующуюСтроку);
				КонецЕсли;
			Иначе
				ВызватьИсключение(НСтр("ru = 'Тип области не указан или указан не корректно.'"));
			КонецЕсли;
			
			ОписаниеОбласти.Вставить("Область", ВыведеннаяОбласть);
			ОписаниеОбласти.Вставить("ПереходНаСледующуюСтроку", ПереходНаСледующуюСтроку);
			
			ПечатнаяФорма.ПоследняяВыведеннаяОбласть = ОписаниеОбласти; // содержит тип области, и границы области (если требуется)
			
		ИначеЕсли ПечатнаяФорма.Тип = "OO" Тогда
			Если		ОписаниеОбласти.ТипОбласти = "ВерхнийКолонтитул" Тогда
				пчДобавитьВерхнийКолонтитулOOWriter(ПечатнаяФорма, ОбластьМакета);
			ИначеЕсли	ОписаниеОбласти.ТипОбласти = "НижнийКолонтитул" Тогда
				пчДобавитьНижнийКолонтитулOOWriter(ПечатнаяФорма, ОбластьМакета);
			ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Общая"
					ИЛИ ОписаниеОбласти.ТипОбласти = "Список" Тогда
				пчУстановитьОсновнойКурсорНаТелоДокументаOOWriter(ПечатнаяФорма);
				пчПрисоединитьОбластьOOWriter(ПечатнаяФорма, ОбластьМакета, ПереходНаСледующуюСтроку, ПрисоединитьСтрокуТаблицы);
			ИначеЕсли	ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы" Тогда
				пчУстановитьОсновнойКурсорНаТелоДокументаOOWriter(ПечатнаяФорма);
				пчПрисоединитьОбластьOOWriter(ПечатнаяФорма, ОбластьМакета, ПереходНаСледующуюСтроку, Истина);
			Иначе
				ВызватьИсключение(НСтр("ru = 'Тип области не указан или указан не корректно'"));
			КонецЕсли;
			ПечатнаяФорма.ПоследняяВыведеннаяОбласть = ОписаниеОбласти; // содержит тип области, и границы области (если требуется)
		КонецЕсли;
	Исключение
		СообщениеОбОшибке = СокрЛП(КраткоеПредставлениеОшибки(ИнформацияОбОШибке()));
		СообщениеОбОшибке = ?(Прав(СообщениеОбОшибке, 1) = ".", СообщениеОбОшибке, СообщениеОбОшибке + ".");
		СообщениеОбОшибке = СообщениеОбОшибке + "Тип области не указан или указан не корректно: " + Строка(ОписаниеОбласти.ТипОбласти);
		ВызватьИсключение СообщениеОбОшибке;
	КонецПопытки;
	
КонецПроцедуры

// Заполняет параметры области печатной формы
//
// Параметры
// ПечатнаяФорма	- СсылкаПечатнаяФорма, Область - область печатной формы, либо сама печатная форма
// Данные			- ДанныеЗаполнения
//
Процедура пчЗаполнитьПараметры(знач ПечатнаяФорма, знач Данные) Экспорт
	
	ОписаниеОбласти = ПечатнаяФорма.ПоследняяВыведеннаяОбласть;
	
	Если ПечатнаяФорма.Тип = "MS" Тогда
		Если		ОписаниеОбласти.ТипОбласти = "ВерхнийКолонтитул" Тогда
			пчЗаполнитьПараметрыВерхнегоКолонтитулаMSWord(ПечатнаяФорма, Данные);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "НижнийКолонтитул" Тогда
			пчЗаполнитьПараметрыНижнегоКолонтитулаMSWord(ПечатнаяФорма, Данные);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Общая"
				ИЛИ ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы"
				ИЛИ ОписаниеОбласти.ТипОбласти = "Список" Тогда
			пчЗаполнитьПараметрыMSWord(ПечатнаяФорма.ПоследняяВыведеннаяОбласть.Область, Данные);
		Иначе
			ВызватьИсключение(НСтр("ru = 'Тип области не указан или указан не корректно'"));
		КонецЕсли;
	ИначеЕсли ПечатнаяФорма.Тип = "OO" Тогда
		Если		ПечатнаяФорма.ПоследняяВыведеннаяОбласть.ТипОбласти = "ВерхнийКолонтитул" Тогда
			пчУстановитьОсновнойКурсорНаВерхнийКолонтитулOOWriter(ПечатнаяФорма);
		ИначеЕсли	ПечатнаяФорма.ПоследняяВыведеннаяОбласть.ТипОбласти = "НижнийКолонтитул" Тогда
			пчУстановитьОсновнойКурсорНаНижнийКолонтитулOOWriter(ПечатнаяФорма);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Общая"
				ИЛИ ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы"
				ИЛИ ОписаниеОбласти.ТипОбласти = "Список" Тогда
			пчУстановитьОсновнойКурсорНаТелоДокументаOOWriter(ПечатнаяФорма);
		КонецЕсли;
		пчЗаполнитьПараметрыOOWriter(ПечатнаяФорма, Данные);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет область в печатную форму из макета, при этом заменяя
// параметры в области значениями из данных объекта.
// Применяется при одиночном выводе области.
//
// Параметры
// ПечатнаяФорма	- СсылкаПечатнаяФорма
// ОбластьМакета	- Область
// Данные			- ДанныеОбъекта
// ПереходНаСледСтроку - булево, требуется ли вставлять разрыв после вывода области
//
Процедура пчПрисоединитьОбластьИЗаполнитьПараметры(знач ПечатнаяФорма,
										знач ОбластьМакета,
										знач Данные,
										знач ПереходНаСледующуюСтроку = Истина,
										знач ПрисоединитьСтрокуТаблицы = Ложь) Экспорт
																			
	Если ОбластьМакета <> Неопределено Тогда
		пчПрисоединитьОбласть(ПечатнаяФорма, ОбластьМакета, ПереходНаСледующуюСтроку, ПрисоединитьСтрокуТаблицы);
		пчЗаполнитьПараметры(ПечатнаяФорма, Данные)
	КонецЕсли;
	
КонецПроцедуры

// Добавляет область в печатную форму из макета, при этом заменяя
// параметры в области значениями из данных объекта.
// Применяется при одиночном выводе области.
//
// Параметры
// ПечатнаяФорма	- СсылкаПечатнаяФорма
// ОбластьМакета	- Область - область макета
// Данные			- ДанныеОбъекта (массив структур)
// ПереходНаСледСтроку - булево, требуется ли вставлять разрыв после вывода области
//
Процедура пчПрисоединитьИЗаполнитьКоллекцию(знач ПечатнаяФорма,
										знач ОбластьМакета,
										знач Данные,
										знач ПереходНаСледСтроку = Истина) Экспорт
	Если ОбластьМакета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОбласти = ОбластьМакета.ОписаниеОбласти;
	
	Если ПечатнаяФорма.Тип = "MS" Тогда
		Если		ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы" Тогда
			пчПрисоединитьИЗаполнитьОбластьТаблицыMSWord(ПечатнаяФорма, ОбластьМакета, Данные, ПереходНаСледСтроку);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Список" Тогда
			пчПрисоединитьИЗаполнитьНаборMSWord(ПечатнаяФорма, ОбластьМакета, Данные, ПереходНаСледСтроку);
		Иначе
			ВызватьИсключение(НСтр("ru = 'Тип области не указан или указан не корректно'"));
		КонецЕсли;
	ИначеЕсли ПечатнаяФорма.Тип = "OO" Тогда
		Если		ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы" Тогда
			пчПрисоединитьИЗаполнитьКоллекциюOOWriter(ПечатнаяФорма, ОбластьМакета, Данные, Истина, ПереходНаСледСтроку);
		ИначеЕсли	ОписаниеОбласти.ТипОбласти = "Список" Тогда
			пчПрисоединитьИЗаполнитьКоллекциюOOWriter(ПечатнаяФорма, ОбластьМакета, Данные, Ложь, ПереходНаСледСтроку);
		Иначе
			ВызватьИсключение(НСтр("ru = 'Тип области не указан или указан не корректно'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Вставляет разрыв между строками в виде символа перевода строки
// Параметры
// ПечатнаяФорма - СсылкаПечатнаяФорма
//
Процедура пчВставитьРазрывНаНовуюСтроку(знач ПечатнаяФорма) Экспорт
	
	Если	  ПечатнаяФорма.Тип = "MS" Тогда
		пчВставитьРазрывНаНовуюСтрокуMSWord(ПечатнаяФорма);
	ИначеЕсли ПечатнаяФорма.Тип = "OO" Тогда
		пчВставитьРазрывНаНовуюСтрокуOOWriter(ПечатнаяФорма);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Microsoft Word - специфичные функции

////////////////////////////////////////////////////////////////////////////////
// Экспортные функции

// Проверяет возможность создания COM соединения с COM объектом Word.Application
//
// Возвращаемое значение:
// Булево - возможность создания соединения
//
Функция пчПроверитьCOMСоединениеMSWord() Экспорт
	Попытка
 		Word = Новый COMОбъект("Word.Application");
        Word.Quit();
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции

// Проверяет возможность создания COM соединения с COM объектом Excel.Application
//
// Возвращаемое значение:
// Булево - возможность создания соединения
//
Функция пчПроверитьCOMСоединениеMSExcel() Экспорт
	Попытка
 		Excel = Новый COMОбъект("Excel.Application");
        Excel.Quit();
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции

// Создает COM соединение с COM объектом Word.Application, создает в нем
// единственный документ.
//
Функция пчИнициализироватьПечатнуюФормуMSWord(НастройкиСтраницыМакета) Экспорт
	
	Handler = Новый Структура("Тип", "MS");
	
	Попытка
		COMОбъект = Новый COMОбъект("Word.Application");
	Исключение
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке());
	КонецПопытки;
	
	Handler.Вставить("COMСоединение", COMОбъект);
	Попытка
		COMОбъект.Documents.Add();
	Исключение
		COMОбъект.Quit(0);
		COMОбъект = 0;
		Handler.COMОбъект = 0;
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если НастройкиСтраницыМакета <> Неопределено Тогда
		Для Каждого Настройка Из НастройкиСтраницыМакета Цикл
			Попытка
				COMОбъект.ActiveDocument.PageSetup[Настройка.Ключ] = Настройка.Значение;
			Исключение
				// Пропустить, если настройка не поддерживается данной версией программы.
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Handler;
	
КонецФункции

// Создает COM соединение с COM объектом Word.Application и открывает
// в нем макет. Файл макета сохраняется на основе двоичных данных
// переданных в параметрах функции.
//
// Параметры:
// ДвоичныеДанныеМакета - ДвоичныеДанные - двоичные данные макета
// Возвращаемое значение:
// структура - ссылка макет
//
Функция пчПолучитьМакетMSWord(знач ДвоичныеДанныеМакета, знач ИмяВременногоФайла = "") Экспорт
	
	Handler = Новый Структура("Тип", "MS");
	Попытка
		COMОбъект = Новый COMОбъект("Word.Application");
	Исключение
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке());
	КонецПопытки;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("MS");
	ДвоичныеДанныеМакета.Записать(ИмяВременногоФайла);
	
	Попытка
		COMОбъект.Documents.Open(ИмяВременногоФайла);
	Исключение
		COMОбъект.Quit(0);
		COMОбъект = 0;
		Handler.COMОбъект = 0;
		УдалитьФайлы(ИмяВременногоФайла);
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение(НСтр("ru = 'Ошибка при открытии файла шаблона.'") + Символы.ПС 
			+ КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Handler.Вставить("COMСоединение", COMОбъект);
	Handler.Вставить("ИмяФайла", ИмяВременногоФайла);
	Handler.Вставить("ЭтоМакет", Истина);
	
	Handler.Вставить("НастройкиСтраницыМакета", Новый Соответствие);
	
	Для Каждого ИмяНастройки Из пчНастройкиПараметровСтраницыMSWord() Цикл
		Попытка
			Handler.НастройкиСтраницыМакета.Вставить(ИмяНастройки, COMОбъект.ActiveDocument.PageSetup[ИмяНастройки]);
		Исключение
			// Пропустить, если настройка не поддерживается данной версией программы.
		КонецПопытки;
	КонецЦикла;
	
	Возврат Handler;
	
КонецФункции

// Закрывает соединение с COM объектом Word.Application
// Параметры:
// Handler - ссылка на печатную форму или макет
// ЗакрытьПриложение - булево - требуется ли закрыть приложение
//
Процедура пчЗакрытьСоединениеMSWord(Handler, знач ЗакрытьПриложение) Экспорт
	
	Если ЗакрытьПриложение Тогда
		Handler.COMСоединение.Quit(0);
	КонецЕсли;
	
	Handler.COMСоединение = 0;
	
	Если Handler.Свойство("ИмяФайла") Тогда
		УдалитьФайлы(Handler.ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает свойство видимости у приложения MS Word
// Handler - ссылка на печатную форму
//
Процедура пчПоказатьДокументMSWord(знач Handler) Экспорт
	
	COMСоединение = Handler.COMСоединение;
	COMСоединение.Application.Visible = Истина;
	COMСоединение.Activate();
	
КонецПроцедуры

#КонецЕсли

// Открывает структуру с макетом печатной формы
//
// Параметры:
// ДвоичныеДанныеМакета - ДвоичныеДанные - двоичные данные макета
// ИмяВременногоФайла - Строка - имя файла для записи макета
// Возвращаемое значение:
// Результат - признак успешного выполнения операции
//
Функция пчВывестиМакетMSExcel(знач ДвоичныеДанныеШаблона, ИмяВременногоФайла=Неопределено) Экспорт
	
	Попытка
		Если ДвоичныеДанныеШаблона.ВысотаТаблицы = 0 Тогда
			Сообщить("ОШИБКА! Пустой табличный документ!");
			Возврат Ложь;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
	Исключение
		Сообщить("ОШИБКА! Приложение MS Excel отсутствует или недостаточно прав доступа!
		|	- " + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецПопытки;
	
	Если ЗначениеЗаполнено(ИмяВременногоФайла) Тогда
		ВременныйФайлXLS = ИмяВременногоФайла;
	Иначе
		#Если ВебКлиент Тогда
			Excel = "";
			Возврат Ложь;
		#Иначе
			ВременныйФайл = ПолучитьИмяВременногоФайла();
			ВременныйФайлXLS = ВременныйФайл + ".xls";
		#КонецЕсли
	КонецЕсли;
	
	// Откроем табличный документ
	ДвоичныеДанныеШаблона.Записать(ВременныйФайлXLS, ТипФайлаТабличногоДокумента.XLS);
	Excel.Workbooks.Open(ВременныйФайлXLS);
	tmpBook = Excel.Workbooks.Item(Excel.Workbooks.Count); 
	
	КнигаExcel = Excel.Workbooks.Add(); 
	КнигаExcel.Colors = tmpBook.Colors;
	
	tmpBook.Sheets(1).Copy(КнигаExcel.WorkSheets(1));
	ЛистExcel = КнигаExcel.WorkSheets.Item(1);
	tmpBook.Close();
	КнигаExcel.Activate();
	ЛистExcel.Activate(); 
	Excel.Visible = 1;
	Excel = "";
	
	Возврат Истина;
	
КонецФункции


#Если ТолстыйКлиентОбычноеПриложение Тогда
////////////////////////////////////////////////////////////////////////////////
// Функции для получения областей из макета

// Получает область из макета.
//
// Параметры:
//  Handler - ссылка на макет
//  ИмяОбласти - имя области в макете
//  СмещениеНачало    - Число - переопределяет границу начала области для случаев, когда область начинается не сразу за
//                              операторной скобкой, а через один или несколько символов.
//                              Значение по умолчанию: 1 - ожидается, что за операторной скобкой открытия области 
//                                                         следует символ перевода строки, который не нужно включать в
//                                                         получаемую область.
//  СмещениеОкончание - Число - переопределяет границу окончания области для случаев, когда область заканчивается не
//                              перед операторной скобкой, а на один или несколько символов раньше. Значение должно 
//                              быть отрицательным.
//                              Значение по умолчанию:-1 - ожидается, что перед операторной скобкой закрытия области
//                                                         есть символ перевода строки, который не нужно включать в
//                                                         получаемую область.
//
Функция пчПолучитьОбластьМакетаMSWord(знач Handler,
									знач ИмяОбласти,
									знач СмещениеНачало = 1,
									знач СмещениеОкончание = -1) Экспорт
	
	Результат = Новый Структура("Document,Start,End");
	
	ПозицияНачало = СмещениеНачало + пчПолучитьПозициюНачалаОбластиMSWord(Handler.COMСоединение, ИмяОбласти);
	ПозицияОкончание = СмещениеОкончание + пчПолучитьПозициюОкончанияОбластиMSWord(Handler.COMСоединение, ИмяОбласти);
	
	Если ПозицияНачало >= ПозицияОкончание или ПозицияНачало < 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат.Document = Handler.COMСоединение.ActiveDocument;
	Результат.Start = ПозицияНачало;
	Результат.End   = ПозицияОкончание-1;
	
	Возврат Результат;
	
КонецФункции

// Получает область верхнего колонтитула первой области макета
// Параметры
// Handler - ссылка на макет
// Возвращаемое значение
// ссылка на верхний колонтитул
//
Функция пчПолучитьОбластьВерхнегоКолонтитулаMSWord(знач Handler) Экспорт
	
	Возврат Новый Структура("Header", Handler.COMСоединение.ActiveDocument.Sections(1).Headers.Item(1));
	
КонецФункции

// Получает область нижнего колонтитула первой области макета
// Параметры
// Handler - ссылка на макет
// Возвращаемое значение
// ссылка на нижний колонтитул
//
Функция пчПолучитьОбластьНижнегоКолонтитулаMSWord(Handler) Экспорт
	
	Возврат Новый Структура("Footer", Handler.COMСоединение.ActiveDocument.Sections(1).Footers.Item(1));
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Функции для добавления областей к печатной форме

// начало: функции работы с колонтитулами документа MS Word

// Добавляет нижний колонтитул в печатную форму из макета.
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете
// Параметры - список параметров для замены на значения
// ДанныеОбъекта - данные объекта для заполнения
//
Процедура пчДобавитьНижнийКолонтитулMSWord(знач ПечатнаяФорма,
									знач ОбластьHandler) Экспорт
	
	ОбластьHandler.Footer.Range.Copy();
	ПечатнаяФорма.COMСоединение.ActiveDocument.Sections(1).Footers.Item(1).Range.Paste();
	
КонецПроцедуры

// Добавляет верхний колонтитул в печатную форму из макета.
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете
// Параметры - список параметров для замены на значения
// ДанныеОбъекта - данные объекта для заполнения
//
Процедура пчЗаполнитьПараметрыНижнегоКолонтитулаMSWord(знач ПечатнаяФорма,
												знач ДанныеОбъекта = Неопределено) Экспорт
	
	Для Каждого ПараметрЗначение Из ДанныеОбъекта Цикл
		Если ТипЗнч(ПараметрЗначение.Значение) <> Тип("Массив") Тогда
			пчВыполнитьЗаменуВНижнемКолонтитулеMSWord(ПечатнаяФорма.COMСоединение,
												ПараметрЗначение.Ключ,
												ПараметрЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция пчВыполнитьЗаменуВНижнемКолонтитулеMSWord(COMСоединение, ТекстДляПоиска, ТекстЗамены)
	
	Range = COMСоединение.ActiveDocument.Sections(1).Footers.Item(1).Range;
	Поиск = Range.Find;
	Поиск.ClearFormatting();
	Поиск.execute("{v8 " + ТекстДляПоиска + "}");
	Если Поиск.Found Тогда
		Range.Text = ТекстЗамены;
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Добавляет верхний колонтитул в печатную форму из макета.
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете
// Параметры - список параметров для замены на значения
// ДанныеОбъекта - данные объекта для заполнения
//
Процедура пчДобавитьВерхнийКолонтитулMSWord(знач ПечатнаяФорма,
									знач ОбластьHandler) Экспорт
	
	ОбластьHandler.Header.Range.Copy();
	ПечатнаяФорма.COMСоединение.ActiveDocument.Sections(1).Headers.Item(1).Range.Paste();
	
КонецПроцедуры

// Добавляет верхний колонтитул в печатную форму из макета.
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете
// Параметры - список параметров для замены на значения
// ДанныеОбъекта - данные объекта для заполнения
//
Процедура пчЗаполнитьПараметрыВерхнегоКолонтитулаMSWord(знач ПечатнаяФорма,
												знач ДанныеОбъекта = Неопределено) Экспорт
	
	Для Каждого ПараметрЗначение Из ДанныеОбъекта Цикл
		Если ТипЗнч(ПараметрЗначение.Значение) <> Тип("Массив") Тогда
			пчВыполнитьЗаменуВВерхнемКолонтитулеMSWord(ПечатнаяФорма.COMСоединение,
												ПараметрЗначение.Ключ,
												ПараметрЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция пчВыполнитьЗаменуВВерхнемКолонтитулеMSWord(COMСоединение, ТекстДляПоиска, ТекстЗамены)
	
	Range = COMСоединение.ActiveDocument.Sections(1).Headers.Item(1).Range;
	Поиск = Range.Find;
	Поиск.ClearFormatting();
	Поиск.execute("{v8 " + ТекстДляПоиска + "}");
	Если Поиск.Found Тогда
		Range.Text = ТекстЗамены;
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// конец: функции работы с колонтитулами документа MS Word

// Добавляет область в печатную форму из макета, при этом заменяя
// параметры в области значениями из данных объекта.
// Применяется при одиночном выводе области.
//
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете.
// ПереходНаСледСтроку - булево, требуется ли вставлять разрыв после вывода области
//
// Возвращаемое значение:
// КоординатыОбласти
//
Функция пчПрисоединитьОбластьMSWord(знач ПечатнаяФорма,
							знач ОбластьHandler,
							знач ПереходНаСледСтроку = Истина,
							знач ПрисоединитьСтрокуТаблицы = Ложь) Экспорт
	
	ОбластьHandler.Document.Range(ОбластьHandler.Start, ОбластьHandler.End).Copy();
	
	ПФ_ActiveDocument = ПечатнаяФорма.COMСоединение.ActiveDocument;
	ПозицияОкончанияДокумента	= ПФ_ActiveDocument.Range().End;
	ОбластьВставки				= ПФ_ActiveDocument.Range(ПозицияОкончанияДокумента-1, ПозицияОкончанияДокумента-1);
	
	Если ПрисоединитьСтрокуТаблицы Тогда
		ОбластьВставки.PasteAppendTable();
	Иначе
		ОбластьВставки.Paste();
	КонецЕсли;
	
	// возвращаем границы вставленной области
	Результат = Новый Структура("Document, Start, End",
							ПФ_ActiveDocument,
							ПозицияОкончанияДокумента-1,
							ПФ_ActiveDocument.Range().End-1);
	
	Если ПереходНаСледСтроку Тогда
		пчВставитьРазрывНаНовуюСтроку(ПечатнаяФорма);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавляет область списка в печатную форму из макета, при этом заменяя
// параметры в области значениями из данных объекта.
// Применяется при выводе данных списка (маркированного или нумерованного)
//
// Параметры
// ОбластьПечатнойФормы - ссылка на область в печатной форме
// ДанныеОбъекта - ДанныеОбъекта
//
Процедура пчЗаполнитьПараметрыMSWord(знач ОбластьПечатнойФормы,
							знач ДанныеОбъекта = Неопределено) Экспорт
	
	Для Каждого ПараметрЗначение Из ДанныеОбъекта Цикл
		Если ТипЗнч(ПараметрЗначение.Значение) <> Тип("Массив") Тогда
			пчВыполнитьЗаменуMSWord(ОбластьПечатнойФормы.Document,
							ПараметрЗначение.Ключ,
							ПараметрЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция пчВыполнитьЗаменуMSWord(знач ActiveDocument, ТекстДляПоиска, ТекстЗамены)
	
	Range = ActiveDocument.Content;
	Поиск = Range.Find;
	Поиск.ClearFormatting();
	Поиск.execute("{v8 " + ТекстДляПоиска + "}");
	Если Поиск.Found Тогда
		Range.Text = Строка(ТекстЗамены);
		пчВыполнитьЗаменуMSWord(ActiveDocument, ТекстДляПоиска, ТекстЗамены);
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// начало: работа с коллекциями

// Добавляет область списка в печатную форму из макета, при этом заменяя
// параметры в области значениями из данных объекта.
// Применяется при выводе данных списка (маркированного или нумерованного)
//
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете.
// Параметры - строка, перечень параметров, которые требуется заменить
// ДанныеОбъекта - ДанныеОбъекта
// ПереходНаСледСтроку - булево, требуется ли вставлять разрыв после вывода области
//
Процедура пчПрисоединитьИЗаполнитьНаборMSWord(знач ПечатнаяФорма,
									  знач ОбластьHandler,
									  знач ДанныеОбъекта = Неопределено,
									  знач ПереходНаСледСтроку = Истина) Экспорт
	
	ОбластьHandler.Document.Range(ОбластьHandler.Start, ОбластьHandler.End).Copy();
	
	ActiveDocument = ПечатнаяФорма.COMСоединение.ActiveDocument;
	
	Для Каждого ДанныеСтроки Из ДанныеОбъекта Цикл
		ПозицияВставки = ActiveDocument.Range().End-1;
		ОбластьВставки = ActiveDocument.Range(ПозицияВставки-1, ПозицияВставки-1);
		ОбластьВставки.Paste();
		
		Если ТипЗнч(ДанныеСтроки) = Тип("Структура") Тогда
			Для Каждого ПараметрЗначение Из ДанныеСтроки Цикл
				пчВыполнитьЗаменуMSWord(ActiveDocument, ПараметрЗначение.Ключ, ПараметрЗначение.Значение);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если ПереходНаСледСтроку Тогда
		пчВставитьРазрывНаНовуюСтроку(ПечатнаяФорма);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет область списка в печатную форму из макета, при этом заменяя
// параметры в области значениями из данных объекта.
// Применяется при выводе строки таблицы.
//
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете.
// ИмяТаблицы - наименование таблицы (для доступа к данным)
// ДанныеОбъекта - ДанныеОбъекта
// ПереходНаСледСтроку - булево, требуется ли вставлять разрыв после вывода области
//
Процедура пчПрисоединитьИЗаполнитьОбластьТаблицыMSWord(знач ПечатнаяФорма,
												знач ОбластьHandler,
												знач ДанныеОбъекта = Неопределено,
												знач ПереходНаСледСтроку = Истина) Экспорт
												
	Если ДанныеОбъекта.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПерваяСтрока = Истина;
	
	ОбластьHandler.Document.Range(ОбластьHandler.Start, ОбластьHandler.End).Copy();
	
	ActiveDocument = ПечатнаяФорма.COMСоединение.ActiveDocument;
	
	// вставляем первый строку, по которому далее будет выполняться
	// вставка новых строк с форматированием по первой
	//пчВставитьРазрывНаНовуюСтроку(ПечатнаяФорма); 
	ПозицияВставки = ActiveDocument.Range().End; //! при вставке области End-1 
	ОбластьВставки = ActiveDocument.Range(ПозицияВставки-1, ПозицияВставки-1);
	ОбластьВставки.Paste();
	ActiveDocument.Range(ПозицияВставки-2, ПозицияВставки-2).Delete();
	
	Если ТипЗнч(ДанныеОбъекта[0]) = Тип("Структура") Тогда
		Для Каждого ПараметрЗначение Из ДанныеОбъекта[0] Цикл
			пчВыполнитьЗаменуMSWord(ActiveDocument, ПараметрЗначение.Ключ, ПараметрЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ДанныеСтрокиТаблицы Из ДанныеОбъекта Цикл
		Если ПерваяСтрока Тогда
			ПерваяСтрока = Ложь;
			Продолжить;
		КонецЕсли;
		
		НоваяПозицияВставки = ActiveDocument.Range().End;
		ActiveDocument.Range(ПозицияВставки-1, ActiveDocument.Range().End-1).Select();
		ПечатнаяФорма.COMСоединение.Selection.InsertRowsBelow();
		
		ActiveDocument.Range(НоваяПозицияВставки-1, ActiveDocument.Range().End-2).Select();
		ПечатнаяФорма.COMСоединение.Selection.Paste();
		ПозицияВставки = НоваяПозицияВставки;
		
		Если ТипЗнч(ДанныеСтрокиТаблицы) = Тип("Структура") Тогда
			Для Каждого ПараметрЗначение Из ДанныеСтрокиТаблицы Цикл
				пчВыполнитьЗаменуMSWord(ActiveDocument, ПараметрЗначение.Ключ, ПараметрЗначение.Значение);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПереходНаСледСтроку Тогда
		пчВставитьРазрывНаНовуюСтроку(ПечатнаяФорма);
	КонецЕсли;
	
КонецПроцедуры

// конец: работа с коллекциями

// Вставляет разрыв на следующую строку
// Параметры
// Handler - ссылка на документ MS Word в который требуется вставить разрыв
//
Процедура пчВставитьРазрывНаНовуюСтрокуMSWord(знач Handler) Экспорт
	
	ActiveDocument = Handler.COMСоединение.ActiveDocument;
	ПозицияОкончанияДокумента = ActiveDocument.Range().End;
	ActiveDocument.Range(ПозицияОкончанияДокумента-1, ПозицияОкончанияДокумента-1).InsertParagraphAfter();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Служебные не экспортные функции

Функция пчПолучитьПозициюНачалаОбластиMSWord(знач COMСоединение, знач ИдентификаторОбласти)
	
	ИдентификаторОбласти = "{v8 Область." + ИдентификаторОбласти + "}";
	
	ВесьДокумент = COMСоединение.ActiveDocument.Content;
	ВесьДокумент.Select();
	
	Поиск = COMСоединение.Selection.Find;
	Поиск.Text = ИдентификаторОбласти;
	Поиск.ClearFormatting();
	Поиск.Forward = Истина;
	Поиск.execute();
	
	Если Поиск.Found Тогда
		Возврат COMСоединение.Selection.End;
	КОнецЕсли;
	
	ВОзврат -1;
	
КонецФункции

Функция пчПолучитьПозициюОкончанияОбластиMSWord(знач COMСоединение, знач ИдентификаторОбласти)
	
	ИдентификаторОбласти = "{/v8 Область." + ИдентификаторОбласти + "}";
	
	ВесьДокумент = COMСоединение.ActiveDocument.Content;
	ВесьДокумент.Select();
	
	Поиск = COMСоединение.Selection.Find;
	Поиск.Text = ИдентификаторОбласти;
	Поиск.ClearFormatting();
	Поиск.Forward = Истина;
	Поиск.execute();
	
	Если Поиск.Found Тогда
		Возврат COMСоединение.Selection.Start;
	КОнецЕсли;
	
	ВОзврат -1;
	
КонецФункции

Функция пчНастройкиПараметровСтраницыMSWord()
	
	МассивНастроек = Новый Массив;
	МассивНастроек.Добавить("Orientation");
	МассивНастроек.Добавить("TopMargin");
	МассивНастроек.Добавить("BottomMargin");
	МассивНастроек.Добавить("LeftMargin");
	МассивНастроек.Добавить("RightMargin");
	МассивНастроек.Добавить("Gutter");
	МассивНастроек.Добавить("HeaderDistance");
	МассивНастроек.Добавить("FooterDistance");
	МассивНастроек.Добавить("PageWidth");
	МассивНастроек.Добавить("PageHeight");
	МассивНастроек.Добавить("FirstPageTray");
	МассивНастроек.Добавить("OtherPagesTray");
	МассивНастроек.Добавить("SectionStart");
	МассивНастроек.Добавить("OddAndEvenPagesHeaderFooter");
	МассивНастроек.Добавить("DifferentFirstPageHeaderFooter");
	МассивНастроек.Добавить("VerticalAlignment");
	МассивНастроек.Добавить("SuppressEndnotes");
	МассивНастроек.Добавить("MirrorMargins");
	МассивНастроек.Добавить("TwoPagesOnOne");
	МассивНастроек.Добавить("BookFoldPrinting");
	МассивНастроек.Добавить("BookFoldRevPrinting");
	МассивНастроек.Добавить("BookFoldPrintingSheets");
	МассивНастроек.Добавить("GutterPos");
	
	Возврат МассивНастроек;
	
КонецФункции

Функция пчСобытиеЖурналаРегистрацииMSWord()
	Возврат НСтр("ru = 'Печать'");
КонецФункции

Процедура пчНеУдалосьСформироватьПечатнуюФормуMSWord(ИнформацияОбОшибке)
#Если ВебКлиент Тогда
	ТекстУточнения = НСтр("ru = 'При работе через веб, требуется браузер Internet Explorer под управлением операционной системы Windows.'");
#Иначе		
	ТекстУточнения = "";	
#КонецЕсли
	ТекстИсключения = "Не удалось сформировать печатную форму: " + КраткоеПредставлениеОшибки(ИнформацияОбОшибке) + ". 
			|Для вывода печатных форм в формате Microsoft Word требуется, чтобы на компьютере был установлен пакет Microsoft Office. "	+ ТекстУточнения;
	ВызватьИсключение ТекстИсключения;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Open Office Writer - специфичные функции                                   

////////////////////////////////////////////////////////////////////////////////
// Экспортные функции

// заполняет массив свойств
Функция пчMakePropertyValue(ServiceManager,Name, Value) Экспорт
	
	Struct = ServiceManager.Bridge_GetStruct("com.sun.star.beans.PropertyValue");
	Struct.Name  = Name;
	Struct.Value = Value;
	
	Возврат Struct;
	
КонецФункции

// Проверяет возможность создания COM соединения с COM объектом Open Office
//
// Возвращаемое значение:
// Булево - возможность создания соединения
//
Функция пчПроверитьCOMСоединениеOOWriter() Экспорт
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		ServiceManager = Неопределено;
		Desktop = Неопределено;
		Возврат Истина;
	Исключение
		Возврат Ложь;	
	КонецПопытки;
КонецФункции

// Инициализация печатной формы: создается COM - объект,
// ему устанавливаются свойства.
//
Функция пчИнициализироватьПечатнуюФормуOOWriter() Экспорт
	
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
	Исключение
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, 
			НСтр("ru = 'Ошибка при связи с сервис менеджером (com.sun.star.ServiceManager).'") + 
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке());
	КонецПопытки;
	
	Попытка
		Desktop = ServiceManager.CreateInstance("com.sun.star.frame.Desktop");
	Исключение
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, 
			НСтр("ru = 'Ошибка при запуске сервиса Desktop (com.sun.star.frame.Desktop).'") + 
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке());
	КонецПопытки;
	
	Параметры = пчПолучитьComSafeArrayOOWriter();
#Если Не ВебКлиент Тогда
	Параметры.SetValue(0, пчСвойствоЗначениеOOWriter(ServiceManager, "Hidden", Истина));
#КонецЕсли
	
	Document = Desktop.LoadComponentFromURL("private:factory/swriter", "_blank", 0, Параметры);
	
#Если ВебКлиент Тогда
	Document.getCurrentController().getFrame().getContainerWindow().setVisible(Ложь);
#КонецЕсли

	// Подготавливаем ссылку на макет
	Handler = Новый Структура("ServiceManager,Desktop,Document,Тип");
	Handler.ServiceManager = ServiceManager;
	Handler.Desktop = Desktop;
	Handler.Document = Document;
	
	Возврат Handler;
	
КонецФункции

// Возвращает структуру с макетом печатной формы
//
// Параметры:
// ДвоичныеДанныеМакета - ДвоичныеДанные - двоичные данные макета
// Возвращаемое значение:
// структура - ссылка макет
//
Функция пчПолучитьМакетOOWriter(знач ДвоичныеДанныеШаблона, ИмяВременногоФайла) Экспорт
	
	Handler = Новый Структура("ServiceManager,Desktop,Document,ИмяФайла");
	
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
	Исключение
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, 
			НСтр("ru = 'Ошибка при связи с сервис менеджером (com.sun.star.ServiceManager).'") + 
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке());
	КонецПопытки;
	
	Попытка
		Desktop = ServiceManager.CreateInstance("com.sun.star.frame.Desktop");
	Исключение
		ЗаписьЖурналаРегистрации("Печать", УровеньЖурналаРегистрации.Ошибка,,, 
			НСтр("ru = 'Ошибка при запуске сервиса Desktop (com.sun.star.frame.Desktop).'") + 
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке());
	КонецПопытки;
	
#Если НЕ ВебКлиент Тогда
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("OO");
	ДвоичныеДанныеШаблона.Записать(ИмяВременногоФайла);
#КонецЕсли
	
	Параметры = пчПолучитьComSafeArrayOOWriter();
#Если Не ВебКлиент Тогда
	Параметры.SetValue(0, пчСвойствоЗначениеOOWriter(ServiceManager, "Hidden", Истина));
#КонецЕсли
	
	Document = Desktop.LoadComponentFromURL("file:///" + ИмяВременногоФайла, "_blank", 0, Параметры);
	
#Если ВебКлиент Тогда
	Document.getCurrentController().getFrame().getContainerWindow().setVisible(Ложь);
#КонецЕсли
	
	// Подготавливаем ссылку на макет
	Handler.ServiceManager = ServiceManager;
	Handler.Desktop = Desktop;
	Handler.Document = Document;
	Handler.ИмяФайла = ИмяВременногоФайла;
	
	Возврат Handler;
	
КонецФункции

// Закрывает макет печатной формы и затирает ссылки на COM объект
//
Функция пчЗакрытьСоединениеOOWriter(Handler, знач ЗакрытьПриложение) Экспорт
	
	Если ЗакрытьПриложение Тогда
		Handler.Document.Close(0);
	КонецЕсли;
	
	Handler.Document = Неопределено;
	Handler.Desktop = Неопределено;
	Handler.ServiceManager = Неопределено;
	ScriptControl = Неопределено;
	
	Если Handler.Свойство("ИмяФайла") Тогда
		УдалитьФайлы(Handler.ИмяФайла);
	КонецЕсли;
	
	Handler = Неопределено;
	
КонецФункции

// Устанавливает свойство видимости у приложения OO Writer
// Handler - ссылка на печатную форму
//
Процедура пчПоказатьДокументOOWriter(знач Handler) Экспорт
	
	ContainerWindow = Handler.Document.getCurrentController().getFrame().getContainerWindow();
	ContainerWindow.setVisible(Истина);
	ContainerWindow.setFocus();
	
КонецПроцедуры

#КонецЕсли


// Открывает структуру с макетом печатной формы
//
// Параметры:
// ДвоичныеДанныеМакета - ДвоичныеДанные - двоичные данные макета
// ИмяВременногоФайла - Строка - имя файла для записи макета
// Возвращаемое значение:
// Результат - признак успешного выполнения операции
//
Функция пчВывестиМакетOpenOffice(знач ДвоичныеДанныеШаблона, ИмяВременногоФайла=Неопределено) Экспорт
	
	// Пытаемся создать COM объект
	Попытка
		ServiceManager = Новый COMОбъект("com.sun.star.ServiceManager");
		Desktop = ServiceManager.createInstance("com.sun.star.frame.Desktop");
		Params = ServiceManager.Bridge_GetStruct("com.sun.star.beans.PropertyValue");
	Исключение
		Сообщить("ОШИБКА! Приложение Open Office (версии не ниже 2.0) отсутствует или недостаточно прав доступа!
		|	- " + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецПопытки;	
	
	Если ЗначениеЗаполнено(ИмяВременногоФайла) Тогда
		ВременныйФайлXLS = ИмяВременногоФайла;	
	Иначе
		#Если ВебКлиент Тогда
			Excel = "";
			Возврат Ложь;
		#Иначе
			ВременныйФайл = ПолучитьИмяВременногоФайла();
			ВременныйФайлXLS = ВременныйФайл + ".xls";
		#КонецЕсли
	КонецЕсли;
	
	ДвоичныеДанныеШаблона.Записать(ВременныйФайлXLS, ТипФайлаТабличногоДокумента.XLS);
	Params.Name = "Hidden";
	Params.Value = Ложь;
	Args = Новый COMSafeArray("VT_DISPATCH", 1);
	Args.SetValue(0,Params);
	
	ВременныйФайлXLS_какУРЛ = "file:///" + СтрЗаменить(ВременныйФайлXLS, "\", "/"); // приводим путь к файлу к формату OpenOffice
	Document = Desktop.LoadComponentFromURL(ВременныйФайлXLS_какУРЛ, "_blank", 0, Args);
	
	#Если ВебКлиент Тогда
		Document.getCurrentController().getFrame().getContainerWindow().setVisible(Ложь);
	#КонецЕсли
	
	Document = Неопределено;
	
	Возврат Истина;
	
КонецФункции

#Если ТолстыйКлиентОбычноеПриложение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// Блок функций для работы с макетом

// Получает область из макета.
// Параметры
// Handler - ссылка на макет
// ИмяОбласти - имя области в макете
// СмещениеНачало - смещение относительно начала области
//					смещение по умолчанию: 1 - означает что область берется без символа
//					перевода строки, за операторной скобкой открытия области
// СмещениеОкончание - смещение относительно окончания области,
//					смещение по умолчанию: -11 - означает что область берется без символа
//					перевода строки, перед операторной скобкой закрытия области
//
Функция пчПолучитьОбластьМакетаOOWriter(знач Handler, знач ИмяОбласти) Экспорт
	
	Результат = Новый Структура("Document,Start,End");
	
	Результат.Start = пчПолучитьПозициюНачалаОбластиOOWriter(Handler.Document, ИмяОбласти);
	Результат.End   = пчПолучитьПозициюОкончанияОбластиOOWriter(Handler.Document, ИмяОбласти);
	Результат.Document = Handler.Document;
	
	Возврат Результат;
	
КонецФункции

// Получает область верхнего колонтитула
//
Функция пчПолучитьОбластьВерхнегоКолонтитулаOOWriter(знач МакетСсылка) Экспорт
	
	Возврат Новый Структура("Document, ServiceManager", МакетСсылка.Document, МакетСсылка.ServiceManager);
	
КонецФункции

// Получает область нижнего колонтитула
//
Функция пчПолучитьОбластьНижнегоКолонтитулаOOWriter(МакетСсылка) Экспорт
	
	Возврат Новый Структура("Document, ServiceManager", МакетСсылка.Document, МакетСсылка.ServiceManager);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Блок функций для работы с печатной формой

// Вставляет разрыв на следующую строку
// Параметры
// Handler - ссылка на документ MS Word в который требуется вставить разрыв
//
Процедура пчВставитьРазрывНаНовуюСтрокуOOWriter(знач Handler) Экспорт
	
	oText = Handler.Document.getText();
	oCursor = oText.createTextCursor();
	oCursor.gotoEnd(False);
	oText.insertControlCharacter(oCursor, 0, False);
	
КонецПроцедуры

// Добавляет верхний колонтитул к печатной форме
//
Процедура пчДобавитьВерхнийКолонтитулOOWriter(знач ПечатнаяФорма,
									знач Область) Экспорт
	
	Макет_oTxtCrsr = пчУстановитьОсновнойКурсорНаВерхнийКолонтитулOOWriter(Область);
	Пока Макет_oTxtCrsr.goRight(1, Истина) Цикл
	КонецЦикла;
	TransferableObject = Область.Document.getCurrentController().Frame.controller.getTransferable();
	
	пчУстановитьОсновнойКурсорНаВерхнийКолонтитулOOWriter(ПечатнаяФорма);
	ПечатнаяФорма.Document.getCurrentController().insertTransferable(TransferableObject);
	
КонецПроцедуры

// Добавляет нижний колонтитул к печатной форме
//
Процедура пчДобавитьНижнийКолонтитулOOWriter(знач ПечатнаяФорма,
									знач Область) Экспорт
	
	Макет_oTxtCrsr = пчУстановитьОсновнойКурсорНаНижнийКолонтитулOOWriter(Область);
	Пока Макет_oTxtCrsr.goRight(1, Истина) Цикл
	КонецЦикла;
	TransferableObject = Область.Document.getCurrentController().Frame.controller.getTransferable();
	
	пчУстановитьОсновнойКурсорНаНижнийКолонтитулOOWriter(ПечатнаяФорма);
	ПечатнаяФорма.Document.getCurrentController().insertTransferable(TransferableObject);
	
КонецПроцедуры

// Добавляет область в печатную форму из макета, при этом заменяя
// параметры в области значениями из данных объекта.
// Применяется при одиночном выводе области.
//
// Параметры
// ПечатнаяФорма - ссылка на печатную форму
// ОбластьHandler - ссылка на область в макете.
// ПереходНаСледСтроку - булево, требуется ли вставлять разрыв после вывода области
//
// Возвращаемое значение:
// КоординатыОбласти
//
Процедура пчПрисоединитьОбластьOOWriter(знач ПечатнаяФормаHandler,
							знач ОбластьHandler,
							знач ПереходНаСледСтроку = Истина,
							знач ПрисоединитьСтрокуТаблицы = Ложь) Экспорт
							
	Макет_oTxtCrsr = ОбластьHandler.Document.getCurrentController().getViewCursor();
	Макет_oTxtCrsr.gotoRange(ОбластьHandler.Start, Ложь);
	
	// возникает ошибка
	//Если ПрисоединитьСтрокуТаблицы Тогда
	//	Макет_oTxtCrsr.goRight(1, Ложь);
	//КонецЕсли;

	Макет_oTxtCrsr.gotoRange(ОбластьHandler.End, Истина);
	
	TransferableObject = ОбластьHandler.Document.getCurrentController().Frame.controller.getTransferable();
	ПечатнаяФормаHandler.Document.getCurrentController().insertTransferable(TransferableObject);
	
	Если ПрисоединитьСтрокуТаблицы Тогда
		пчУдалитьСтрокуOOWriter(ПечатнаяФормаHandler);
	КонецЕсли;
	
	Если ПереходНаСледСтроку Тогда
		пчВставитьРазрывНаНовуюСтроку(ПечатнаяФормаHandler);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет параметры в табличном поле печатной формы
//
Процедура пчЗаполнитьПараметрыOOWriter(ПечатнаяФорма, Данные) Экспорт
	
	Для Каждого КлючЗначение Из Данные Цикл
		Если ТипЗнч(КлючЗначение) <> Тип("Массив") Тогда
			ПФ_oDoc = ПечатнаяФорма.Document;
			ПФ_ReplaceDescriptor = ПФ_oDoc.createReplaceDescriptor();
			ПФ_ReplaceDescriptor.SearchString = "{v8 " + КлючЗначение.Ключ + "}";
			ПФ_ReplaceDescriptor.ReplaceString = Строка(КлючЗначение.Значение);
			ПФ_oDoc.replaceAll(ПФ_ReplaceDescriptor);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавляет область коллекции к печатной форме
//
Процедура пчПрисоединитьИЗаполнитьКоллекциюOOWriter(знач ПечатнаяФормаHandler,
										  знач ОбластьHandler,
										  знач Данные,
										  знач ЭтоСтрокаТаблицы = Ложь,
										  знач ПереходНаСледСтроку = Истина) Экспорт
	
	Макет_oTxtCrsr = ОбластьHandler.Document.getCurrentController().getViewCursor();
	Макет_oTxtCrsr.gotoRange(ОбластьHandler.Start, Ложь);
	
	Если НЕ ЭтоСтрокаТаблицы Тогда
		Макет_oTxtCrsr.goRight(1, Ложь);
	КонецЕсли;
	Макет_oTxtCrsr.gotoRange(ОбластьHandler.End, Истина);
	
	TransferableObject = ОбластьHandler.Document.getCurrentController().Frame.controller.getTransferable();
	
	Для Каждого СтрокаСДанными Из Данные Цикл
		ПечатнаяФормаHandler.Document.getCurrentController().insertTransferable(TransferableObject);
		Если ЭтоСтрокаТаблицы Тогда
			пчУдалитьСтрокуOOWriter(ПечатнаяФормаHandler);
		КонецЕсли;
		пчЗаполнитьПараметры(ПечатнаяФормаHandler, СтрокаСДанными);
	КонецЦикла;
	
	Если ПереходНаСледСтроку Тогда
		пчВставитьРазрывНаНовуюСтроку(ПечатнаяФормаHandler);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает курсор в конец документа ДокументСсылка
//
Процедура пчУстановитьОсновнойКурсорНаТелоДокументаOOWriter(знач ДокументСсылка) Экспорт
	
	oDoc = ДокументСсылка.Document;
	oViewCursor = oDoc.getCurrentController().getViewCursor();
	oTextCursor = oDoc.Text.createTextCursor();
	oViewCursor.gotoRange(oTextCursor, Ложь);
	oViewCursor.gotoEnd(Ложь);
	
КонецПроцедуры

// Устанавливает курсор на верхний колонтитул
//
Функция пчУстановитьОсновнойКурсорНаВерхнийКолонтитулOOWriter(знач ДокументСсылка) Экспорт
	
	xCursor = ДокументСсылка.Document.getCurrentController().getViewCursor();
	PageStyleName = xCursor.getPropertyValue("PageStyleName");
	oPStyle = ДокументСсылка.Document.getStyleFamilies().getByName("PageStyles").getByName(PageStyleName);
	oPStyle.HeaderIsOn = Истина;
	HeaderTextCursor = oPStyle.getPropertyValue("HeaderText").createTextCursor();
	xCursor.gotoRange(HeaderTextCursor, Ложь);
	Возврат xCursor;
	
КонецФункции

// Устанавливает курсор на нижний колонтитул
//
Функция пчУстановитьОсновнойКурсорНаНижнийКолонтитулOOWriter(знач ДокументСсылка) Экспорт
	
	xCursor = ДокументСсылка.Document.getCurrentController().getViewCursor();
	PageStyleName = xCursor.getPropertyValue("PageStyleName");
	oPStyle = ДокументСсылка.Document.getStyleFamilies().getByName("PageStyles").getByName(PageStyleName);
	oPStyle.FooterIsOn = Истина;
	FooterTextCursor = oPStyle.getPropertyValue("FooterText").createTextCursor();
	xCursor.gotoRange(FooterTextCursor, Ложь);
	Возврат xCursor;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Служебные функции

// Получает специальную структуру, через которую объектам UNO устанавливаются
// параметры.
//
Функция пчСвойствоЗначениеOOWriter(знач ServiceManager, знач Свойство, знач Значение)
	
	PropertyValue = ServiceManager.Bridge_GetStruct("com.sun.star.beans.PropertyValue");
	PropertyValue.Name = Свойство;
	PropertyValue.Value = Значение;
	
	Возврат PropertyValue;
	
КонецФункции

Функция пчПолучитьПозициюНачалаОбластиOOWriter(знач xDocument, знач ИмяОбласти)
	
	ТекстДляПоиска = "{v8 Область." + ИмяОбласти + "}";
	
	xSearchDescr = xDocument.createSearchDescriptor();
	xSearchDescr.SearchString = ТекстДляПоиска;
	xSearchDescr.SearchCaseSensitive = Ложь;
	xSearchDescr.SearchWords = Истина;
	xFound = xDocument.findFirst(xSearchDescr);
	Если xFound = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не найдено начало области макета:'") + " " + ИмяОбласти;	
	КонецЕсли;
	Возврат xFound.End;
	
КонецФункции

Функция пчПолучитьПозициюОкончанияОбластиOOWriter(знач xDocument, знач ИмяОбласти)
	
	ТекстДляПоиска = "{/v8 Область." + ИмяОбласти + "}";
	
	xSearchDescr = xDocument.createSearchDescriptor();
	xSearchDescr.SearchString = ТекстДляПоиска;
	xSearchDescr.SearchCaseSensitive = Ложь;
	xSearchDescr.SearchWords = Истина;
	xFound = xDocument.findFirst(xSearchDescr);
	Если xFound = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не найден конец области макета:'") + " " + ИмяОбласти;	
	КонецЕсли;
	Возврат xFound.Start;
	
КонецФункции

Процедура пчУдалитьСтрокуOOWriter(ПечатнаяФормаHandler)
	
	oFrame = ПечатнаяФормаHandler.Document.getCurrentController().Frame;
	
	dispatcher = ПечатнаяФормаHandler.ServiceManager.CreateInstance ("com.sun.star.frame.DispatchHelper");
	
	oViewCursor = ПечатнаяФормаHandler.Document.getCurrentController().getViewCursor();
	
	dispatcher.executeDispatch(oFrame, ".uno:GoUp", "", 0, пчПолучитьComSafeArrayOOWriter());
	
	Пока oViewCursor.TextTable <> Неопределено Цикл
		dispatcher.executeDispatch(oFrame, ".uno:GoUp", "", 0, пчПолучитьComSafeArrayOOWriter());
	КонецЦикла;
	
	dispatcher.executeDispatch(oFrame, ".uno:Delete", "", 0, пчПолучитьComSafeArrayOOWriter());
	
	Пока oViewCursor.TextTable <> Неопределено Цикл
		dispatcher.executeDispatch(oFrame, ".uno:GoDown", "", 0, пчПолучитьComSafeArrayOOWriter());
	КонецЦикла;
	
КонецПроцедуры

Функция пчПолучитьComSafeArrayOOWriter()
	
#Если ВебКлиент Тогда
	scr = Новый COMОбъект("MSScriptControl.ScriptControl");
	scr.language = "javascript";
	scr.eval("Массив=new Array()");
	Возврат scr.eval("Массив");
#Иначе
	Возврат Новый COMSafeArray("VT_DISPATCH", 1);
#КонецЕсли
	
КонецФункции

Функция пчСобытиеЖурналаРегистрацииOOWriter()
	Возврат НСтр("ru = 'Печать'");
КонецФункции

Процедура пчНеУдалосьСформироватьПечатнуюФормуOOWriter(ИнформацияОбОшибке)
#Если ВебКлиент Тогда
	ТекстУточнения = НСтр("ru = 'При работе через веб, требуется браузер Internet Explorer под управлением операционной системы Windows.'");
#Иначе		
	ТекстУточнения = "";	
#КонецЕсли
	ТекстИсключения = "Не удалось сформировать печатную форму: " + КраткоеПредставлениеОшибки(ИнформацияОбОшибке) + ". 
			|Для вывода печатных форм в формате OpenOffice.org Writer требуется, чтобы на компьютере был установлен пакет OpenOffice.org. "	+ ТекстУточнения;
	ВызватьИсключение ТекстИсключения;
КонецПроцедуры

#КонецЕсли
Функция ЗаполнитьСправочникGoodsType() Экспорт
	Попытка
		НачатьТранзакцию();
		
		НайтиИДобавитьЭлементыGoodsType("Ремонт и строительство", ПолучитьGoodsType_РемонтИСтроительство());
		НайтиИДобавитьЭлементыGoodsType("Мебель и интерьер", ПолучитьGoodsType_МебельИИнтерьер());
		НайтиИДобавитьЭлементыGoodsType("Бытовая техника", ПолучитьGoodsType_БытоваяТехника());
		НайтиИДобавитьЭлементыGoodsType("Посуда и товары для кухни", ПолучитьGoodsType_ПосудаИТоварыДляКухни());
		
		ЗаполнитьGoodsSubType();
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
	    ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки; 
	
КонецФункции

Процедура НайтиИДобавитьЭлементыGoodsType(ИмяВладелеца, СписокGoodsType)
	Категория = Справочники.ns_Category.НайтиПоНаименованию(ИмяВладелеца);
    Если Категория.Пустая() Тогда
		Возврат;
	КонецЕсли;  
	
	Для каждого Элемент Из СписокGoodsType Цикл
		НайтиИДобавитьЭлементGoodsType(Элемент, Категория);
	КонецЦикла;
КонецПроцедуры

Процедура НайтиИДобавитьЭлементGoodsType(Наименование, Владелец)
	Элемент = Справочники.ns_GoodsType_ДляДомаИДачи.НайтиПоНаименованию(Наименование,,, Владелец);
	Если НЕ ЗначениеЗаполнено(Элемент) Тогда
		Элемент = Справочники.ns_GoodsType_ДляДомаИДачи.СоздатьЭлемент();
		Элемент.Владелец = Владелец; 
		Элемент.Наименование = Наименование;
		Элемент.Записать();
	КонецЕсли; 
КонецПроцедуры

Функция ПолучитьGoodsType_РемонтИСтроительство()
	Список = Новый Массив;
	Список.Добавить("Бытовки");
	Список.Добавить("Двери");
	Список.Добавить("Инструменты");
	Список.Добавить("Камины и обогреватели");
	Список.Добавить("Окна и балконы");
	Список.Добавить("Потолки");
	Список.Добавить("Садовая техника");
	Список.Добавить("Сантехника и сауна");
	Список.Добавить("Стройматериалы");
	
	Возврат Список;
КонецФункции

Функция ПолучитьGoodsType_МебельИИнтерьер()
	Список = Новый Массив;
	Список.Добавить("Компьютерные столы и кресла");
	Список.Добавить("Кровати, диваны и кресла");
	Список.Добавить("Кухонные гарнитуры");
	Список.Добавить("Освещение");
	Список.Добавить("Подставки и тумбы");
	Список.Добавить("Предметы интерьера, искусство");
	Список.Добавить("Столы и стулья");
	Список.Добавить("Текстиль и ковры");
	Список.Добавить("Шкафы и комоды");
	Список.Добавить("Другое");
	
	Возврат Список;
КонецФункции

Функция ПолучитьGoodsType_БытоваяТехника()
	Список = Новый Массив;
	Список.Добавить("Пылесосы");
	Список.Добавить("Стиральные машины");
	Список.Добавить("Утюги");
	Список.Добавить("Швейные машины");
	Список.Добавить("Бритвы и триммеры");
	Список.Добавить("Машинки для стрижки");
	Список.Добавить("Фены и приборы для укладки");
	Список.Добавить("Эпиляторы");
	Список.Добавить("Вытяжки");
	Список.Добавить("Мелкая кухонная техника");
	Список.Добавить("Микроволновые печи");
	Список.Добавить("Плиты");
	Список.Добавить("Посудомоечные машины");
	Список.Добавить("Холодильники и морозильные камеры");
	Список.Добавить("Вентиляторы");
	Список.Добавить("Кондиционеры");
	Список.Добавить("Обогреватели");
	Список.Добавить("Очистители воздуха");
	Список.Добавить("Термометры и метеостанции");
	Список.Добавить("Другое");
	
	Возврат Список;
КонецФункции

Функция ПолучитьGoodsType_ПосудаИТоварыДляКухни()
	Список = Новый Массив;
	Список.Добавить("Посуда");
	Список.Добавить("Товары для кухни");
	
	Возврат Список;
КонецФункции

Функция ЗаполнитьGoodsSubType()

	Список = Новый Массив;
	Список.Добавить("Изоляция");
	Список.Добавить("Крепёж");
	Список.Добавить("Кровля и водосток");
	Список.Добавить("Лаки и краски");
	Список.Добавить("Листовые материалы");
	Список.Добавить("Металлопрокат");
	Список.Добавить("Общестроительные материалы");
	Список.Добавить("Отделка");
	Список.Добавить("Пиломатериалы");
	Список.Добавить("Строительные смеси");
	Список.Добавить("Строительство стен");
	Список.Добавить("Электрика");
	Список.Добавить("Другое");
	
	Для каждого Наименование Из Список Цикл
		Элемент = Справочники.ns_GoodsSubType_ДляДомаИДачи.НайтиПоНаименованию(Наименование);
		Если НЕ ЗначениеЗаполнено(Элемент) Тогда
			Элемент = Справочники.ns_GoodsSubType_ДляДомаИДачи.СоздатьЭлемент();
			Элемент.Наименование = Наименование;
			Элемент.Записать();
		КонецЕсли; 
	КонецЦикла;
	
КонецФункции

Функция СправочникДляДомаИДачиСопоставлен() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ns_ДляДомаИДачи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ns_ДляДомаИДачи КАК ns_ДляДомаИДачи";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 
КонецФункции

// Область Выгрузка

Функция ПолучитьДанныеНоменклатуры(МагазинСтрока) Экспорт 
	Если ns_ОбщегоНазначенияПовтИсп.ПолучитьДобавлятьIDКатегории() Тогда
		Префикс = "home-" + Строка(МагазинСтрока.Магазин.Префикс) + "-";
	Иначе
		Префикс = Строка(МагазинСтрока.Магазин.Префикс);
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ДляДомаИДачи.Номенклатура КАК Номенклатура,
	|	ns_ДляДомаИДачи.Title КАК Title,
	|	ns_ДляДомаИДачи.AdStatus КАК AdStatus,
	|	ns_ДляДомаИДачи.AllowEmail КАК AllowEmail,
	|	ns_ДляДомаИДачи.AvitoId КАК AvitoId,
	|	ns_ДляДомаИДачи.Category КАК Category,
	|	ns_ДляДомаИДачи.GoodsType КАК GoodsType,
	|	ns_ДляДомаИДачи.DateBegin КАК DateBegin,
	|	ns_ДляДомаИДачи.DateEnd КАК DateEnd,
	|	&Префикс + ns_ДляДомаИДачи.Id КАК Id,
	|	ns_ДляДомаИДачи.ListingFee КАК ListingFee,
	|	ns_ДляДомаИДачи.nsDescription КАК nsDescription,
	|	ns_ДляДомаИДачи.VideoURL КАК VideoURL,
	|	ns_ДляДомаИДачи.Шаблон КАК Шаблон,
	|	0 КАК Price,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(17)) КАК ContactPhone,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(40)) КАК ManagerName,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(255)) КАК Address,
	|	ns_ДляДомаИДачи.Артикул КАК Артикул,
	|	ns_ДляДомаИДачи.Наименование КАК Наименование,
	|	ns_ДляДомаИДачи.Характеристика КАК Характеристика,
	|	ns_ДляДомаИДачи.Ссылка КАК Ссылка,
	|	ns_ДляДомаИДачи.Condition КАК Condition,
	|	ns_ДляДомаИДачи.GoodsSubType КАК GoodsSubType,
	|	ns_ДляДомаИДачи.Availability КАК Availability
	|ИЗ
	|	Справочник.ns_ДляДомаИДачи КАК ns_ДляДомаИДачи
	|ГДЕ
	|	ns_ДляДомаИДачи.ЭтоГруппа = ЛОЖЬ
	|	И ns_ДляДомаИДачи.Выгружать = ИСТИНА";
	Запрос.УстановитьПараметр("Префикс", Префикс);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
КонецФункции

// Область Выгрузка

//Область Сопоставление

Процедура Сопоставить(Ссылка, Характеристика, Родитель, ТаблицаСопоставления) Экспорт
	Изменен = Истина;
	
	СсылкаНаЭлемент = ns_Сопоставление.НайтиСопоставление(Ссылка, Характеристика, ТаблицаСопоставления);
	Если СсылкаНаЭлемент = Неопределено Тогда
		ОбъектЭлемент = Справочники.ns_ДляДомаИДачи.СоздатьЭлемент();
		ОбъектЭлемент.Номенклатура = Ссылка;
		Если ЗначениеЗаполнено(Характеристика) Тогда
			ОбъектЭлемент.Характеристика = Характеристика;
			Id = Строка(Ссылка.УникальныйИдентификатор()) + "#" + Строка(Характеристика.УникальныйИдентификатор());
		Иначе
			Id = Строка(Ссылка.УникальныйИдентификатор());
		КонецЕсли; 
		ОбъектЭлемент.Id = Id;
		ОбъектЭлемент.Выгружать = ns_ОбщегоНазначенияПовтИсп.ПолучитьДобавлятьВВыгрузку();
		ОбъектЭлемент.Condition = ns_ОбщегоНазначенияПовтИсп.ПолучитьConditionДляДомаИДачи();
		ОбъектЭлемент.AdType = ns_ОбщегоНазначенияПовтИсп.ПолучитьAdType();
		
		Изменен = Истина;
	Иначе
		ОбъектЭлемент = СсылкаНаЭлемент.ПолучитьОбъект();
	КонецЕсли; 
	
	ns_Сопоставление.ЗаполнитьTitle(ОбъектЭлемент);
	ns_Сопоставление.ЗаполнитьDescription(Ссылка, ОбъектЭлемент);
	
	ОбъектЭлемент.ПометкаУдаления = Ссылка.ПометкаУдаления;
	ОбъектЭлемент.Наименование = Ссылка.Наименование;
	ОбъектЭлемент.Артикул = Ссылка.Артикул;
	ОбъектЭлемент.Родитель = Справочники.ns_ДляДомаИДачи.ПолучитьСсылку(Родитель.УникальныйИдентификатор());
	
	Если Изменен Тогда
		ОбъектЭлемент.Записать();
	КонецЕсли; 
	
КонецПроцедуры

Процедура СопоставитьЭлементНоменклатуры(Ссылка) Экспорт
	ns_Сопоставление.СопоставитьЭлементНоменклатуры(Ссылка, "ns_ДляДомаИДачи");			
КонецПроцедуры
 
//КонецОбласти 













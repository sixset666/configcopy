Функция ВыгрузитьОбъявленияВXML(УникальныйИдентификатор = Неопределено, Обновление = Ложь) Экспорт
	
	ns_Авито.ПроверитьВерсиюМодуля();
	
	Замер = ns_ОбщегоНазначения.НачатьЗамер();
	
	ТаблицаМагазинов = Справочники.ns_Магазины.ПолучитьТаблицуМагазинов();
	Если ТаблицаМагазинов = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	ПапкаВыгрузкиАВИТО = ПолучитьПутьКХранилищу();
	Если ПапкаВыгрузкиАВИТО = Неопределено Тогда
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаОтменена, "ПапкаВыгрузкиАВИТО = Непределено");
		Возврат Неопределено;
	КонецЕсли; 
	//ОчиститьПапкуВыгрузки(ПапкаВыгрузкиАВИТО, ТаблицаМагазинов);
	
	ОбщаяТаблицаОшибок = ns_АвитоОбработкаОшибок.СформироватьСтруктуруТаблицыОшибок();
	
	Для каждого Магазин Из ТаблицаМагазинов Цикл
		ТаблицаОшибок = ns_АвитоОбработкаОшибок.СформироватьСтруктуруТаблицыОшибок();
		СформироватьДанныеОбъявлений(ПапкаВыгрузкиАВИТО, Магазин, ТаблицаОшибок);
		Для каждого Строка Из ТаблицаОшибок Цикл
			НовСтр = ОбщаяТаблицаОшибок.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, Строка);
			НовСтр.Магазин = Магазин.Магазин;
		КонецЦикла; 
	КонецЦикла; 
	
	ns_ОбщегоНазначения.СообщениеПользователю("Время выгрузки: " + ns_ОбщегоНазначения.ЗакончитьЗамер(Замер));
	
	Возврат РегистрыСведений.ns_ЖурналРезультатов.ЗаписатьОшибкиВЖурнал(ОбщаяТаблицаОшибок);	
КонецФункции

Функция СформироватьДанныеОбъявлений(ПапкаВыгрузкиАВИТО, Магазин, ТаблицаОшибок)
	
	ДанныеНоменклатуры = СформироватьДанныеНоменклатуры(Магазин);
	Если ДанныеНоменклатуры.Количество() = 0 Тогда
		Возврат ВозвратСОшибкой("Нет товаров отмеченных к выгрузке");	
	КонецЕсли;
	
	Номенклатура = ДанныеНоменклатуры.ВыгрузитьКолонку("Номенклатура");
	
	Цены = ns_Ценообразование.ПолучитьТаблицуЦенНоменклатуры(Номенклатура);
	
	Картинки = ПолучитьДанныеКартинок(ДанныеНоменклатуры, ПапкаВыгрузкиАВИТО, Магазин);
	
	Остатки = ns_Переопределяемый.ПолучитьТаблицуОстатков(Номенклатура);

	ДанныеДляШаблонизатора = ns_ШаблонизаторСервер.ПолучитьДанныеДляШаблонизатора(Остатки, Номенклатура);

	Попытка
		ИмяФайлаОбмена = ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки + "\" + "avito.xml";
		ПапкаВыгрузкиМагазина = ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки;
		
		КаталогНаДиске = Новый Файл(ПапкаВыгрузкиМагазина);
		Если НЕ КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ПапкаВыгрузкиМагазина);
		КонецЕсли;
	Исключение
		Возврат ВозвратСОшибкой("Ошибка создания папки выгрузки" + Символы.ПС + ОписаниеОшибки());
	КонецПопытки; 
	
	Попытка
		ПараметрыЗаписиВXML = Новый ПараметрыЗаписиXML("UTF-8", "1.0", Истина); 
		ДанныеXML = Новый ЗаписьXML;
		ДанныеXML.УстановитьСтроку(ПараметрыЗаписиВXML);
		
		ДанныеXML.ЗаписатьНачалоЭлемента("Ads");
		
		ДанныеXML.ЗаписатьСоответствиеПространстваИмен("", "https://nizamov.studio/avito");
		ДанныеXML.ЗаписатьСоответствиеПространстваИмен("xs", "http://www.w3.org/2001/XMLSchema");
		ДанныеXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
		
		ДанныеXML.ЗаписатьАтрибут("formatVersion", "3");
		ДанныеXML.ЗаписатьАтрибут("target", "Avito.ru");
		
		// Выгружаем в XML
		ЗаписатьДанныеПоКатегрииВXML(ДанныеXML, ДанныеНоменклатуры, Цены, Картинки, ТаблицаОшибок, Магазин, ДанныеДляШаблонизатора);
		
		ДанныеXML.ЗаписатьКонецЭлемента(); // Ads
		ДанныеИтогXML = ДанныеXML.Закрыть();
		
		ДанныеИтогXML = СтрЗаменить(ДанныеИтогXML, "<Ads xmlns=""https://nizamov.studio/avito"" xmlns:xs=""http://www.w3.org/2001/XMLSchema"" xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" formatVersion=""3"" target=""Avito.ru""", "<Ads formatVersion=""3"" target=""Avito.ru""");
		ДанныеИтогXML = СтрЗаменить(ДанныеИтогXML, "<Ad xmlns=""https://nizamov.studio/avito"">", "<Ad>");
		ДанныеИтогXML = СтрЗаменить(ДанныеИтогXML, "&lt;", "<");
		ДанныеИтогXML = СтрЗаменить(ДанныеИтогXML, "&gt;", ">");
		ДанныеИтогXML = СтрЗаменить(ДанныеИтогXML, "amp;", "");
		
	Исключение
		Возврат ВозвратСОшибкой("Ошибка формирования xml файла магазина: " + Магазин.Наименование + Символы.ПС + ОписаниеОшибки());
	КонецПопытки; 
	
	Попытка
		ДанныеXML = Новый ЗаписьXML;
		ДанныеXML.ОткрытьФайл(ИмяФайлаОбмена, "UTF-8", Ложь);
		ДанныеXML.ЗаписатьБезОбработки(ДанныеИтогXML);
		ДанныеXML.Закрыть();
		ns_ОбщегоНазначения.СообщениеПользователю("Данные по магазину: " + Магазин.Наименование + " Выгружены: " + ИмяФайлаОбмена);
	Исключение
		Возврат ВозвратСОшибкой("Ошибка записи в xml файл магазина: " + Магазин.Наименование + Символы.ПС + ОписаниеОшибки());
	КонецПопытки;

	Если ns_ОбщегоНазначенияПовтИсп.ПолучитьВидВыгрузки() = Перечисления.ns_ВидыВыгрузки.FTP Тогда
		ВыгрузитьДанныеНаFTP(ПапкаВыгрузкиАВИТО, Магазин);
	ИначеЕсли ns_ОбщегоНазначенияПовтИсп.ПолучитьВидВыгрузки() = Перечисления.ns_ВидыВыгрузки.FileHosting Тогда
		ВыгрузитьДанныеВПапкуВыгрузкиSQLВерсия(ПапкаВыгрузкиАВИТО, Магазин);
	КонецЕсли; 
	
	Возврат "";
КонецФункции

Функция ВозвратСОшибкой(ТекстОшибки)
	РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаОтменена, ТекстОшибки);
	ns_ОбщегоНазначения.СообщениеПользователю(ТекстОшибки);
	Возврат Неопределено;
КонецФункции

Функция СформироватьДанныеНоменклатуры(Магазин)
	ДанныеНоменклатуры = ns_Авито.ПолучитьТаблицуДляВыгрузки();		
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ЗапчастиИАксессуары") = Неопределено Тогда
		ДанныеЗапчасти = ns_ЗапчастиИАксессуары.ПолучитьДанныеНоменклатуры(Магазин);
		Для Каждого Строка Из ДанныеЗапчасти Цикл 
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры.Добавить(), Строка); 
		КонецЦикла
	КонецЕсли; 
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.БытоваяЭлектроника") = Неопределено Тогда
		ДанныеБытоваяЭлектроника = ns_БытоваяЭлектроника.ПолучитьДанныеНоменклатуры(Магазин);
		Для Каждого Строка Из ДанныеБытоваяЭлектроника Цикл                 
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры.Добавить(), Строка); 
		КонецЦикла
	КонецЕсли; 
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ДляДомаИДачи") = Неопределено Тогда
		ДанныеДляДомаИДачиа = ns_ДляДомаИДачи.ПолучитьДанныеНоменклатуры(Магазин);
		Для Каждого Строка Из ДанныеДляДомаИДачиа Цикл                 
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры.Добавить(), Строка); 
		КонецЦикла
	КонецЕсли; 

	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ДляБизнеса") = Неопределено Тогда
		ДанныеДляБизнеса = ns_ДляБизнеса.ПолучитьДанныеНоменклатуры(Магазин);
		Для Каждого Строка Из ДанныеДляБизнеса Цикл                 
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры.Добавить(), Строка); 
		КонецЦикла
	КонецЕсли; 
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ХоббиИОтдых") = Неопределено Тогда
		ДанныеХоббиИОтдых = ns_ХоббиИОтдых.ПолучитьДанныеНоменклатуры(Магазин);
		Для Каждого Строка Из ДанныеХоббиИОтдых Цикл                 
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры.Добавить(), Строка); 
		КонецЦикла
	КонецЕсли; 

	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.МотоциклыИМототехника") = Неопределено Тогда
		ДанныеМотоциклыИМототехника = ns_МотоциклыИМототехника.ПолучитьДанныеНоменклатуры(Магазин);
		Для Каждого Строка Из ДанныеМотоциклыИМототехника Цикл                 
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры.Добавить(), Строка); 
		КонецЦикла
	КонецЕсли; 
	
	Если НЕ Метаданные.НайтиПоПолномуИмени("Подсистема.Авито.Подсистема.Категории.Подсистема.ЛичныеВещи") = Неопределено Тогда
		ДанныеЛичныеВещи = ns_ЛичныеВещи.ПолучитьДанныеНоменклатуры(Магазин);
		Для Каждого Строка Из ДанныеЛичныеВещи Цикл                 
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры.Добавить(), Строка); 
		КонецЦикла
	КонецЕсли; 
	
	Возврат ДанныеНоменклатуры;
КонецФункции

Процедура ЗаписатьДанныеПоКатегрииВXML(ДанныеXML, ДанныеНоменклатуры, Цены, Картинки, ТаблицаОшибок, Магазин, ДанныеДляШаблонизатора)
	
	ШаблонПоУмолчанию = ns_ОбщегоНазначенияПовтИсп.ПолучитьШаблонПоУмолчанию();
	ЗонаПоказаПоУмолчанию = ns_ОбщегоНазначенияПовтИсп.ПолучитьЗонуПоказаПоУмолчанию();
	
	Счетчик = 0;
	ns_ОбщегоНазначения.СообщениеПользователю("По магазину """ + Магазин.Наименование + """ подготовлено на выгрузку: " + ДанныеНоменклатуры.Количество()); 
	Для каждого Строка Из ДанныеНоменклатуры Цикл
		Если ns_АвитоОбработкаОшибок.ЕстьОшибкиВыгрузки(Строка, ДанныеДляШаблонизатора.Остатки, Картинки, ТаблицаОшибок) Тогда
			Продолжить;		
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Строка.ЗонаПоказаОбъявления) Тогда
			ЗонаПоказа = Строка.ЗонаПоказаОбъявления;
		ИначеЕсли ЗначениеЗаполнено(ЗонаПоказаПоУмолчанию) Тогда
			ЗонаПоказа = ЗонаПоказаПоУмолчанию;
		Иначе
			ЗонаПоказа = Неопределено;
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Строка.Шаблон) Тогда
			Шаблон = Строка.Шаблон;
		ИначеЕсли ЗначениеЗаполнено(Магазин.Шаблон) Тогда 	
			Шаблон = Магазин.Шаблон;
		ИначеЕсли ЗначениеЗаполнено(ШаблонПоУмолчанию) Тогда 	
			Шаблон = ШаблонПоУмолчанию;
		Иначе
			ВызватьИсключение "Не задан шаблон выгрузки объявления";
		КонецЕсли;
		
		Если Шаблон.СписокАдресов.Количество() = 0 Тогда
			ВызватьИсключение "В шаблоне не указан адрес компании";
		КонецЕсли;
		
		Для каждого СтрокаАдреса Из Шаблон.СписокАдресов Цикл
			ТипAd = ФабрикаXDTO.Тип("https://nizamov.studio/avito", "Ad");
			Объявление = ФабрикаXDTO.Создать(ТипAd); 
			
			ns_ШаблонизаторСервер.ЗаполнитьДанныеИзШаблона(Строка, Шаблон, Магазин, ДанныеДляШаблонизатора, СтрокаАдреса.Адрес);
			ЗаполнитьЦену(Строка, Цены);
			
			ns_Авито.ЗаполнитьЗначенияСвойствXDTO(Объявление, Строка, ДанныеНоменклатуры.Колонки);
			Если ЗначениеЗаполнено(СтрокаАдреса.Префикс) Тогда
				Объявление.Id = СтрокаАдреса.Префикс + "-" + Строка.Id;
			КонецЕсли;
			
			Images = СохранитьКартинкиНаДиске(Строка, Картинки);
			Если НЕ Images = Неопределено И Images.Количество() > 0 Тогда
				ns_Авито.Добавить_Images(Объявление, Images);
			КонецЕсли; 
			
			ЗаполнитьЗоныПоказа(Объявление, Строка, ЗонаПоказа);
			
			ФабрикаXDTO.ЗаписатьXML(ДанныеXML, Объявление);
		КонецЦикла;
		
		Счетчик = Счетчик + 1;
	КонецЦикла; 
	
	ns_ОбщегоНазначения.СообщениеПользователю("Сформировано без ошибок: " + Счетчик); 

КонецПроцедуры

Процедура ЗаполнитьЗоныПоказа(Объявление, Строка, ЗонаПоказа)
	
	Если НЕ ЗначениеЗаполнено(ЗонаПоказа) Тогда
		Возврат;
	КонецЕсли; 
	
	Если Строка.Category = ns_ЗапчастиИАксессуарыПовтИсп.ПолучитьЗапчастиИАксессуары() Тогда
		ns_Авито.Добавить_DisplayAreas(Объявление, ЗонаПоказа);
	КонецЕсли; 
	
КонецПроцедуры
 
Процедура ЗаполнитьЦену(Строка, Цены)
	Если ЗначениеЗаполнено(Строка.Характеристика) Тогда
		Идентификатор = Строка(Строка.Номенклатура.УникальныйИдентификатор()) + "#" + Строка(Строка.Характеристика.УникальныйИдентификатор());
	Иначе
		Идентификатор = Строка(Строка.Номенклатура.УникальныйИдентификатор());
	КонецЕсли; 
	Цена = Цены[Идентификатор];
	Если ЗначениеЗаполнено(Цена) Тогда
		Строка.Price = Цена;
	КонецЕсли; 
КонецПроцедуры
 
Функция ПолучитьПутьКХранилищу()
	Если ЭтоФайловаяБаза() Тогда
		Возврат ns_ОбщегоНазначенияПовтИсп.ПолучитьПутьДляВыгрузки() + "\";
	Иначе
		Возврат КаталогВременныхФайлов();
	КонецЕсли; 
КонецФункции

Функция ЭтоФайловаяБаза() Экспорт
	Возврат Найти(СтрокаСоединенияИнформационнойБазы(), "File") > 0;
КонецФункции

Процедура ВыгрузитьДанныеВПапкуВыгрузкиSQLВерсия(ПапкаВыгрузкиАВИТО, Магазин)
	
	Если ЭтоФайловаяБаза() Тогда
		Возврат;
	КонецЕсли; 
	
	Попытка
		ПутьДляВыгрузкиHosting = ns_ОбщегоНазначенияПовтИсп.ПолучитьПутьДляВыгрузки() + "\" + Магазин.ПапкаВыгрузки + "\";
		
		КаталогНаДиске = Новый Файл(ПутьДляВыгрузкиHosting);
		Если НЕ КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ПутьДляВыгрузкиHosting);
		КонецЕсли;
		
		ПапкаВыгрузкиМагазина = ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки;
		
		СписокФайлов = НайтиФайлы(ПапкаВыгрузкиМагазина, "*.*");
		Для Каждого ТекущийФайл из СписокФайлов Цикл
			Если ТекущийФайл.ЭтоКаталог() Тогда
				Продолжить;
			КонецЕсли;
			
			КопироватьФайл(ТекущийФайл.ПолноеИмя, ПутьДляВыгрузкиHosting + ТекущийФайл.Имя);
		КонецЦикла;
		
		ns_ОбщегоНазначения.СообщениеПользователю("Данные по магазину: " + Магазин + " Выгружены в папку хостинга: " + ПутьДляВыгрузкиHosting);
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаОтменена, ОписаниеОшибки());
		ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки; 

КонецПроцедуры
 
Процедура ОчиститьПапкуВыгрузки(ПапкаВыгрузкиАВИТО, ТаблицаМагазинов)
	Для каждого Магазин Из ТаблицаМагазинов Цикл
		ПапкаВыгрузкиМагазина = ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки;
		КаталогНаДиске = Новый Файл(ПапкаВыгрузкиМагазина);
		Если КаталогНаДиске.Существует() Тогда
			Попытка
				УдалитьФайлы(ПапкаВыгрузкиМагазина, "*.jpg");	
			Исключение                          
				ТекстОшибки = "Ошибка очистки каталога выгрузки. " + ОписаниеОшибки();
				РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ОшибкаРаботыМодуля, ТекстОшибки);
				ns_ОбщегоНазначения.СообщениеПользователю(ТекстОшибки);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//Область Изображения

Функция ПолучитьДанныеКартинок(ДанныеОбъявлений, ПапкаВыгрузкиАВИТО, Магазин)
	ДанныеФото = Новый Соответствие;
	
	ШаблонПоУмолчанию = ns_ОбщегоНазначенияПовтИсп.ПолучитьШаблонПоУмолчанию();

	ХранилищеСхема = Магазин.СхемаПроезда.Схема;

	Для каждого Объявление Из ДанныеОбъявлений Цикл
		Если НЕ ЗначениеЗаполнено(Объявление.Category) Тогда
			Продолжить;
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Объявление.Шаблон) Тогда
			Шаблон = Объявление.Шаблон;
		ИначеЕсли ЗначениеЗаполнено(Магазин.Шаблон) Тогда 	
			Шаблон = Магазин.Шаблон;
		ИначеЕсли ЗначениеЗаполнено(ШаблонПоУмолчанию) Тогда 	
			Шаблон = ШаблонПоУмолчанию;
		КонецЕсли;
		
		МассивФото = Новый Массив;
		
		ТаблицаФото = Справочники.ns_ПрисоединенныеФайлы.ИнициализироватьТаблицуФото();
		// Первые идут фото из модуля
		Справочники.ns_ПрисоединенныеФайлы.ПолучитьФото_Хранилище(Объявление.Ссылка, ТаблицаФото);
		// Потом из номенклатуры
		ns_Переопределяемый.ДобавитьКартинкиИзНоменклатуры(Объявление, ТаблицаФото);
		// Если помещаются то общие из шаблона
		Если НЕ Шаблон = Неопределено Тогда
			Справочники.ns_ПрисоединенныеФайлы.ПолучитьФото_Хранилище(Шаблон, ТаблицаФото);
		КонецЕсли; 
		
		МаксимальноеКоличествоФото = Объявление.Category.МаксимальноеКоличествоФото;
		Для каждого Строка Из ТаблицаФото Цикл                                   
			Если МассивФото.Количество() >= МаксимальноеКоличествоФото Тогда
				Прервать;
			КонецЕсли; 
			
			СтруктураИзображения = ЗаполнитьСтруктуруИзображения(Строка.Хранилище, СформироватьНаименованиеФото(Строка), ПапкаВыгрузкиАВИТО, Магазин, Строка.ПутьНаДиске);	
			МассивФото.Добавить(СтруктураИзображения);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ХранилищеСхема.Получить()) Тогда
			СтруктураИзображения = ЗаполнитьСтруктуруИзображения(ХранилищеСхема, СформироватьНаименованиеФото(Объявление.Номенклатура), ПапкаВыгрузкиАВИТО, Магазин);
			// Если нет фото, но в магазине указана схема, то добавляем ее в выгрузку
			Если ТаблицаФото.Количество() = 0 Тогда
				МассивФото.Добавить(СтруктураИзображения);
			Иначе	
				// Иначе схему (при наличии), добавляем в качестве последнего фото
				Если МассивФото.Количество() >= МаксимальноеКоличествоФото - 1 Тогда
					МассивФото.Вставить(МаксимальноеКоличествоФото - 1, СтруктураИзображения);
				Иначе
					МассивФото.Добавить(СтруктураИзображения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
			
		ДанныеФото.Вставить(Объявление.Ссылка, МассивФото);
	КонецЦикла; 
	
	Возврат ДанныеФото;
КонецФункции
 
Функция СформироватьНаименованиеФото(Данные)
	Если ТипЗнч(Данные) = Тип("СтрокаТаблицыЗначений") Тогда
		Если ТипЗнч(Данные.Ссылка) = Тип("Строка") Тогда
			ИмяФайла = Данные.Ссылка;	
		Иначе
			ИмяФайла = Данные.Ссылка.УникальныйИдентификатор();	
		КонецЕсли; 
		ИмяФайла = СтрЗаменить(ИмяФайла, "-", "_");
		ИмяФайла = ИмяФайла + ?(Найти(Данные.Расширение, ".") > 0, Данные.Расширение, "." + НРег(Данные.Расширение));
	ИначеЕсли ТипЗнч(Данные) = Тип("СправочникСсылка.Номенклатура") Тогда 	
		ИмяФайла = Строка(Данные.УникальныйИдентификатор());	
		ИмяФайла = СтрЗаменить(ИмяФайла, "-", "_");
		ИмяФайла = ИмяФайла + ".jpg";
	КонецЕсли; 
	Возврат ИмяФайла;
КонецФункции

Функция ЗаполнитьСтруктуруИзображения(Хранилище, Наименование, ПапкаВыгрузкиАВИТО, Магазин, ПутьНаДиске = Неопределено, Счетчик = Неопределено)
	Домен = ns_ОбщегоНазначенияПовтИсп.ПолучитьИмяДомена();
	ВидВыгрузки = ns_ОбщегоНазначенияПовтИсп.ПолучитьВидВыгрузки();
	
	СтруктураИзображения = Новый Структура;
	СтруктураИзображения.Вставить("ИмяФайла", Наименование);
	СтруктураИзображения.Вставить("ЭтоСсылка", Ложь);
	СтруктураИзображения.Вставить("ПутьНаДиске", ПутьНаДиске);
	СтруктураИзображения.Вставить("Счетчик", Счетчик);
	
	Если ВидВыгрузки = Перечисления.ns_ВидыВыгрузки.File Тогда
		СтруктураИзображения.Вставить("UrlФайла", Наименование);
		СтруктураИзображения.Вставить("Url", Ложь);
		СтруктураИзображения.Вставить("ПолныйПуть", ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки + "\");
	ИначеЕсли ВидВыгрузки = Перечисления.ns_ВидыВыгрузки.FileHosting Тогда 	
		СтруктураИзображения.Вставить("UrlФайла", ?(ЗначениеЗаполнено(Домен), Домен + "/" + Магазин.ПапкаВыгрузки + "/" + Наименование, Наименование));
		СтруктураИзображения.Вставить("Url", ?(ЗначениеЗаполнено(Домен), Истина, Ложь));
		СтруктураИзображения.Вставить("ПолныйПуть", ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки + "\");
	Иначе
		СтруктураИзображения.Вставить("UrlФайла", ?(ЗначениеЗаполнено(Домен), Домен + "/" + Магазин.ПапкаВыгрузки + "/" + Наименование, Наименование));
		СтруктураИзображения.Вставить("Url", ?(ЗначениеЗаполнено(Домен), Истина, Ложь));
		СтруктураИзображения.Вставить("ПолныйПуть", ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки + "\");
	КонецЕсли; 
	
	Если ns_ОбщегоНазначенияПовтИсп.ПолучитьВыгружатьКартинкиБезОптимизации() Тогда
		СтруктураИзображения.Вставить("Изображение", Неопределено);
		СохранитьКартинкуНаДиске(СтруктураИзображения, Хранилище);
	Иначе	
		СтруктураИзображения.Вставить("Изображение", Хранилище);
	КонецЕсли;
	
	Возврат СтруктураИзображения;
КонецФункции

Функция СохранитьКартинкиНаДиске(Строка, Картинки)
	
	МассивКартинок = Картинки[Строка.Ссылка];
	Если ns_ОбщегоНазначенияПовтИсп.ПолучитьВыгружатьКартинкиБезОптимизации() Тогда
		Возврат МассивКартинок;
	КонецЕсли;
	
	Если НЕ МассивКартинок = Неопределено Тогда
		Для каждого Картинка Из МассивКартинок Цикл
			СохранитьКартинкуНаДиске(Картинка);
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат МассивКартинок;
КонецФункции

Процедура СохранитьКартинкуНаДиске(Картинка, Хранилище = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(Картинка.ПолныйПуть) ИЛИ НЕ ЗначениеЗаполнено(Картинка.ИмяФайла) Тогда
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНеПолная, "Не удалось выгрузить картинку");
		Возврат;
	КонецЕсли;
	
	ПолныйПуть = Новый Файл(Картинка.ПолныйПуть);
	Если НЕ ПолныйПуть.Существует() Тогда
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНеПолная, "Не могу определить путь для выгрузки картинки");
		Возврат;
	КонецЕсли; 
	
	ПолныйПутьСИменемФайла = Картинка.ПолныйПуть + Картинка.ИмяФайла;
	
	// Не перезаписываем картинку если она уже есть на диске
	ПутьСФайлом = Новый Файл(ПолныйПутьСИменемФайла);
	Если ПутьСФайлом.Существует() Тогда
		Возврат;
	КонецЕсли; 
	
	Попытка
		Если НЕ ЗначениеЗаполнено(Картинка.ПутьНаДиске) Тогда
			Если Хранилище = Неопределено Тогда
				ДвоичныеДанные = Картинка.Изображение.Получить();
			Иначе
				ДвоичныеДанные = Хранилище.Получить();
			КонецЕсли;
		Иначе
			ДвоичныеДанные = Новый ДвоичныеДанные(Картинка.ПутьНаДиске);
		КонецЕсли;

		Если НЕ ДвоичныеДанные = Неопределено Тогда
			ДвоичныеДанные.Записать(ПолныйПутьСИменемФайла);
		КонецЕсли;
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНеПолная, "Не удалось выгрузить картинку. " + ОписаниеОшибки());
	КонецПопытки; 
	
КонецПроцедуры

//КонецОбласти 

//Область Регламентные

Процедура ns_ВыгрузкаАвитоПолная() Экспорт
	ВыгрузитьОбъявленияВXML();
КонецПроцедуры

Процедура ns_ВыгрузкаАвитоОбновление() Экспорт
	ВыгрузитьОбъявленияВXML(, Истина);
КонецПроцедуры

//КонецОбласти 

//Область FTP

Процедура ВыгрузитьДанныеНаFTP(ПапкаВыгрузкиАВИТО, Магазин)
	
	Попытка
		
		ПапкаВыгрузкиМагазина = ПапкаВыгрузкиАВИТО + Магазин.ПапкаВыгрузки;
		
		FTPСервер = ns_ОбщегоНазначенияПовтИсп.ПолучитьFTPIPАдрес();
		FTPLogin = ns_ОбщегоНазначенияПовтИсп.ПолучитьFTPLogin();
		FTPPass = ns_ОбщегоНазначенияПовтИсп.ПолучитьFTPPassword();
		FTPКаталог = Магазин.ПапкаВыгрузки;
		FTPПорт = ns_ОбщегоНазначенияПовтИсп.ПолучитьFTPПорт();
		FTPПассивныйРежим = ns_ОбщегоНазначенияПовтИсп.ПолучитьFTPПассивныйРежим();
		
		Если НЕ ЗначениеЗаполнено(FTPСервер) И НЕ ЗначениеЗаполнено(FTPLogin) И НЕ ЗначениеЗаполнено(FTPPass) И НЕ ЗначениеЗаполнено(FTPКаталог) И НЕ ЗначениеЗаполнено(FTPПорт) Тогда
			РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНаFTP, "Не заполнены данные для подключения к FTP.");
			Возврат;
		КонецЕсли; 
		
		Таймаут = 60 * 30;
		FTP = Новый FTPСоединение(FTPСервер, FTPПорт, FTPLogin, FTPPass,, FTPПассивныйРежим, Таймаут);
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНаFTP, "Ошибка соединения FTP. " + ОписаниеОшибки());
		Возврат;
	КонецПопытки; 
	
	Попытка
		FTP.УстановитьТекущийКаталог(FTPКаталог);
		ns_ОбщегоНазначения.СообщениеПользователю("Каталог на FTP: " + FTP.ТекущийКаталог());
	Исключение
		Попытка
			FTP.СоздатьКаталог(FTPКаталог);
			FTP.УстановитьТекущийКаталог(FTPКаталог);
			ns_ОбщегоНазначения.СообщениеПользователю("Каталог на FTP: " + FTP.ТекущийКаталог());
		Исключение
			РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНаFTP, "Ошибка установки каталога на FTP. " + ОписаниеОшибки());
			Возврат;
		КонецПопытки; 
	КонецПопытки; 
	
	Попытка
		СписокФайловНаFTP = FTP.НайтиФайлы("/" + FTPКаталог, "*.*");
		ТаблицаФайловНаFTP = Новый ТаблицаЗначений;
		ТаблицаФайловНаFTP.Колонки.Добавить("Имя");
		Для каждого Строка Из СписокФайловНаFTP Цикл
			Если Строка.Имя = "avito.xml" Тогда
				Продолжить;
			КонецЕсли; 
			НовСтр = ТаблицаФайловНаFTP.Добавить();	
			НовСтр.Имя = Строка.Имя;
		КонецЦикла; 
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНаFTP, "Ошибка получения списка файлов с FTP. " + ОписаниеОшибки());
		Возврат;
	КонецПопытки; 

	Попытка
		ФайлыДляВыгрузки = Новый ТаблицаЗначений;
		ФайлыДляВыгрузки.Колонки.Добавить("Имя");
		ФайлыДляВыгрузки.Колонки.Добавить("ПолноеИмя");
		
		Файлы = НайтиФайлы(ПапкаВыгрузкиМагазина, "*.*");
		
		Для Каждого ФФ из Файлы Цикл
			Если ФФ.ЭтоКаталог() Тогда
				Продолжить;
			КонецЕсли;
			НовСтрока = ФайлыДляВыгрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ФФ);
		КонецЦикла;
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНаFTP, "Ошибка формирования списка файлов для выгрузки. " + ОписаниеОшибки());
		Возврат;
	КонецПопытки; 

	// Выгружаем XML
	Попытка
		ПараметрыОтбора = Новый Структура("Имя", "avito.xml");
		НайденнаяСтрока = ФайлыДляВыгрузки.НайтиСтроки(ПараметрыОтбора);
		Если НайденнаяСтрока.Количество() > 0 Тогда
			Строка = НайденнаяСтрока[0];
			ФайлНаДиске = Новый Файл(Строка.ПолноеИмя);
			Если ФайлНаДиске.Существует() Тогда
				FTP.Записать(Строка.ПолноеИмя, Строка.Имя);
			КонецЕсли; 
		КонецЕсли;                   
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНаFTP, "Ошибка загрузки avito.xml на FTP. " + ОписаниеОшибки());
		Возврат;
	КонецПопытки; 

	// Выгружаем фотографии
	Попытка
		Для каждого Строка Из ФайлыДляВыгрузки Цикл
			Если Строка.Имя = "avito.xml" Тогда
				Продолжить;
			КонецЕсли; 
			ФайлНаДиске = Новый Файл(Строка.ПолноеИмя);
			Если ФайлНаДиске.Существует() Тогда
				НайденныйФайлНаFTP = ТаблицаФайловНаFTP.Найти(Строка.Имя, "Имя");
				Если НайденныйФайлНаFTP = Неопределено Тогда
					FTP.Записать(Строка.ПолноеИмя, Строка.Имя);
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла; 	
	Исключение
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаНаFTP, "Ошибка загрузки файлов на FTP. " + ОписаниеОшибки());
		Возврат;
	КонецПопытки; 
	
КонецПроцедуры
 
//КонецОбласти 














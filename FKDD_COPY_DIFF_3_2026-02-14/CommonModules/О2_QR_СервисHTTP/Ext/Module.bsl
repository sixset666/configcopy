
#Область ОшибкиAPI   
Функция ПолучитьОписаниеОшибки(КодОшибки, ДопПараметр = Неопределено)

	Соответствияошибок = новый Соответствие;
	
	//Ошибки API
	Соответствияошибок.Вставить("ERR_SERVER","Ошибка соединения. Возможно указан некорректный путь или метод запроса.");
	Соответствияошибок.Вставить("ERR_JSON",	"Не удалось обработать тело запроса как JSON.");
	
	//Смена статуса оплаты
	Соответствияошибок.Вставить("ERR_DATA_INVALID", "Не заполнены обязательные параметры для изменения статуса оплаты. Проверьте что параметры order_id, status - заполнены.");
	Соответствияошибок.Вставить("ERR_ORDERID_DATA_INVALID", "order_id имеет некорректный формат. Должно быть число.");
	Соответствияошибок.Вставить("ERR_STATUS_DATA_INVALID", "status имеет некорректный формат. Должна быть строка.");
	Соответствияошибок.Вставить("ERR_ORDERID_NOT_FOUND", "Не найден существующий платеж.");
	Соответствияошибок.Вставить("ERR_STATUS_NOT_FOUND", "Не найден статус оплаты. Возможные значения - Paid, Fiscalized, Cancelled");
	Соответствияошибок.Вставить("ERR_STATUS_INVALID", "Невозможно установить статус. Текущий статус платежа - " + ДопПараметр);
	Соответствияошибок.Вставить("ERR_STATUS_UPDATE_FAILED", "Ошибка при установке статуса платежа. Ошибка - " + ДопПараметр);
	Соответствияошибок.Вставить("ERR_RECEIPT_DATA_INVALID", "Данные чека не заполнены или заполнены некорректно.");
	
	Если ЗначениеЗаполнено(Соответствияошибок.Получить(КодОшибки)) Тогда 
		Возврат Соответствияошибок.Получить(КодОшибки);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции
Функция ВернутьОшибку(КодОшибки, ДопПараметр = Неопределено)
	
	Ответ = Новый HTTPСервисОтвет(400);
	
	//Структура ошибки
	СтруктураОтвета = новый Структура;
	МассивОшибок = Новый Массив;
	СтруктураОшибки = Новый Структура;
	
	СтруктураОшибки.Вставить("code",КодОшибки);
	СтруктураОшибки.Вставить("text",ПолучитьОписаниеОшибки(КодОшибки,ДопПараметр));
	
	МассивОшибок.Добавить(СтруктураОшибки);
	
	СтруктураОтвета.Вставить("errors", МассивОшибок);
	
	//Json
	ЗаписьJSON = Новый ЗаписьJSON;			
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON,СтруктураОтвета,);
	ТелоОтвета = ЗаписьJSON.Закрыть();

	Ответ.Заголовки.Вставить("Content-Type","application/json;charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
	Возврат Ответ;
	
КонецФункции
#КонецОбласти

#Область ЛогированиеЗапросов
Функция ЛогированиеЗапросовHTTPСервисы(Период,Запрос,Ответ=Неопределено,Документ=Неопределено,Ошибка=Неопределено)
		
	Набор = РегистрыСведений.О2_QR_ЛогиЗапросов.СоздатьНаборЗаписей();
	Если ЗначениеЗаполнено(Период) Тогда
		Набор.Отбор.Период.Установить(Период);
	Иначе
		Набор.Отбор.Период.Установить(ТекущаяДатаСеанса());
	КонецЕсли;
	
	//Запрос
	ТекстЗаголовкиЗапроса = "";
	Для Каждого Заголовок Из Запрос.Заголовки Цикл
		ТекстЗаголовкиЗапроса = ТекстЗаголовкиЗапроса + Заголовок.Ключ + ": " + Заголовок.Значение + Символы.ПС;
	КонецЦикла;
	
	Набор.Отбор.Метод.Установить(Запрос.HTTPМетод + " " + Запрос.БазовыйURL + Запрос.ОтносительныйURL);
	Набор.Прочитать();
	
	Если Набор.Количество() = 0 тогда
		НоваяЗапись = Набор.Добавить();
	Иначе
		НоваяЗапись = Набор[0];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Период) Тогда
		НоваяЗапись.Период = Период;
	Иначе
		НоваяЗапись.Период = ТекущаяДатаСеанса();
	КонецЕсли;

	НоваяЗапись.Метод = Запрос.HTTPМетод + " " + Запрос.БазовыйURL + Запрос.ОтносительныйURL;
	НоваяЗапись.ЗаголовкиЗапроса = ТекстЗаголовкиЗапроса;
	НоваяЗапись.ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку("UTF-8");

	
	//Ответ
	Если ТипЗнч(Ответ) = Тип("HTTPСервисОтвет") Тогда  
		ТекстЗаголовкиОтвета = "";
		Для Каждого Заголовок Из Ответ.Заголовки Цикл
			ТекстЗаголовкиОтвета = ТекстЗаголовкиОтвета + Заголовок.Ключ + ": " + Заголовок.Значение + Символы.ПС;
		КонецЦикла;
		НоваяЗапись.ЗаголовкиОтвета = ТекстЗаголовкиОтвета;
		НоваяЗапись.КодОтвета = Ответ.КодСостояния;
		НоваяЗапись.ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;
		
	
	//Прочая информация
	НоваяЗапись.ОписаниеОшибки = Ошибка;
	НоваяЗапись.Документ = Документ;
	
	Набор.Записать();
КонецФункции
функция ВЖурнал(Значение) Экспорт
	ЗаписьЖурналаРегистрации("О2 API",УровеньЖурналаРегистрации.Информация,Значение,Строка(Значение),Строка(Значение));
КонецФункции
Функция ЛогированиеЗапросов(Хост,Метод,ПолнаяСсылка,ЗаголовкиЗапроса,ТелоЗапроса,КодОтвета,ЗаголовкиОтвета,ТелоОтвета,Документ)
	//Подготовка заголовков
	//Запроса  
	ТекстЗаголовкиЗапроса = "";
	Для Каждого Заголовок Из ЗаголовкиЗапроса Цикл
		ТекстЗаголовкиЗапроса = ТекстЗаголовкиЗапроса + Заголовок.Ключ + ": " + Заголовок.Значение + Символы.ПС;
	КонецЦикла;
	//Ответа  
	ТекстЗаголовкиОтвета = "";
	Для Каждого Заголовок Из ЗаголовкиОтвета Цикл
		ТекстЗаголовкиОтвета = ТекстЗаголовкиОтвета + Заголовок.Ключ + ": " + Заголовок.Значение + Символы.ПС;
	КонецЦикла;
	
	НоваяЗапись = РегистрыСведений.О2_QR_ЛогиЗапросов.СоздатьМенеджерЗаписи();
	НоваяЗапись.Период = ТекущаяДатаСеанса();
	НоваяЗапись.Метод = Метод + " " + ПолнаяСсылка;
	НоваяЗапись.КодОтвета = КодОтвета;
	НоваяЗапись.Документ = Документ;
	НоваяЗапись.ЗаголовкиЗапроса = ТекстЗаголовкиЗапроса;
	НоваяЗапись.ТелоЗапроса = ТелоЗапроса;
	НоваяЗапись.ЗаголовкиОтвета = ТекстЗаголовкиОтвета;
	НоваяЗапись.ТелоОтвета = ТелоОтвета;
	НоваяЗапись.Записать();
КонецФункции
#КонецОбласти

#Область ОбработкаJSON
Функция ПолучитьСтруктуруИзJSON(Текст)
	Попытка
		//Попытаемся прочитать JSON
		ЧтениеJson = Новый ЧтениеJSON;
		ЧтениеJson.УстановитьСтроку(Текст);
		ДанныеСтруктура = ПрочитатьJSON(ЧтениеJson);
		Возврат ДанныеСтруктура;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
КонецФункции
//Получает данные из структуры если ключ существует
Функция ПолучитьЗначениеЕслиСуществуетИЗаполнено(Данные,Наименование) Экспорт
	Если НЕ ЗначениеЗаполнено(Данные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если ТипЗнч(Данные) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если Данные.Свойство(Наименование) И ЗначениеЗаполнено(Данные[Наименование]) Тогда
		Возврат Данные[Наименование];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции
//Получает данные из структуры по пути "ключ1.ключ2" если ключи сущестуют
Функция ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Данные,Путь) Экспорт
	МассивПути = СтрРазделить(Путь,".",Ложь);
	ТекущиеДанные = Данные;
	Для Каждого ТекущийКлюч из МассивПути Цикл
		ТекущиеДанные = ПолучитьЗначениеЕслиСуществуетИЗаполнено(ТекущиеДанные,ТекущийКлюч); 		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат ТекущиеДанные;
КонецФункции
//упр вызов функции ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути
Функция ПолучитьПоПути(Данные,Путь)
	Возврат ПолучитьЗначениеЕслиСуществуетИЗаполненоПоПути(Данные,Путь);
КонецФункции	
Функция ПолучитьМассивСтруктурИзТаблицы(Таблица)
	МассивТЗ = Новый Массив;
	Для каждого СтрокаТЗ Из Таблица Цикл
		СтруктураСтроки = Новый Структура;
		Для каждого Колонка Из Таблица.Колонки Цикл
		    СтруктураСтроки.Вставить(Колонка.Имя, СтрокаТЗ[Колонка.Имя]);
		КонецЦикла;
		МассивТЗ.Добавить(СтруктураСтроки);
	КонецЦикла;
	Возврат МассивТЗ;
КонецФункции
#КонецОбласти

#Область HTTPЗапросыИДанные
// Разбирает строку URI на составные части и возвращает в виде структуры.
// На основе RFC 3986.
//
// Параметры:
//  СтрокаURI - Строка - ссылка на ресурс в формате:
//                       <схема>://<логин>:<пароль>@<хост>:<порт>/<путь>?<параметры>#<якорь>.
//
// Возвращаемое значение:
//  Структура - составные части URI согласно формату:
//   * Схема         - Строка - схема из URI.
//   * Логин         - Строка - логин из URI.
//   * Пароль        - Строка - пароль из URI.
//   * ИмяСервера    - Строка - часть <хост>:<порт> из URI.
//   * Хост          - Строка - хост из URI.
//   * Порт          - Строка - порт из URI.
//   * ПутьНаСервере - Строка - часть <путь>?<параметры>#<якорь> из URI.
//
Функция СтруктураURI(Знач СтрокаURI) Экспорт
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема
	Схема = "";
	Позиция = СтрНайти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;
	
	// Строка соединения и путь на сервере.
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = СтрНайти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
	
	// Информация пользователя и имя сервера.
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = СтрНайти(СтрокаСоединения, "@", НаправлениеПоиска.СКонца);
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = СтрНайти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = "";
	Позиция = СтрНайти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = Сред(ИмяСервера, Позиция + 1);
		Если Не ТолькоЦифрыВСтроке(Порт) Тогда
			Порт = "";
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", ?(ПустаяСтрока(Порт), Неопределено, Число(Порт)));
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции
//HTTP запрос
Функция ВыполнитьHTTPЗапрос(URL, МетодЗапроса = "GET", ТелоЗапроса = "", Заголовки = Неопределено, Авторизация = Неопределено,SSL = Неопределено,Документ = Неопределено) Экспорт
	
	ОшибкаЗапроса = Неопределено;
	
	СтруктураURL = СтруктураURI(URL);
	
	Если SSL = Неопределено Тогда
		SSL = Новый ЗащищенноеСоединениеOpenSSL;
	ИначеЕсли SSL = Ложь Тогда
		SSL = Неопределено;
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(СтруктураURL.Хост,СтруктураURL.Порт,,,,,SSL);
	HTTPЗапрос = Новый HTTPЗапрос(СтруктураURL.ПутьНаСервере);
	
	//Заголовки
	Если ЗначениеЗаполнено(Заголовки) И ТипЗнч(Заголовки) = Тип("Соответствие") тогда
		HTTPЗапрос.Заголовки = Заголовки;		
	КонецЕсли;
	//Обяз хост
	Если HTTPЗапрос.Заголовки.Получить("Host") = Неопределено Тогда
		HTTPЗапрос.Заголовки.Вставить("Host",СтруктураURL.Хост);	
	КонецЕсли;
	//Обяз ассерт
	Если HTTPЗапрос.Заголовки.Получить("Accept") = Неопределено Тогда
		HTTPЗапрос.Заголовки.Вставить("Accept","application/json");	
	КонецЕсли;
	
	//Авторизация
	Если ЗначениеЗаполнено(Авторизация) Тогда
		Если ТипЗнч(Авторизация) = тип("ТокенДоступа") Тогда
			HTTPЗапрос.ДобавитьТокенДоступа(Авторизация);			
		ИначеЕсли ТипЗнч(Авторизация) = тип("Строка") Тогда
			 HTTPЗапрос.Заголовки.Вставить("Authorization",Авторизация);
		КонецЕсли;
	КонецЕсли;
	
	//Тело запроса
	Если ЗначениеЗаполнено(ТелоЗапроса) И ТипЗнч(ТелоЗапроса) = Тип("Строка") Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса,,ИспользованиеByteOrderMark.НеИспользовать); 
		HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded")
	ИначеЕсли ЗначениеЗаполнено(ТелоЗапроса) И ТипЗнч(ТелоЗапроса) = Тип("Структура") Тогда
		ЗаписьJSON = Новый ЗаписьJSON;			
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON,ТелоЗапроса,);
		ТелоЗапроса = ЗаписьJSON.Закрыть();

		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса);
		HTTPЗапрос.Заголовки.Вставить("Content-Type","application/json");
	КонецЕсли;
	
	//Выполнение запроса 
	Попытка
		
			ОтветHTTP = HTTPСоединение.ВызватьHTTPМетод(МетодЗапроса, HTTPЗапрос); 
			КодСостояния = ОтветHTTP.КодСостояния;
			ЗаголовкиОтвета = ОтветHTTP.Заголовки;
			ТекстОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
			ДанныеСтруктура = Неопределено;
			
		Попытка
			//Попытаемся прочитать JSON
			ЧтениеJson = Новый ЧтениеJSON;
			ЧтениеJson.УстановитьСтроку(ТекстОтвета);
			ДанныеСтруктура = ПрочитатьJSON(ЧтениеJson);
		Исключение
		КонецПопытки;
		
		ЛогированиеЗапросов(СтруктураURL.Хост,МетодЗапроса,URL,HTTPЗапрос.Заголовки,ТелоЗапроса,КодСостояния,ЗаголовкиОтвета,ТекстОтвета,Документ);
			
		Если ЗначениеЗаполнено(ДанныеСтруктура) Тогда
			Возврат ДанныеСтруктура;
		ИначеЕсли КодСостояния = 302 Тогда
			Возврат ЗаголовкиОтвета["location"]; 
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	Исключение
		Сообщить("Ошибка выполнения запроса!");
		ТекстОшибки = ОписаниеОшибки();
		ВЖурнал(ТекстОшибки);
		
		ВторойТекстОшибки = "";
		ВторойТекстОшибки = ВторойТекстОшибки + ?(ЗначениеЗаполнено(КодСостояния),"КодСостояния - " + Строка(КодСостояния) + Символы.ПС,"");
		ВторойТекстОшибки = ВторойТекстОшибки + ?(ЗначениеЗаполнено(ТекстОтвета),"ТекстОтвета - " + Строка(ТекстОтвета) + Символы.ПС,"");
		Если ЗначениеЗаполнено(ВторойТекстОшибки) Тогда
			Сообщить(ВторойТекстОшибки);
		КонецЕсли;
		Возврат Неопределено;
	КонецПопытки;
КонецФункции
//Возвращает Basic Base64(Логин:Пароль) 
Функция ПолучитьАвторизациюКакBASIC(Логин,Пароль)
	Возврат "Basic "+Base64Строка(ПолучитьДвоичныеДанныеИзСтроки(Логин + ":" + Пароль));	
КонецФункции 
//Возвращает Bearer
Функция ПолучитьАвторизациюКакBearer(Токен)
	Возврат "Bearer "+Токен;	
КонецФункции
//добавляет к url структуру в виде ?param1=1&param2=2
Функция ДобавитьBodyURLEncoded(URL, Params)
	URL = URL + "?";
	Для Каждого Параметр из Params Цикл
		URL = URL + Параметр.Ключ + "=" + Параметр.Значение + "&";
	КонецЦикла;
	Возврат URL;
КонецФункции
Функция ВернутьBodyURLEncoded(Params)
	URL = "";
	Для Каждого Параметр из Params Цикл
		URL = URL + Параметр.Ключ + "=" + Параметр.Значение + "&";
	КонецЦикла;
	Возврат URL;
КонецФункции
Функция СтруктуруВJSONТекст(JSON)
	Попытка
		JSONобъект = новый ЗаписьJSON;
		JSONобъект.УстановитьСтроку();
		ЗаписатьJSON(JSONобъект,JSON);
		JSONТекстом = JSONобъект.Закрыть(); 
		Возврат JSONТекстом;
	Исключение
		JSONТекстом = "Не удалось получить JSON конкретного объекта";
		Возврат JSONТекстом;
	КонецПопытки;
КонецФункции
// Нормализует дату в формате 2024-12-03T21:00:00Z в дату 1с
Функция JSONДатаНормализовать(ВходнаяСтрока)
	ДатаJSON = СтрЗаменить(ДатаJSON,"Z","");
	ДатаJSON = XMLЗначение(Тип("Дата"),ДатаJSON);
	Возврат ДатаJSON;	
КонецФункции
// Проверяет, содержит ли строка только цифры.
//
// Параметры:
//  Значение         - Строка - проверяемая строка.
//  Устаревший       - Булево - устаревший параметр, не используется.
//  ПробелыЗапрещены - Булево - если Ложь, то в строке допустимо наличие пробелов.
//
// Возвращаемое значение:
//   Булево - Истина - строка содержит только цифры или пустая, Ложь - строка содержит иные символы.
//
// Пример:
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("0123"); // Истина
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("0123abc"); // Ложь
//  Результат = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке("01 2 3",, Ложь); // Истина
//
Функция ТолькоЦифрыВСтроке(Знач Значение, Знач Устаревший = Истина, Знач ПробелыЗапрещены = Истина) Экспорт

	Если ТипЗнч(Значение) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;

	Если Не ПробелыЗапрещены Тогда
		Значение = СтрЗаменить(Значение, " ", "");
	КонецЕсли;

	Если СтрДлина(Значение) = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Если содержит только цифры, то в результате замен должна быть получена пустая строка.
	// Проверять при помощи ПустаяСтрока нельзя, так как в исходной строке могут быть пробельные символы.
	Возврат СтрДлина(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( 
			Значение, "0", ""), "1", ""), "2", ""), "3", ""), "4", ""), "5", ""), "6", ""), "7", ""), "8", ""), "9",
		"")) = 0;

КонецФункции
#КонецОбласти

#Область ОбработчикиAPI
Функция ВсеОстальныеЛюбой(Запрос) Экспорт
	Ответ = ВернутьОшибку("ERR_SERVER");
	ЛогированиеЗапросовHTTPСервисы(Неопределено,Запрос,Ответ,,"Некорректный путь или метод запроса.");
	Возврат Ответ;
КонецФункции

Функция update_payment_statusPOST(Запрос) Экспорт
	//Запишем запрос в лог
	ВремяЗапроса = ТекущаяДатаСеанса();
	ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос);
	//
	
	//Проверим валидность JSON структуры
	СтруктураВхДанных = новый Структура;
	СтруктураВхДанных = ПолучитьСтруктуруИзJSON(Запрос.ПолучитьТелоКакСтроку()); //Запрос.ПолучитьТелоКакСтроку("UTF-8")
	Если СтруктураВхДанных = Неопределено Тогда
		Ответ = ВернутьОшибку("ERR_JSON");
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"Не удалось обработать тело запроса как JSON.");
		Возврат Ответ;
	КонецЕсли;
	//
	
	//Проверим валидность структуры Для смены статуса платежа
	order_id 			= ПолучитьПоПути(СтруктураВхДанных,"order_id");
	status 				= ПолучитьПоПути(СтруктураВхДанных,"status");
	
	
	//Заполнены ли основные параметры
	Если НЕ ЗначениеЗаполнено(order_id) ИЛИ НЕ ЗначениеЗаполнено(status) Тогда
		Ответ = ВернутьОшибку("ERR_DATA_INVALID"); 
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"Не заполнены значения");
		Возврат Ответ;	
	КонецЕсли;
	
	//Проверяем order_id что это число
	ВЖурнал("Проверяем order_id что это число");
	Если НЕ ТипЗнч(order_id) = Тип("Число") Тогда
		Ответ = ВернутьОшибку("ERR_ORDERID_DATA_INVALID"); 
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"order_id не является числом");
		Возврат Ответ;
	КонецЕсли;
	
	//Проверяем status что это Строка
	ВЖурнал("Проверяем status что это Строка");
	Если НЕ ТипЗнч(status) = Тип("Строка") Тогда
		Ответ = ВернутьОшибку("ERR_STATUS_DATA_INVALID"); 
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"status не является строкой");
		Возврат Ответ;
	КонецЕсли;
	
	//Проверим что передаваемый статус платежа корректный
	Попытка
		УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты[status];
	Исключение
		УстанавливаемыйСтатус = Неопределено;
	КонецПопытки;
	Если УстанавливаемыйСтатус = Неопределено Тогда
		Ответ = ВернутьОшибку("ERR_STATUS_NOT_FOUND"); 
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"Не найден статус платежа " + Строка(status));
		Возврат Ответ;
	КонецЕсли;
	
	//Найдем существующий платеж по order_id
	ВЖурнал("Найдем существующий платеж по order_id");
	СтрокаСтатуса = ПолучитьСтатусОплаты(order_id);
	Если СтрокаСтатуса = Неопределено Тогда
		// БИЗТЕХ КОВАЛЬ 06.08.2025 ++
		// Если Платежа нет то отправим запрос в Кавказ
		// Весь ответ кавказа вернем как ответ
		
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"Платеж не найден. Отправляем запрос в Кавказ");
		
		//Отправим запрос на оплату
		Хост = "192.168.1.3";
		URL = "http://" + Хост + "/Alfa_OptMK/hs/O2QR/update_payment_status";	
		Авторизация = "Basic 0JHQuNC30YLQtdGFOkJpenRlaF8yMjUyODMx";
		Ответ = ВыполнитьHTTPЗапрос(URL,"POST",Запрос.ПолучитьТелоКакСтроку(),,Авторизация,Ложь);
		ВЖурнал(Ответ);
		
		ЗаписьJSON = Новый ЗаписьJSON;			
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON,Ответ,);
		ТелоОтвета = ЗаписьJSON.Закрыть();
		
		Если Ответ = Неопределено ИЛИ Ответ = Null Тогда
			Ответ = ВернутьОшибку("ERR_STATUS_UPDATE_FAILED","Ошибка передачи платежа в базу Кавказ"); 
			ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"Ошибка передачи платежа в базу Кавказ");
			Возврат Ответ;	
		КонецЕсли;
		
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
		Ответ.Заголовки.Вставить("Content-Type","application/json;charset=utf-8");
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"Ответ от Кавказа получен, возвращаем ответ");
		Возврат Ответ;
		//
		//Ответ = ВернутьОшибку("ERR_ORDERID_NOT_FOUND"); 
		//ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,,"Не найден платеж " + Строка(order_id));
		//Возврат Ответ;
	КонецЕсли;
	ДокументОплаты = СтрокаСтатуса.ДокументОплаты;
	ТекущийСтатусПлатежа = СтрокаСтатуса.Статус;
	
	
	//Проверим что статус возможно изменить
	Если ТекущийСтатусПлатежа = Перечисления.О2_QR_СтатусыОплаты.Cancelled ИЛИ
		ТекущийСтатусПлатежа = Перечисления.О2_QR_СтатусыОплаты.Refunded ИЛИ
		ТекущийСтатусПлатежа = Перечисления.О2_QR_СтатусыОплаты.Fiscalized ИЛИ
		УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты.Сreated ИЛИ
		(ТекущийСтатусПлатежа = Перечисления.О2_QR_СтатусыОплаты.Paid И УстанавливаемыйСтатус <> Перечисления.О2_QR_СтатусыОплаты.Fiscalized) ИЛИ
		ТекущийСтатусПлатежа = УстанавливаемыйСтатус ИЛИ
		(ТекущийСтатусПлатежа <> Перечисления.О2_QR_СтатусыОплаты.Сreated И УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты.RefundInProgress) ИЛИ 
		(ТекущийСтатусПлатежа <> Перечисления.О2_QR_СтатусыОплаты.Сreated И УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты.RefundInProgress) Тогда
		
		Ответ = ВернутьОшибку("ERR_STATUS_INVALID",Строка(ТекущийСтатусПлатежа)); 
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,ДокументОплаты,"Ошибка смены статуса. Текущий статус - " + Строка(ТекущийСтатусПлатежа));
		Возврат Ответ;
	КонецЕсли;
	
	//Проверим что есть данные чека
	Если УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты.Fiscalized ИЛИ
		УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты.RefundedFiscalized тогда
		
		receipt_number 			= ПолучитьПоПути(СтруктураВхДанных,"receipt.number");
		document_id 			= ПолучитьПоПути(СтруктураВхДанных,"receipt.document_id");
		created_at              = ПолучитьПоПути(СтруктураВхДанных,"receipt.created_at");
		shift_number            = ПолучитьПоПути(СтруктураВхДанных,"receipt.shift_number");
				
		//Приведем данные в корректный формат
		Попытка
			receipt_number 			= Число(receipt_number);
			document_id 			= Число(document_id);
			created_at              = XMLЗначение(Тип("Дата"),Строка(created_at));
			shift_number            = Число(shift_number);	
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			ВЖурнал(ТекстОшибки);
			Ответ = ВернутьОшибку("ERR_RECEIPT_DATA_INVALID",Строка(ТекущийСтатусПлатежа)); 
			ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,ДокументОплаты,"Ошибка типов данных чека");
			Возврат Ответ;
		КонецПопытки;
		
		//Проверим что данные заполнены
		Если НЕ ЗначениеЗаполнено(receipt_number) ИЛИ
			НЕ ЗначениеЗаполнено(document_id) ИЛИ 
			НЕ ЗначениеЗаполнено(created_at) ИЛИ
			НЕ ЗначениеЗаполнено(shift_number) Тогда
			
			Ответ = ВернутьОшибку("ERR_RECEIPT_DATA_INVALID",Строка(ТекущийСтатусПлатежа)); 
			ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,ДокументОплаты,"Данные чека не заполнены");
			Возврат Ответ;
		КонецЕсли;	
	КонецЕсли;
		
	Если УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты.Fiscalized ИЛИ
		УстанавливаемыйСтатус = Перечисления.О2_QR_СтатусыОплаты.RefundedFiscalized Тогда
		НачатьТранзакцию();
		Попытка
			ВЖурнал("Получаем модуль");
			ДокОбъект = ДокументОплаты.ПолучитьОбъект();
			Если 1=2 Тогда
				ДокОбъект = Документы.ЧекНаОплату.СоздатьДокумент();
			КонецЕсли;
			ДокОбъект.О2_QR_ОплатаПоQR = Истина;
			//Данные чека
			ДокОбъект.НомерДокумента = document_id;
			ДокОбъект.НомерЧека = receipt_number;
			ДокОбъект.НомерСмены = shift_number;
			ДокОбъект.ДатаФР = created_at; 
			//Данные чека
			ВЖурнал("Записываем документ");
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);
			ВЖурнал("Запись прошла");
		Исключение
			ВЖурнал("Отменяем транзакцию");
			ОтменитьТранзакцию();
			ТекстОшибки = ОписаниеОшибки();
			Сообщить(ТекстОшибки);
			Ответ = ВернутьОшибку("ERR_STATUS_UPDATE_FAILED",Строка(ТекстОшибки)); 
			ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,ДокументОплаты,"Ошибка при установке статуса платежа. Ошибка - " + Строка(ТекстОшибки));
			Возврат Ответ;
		КонецПопытки;
		ЗафиксироватьТранзакцию(); 
	КонецЕсли;
	
	Попытка 
		//Данные чека
		ВЖурнал("Записываем документ");
		ЗаписатьСтатусПлатежа(УстанавливаемыйСтатус,ДокументОплаты,order_id);
		ВЖурнал("Запись прошла");
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Сообщить(ТекстОшибки);
		Ответ = ВернутьОшибку("ERR_STATUS_UPDATE_FAILED",Строка(ТекстОшибки)); 
		ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,ДокументОплаты,"Ошибка при установке статуса платежа. Ошибка - " + Строка(ТекстОшибки));
		Возврат Ответ;
	КонецПопытки;
	
	
	//Вернем ответ
	//Структура ошибки
	СтруктураОтвета = новый Структура;
	
	СтруктураОтвета.Вставить("order_id", order_id);
	СтруктураОтвета.Вставить("status", Строка(УстанавливаемыйСтатус));
    СтруктураОтвета.Вставить("message", "Статус записан успешно");
		
	ЗаписьJSON = Новый ЗаписьJSON;			
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON,СтруктураОтвета,);
	ТелоОтвета = ЗаписьJSON.Закрыть();
	//
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
	Ответ.Заголовки.Вставить("Content-Type","application/json;charset=utf-8");
	ЛогированиеЗапросовHTTPСервисы(ВремяЗапроса,Запрос,Ответ,ДокументОплаты,"Статус изменен успешно.");
	Возврат Ответ;
	
КонецФункции
#КонецОбласти  

Функция ПолучитьСтатусОплаты(IDОплаты) 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	О2_QR_СтатусыОплатыСрезПоследних.Статус КАК Статус,
		|	О2_QR_СтатусыОплатыСрезПоследних.ДокументОплаты КАК ДокументОплаты
		|ИЗ
		|	РегистрСведений.О2_QR_СтатусыОплаты.СрезПоследних(, ИдПлатежа = &ИдПлатежа) КАК О2_QR_СтатусыОплатыСрезПоследних";
	
	Запрос.УстановитьПараметр("ИдПлатежа", IDОплаты);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() Тогда
		Возврат РезультатЗапроса[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЗаписатьСтатусПлатежа(Статус,ДокументОплаты,ИдПлатежа = Неопределено,ОснованиеДокументаОплаты = Неопределено)
	НоваяЗапись = РегистрыСведений.О2_QR_СтатусыОплаты.СоздатьМенеджерЗаписи();
	НоваяЗапись.Период = ТекущаяДатаСеанса();
	НоваяЗапись.ДокументОплаты = ДокументОплаты.Ссылка;
	Если ОснованиеДокументаОплаты = Неопределено Тогда
		НоваяЗапись.ОснованиеДокументаОплаты = ДокументОплаты.ДокументОснование;
	Иначе
		НоваяЗапись.ОснованиеДокументаОплаты = ОснованиеДокументаОплаты;
	КонецЕсли;
	Если ИдПлатежа = Неопределено Тогда
		НоваяЗапись.ИдПлатежа = ДокументОплаты.О2_QR_ИдПлатежа;
	Иначе
		НоваяЗапись.ИдПлатежа = ИдПлатежа;
	КонецЕсли;
	НоваяЗапись.Статус = Статус;
	НоваяЗапись.Записать();
КонецФункции
 
// Получение суммы табличной части документа без скидки
// Параметры
//  ЭтотОбъект	– ДокументОбъект – документ, сумму которого необходимо получить
//  Товары		– ТабличнаяЧасть – табличная часть документа, сумму по которой необходимо получить
// Возвращаемое значение:
//   Число		– Сумма документа без скидки
Функция рсПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, Знач Товары = Неопределено) Экспорт
	СуммаДокументаБезСкидки = орПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, "Товары");
	Если СуммаДокументаБезСкидки <> Неопределено Тогда
		Возврат СуммаДокументаБезСкидки;
	КонецЕсли; 
	
	// Получим табличную часть документа
	Попытка
		Товары = ?(Товары = Неопределено, ЭтотОбъект.Товары, Товары);
	Исключение КонецПопытки;
	
	// Если так и не удалось получить табличную часть или она не содержит ни одной строки, то сумма документа равна "0"
	Если (Товары = Неопределено)ИЛИ(Товары.Количество() = 0) Тогда
		Возврат 0;
	КонецЕсли;
	
	// Получим сумму документу без учета НДС и скидки
	Попытка
		СуммаДокументаБезСкидки = 0;
		СуммаДокументаБезСкидки = Товары.Итог("Сумма");
	Исключение КонецПопытки; 
	
	// Определим включают ли в себя НДС цены документа
	Попытка
		ЦенаВключаетНДС = ИСТИНА;
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
	Исключение КонецПопытки;
	
	//Если цена не включает НДС, то получим суммарную НДС по всем строкам
	Если НЕ ЦенаВключаетНДС Тогда
		Попытка
			Для Каждого ТекСтрока Из Товары Цикл
				СуммаДокументаБезСкидки = СуммаДокументаБезСкидки + (ТекСтрока.Сумма * ТекСтрока.СтавкаНДС.Ставка)/100;
			КонецЦикла;
		Исключение КонецПопытки;
	КонецЕсли;
	
	Возврат СуммаДокументаБезСкидки;
КонецФункции // дкПолучитьСуммуДокументаБезСкидки()

// Функция возвращает таблицу содержащую все действующие на данный момент скидки, удовлетворяющие
// переданным параметрам
Функция рсПолучитьСписокДоступныхСкидок(Дата, Отбор) Экспорт
	// Проверим и обработаем переданные данные
	Если ТипЗнч(Отбор) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Создадим специальную структуру, где первый параметр это имя регистра сведений,
	// а второй - список параметров которые нам не нужно включать в запрос
	ВидыСкидок = Новый Структура();
	Если Отбор.Свойство("СкидкиШапки") Тогда
		ВидыСкидок.Вставить("СкидкиШапки", ",Объект,СуммаСтроки,Количество,");
	КонецЕсли;
	Если Отбор.Свойство("СкидкиСтроки") Тогда
		ВидыСкидок.Вставить("СкидкиСтроки",",СуммаЧека,");
	КонецЕсли;
	
	// Создадим список не иерархических параметров
	Параметры = Новый Структура();
	Параметры.Вставить("ЭтоПодарок",	Неопределено);
	Параметры.Вставить("СуммаЧека",		Неопределено);
	Параметры.Вставить("СуммаСтроки",	Неопределено);
	Параметры.Вставить("Количество",	Неопределено);
	Параметры.Вставить("ФронтОфис",		Неопределено);
	Параметры.Вставить("БэкОфис",		Неопределено);
	Параметры.Вставить("Терминал",		Неопределено);
	Параметры.Вставить("Время",			'00010101' + (Дата - НачалоДня(Дата)));
	Параметры.Вставить("ДеньНедели",	ДеньНедели(Дата));
	Параметры.Вставить("СкидкаНаТовары",Неопределено);
	Параметры.Вставить("СкидкаНаРаботы",Неопределено);
	ЗаполнитьЗначенияСвойств(Параметры, Отбор);
	
	// Список иерархических параметров
	Иерархические = Новый Структура("ПодразделениеКомпании,Карточка,СкладКомпании,Объект");
	ЗаполнитьЗначенияСвойств(Иерархические, Отбор);
	
	// Получим список всех родителей для иерархических параметров и добавим их в структуру "Параметры"
	Для Каждого ТекПараметр Из Иерархические Цикл
		Элемент = ТекПараметр.Значение;
		Если Элемент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МассивЗначений = Новый Массив();
		МассивЗначений.Добавить(Элемент);
		Пока НЕ Элемент.Пустая() Цикл
			Элемент = Элемент.Родитель;
			МассивЗначений.Добавить(Элемент);
		КонецЦикла;
		Если ТекПараметр.Ключ = "Объект" Тогда
			Попытка
				МассивЗначений.Добавить(ТекПараметр.Значение.ТипНоменклатуры);
			Исключение КонецПопытки;
		КонецЕсли;
		Параметры.Вставить(ТекПараметр.Ключ, МассивЗначений);
	КонецЦикла;
	
	// Создадим объект запроса
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("КоэффициентПересчета", обПересчет(1, Константы.ВалютаНакопительныхСумм.Получить(), Дата, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), 1));
	
	// Установим все не иерархические параметры
	Для Каждого ТекПараметр Из Параметры Цикл
		Если ТекПараметр.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Номенклатура", Иерархические.Объект);
	
	// Установим соответствие между именами параметров и строками условий виртуальной таблицы
	СтрокаФильтра = Новый Соответствие();
	СтрокаФильтра.Вставить("ЭтоПодарок",			"ЭтоПодарок = &ЭтоПодарок");
	СтрокаФильтра.Вставить("ВидСкидки",				"Скидка.ВидСкидки = &ВидСкидки");
	СтрокаФильтра.Вставить("СуммаЧека",				"ОтСуммыЧека <= &СуммаЧека");
	СтрокаФильтра.Вставить("СуммаСтроки",			"ОтСуммыСтроки <= &СуммаСтроки");
	СтрокаФильтра.Вставить("Количество",			"ОтКоличества <= &Количество");
	СтрокаФильтра.Вставить("ФронтОфис",				"Скидка.ИспользоватьДляФронтОфиса = &ФронтОфис");
	СтрокаФильтра.Вставить("БэкОфис",				"Скидка.ИспользоватьДляБэкОфиса = &БэкОфис");
	СтрокаФильтра.Вставить("Терминал",				"Скидка.ИспользоватьДляТерминалов = &Терминал");
	СтрокаФильтра.Вставить("Время",					"(&Время МЕЖДУ НачВремя И КонВремя)");
	СтрокаФильтра.Вставить("ДеньНедели",			"ПОДСТРОКА(ДниНедели, &ДеньНедели, 1) = ""1""");
	СтрокаФильтра.Вставить("Объект",				"Объект В (&Объект)");
	СтрокаФильтра.Вставить("ПодразделениеКомпании",	"Подразделение В (&ПодразделениеКомпании)");
	СтрокаФильтра.Вставить("Карточка",				"ДисконтнаяКарта В (&Карточка)");
	СтрокаФильтра.Вставить("СкладКомпании",			"ЗалОбслуживания В (&СкладКомпании)");
	СтрокаФильтра.Вставить("СкидкаНаТовары",		"СкидкаНаТовары = &СкидкаНаТовары");
	СтрокаФильтра.Вставить("СкидкаНаРаботы",		"СкидкаНаРаботы = &СкидкаНаРаботы");
	
	// Создадим объединенный запрос к таблицам регистра
	ТекстЗапроса = "//";
	Для Каждого ВидСкидки Из ВидыСкидок Цикл
		ФильтрВиртуальнойТаблицы = "";
		
		// Составим строку фильтра для виртуальной таблицы
		Для Каждого ТекПараметр Из Параметры Цикл
			Если ВидСкидки.Ключ<>"СкидкиШапки" Тогда
				Если ТекПараметр.Ключ="СкидкаНаТовары" ИЛИ ТекПараметр.Ключ="СкидкаНаРаботы" Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли; 
			Если (ТекПараметр.Значение = Неопределено)ИЛИ(Найти(ВидСкидки.Значение,","+ТекПараметр.Ключ+",") > 0) Тогда
				Продолжить;
			КонецЕсли;
			ФильтрВиртуальнойТаблицы = ФильтрВиртуальнойТаблицы+Символы.ПС+"			И "+СтрокаФильтра[ТекПараметр.Ключ];
		КонецЦикла;
		
		// Сформируем текст запроса
		ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	$ИмяТаблицы$СрезПоследних.Скидка,
		|	$ИмяТаблицы$СрезПоследних.Скидка.ВидСкидки КАК ВидСкидки,
		|	$ИмяТаблицы$СрезПоследних.ЗначениеСкидки,
		|	$ИмяТаблицы$СрезПоследних.ЭтоПодарок,
		|	$ИмяТаблицы$СрезПоследних.СпособВычисления
		|ИЗ
		|	РегистрСведений.$ИмяТаблицы$.СрезПоследних(
		|		&Дата,
		|		РучнаяСкидка//ФИЛЬТРЫ) КАК $ИмяТаблицы$СрезПоследних";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ФИЛЬТРЫ", ФильтрВиртуальнойТаблицы);
		
		Если ВидСкидки.Ключ = "СкидкиСтроки" Тогда
			ТекстСоединенияПоСвойствам = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
			|ПО
			|	(НЕ $ИмяТаблицы$СрезПоследних.Свойство        = НЕОПРЕДЕЛЕНО) И 
			|	ЗначенияСвойствОбъектов.Объект               = &Номенклатура И 
			|	$ИмяТаблицы$СрезПоследних.Скидка.ВидСвойства = ЗначенияСвойствОбъектов.Свойство";
		Иначе
			ТекстСоединенияПоСвойствам = "";
		КонецЕсли;
		
		// В случае если используется отбор по карточке, то дополним запрос подзапросом
		// к накопленным суммам на карточках
		Если Иерархические.Карточка <> Справочники.Карточки.ПустаяСсылка() Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	(ВЫБРАТЬ
			|		ЕСТЬNULL(СУММА(НакоплениеСумм.Сумма), 0) * &КоэффициентПересчета КАК Сумма
			|	ИЗ
			|		РегистрСведений.НакоплениеСумм КАК НакоплениеСумм
			|	ГДЕ
			|		НакоплениеСумм.ПериодНакопления <= &Дата
			|		И НакоплениеСумм.Карточка = &КарточкаЗначение) КАК СуммыНакопления
			|ПО
			|	ИСТИНА " + ТекстСоединенияПоСвойствам + "
			|
			|ГДЕ
			|	$ИмяТаблицы$СрезПоследних.Действует
			|	И $ИмяТаблицы$СрезПоследних.ОтСуммыНакопленияНаКарте <= ЕСТЬNULL(СуммыНакопления.Сумма, 0)";
			Запрос.УстановитьПараметр("КарточкаЗначение", Иерархические.Карточка);
		Иначе
			ТекстЗапроса = ТекстЗапроса + ТекстСоединенияПоСвойствам + "
			|ГДЕ
			|	$ИмяТаблицы$СрезПоследних.Действует";
		КонецЕсли;
		
		Если ВидСкидки.Ключ = "СкидкиСтроки" Тогда
			ТекстЗапроса = ТекстЗапроса + " И 
			|	($ИмяТаблицы$СрезПоследних.Свойство = НЕОПРЕДЕЛЕНО ИЛИ 
			|	((НЕ ЗначенияСвойствОбъектов.Значение ЕСТЬ NULL) И 
			|	$ИмяТаблицы$СрезПоследних.Свойство = ЗначенияСвойствОбъектов.Значение))";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + Символы.ПС+Символы.ПС;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "$ИмяТаблицы$", ВидСкидки.Ключ);
	КонецЦикла;
	
	// Выполним запрос и получим таблицу действующих скидок
	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

// Функция делает выборку актуальных скидок по заданным параметрам и возвращает одну строку общей скидки
//
// Параметры
//  Дата	- <Дата и Время> – Дата на которую делается выборка из регистра сведений
//  Отбор	– <Структура> – Структура параметров (фильтров) накладываемых на таблицу регистра
//
// Возвращаемое значение:
//   <СтрокаВыборки> – строка повторяющая структура регистра сведений. Содержит найденную скидку
//					   удовлетворяющую заданным параметрам.
//					   Если в процессе получения данных возникла ошибка, то возвращается "Неопределено"
//
Функция рсСкидкиПолучитьШапочные(Дата, Отбор, СуммаДокумента = 0) Экспорт
	// Проверим и обработаем переданные данные
	Если ТипЗнч(Отбор) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Определим коэффициент пересчета из валюты накопительных сумм в регламентированную	//Оптимизировать
	КоэффициентПересчета = обПересчет(1, Константы.ВалютаНакопительныхСумм.Получить(), Дата, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), 1);
	
	// Создадим список не иерархических обязательных параметров
	Параметры = Новый Структура();
	Параметры.Вставить("РучнаяСкидка",	Неопределено);
	Параметры.Вставить("Время",			'00010101' + (Дата - НачалоДня(Дата)));
	Параметры.Вставить("ДеньНедели",	ДеньНедели(Дата));
	Параметры.Вставить("БазаРасчета",	Неопределено);
	ЗаполнитьЗначенияСвойств(Параметры, Отбор);
	
	// Список иерархических параметров
	Иерархические = Новый Структура();
	Иерархические.Вставить("ПодразделениеКомпании",	Справочники.ПодразделенияКомпании.ОсновноеПодразделение);
	Иерархические.Вставить("Карточка",				Справочники.Карточки.ПустаяСсылка());
	Иерархические.Вставить("СкладКомпании",			Справочники.СкладыКомпании.ПустаяСсылка());
	ЗаполнитьЗначенияСвойств(Иерархические, Отбор);
	
	// Дополнительные параметры
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("Скидка",		Неопределено);
	ДопПараметры.Вставить("ЭтоПодарок",	Неопределено);
	ДопПараметры.Вставить("Свойство",	Неопределено);
	ДопПараметры.Вставить("ВидСвойства",Неопределено);
	ДопПараметры.Вставить("ФронтОфис",	Неопределено);
	ДопПараметры.Вставить("БэкОфис",	Неопределено);
	ДопПараметры.Вставить("Терминал",	Неопределено);
	ДопПараметры.Вставить("СкидкаНаТовары",	Неопределено);
	ДопПараметры.Вставить("СкидкаНаРаботы",	Неопределено);
	ЗаполнитьЗначенияСвойств(ДопПараметры, Отбор);
	ДопФильтр = "";
	
	// Создадим соответствие имен параметров строкам фильтра
	СтрокиФильтра = Новый Соответствие;
	СтрокиФильтра.Вставить("Скидка",		"Скидка = &Скидка");
	СтрокиФильтра.Вставить("ЭтоПодарок",	"ЭтоПодарок = &ЭтоПодарок");
	СтрокиФильтра.Вставить("Свойство",		"Свойство = &Свойство");
	СтрокиФильтра.Вставить("ВидСвойства",	"Скидка.ВидСвойства = &ВидСвойства");
	СтрокиФильтра.Вставить("ФронтОфис",		"Скидка.ИспользоватьДляФронтОфиса = &ФронтОфис");
	СтрокиФильтра.Вставить("БэкОфис",		"Скидка.ИспользоватьДляБэкОфиса = &БэкОфис");
	СтрокиФильтра.Вставить("Терминал",		"Скидка.ИспользоватьДляТерминалов = &Терминал");
	СтрокиФильтра.Вставить("СкидкаНаТовары","СкидкаНаТовары = &СкидкаНаТовары");
	СтрокиФильтра.Вставить("СкидкаНаРаботы","СкидкаНаРаботы = &СкидкаНаРаботы");
	
	// Создадим объект запроса
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("АбсолютнаяСкидка",		Перечисления.СкидкиСпособВычисления.Абсолютная);
	Запрос.УстановитьПараметр("ВидСкидкиНаДокумент",	Перечисления.ВидыСкидок.НаДокумент);
	Запрос.УстановитьПараметр("КоэффициентПересчета",	КоэффициентПересчета);
	Запрос.УстановитьПараметр("СуммаДокумента",			СуммаДокумента);
	
	// Установим все обязательные не иерархические параметры
	Для Каждого ТекПараметр Из Параметры Цикл
		Если ТекПараметр.Значение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	// Установим иерархические параметры
	// VIEW Получать всех родителей будем через обращение к реквизиту "Родитель". Как показали тесты
	// этот вариант является более быстродейственным по сравнению с вариантом при котором формируется
	// текст вложенного запроса (состоящего из объединенния нескольких подзапросов к Справочник.Родитель.Родитель...)
	// а также более эффективен чем получение списка всех родителей через запрос с итогом по иерархии.
	Для Каждого ТекПараметр Из Иерархические Цикл
		Элемент = ТекПараметр.Значение;
		МассивЗначений = Новый Массив();
		МассивЗначений.Добавить(Элемент);
		Пока НЕ Элемент.Пустая() Цикл
			Элемент = Элемент.Родитель;
			МассивЗначений.Добавить(Элемент);
		КонецЦикла;
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, МассивЗначений);
	КонецЦикла;
	
	// Установим дополнительные параметры
	Для Каждого ТекПараметр Из ДопПараметры Цикл
		Значение = ТекПараметр.Значение;
		Если Значение <> Неопределено Тогда
			ИмяПараметра = ТекПараметр.Ключ;
			ДопФильтр = ДопФильтр + "		    И " + СтрокиФильтра[ИмяПараметра] + Символы.ПС;
			Запрос.УстановитьПараметр(ИмяПараметра, Значение);
		КонецЕсли;
	КонецЦикла;
	
	// Сформируем текст запроса
	ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 20
	|	СкидкиШапкиСрезПоследних.Скидка КАК Скидка,
	|	СкидкиШапкиСрезПоследних.ОтСуммыЧека КАК СуммаЧека,
	|	СкидкиШапкиСрезПоследних.СпособВычисления,
	|	СкидкиШапкиСрезПоследних.ЗначениеСкидки,
	|	СкидкиШапкиСрезПоследних.МаксимальнаяСуммаПодарка,
	|	СкидкиШапкиСрезПоследних.ВидПрайсЛистаПодарка,
	|	СкидкиШапкиСрезПоследних.ФлагВытеснения КАК ФлагВытеснения,
	|	СкидкиШапкиСрезПоследних.ЭтоПодарок КАК ЭтоПодарок,
	|	ВЫБОР
	|		КОГДА &СуммаДокумента >= СкидкиШапкиСрезПоследних.ОтСуммыЧека
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СкидкаДействует,
	|	ВЫБОР
	|		КОГДА СкидкиШапкиСрезПоследних.СпособВычисления = &АбсолютнаяСкидка
	|			ТОГДА СкидкиШапкиСрезПоследних.ЗначениеСкидки
	|		ИНАЧЕ &БазаРасчета * СкидкиШапкиСрезПоследних.ЗначениеСкидки / 100
	|	КОНЕЦ КАК СуммаСкидки
	|ИЗ
	|	РегистрСведений.СкидкиШапки.СрезПоследних(
	|		&Дата,
	|		Скидка.ВидСкидки = &ВидСкидкиНаДокумент
	|		    И РучнаяСкидка = &РучнаяСкидка
	|//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ
	|		    И (&Время МЕЖДУ НачВремя И КонВремя)
	|		    И ПОДСТРОКА(ДниНедели, &ДеньНедели, 1) = ""1""
	|		    И Подразделение В (&ПодразделениеКомпании)
	|		    И ДисконтнаяКарта В (&Карточка)
	|		    И ЗалОбслуживания В (&СкладКомпании)) КАК СкидкиШапкиСрезПоследних";
	
	// Установим дополнительные фильтры
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ", ДопФильтр);
	
	// Если требуется получить скидку по конкретной карточке, то необходимо также сделать подзапрос
	// и к накопленным суммам на этой карточке
    Если Иерархические.Карточка <> Справочники.Карточки.ПустаяСсылка() Тогда
        ТекстЗапроса = ТекстЗапроса + "
        |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	(ВЫБРАТЬ
        |			ЕСТЬNULL(СУММА(НакоплениеСумм.Сумма), 0) * &КоэффициентПересчета КАК Сумма
        |		ИЗ
        |			РегистрСведений.НакоплениеСумм КАК НакоплениеСумм
        |		ГДЕ
        |			НакоплениеСумм.ПериодНакопления <= &Дата
        |			И НакоплениеСумм.Карточка = &КарточкаЗначение) КАК СуммыНакопления
		|ПО
		|	ИСТИНА
        |ГДЕ
        |	СкидкиШапкиСрезПоследних.Действует И
        |	(СкидкиШапкиСрезПоследних.ДисконтнаяКарта = ЗНАЧЕНИЕ(Справочник.Карточки.ПустаяСсылка) ИЛИ СкидкиШапкиСрезПоследних.ОтСуммыНакопленияНаКарте <= ЕСТЬNULL(СуммыНакопления.Сумма, 0))";
        Запрос.УстановитьПараметр("КарточкаЗначение", Иерархические.Карточка);
    Иначе
        ТекстЗапроса = ТекстЗапроса + "
        |ГДЕ
        |	СкидкиШапкиСрезПоследних.Действует";
    КонецЕсли;

	
	// Добавим порядок к тексту запроса
	Запрос.Текст = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	СкидкаДействует	УБЫВ,
	|	ЭтоПодарок		УБЫВ,
	|	ФлагВытеснения	УБЫВ,
	|	СуммаСкидки		УБЫВ,
	|	СуммаЧека		УБЫВ";
	
	// Выполним запрос, и вернем результат
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции // рсСкидкиПолучитьШапочные()

// Функция делает выборку актуальных скидок по заданным параметрам и возвращает одну строку строчной скидки
//
// Параметры
//  Дата		- <Дата и Время> – Дата на которую делается выборка из регистра сведений
//  Параметры	– <Структура> – Структура параметров (фильтров) накладываемых на таблицу регистра
//
// Возвращаемое значение:
//   <СтрокаВыборки> – строка повторяющая структура регистра сведений. Содержит найденную скидку
//					   удовлетворяющую заданным параметрам.
//					   Если в процессе получения данных возникла ошибка, то возвращается "Неопределено"
//
Функция рсСкидкиПолучитьСтрочные(Дата, Знач Отбор) Экспорт
	// Проверим и обработаем переданные данные
	Если ТипЗнч(Отбор) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Определим коэффициент пересчета из валюты накопительных сумм в регламентированную	//Оптимизировать
	КоэффициентПересчета = обПересчет(1, Константы.ВалютаНакопительныхСумм.Получить(), Дата, Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), 1);
	
	// Создадим список не иерархических обязательных параметров
	Параметры = Новый Структура();
	Параметры.Вставить("РучнаяСкидка",	Неопределено);
	Параметры.Вставить("Время",			'00010101' + (Дата - НачалоДня(Дата)));
	Параметры.Вставить("ДеньНедели",	ДеньНедели(Дата));
	Параметры.Вставить("СуммаСтроки",	0);
	Параметры.Вставить("Количество",	0);
	Параметры.Вставить("БазаРасчета",	Неопределено);
	ЗаполнитьЗначенияСвойств(Параметры, Отбор);
	
	// Список иерархических параметров
	Иерархические = Новый Структура();
	Иерархические.Вставить("Объект",				Справочники.Номенклатура.ПустаяСсылка());
	Иерархические.Вставить("Карточка",				Справочники.Карточки.ПустаяСсылка());
	Иерархические.Вставить("СкладКомпании",			Справочники.СкладыКомпании.ПустаяСсылка());
	Иерархические.Вставить("ПодразделениеКомпании",	Справочники.ПодразделенияКомпании.ОсновноеПодразделение);
	ЗаполнитьЗначенияСвойств(Иерархические, Отбор);
	
	// Дополнительные параметры
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("Скидка",		Неопределено);
	ДопПараметры.Вставить("ЭтоПодарок",	Неопределено);
	ДопПараметры.Вставить("Свойство",	Неопределено);
	ДопПараметры.Вставить("ВидСвойства",Неопределено);
	ДопПараметры.Вставить("ФронтОфис",	Неопределено);
	ДопПараметры.Вставить("БэкОфис",	Неопределено);
	ДопПараметры.Вставить("Терминал",	Неопределено);
	ДопПараметры.Вставить("СкидкаНаТовары",	Неопределено);
	ДопПараметры.Вставить("СкидкаНаРаботы",	Неопределено);
	ЗаполнитьЗначенияСвойств(ДопПараметры, Отбор);
	ДопФильтр = "";
	
	// Создадим соответствие имен параметров строкам фильтра
	СтрокиФильтра = Новый Соответствие;
	СтрокиФильтра.Вставить("Скидка",		"Скидка = &Скидка");
	СтрокиФильтра.Вставить("ЭтоПодарок",	"ЭтоПодарок = &ЭтоПодарок");
	СтрокиФильтра.Вставить("Свойство",		"Свойство = &Свойство");
	СтрокиФильтра.Вставить("ВидСвойства",	"Скидка.ВидСвойства = &ВидСвойства");
	СтрокиФильтра.Вставить("ФронтОфис",		"Скидка.ИспользоватьДляФронтОфиса = &ФронтОфис");
	СтрокиФильтра.Вставить("БэкОфис",		"Скидка.ИспользоватьДляБэкОфиса = &БэкОфис");
	СтрокиФильтра.Вставить("Терминал",		"Скидка.ИспользоватьДляТерминалов = &Терминал");
	СтрокиФильтра.Вставить("СкидкаНаТовары","СкидкаНаТовары = &СкидкаНаТовары");
	СтрокиФильтра.Вставить("СкидкаНаРаботы","СкидкаНаРаботы = &СкидкаНаРаботы");
	
	// Создадим объект запроса
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("АбсолютнаяСкидка",		Перечисления.СкидкиСпособВычисления.Абсолютная);
	Запрос.УстановитьПараметр("ВидСкидкиНаСтроку", Перечисления.ВидыСкидок.НаСтрокуДокумента);
	Запрос.УстановитьПараметр("КоэффициентПересчета",	КоэффициентПересчета);
	Запрос.УстановитьПараметр("ПравилоБольше",	Перечисления.ПравилаРасчетаСкидкиПоКоличеству.Больше);
	Запрос.УстановитьПараметр("ПравилоМеньше",	Перечисления.ПравилаРасчетаСкидкиПоКоличеству.Меньше);
	Запрос.УстановитьПараметр("ПравилоУпаковка",Перечисления.ПравилаРасчетаСкидкиПоКоличеству.Упаковка);
	Запрос.УстановитьПараметр("ПравилоНеИспользуется",Перечисления.ПравилаРасчетаСкидкиПоКоличеству.НеИспользуется);
	
	// Установим все обязательные не иерархические параметры
	Для Каждого ТекПараметр Из Параметры Цикл
		Если ТекПараметр.Значение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, ТекПараметр.Значение);
	КонецЦикла;
	
	// Установим иерархические параметры
	Для Каждого ТекПараметр Из Иерархические Цикл
		Элемент = ТекПараметр.Значение;
		МассивЗначений = Новый Массив();
		МассивЗначений.Добавить(Элемент);
		Пока НЕ Элемент.Пустая() Цикл
			Элемент = Элемент.Родитель;
			МассивЗначений.Добавить(Элемент);
		КонецЦикла;
		Если ТекПараметр.Ключ = "Объект" Тогда
			Попытка
				МассивЗначений.Добавить(ТекПараметр.Значение.ТипНоменклатуры);
			Исключение КонецПопытки;
		КонецЕсли;
		Запрос.УстановитьПараметр(ТекПараметр.Ключ, МассивЗначений);
	КонецЦикла;
	
	// Установим дополнительные параметры
	Для Каждого ТекПараметр Из ДопПараметры Цикл
		Значение = ТекПараметр.Значение;
		Если Значение <> Неопределено Тогда
			ИмяПараметра = ТекПараметр.Ключ;
			ДопФильтр = ДопФильтр + "		    И " + СтрокиФильтра[ИмяПараметра] + Символы.ПС;
			Запрос.УстановитьПараметр(ИмяПараметра, Значение);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Номенклатура", Иерархические.Объект);
	
	// Сформируем текст запроса
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СкидкиСтрокиСрезПоследних.Скидка КАК Скидка,
	|	СкидкиСтрокиСрезПоследних.ОтСуммыСтроки КАК СуммаСтроки,
	|	СкидкиСтрокиСрезПоследних.ОтКоличества КАК Количество,
	|	СкидкиСтрокиСрезПоследних.Правило,
	|	СкидкиСтрокиСрезПоследних.СпособВычисления,
	|	СкидкиСтрокиСрезПоследних.ЭтоПодарок КАК ЭтоПодарок,
	|	СкидкиСтрокиСрезПоследних.ЗначениеСкидки,
	|	СкидкиСтрокиСрезПоследних.ФлагВытеснения КАК ФлагВытеснения,
	|	СкидкиСтрокиСрезПоследних.ВидПрайсЛистаПодарка,
	|	СкидкиСтрокиСрезПоследних.МаксимальнаяСуммаПодарка,
	|	ВЫБОР
	|		КОГДА СкидкиСтрокиСрезПоследних.СпособВычисления = &АбсолютнаяСкидка
	|			ТОГДА СкидкиСтрокиСрезПоследних.ЗначениеСкидки
	|		ИНАЧЕ СкидкиСтрокиСрезПоследних.ЗначениеСкидки / 100 * &БазаРасчета *
	|			ВЫБОР
	|				КОГДА СкидкиСтрокиСрезПоследних.Правило = &ПравилоБольше
	|					ТОГДА (&Количество - СкидкиСтрокиСрезПоследних.ОтКоличества) / &Количество
	|				КОГДА СкидкиСтрокиСрезПоследних.Правило = &ПравилоМеньше
//	|					ТОГДА СкидкиСтрокиСрезПоследних.ОтКоличества / &Количество
	|					ТОГДА ВЫБОР
	|						КОГДА СкидкиСтрокиСрезПоследних.ОтКоличества > &Количество ТОГДА  1
	|						ИНАЧЕ СкидкиСтрокиСрезПоследних.ОтКоличества / &Количество
	|						КОНЕЦ
	|				КОГДА СкидкиСтрокиСрезПоследних.Правило = &ПравилоУпаковка
	|					ТОГДА ВЫБОР
	|							КОГДА &Количество / СкидкиСтрокиСрезПоследних.ОтКоличества < (ВЫРАЗИТЬ(&Количество / СкидкиСтрокиСрезПоследних.ОтКоличества КАК ЧИСЛО(15, 0)))
	|								ТОГДА (ВЫРАЗИТЬ(&Количество / СкидкиСтрокиСрезПоследних.ОтКоличества КАК ЧИСЛО(15, 0))) - 1
	|							ИНАЧЕ ВЫРАЗИТЬ(&Количество / СкидкиСтрокиСрезПоследних.ОтКоличества КАК ЧИСЛО(15, 0))
	|						КОНЕЦ * СкидкиСтрокиСрезПоследних.ОтКоличества / &Количество
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаСкидки
	|ИЗ
	|	РегистрСведений.СкидкиСтроки.СрезПоследних(
	|		&Дата,
	|		Скидка.ВидСкидки = &ВидСкидкиНаСтроку
	|		    И РучнаяСкидка = &РучнаяСкидка
	|		    И ОтСуммыСтроки <= &СуммаСтроки
	|		    И ((ОтКоличества <= &Количество) ИЛИ (ОтКоличества >= &Количество И Правило  = &ПравилоМеньше)) 
	|//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ
	|		    И (&Время МЕЖДУ НачВремя И КонВремя)
	|		    И ПОДСТРОКА(ДниНедели, &ДеньНедели, 1) = ""1""
	|		    И Объект В (&Объект)
	|		    И Подразделение В (&ПодразделениеКомпании)
	|		    И ДисконтнаяКарта В (&Карточка)
	|		    И ЗалОбслуживания В (&СкладКомпании)) КАК СкидкиСтрокиСрезПоследних";
	
	// Установим дополнительные фильтры
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДОПОЛНИТЕЛЬНЫЕФИЛЬТРЫ", ДопФильтр);
	
	ТекстСоединенияПоСвойствам = "
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ПО
	|	(НЕ СкидкиСтрокиСрезПоследних.Свойство        = НЕОПРЕДЕЛЕНО) И 
	|	ЗначенияСвойствОбъектов.Объект               = &Номенклатура И 
	|	СкидкиСтрокиСрезПоследних.Скидка.ВидСвойства = ЗначенияСвойствОбъектов.Свойство";
	
	// Если требуется получить скидку по конкретной карточке, то необходимо также сделать подзапрос
	// и к накопленным суммам на этой карточке
	Если Иерархические.Карточка <> Справочники.Карточки.ПустаяСсылка() Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	(ВЫБРАТЬ
		|			ЕСТЬNULL(СУММА(НакоплениеСумм.Сумма), 0) * &КоэффициентПересчета КАК Сумма
		|		ИЗ
		|			РегистрСведений.НакоплениеСумм КАК НакоплениеСумм
		|		ГДЕ
		|			НакоплениеСумм.ПериодНакопления <= &Дата
		|			И НакоплениеСумм.Карточка = &КарточкаЗначение) КАК СуммыНакопления
		|ПО
		|	ИСТИНА " + ТекстСоединенияПоСвойствам + "
		|ГДЕ
		|	СкидкиСтрокиСрезПоследних.Действует
		|	И (СкидкиСтрокиСрезПоследних.ДисконтнаяКарта = ЗНАЧЕНИЕ(Справочник.Карточки.ПустаяСсылка) ИЛИ СкидкиСтрокиСрезПоследних.ОтСуммыНакопленияНаКарте <= ЕСТЬNULL(СуммыНакопления.Сумма, 0))";
		Запрос.УстановитьПараметр("КарточкаЗначение", Иерархические.Карточка);
	Иначе
		ТекстЗапроса = ТекстЗапроса + ТекстСоединенияПоСвойствам + "
		|ГДЕ
		|	СкидкиСтрокиСрезПоследних.Действует";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + " И 
	|	(СкидкиСтрокиСрезПоследних.Свойство = НЕОПРЕДЕЛЕНО ИЛИ 
	|	((НЕ ЗначенияСвойствОбъектов.Значение ЕСТЬ NULL) И 
	|	СкидкиСтрокиСрезПоследних.Свойство = ЗначенияСвойствОбъектов.Значение))";
	
	// Добавим порядок к тексту запроса
	Запрос.Текст = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоПодарок		УБЫВ,
	|	ФлагВытеснения	УБЫВ,
	|	СуммаСкидки		УБЫВ,
	|	СуммаСтроки		ВОЗР";

	// Выполним запрос, и вернем результат
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка, Неопределено);
КонецФункции // рсСкидкиПолучитьСтрочные()

// Функция возвращает идентификатор скидки из шапки
//
Функция рсПолучитьИдентификаторСкидкиШапки(Параметры, ТекущийИдентификатор) Экспорт
	Перем Подразделение; // Подразделение
	ИдентификаторСкидки=ТекущийИдентификатор;
	// Проверим и обработаем переданные данные
	Если ТипЗнч(Параметры) <> Тип("Структура") Тогда
		Возврат Неопределено;
	Иначе
		// Создадим список обязательных параметров
		Обязательные = Новый Структура();
		Обязательные.Вставить("Подразделение",	Справочники.ПодразделенияКомпании.ПустаяСсылка());
		Обязательные.Вставить("РучнаяСкидка",	Неопределено);
		Обязательные.Вставить("НачВремя",		'00010101');
		Обязательные.Вставить("КонВремя",		'00010101');
		Обязательные.Вставить("ДниНедели",		Неопределено);
		Обязательные.Вставить("ЭтоПодарок",		Неопределено);
		Обязательные.Вставить("ДисконтнаяКарта",Справочники.Карточки.ПустаяСсылка());
		Обязательные.Вставить("ОтСуммыНакопленияНаКарте",0);
		Обязательные.Вставить("ЗалОбслуживания",Справочники.СкладыКомпании.ПустаяСсылка());
		Обязательные.Вставить("Свойство",		ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка());
		Обязательные.Вставить("ОтСуммыЧека",	0);
		Обязательные.Вставить("Скидка",			Справочники.ТипыСкидок.ПустаяСсылка());
		ЗаполнитьЗначенияСвойств(Обязательные, Параметры);
	КонецЕсли;
	
	// определим подразделение
	Обязательные.Свойство("Подразделение",Подразделение);
	Если Подразделение=Справочники.ПодразделенияКомпании.ПустаяСсылка() Тогда Возврат Неопределено; КонецЕсли;
	
	 // попробуем найти похожую запись
	СкидкиШапки=РегистрыСведений.СкидкиШапки.СрезПоследних(,Обязательные);
	Если СкидкиШапки.Количество()>0 Тогда
		ИдентификаторСкидки=СкидкиШапки.Получить(0).ИдентификаторСкидки;
		ТекущийИдентификатор=ИдентификаторСкидки;
	КонецЕсли;
	
	// определим новый идентификатор
	Если обЗначениеНеЗаполнено(ИдентификаторСкидки) Тогда
		ИдентификаторШапки=0;
		ИдентификаторСтроки=0;
		Запрос=Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СкидкиШапки.ИдентификаторСкидки КАК ИдентификаторСкидки
		|ИЗ
		|	РегистрСведений.СкидкиШапки КАК СкидкиШапки
		|ГДЕ
		|	СкидкиШапки.Подразделение В ИЕРАРХИИ(&Подразделение)	
		|УПОРЯДОЧИТЬ ПО
		|	СкидкиШапки.ИдентификаторСкидки Убыв");
		Запрос.УстановитьПараметр("Подразделение",Подразделение);
		Результат=Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			ИдентификаторШапки=Результат.ИдентификаторСкидки;
		КонецЕсли;
		Запрос=Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СкидкиСтроки.ИдентификаторСкидки КАК ИдентификаторСкидки
		|ИЗ
		|	РегистрСведений.СкидкиСтроки КАК СкидкиСтроки
		|ГДЕ
		|   СкидкиСтроки.Подразделение В ИЕРАРХИИ(&Подразделение)
		|УПОРЯДОЧИТЬ ПО
		|	СкидкиСтроки.ИдентификаторСкидки Убыв");
		Запрос.УстановитьПараметр("Подразделение",Подразделение);
		Результат=Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			ИдентификаторСтроки=Результат.ИдентификаторСкидки;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ИдентификаторШапки) Тогда ИдентификаторШапки=0; КонецЕсли;
		Если обЗначениеНеЗаполнено(ИдентификаторСтроки) Тогда ИдентификаторСтроки=0; КонецЕсли;
		ИдентификаторСкидки=Макс(ИдентификаторШапки,ИдентификаторСтроки);
	КонецЕсли;
	
	Возврат ИдентификаторСкидки;
КонецФункции //рсПолучитьИдентификаторСкидкиШапки()

// Функция возвращает идентификатор строчной скидки
//
Функция рсПолучитьИдентификаторСкидкиСтроки(Параметры, ТекущийИдентификатор) Экспорт
	Перем Подразделение; // Подразделение
	ИдентификаторСкидки=ТекущийИдентификатор;
	// Проверим и обработаем переданные данные
	Если ТипЗнч(Параметры) <> Тип("Структура") Тогда
		Возврат Неопределено;
	Иначе
		// Создадим список обязательных параметров
		Обязательные = Новый Структура();
		Обязательные.Вставить("Подразделение",	Справочники.ПодразделенияКомпании.ПустаяСсылка());
		Обязательные.Вставить("РучнаяСкидка",	Неопределено);
		Обязательные.Вставить("Объект",			Справочники.Номенклатура.ПустаяСсылка());
		Обязательные.Вставить("НачВремя",		'00010101');
		Обязательные.Вставить("КонВремя",		'00010101');
		Обязательные.Вставить("ДниНедели",		Неопределено);
		Обязательные.Вставить("ЭтоПодарок",		Неопределено);
		Обязательные.Вставить("ДисконтнаяКарта",Справочники.Карточки.ПустаяСсылка());
		Обязательные.Вставить("ОтСуммыНакопленияНаКарте",0);
		Обязательные.Вставить("ЗалОбслуживания",Справочники.СкладыКомпании.ПустаяСсылка());
		Обязательные.Вставить("Свойство",		ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка());
		Обязательные.Вставить("ОтСуммыСтроки",	0);
		Обязательные.Вставить("ОтКоличества",	0);
		Обязательные.Вставить("Правило",		Перечисления.ПравилаРасчетаСкидкиПоКоличеству.ПустаяСсылка());
		Обязательные.Вставить("Скидка",			Справочники.ТипыСкидок.ПустаяСсылка());
		ЗаполнитьЗначенияСвойств(Обязательные, Параметры);
	КонецЕсли;
	
	// определим подразделение
	Обязательные.Свойство("Подразделение",Подразделение);
	Если Подразделение=Справочники.ПодразделенияКомпании.ПустаяСсылка() Тогда Возврат Неопределено; КонецЕсли;
	
	// попробуем найти похожую запись
	СкидкиСтроки=РегистрыСведений.СкидкиСтроки.СрезПоследних(,Обязательные);
	Если СкидкиСтроки.Количество()>0 Тогда
		ИдентификаторСкидки=СкидкиСтроки.Получить(0).ИдентификаторСкидки;
		ТекущийИдентификатор=ИдентификаторСкидки;
	КонецЕсли;
	
	// определим новый идентификатор
	Если обЗначениеНеЗаполнено(ИдентификаторСкидки) Тогда
		ИдентификаторШапки=0;
		ИдентификаторСтроки=0;
		Запрос=Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СкидкиШапки.ИдентификаторСкидки КАК ИдентификаторСкидки
		|ИЗ
		|	РегистрСведений.СкидкиШапки КАК СкидкиШапки
		|ГДЕ
		|	СкидкиШапки.Подразделение В ИЕРАРХИИ(&Подразделение)	
		|УПОРЯДОЧИТЬ ПО
		|	СкидкиШапки.ИдентификаторСкидки Убыв");
		Запрос.УстановитьПараметр("Подразделение",Подразделение);
		Результат=Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			ИдентификаторШапки=Результат.ИдентификаторСкидки;
		КонецЕсли;
		Запрос=Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СкидкиСтроки.ИдентификаторСкидки КАК ИдентификаторСкидки
		|ИЗ
		|	РегистрСведений.СкидкиСтроки КАК СкидкиСтроки
		|ГДЕ
		|   СкидкиСтроки.Подразделение В ИЕРАРХИИ(&Подразделение)
		|УПОРЯДОЧИТЬ ПО
		|	СкидкиСтроки.ИдентификаторСкидки Убыв");
		Запрос.УстановитьПараметр("Подразделение",Подразделение);
		Результат=Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			ИдентификаторСтроки=Результат.ИдентификаторСкидки;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ИдентификаторШапки) Тогда ИдентификаторШапки=0; КонецЕсли;
		Если обЗначениеНеЗаполнено(ИдентификаторСтроки) Тогда ИдентификаторСтроки=0; КонецЕсли;
		ИдентификаторСкидки=Макс(ИдентификаторШапки,ИдентификаторСтроки);
	КонецЕсли;
	
	Возврат ИдентификаторСкидки;
КонецФункции //рсПолучитьИдентификаторСкидкиСтроки()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УСТАНОВКИ ОБЩЕЙ СКИДКИ/НАЦЕНКИ

// Функция применяет полученные параметры скидки шапки к указанной табличной части документа
//
// Параметры:
//	ЭтотОбъект   - ДокументОбъект - Документ, для которого производится поиск скидки
//  ИмяТаблицы   – Строка         - Имя табличной части, к которой необходимо применить скидку
//  ЭтаФорма     - Форма          - Форма документа, для которого производится поиск скидки
//	ДопПараметры - Произвольный   - Дополнительные параметры, которые могут использоваться при расчете табличной части
//
// Возвращаемое значение:
//	БУЛЕВО – Признак успешного завершения действия
//
Функция рсПрименитьСкидкуШапкиКТабличнойЧасти(ЭтотОбъект, ИмяТаблицы="Товары", ЭтаФорма=Неопределено, ДопПараметры=Неопределено) Экспорт
	Результат = орПрименитьСкидкуШапкиКТабличнойЧасти(ЭтотОбъект, ИмяТаблицы, ЭтаФорма, ДопПараметры);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли; 
	
	// Получим основные объекты для расчета
	Попытка
		// Возможно необходимо выполнять пересчет не в табличной части документа, а, например, в обработке
		// табличной части "Товары". В этом случае в доп. параметрах придет альтернативная табличная часть или
		// таблица значений.
		Если (ДопПараметры<>Неопределено) И (ДопПараметры.Свойство("ТабличнаяЧасть")) Тогда
			Таблица = ДопПараметры.ТабличнаяЧасть; 
		Иначе
			Таблица = ЭтотОбъект[ИмяТаблицы]; 
		КонецЕсли;                             
		
		// Если таблица не содержит ни одной строки, то нет смысла в дальнейшем расчете
		Если Таблица.Количество()=0 Тогда
			Возврат ИСТИНА;
		КонецЕсли;
		
		// Получим общую скидку и ее параметры для расчета табличной части
		ПараметрыСкидкиШапки= ЭтотОбъект.ПараметрыСкидкиШапки;
		СкидкаНаценка		= ЭтотОбъект.СкидкаНаценка;
		
		// Произведем дополнительную проверку табличной части
		Результат = (Таблица[0].ПроцентСкидки="") ИЛИ (Таблица[0].СуммаСкидки="");
	Исключение
		Возврат ЛОЖЬ;
	КонецПопытки;
	
	// Определим каким образом у нас вычисляется НДС
	Попытка
		ЦенаВключаетНДС = ИСТИНА;
		ЦенаВключаетНДС = (обЗначениеНеЗаполнено(ЭтотОбъект.ТипЦен) ИЛИ ЭтотОбъект.ТипЦен.ЦенаВключаетНДС);
	Исключение КонецПопытки;
	
	// Определим сумму документа без скидок
	КоэффициентКурса= ?((ЭтотОбъект.КурсДокумента=0) ИЛИ (ЭтотОбъект.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()), 1, ЭтотОбъект.КурсДокумента);
	СуммаДокумента	= рсПолучитьСуммуДокументаБезСкидки(ЭтотОбъект, Таблица);
	
	// Определим основные показатели скидки
	Если обЗначениеНеЗаполнено(СкидкаНаценка) ИЛИ (НЕ ТипЗнч(ПараметрыСкидкиШапки)=Тип("ТаблицаЗначений")) ИЛИ (ПараметрыСкидкиШапки.Количество()=0) ИЛИ (ПараметрыСкидкиШапки[0].СуммаЧека/КоэффициентКурса>СуммаДокумента) Тогда
		Процентная		= ИСТИНА;
		ЗначениеСкидки	= 0;
	Иначе
		Процентная		=(ПараметрыСкидкиШапки[0].СпособВычисления=Перечисления.СкидкиСпособВычисления.Относительная);
		ЗначениеСкидки	= ПараметрыСкидкиШапки[0].ЗначениеСкидки;
	КонецЕсли;
	ЭтоНаценка = (ЗначениеСкидки < 0);
	
	// Определим коэффициент расчета скидки
	Если Процентная Тогда
		// Процент скидки не может быть более 100%
		КоэффициентСкидки = МИН(ЗначениеСкидки, 100);
	Иначе
		ЗначениеСкидки = ЗначениеСкидки / КоэффициентКурса;
		
		// Определим сумму по которой будет распределена скидка/наценка
		СуммаДляСкидки = 0;
		Для Каждого ТекСтрока Из Таблица Цикл
			Если ЭтоНаценка ИЛИ (ТекСтрока.Сумма > 0) Тогда
				СуммаДляСкидки=СуммаДляСкидки + обМод(ТекСтрока.Сумма) * ?(ЦенаВключаетНДС, 1, 1 + ТекСтрока.СтавкаНДС.Ставка / 100);
			КонецЕсли;
		КонецЦикла;
		
		// Важно чтобы (СуммаДляСкидки-ЗначениеСкидки) >= 0, изменим значение скидки
		ЗначениеСкидки = МИН(СуммаДляСкидки,ЗначениеСкидки);
		
		// Определяем коэффициент для расчета скидки
		КоэффициентСкидки = ?(СуммаДляСкидки=0,0,ЗначениеСкидки / СуммаДляСкидки);
	КонецЕсли;
	
	// Устанавливаем скидку на каждую строку документа
	Для Каждого ТекСтрока Из Таблица Цикл
		ТекСтрока_СуммаБезСкидки = ?(ЭтоНаценка ИЛИ (ТекСтрока.Сумма > 0), обМод(ТекСтрока.Сумма), 0) * ?(ЦенаВключаетНДС, 1, 1 + ТекСтрока.СтавкаНДС.Ставка / 100);
		Если Процентная Тогда
			ТекСтрока.ПроцентСкидки	= ?(ТекСтрока_СуммаБезСкидки=0, 0, КоэффициентСкидки);
			ТекСтрока.СуммаСкидки	= ТекСтрока.ПроцентСкидки / 100 * ТекСтрока_СуммаБезСкидки;
			ТекСтрока.СуммаСкидки	= дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценка);
		Иначе
			ТекСтрока.СуммаСкидки	= КоэффициентСкидки * ТекСтрока_СуммаБезСкидки;
			ТекСтрока.СуммаСкидки	= дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценка);
			ТекСтрока.ПроцентСкидки	= ?(ТекСтрока.СуммаСкидки=0, 0, КоэффициентСкидки * 100);
		КонецЕсли;
	КонецЦикла;
	ОстатокСкидки = ?(Процентная, 0, ЗначениеСкидки - Таблица.Итог("СуммаСкидки"));
	
	// В случае абсолютной скидки необходимо распределить "последнюю копейку",
	// а также выполним расчет прочих суммовых реквизитов таблицы
	Для Каждого ТекСтрока Из Таблица Цикл
		ТекСтрока_СуммаБезСкидки = ТекСтрока.Сумма * ?(ЦенаВключаетНДС, 1, 1 + ТекСтрока.СтавкаНДС.Ставка / 100);
		
		// Если остались не распределенные "копейки", то произведем уточнение текущей суммы скидки
		Если ОстатокСкидки <> 0 Тогда
			// Скидка не может стать наценкой, а наценка скидкой
			ДопСкидка = ?(ЭтоНаценка, МИН(ОстатокСкидки,-ТекСтрока.СуммаСкидки), МАКС(ОстатокСкидки,-ТекСтрока.СуммаСкидки));
			
			// Если назначается скидка, то сумма итого всегда должна быть не отрицательной
			ДопСкидка = ?(ЭтоНаценка, ДопСкидка, МИН(МАКС(ТекСтрока_СуммаБезСкидки, 0) - ТекСтрока.СуммаСкидки, ДопСкидка));
			
			// На дополнение к скидке наложены все ограничения, теперь произведем коррекцию текущей строки
			Если ДопСкидка <> 0 Тогда
				ТекСтрока.СуммаСкидки	= ТекСтрока.СуммаСкидки + ДопСкидка;
				ТекСтрока.СуммаСкидки	= дкОкруглитьСуммуСкидки(ТекСтрока.СуммаСкидки,СкидкаНаценка);
				ТекСтрока.ПроцентСкидки	= ?(ТекСтрока_СуммаБезСкидки=0, 0, 100 * ТекСтрока.СуммаСкидки / ТекСтрока_СуммаБезСкидки);
				ОстатокСкидки = ОстатокСкидки - ДопСкидка;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			// Выполним расчет прочих реквизитов строки
			ТекСтрока.СуммаВсего = ТекСтрока_СуммаБезСкидки - ТекСтрока.СуммаСкидки;
			Если ЦенаВключаетНДС Тогда
				ТекСтрока.СуммаНДС = (ТекСтрока.СуммаВсего * ТекСтрока.СтавкаНДС.Ставка) / (100 + ТекСтрока.СтавкаНДС.Ставка);
			Иначе
				ТекСтрока.СуммаНДС = (ТекСтрока.Сумма-ТекСтрока.СуммаСкидки) * ТекСтрока.СтавкаНДС.Ставка / 100;
			КонецЕсли;
		Исключение КонецПопытки;
	КонецЦикла;
	
	Возврат ИСТИНА;
КонецФункции // рсПрименитьСкидкуШапкиКТабличнойЧасти()

// Функция производит поиск, получение параметров и установку общей скидки на документ
//
// Параметры
//	ЭтотОбъект             - ДокументОбъект - Документ, для которого производится поиск скидки
//	НеЗаменятьПустуюСкидку - БУЛЕВО         - Если скидка пустая, то данным флагом можно запретить поиск
//											  новой автоматической скидки
//	ПолныйПересчет         - БУЛЕВО         - Указывает необходимость полного пересчета (выполнения запроса к базе)
//  ЭтаФорма               - Форма          - Форма документа, для которого производится поиск скидки
//	ДопПараметры           - Произвольный   - Дополнительные параметры, которые могут использоваться при
//											  расчете табличной части
//
// Возвращаемое значение:
//	БУЛЕВО – Признак успешного завершения действия
//
Функция рсУстановитьСкидкуШапки(ЭтотОбъект, НеЗаменятьПустуюСкидку=ЛОЖЬ, ПолныйПересчет=ИСТИНА, ЭтаФорма=Неопределено, ДопПараметры=Неопределено) Экспорт
	Перем СкидкаНаценка, ПараметрыСкидкиШапки; // Переменные процедуры
	
	// А. Проверка документа на предмет возможности работы со скидками
	Попытка
		СкидкаНаценка		=ЭтотОбъект.СкидкаНаценка;
		ПараметрыСкидкиШапки=ЭтотОбъект.ПараметрыСкидкиШапки;
	Исключение
		// Скидки документом не поддерживаются
		Возврат ЛОЖЬ;
	КонецПопытки;
	
	// Б. Если полный пересчет не нужен, но при этом кэш пуст, необходимо произвести дополнительный анализ
	Если НеЗаменятьПустуюСкидку И (НЕ ПолныйПересчет) И обЗначениеНеЗаполнено(СкидкаНаценка) тогда
		НеЗаменятьПустуюСкидку = (ПараметрыСкидкиШапки="НеЗаменятьПустуюСкидку");
	КонецЕсли;
	
	// В. Анализируем права: способ выбора и назначения скидки
	СпособВыбораСкидки = обПраво("СпособВыбораСкидки", ЭтотОбъект.Права,,ЭтотОбъект);
	Если НЕ (СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.АвтоматическиеИРучныеСкидки) Тогда
		Если СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.АвтоматическиеСкидки Тогда
			Если СкидкаНаценка.РучнаяСкидка Тогда
				СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецЕсли;
			НеЗаменятьПустуюСкидку = ЛОЖЬ;
		Иначе //(СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.СкидкиЗапрещены) ИЛИ (СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидки) ИЛИ (СпособВыбораСкидки=Перечисления.СкидкиСпособыВыбора.РучныеСкидкиИРедактированиеПроцентов)
			Если (СпособВыбораСкидки = Перечисления.СкидкиСпособыВыбора.СкидкиЗапрещены) ИЛИ (НЕ СкидкаНаценка.РучнаяСкидка) Тогда
				СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецЕсли;
			НеЗаменятьПустуюСкидку = ИСТИНА;
		КонецЕсли;
	КонецЕсли;
	
	// Г. Получение параметров скидки
	Если обЗначениеНеЗаполнено(СкидкаНаценка) И НеЗаменятьПустуюСкидку Тогда
		// Скидка не назначена и ее заменять нельзя, ...
		ПараметрыСкидкиШапки = "НеЗаменятьПустуюСкидку";
	Иначе
		// Определим сумму документа без учета скидок (в регламентной валюте)
		СуммаДокумента = рсПолучитьСуммуДокументаБезСкидки(ЭтотОбъект) * ?((ЭтотОбъект.КурсДокумента=0) ИЛИ (ЭтотОбъект.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить()), 1, ЭтотОбъект.КурсДокумента);
		
		// Если требуется полный пересчет скидки или кэш не обнаружен, то выполняем запрос к базе
		Если ПолныйПересчет ИЛИ (НЕ ТипЗнч(ПараметрыСкидкиШапки)=Тип("ТаблицаЗначений")) Тогда
			// Сформируем специальную структуру отбора
			Отбор = Новый Структура("ПодразделениеКомпании,Карточка,СкладКомпании,ЭтоПодарок,БэкОфис,ФронтОфис,Терминал", Справочники.ПодразделенияКомпании.ОсновноеПодразделение, Справочники.Карточки.ПустаяСсылка(), Справочники.СкладыКомпании.ПустаяСсылка(), ЛОЖЬ, ИСТИНА);
			Отбор.Вставить("Скидка",		?(СкидкаНаценка.РучнаяСкидка, СкидкаНаценка, Неопределено));
			Отбор.Вставить("РучнаяСкидка",	СкидкаНаценка.РучнаяСкидка);
			Отбор.Вставить("БазаРасчета",	СуммаДокумента);
			Отбор.Вставить("СкидкаНаТовары",Истина);
			
			// Заполним отбор данными из объекта
			ЗаполнитьЗначенияСвойств(Отбор, ЭтотОбъект);
			
			// Дополним структуру отбора дополнительными значениями из переменной ДопПараметры
			Попытка
				ЗаполнитьЗначенияСвойств(Отбор, ДопПараметры);	//"ЭтоПодарок,БэкОфис,ФронтОфис,Терминал"
			Исключение КонецПопытки;
			
			// Выполним запрос к информационной базе для получения параметров скидки
			ПараметрыСкидкиШапки = рсСкидкиПолучитьШапочные(ЭтотОбъект.Дата, Отбор, СуммаДокумента);
		Иначе
			// Ищем подходящую скидку (строку параметров)
			Для Каждого ТекСкидка Из ПараметрыСкидкиШапки Цикл
				ТекСкидка.СкидкаДействует = (ТекСкидка.СуммаЧека <= СуммаДокумента);
				ТекСкидка.СуммаСкидки = ?(ТекСкидка.СпособВычисления=Перечисления.СкидкиСпособВычисления.Абсолютная, ТекСкидка.ЗначениеСкидки, СуммаДокумента * ТекСкидка.ЗначениеСкидки / 100);
				ТекСкидка.СуммаСкидки = дкОкруглитьСуммуСкидки(ТекСкидка.СуммаСкидки, ТекСкидка.Скидка);
			КонецЦикла;
			ПараметрыСкидкиШапки.Сортировать("СкидкаДействует УБЫВ, ЭтоПодарок УБЫВ, ФлагВытеснения УБЫВ, СуммаСкидки УБЫВ, СуммаЧека УБЫВ");
		КонецЕсли;
		
		// Если производился поиск автоматической скидки, то необходимо заменить текущую найденной или очистить
		Если НЕ СкидкаНаценка.РучнаяСкидка Тогда
			Попытка
				//СкидкаНаценка = ?(ПараметрыСкидкиШапки[0].СуммаЧека<=СуммаДокумента,
				//ПараметрыСкидкиШапки[0].Скидка, ?(ПолныйПересчет, Справочники.ТипыСкидок.ПустаяСсылка(),
				//СкидкаНаценка));
				СкидкаНаценка = ?(ПараметрыСкидкиШапки[0].СуммаЧека<=СуммаДокумента, ПараметрыСкидкиШапки[0].Скидка, Справочники.ТипыСкидок.ПустаяСсылка());
			Исключение
				//СкидкаНаценка = ?(ПолныйПересчет, Справочники.ТипыСкидок.ПустаяСсылка(), СкидкаНаценка);
				СкидкаНаценка = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	// Если в процессе расчета скидка изменилась, то необходимо внести изменения в объект
	Если ЭтотОбъект.СкидкаНаценка<>СкидкаНаценка Тогда
		ЭтотОбъект.СкидкаНаценка = СкидкаНаценка;
	КонецЕсли;
	ЭтотОбъект.ПараметрыСкидкиШапки=ПараметрыСкидкиШапки;
	
	// Д. Параметры скидки шапки получены. Теперь следует применить полученную скидку к табличным частям документа
	Возврат ЭтотОбъект.ОбработкаРеквизита("ПрименитьСкидкуКТабличнымЧастям",, ЭтаФорма, ДопПараметры);
КонецФункции // рсУстановитьСкидкуШапки()

// Процедура - Инициализировать код состояния
//  Устанавливает код состояния 0, 1, 3, 7 в зависимости от Прав и настроек
//
Процедура ИнициализироватьКодСостояния() Экспорт
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	СтатусСервера = Цел(КодСостояния/10);
	// Статусы сервиса
	//(0, "Сервис не используется в системе");
	//(1, "Не указаны параметры партнера"); // Партнер еще не подключен к сервису?
	//(2, "Параметры партнера заполнены"); // Статус сервера не проверялся
	//(3, "Сервер не доступен"); // Пинг не прошел
	//(4, "Сервис доступен"); // Ок, Пинг партнера прошел
	//(5, "Ошибка данных партнера"); // Err, Пинг партнера не прошел
	//(6, "Доступ запрещен"); // Err, Пинг партнера не прошел
	
	СтатусПользователя = КодСостояния%10;
	// Статусы пользователя
	//(0, "Доступ запрещен");
	//(1, "Не указан логин");
	//(2, "Ожидание авторизации");
	//(3, "Авторизован"); // Ок
	//(4, "Отказ от авторизации");
	//(5, "Ошибка авторизации"); // Err
	
	ИспользоватьИнтеграцию = обПраво("атИспользоватьИнтеграцию") = Истина;
	НеИспользоватьПомощникПродаж = обПраво("атНеИспользоватьПомощникПродаж") = Истина;
	
	Если НеИспользоватьПомощникПродаж Тогда
		
		Если СтатусПользователя <> 0 Тогда
			// Пользователю запретили использовать сервис Помощник продаж
			СтатусПользователя = 0;
		КонецЕсли;
		
	Иначе
		
		Если СтатусПользователя = 0 Тогда
			Если Не ПустаяСтрока(ПолучитьНастройку(ПараметрыСеанса.Пользователь, "Логин")) Тогда
				СтатусПользователя = 2; // Ожидаем авторизации
			Иначе
				СтатусПользователя = 1; // Не указан логин
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
		
	Если ИспользоватьИнтеграцию Тогда
		
		Если СтатусСервера = 0 Тогда
			// Установили использовать сервис
			Если ПараметрыПартнераЗаполнены() Тогда
				СтатусСервера = 2; // Сохранены логин и пароль партнера
			Иначе
				СтатусСервера = 1; // Не указаны данные партнера
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Если СтатусСервера <> 0 Тогда
			// Решили не использовать сервис
			СтатусСервера = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыСеанса.атКодСостояния = 10*СтатусСервера + СтатусПользователя;
	
КонецПроцедуры

Функция ПараметрыПартнераЗаполнены()
	Возврат ЗначениеЗаполнено(ПолучитьНастройку("Партнер", "ПользовательПартнера")) И ЗначениеЗаполнено(ПолучитьНастройку("Партнер", "ПарольПартнера"));
КонецФункции

// Функция - Сервис доступен
//  Проверяет физическую доступность сервера "Помощник продаж"
//  устанавливает КодСостояния = 2, если сервер не доступен
//
// Параметры:
//  Ошибка	 - Строка	 - Переменная для возврата описания ошибки клиенту
// 
// Возвращаемое значение:
//  Булево - признак доступности сервиса партнеру
//
Функция СервисДоступен(Ошибка = "", Пинг = Истина) Экспорт 
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	СтатусСервера = Цел(КодСостояния/10);
	СтатусПользователя = КодСостояния%10;
	
	Если СтатусСервера = 0 Тогда
		Ошибка = "В программе не используется интеграция с сервисом.
				|Для подключения и настройки сервиса можно воспользоваться меню:
				|Сервис - Настройка параметров - закладка ""Помощник продаж"".";
		Возврат Ложь;
	КонецЕсли;
	
	Если СтатусПользователя = 0 Тогда
		Ошибка = "Пользователю запрещено использовать сервис.";
		Возврат Ложь;
	КонецЕсли;
	
	Если Пинг ИЛИ СтатусСервера = 2 Тогда
		// Требуется пинг сервера или заполнены параметры партнера, но сервис еще не проверялся
		УстановитьСтатусСервера(Ошибка);
	КонецЕсли;
	
	Возврат Цел(ПараметрыСеанса.атКодСостояния/10) = 4;
	
КонецФункции

// Функция - Установить статус сервера
//  Проверяет параметры программы и физическую доступность сервера "Помощник продаж"
//  устанавливает параметр серанса атКодСостояния в соотстветсвии со полученнным ответом сервера.
//  Вызывается только если разрешено использовать интеграцию с сервисом "Помощник продаж"
//
// Параметры:
//  Ошибка	 - Строка	 - Переменная для возврата описания ошибки клиенту
// 
Процедура УстановитьСтатусСервера(Ошибка) Экспорт 
	
	// Статусы сервиса
	//(0, "Сервис не используется");
	//(1, "Не указаны параметры партнера"); // Партнер еще не подключен к сервису?
	//(2, "Параметры партнера заполнены"); // Статус сервера не проверялся
	//(3, "Сервер не доступен"); // Пинг не прошел
	//(4, "Сервис доступен"); // Ок, Пинг партнера прошел
	//(5, "Ошибка данных партнера"); // Err, Пинг партнера не прошел
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	СтатусСервера = Цел(КодСостояния/10);
	СтатусПользователя = КодСостояния%10;
	
	Результат = ВыполнитьЗапрос("registration", "Пинг");
	
	Если Результат = Неопределено Тогда
		
		Ошибка = "Сервер ""Альфа-Авто: ""Помощник продаж"" не отвечает.
				|Возможно не правильно указан адрес или порт сервера.
				|Возможно адрес заблокирован брандмаузером.
				|Возможно временные неполадки на сервере.";
		СтатусСервера = 3; // Сервер не доступен
		
	Иначе
		
		Если Не ПараметрыПартнераЗаполнены() Тогда
			
			Ошибка = "Не указаны параметры партнера.";
			СтатусСервера = 1; // Не указаны параметры партнера
			
		Иначе
			
			СтатусСервера = 2; // Заполнены параметры партнера
			
			Результат = ВыполнитьЗапрос("partner", "Пинг");
			Если Результат = Неопределено Тогда
				Ошибка = "Ошибка авторизации партнера на сервере.";
				СтатусСервера = 5; // Ошибка данных партнера
			Иначе
				// Проверка доступности для партнера успешно пройдена
				СтатусСервера = ?(Результат, 4, 6); // Сервис доступен
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыСеанса.атКодСостояния = СтатусСервера*10 + СтатусПользователя;
	
КонецПроцедуры


Функция ПолучитьТекстСообщения(СтруктураСообщения) Экспорт 
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(Новый ПараметрыЗаписиJSON());
	НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON;
	//НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	ЗаписатьJSON(Запись, СтруктураСообщения, НастройкиСериализацииJSON);
	
	ТекстСообщения = Запись.Закрыть();
	
	ТекстСообщения = СтрЗаменить(ТекстСообщения,Символы.ВК,"");
	
	ТекстСообщения = СокрЛП(ТекстСообщения);
	
	Возврат ТекстСообщения;
	
КонецФункции

Функция ПолучитьСтруктуруСообщения(ТекстСообщения) Экспорт 
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстСообщения);
	СтруктураJSON = ПрочитатьJSON(ЧтениеJSON,,, ФорматДатыJSON.ISO);
	ЧтениеJSON.Закрыть();
	
	Возврат СтруктураJSON;
	
КонецФункции

// Функция - Выполнить запрос
//  Выполняет предварительные проверки и запрос на сервер
//
// Параметры:
//  ИмяСервиса		 - Строка	 - Сервис, к которому адресован запрос
//  ИмяМетода		 - Строка	 - Имя выполняемого метода
//  ТелоЗапроса		 - Строка	 - Сформированное тело запроса
//  ИмяМетода2		 - Строка	 - Для сервиса dictionaries - "Объект", "Список", "Создать" и т.д.
//  ИспользоватьКеш	 - Булево	 - Признак возможности использования кешированных на время сеанса данных
//  Ошибка			 - Строка	 - Переменная для возврата описания ошибки клиенту
// 
// Возвращаемое значение:
//   - Ответ сервиса
//
Функция ВыполнитьЗапрос(Знач ИмяСервиса, Знач ИмяМетода, Знач ТелоЗапроса = "", Знач ИмяМетода2 = "", Знач ИспользоватьКеш = Ложь, Ошибка = "") Экспорт 
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	
	СтатусСервера = Цел(КодСостояния/10);
	СтатусПользователя = КодСостояния%10;
	
	Если СтатусПользователя = 0 Тогда
		// Запрещено использовать, больше ничего не проверяем
		Ошибка = "Пользователю запрещено использовать сервис ""Помощник продаж""";
		Возврат Неопределено;
	КонецЕсли;
	
	Если нРег(ИмяМетода) = "пинг" ИЛИ нРег(ИмяМетода) = "ping" Тогда
		// Пинги можем делать при любом коде состояния
		// при этом не будем использовать Кеш
		ИспользоватьКеш = Ложь;
		
	Иначе
		
		Если СтатусСервера = 5 Тогда
			// Ранее пинг вернул ошибку 401
			Ошибка = "Не верные параметры партнера";
			Возврат Неопределено;
			
		ИначеЕсли СтатусСервера = 6 Тогда
			// Ранее пинг вернул запрет доступа
			Ошибка = "Доступ к сервису запрещен";
			Возврат Неопределено;
			
		ИначеЕсли СтатусСервера = 3 Тогда
			// Ранее пинг не прошел
			Ошибка = "Сервер ""Помощник продаж"" временно недоступен";
			Возврат Неопределено;
			
		ИначеЕсли ИмяСервиса <> "registration" И СтатусПользователя <> 3 Тогда
			// Требуется авторизация, а пользователь еще не авторизовался!
			Ошибка = "Пользователь не авторизован";
			Возврат Неопределено;
			
		КонецЕсли;
		
		Если ПараметрыСеанса.атДоступныеСервисы.Свойство("Сервисы") Тогда
			
			ДоступныеСервисы = ПараметрыСеанса.атДоступныеСервисы.ДоступностьСервисов;
			
			Если ДоступныеСервисы.Свойство(ИмяСервиса) И НЕ ДоступныеСервисы[ИмяСервиса] Тогда
				// Попытка обращения к недоступному для пользователя сервису
				Ошибка = "Сервис """+ИмяСервиса+""" запрещен пользователю";
				Возврат Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если ИспользоватьКеш Тогда
		Возврат атПовторноеИспользование.ВыполнитьЗапросКСервису(ИмяСервиса, ИмяМетода, ИмяМетода2, ТелоЗапроса, Ошибка);
	Иначе
		Возврат ВыполнитьЗапросКСервису(ИмяСервиса, ИмяМетода, ИмяМетода2, ТелоЗапроса, Ошибка);
	КонецЕсли;
	
КонецФункции

// Функция - Выполнить запрос к сервису
//  Выполняет непосредственно запрос на сервер. Все предварительные проверки выполнены
//
// Параметры:
//  ИмяСервиса		 - Строка	 - Сервис, к которому адресован запрос
//  ИмяМетода		 - Строка	 - Имя выполняемого метода
//  ИмяМетода2		 - Строка	 - Для сервиса dictionaries - "Объект", "Список", "Создать" и т.д.
//  ТелоЗапроса		 - Строка	 - Сформированное тело запроса
//  Ошибка			 - Строка	 - Переменная для возврата описания ошибки клиенту
// 
// Возвращаемое значение:
//   - Ответ сервиса
//
Функция ВыполнитьЗапросКСервису(Знач ИмяСервиса, Знач ИмяМетода, Знач ИмяМетода2, Знач ТелоЗапроса = "", Ошибка = "") Экспорт 
	
	Результат = Неопределено;
	
	ТребуетсяАвторизацияПартнера = (ИмяСервиса <> "registration");
	ТребуетсяАвторизацияПользователя = ТребуетсяАвторизацияПартнера И НЕ (нРег(ИмяМетода) = "пинг" ИЛИ нРег(ИмяМетода) = "ping");
	
	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("ПолныйАдресМетода", "alfatouch/hs/"+СокрЛП(ИмяСервиса)+"/"+СокрЛП(ИмяМетода)+?(ПустаяСтрока(ИмяМетода2), "", "/"+СокрЛП(ИмяМетода2)));
	СтруктураЗапроса.Вставить("АвторизацияПартнера", ТребуетсяАвторизацияПартнера);
	СтруктураЗапроса.Вставить("Заголовки", СформироватьЗаголовки(ТребуетсяАвторизацияПользователя));
	
	Если ТребуетсяАвторизацияПользователя Тогда
		СтруктураЗапроса.ПолныйАдресМетода = СтруктураЗапроса.ПолныйАдресМетода + "?login=" + ПолучитьНастройку(ПараметрыСеанса.Пользователь, "Логин");
	КонецЕсли;
	
	СтруктураЗапроса.Вставить("ТелоЗапроса", ТелоЗапроса);
	
	HTTPОтвет = ВыполнитьHTTPЗапрос(СтруктураЗапроса);
	
	Если HTTPОтвет = Неопределено Тогда
		
		Ошибка = "Ошибка при обращении к серверу. Подробности в журнале регистрации.";
		
	Иначе
		
		Если нРег(ИмяМетода) = "пинг" ИЛИ нРег(ИмяМетода) = "ping" Тогда
			
			Если HTTPОтвет.КодСостояния = 204 Тогда
				
				// Все хорошо
				Результат = Истина;
				
			ИначеЕсли HTTPОтвет.КодСостояния = 402 Тогда
				
				// Доступ запрещен
				Результат = Ложь;
				
			КонецЕсли;
			
		Иначе
			
			Если HTTPОтвет.КодСостояния = 200 Тогда
				
				Результат = ПолучитьСтруктуруСообщения(HTTPОтвет.ПолучитьТелоКакСтроку()).Данные;
				
			ИначеЕсли HTTPОтвет.КодСостояния = 204 Тогда
				
				Результат = Истина;
				
			Иначе
				
				ЗаписатьВЖурналРегистрации = Истина;
				
				Ответ = HTTPОтвет.ПолучитьТелоКакСтроку();
				Попытка
					Ошибки = ПолучитьСтруктуруСообщения(Ответ).Ошибки;
					Для Каждого Элемент Из Ошибки Цикл
						Ошибка = Ошибка + Элемент + Символы.ПС;
					КонецЦикла;
					ОшибкаВебСервера = Ложь;
				Исключение
					Ошибка = "Ошибка на сервере при выполнении запроса. Подробности в журнале регистрации.";
					ОшибкаВебСервера = Истина;
				КонецПопытки;
				
				Сообщение = "Код состояния - " + HTTPОтвет.КодСостояния + ".";
				
				Если HTTPОтвет.КодСостояния = 400 Тогда
					// Ошибка параметров запроса
					Сообщение = Сообщение + " Ошибка параметров запроса.";
					
				ИначеЕсли HTTPОтвет.КодСостояния = 401 Тогда
					// Недостаточно прав пользователя или ошибка авторизации
					ЗаписатьВЖурналРегистрации = ОшибкаВебСервера;
					
				ИначеЕсли HTTPОтвет.КодСостояния = 402 Тогда
					// Превышен лимит запросов
					ЗаписатьВЖурналРегистрации = Ложь;
					Ошибка = "Превышен лимит запросов.";
					
				ИначеЕсли HTTPОтвет.КодСостояния = 500 Тогда
					// Внутренняя ошибка сервера
					Сообщение = Сообщение + " Внутренняя ошибка сервера.";
					
				КонецЕсли;
				
				Если ЗаписатьВЖурналРегистрации Тогда
					ДопИнформация = "";
					ДобавитьСтроку(ДопИнформация, "Запрос: " + СтруктураЗапроса.ПолныйАдресМетода);
					ДобавитьСтроку(ДопИнформация, "Тело запроса: " + ТелоЗапроса);
					ДобавитьСтроку(ДопИнформация, "Ответ: " + Ответ);
					
					СоздатьЗаписьЖурналаРегистрации("HTTP-запрос", Сообщение, ДопИнформация, Истина);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВыполнитьHTTPЗапрос(Знач СтруктураЗапроса)
	
	HTTPОтвет = Неопределено;
	
	HTTPСоединение = атПовторноеИспользование.СоздатьHTTPСоединение(СтруктураЗапроса.АвторизацияПартнера);
	
	Если HTTPСоединение = Неопределено Тогда
		Возврат HTTPОтвет;
	КонецЕсли;
	
	Попытка
		
		HTTPЗапрос = Новый HTTPЗапрос(СтруктураЗапроса.ПолныйАдресМетода, СтруктураЗапроса.Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(СтруктураЗапроса.ТелоЗапроса);
		HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос); // POST запрос
		
	Исключение
		
		ТекстОшибки = "Ошибка при отправке запроса на сервер";
		ДопИнформация = "";
		ДобавитьСтроку(ДопИнформация, "Запрос: " + СтруктураЗапроса.ПолныйАдресМетода);
		ДобавитьСтроку(ДопИнформация, "Тело запроса: " + СтруктураЗапроса.ТелоЗапроса);
		ДобавитьСтроку(ДопИнформация, "Описание ошибки: " + ОписаниеОшибки());
		
		СоздатьЗаписьЖурналаРегистрации("HTTP-запрос", ТекстОшибки, ДопИнформация, Истина);
	КонецПопытки;
	
	Возврат HTTPОтвет;
	
КонецФункции

Функция СоздатьHTTPСоединение(АвторизацияПартнера) Экспорт 
	
	HTTPСоединение = Неопределено;
	
	Попытка
		
		Если АвторизацияПартнера Тогда
			HTTPСоединение = Новый HTTPСоединение(
				ПолучитьНастройку("Партнер", "АдресСервиса"),
				ПолучитьНастройку("Партнер", "ПортСервиса"),
				ПолучитьНастройку("Партнер", "ПользовательПартнера"),
				ПолучитьНастройку("Партнер", "ПарольПартнера")
				);
		Иначе
			HTTPСоединение = Новый HTTPСоединение(
				атСервер.ПолучитьНастройку("Партнер", "АдресСервиса"),
				атСервер.ПолучитьНастройку("Партнер", "ПортСервиса")
				);
		КонецЕсли;
		
	Исключение
		
		ДопИнформация = "";
		ТекстОшибки = ОписаниеОшибки();
		ДобавитьСтроку(ДопИнформация, "Хост: " + ПолучитьНастройку("Партнер", "АдресСервиса"));
		ДобавитьСтроку(ДопИнформация, "Порт: " + Формат(ПолучитьНастройку("Партнер", "ПортСервиса"), "ЧГ=0"));
		ДобавитьСтроку(ДопИнформация, "Basic-авторизация: " + ?(АвторизацияПартнера, "Да", "Нет"));
		СоздатьЗаписьЖурналаРегистрации("HTTP-Соединение", ТекстОшибки, ДопИнформация, Истина);
		
	КонецПопытки;
	
	Возврат HTTPСоединение;
	
КонецФункции

функция СформироватьЗаголовки(ЗаголовокАвторизации = Истина)
	
	ВсеЗаголовки = Новый Соответствие;
	ВсеЗаголовки.Вставить("Accept", "application/json");
	ВсеЗаголовки.Вставить("Content-Type", "application/json");
	ВсеЗаголовки.Вставить("Accept-Charset", "utf-8");
	
	Если ЗаголовокАвторизации Тогда 
		ВсеЗаголовки.Вставить("UID", ПараметрыСеанса.атИдентификаторПодключения);
	КонецЕсли;
	
	Возврат ВсеЗаголовки;
	
КонецФункции

Процедура СоздатьЗаписьЖурналаРегистрации(Знач ИмяСобытия, Знач Комментарий, Знач ДополнительнаяИнформация = "", Знач ЭтоОшибка = Ложь) 
	
	ТекстКомментария = "";
	Если ЭтоОшибка Тогда
		ИмяСобытия = "Сервис ""Помощник продаж"". " + Строка(ИмяСобытия);
		ДобавитьСтроку(ТекстКомментария, "Ошибка:");
		ДобавитьСтроку(ТекстКомментария, Комментарий);
		Если Не ПустаяСтрока(ДополнительнаяИнформация) Тогда
			ДобавитьСтроку(ТекстКомментария, "Дополнительная информация:");
			ДобавитьСтроку(ТекстКомментария, ДополнительнаяИнформация);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,,ТекстКомментария,);
	Иначе
		ИмяСобытия = "Сервис ""Помощник продаж"". " + Строка(ИмяСобытия);
		ДобавитьСтроку(ТекстКомментария, "Предупреждение:");
		ДобавитьСтроку(ТекстКомментария, Комментарий);
		Если Не ПустаяСтрока(ДополнительнаяИнформация) Тогда
			ДобавитьСтроку(ТекстКомментария, "Дополнительная информация:");
			ДобавитьСтроку(ТекстКомментария, ДополнительнаяИнформация);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Предупреждение,,,ТекстКомментария,);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьСтроку(ГлавнаяСтрока, Знач ДобавляемаяСтрока)
	ДобавляемаяСтрока = Строка(ДобавляемаяСтрока);
	Если Не ПустаяСтрока(ГлавнаяСтрока) И Не ПустаяСтрока(ДобавляемаяСтрока) Тогда
		ГлавнаяСтрока = ГлавнаяСтрока + Символы.ПС;
	КонецЕсли;
	ГлавнаяСтрока = ГлавнаяСтрока + ДобавляемаяСтрока;
КонецПроцедуры


Процедура ЗаписатьНастройку(Знач Объект, Знач ВидНастройки, Знач Значение, Знач ДополнительныеДанные = Неопределено) Экспорт 
	
	МенеджерЗаписи = РегистрыСведений.атНастройки.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВидНастройки = ВидНастройки;
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.Значение = Значение;
	Если ТипЗнч(ДополнительныеДанные) = Тип("ХранилищеЗначения") Тогда 
		МенеджерЗаписи.ДополнительныеДанные = ДополнительныеДанные;
	ИначеЕсли ЗначениеЗаполнено(ДополнительныеДанные) Тогда
		МенеджерЗаписи.ДополнительныеДанные = Новый ХранилищеЗначения(ДополнительныеДанные);
	КонецЕсли;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция ПолучитьНастройку(Знач Объект, Знач ВидНастройки, Знач ИспользоватьКеш = Ложь) Экспорт 
	
	Если ИспользоватьКеш Тогда
		Возврат атПовторноеИспользование.ПолучитьНастройку(Объект, ВидНастройки);
	КонецЕсли;
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	атНастройки.Значение
	|ИЗ
	|	РегистрСведений.атНастройки КАК атНастройки
	|ГДЕ
	|	атНастройки.ВидНастройки = &ВидНастройки
	|	И атНастройки.Объект = &Объект";
	Запрос.УстановитьПараметр("ВидНастройки", ВидНастройки);
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Значение;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьДополнительныеДанные(Знач Объект, Знач ВидНастройки) Экспорт 
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	атНастройки.ДополнительныеДанные
	|ИЗ
	|	РегистрСведений.атНастройки КАК атНастройки
	|ГДЕ
	|	атНастройки.ВидНастройки = &ВидНастройки
	|	И атНастройки.Объект = &Объект";
	
	Запрос.УстановитьПараметр("ВидНастройки", ВидНастройки);
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ДополнительныеДанные.Получить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СохранитьПравоНастройку(Знач Объект, Знач ПравоНастройка, Знач Значение, ЗначенияПрав=Неопределено) Экспорт 
	
	МенеджерЗаписи = РегистрыСведений.ПраваИНастройки.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.ПравоНастройка = ПравоНастройка;
	МенеджерЗаписи.Значение = Значение;
	МенеджерЗаписи.Записать();
	
	Если ЗначенияПрав <> Неопределено Тогда
		ЗначенияПрав.Вставить(ПравоНастройка, Значение);
	КонецЕсли;
	
КонецПроцедуры



Процедура ДобавитьДанныеКонфигурации(СтруктураПараметров)
	
	СтруктураПараметров.Данные.Вставить("ПрограммныйПродукт", Новый Структура("Версия, Наименование", Метаданные.Версия, Метаданные.Синоним));
	
КонецПроцедуры

// Функция - Выполнить регистрация партнера
//
// Параметры:
//  СтруктураПараметров	 - Структура	 - Параметры необходимые для регистрации нового партнера сервиса
// 
// Возвращаемое значение:
//  Структура - Ответ сервиса
//
Функция ВыполнитьРегистрацияПартнера(Знач СтруктураПараметров, Ошибка="") Экспорт 
	
	ДобавитьДанныеКонфигурации(СтруктураПараметров);
	
	Возврат ВыполнитьЗапрос("registration", "РегистрацияПартнера", ПолучитьТекстСообщения(СтруктураПараметров),,, Ошибка);
	
КонецФункции

// Функция - Выполнить подтверждение подключения
//  Сохраняет полученные словари в регистр атСловари
//
// Параметры:
//  КодПодтверждения - Строка	 - Строка, полученная на почту после регистрации партнера
// 
// Возвращаемое значение:
//  Структура - Ответ сервиса
//
Функция ВыполнитьПодтверждениеПодключения(Знач КодПодтверждения, Ошибка = "") Экспорт 
	
	СтруктураПараметров = Новый Структура("Данные", Новый Структура("КодПодтверждения", КодПодтверждения));
	
	РезультатЗапроса = ВыполнитьЗапрос("registration", "Подтверждение", ПолучитьТекстСообщения(СтруктураПараметров),,, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		
		ЗаписатьЭлементы(РезультатЗапроса);
		
	КонецЕсли;
	
	Возврат РезультатЗапроса; 
	
КонецФункции

// Функция - Сохранить данные партнера
//
// Параметры:
//  СтруктураПараметров	 - Структура	 - Параметры необходимые для регистрации нового партнера сервиса
// 
// Возвращаемое значение:
//  Структура - Ответ сервиса
//
Функция СохранитьДанныеПартнера(Знач СтруктураПараметров, Ошибка="") Экспорт 
	
	Для Каждого КлючЗначение Из СтруктураПараметров Цикл
		// Надо обновить словари в сервисе
		СтруктураЗапроса = Новый Структура("Отбор, Данные",
						   Новый Структура("Код", КлючЗначение.Значение.Код),
						   КлючЗначение.Значение);
		Результат = ВыполнитьЗапрос("dictionaries", КлючЗначение.Значение.Тип, ПолучитьТекстСообщения(СтруктураЗапроса), "Изменить",, Ошибка);
		Если Результат <> Неопределено Тогда
			ЗаписатьЭлемент(Результат);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ВычислитьХешЗапроса(Знач Структура)
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	
	Для Каждого КлючЗначение ИЗ Структура Цикл
		ХешированиеДанных.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат СтрЗаменить(ХешированиеДанных.ХешСумма, " ","");
	
КонецФункции

Функция ВыполнитьЗапросАвторизации(Знач Логин, Знач Пароль, Ошибка="") Экспорт
	
	Результат = Ложь;
	
	КодСостояния = ПараметрыСеанса.атКодСостояния;
	СтатусСервера = Цел(КодСостояния/10);
	СтатусПользователя = КодСостояния%10;
	
	Если СтатусСервера <> 4 Тогда
		// Пинг партнера не прошел
		Возврат Неопределено;
	КонецЕсли;
	
	ТекДата = Формат(ТекущаяУниверсальнаяДата(), "ДФ=yyyy-MM-ddTHH:mm:ss");
	Хеш = ВычислитьХешЗапроса(Новый Структура("Логин, ТекДата, Пароль", Логин, ТекДата, Пароль));
	
	ПолныйАдресМетода = "alfatouch/hs/partner/Auth?login="+Логин+"&key="+ТекДата+"&hash="+Хеш;
	
	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("ПолныйАдресМетода", ПолныйАдресМетода);
	СтруктураЗапроса.Вставить("АвторизацияПартнера", Истина);
	СтруктураЗапроса.Вставить("Заголовки", СформироватьЗаголовки(Ложь));
	СтруктураЗапроса.Вставить("ТелоЗапроса", "");
	
	HTTPОтвет = ВыполнитьHTTPЗапрос(СтруктураЗапроса);
	
	Если HTTPОтвет = Неопределено Тогда
		
		// Статус пользователя не меняется
		
	ИначеЕсли HTTPОтвет.КодСостояния = 200 Тогда
		
		РезультатЗапроса = ПолучитьСтруктуруСообщения(HTTPОтвет.ПолучитьТелоКакСтроку()).Данные;
		
		РезультатЗапроса.Вставить("ДоступностьСервисов", Новый Структура);
		Для Каждого КлючЗначение Из РезультатЗапроса.Сервисы Цикл
			РезультатЗапроса.ДоступностьСервисов.Вставить(КлючЗначение.ИмяСервиса, КлючЗначение.Доступен);
		КонецЦикла;
		
		ПараметрыСеанса.атДоступныеСервисы = ФиксированныеДанные(РезультатЗапроса);
		
		СтатусПользователя = 3; // Авторизован
		
		ИдентификаторПодключения = HTTPОтвет.Заголовки.Получить("UID");
		
		Если ИдентификаторПодключения <> Неопределено Тогда
			ПараметрыСеанса.атИдентификаторПодключения = ИдентификаторПодключения;
		КонецЕсли;
		
		Результат = Истина;
		
	Иначе
		
		СтатусПользователя = ?(ЗначениеЗаполнено(Логин), 2, 1);
		
		Если HTTPОтвет.КодСостояния = 401 Тогда
			Ошибка = "Неправильный логин или пароль";
		Иначе
			Ошибка = HTTPОтвет.ПолучитьТелоКакСтроку(); // Пока так сделаем
		КонецЕсли;
		
		
	КонецЕсли;
	
	ПараметрыСеанса.атКодСостояния = СтатусСервера*10 + СтатусПользователя;
	
	Возврат Результат;
	
КонецФункции

// Функция - Изменить пароль пользователя
//
// Параметры:
//  Логин		 - Строка	 - Код пользователя сервиса
//  НовыйПароль	 - Строка	 - Новый пароль пользователя
// 
// Возвращаемое значение:
//   - 
//
Функция ИзменитьПарольПользователя(Знач Логин, Знач НовыйПароль, Ошибка = "") Экспорт 
	
	СтруктураПараметров = Новый Структура("Отбор, Данные",
						  Новый Структура("Код", Логин),
						  Новый Структура("Пароль", НовыйПароль));
	
	РезультатЗапроса = ВыполнитьЗапрос("dictionaries", "Пользователи", ПолучитьТекстСообщения(СтруктураПараметров), "Изменить",, Ошибка);
	
	Возврат РезультатЗапроса <> Неопределено;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////DICTIONARIES/////////////////////////////////////////////


// Процедура - Обновить список словаря
//  получает список элементов из сервиса и синхронизирует сохраненные в базе данные
//
// Параметры:
//  ИмяСловаря	 - Строка	 - Имя словаря
//  Отбор		 - Структура	 - Структура, содержащая отбор списка словаря
//
Процедура ОбновитьСписокСловаря(Знач ИмяСловаря, Знач Отбор=Неопределено) Экспорт 
	
	ТелоЗапроса = "";
	Если ЗначениеЗаполнено(Отбор) Тогда
		ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Отбор", Отбор));
	КонецЕсли;
	
	РезультатЗапроса = ВыполнитьЗапрос("dictionaries", ИмяСловаря, ТелоЗапроса, "Список");
	
	Если РезультатЗапроса <> Неопределено Тогда 
		
		ЗаписатьЭлементы(РезультатЗапроса);
		
		ПометитьУдаленныеОбъекты(ИмяСловаря, РезультатЗапроса, Отбор);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПометитьУдаленныеОбъекты(Знач ИмяСловаря, Данные, Отбор = Неопределено)
	
	МассивПолученныхКодов = Новый Массив;
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		
		Для Каждого ЭлементДанных ИЗ Данные Цикл
			Если ТипЗнч(ЭлементДанных.Значение) = Тип("Структура") Тогда
				МассивПолученныхКодов.Добавить(ЭлементДанных.Код);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Данные) = Тип("Массив") Тогда
		
		Для Каждого ЭлементДанных ИЗ Данные Цикл
			Если ТипЗнч(ЭлементДанных) = Тип("Структура") Тогда
				МассивПолученныхКодов.Добавить(ЭлементДанных.Код);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

	ВладелецТип = Неопределено;
	ВладелецКод = "";
	
	Если НЕ Отбор = Неопределено Тогда
		Если Отбор.Свойство("Владелец") Тогда
			ВладелецКод = Отбор.Владелец;
		ИначеЕсли Отбор.Свойство("ВидСвойства") Тогда
			ВладелецТип = "ВидыСвойств";
			ВладелецКод = Отбор.ВидСвойства;
		ИначеЕсли Отбор.Свойство("ТипНоменклатуры") Тогда
			ВладелецТип = "ТипыНоменклатуры";
			ВладелецКод = Отбор.ТипНоменклатуры;
		ИначеЕсли Отбор.Свойство("Каталог") Тогда
			ВладелецТип = "Каталоги";
			ВладелецКод = Отбор.Каталог;
		ИначеЕсли Отбор.Свойство("Модель") Тогда
			ВладелецТип = "Модели";
			ВладелецКод = Отбор.Модель;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивКодов", МассивПолученныхКодов);
	Запрос.УстановитьПараметр("Тип",         ИмяСловаря);
	Запрос.УстановитьПараметр("ВладелецТип", ВладелецТип);
	Запрос.УстановитьПараметр("ВладелецКод", ВладелецКод);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	атСловари.Тип,
		|	атСловари.Код,
		|	атСловари.ВладелецТип,
		|	атСловари.ВладелецКод,
		|	атСловари.Значение
		|ИЗ
		|	РегистрСведений.атСловари КАК атСловари
		|ГДЕ
		|	атСловари.Тип = &Тип
		|	И НЕ атСловари.Код В (&МассивКодов)
		|	"+?(ВладелецТип = Неопределено, "", "И атСловари.ВладелецТип = &ВладелецТип")+"
		|	И атСловари.ВладелецКод = &ВладелецКод";
	
	Результат = Запрос.Выполнить();
	
	// Надо будет сделать транзакцию и установить блокировку
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.атСловари.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка, "Тип, Код, ВладелецТип, ВладелецКод");
		
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			Если ЗначениеЗаполнено(Выборка.Значение) Тогда
				МенеджерЗаписи.ОбъектУдален = Истина;
				МенеджерЗаписи.Записать();
			Иначе
				МенеджерЗаписи.Удалить();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция - Получить данные объекта с сервера
//  Выполняет запрос объекта из сервиса
//  Сохраняет полученный объект сервиса в регистр атСловари
//
// Параметры:
//  ИмяСловаря	 - Строка	 - Имя словаря
//  Код			 - Строка	 - Код элемента словаря
// 
// Возвращаемое значение:
//  Структура - Объект словарь
//
Функция ПолучитьОбъектССервера(Знач СтруктураСловаря, Ошибка="") Экспорт 
	
	Если СтруктураСловаря.Тип = "Номенклатура" Тогда
		
		Если ПараметрыСеанса.атДоступныеСервисы.Свойство("Сервисы")
			И ПараметрыСеанса.атДоступныеСервисы.ДоступностьСервисов.Свойство("information")
			И ПараметрыСеанса.атДоступныеСервисы.ДоступностьСервисов.information Тогда
			
			Если ЗначениеЗаполнено(СтруктураСловаря.Код) Тогда
				Данные = Новый Структура("Код", СтруктураСловаря.Код);
				ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
				Результат = ВыполнитьЗапрос("information", "КарточкаНоменклатуры", ТелоЗапроса,, Истина, Ошибка);
			
			ИначеЕсли СтруктураСловаря.Свойство("Артикул") И СтруктураСловаря.Свойство("Производитель")
				И ЗначениеЗаполнено(СтруктураСловаря.Артикул) И ЗначениеЗаполнено(СтруктураСловаря.Производитель) Тогда
				
				Если ТипЗнч(СтруктураСловаря.Производитель) = Тип("Структура") Тогда
					Производитель = СтруктураСловаря.Производитель.Код;
				Иначе
					Производитель = СтруктураСловаря.Производитель;
				КонецЕсли;
				
				Данные = Новый Структура("Артикул, Производитель", СтруктураСловаря.Артикул, Производитель);
				ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
				Результат = ВыполнитьЗапрос("information", "КарточкаАртикула", ТелоЗапроса,, Истина, Ошибка);
				
			Иначе
				Ошибка = "Не возможно получить номенклатуру с сервера. Не указан артикул или код элемента";
				
			КонецЕсли;
			
			Если Результат <> Неопределено Тогда
				Если Результат.Количество() > 0 Тогда
					Результат = Результат[0];
				Иначе
					Ошибка = "На сервере нет дополнительной информации про номенклатуру";
					
					СтруктураСловаря.Вставить("ЭтоСсылка", Ложь);
					
					Если ТипЗнч(СтруктураСловаря.Производитель) = Тип("Строка") Тогда
						Производитель = ПолучитьСловарь("Производители", СтруктураСловаря.Производитель);
						СтруктураСловаря.Вставить("Производитель", Производитель);
					КонецЕсли;
					
					Если Не СтруктураСловаря.Свойство("АртикулДляПоиска") Тогда
						СтруктураСловаря.Вставить("АртикулДляПоиска", Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(СтруктураСловаря.Артикул));
					КонецЕсли;
					
					Результат = СтруктураСловаря;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(СтруктураСловаря.Код) Тогда
			
			Отбор = Новый Структура("Код", СтруктураСловаря.Код);
			ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Отбор", Отбор));
			Результат = ВыполнитьЗапрос("dictionaries", СтруктураСловаря.Тип, ТелоЗапроса, "Объект", Истина, Ошибка);
			
		Иначе
			Ошибка = "Не возможно получить номенклатуру с сервера. Не указан код элемента";
			
		КонецЕсли;
		
	Иначе
		Отбор = Новый Структура("Код", СтруктураСловаря.Код);
		Если ЗначениеЗаполнено(СтруктураСловаря.ВладелецКод) Тогда
			Отбор.Вставить("Владелец", СтруктураСловаря.ВладелецКод);
		КонецЕсли;
		ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Отбор", Отбор));
		Результат = ВыполнитьЗапрос("dictionaries", СтруктураСловаря.Тип, ТелоЗапроса, "Объект",, Ошибка);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Получить данные объекта
//  Проверяет наличие сохраненного в ИБ объекта словаря,
//  если не найден, то выполняет запрос объекта из сервиса
//  Сохраняет полученный объект сервиса в регистр атСловари
//
// Параметры:
//  ИмяСловаря	 - Строка	 - Имя словаря
//  Код			 - Строка	 - Код элемента словаря
//  СопоставленноеЗначениеИБ - СправочникСсылка, Неопределено - Ссылка на сопоставленное значение в ИБ, Неопределено
// 
// Возвращаемое значение:
//  Структура - Объект словарь
//
Функция ПолучитьОбъект(Знач СтруктураСловаря, Знач Код="", СопоставленноеЗначениеИБ=Неопределено, Ошибка="") Экспорт 
	
	Если ТипЗнч(СтруктураСловаря) = Тип("Строка") Тогда
		СтруктураСловаря = Новый Структура("Тип, Код", СтруктураСловаря, Код);
	КонецЕсли;
	
	Если НЕ СтруктураСловаря.Свойство("ВладелецКод") Тогда
		СтруктураСловаря.Вставить("ВладелецТип", "");
		СтруктураСловаря.Вставить("ВладелецКод", "");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтруктураСловаря.Тип) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураСловаря.Код) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Словари.ЭтоСсылка,
		|	Словари.Тип,
		|	Словари.Код,
		|	Словари.ВладелецТип,
		|	Словари.ВладелецКод,
		|	Словари.Наименование,
		|	Словари.Значение,
		|	Словари.ДанныеОбъекта,
		|	Словари.ВерсияДанных,
		|	Словари.ВерсияУстарела
		|ИЗ
		|	РегистрСведений.атСловари КАК Словари
		|ГДЕ
		|	Словари.Тип = &Тип
		|	И Словари.Код = &Код
		|	И (&ВладелецКод = """"
		|			ИЛИ Словари.ВладелецТип = &ВладелецТип
		|				И Словари.ВладелецКод = &ВладелецКод)
		|";
		
		Запрос.УстановитьПараметр("Тип", СтруктураСловаря.Тип);
		Запрос.УстановитьПараметр("Код", СтруктураСловаря.Код);
		Запрос.УстановитьПараметр("ВладелецТип", СтруктураСловаря.ВладелецТип);
		Запрос.УстановитьПараметр("ВладелецКод", СтруктураСловаря.ВладелецКод);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			СопоставленноеЗначениеИБ = Выборка.Значение;
			СтруктураСловаря.ВладелецТип = Выборка.ВладелецТип;
			СтруктураСловаря.ВладелецКод = Выборка.ВладелецКод;
			Если Не Выборка.ЭтоСсылка И НЕ Выборка.ВерсияУстарела Тогда
				ДанныеОбъекта = Выборка.ДанныеОбъекта.Получить();
			КонецЕсли;
		Иначе
			Выборка = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОбъекта <> Неопределено Тогда
		Возврат ДанныеОбъекта;
	КонецЕсли;
	
	Результат = ПолучитьОбъектССервера(СтруктураСловаря, Ошибка);
	
	Если Результат <> Неопределено Тогда
		// Когда получаем объект - всегда обновляем регистр атСловари!
		// Исключением будет когда не найдена дополнительная информация для номенклатуры
		// В этом случае просто вернем модернизированный объект
		ЗаписатьЭлемент(Результат, СопоставленноеЗначениеИБ, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьЭлементы(Знач СтруктураДанных) Экспорт
	
	Если ТипЗнч(СтруктураДанных) = Тип("Структура") Тогда
		
		Для Каждого КлючЗначение ИЗ СтруктураДанных Цикл
			
			Если ТипЗнч(КлючЗначение.Значение) = Тип("Структура") И КлючЗначение.Значение.Свойство("ЭтоСсылка") Тогда
				
				ЗаписатьЭлемент(КлючЗначение.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(СтруктураДанных) = Тип("Массив") Тогда
		
		Для Каждого СтруктураСловаря ИЗ СтруктураДанных Цикл
			
			Если ТипЗнч(СтруктураСловаря) = Тип("Структура") И СтруктураСловаря.Свойство("ЭтоСсылка") Тогда
				
				ЗаписатьЭлемент(СтруктураСловаря);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьЭлемент(Знач СтруктураСловаря, СопоставленноеЗначениеИБ=Неопределено, Знач Выборка=Неопределено) Экспорт 
	
	Если Не СтруктураСловаря.Свойство("ВладелецКод") Тогда
		СтруктураСловаря.Вставить("ВладелецТип", "");
		СтруктураСловаря.Вставить("ВладелецКод", "");
	КонецЕсли;
	
	Если Не (ЗначениеЗаполнено(СтруктураСловаря.Тип) И ЗначениеЗаполнено(СтруктураСловаря.Код)) Тогда
		Возврат;
	КонецЕсли;
	
	Если Выборка = Истина Тогда
		НоваяЗапись = Истина;
		
	ИначеЕсли Выборка = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Словари.ЭтоСсылка,
		|	Словари.Тип,
		|	Словари.Код,
		|	Словари.ВладелецТип,
		|	Словари.ВладелецКод,
		|	Словари.Наименование,
		|	Словари.Значение,
		|	Словари.ВерсияДанных,
		|	Словари.ДанныеОбъекта,
		|	Словари.ВерсияУстарела
		|ИЗ
		|	РегистрСведений.атСловари КАК Словари
		|ГДЕ
		|	Словари.Тип = &Тип
		|	И Словари.Код = &Код
		|	И (&ВладелецКод = """"
		|			ИЛИ Словари.ВладелецТип = &ВладелецТип
		|				И Словари.ВладелецКод = &ВладелецКод)";
		
		Запрос.УстановитьПараметр("Тип", СтруктураСловаря.Тип);
		Запрос.УстановитьПараметр("Код", СтруктураСловаря.Код);
		Запрос.УстановитьПараметр("ВладелецТип", СтруктураСловаря.ВладелецТип);
		Запрос.УстановитьПараметр("ВладелецКод", СтруктураСловаря.ВладелецКод);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			НоваяЗапись = Ложь;
			СтруктураСловаря.ВладелецТип = Выборка.ВладелецТип;
			СтруктураСловаря.ВладелецКод = Выборка.ВладелецКод;
			Если СопоставленноеЗначениеИБ = Неопределено Тогда
				СопоставленноеЗначениеИБ = Выборка.Значение;
			КонецЕсли;
		Иначе
			// Все-таки новая запись ))
			НоваяЗапись = Истина;
		КонецЕсли;
		
	Иначе
		НоваяЗапись = Ложь;
	КонецЕсли;
	
	Если НоваяЗапись И НЕ СтруктураСловаря.Свойство("ЭтоСсылка") Тогда
		// ничего не будем делать, так как структура словаря не правильная
		Возврат;
	КонецЕсли;
	
	НастройкиСинхронизации = атПовторноеИспользование.ПолучитьНастройкуСинхронизации(СтруктураСловаря.Тип);
	Автосинхронизация = Не ПустаяСтрока(НастройкиСинхронизации.ИмяСправочника) И НастройкиСинхронизации.Автосинхронизация;
	
	Если Автосинхронизация И СопоставленноеЗначениеИБ = Неопределено Тогда
		
		НайденноеЗначение = НайтиСоответствиеЭлементу(СтруктураСловаря, НастройкиСинхронизации);
		
		Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
			// Что-то нашли
			СопоставленноеЗначениеИБ = НайденноеЗначение;
			
		Иначе
			
			Если НастройкиСинхронизации.Автосоздание = Истина Тогда
				
				Если СтруктураСловаря.ЭтоСсылка Тогда
					// В процедуре получения объекта мы запишем ссылку и создадим ОбъектИБ
					ПолучитьОбъект(СтруктураСловаря,, СопоставленноеЗначениеИБ);
					Возврат;
				Иначе
					// Осталось только создать ОбъектИБ
					СопоставленноеЗначениеИБ = СоздатьОбъектИБ(НастройкиСинхронизации.ИмяСправочника, СтруктураСловаря, Истина);
				КонецЕсли;
				
			//Иначе
			//	// Автоматически не создаем, значит сопоставим пустому значению!
			//	СопоставленноеЗначениеИБ = НайденноеЗначение;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НоваяЗапись Тогда
		// Создадим новую запись
		МенеджерЗаписи = РегистрыСведений.атСловари.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураСловаря);
		
		Если СопоставленноеЗначениеИБ <> Неопределено Тогда
			МенеджерЗаписи.Значение = СопоставленноеЗначениеИБ;
			МенеджерЗаписи.ВерсияОбъектаАльфаАвто = СопоставленноеЗначениеИБ.ВерсияДанных;
		КонецЕсли;
		
		МенеджерЗаписи.ВерсияУстарела = Ложь;
		МенеджерЗаписи.ОбъектУдален = Ложь;
		
		Если СтруктураСловаря.ЭтоСсылка Тогда
			МенеджерЗаписи.ДанныеОбъекта = Новый ХранилищеЗначения(Неопределено);
		Иначе
			МенеджерЗаписи.ДанныеОбъекта = Новый ХранилищеЗначения(СтруктураСловаря);
		КонецЕсли;
		
		МенеджерЗаписи.Записать();
		
	Иначе
		// Обновим если потребуется существующую запись в атСловари
		Если (СтруктураСловаря.Свойство("ВерсияДанных") И СтруктураСловаря.ВерсияДанных <> Выборка.ВерсияДанных)
			ИЛИ (СтруктураСловаря.Свойство("ЭтоСсылка") И НЕ СтруктураСловаря.ЭтоСсылка И Выборка.ЭтоСсылка)
			ИЛИ (СопоставленноеЗначениеИБ <> Неопределено И Выборка.Значение <> СопоставленноеЗначениеИБ) Тогда
			
			МенеджерЗаписи = РегистрыСведений.атСловари.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураСловаря, "Тип, Код, ВладелецТип, ВладелецКод");
			МенеджерЗаписи.Прочитать();
			
			Если СтруктураСловаря.Свойство("ВерсияДанных") И СтруктураСловаря.ВерсияДанных <> Выборка.ВерсияДанных Тогда
				// Изменилась версия в сервисе и возможно получили сразу новый объект
				МенеджерЗаписи.ЭтоСсылка = СтруктураСловаря.ЭтоСсылка;
				МенеджерЗаписи.Наименование = СтруктураСловаря.Наименование;
				МенеджерЗаписи.ВерсияДанных = СтруктураСловаря.ВерсияДанных;
				МенеджерЗаписи.ВерсияУстарела = Ложь;
				Если СтруктураСловаря.ЭтоСсылка Тогда
					МенеджерЗаписи.ДанныеОбъекта = Новый ХранилищеЗначения(Неопределено);
				Иначе
					МенеджерЗаписи.ДанныеОбъекта = Новый ХранилищеЗначения(СтруктураСловаря);
				КонецЕсли;
				
			ИначеЕсли СтруктураСловаря.Свойство("ЭтоСсылка") И НЕ СтруктураСловаря.ЭтоСсылка И Выборка.ЭтоСсылка Тогда
				// Просто пришел объект из сервиса
				МенеджерЗаписи.ЭтоСсылка = Ложь;
				МенеджерЗаписи.ВерсияУстарела = Ложь;
				МенеджерЗаписи.ДанныеОбъекта = Новый ХранилищеЗначения(СтруктураСловаря);
			КонецЕсли;
			
			Если СопоставленноеЗначениеИБ <> Неопределено И Выборка.Значение <> СопоставленноеЗначениеИБ Тогда
				// Установим новое соответствие
				МенеджерЗаписи.Значение = СопоставленноеЗначениеИБ;
				МенеджерЗаписи.ВерсияОбъектаАльфаАвто = СопоставленноеЗначениеИБ.ВерсияДанных;
			КонецЕсли;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьДополнительныеПараметры(Знач СтруктураСловаря, Знач ДополнительныеПараметры) Экспорт 
	
	Если Не СтруктураСловаря.Свойство("ВладелецКод") Тогда
		СтруктураСловаря.Вставить("ВладелецТип", "");
		СтруктураСловаря.Вставить("ВладелецКод", "");
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.атСловари.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураСловаря, "Тип, Код, ВладелецТип, ВладелецКод");
	
	МенеджерЗаписи.Прочитать();
	
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураСловаря);
		
	КонецЕсли;
	
	МенеджерЗаписи.ДополнительныеПараметры = Новый ХранилищеЗначения(ДополнительныеПараметры);
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция ПолучитьДополнительныеПараметры(Знач СтруктураСловаря, Знач Код="") Экспорт 
	
	Результат = Неопределено;
	
	Если ТипЗнч(СтруктураСловаря) = Тип("Строка") Тогда
		СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод", СтруктураСловаря, Код, "", "");
	КонецЕсли;
	
	Если НЕ СтруктураСловаря.Свойство("ВладелецКод") Тогда
		СтруктураСловаря.Вставить("ВладелецТип", "");
		СтруктураСловаря.Вставить("ВладелецКод", "");
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.атСловари.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураСловаря, "Тип, Код, ВладелецТип, ВладелецКод");
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		
		Результат = МенеджерЗаписи.ДополнительныеПараметры.Получить();
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСопоставленнуюСсылку(Знач СтруктураСловаря, Знач ВозвращатьНаименование = Неопределено, Знач ЗаписыватьОбъект = Ложь) Экспорт 
	
	Результат = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(СтруктураСловаря) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если НЕ СтруктураСловаря.Свойство("ВладелецКод") Тогда
		СтруктураСловаря.Вставить("ВладелецТип", "");
		СтруктураСловаря.Вставить("ВладелецКод", "");
	КонецЕсли;
	
	Если НЕ (СтруктураСловаря.Свойство("Код") И ЗначениеЗаполнено(СтруктураСловаря.Код)) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	атСловари.Значение,
	|	атСловари.ВерсияДанных,
	|	атСловари.ВерсияОбъектаАльфаАвто
	|ИЗ
	|	РегистрСведений.атСловари КАК атСловари
	|ГДЕ
	|	атСловари.Тип = &Тип
	|	И атСловари.Код = &Код
	|	И (&ВладелецКод = """"
	|			ИЛИ атСловари.ВладелецТип = &ВладелецТип
	|				И атСловари.ВладелецКод = &ВладелецКод)";
	
	Запрос.УстановитьПараметр("Тип", СокрЛП(СтруктураСловаря.Тип));
	Запрос.УстановитьПараметр("Код", СокрЛП(СтруктураСловаря.Код));
	Запрос.УстановитьПараметр("ВладелецТип", СокрЛП(СтруктураСловаря.ВладелецТип));
	Запрос.УстановитьПараметр("ВладелецКод", СокрЛП(СтруктураСловаря.ВладелецКод));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат = Выборка.Значение;
		
	Иначе
		
		Если ЗаписыватьОбъект Тогда
			ЗаписатьЭлемент(СтруктураСловаря, Результат, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Результат = Неопределено Тогда // нет связи с локальным справочником
		
		Если ВозвращатьНаименование = Истина Тогда
			Результат = СтруктураСловаря.Наименование;
		ИначеЕсли ВозвращатьНаименование = Ложь Тогда
			Результат = СтруктураСловаря.Код;
		КонецЕсли;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Результат) Тогда // связан с пустым элементом
		
		// Надо ли возвращать наименование?
		//Если ВозвращатьНаименование = Истина Тогда
		//	Результат = СтруктураСловаря.Наименование;
		//ИначеЕсли ВозвращатьНаименование = Ложь Тогда
		//	Результат = СтруктураСловаря.Код;
		//КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиСоответствиеЭлементу(Знач СтруктураСловаря, Знач НастройкиСинхронизации=Неопределено) Экспорт
	
	Если Не СтруктураСловаря.Свойство("ЭтоСсылка") Тогда
		// Передали непонятную структуру
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтруктураСловаря.Тип = "ЗначенияСвойств" Тогда
		
		// получим соответствие для владеющего вида свойств
		Если СтруктураСловаря.Свойство("ВладелецТип") И ЗначениеЗаполнено(СтруктураСловаря.ВладелецТип) Тогда
			СтруктураВладельца = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод", СтруктураСловаря.ВладелецТип, СтруктураСловаря.ВладелецКод, "", "");
		ИначеЕсли СтруктураСловаря.Свойство("ВладелецКод") Тогда
			СтруктураВладельца = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод", "ВидыСвойств", СтруктураСловаря.ВладелецКод, "", "");
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
		Владелец = ПолучитьСопоставленнуюСсылку(СтруктураВладельца,, Ложь);
		Если Не ЗначениеЗаполнено(Владелец) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	// Для создания нового элемента в базе!
	СтруктураСловаря.Вставить("Владелец", Владелец);
	
	Претендент = Неопределено;
	
	Если СтруктураСловаря.Тип = "Производители" Тогда
		
		Претендент = Справочники.Производители.НайтиПоНаименованию(СтруктураСловаря.Наименование, Истина);
		Если Претендент.Пустая() Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	Синонимы.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Производители.Синонимы КАК Синонимы
				|ГДЕ
				|	Синонимы.Синоним = &Наименование
				|	И НЕ Синонимы.Ошибка";
			Запрос.УстановитьПараметр("Наименование", СтруктураСловаря.Наименование);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Претендент = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли СтруктураСловаря.Тип = "Номенклатура" Тогда
		// Только объект можем синхронизировать, так как там есть Артикул и Производитель!
		Если НЕ СтруктураСловаря.ЭтоСсылка Тогда
			
			Производитель = ПолучитьСопоставленнуюСсылку(СтруктураСловаря.Производитель,, Истина);
			
			// Надо решить - Если Производитель.Пустая() - значит он нам не нужен, а нужна ли нам такая номенклатура?
			// Может принудительно возвращать Пустую ссылку???? номенклатуры
			//Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") И Производитель.Пустая() Тогда
			//	Возврат Справочники.Номенклатура.ПустаяСсылка();
			//КонецЕсли;
			
			Если ЗначениеЗаполнено(Производитель) Тогда
				// Попробуем найти по артикулу
				Если ЗначениеЗаполнено(СтруктураСловаря.АртикулДляПоиска) Тогда
					Претендент = НайтиНоменклатуру(СтруктураСловаря.АртикулДляПоиска, Производитель);
				КонецЕсли;
				
				// Если не нашли, то попробуем поискать через свойства,
				// засада, что будем искать по всем свойствам и редко будет полное совпадение
				Если Не ЗначениеЗаполнено(Претендент) И СтруктураСловаря.СвойстваНоменклатуры.Количество() > 0 Тогда
					ТаблицаНоменклатуры = НайтиНоменклатуруПоСвойствам(СтруктураСловаря.СвойстваНоменклатуры, Производитель, ?(СтруктураСловаря.Свойство("Модель"), СтруктураСловаря.Модель, ""));
					Если ТаблицаНоменклатуры.Количество() = 1 Тогда
						Претендент = ТаблицаНоменклатуры[0].Ссылка;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если НастройкиСинхронизации = Неопределено Тогда
			НастройкиСинхронизации = атПовторноеИспользование.ПолучитьНастройкуСинхронизации(СтруктураСловаря.Тип);
		КонецЕсли;
		
		Если СтруктураСловаря.Тип = "ВидыСвойств" Тогда
			Менеджер = ПланыВидовХарактеристик[НастройкиСинхронизации.ИмяСправочника];
		Иначе
			Менеджер = Справочники[НастройкиСинхронизации.ИмяСправочника];
		КонецЕсли;
		
		РеквизитСинхронизации = НастройкиСинхронизации.РеквизитСинхронизации;
		
		Если РеквизитСинхронизации = "Код" Тогда
			Если Владелец = Неопределено Тогда
				Претендент = Менеджер.НайтиПоКоду(СтруктураСловаря.Код, Ложь);
			Иначе
				Претендент = Менеджер.НайтиПоКоду(СтруктураСловаря.Код, Ложь,, Владелец);
			КонецЕсли;
		ИначеЕсли РеквизитСинхронизации = "Наименование" Тогда
			Если Владелец = Неопределено Тогда
				Претендент = Менеджер.НайтиПоНаименованию(СтруктураСловаря.Наименование, Истина);
			Иначе
				Претендент = Менеджер.НайтиПоНаименованию(СтруктураСловаря.Наименование, Истина,, Владелец);
			КонецЕсли;
		Иначе
			// получать реквизит из ДанныеОбъекта?
			Если НЕ СтруктураСловаря.ЭтоСсылка
				И СтруктураСловаря.Свойство(РеквизитСинхронизации)
				И ЗначениеЗаполнено(СтруктураСловаря[РеквизитСинхронизации]) Тогда
				Если Владелец = Неопределено Тогда
					Претендент = Менеджер.НайтиПоРеквизиту(РеквизитСинхронизации, СтруктураСловаря[РеквизитСинхронизации]);
				Иначе
					Претендент = Менеджер.НайтиПоРеквизиту(РеквизитСинхронизации, СтруктураСловаря[РеквизитСинхронизации],, Владелец);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Претендент) Тогда
		Возврат Претендент;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция СопоставитьСловарюСсылку(Знач СтруктураСловаря, Знач Ссылка) Экспорт
	
	Если Не СтруктураСловаря.Свойство("ВладелецКод") Тогда
		СтруктураСловаря.Вставить("ВладелецТип", "");
		СтруктураСловаря.Вставить("ВладелецКод", "");
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.атСловари.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураСловаря, "Тип, Код, ВладелецТип, ВладелецКод");
	
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() И Ссылка <> МенеджерЗаписи.Значение Тогда
		МенеджерЗаписи.ВерсияУстарела = Ложь; // Так как всегда получаем новую версию перед установкой соответствия?
		МенеджерЗаписи.Значение = Ссылка;
		Если ЗначениеЗаполнено(Ссылка) Тогда
			МенеджерЗаписи.ВерсияОбъектаАльфаАвто = Ссылка.ВерсияДанных;
		Иначе
			МенеджерЗаписи.ВерсияОбъектаАльфаАвто = "";
		КонецЕсли;
		
		МенеджерЗаписи.Записать();
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьСловарьПоСсылке(Знач Ссылка, Знач ПолучатьОбъект=Ложь) Экспорт 
	
	Результат = Неопределено;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	атСловари.ЭтоСсылка,
	|	атСловари.Тип,
	|	атСловари.Код,
	|	атСловари.ВладелецТип,
	|	атСловари.ВладелецКод,
	|	атСловари.Наименование,
	|	атСловари.ВерсияДанных,
	|	атСловари.ДанныеОбъекта,
	|	атСловари.ВерсияУстарела
	|ИЗ
	|	РегистрСведений.атСловари КАК атСловари
	|ГДЕ
	|	атСловари.Значение = &Значение
	|	И НЕ атСловари.ОбъектУдален";
	
	Запрос.УстановитьПараметр("Значение", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ПолучатьОбъект И Выборка.ЭтоСсылка Тогда
			// В регистре нет данных. Нужно получить объект с сервера!
			СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод");
			ЗаполнитьЗначенияСвойств(СтруктураСловаря, Выборка);
			
			Результат = ПолучитьОбъектССервера(СтруктураСловаря);
			Если Результат <> Неопределено Тогда
				ЗаписатьЭлемент(Результат,, Выборка);
			КонецЕсли;
			
		Иначе
			
			Результат = Новый Структура;
			Результат.Вставить("ЭтоСсылка", Не ПолучатьОбъект);
			Результат.Вставить("Тип", Выборка.Тип);
			Результат.Вставить("Код", Выборка.Код);
			Результат.Вставить("ВладелецТип", Выборка.ВладелецТип);
			Результат.Вставить("ВладелецКод", Выборка.ВладелецКод);
			Результат.Вставить("Наименование", Выборка.Наименование);
			Результат.Вставить("ВерсияДанных", Выборка.ВерсияДанных);
			
			Если ПолучатьОбъект Тогда
				РеквизитыОбъекта = Выборка.ДанныеОбъекта.Получить();
				Если ТипЗнч(РеквизитыОбъекта) = Тип("Структура") Тогда
					Для Каждого Реквизит ИЗ РеквизитыОбъекта Цикл
						Результат.Вставить(Реквизит.Ключ, Реквизит.Значение);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСловарь(Знач Тип, Знач Код, Знач ВладелецТип="", Знач ВладелецКод="", Знач ПолучатьОбъект=Ложь) Экспорт 
	
	Результат = Неопределено;
	
	Если Не (ЗначениеЗаполнено(Тип) И ЗначениеЗаполнено(Код)) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Словари.ЭтоСсылка,
	|	Словари.Тип,
	|	Словари.Код,
	|	Словари.ВладелецТип,
	|	Словари.ВладелецКод,
	|	Словари.Наименование,
	|	Словари.ВерсияДанных,
	|	Словари.ДанныеОбъекта,
	|	Словари.ВерсияУстарела,
	|	Словари.ОбъектУдален
	|ИЗ
	|	РегистрСведений.атСловари КАК Словари
	|ГДЕ
	|	Словари.Тип = &Тип
	|	И Словари.Код = &Код
	|	И (&ВладелецКод = """"
	|			ИЛИ Словари.ВладелецТип = &ВладелецТип
	|				И Словари.ВладелецКод = &ВладелецКод)";
	
	Запрос.УстановитьПараметр("Тип", Тип);
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("ВладелецТип", ВладелецТип);
	Запрос.УстановитьПараметр("ВладелецКод", ВладелецКод);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если Выборка.ОбъектУдален Тогда
			
			Результат = Новый Структура;
			Результат.Вставить("ЭтоСсылка", Неопределено);
			Результат.Вставить("Тип", Выборка.Тип);
			Результат.Вставить("Код", Выборка.Код);
			Результат.Вставить("ВладелецТип", Выборка.ВладелецТип);
			Результат.Вставить("ВладелецКод", Выборка.ВладелецКод);
			Результат.Вставить("Наименование", Выборка.Наименование);
			Результат.Вставить("ВерсияДанных", "");
			Результат.Вставить("ОбъектУдален", Истина);
			
		Иначе
			Если ПолучатьОбъект И Выборка.ЭтоСсылка ИЛИ Выборка.ВерсияУстарела Тогда
				// В регистре нет данных или они устарели. Нужно получить объект с сервера!
				СтруктураСловаря = Новый Структура("Тип, Код, ВладелецТип, ВладелецКод");
				ЗаполнитьЗначенияСвойств(СтруктураСловаря, Выборка);
				
				Результат = ПолучитьОбъектССервера(СтруктураСловаря);
				Если Результат <> Неопределено Тогда
					ЗаписатьЭлемент(Результат,, Выборка);
				КонецЕсли;
				
			Иначе
				
				Результат = Новый Структура;
				Результат.Вставить("ЭтоСсылка", Не ПолучатьОбъект);
				Результат.Вставить("Тип", Выборка.Тип);
				Результат.Вставить("Код", Выборка.Код);
				Результат.Вставить("ВладелецТип", Выборка.ВладелецТип);
				Результат.Вставить("ВладелецКод", Выборка.ВладелецКод);
				Результат.Вставить("Наименование", Выборка.Наименование);
				Результат.Вставить("ВерсияДанных", Выборка.ВерсияДанных);
				
				Если ПолучатьОбъект Тогда
					РеквизитыОбъекта = Выборка.ДанныеОбъекта.Получить();
					Если ТипЗнч(РеквизитыОбъекта) = Тип("Структура") Тогда
						Для Каждого Реквизит ИЗ РеквизитыОбъекта Цикл
							Результат.Вставить(Реквизит.Ключ, Реквизит.Значение);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции



/////////////////////////////////////////////////////////////////////////////////////////

Функция СоздатьОбъектИБ(Знач ИмяСправочника, СтруктураСловаря, Знач ОбменДаннымиЗагрузка = Ложь) Экспорт 
	
	Если СтруктураСловаря.Свойство("Владелец") Тогда
		Владелец = СтруктураСловаря.Владелец;
	КонецЕсли;
	
	Если Не СтруктураСловаря.Свойство("ЭтоСсылка") ИЛИ СтруктураСловаря.ЭтоСсылка Тогда
		// На входе не ссылка. Получим объект с сервера
		// При получении могли автоматически создать или найти новый элемент,
		// его помещаем в выбранное значение
		ВыбраноеЗначение = Неопределено;
		Результат = ПолучитьОбъект(СтруктураСловаря,, ВыбраноеЗначение);
		Если ВыбраноеЗначение <> Неопределено Тогда
			Возврат ВыбраноеЗначение;
		КонецЕсли;
		Если Результат <> Неопределено Тогда
			СтруктураСловаря = Результат;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСправочника = "ЗначенияСвойств" Тогда
		Если НЕ ЗначениеЗаполнено(Владелец) ИЛИ ТипЗнч(Владелец) <> Тип("ПланВидовХарактеристикСсылка.СвойстваОбъектов") Тогда
			Сообщить("Не указан владелец для значения свойства!");
			// Косяк!
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСправочника = "СвойстваОбъектов" Тогда
		НовыйОбъект = ПланыВидовХарактеристик.СвойстваОбъектов.СоздатьЭлемент();
	Иначе
		НовыйОбъект = Справочники[ИмяСправочника].СоздатьЭлемент();
	КонецЕсли;
	
	НовыйОбъект.Наименование = СтруктураСловаря.Наименование;
	НовыйОбъект.Заполнить(Неопределено);
	
	Если ИмяСправочника = "ЗначенияСвойств" Тогда
		НовыйОбъект.Владелец = Владелец;
		
	ИначеЕсли ИмяСправочника = "ТипыНоменклатуры" Тогда
		НовыйОбъект.ОсновнаяБазоваяЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт;
		
	ИначеЕсли ИмяСправочника = "Производители" Тогда
		
		Если СтруктураСловаря.Свойство("АртикулыНеОбязательны") Тогда
			НовыйОбъект.АртикулыОбязательны = Не СтруктураСловаря.АртикулыНеОбязательны;
		Иначе
			НовыйОбъект.АртикулыОбязательны = Истина;
		КонецЕсли;
		Если СтруктураСловаря.Свойство("НаименованияУникальны") Тогда
			НовыйОбъект.НаименованияУникальны = СтруктураСловаря.НаименованияУникальны;
		Иначе
			НовыйОбъект.НаименованияУникальны = Ложь;
		КонецЕсли;
		
		Для Каждого Синоним Из СтруктураСловаря.Синонимы Цикл
			НовыйСиноним = НовыйОбъект.Синонимы.Добавить();
			НовыйСиноним.Синоним = Синоним.Синоним;
		КонецЦикла;
		
		Для Каждого Донор Из СтруктураСловаря.Доноры Цикл
			ДонорПроизводитель = ПолучитьСопоставленнуюСсылку(Донор.Производитель,, Истина);
			Если ЗначениеЗаполнено(ДонорПроизводитель) Тогда
				НовыйДонор = НовыйОбъект.ДонорыАртикулов.Добавить();
				НовыйДонор.Производитель = ДонорПроизводитель;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяСправочника = "Номенклатура" Тогда
		
		Производитель = ПолучитьСопоставленнуюСсылку(СтруктураСловаря.Производитель,, Истина);
		Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") И Производитель.Пустая() Тогда
			// Такого производителя мы не загружаем в базу, поэтому и номенклатуру тоже игнорируем!
			Возврат Справочники.Номенклатура.ПустаяСсылка();
		Иначе
			Если Не ЗначениеЗаполнено(Производитель) Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		НовыйОбъект.НаименованиеПолное = СтруктураСловаря.Наименование;
		
		Если СтруктураСловаря.Свойство("ТипНоменклатуры") И ЗначениеЗаполнено(СтруктураСловаря.ТипНоменклатуры) Тогда
			ТипНоменклатуры = ПолучитьСопоставленнуюСсылку(СтруктураСловаря.ТипНоменклатуры,, Истина);
		КонецЕсли;
		
		НоменклатурнаяГруппа = ПолучитьНастройку("Номенклатура", "НоменклатурнаяГруппа");
		
		Если ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
			Если Не ЗначениеЗаполнено(ТипНоменклатуры) Тогда
				ТипНоменклатуры = НоменклатурнаяГруппа.ТипНоменклатуры;
			КонецЕсли;
			НовыйОбъект.Родитель         = НоменклатурнаяГруппа;
			НовыйОбъект.ПроцентНаценки   = НоменклатурнаяГруппа.ПроцентНаценки;
			НовыйОбъект.СтавкаНДС        = ?(Не ЗначениеЗаполнено(НоменклатурнаяГруппа.СтавкаНДС), Справочники.СтавкиНДС.ОсновнаяСтавкаНДС, НоменклатурнаяГруппа.СтавкаНДС);
			НовыйОбъект.ВалютаУчета      = ?(Не ЗначениеЗаполнено(НоменклатурнаяГруппа.ВалютаУчета), Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), НоменклатурнаяГруппа.ВалютаУчета);
		Иначе
			НовыйОбъект.ПроцентНаценки   = 0;
			НовыйОбъект.СтавкаНДС        = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
			НовыйОбъект.ВалютаУчета      = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТипНоменклатуры) Тогда
			ТипНоменклатуры = ПредопределенноеЗначение("Справочник.ТипыНоменклатуры.Штучный");
		КонецЕсли;
		
		НовыйОбъект.ТипНоменклатуры      = ТипНоменклатуры;
		НовыйОбъект.ВидНоменклатуры      = ?(Не ЗначениеЗаполнено(НовыйОбъект.ТипНоменклатуры.ВидНоменклатуры), Перечисления.ВидыНоменклатуры.Товар, НовыйОбъект.ТипНоменклатуры.ВидНоменклатуры);
		НовыйОбъект.БазоваяЕдиницаИзмерения = ?(Не ЗначениеЗаполнено(НовыйОбъект.ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения), Константы.ОсновнаяЕдиницаИзмеренияКоличества.Получить(), НовыйОбъект.ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения);
		
		НовыйОбъект.Производитель        = Производитель;
		НовыйОбъект.СтранаПроисхождения  = Производитель.СтранаПроисхождения;
		НовыйОбъект.Артикул              = СтруктураСловаря.Артикул;
		Если СтруктураСловаря.Свойство("АртикулДляПоиска") Тогда
			НовыйОбъект.АртикулДляПоиска = СтруктураСловаря.АртикулДляПоиска;
		Иначе
			НовыйОбъект.АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(СтруктураСловаря.Артикул, Производитель);
		КонецЕсли;
		
		Попытка
			НовыйОбъект.Вес = СтруктураСловаря.Вес;
		Исключение
		КонецПопытки;
		Попытка
			НовыйОбъект.Объем = СтруктураСловаря.Объем;
		Исключение
		КонецПопытки;
		
		НовыйОбъект.УстановитьНовыйКод();
		
		Попытка
			НовыйОбъект.ОбменДанными.Загрузка = ОбменДаннымиЗагрузка;
			НовыйОбъект.Записать();
			Попытка // Возможна ошибка при записи основной единицы измерения (если не задана базовая)
				НовыйОбъект.ОсновнаяЕдиницаИзмерения = НовыйОбъект.ПолучитьЕдиницуИзмеренияПоБазовой();
				НовыйОбъект.Записать();
			Исключение
				Сообщить("Ошибка записи основной единицы измерения для номенклатуры <" + спПолучитьНаименование(НовыйОбъект) + ">");
			КонецПопытки;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Неопределено;
		КонецПопытки;
	
		Возврат НовыйОбъект.Ссылка;
		
	КонецЕсли;
	
	НовыйОбъект.УстановитьНовыйКод();
	
	Попытка 
		НовыйОбъект.ОбменДанными.Загрузка = ОбменДаннымиЗагрузка;
		НовыйОбъект.Записать();
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции


// функция - НайтиНоменклатуру производит поиск номенклатуры по переданным параметрам
//
// Параметры:
//  АртикулДляПоиска - Строка						 - Артикул, значение которого будет использовано для поиска
//  Производитель	 - СправочникСсылка.Производители	 - Производитель номенклатуры
//  Наименование	 - Строка							 - Наименование номенклатуры
//  Родитель		 - СправочникСсылка.Номенклатура  – Группа номенклатуры для поиска
// 
// Возвращаемое значение:
//  СправочникСсылка.Номенклатура - Ссылка на найденную номенклатуру
//
Функция НайтиНоменклатуру(АртикулДляПоиска, Производитель, Наименование = Неопределено, Родитель = Неопределено) Экспорт 
	
	Если Не (ЗначениеЗаполнено(АртикулДляПоиска) ИЛИ ЗначениеЗаполнено(Наименование)) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Номенклатура.Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.АртикулДляПоиска = &АртикулДляПоиска
		|	И Номенклатура.Производитель = &Производитель
		|	И Номенклатура.Родитель = &Родитель
		|	И Номенклатура.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	Запрос.УстановитьПараметр("Производитель" , Производитель);
	
	Если Родитель <> Неопределено Тогда
		Запрос.УстановитьПараметр("Родитель", Родитель);
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Номенклатура.Родитель = &Родитель","");
	КонецЕсли;
	
	Если Наименование <> Неопределено Тогда
		Запрос.УстановитьПараметр("Наименование", Наименование);
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Номенклатура.Наименование = &Наименование","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// функция - НайтиНоменклатуру производит поиск номенклатуры по переданным параметрам
//
// Параметры:
//  МассивСвойств		 - Массив					 - Массив свойств для поиска номенклатуры
//  Производитель		 - СправочникСсылка.Производители	 - Производитель номенклатуры
//  МодельНоменклатуры	 - Строка							 - Модель номенклатуры
// 
// Возвращаемое значение:
//  ТаблицаЗначений. Выгрузка - результат запроса (Родитель, Артикул, Производитель, Наименование, Ссылка)
//
Функция НайтиНоменклатуруПоСвойствам(МассивСвойств, Производитель, МодельНоменклатуры="") Экспорт 
	
	Если МассивСвойств.Количество()=0 ИЛИ Не ЗначениеЗаполнено(Производитель) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Сначала попробуем найти через значения свойств
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Родитель,
	|	Номенклатура.Артикул,
	|	Номенклатура.Производитель,
	|	Номенклатура.Наименование,
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|";
	
	Сч = 0;
	
	Для Каждого ЭлементМассива ИЗ МассивСвойств Цикл
		Сч = Сч+1;
		ИмяПараметра = "Значение"+Формат(Сч, "ЧГ=0");
		ИмяТаблицы = "ЗначенияСвойствОбъектов"+Формат(Сч, "ЧГ=0");
		
		Если ТипЗнч(ЭлементМассива) = Тип("Структура") Тогда // Ключи: ВидСвойства, Значение, Ключевое
			
			Если ЭлементМассива.Свойство("Ключевое") И Не ЭлементМассива.Ключевое Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("Структура") Тогда
				Запрос.Текст = Запрос.Текст + "
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК "+ИмяТаблицы+"
				|		ПО (Номенклатура.Ссылка = (ВЫРАЗИТЬ("+ИмяТаблицы+".Объект КАК Справочник.Номенклатура)))
				|			И ("+ИмяТаблицы+".Значение.Наименование = &"+ИмяПараметра+")";
				Запрос.УстановитьПараметр(ИмяПараметра , ЭлементМассива.Значение.Наименование);
			Иначе
				Запрос.Текст = Запрос.Текст + "
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК "+ИмяТаблицы+"
				|		ПО (Номенклатура.Ссылка = (ВЫРАЗИТЬ("+ИмяТаблицы+".Объект КАК Справочник.Номенклатура)))
				|			И ("+ИмяТаблицы+".Значение = &"+ИмяПараметра+")";
				Запрос.УстановитьПараметр(ИмяПараметра , ЭлементМассива.Значение);
			КонецЕсли;
			
		Иначе
			
			Запрос.Текст = Запрос.Текст + "
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК "+ИмяТаблицы+"
			|		ПО (Номенклатура.Ссылка = (ВЫРАЗИТЬ("+ИмяТаблицы+".Объект КАК Справочник.Номенклатура)))
			|			И ("+ИмяТаблицы+".Значение = &"+ИмяПараметра+")";
			Запрос.УстановитьПараметр(ИмяПараметра , ЭлементМассива);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст = Запрос.Текст + "
	|ГДЕ
	|	Номенклатура.Производитель = &Производитель
	|	"+?(ЗначениеЗаполнено(МодельНоменклатуры), "И Номенклатура.Наименование ПОДОБНО &МодельНоменклатуры", "");
	
	Запрос.УстановитьПараметр("Производитель" , Производитель);
	Запрос.УстановитьПараметр("МодельНоменклатуры" , "%"+МодельНоменклатуры+"%");
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		// Попробуем найти свойства в наименовании
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Родитель,
		|	Номенклатура.Артикул,
		|	Номенклатура.Производитель,
		|	Номенклатура.Наименование,
		|	Номенклатура.Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Производитель = &Производитель
		|	"+?(ЗначениеЗаполнено(МодельНоменклатуры), "И Номенклатура.Наименование ПОДОБНО &МодельНоменклатуры", "");
		
		Запрос.УстановитьПараметр("Производитель" , Производитель);
		Запрос.УстановитьПараметр("МодельНоменклатуры" , "%"+МодельНоменклатуры+"%");
		
		Сч = 0;
		Для Каждого ЭлементМассива ИЗ МассивСвойств Цикл
			Сч = Сч+1;
			ИмяПараметра = "Значение"+Формат(Сч, "ЧГ=0");
			Если ТипЗнч(ЭлементМассива) = Тип("Структура") Тогда // Ключи: ВидСвойства, Значение, Ключевое
				Если ЭлементМассива.Свойство("Ключевое") И Не ЭлементМассива.Ключевое Тогда
					Продолжить;
				КонецЕсли;
				Если ТипЗнч(ЭлементМассива.Значение) = Тип("Число") Тогда
					Запрос.УстановитьПараметр(ИмяПараметра , "%"+Формат(ЭлементМассива.Значение, "ЧГ=0")+"%");
				ИначеЕсли ТипЗнч(ЭлементМассива.Значение) = Тип("Строка") Тогда
					Запрос.УстановитьПараметр(ИмяПараметра , "%"+ЭлементМассива.Значение+"%");
				ИначеЕсли ТипЗнч(ЭлементМассива.Значение) = Тип("Структура") Тогда
					Запрос.УстановитьПараметр(ИмяПараметра , "%"+ЭлементМассива.Значение.Наименование+"%");
				Иначе
					Продолжить;
				КонецЕсли;
			Иначе
				Если ТипЗнч(ЭлементМассива) = Тип("Число") Тогда
					Запрос.УстановитьПараметр(ИмяПараметра , "%"+Формат(ЭлементМассива, "ЧГ=0")+"%");
				ИначеЕсли ТипЗнч(ЭлементМассива) = Тип("Строка") Тогда
					Запрос.УстановитьПараметр(ИмяПараметра , "%"+ЭлементМассива+"%");
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Запрос.Текст = Запрос.Текст + "
			|	И Номенклатура.Наименование ПОДОБНО &"+ИмяПараметра;
		КонецЦикла;
		
		Результат = Запрос.Выполнить();
		
	КонецЕсли;
	
	Возврат Результат.Выгрузить();
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////INFORMATION/////////////////////////////////////////////

// функция - ПроверитьКэшИнформация производит проверку наличия кеш данных сервиса по переданным параметрам
//
// Параметры:
//  АртикулДляПоиска - Строка						 - Артикул, значение которого будет использовано для поиска
//  Производитель	 - Строка, СправочникСсылка.Производители	 - Производитель номенклатуры или пустая строка
//  УчитыватьАктуальность	 - Булево	 - Требуется ли анализировать актуальность имеющихся данных при проверке
// 
// Возвращаемое значение:
//  Истина - Кеш актуален, Ложь - Кеш неактуален или отсутствует, Неопределено - Данные в сервисе отсутствуют
//
Функция ПроверитьКэшИнформация(Знач АртикулДляПоиска, Знач Производитель, Знач УчитыватьАктуальность=Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Информация.Производитель
	|ИЗ
	|	РегистрСведений.атИнформация КАК Информация
	|ГДЕ
	|	Информация.АртикулДляПоиска = &АртикулДляПоиска
	|	И Информация.Производитель = &Производитель
	|	"+?(УчитыватьАктуальность, "И Информация.ДатаАктуальности > &ТекущаяДата", "")+"
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Информация.Производитель
	|ИЗ
	|	РегистрСведений.атИнформация КАК Информация
	|ГДЕ
	|	Информация.АртикулДляПоиска = &АртикулДляПоиска
	|	"+?(Производитель="", "И Информация.Производитель <> """"", "И Информация.Производитель = """"")+"
	|	"+?(УчитыватьАктуальность, "И Информация.ДатаАктуальности > &ТекущаяДата", "")+"
	|";
	
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	Запрос.УстановитьПараметр("Производитель", Производитель);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяУниверсальнаяДата()));
	
	Результат = Запрос.Выполнить().Выгрузить();
	Количество = Результат.Количество();
	Если Количество = 2 Тогда
		// Есть кеш требуемой актуальности
		Возврат Истина;
	ИначеЕсли Количество = 0 Тогда
		// Кеш еще не запрашивался или больше не актуален
		Возврат Ложь;
	Иначе
		Если Производитель = "" Тогда
			// Интересовал кеш вообще
			Если Результат[0].Производитель = "" Тогда
				// Запрос к сервису был. Отсутствуют данные
				Возврат Неопределено;
			Иначе
				// Кеш еще не запрашивался или больше не актуален
				Возврат Ложь;
			КонецЕсли;
		Иначе
			// Интересовал кеш по конкретному производителю
			Если Результат[0].Производитель = Производитель Тогда
				// Для конкретного производителя есть кеш нужной актуальности
				Возврат Истина;
			Иначе
				// Запрос к сервису был. Отсутствуют данные
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьКэшИнформация(Знач АртикулДляПоиска, Знач ОбязательныйПроизводитель="", Ошибка = "") 
	
	ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Данные", Новый Структура("Артикул", АртикулДляПоиска)));
	РезультатЗапроса = ВыполнитьЗапрос("information", "ПолучитьАналоги", ТелоЗапроса,,, Ошибка);
	
	Если РезультатЗапроса <> Неопределено Тогда
		
		Соответствие = Новый Соответствие;
		
		Для Каждого СтрокаРезультата ИЗ РезультатЗапроса Цикл
			
			Производитель = ПолучитьСопоставленнуюСсылку(СтрокаРезультата.Производитель, Истина, Истина);
			
			Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") И Производитель.Пустая() Тогда
				// Установлено не использовать!
				Продолжить;
			КонецЕсли;
			
			Если Соответствие.Получить(Производитель) = Неопределено Тогда
				НовыйПроизводитель = Истина;
				Соответствие.Вставить(Производитель, СтруктураСтрокиАртикула());
			Иначе
				НовыйПроизводитель = Ложь;
			КонецЕсли;
			СтрокаПроизводитель = Соответствие[Производитель];
			
			Если НовыйПроизводитель Тогда
				// Заполним реквизиты производителя
				ЗаполнитьЗначенияСвойств(СтрокаПроизводитель, СтрокаРезультата,, "Производитель, Аналоги");
				СтрокаПроизводитель.Артикул          = СтрокаРезультата.Артикул;
				СтрокаПроизводитель.Производитель    = Производитель;
				СтрокаПроизводитель.ПроизводительКод = СтрокаРезультата.Производитель.Код;
				
				// Добавим в аналоги самого производителя
				СтрокаАналога = СтрокаПроизводитель.Аналоги.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаАналога, СтрокаПроизводитель);
				СтрокаАналога.Раздел = 0;
				СтрокаАналога.ВидДополнения = "Группа аналогов";
			КонецЕсли;
			
			// Добавим аналоги из сервиса
			Если СтрокаРезультата.Свойство("Аналоги") И ТипЗнч(СтрокаРезультата.Аналоги) = Тип("Массив") Тогда
				
				Для Каждого РезультатАналог ИЗ СтрокаРезультата.Аналоги Цикл
					
					ПроизводительАналога = ПолучитьСопоставленнуюСсылку(РезультатАналог.Производитель, Истина, Истина);
					
					Если ТипЗнч(ПроизводительАналога) = Тип("СправочникСсылка.Производители") И ПроизводительАналога.Пустая() Тогда
						// Установлено не использовать!
						Продолжить;
					КонецЕсли;
					
					Если СтрокаПроизводитель.Аналоги.НайтиСтроки(Новый Структура("АртикулДляПоиска, Производитель", РезультатАналог.АртикулДляПоиска, ПроизводительАналога)).Количество() = 0 Тогда
						СтрокаАналога = СтрокаПроизводитель.Аналоги.Добавить();
						СтрокаАналога.АртикулДляПоиска        = РезультатАналог.АртикулДляПоиска;
						СтрокаАналога.Артикул                 = РезультатАналог.Артикул;
						СтрокаАналога.Производитель           = ПроизводительАналога;
						СтрокаАналога.ПроизводительКод        = РезультатАналог.Производитель.Код;
						СтрокаАналога.Наименование            = РезультатАналог.Наименование;
						СтрокаАналога.Раздел                  = РезультатАналог.Раздел;
						СтрокаАналога.ВидДополнения           = РезультатАналог.ВидДополнения;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	НачатьТранзакцию();
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.атИнформация");
	ЭлементБлокировки.УстановитьЗначение("АртикулДляПоиска", АртикулДляПоиска);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.атИнформация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.АртикулДляПоиска.Установить(АртикулДляПоиска, Истина);
	
	НаборЗаписей.Прочитать();
	
	ОбязательныеСтроки = Новый Соответствие;
	
	Сч = 0;
	Пока Сч < НаборЗаписей.Количество() Цикл
		СтрокаНабора = НаборЗаписей[0];
		Если Не ЗначениеЗаполнено(СтрокаНабора.Производитель) ИЛИ ЗначениеЗаполнено(СтрокаНабора.Артикул) Тогда
			// Это либо служебная строка или строка производителя из сервиса
			НаборЗаписей.Удалить(СтрокаНабора);
			Продолжить;
		КонецЕсли;
		// Это строка обязательного производителя, ее нужно оставить
		ОбязательныеСтроки.Вставить(СтрокаНабора);
		Сч = Сч + 1;
	КонецЦикла;
	
	ДатаАктуальности = ТекущаяУниверсальнаяДата() + ?(Соответствие = Неопределено, 1*60*60, 30*24*60*60);
	
	СтрокаНабора = НаборЗаписей.Добавить();
	СтрокаНабора.АртикулДляПоиска = АртикулДляПоиска;
	СтрокаНабора.Производитель    = "";
	СтрокаНабора.ДатаАктуальности = ДатаАктуальности;
	СтрокаНабора.СтатусЗапроса    = ?(Соответствие = Неопределено, 2, 1);
	СтрокаНабора.Артикул          = "";
	СтрокаНабора.Наименование     = ?(Соответствие = Неопределено, "Ошибка при выполении запроса", "Получены данные из сервиса");
	
	Если Соответствие <> Неопределено Тогда
		ОбязательныйПроизводительПолученИзСервиса = (ОбязательныйПроизводитель = "");
		Для Каждого КлючЗначение Из Соответствие Цикл
			СтрокаПроизводитель = КлючЗначение.Значение;
			ОбязательнаяСтрока = ОбязательныеСтроки.Получить(СтрокаПроизводитель.Производитель);
			Если ОбязательнаяСтрока <> Неопределено Тогда
				// Появились данные в сервисе!!
				НаборЗаписей.Удалить(ОбязательнаяСтрока);
			КонецЕсли;
			Если ОбязательныйПроизводитель = СтрокаПроизводитель.Производитель Тогда
				ОбязательныйПроизводительПолученИзСервиса = Истина;
			КонецЕсли;
			СтрокаНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНабора, СтрокаПроизводитель);
			СтрокаНабора.АртикулДляПоиска = АртикулДляПоиска;
			СтрокаНабора.ДатаАктуальности = ДатаАктуальности;
			СтрокаНабора.СтатусЗапроса    = 1;
			СтрокаНабора.Значение         = Новый ХранилищеЗначения(СтрокаПроизводитель, Новый СжатиеДанных(9));
		КонецЦикла;
		Если Не ОбязательныйПроизводительПолученИзСервиса Тогда
			// Добавим служебную строку про запрошенного производителя
			СтрокаНабора = НаборЗаписей.Добавить();
			СтрокаНабора.АртикулДляПоиска = АртикулДляПоиска;
			СтрокаНабора.Производитель    = ОбязательныйПроизводитель;
			СтрокаНабора.ДатаАктуальности = ДатаАктуальности;
			СтрокаНабора.СтатусЗапроса    = 1; // Ок, но в сервисе нет данных
			СтрокаНабора.Артикул          = "";
			СтрокаНабора.Наименование     = "В сервисе нет данных";
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		НаборЗаписей.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Ошибка = ?(ЗначениеЗаполнено(Ошибка), Ошибка+Символы.ПС, "")+ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

// Функция возвращает идентификатор группы аналогов
Функция НайтиИдентификаторГруппыАналогов(Знач Номенклатура, Знач АртикулДляПоиска=Неопределено, Знач Производитель=Неопределено, ИдентификаторНомераДетали=Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Если Не ЗначениеЗаполнено(АртикулДляПоиска) Тогда
			ИдентификаторНомераДетали = "";
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Если Не ЗначениеЗаполнено(Номенклатура.АртикулДляПоиска) Тогда
			// Не будет аналогов, поэтому
			ИдентификаторНомераДетали = "" + Номенклатура.УникальныйИдентификатор();
			Возврат ИдентификаторНомераДетали;
		КонецЕсли;
		АртикулДляПоиска = Номенклатура.АртикулДляПоиска;
		Производитель    = Номенклатура.Производитель;
	КонецЕсли;
	
	Данные = Новый Массив;
	Данные.Добавить(?(ЗначениеЗаполнено(Производитель), ?(ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители"), Производитель.Код, Производитель), "000000000"));
	Данные.Добавить("_");
	Данные.Добавить(АртикулДляПоиска);
	ИдентификаторНомераДетали = ХешированиеДанных(Данные);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ГруппыАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы
		|ИЗ
		|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
		|ГДЕ
		|	ГруппыАналогов.АртикулДляПоиска = &АртикулДляПоиска
		|	И ГруппыАналогов.Производитель = &Производитель";
		
	Запрос.УстановитьПараметр("Производитель", ?(ЗначениеЗаполнено(Производитель), Производитель, Справочники.Производители.ПустаяСсылка()));
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИдентификаторГруппы;
	КонецЕсли;
	
	Возврат ИдентификаторНомераДетали;
	
КонецФункции // НайтиИдентификаторГруппыАналогов() 

Функция СтруктураСтрокиАналога() Экспорт
	СтруктураАналога = Новый Структура();
	СтруктураАналога.Вставить("ХешАртикулПроизводитель", "");
	СтруктураАналога.Вставить("ХешАналога", "");
	СтруктураАналога.Вставить("Артикул", "");
	СтруктураАналога.Вставить("АртикулДляПоиска", "");
	СтруктураАналога.Вставить("ПроизводительКод", "");
	СтруктураАналога.Вставить("Производитель", Неопределено);
	СтруктураАналога.Вставить("Наименование", "");
	СтруктураАналога.Вставить("Номенклатура", Неопределено);
	СтруктураАналога.Вставить("Раздел", 0);
	СтруктураАналога.Вставить("ВидДополнения", "");
	Возврат СтруктураАналога;
КонецФункции

Функция СтруктураСтрокиАртикула() Экспорт
	СтруктураАртикула = Новый Структура();
	СтруктураАртикула.Вставить("ХешАртикулПроизводитель", "");
	СтруктураАртикула.Вставить("Артикул", "");
	СтруктураАртикула.Вставить("АртикулДляПоиска", "");
	СтруктураАртикула.Вставить("ПроизводительКод", "");
	СтруктураАртикула.Вставить("Производитель", Неопределено);
	СтруктураАртикула.Вставить("Наименование", "");
	СтруктураАртикула.Вставить("Номенклатура", Неопределено);
	СтруктураАртикула.Вставить("Аналоги", ПустаяТаблицаАналогов());
	Возврат СтруктураАртикула;
КонецФункции

Функция ХешированиеДанных(Знач Данные) Экспорт
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	Для Каждого Значение Из Данные Цикл
		Хеш.Добавить(Значение);
	КонецЦикла;
	Возврат СтрЗаменить(Хеш.ХешСумма, " ", "");
КонецФункции

Функция ПустаяТаблицаАналогов() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	// Составные типы
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Производители"));
	МассивТипов.Добавить(Тип("Строка"));
	Таблица.Колонки.Добавить("Производитель", Новый ОписаниеТипов(МассивТипов,, Новый КвалификаторыСтроки(100)));
	
	Таблица.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Таблица.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(100)));
	
	// Простые типы
	Таблица.Колонки.Добавить("ХешАртикулПроизводитель", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	Таблица.Колонки.Добавить("ХешАналога", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	Таблица.Колонки.Добавить("Артикул", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(30)));
	Таблица.Колонки.Добавить("АртикулДляПоиска", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(30)));
	Таблица.Колонки.Добавить("ПроизводительКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Таблица.Колонки.Добавить("Раздел", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный)));
	Таблица.Колонки.Добавить("ВидДополнения", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(20)));
	
	Возврат Таблица;
	
КонецФункции

// Функция - Получить информацию артикула
//  Выполняет запрос к сервису information КарточкаАртикула или КарточкаНоменклатуры
//
// Параметры:
//  СтруктураЗапроса - Структура	 - Параметры получения карточки (Артикул, Производитель (код) для артикула) или (Код для номенклатуры)
// 
// Возвращаемое значение:
//  Структура - Ответ сервиса
//
Функция ПолучитьИнформациюАртикула(Знач СтруктураЗапроса) Экспорт
	
	ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Данные", СтруктураЗапроса));
	
	Возврат ВыполнитьЗапрос("information", ?(СтруктураЗапроса.Свойство("Код"), "КарточкаНоменклатуры", "КарточкаАртикула"), ТелоЗапроса,, Истина);
	
КонецФункции

// Функция - Получить картинку
//
// Параметры:
//  АдресКартинки	 - Строка	 - веб адрес изображения
// 
// Возвращаемое значение:
//  ДвоичныеДанные - Данные картинки
//
Функция ПолучитьКартинку(Знач АдресКартинки) Экспорт 
	

	
КонецФункции

// Функция - Получить аналоги артикула производителя
//  Выполняет запрос к сервису information "ПолучитьАналоги"
//
// Параметры:
//  Артикул			 - Строка	 - Артикул производителя
//  Производитель	 - Строка	 - Код или наименование производителя. Если не указан, то аналоги будут возвращены для всех производителей артикула
//  ТолькоЛокальныеДанные	 - Булево	 - Признак того, что не требуется искать данные в сервисе Помощник продаж
//  Ошибка	 - Строка	 - Описание ошибки, если произошла ошибка при обращении к сервису Помощник продаж
//  ОбновитьКешСервиса	 - Неопределено, Булево	- Определяет необходимость принудительно обновления кеш сервиса. Истина - Обновить, Ложь - Актуализировать, Неопределено - Не заполнять кеш
// 
// Возвращаемое значение:
//  Массив - 
//
Функция ПолучитьАналоги(Знач АртикулДляПоиска, Знач Производитель, Знач ТолькоЛокальныеДанные=Ложь, Ошибка="", Знач ОбновитьКешСервиса=Неопределено) Экспорт 
	
	ТаблицаАналогов = ПустаяТаблицаАналогов();
	
	Если Не ЗначениеЗаполнено(АртикулДляПоиска) Тогда
		Возврат ТаблицаАналогов;
	КонецЕсли;
	
	Данные = Новый Массив;
	Данные.Добавить(?(ЗначениеЗаполнено(Производитель), ?(ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители"), Производитель.Код, Производитель), "000000000"));
	Данные.Добавить("_");
	Данные.Добавить(АртикулДляПоиска);
	ХешАртикулПроизводитель = ХешированиеДанных(Данные);
	
	Если Не ТолькоЛокальныеДанные Тогда
		// Проверяем кеш
		ПроверяемыйПроизводитель = ?(ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") И Не Производитель.Пустая(), Производитель, "");
		
		Если ОбновитьКешСервиса = Неопределено Тогда
			// Обновляем кеш только при необходимости, когда он не запрашивался или утратил актуальность
			Если ПроверитьКэшИнформация(АртикулДляПоиска, ПроверяемыйПроизводитель, Истина) = Ложь Тогда
				ЗаполнитьКэшИнформация(АртикулДляПоиска, ПроверяемыйПроизводитель, Ошибка);
			КонецЕсли;
			
		ИначеЕсли ОбновитьКешСервиса Тогда
			// Принудительно обновляем кеш
			ЗаполнитьКэшИнформация(АртикулДляПоиска, ПроверяемыйПроизводитель, Ошибка);
			
		КонецЕсли;
		
		// Здесь нам уже не важно актуален кеш или нет. Все что хотели, мы попытались обновить
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Информация.Значение
		|ИЗ
		|	РегистрСведений.атИнформация КАК Информация
		|ГДЕ
		|	Информация.АртикулДляПоиска = &АртикулДляПоиска
		|	И Информация.Производитель = &Производитель
		|	И Информация.СтатусЗапроса = 1";
		Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
		Запрос.УстановитьПараметр("Производитель", Производитель);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтрокаПроизводитель = Выборка.Значение.Получить();
			Если ТипЗнч(СтрокаПроизводитель) = Тип("Структура") Тогда
				ТаблицаАналогов = СтрокаПроизводитель.Аналоги;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Добавим локальные аналоги
	Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") Тогда
		
		РезультатАналоги = ПолучитьЛокальныеАналоги(ХешАртикулПроизводитель, АртикулДляПоиска, Производитель);
		
		// Объединим с аналогами сервиса, причем локальные аналоги в приоритете
		Для Каждого СтрокаАналог Из ТаблицаАналогов Цикл
			Если РезультатАналоги.НайтиСтроки(Новый Структура("АртикулДляПоиска, ПроизводительКод", СтрокаАналог.АртикулДляПоиска, СтрокаАналог.ПроизводительКод)).Количество() = 0
				И РезультатАналоги.НайтиСтроки(Новый Структура("АртикулДляПоиска, Производитель", СтрокаАналог.АртикулДляПоиска, СтрокаАналог.Производитель)).Количество() = 0 Тогда
				НоваяСтрока = РезультатАналоги.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаАналог);
				Данные = Новый Массив;
				Данные.Добавить(?(ЗначениеЗаполнено(СтрокаАналог.Производитель), ?(ТипЗнч(СтрокаАналог.Производитель) = Тип("СправочникСсылка.Производители"), СтрокаАналог.Производитель.Код, СтрокаАналог.Производитель), "000000000"));
				Данные.Добавить("_");
				Данные.Добавить(СтрокаАналог.АртикулДляПоиска);
				НоваяСтрока.ХешАналога = ХешированиеДанных(Данные);
				НоваяСтрока.ХешАртикулПроизводитель = ХешАртикулПроизводитель;
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаАналогов = РезультатАналоги;
		
	Иначе
		
		Для Каждого СтрокаАналог Из ТаблицаАналогов Цикл
			Данные = Новый Массив;
			Данные.Добавить(?(ЗначениеЗаполнено(СтрокаАналог.Производитель), ?(ТипЗнч(СтрокаАналог.Производитель) = Тип("СправочникСсылка.Производители"), СтрокаАналог.Производитель.Код, СтрокаАналог.Производитель), "000000000"));
			Данные.Добавить("_");
			Данные.Добавить(СтрокаАналог.АртикулДляПоиска);
			СтрокаАналог.ХешАналога = ХешированиеДанных(Данные);
			СтрокаАналог.ХешАртикулПроизводитель = ХешАртикулПроизводитель;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТаблицаАналогов.Количество() = 0 Тогда
		// Ни в сервисе, ни в группах аналогов нет данных для артикула производителя
		// Таблица аналогов будет пустая, а этого нам не надо!
		НоваяСтрока = ТаблицаАналогов.Добавить();
		НоваяСтрока.Артикул          = АртикулДляПоиска;
		НоваяСтрока.АртикулДляПоиска = АртикулДляПоиска;
		НоваяСтрока.ПроизводительКод = "";
		НоваяСтрока.Производитель    = Производитель;
		НоваяСтрока.Раздел           = 0;
		НоваяСтрока.ВидДополнения    = "Группа аналогов"; // Сами для себя мы всегда Группа аналогов
		НоваяСтрока.ХешАртикулПроизводитель = ХешАртикулПроизводитель;
		НоваяСтрока.ХешАналога       = ХешАртикулПроизводитель;
		
	КонецЕсли;
	
	Возврат ТаблицаАналогов;
	
КонецФункции

// Функция - Найти производителей артикула
//  Выполняет запрос к сервису information "НайтиПроизводителей"
//
// Параметры:
//  Артикул	 - Строка	 - Артикул производителя
// 
// Возвращаемое значение:
//  Массив - Массив описаний артикулов
//
Функция НайтиПроизводителейАртикула(Знач Артикул, Знач ПолучатьАналоги=Ложь, Знач ЛокальныеПроизводители=Ложь, Знач ТолькоЛокальныеДанные=Ложь, Ошибка="", Знач ОбновитьКешСервиса=Неопределено) Экспорт 
	
	АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул);
	Если Не ЗначениеЗаполнено(АртикулДляПоиска) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	СоответствиеПроизводителей = Новый Соответствие;
	
	Если Не ТолькоЛокальныеДанные Тогда
		
		Если ОбновитьКешСервиса = Неопределено Тогда
			// Обновляем кеш при необходимости, когда он еще не запрашивался или утратил актуальность
			Если ПроверитьКэшИнформация(АртикулДляПоиска, "", Истина) = Ложь Тогда
				ЗаполнитьКэшИнформация(АртикулДляПоиска, "", Ошибка);
			КонецЕсли;
			
		ИначеЕсли ОбновитьКешСервиса Тогда
			// Принудительно обновляем кеш
			ЗаполнитьКэшИнформация(АртикулДляПоиска, "", Ошибка);
			
		КонецЕсли;
		
		// Читаем кеш
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Информация.Значение
		|ИЗ
		|	РегистрСведений.атИнформация КАК Информация
		|ГДЕ
		|	Информация.АртикулДляПоиска = &АртикулДляПоиска
		|	И Информация.Производитель <> """"
		|	И Информация.СтатусЗапроса = 1";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			СтруктураПроизводителя = Выборка.Значение.Получить();
			Если ТипЗнч(СтруктураПроизводителя) = Тип("Структура") Тогда
				
				Если Не ЗначениеЗаполнено(СтруктураПроизводителя.Производитель) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ТипЗнч(СтруктураПроизводителя.Производитель) = Тип("Строка") Тогда
					// Производитель не был сопоставлен на момент сохранения кеш
					СтруктураСловаря = Новый Структура("Тип, Код, Наименование", "Производители", СтруктураПроизводителя.ПроизводительКод, СтруктураПроизводителя.Производитель);
					Производитель = ПолучитьСопоставленнуюСсылку(СтруктураСловаря, Истина, Ложь);
					Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") Тогда
						Если Производитель.Пустая() Тогда
							Продолжить;
						КонецЕсли;
						ПроизводительКод = Производитель.Код;
					Иначе
						ПроизводительКод = Производитель; // Просто наименование производителя
					КонецЕсли;
				Иначе
					// Ранее была сохранена ссылка производителя
					Производитель = СтруктураПроизводителя.Производитель;
					ПроизводительКод = Производитель.Код;
				КонецЕсли;
				
				Данные = Новый Массив;
				Данные.Добавить(ПроизводительКод);
				Данные.Добавить("_");
				Данные.Добавить(СтруктураПроизводителя.АртикулДляПоиска);
				
				ХешАртикулПроизводитель = ХешированиеДанных(Данные);
				
				Если СоответствиеПроизводителей.Получить(ХешАртикулПроизводитель) = Неопределено Тогда
					НовыйПроизводитель = Истина;
					СтруктураСтроки = СтруктураСтрокиАртикула();
					СтруктураСтроки.Вставить("ИсточникСтроки", "Артикул");
					СтруктураСтроки.Вставить("Онлайн", Истина);
					СоответствиеПроизводителей.Вставить(ХешАртикулПроизводитель, СтруктураСтроки);
				Иначе
					НовыйПроизводитель = Ложь;
				КонецЕсли;
				
				НоваяСтрока = СоответствиеПроизводителей[ХешАртикулПроизводитель];
				
				Если НовыйПроизводитель Тогда
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураПроизводителя,, "Производитель, Аналоги");
					НоваяСтрока.ХешАртикулПроизводитель = ХешАртикулПроизводитель;
					НоваяСтрока.Производитель = Производитель;
					Если Не ЗначениеЗаполнено(НоваяСтрока.Артикул) Тогда
						НоваяСтрока.Артикул = СтруктураПроизводителя.АртикулДляПоиска;
					КонецЕсли;
				КонецЕсли;
				
				Если ПолучатьАналоги Тогда
					
					Если НовыйПроизводитель Тогда
						// Добавим в аналоги самого производителя
						НоваяСтрокаАналога = НоваяСтрока.Аналоги.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаАналога, НоваяСтрока);
						НоваяСтрокаАналога.Раздел = 0;
						НоваяСтрокаАналога.ВидДополнения = "Группа аналогов";
						НоваяСтрокаАналога.ХешАналога = НоваяСтрока.ХешАртикулПроизводитель;
					КонецЕсли;
					
					Для Каждого СтрокаАналога ИЗ СтруктураПроизводителя.Аналоги Цикл
						
						Если Не ЗначениеЗаполнено(СтрокаАналога.Производитель) Тогда
							Продолжить;
						КонецЕсли;
						
						Если ТипЗнч(СтрокаАналога.Производитель) = Тип("Строка") Тогда
							// Производитель не был сопоставлен на момент сохранения кеш
							СтруктураСловаря = Новый Структура("Тип, Код, Наименование", "Производители", СтрокаАналога.ПроизводительКод, СтрокаАналога.Производитель);
							ПроизводительАналога = ПолучитьСопоставленнуюСсылку(СтруктураСловаря, Истина, Ложь);
							Если ТипЗнч(ПроизводительАналога) = Тип("СправочникСсылка.Производители") Тогда
								Если ПроизводительАналога.Пустая() Тогда
									Продолжить;
								КонецЕсли;
								ПроизводительАналогаКод = ПроизводительАналога.Код;
							Иначе
								ПроизводительАналогаКод = ПроизводительАналога;
							КонецЕсли;
						Иначе
							ПроизводительАналога    = СтрокаАналога.Производитель;
							ПроизводительАналогаКод = ПроизводительАналога.Код;
						КонецЕсли;
						
						Данные = Новый Массив;
						Данные.Добавить(ПроизводительАналогаКод);
						Данные.Добавить("_");
						Данные.Добавить(СтрокаАналога.АртикулДляПоиска);
						
						ХешАналога = ХешированиеДанных(Данные);
						
						Если НоваяСтрока.Аналоги.Найти(ХешАналога, "ХешАналога") = Неопределено Тогда
							
							НоваяСтрокаАналога = НоваяСтрока.Аналоги.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрокаАналога, СтрокаАналога);
							НоваяСтрокаАналога.Производитель           = ПроизводительАналога;
							НоваяСтрокаАналога.ХешАналога              = ХешАналога;
							НоваяСтрокаАналога.ХешАртикулПроизводитель = НоваяСтрока.ХешАртикулПроизводитель;
							
						КонецЕсли;
						
					КонецЦикла;
					
					// Добавим локальных аналогов
					Если НовыйПроизводитель И ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") Тогда
						
						РезультатАналоги = ПолучитьЛокальныеАналоги(НоваяСтрока.ХешАртикулПроизводитель, НоваяСтрока.АртикулДляПоиска, НоваяСтрока.Производитель);
						
						// Соединим аналоги сервиса с локальными аналогам (приоритет у локальных аналогов)
						Для Каждого СтрокаАналог Из НоваяСтрока.Аналоги Цикл
							
							Если РезультатАналоги.НайтиСтроки(Новый Структура("АртикулДляПоиска, ПроизводительКод", СтрокаАналог.АртикулДляПоиска, СтрокаАналог.ПроизводительКод)).Количество() = 0
								И РезультатАналоги.НайтиСтроки(Новый Структура("АртикулДляПоиска, Производитель", СтрокаАналог.АртикулДляПоиска, СтрокаАналог.Производитель)).Количество() = 0 Тогда
								НоваяСтрокаАналога = РезультатАналоги.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрокаАналога, СтрокаАналог);
								Данные = Новый Массив;
								Данные.Добавить(?(ЗначениеЗаполнено(СтрокаАналог.Производитель), ?(ТипЗнч(СтрокаАналог.Производитель) = Тип("СправочникСсылка.Производители"), СтрокаАналог.Производитель.Код, СтрокаАналог.Производитель), "000000000"));
								Данные.Добавить("_");
								Данные.Добавить(СтрокаАналог.АртикулДляПоиска);
								НоваяСтрокаАналога.ХешАналога              = ХешированиеДанных(Данные);
								НоваяСтрокаАналога.ХешАртикулПроизводитель = НоваяСтрока.ХешАртикулПроизводитель;
							КонецЕсли;
							
						КонецЦикла;
						
						// Заменим таблицу
						НоваяСтрока.Аналоги = РезультатАналоги;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Добавим локальных производителей
	Если ЛокальныеПроизводители Тогда
		
		МассивПроизводителей = Справочники.Номенклатура.НайтиПроизводителейПоАртикулу(Артикул);
		
		Для Каждого Производитель Из МассивПроизводителей Цикл
			
			АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, Производитель);
			
			Данные = Новый Массив;
			Данные.Добавить(?(Производитель.Пустая(), "000000000", Производитель.Код));
			Данные.Добавить("_");
			Данные.Добавить(АртикулДляПоиска);
			
			ХешАртикулПроизводитель = ХешированиеДанных(Данные);
			
			СтрокаПроизводителя = СоответствиеПроизводителей.Получить(ХешАртикулПроизводитель);
			Если СтрокаПроизводителя <> Неопределено Тогда
				// Локально у нас есть такой производитель
				СтрокаПроизводителя.Онлайн = Ложь;
				Продолжить;
			КонецЕсли;
			
			СтруктураСтроки = СтруктураСтрокиАртикула();
			СтруктураСтроки.Вставить("ИсточникСтроки", "Артикул");
			СтруктураСтроки.Вставить("Онлайн", Ложь);
			
			СоответствиеПроизводителей.Вставить(ХешАртикулПроизводитель, СтруктураСтроки);
			НоваяСтрока = СоответствиеПроизводителей[ХешАртикулПроизводитель];
			
			ПроизводительКод = "";
			Если Не Производитель.Пустая() Тогда
				ПроизводительСервиса = ПолучитьСловарьПоСсылке(Производитель, Ложь);
				Если ЗначениеЗаполнено(ПроизводительСервиса) Тогда
					ПроизводительКод = ПроизводительСервиса.Код;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока.ХешАртикулПроизводитель = ХешАртикулПроизводитель;
			НоваяСтрока.Артикул                 = Артикул;
			НоваяСтрока.АртикулДляПоиска        = АртикулДляПоиска;
			НоваяСтрока.ПроизводительКод        = ПроизводительКод;
			НоваяСтрока.Производитель           = Производитель;
			//НоваяСтрока.Наименование            = "";
			//НоваяСтрока.Номенклатура            = Неопределено;
			
			Если ПолучатьАналоги Тогда
				НоваяСтрока.Аналоги             = ПолучитьЛокальныеАналоги(ХешАртикулПроизводитель, АртикулДляПоиска, Производитель);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Соорудим массив для возврата
	МассивАртикулов = Новый Массив;
	Для Каждого КлючЗначение Из СоответствиеПроизводителей Цикл
		МассивАртикулов.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат МассивАртикулов;
	
КонецФункции

Функция ПолучитьЛокальныеАналоги(ХешАртикулПроизводитель, АртикулДляПоиска, Производитель)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГруппыАналогов.Артикул,
	|	ГруппыАналогов.АртикулДляПоиска,
	|	ЕСТЬNULL(атСловари.Код, """") КАК ПроизводительКод,
	|	ГруппыАналогов.Производитель КАК ПроизводительЛок,
	|	ЕСТЬNULL(ГруппыАналогов.Производитель.Код, ""000000000"") КАК ПроизводительЛокКод,
	|	ГруппыАналогов.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ГруппыАналогов.АртикулДляПоиска = &АртикулДляПоиска
	|				И ГруппыАналогов.Производитель = &Производитель
	|			ТОГДА 0
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК Раздел,
	|	""Группа аналогов"" КАК ВидДополнения
	|ПОМЕСТИТЬ втАналоги
	|ИЗ
	|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атСловари КАК атСловари
	|		ПО (атСловари.Тип = ""Производители"")
	|			И (ГруппыАналогов.Производитель = (ВЫРАЗИТЬ(атСловари.Значение КАК Справочник.Производители)))
	|			И (ГруппыАналогов.Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка))
	|ГДЕ
	|	ГруппыАналогов.ИдентификаторГруппы В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ГруппыАналогов.ИдентификаторГруппы
	|			ИЗ
	|				РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	|			ГДЕ
	|				ГруппыАналогов.АртикулДляПоиска = &АртикулДляПоиска
	|				И ГруппыАналогов.Производитель = &Производитель)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Замены.Артикул,
	|	Замены.АртикулДляПоиска,
	|	ЕСТЬNULL(атСловари.Код, """"),
	|	Замены.Производитель,
	|	ЕСТЬNULL(Замены.Производитель.Код, ""000000000""),
	|	"" "" + Замены.Группа + "" "" + Замены.Описание,
	|	1,
	|	""Старый номер""
	|ИЗ
	|	РегистрСведений.Замены КАК Замены
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атСловари КАК атСловари
	|		ПО (атСловари.Тип = ""Производители"")
	|			И (Замены.Производитель = (ВЫРАЗИТЬ(атСловари.Значение КАК Справочник.Производители)))
	|			И (Замены.Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка))
	|ГДЕ
	|	Замены.АртикулЗаменыДляПоиска = &АртикулДляПоиска
	|	И Замены.Производитель = &Производитель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Замены.АртикулЗамены,
	|	Замены.АртикулЗаменыДляПоиска,
	|	ЕСТЬNULL(атСловари.Код, """"),
	|	Замены.Производитель,
	|	ЕСТЬNULL(Замены.Производитель.Код, ""000000000""),
	|	"" "" + Замены.Группа + "" "" + Замены.Описание,
	|	2,
	|	""Новый номер""
	|ИЗ
	|	РегистрСведений.Замены КАК Замены
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атСловари КАК атСловари
	|		ПО (атСловари.Тип = ""Производители"")
	|			И (Замены.Производитель = (ВЫРАЗИТЬ(атСловари.Значение КАК Справочник.Производители)))
	|			И (Замены.Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка))
	|ГДЕ
	|	Замены.АртикулДляПоиска = &АртикулДляПоиска
	|	И Замены.Производитель = &Производитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втАналоги.Артикул) КАК Артикул,
	|	втАналоги.АртикулДляПоиска,
	|	втАналоги.ПроизводительКод,
	|	втАналоги.ПроизводительЛок,
	|	втАналоги.ПроизводительЛокКод,
	|	МИНИМУМ(втАналоги.Наименование) КАК Наименование,
	|	МИНИМУМ(втАналоги.Раздел) КАК Раздел,
	|	МАКСИМУМ(втАналоги.ВидДополнения) КАК ВидДополнения
	|ИЗ
	|	втАналоги КАК втАналоги
	|
	|СГРУППИРОВАТЬ ПО
	|	втАналоги.АртикулДляПоиска,
	|	втАналоги.ПроизводительКод,
	|	втАналоги.ПроизводительЛок,
	|	втАналоги.ПроизводительЛокКод";
	
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	Запрос.УстановитьПараметр("Производитель", Производитель);
	
	РезультатАналоги = Запрос.Выполнить().Выгрузить();
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Производители"));
	МассивТипов.Добавить(Тип("Строка"));
	РезультатАналоги.Колонки.Добавить("Производитель", Новый ОписаниеТипов(МассивТипов,, Новый КвалификаторыСтроки(100)));
	РезультатАналоги.Колонки.Добавить("ХешАртикулПроизводитель", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	РезультатАналоги.Колонки.Добавить("ХешАналога", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	
	РезультатАналоги.ЗаполнитьЗначения(ХешАртикулПроизводитель, "ХешАртикулПроизводитель");
	
	Для Каждого СтрокаАналог Из РезультатАналоги Цикл
		Данные = Новый Массив;
		Данные.Добавить(СтрокаАналог.ПроизводительЛокКод);
		Данные.Добавить("_");
		Данные.Добавить(СтрокаАналог.АртикулДляПоиска);
		СтрокаАналог.ХешАналога = ХешированиеДанных(Данные);
		СтрокаАналог.Производитель = СтрокаАналог.ПроизводительЛок;
	КонецЦикла;
	
	РезультатАналоги.Колонки.Удалить("ПроизводительЛок");
	РезультатАналоги.Колонки.Удалить("ПроизводительЛокКод");
	
	Возврат РезультатАналоги;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////SELECTION///////////////////////////////////////////////

Функция ВыполнитьЗапросSelection(ИмяМетода, Отбор = Неопределено, Данные = Неопределено, ИспользоватьКеш = Истина, Ошибка = "") Экспорт
	
	ТелоЗапроса = "";
	СтруктураПараметров = Новый Структура;
	
	Если Отбор <> Неопределено Тогда
		СтруктураПараметров.Вставить("Отбор", Отбор);
	КонецЕсли;
	Если Данные <> Неопределено Тогда
		СтруктураПараметров.Вставить("Данные", Данные);
	КонецЕсли;
	
	Если СтруктураПараметров.Количество() > 0 Тогда
		ТелоЗапроса = ПолучитьТекстСообщения(СтруктураПараметров);
	КонецЕсли;
	
	Возврат ВыполнитьЗапрос("selection", ИмяМетода, ТелоЗапроса,, ИспользоватьКеш, Ошибка);
	
КонецФункции

Функция ПреобразоватьНомерПоМаске(Знач НомерПоиска)
	НомерПоиска = СтрЗаменить(СокрЛП(ВРег(НомерПоиска)), " ", "");
	ДопустимыеСимволы = "-?0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ";
	Возврат обСтрокаПоМаске(НомерПоиска, ДопустимыеСимволы, "?");
КонецФункции

Функция ПроверитьНомер(Знач Текст) Экспорт
	
	ПараметрыНомера = Новый Структура("ВидНомера, Номер", "", Текст);
	
	Если ЗначениеЗаполнено(Текст) Тогда
		Номер = ПреобразоватьНомерПоМаске(Текст);
		Если Найти(Номер, "?") = 0 Тогда
			Если СтрЧислоВхождений(Номер, "-") > 0 Тогда
				Если СтрЧислоВхождений(Номер, "-") = 1 Тогда
					Если СтрДлина(Номер) >= 9 И СтрДлина(Номер) <= 17 Тогда
						ПараметрыНомера.ВидНомера = "Frame";
						Поз_ = Найти(Номер, "-");
						ПараметрыНомера.Вставить("КодКузова", ЛЕВ(Номер, Поз_-1)); 
						ПараметрыНомера.Вставить("НомерКузова", СРЕД(Номер, Поз_+1)); 
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли СтрДлина(Номер) = 17 Тогда
				ПараметрыНомера.ВидНомера = "VIN";
				ПараметрыНомера.Вставить("VIN", Номер); 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыНомера;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////ВЕБ ПРАЙС-ЛИСТЫ///////////////////////////////////////

Процедура ИзменитьПроизводителяВРегистреПрайсЛистыКонтрагентов(ПрайсЛист=Неопределено, ПроизводительВПрайсЛисте, НовыйПроизводитель) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.ПрайсЛист КАК ПрайсЛист,
	|	ПрайсЛистыКонтрагентов.ПрайсЛист.Владелец КАК Контрагент,
	|	ПрайсЛистыКонтрагентов.АртикулДляПоиска,
	|	ПрайсЛистыКонтрагентов.Производитель,
	|	ПрайсЛистыКонтрагентов.ДатаЗаписи,
	|	ПрайсЛистыКонтрагентов.КодПредложения
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовВременный КАК ПрайсЛистыКонтрагентов
	|ГДЕ
	|	"+?(ПрайсЛист = Неопределено, "ПрайсЛистыКонтрагентов.ПрайсЛист.ВидПрайсЛиста = ЗНАЧЕНИЕ(Перечисление.ВидыПрайсЛистов.ВебПрайсЛист)", "ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист")+"
	|	И ПрайсЛистыКонтрагентов.Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
	|	И ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте = &ПроизводительВПрайсЛисте
	|ИТОГИ ПО
	|	ПрайсЛист";
	
	Запрос.УстановитьПараметр("ПрайсЛист",                ПрайсЛист);
	Запрос.УстановитьПараметр("ПроизводительВПрайсЛисте", ПроизводительВПрайсЛисте);
	
	ВыборкаПрайсЛистов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПрайсЛистов.Следующий() Цикл
	
		ДатаЗаписи = ТекущаяДата();
		КодПредложения = 0;
		
		НаборЗаписей = РегистрыСведений.ПрайсЛистыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПрайсЛист.Установить(ВыборкаПрайсЛистов.ПрайсЛист);
		НаборЗаписей.Отбор.ДатаЗаписи.Установить(ДатаЗаписи);
		
		Выборка = ВыборкаПрайсЛистов.Выбрать();
		Пока Выборка.Следующий() Цикл
			Менеджер = РегистрыСведений.ПрайсЛистыКонтрагентовВременный.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Менеджер, Выборка);
			Менеджер.Прочитать();
			Если Менеджер.Выбран() Тогда
				КодПредложения = КодПредложения + 1;
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Менеджер);
				НоваяЗапись.Производитель = НовыйПроизводитель;
				НоваяЗапись.ДатаЗаписи = ДатаЗаписи;
				НоваяЗапись.КодПредложения = КодПредложения;
				НоваяЗапись.Контрагент = Выборка.Контрагент;
				Менеджер.Удалить();
			КонецЕсли;
		КонецЦикла;
		
		Если НаборЗаписей.Количество() > 0 Тогда
			Попытка
				НаборЗаписей.Записать();
			Исключение
				// Ничего страшного!
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрайсЛистыЗаполнитьТаблицуОпций(Знач КодПрайсЛиста, Параметры, ТаблицаОпций) Экспорт 
	
	ТаблицаОпций.Очистить();
	
	МассивПараметров = Новый Массив;
	
	Для Каждого СтрокаПараметров ИЗ Параметры Цикл
		
		МассивПараметров.Добавить(Новый Структура("Имя, Значение", СтрокаПараметров.Имя, СтрокаПараметров.Значение));
		
	КонецЦикла;
	
	МассивПрайсЛиста = Новый Массив;
	МассивПрайсЛиста.Добавить(Новый Структура("ВебПрайсЛист, Параметры", СокрЛП(КодПрайсЛиста), МассивПараметров));
	
	СтруктураСообщения = Новый Структура("Данные", Новый Структура("ВебПрайсЛисты", МассивПрайсЛиста));
	ТелоЗапроса = СокрЛП(ПолучитьТекстСообщения(СтруктураСообщения));
	
	Результат = ВыполнитьЗапрос("webpricelists", "ПолучитьДоступныеОпции", ТелоЗапроса);
	
	Если Результат <> Неопределено И Результат.Количество() > 0 Тогда 
		
		Для Каждого СтрокаОпции ИЗ Результат[0].ДоступныеОпции Цикл
			
			НоваяСтрока = ТаблицаОпций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОпции,, "СписокЗначений");
			
			СписокЗначений = Новый СписокЗначений;
			
			Для Каждого СтрокаЗначения ИЗ СтрокаОпции.СписокЗначений Цикл
				
				СписокЗначений.Добавить(СтрокаЗначения.Значение, СтрокаЗначения.Представление);
				
			КонецЦикла;
			
			НоваяСтрока.СписокЗначений = СписокЗначений;
			
			Если ПустаяСтрока(НоваяСтрока.ЗначениеПоУмолчанию) И НоваяСтрока.СписокЗначений.Количество() > 0 Тогда
				НоваяСтрока.ЗначениеПоУмолчанию = НоваяСтрока.СписокЗначений[0];
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьВыборкуПрайсЛистов() Экспорт 
	
	Если ПараметрыСеанса.атДоступныеСервисы.Свойство("ВебПрайсЛисты") Тогда
		
		ВебПрайсЛисты = Новый СписокЗначений;
		
		Для Каждого ПрайсЛист Из ПараметрыСеанса.атДоступныеСервисы.ВебПрайсЛисты Цикл
			Если ПрайсЛист.Доступен Тогда
				ВебПрайсЛисты.Добавить(ПрайсЛист.ВебПрайсЛист.Код);
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(атСловари.Значение КАК Справочник.ПрайсЛистыКонтрагентов).Код КАК Код,
		|	ВЫРАЗИТЬ(атСловари.Значение КАК Справочник.ПрайсЛистыКонтрагентов).Наименование КАК Наименование,
		|	ВЫРАЗИТЬ(атСловари.Значение КАК Справочник.ПрайсЛистыКонтрагентов).Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.атСловари КАК атСловари
		|ГДЕ
		|	атСловари.Тип = ""ВебПрайсЛисты""
		|	И атСловари.Значение ССЫЛКА Справочник.ПрайсЛистыКонтрагентов
		|	И НЕ атСловари.Значение = ЗНАЧЕНИЕ(Справочник.ПрайсЛистыКонтрагентов.ПустаяСсылка)
		|	И атСловари.Код В(&ВебПрайсЛисты)";
		Запрос.УстановитьПараметр("ВебПрайсЛисты", ВебПрайсЛисты);
		
		Возврат Запрос.Выполнить().Выгрузить();
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПроверитьКэшПрайсЛиста(Знач ПрайсЛист, Знач ХешАртикулПроизводитель) Экспорт 
	
	Если ПрайсЛист.ВидПрайсЛиста <> Перечисления.ВидыПрайсЛистов.ВебПрайсЛист ИЛИ Не ЗначениеЗаполнено(ХешАртикулПроизводитель) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВебПрайсЛисты.ИдентификаторЗапроса
	|ИЗ
	|	РегистрСведений.атВебПрайсЛисты КАК ВебПрайсЛисты
	|ГДЕ
	|	ВебПрайсЛисты.ПрайсЛист = &ПрайсЛист
	|	И ВебПрайсЛисты.ДатаАктуальности >= &ТекущаяДата
	|	И ВебПрайсЛисты.ИдентификаторЗапроса = &ИдентификаторЗапроса";
	
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛист);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяУниверсальнаяДата());
	Запрос.УстановитьПараметр("ИдентификаторЗапроса", ХешАртикулПроизводитель);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция РазобратьНаличиеВПрайсЛисте(Знач ЗначениеПоля)
	
	Наличие = Ложь;
	
	Если ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
		Если ЗначениеПоля <> 0 Тогда
			Наличие = ЗначениеПоля;
		КонецЕсли;
	Иначе
		// Строка
		Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
			Если Найти("*+VЕД><YОЗБМ", ВРЕГ(ЛЕВ(ЗначениеПоля, 1))) > 0 Тогда
				//(+,v,Есть,Да,Yes,>1,<5,Ост,Завались,Больше,Много)
				НовоеКоличество = "";
				Для Сч = 1 По СтрДлина(ЗначениеПоля) Цикл
					Если Найти("1234567890,", Сред(ЗначениеПоля, Сч, 1)) > 0 Тогда
						НовоеКоличество = НовоеКоличество + Сред(ЗначениеПоля, Сч, 1);
					КонецЕсли;
				КонецЦикла;
				Если ЗначениеЗаполнено(НовоеКоличество) Тогда
					Наличие = Число(НовоеКоличество);
				Иначе
					Наличие = Истина;
				КонецЕсли;
			ИначеЕсли Найти("123456789", ЛЕВ(ЗначениеПоля, 1))>0 Тогда
				Попытка
					Количество = Число(ЗначениеПоля);
					Наличие = Количество;
				Исключение
					Наличие = Истина;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Наличие;
	
КонецФункции //РазобратьНаличиеВПрайсЛисте()

Процедура ЗаполнитьКэшПрайсЛиста(Знач Артикул, Знач ПроизводительКод, Знач Производитель, Знач ПрайсЛист, Знач ХешАртикулПроизводитель, Ошибка="") Экспорт 
	
	Если ПрайсЛист.ВидПрайсЛиста <> Перечисления.ВидыПрайсЛистов.ВебПрайсЛист ИЛИ Не ЗначениеЗаполнено(ХешАртикулПроизводитель) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаАктуальности = ТекущаяУниверсальнаяДата() + Макс(ПрайсЛист.СрокАктуальностиПредложений, 24*60*60);
	
	ТЗПрайсЛиста = РегистрыСведений.ПрайсЛистыКонтрагентовВременный.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТЗПрайсЛиста.Колонки.Добавить("ПроизводительКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	
	ДанныеПрайсЛиста = ПолучитьСловарьПоСсылке(ПрайсЛист);
	
	Если ДанныеПрайсЛиста <> Неопределено Тогда
		
		ДополнительныеПараметры = ПолучитьДополнительныеПараметры(ДанныеПрайсЛиста.Тип, ДанныеПрайсЛиста.Код);
		
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
			И ДополнительныеПараметры.Свойство("Параметры") Тогда
			
			МассивПараметров = Новый Массив;
			
			Для Каждого СтрокаПараметров ИЗ ДополнительныеПараметры.Параметры Цикл
				
				МассивПараметров.Добавить(Новый Структура("Имя, Значение", СтрокаПараметров.Имя, СтрокаПараметров.Значение));
				
			КонецЦикла;
			
			МассивПрайсЛиста = Новый Массив;
			МассивПрайсЛиста.Добавить(Новый Структура("ВебПрайсЛист, Параметры", ДанныеПрайсЛиста.Код, МассивПараметров));
			
			Данные = Новый Структура;
			Данные.Вставить("ВебПрайсЛисты", МассивПрайсЛиста);
			Данные.Вставить("Артикул", Артикул);
			
			Если ЗначениеЗаполнено(ПроизводительКод) Тогда
				ПроизводительЗапроса = ПроизводительКод;
			Иначе
				// А вдруг у нас есть Производитель сервиса?
				Если ТипЗнч(Производитель) = Тип("СправочникСсылка.Производители") Тогда
					ПроизводительСервиса = ПолучитьСловарьПоСсылке(Производитель, Ложь);
					Если ЗначениеЗаполнено(ПроизводительСервиса) Тогда
						ПроизводительЗапроса = ПроизводительСервиса.Код;
					Иначе
						ПроизводительЗапроса = "" + Производитель;
					КонецЕсли;
				Иначе
					ПроизводительЗапроса = Производитель;
				КонецЕсли;
			КонецЕсли;
			Данные.Вставить("Производитель", ПроизводительЗапроса);
			
			ТелоЗапроса = ПолучитьТекстСообщения(Новый Структура("Данные", Данные));
			
			Результат = ВыполнитьЗапрос("webpricelists", "ПолучитьЦены", ТелоЗапроса,,, Ошибка);
			
			Если Результат <> Неопределено И Результат.Количество() > 0 Тогда
				
				//СоответствиеПроизводители = Новый Соответствие;
				СоответствиеВалюта = Новый Соответствие;
				СоответствиеНаличие = Новый Соответствие;
				
				КодПредложения = 0;
				
				Для Каждого СтрокаПрайса ИЗ Результат Цикл
					
					КодПредложения = КодПредложения + 1;
					
					НоваяСтрока = ТЗПрайсЛиста.Добавить();
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПрайса,, "Валюта, Производитель");
					
					НоваяСтрока.ПрайсЛист = ПрайсЛист;
					НоваяСтрока.КодПредложения = КодПредложения;
					НоваяСтрока.КлючСтрокиПоставщика = СтрЗаменить(СтрокаПрайса.ИдентификаторВариантаЦен, "-", "");
					
					НоваяСтрока.СрокПоставкиМинимальный = СтрокаПрайса.СрокПоставкиМин;
					НоваяСтрока.СрокПоставкиМаксимальный = СтрокаПрайса.СрокПоставкиМакс;
					НоваяСтрока.СрокПоставкиВПрайсЛисте = СтрокаПрайса.СрокПоставки;
					
					НоваяСтрока.АртикулВПрайсЛисте = СтрокаПрайса.Артикул;
					
					Если СоответствиеНаличие[СтрокаПрайса.Наличие] = Неопределено Тогда
						СоответствиеНаличие.Вставить(СтрокаПрайса.Наличие, РазобратьНаличиеВПрайсЛисте(СтрокаПрайса.Наличие));
					КонецЕсли;
					НоваяСтрока.Количество = СоответствиеНаличие[СтрокаПрайса.Наличие];
					
					Если СоответствиеВалюта[СтрокаПрайса.Валюта.Код] = Неопределено Тогда
						Валюта = ПолучитьСопоставленнуюСсылку(СтрокаПрайса.Валюта,, Истина);
						Если Не ЗначениеЗаполнено(Валюта) Тогда // Вдруг не установлена автосинхронизация
							Валюта = Справочники.Валюты.НайтиПоКоду(СтрокаПрайса.Валюта.Код);
						КонецЕсли;
						Если Не ЗначениеЗаполнено(Валюта) Тогда
							Валюта = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
						КонецЕсли;
						СоответствиеВалюта.Вставить(СтрокаПрайса.Валюта.Код, Валюта);
					КонецЕсли;
					НоваяСтрока.Валюта = СоответствиеВалюта[СтрокаПрайса.Валюта.Код];
					
					НоваяСтрока.ПроизводительКод = СтрокаПрайса.Производитель.Код;
					НоваяСтрока.ПроизводительВПрайсЛисте = СтрокаПрайса.Производитель.Наименование;
					
					// Ссылку на производителя будем искать уже в запросе на выборку данных из прайс-листа
					
					//Если СоответствиеПроизводители[СтрокаПрайса.Производитель.Код] = Неопределено Тогда
					//	СоответствиеПроизводители.Вставить(СтрокаПрайса.Производитель.Код, ПолучитьСсылкуОбъекта(СтрокаПрайса.Производитель, Истина, Ложь));
					//КонецЕсли;
					//НоваяСтрока.Производитель = СоответствиеПроизводители[СтрокаПрайса.Производитель.Код];
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НачатьТранзакцию();
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.атВебПрайсЛисты");
	ЭлементБлокировки.УстановитьЗначение("ПрайсЛист", ПрайсЛист);
	ЭлементБлокировки.УстановитьЗначение("ИдентификаторЗапроса", ХешАртикулПроизводитель);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.атВебПрайсЛисты.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ПрайсЛист.Установить(ПрайсЛист, Истина);
	НаборЗаписей.Отбор.ИдентификаторЗапроса.Установить(ХешАртикулПроизводитель, Истина);
	
	СтрокаНабора = НаборЗаписей.Добавить();
	СтрокаНабора.ПрайсЛист = ПрайсЛист;
	СтрокаНабора.ИдентификаторЗапроса = ХешАртикулПроизводитель;
	СтрокаНабора.ДатаАктуальности = ДатаАктуальности;
	СтрокаНабора.Артикул = Артикул;
	СтрокаНабора.Производитель = Производитель;
	СтрокаНабора.ПроизводительКод = Производитель;
	СтрокаНабора.Номенклатура = "";
	СтрокаНабора.Значение = Новый ХранилищеЗначения(ТЗПрайсЛиста, Новый СжатиеДанных(9));
	СтрокаНабора.КоличествоПредложений = ТЗПрайсЛиста.Количество();
	СтрокаНабора.Обработан = Ложь;
	
	Попытка
		НаборЗаписей.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		#Если Клиент Тогда
			Сообщить(ОписаниеОшибки());
		#КонецЕсли
		Возврат;
	КонецПопытки;
	
	
КонецПроцедуры

Функция ПолучитьЦеныАртикула(Знач ПрайсЛист, Знач ХешАртикулПроизводитель, Знач МассивАналогов, Знач ПараметрыПоиска) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.атВебПрайсЛисты.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ПрайсЛист = ПрайсЛист;
	МенеджерЗаписи.ИдентификаторЗапроса = ХешАртикулПроизводитель;
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() ИЛИ МенеджерЗаписи.КоличествоПредложений = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТЗПрайсЛиста = МенеджерЗаписи.Значение.Получить();
	
	// Валюта, в которой требуется вернуть цены
	Если ПараметрыПоиска.Свойство("Валюта") И ЗначениеЗаполнено(ПараметрыПоиска.Валюта) Тогда
		ВалютаДокумента = ПараметрыПоиска.Валюта;
	Иначе
		ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПодразделениеКомпании") И ЗначениеЗаполнено(ПараметрыПоиска.ПодразделениеКомпании) Тогда
		ПодразделениеКомпании = ПараметрыПоиска.ПодразделениеКомпании;
	Иначе
		ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ТипЦен") И ЗначениеЗаполнено(ПараметрыПоиска.ТипЦен) Тогда
		ТипЦен = ПараметрыПоиска.ТипЦен;
	Иначе
		ТипЦен = обПраво("ОсновнойТипЦенПродажи");
	КонецЕсли;
	
	СУчетомАналогов = Истина;
	Если ПараметрыПоиска.Свойство("СУчетомАналогов") Тогда
		СУчетомАналогов = ПараметрыПоиска.СУчетомАналогов;
	КонецЕсли;
	
	// С рабочим листом работаем только текущей датой!!
	ДатаЦен = ТекущаяДата();
	
	// Курс валюты, в которой требуется вернуть цены
	Если ПараметрыПоиска.Свойство("Курс") Тогда
		КурсДокумента = ПараметрыПоиска.Курс;
	Иначе
		СтруктураКурс = обКурсДляВалюты(ВалютаДокумента, ДатаЦен);
		КурсДокумента = СтруктураКурс.Курс / ?(СтруктураКурс.Кратность=0, 1, СтруктураКурс.Кратность);
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПодразделениеКомпании") Тогда
		ПодразделениеКомпании = ПараметрыПоиска.ПодразделениеКомпании;
	Иначе
		ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	КонецЕсли;
	
	ТаблицаСтарт = ПустаяТаблицаАналогов();
	
	Для Каждого Артикул Из МассивАналогов Цикл
		Если Не СУчетомАналогов И Артикул.Раздел > 1 Тогда
			Продолжить;
		КонецЕсли;
		СтрокаСтарт = ТаблицаСтарт.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтарт, Артикул);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛист);
	Запрос.УстановитьПараметр("ХешАртикулПроизводитель", ХешАртикулПроизводитель);
	Запрос.УстановитьПараметр("Подразделение", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ДатаЦен", ДатаЦен);
	Запрос.УстановитьПараметр("ТаблицаСтарт", ТаблицаСтарт);
	Запрос.УстановитьПараметр("ТЗПрайсЛиста", ТЗПрайсЛиста);
	Запрос.УстановитьПараметр("КурсДокумента", КурсДокумента);
	Запрос.УстановитьПараметр("ТипЦенЦенаВключаетНДС", ТипЦен.ЦенаВключаетНДС);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб.ХешАртикулПроизводитель,
	|	Таб.ХешАналога,
	|	Таб.Артикул,
	|	Таб.АртикулДляПоиска,
	|	Таб.Производитель,
	|	Таб.ПроизводительКод,
	|	Таб.Наименование,
	|	Таб.Раздел,
	|	Таб.ВидДополнения
	|ПОМЕСТИТЬ втТаблицаПолная
	|ИЗ
	|	&ТаблицаСтарт КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	Таб.ПрайсЛист,
	|	Таб.АртикулДляПоиска,
	|	Таб.ПроизводительКод,
	|	Таб.АртикулВПрайсЛисте,
	|	Таб.ПроизводительВПрайсЛисте,
	|	Таб.Наименование,
	|	Таб.Цена,
	|	Таб.Количество,
	|	Таб.КратностьПоставок,
	|	Таб.КлючСтрокиПоставщика,
	|	Таб.ТегПозиции,
	|	Таб.Валюта,
	|	Таб.СрокПоставкиМинимальный,
	|	Таб.СрокПоставкиМаксимальный,
	|	Таб.СрокПоставки,
	|	Таб.Артикул,
	|	Таб.Вес,
	|	Таб.Объем
	|ПОМЕСТИТЬ втТаб
	|ИЗ
	|	&ТЗПрайсЛиста КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	Таб.ПрайсЛист,
	|	Таб.ПрайсЛист.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	Таб.ПрайсЛист.СтавкаНДС КАК СтавкаНДС,
	|	Таб.АртикулДляПоиска,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(Словари.Значение КАК Справочник.Производители) ЕСТЬ NULL 
	|				ИЛИ (ВЫРАЗИТЬ(Словари.Значение КАК Справочник.Производители)) = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)
	|			ТОГДА Таб.ПроизводительВПрайсЛисте
	|		ИНАЧЕ ВЫРАЗИТЬ(Словари.Значение КАК Справочник.Производители)
	|	КОНЕЦ КАК Производитель,
	|	Таб.ПроизводительКод,
	|	Таб.АртикулВПрайсЛисте,
	|	Таб.ПроизводительВПрайсЛисте,
	|	Таб.Наименование,
	|	Таб.Цена,
	|	Таб.Количество,
	|	Таб.КратностьПоставок,
	|	Таб.КлючСтрокиПоставщика,
	|	Таб.ТегПозиции,
	|	Таб.Валюта,
	|	Таб.СрокПоставкиМинимальный,
	|	Таб.СрокПоставкиМаксимальный,
	|	Таб.СрокПоставки,
	|	Таб.Артикул,
	|	Таб.Вес,
	|	Таб.Объем
	|ПОМЕСТИТЬ втВременный
	|ИЗ
	|	втТаб КАК Таб
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.атСловари КАК Словари
	|		ПО (Словари.Тип = ""Производители"")
	|			И (Словари.Код = Таб.ПроизводительКод)
	|		"+?(НЕ СУчетомАналогов, "ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаПолная КАК ТаблицаПолная
	|		ПО (ТаблицаПолная.АртикулДляПоиска = Таб.АртикулДляПоиска)
	|			И (ТаблицаПолная.ПроизводительКод = Таб.ПроизводительКод)", "")+"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	ЕСТЬNULL(ТаблицаПолная.ХешАналога, """") КАК ХешАналога,
	|	Кеш.Артикул КАК Артикул,
	|	ЕСТЬNULL(ТаблицаПолная.АртикулДляПоиска, Кеш.АртикулДляПоиска) КАК АртикулДляПоиска,
	|	ЕСТЬNULL(ТаблицаПолная.Производитель, Кеш.Производитель) КАК Производитель,
	|	ЕСТЬNULL(ТаблицаПолная.ПроизводительКод, Кеш.ПроизводительКод) КАК ПроизводительКод,
	|	ЕСТЬNULL(ТаблицаПолная.Наименование, Кеш.Наименование) КАК Наименование,
	|	Кеш.АртикулВПрайсЛисте КАК АртикулВПрайсЛисте,
	|	Кеш.ПроизводительВПрайсЛисте КАК ПроизводительВПрайсЛисте,
	|	Кеш.Наименование КАК НаименованиеВПрайсЛисте,
	|	Кеш.ПрайсЛист КАК ПрайсЛист,
	|	Кеш.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	Кеш.СтавкаНДС КАК СтавкаНДС,
	|	Кеш.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
	|	Кеш.ТегПозиции КАК ТегПозиции,
	|	Кеш.Цена КАК Цена,
	|	Кеш.Валюта.Код КАК ВалютаКод,
	|	Кеш.Валюта КАК Валюта,
	|	Кеш.СрокПоставки КАК СрокПоставки,
	|	Кеш.СрокПоставкиМинимальный КАК СрокПоставкиМинимальный,
	|	Кеш.СрокПоставкиМаксимальный КАК СрокПоставкиМаксимальный,
	|	Кеш.Количество КАК Наличие,
	|	ЕСТЬNULL(ТаблицаПолная.Раздел, 9) КАК Раздел,
	|	ЕСТЬNULL(ТаблицаПолная.ВидДополнения, ""Замена от поставщика"") КАК ВидДополнения
	|ПОМЕСТИТЬ втПрайсЛисты
	|ИЗ
	|	втВременный КАК Кеш
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
	|		ПО Кеш.ПрайсЛист = ЗапрещенныеПодразделения.ПрайсЛист
	|			И (ЗапрещенныеПодразделения.ПодразделениеКомпании = &Подразделение
	|				ИЛИ ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаПолная КАК ТаблицаПолная
	|		ПО (ТаблицаПолная.АртикулДляПоиска = Кеш.АртикулДляПоиска)
	|			И (ТаблицаПолная.ПроизводительКод = Кеш.ПроизводительКод)
	|ГДЕ
	|	ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втТаблицаПолная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втТаб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втВременный";
	
	Результат = Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	ТекстЗапросаНаценки = "ВЫБОР";
	МассивПодразделений = Новый Массив;
	ТекущееПодразделение = ПодразделениеКомпании;
	ПеременнаяЦикла = 0;
	Пока Истина Цикл
		// Отбор подразделений
		МассивПодразделений.Добавить(ТекущееПодразделение);
		
		// Сортировка подразделений
		ТекстЗапросаНаценки = ТекстЗапросаНаценки + "
		|	КОГДА ЕСТЬNULL(Наценки.ПодразделениеКомпании, ЕСТЬNULL(НаценкиТегПозиции.ПодразделениеКомпании, ЕСТЬNULL(НаценкиПроизводитель.ПодразделениеКомпании, НаценкиБазовые.ПодразделениеКомпании))) = &Подразделение"+ПеременнаяЦикла+"
		|		ТОГДА "+ПеременнаяЦикла;
		
		Запрос.УстановитьПараметр("Подразделение"+ПеременнаяЦикла, ТекущееПодразделение);
		
		// Получаем родителя подразделения
		ТекущееПодразделение = ТекущееПодразделение.Родитель;
		Если Не ЗначениеЗаполнено(ТекущееПодразделение) Тогда
			// Добавим пустое подразделение
			ПеременнаяЦикла = ПеременнаяЦикла + 1;
			ТекущееПодразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
			МассивПодразделений.Добавить(ТекущееПодразделение);
			
			ТекстЗапросаНаценки = ТекстЗапросаНаценки + "
			|	КОГДА ЕСТЬNULL(Наценки.ПодразделениеКомпании, ЕСТЬNULL(НаценкиТегПозиции.ПодразделениеКомпании, ЕСТЬNULL(НаценкиПроизводитель.ПодразделениеКомпании, НаценкиБазовые.ПодразделениеКомпании))) = &Подразделение"+ПеременнаяЦикла+"
			|		ТОГДА "+ПеременнаяЦикла;
			
			Запрос.УстановитьПараметр("Подразделение"+ПеременнаяЦикла, ТекущееПодразделение);
			
			Прервать;
		КонецЕсли;
		
		ПеременнаяЦикла = ПеременнаяЦикла + 1;
	КонецЦикла;
	
	ТекстЗапросаНаценки = ТекстЗапросаНаценки + "
	|	ИНАЧЕ 999
	|КОНЕЦ";
	
	Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.ПрайсЛист,
	|	ВложенныйЗапрос.ТегПозиции,
	|	ВложенныйЗапрос.Производитель,
	|	ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0)))) КАК Скидка,
	|	ЕСТЬNULL(Наценки.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиТегПозиции.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиПроизводитель.АлгоритмРасчетаЦены, НаценкиБазовые.АлгоритмРасчетаЦены))) КАК АлгоритмРасчетаЦены,
	|	ЕСТЬNULL(Наценки.Наценка, ЕСТЬNULL(НаценкиТегПозиции.Наценка, ЕСТЬNULL(НаценкиПроизводитель.Наценка, НаценкиБазовые.Наценка))) КАК Наценка,
	|	ЕСТЬNULL(Наценки.ОкруглятьДо, ЕСТЬNULL(НаценкиТегПозиции.ОкруглятьДо, ЕСТЬNULL(НаценкиПроизводитель.ОкруглятьДо, НаценкиБазовые.ОкруглятьДо))) КАК ОкруглятьДо,
	|	99999 КАК ПрядокСортировки
	|ПОМЕСТИТЬ втСкидкиНаценки
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПрайсЛисты.ПрайсЛист КАК ПрайсЛист,
	|		ПрайсЛисты.ТегПозиции КАК ТегПозиции,
	|		ПрайсЛисты.Производитель КАК Производитель
	|	ИЗ
	|		втПрайсЛисты КАК ПрайсЛисты) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|				&ДатаЦен,
	|				ПрайсЛист = &ПрайсЛист
	|					И ТегПозиции = """"
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиБазовые
	|		ПО (НЕ СкидкиБазовые.Отменена)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(&ДатаЦен, ПрайсЛист = &ПрайсЛист) КАК Скидки
	|		ПО (НЕ Скидки.Отменена)
	|			И (Скидки.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|			И (Скидки.Производитель = ВложенныйЗапрос.Производитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|				&ДатаЦен,
	|				ПрайсЛист = &ПрайсЛист
	|					И ТегПозиции <> """"
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиТегПозиции
	|		ПО (НЕ СкидкиТегПозиции.Отменена)
	|			И (СкидкиТегПозиции.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|				&ДатаЦен,
	|				ПрайсЛист = &ПрайсЛист
	|					И ТегПозиции = """"
	|					И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиПроизводитель
	|		ПО (НЕ СкидкиПроизводитель.Отменена)
	|			И (СкидкиПроизводитель.Производитель = ВложенныйЗапрос.Производитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист = &ПрайсЛист
	|					И ТегПозиции = """"
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиБазовые
	|		ПО (НЕ НаценкиБазовые.Отменена)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист = &ПрайсЛист) КАК Наценки
	|		ПО (НЕ Наценки.Отменена)
	|			И (Наценки.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|			И (Наценки.Производитель = ВложенныйЗапрос.Производитель)
	|			И (Наценки.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист = &ПрайсЛист
	|					И ТегПозиции <> """"
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиТегПозиции
	|		ПО (НЕ НаценкиТегПозиции.Отменена)
	|			И (НаценкиТегПозиции.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|			И (НаценкиТегПозиции.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист = &ПрайсЛист
	|					И ТегПозиции = """"
	|					И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиПроизводитель
	|		ПО (НЕ НаценкиПроизводитель.Отменена)
	|			И (НаценкиПроизводитель.Производитель = ВложенныйЗапрос.Производитель)
	|			И (НаценкиПроизводитель.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании)
	|ГДЕ
	|	99999 < 999
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкидкиНаценки.ПрайсЛист,
	|	СкидкиНаценки.ТегПозиции,
	|	СкидкиНаценки.Производитель,
	|	СкидкиНаценки.Скидка КАК Скидка,
	|	СкидкиНаценки.АлгоритмРасчетаЦены КАК АлгоритмРасчетаЦены,
	|	СкидкиНаценки.Наценка КАК Наценка,
	|	ВЫБОР
	|		КОГДА СкидкиНаценки.ОкруглятьДо ЕСТЬ NULL 
	|			ТОГДА 0.01
	|		ИНАЧЕ СкидкиНаценки.ОкруглятьДо
	|	КОНЕЦ КАК ОкруглятьДо
	|ПОМЕСТИТЬ втСкидкиНаценкиИтог
	|ИЗ
	|	втСкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	(СкидкиНаценки.ПрайсЛист, СкидкиНаценки.ПрядокСортировки) В
	|			(ВЫБРАТЬ
	|				СкидкиНаценки.ПрайсЛист,
	|				МИНИМУМ(СкидкиНаценки.ПрядокСортировки) КАК ПрядокСортировки
	|			ИЗ
	|				втСкидкиНаценки КАК СкидкиНаценки
	|			СГРУППИРОВАТЬ ПО
	|				СкидкиНаценки.ПрайсЛист)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛисты.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	ПрайсЛисты.ХешАналога КАК ХешАналога,
	|	ПрайсЛисты.ПрайсЛист КАК Размещение,
	|	ПрайсЛисты.Артикул КАК Артикул,
	|	ПрайсЛисты.АртикулДляПоиска КАК АртикулДляПоиска,
	|	ПрайсЛисты.ПроизводительКод КАК ПроизводительКод,
	|	ПрайсЛисты.Производитель КАК Производитель,
	|	ПрайсЛисты.АртикулВПрайсЛисте КАК АртикулВПрайсЛисте,
	|	ПрайсЛисты.ПроизводительВПрайсЛисте КАК ПроизводительВПрайсЛисте,
	|	ПрайсЛисты.Наименование КАК Наименование,
	|	ПрайсЛисты.НаименованиеВПрайсЛисте КАК НаименованиеВПрайсЛисте,
	|	ПрайсЛисты.СрокПоставки КАК СрокПоставки,
	|	ПрайсЛисты.СрокПоставкиМинимальный КАК СрокПоставкиМинимальный,
	|	ПрайсЛисты.СрокПоставкиМаксимальный КАК СрокПоставкиМаксимальный,
	|	ПрайсЛисты.Наличие КАК Наличие,
	|	0 КАК Количество,
	|	ПрайсЛисты.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ПрайсЛисты.СтавкаНДС КАК СтавкаНДС,
	|	ПрайсЛисты.Цена КАК ЦенаВПрайсЛисте,
	|	ВЫБОР КОГДА ПрайсЛисты.ЦенаВключаетНДС = &ТипЦенЦенаВключаетНДС ТОГДА 1 
	|		КОГДА ПрайсЛисты.ЦенаВключаетНДС ТОГДА 100 / (100 + ЕСТЬNULL(ПрайсЛисты.СтавкаНДС.Ставка, 0)) 
	|		ИНАЧЕ (100 + ЕСТЬNULL(ПрайсЛисты.СтавкаНДС.Ставка, 0)) / 100 КОНЕЦ * ПрайсЛисты.Цена * (1 - СкидкиНаценкиИтог.Скидка / 100) КАК ЦенаПокупки,
	|	ПрайсЛисты.ВалютаКод КАК ВалютаКод,
	|	ПрайсЛисты.Валюта КАК Валюта,
	|	СкидкиНаценкиИтог.Наценка КАК Наценка,
	|	СкидкиНаценкиИтог.АлгоритмРасчетаЦены КАК АлгоритмРасчетаЦены,
	|	СкидкиНаценкиИтог.ОкруглятьДо КАК ОкруглятьДо,
	|	(ЕСТЬNULL(КурсыВалют.Курс, 1) / ЕСТЬNULL(КурсыВалют.Кратность, 1)) / &КурсДокумента КАК КоэффициентПересчетаКурса,
	|	ПрайсЛисты.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
	|	ПрайсЛисты.ТегПозиции КАК ТегПозиции,
	|	ПрайсЛисты.Раздел КАК Раздел,
	|	ПрайсЛисты.ВидДополнения КАК ВидДополнения,
	|	3 КАК ВидПредложения,
	|	ИСТИНА КАК ЕстьВПодразделении
	|ИЗ
	|	втПрайсЛисты КАК ПрайсЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСкидкиНаценкиИтог КАК СкидкиНаценкиИтог
	|		ПО ПрайсЛисты.ПрайсЛист = СкидкиНаценкиИтог.ПрайсЛист
	|			И ПрайсЛисты.ТегПозиции = СкидкиНаценкиИтог.ТегПозиции
	|			И ПрайсЛисты.Производитель = СкидкиНаценкиИтог.Производитель
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаЦен, ) КАК КурсыВалют
	|		ПО ПрайсЛисты.Валюта = КурсыВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втПрайсЛисты";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "99999", ТекстЗапросаНаценки);
	
	ТаблицаПредложений = Запрос.Выполнить().Выгрузить();
	ТаблицаПредложений.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	//Создадим Кеш для хранения установленных цен
	КешЦенНоменклатуры = Новый ТаблицаЗначений;
	КешЦенНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КешЦенНоменклатуры.Колонки.Добавить("АртикулДляПоиска", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(25)));
	КешЦенНоменклатуры.Колонки.Добавить("Производитель", Новый ОписаниеТипов("СправочникСсылка.Производители"));
	КешЦенНоменклатуры.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	Для Каждого СтрокаПредложения Из ТаблицаПредложений Цикл
		Если СтрокаПредложения.Наценка = Null Тогда
			СтрокаПредложения.Цена = 0;
		Иначе
			ЦенаВПрайсЛисте = СтрокаПредложения.ЦенаВПрайсЛисте;
			ЦенаПокупки = СтрокаПредложения.ЦенаПокупки;
			Цена = СтрокаПредложения.ЦенаПокупки;
			
			Если ЗначениеЗаполнено(СтрокаПредложения.АлгоритмРасчетаЦены) Тогда
				ЦенаНоменклатуры = 0;
				Если Найти(СтрокаПредложения.АлгоритмРасчетаЦены, "ЦенаНоменклатуры") > 0 Тогда
					Если ЗначениеЗаполнено(СтрокаПредложения.АртикулДляПоиска) Тогда
						СтрокиЦен = КешЦенНоменклатуры.НайтиСтроки(Новый Структура("АртикулДляПоиска, Производитель", СтрокаПредложения.АртикулДляПоиска, СтрокаПредложения.Производитель));
						Если СтрокиЦен.Количество() = 0 Тогда
							Номенклатура = Справочники.Номенклатура.НайтиНоменклатуру(СтрокаПредложения.АртикулДляПоиска, СтрокаПредложения.Производитель);
							Если ЗначениеЗаполнено(Номенклатура) Тогда
								СтрокаЦены = КешЦенНоменклатуры.Найти(Номенклатура, "Номенклатура");
								Если СтрокаЦены = Неопределено Тогда
									СтрокаЦены = КешЦенНоменклатуры.Добавить();
									СтрокаЦены.Номенклатура = Номенклатура;
									СтрокаЦены.АртикулДляПоиска = СтрокаПредложения.АртикулДляПоиска;
									СтрокаЦены.Производитель = СтрокаПредложения.Производитель;
									СтрокаЦены.Цена = обПолучитьЦену(ТипЦен, Номенклатура, ДатаЦен,, ВалютаДокумента, КурсДокумента,,, ПодразделениеКомпании);
								КонецЕсли;
							Иначе
								СтрокаЦены = КешЦенНоменклатуры.Добавить();
								СтрокаЦены.АртикулДляПоиска = СтрокаПредложения.АртикулДляПоиска;
								СтрокаЦены.Производитель = СтрокаПредложения.Производитель;
								СтрокаЦены.Цена = 0;
							КонецЕсли;
						Иначе
							СтрокаЦены = СтрокиЦен[0];
						КонецЕсли;
						ЦенаНоменклатуры = СтрокаЦены.Цена;
					КонецЕсли;
				КонецЕсли;
				
				Если Найти(СтрокаПредложения.АлгоритмРасчетаЦены, "#Процедура") > 0 Тогда
					Попытка
						Выполнить(СтрЗаменить(СтрокаПредложения.АлгоритмРасчетаЦены, "#Процедура", ""));
					Исключение
						Цена = 0;
					КонецПопытки;
				Иначе
					Попытка
						Выполнить("Цена = Макс(0, " + СтрокаПредложения.АлгоритмРасчетаЦены + ")");
					Исключение
						Цена = 0;
					КонецПопытки;
				КонецЕсли;
			Иначе
				// Простая наценка
				Цена = МАКС(0, Цена + Цена * СтрокаПредложения.Наценка / 100);
			КонецЕсли;
			Цена = Цена * СтрокаПредложения.КоэффициентПересчетаКурса;
			//округляем
			ДельтаОкругления = Цена / СтрокаПредложения.ОкруглятьДо;
			ДельтаОкругленияЦел = Цел(ДельтаОкругления);
			Если ДельтаОкругления <> ДельтаОкругленияЦел Тогда
				Цена = (ДельтаОкругленияЦел+1) * СтрокаПредложения.ОкруглятьДо;
			КонецЕсли; 
			СтрокаПредложения.Цена = Цена;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаПредложений;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
////////////////////////ПОЛУЧЕНИЕ ЦЕН////////////////////////////////////////////////////

Функция ТаблицаЗапросы() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	// Простые типы
	Таблица.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	Таблица.Колонки.Добавить("ДатаОбращения", Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	Таблица.Колонки.Добавить("СервисКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Таблица.Колонки.Добавить("Сервис", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	Таблица.Колонки.Добавить("КаталогКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Таблица.Колонки.Добавить("Каталог", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	Таблица.Колонки.Добавить("МаркаКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Таблица.Колонки.Добавить("Марка", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	Таблица.Колонки.Добавить("МодельКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Таблица.Колонки.Добавить("Модель", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	Таблица.Колонки.Добавить("МодификацияКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Таблица.Колонки.Добавить("Модификация", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	Таблица.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	Таблица.Колонки.Добавить("Параметры", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	
	Возврат Таблица;
	
КонецФункции

Функция ТаблицаНомераДеталей() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	// Составные типы
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
	МассивТипов.Добавить(Тип("Строка"));
	Таблица.Колонки.Добавить("Автор", Новый ОписаниеТипов(МассивТипов,, Новый КвалификаторыСтроки(100)));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Производители"));
	МассивТипов.Добавить(Тип("Строка"));
	Таблица.Колонки.Добавить("Производитель", Новый ОписаниеТипов(МассивТипов,, Новый КвалификаторыСтроки(100)));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
	МассивТипов.Добавить(Тип("Строка"));
	Таблица.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов(МассивТипов,, Новый КвалификаторыСтроки(100)));
	
	// Простые типы
	Таблица.Колонки.Добавить("ИдентификаторЗапроса", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	Таблица.Колонки.Добавить("ИмяНабора", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(100)));
	Таблица.Колонки.Добавить("Артикул", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(30)));
	Таблица.Колонки.Добавить("АртикулДляПоиска", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(30)));
	Таблица.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(100)));
	Таблица.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный)));
	Таблица.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	Таблица.Колонки.Добавить("ДатаЗаписи", Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	Таблица.Колонки.Добавить("Параметры", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)));
	
	Таблица.Колонки.Добавить("ПроизводительКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Таблица.Колонки.Добавить("НоменклатураКод", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	
	Возврат Таблица;
	
КонецФункции

Функция СтруктураСтрокиЗапроса() Экспорт
	Возврат Новый Структура("Идентификатор,
							|ДатаОбращения,
							|Описание,
							|Комментарий,
							|СервисКод,
							|СервисНаименование,
							|КаталогКод,
							|КаталогНаименование,
							|МодельКод,
							|МодельНаименование,
							|МодификацияКод,
							|МодификацияНаименование");
КонецФункции

Функция СтруктураСтрокиНомераДетали() Экспорт
	Возврат Новый Структура("ДатаЗаписи,
							|Идентификатор,
							|ИдентификаторЗапроса,
							|ХешАртикулПроизводитель,
							|Артикул,
							|АртикулДляПоиска,
							|ПроизводительКод,
							|Производитель,
							|НоменклатураКод,
							|Номенклатура,
							|Количество,
							|ИмяНабора,
							|Параметры,
							|Комментарий");
КонецФункции

Функция СтруктураСтрокиЦен()
	СтрокаЦен = Новый Структура;
	СтрокаЦен.Вставить("ДанныеВоВнешнемИсточнике", Ложь);
	СтрокаЦен.Вставить("ХешАртикулПроизводитель", "");
	СтрокаЦен.Вставить("Размещение", Неопределено);
	СтрокаЦен.Вставить("Артикул", "");
	СтрокаЦен.Вставить("АртикулДляПоиска", "");
	СтрокаЦен.Вставить("ПроизводительКод", "");
	СтрокаЦен.Вставить("Производитель", Неопределено);
	СтрокаЦен.Вставить("Номенклатура", Неопределено);
	СтрокаЦен.Вставить("ЦенаВПрайсЛисте", 0);
	СтрокаЦен.Вставить("ЦенаПокупки", 0);
	СтрокаЦен.Вставить("ВалютаКод", "");
	СтрокаЦен.Вставить("Валюта", Неопределено);
	СтрокаЦен.Вставить("ЦенаВключаетНДС", Истина);
	СтрокаЦен.Вставить("СтавкаНДС", Неопределено);
	СтрокаЦен.Вставить("Цена", 0);
	СтрокаЦен.Вставить("СрокПоставки", "");
	СтрокаЦен.Вставить("СрокПоставкиМинимальный", 0);
	СтрокаЦен.Вставить("СрокПоставкиМаксимальный", 0);
	СтрокаЦен.Вставить("КлючСтрокиПоставщика", "");
	СтрокаЦен.Вставить("Раздел", 0);
	СтрокаЦен.Вставить("ВидДополнения", "");
	СтрокаЦен.Вставить("Порядок", 0);
	Возврат СтрокаЦен;
КонецФункции

Функция ПолучитьЦеныАртикуловПрайсЛисты(Знач МассивАртикулов, Знач ПараметрыПоиска) Экспорт
	
	// Валюта, в которой требуется вернуть цены
	Если ПараметрыПоиска.Свойство("Валюта") И ЗначениеЗаполнено(ПараметрыПоиска.Валюта) Тогда
		ВалютаДокумента = ПараметрыПоиска.Валюта;
	Иначе
		ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПрайсЛист") Тогда
		ПрайсЛистПоставщика = ПараметрыПоиска.ПрайсЛист;
	Иначе
		ПрайсЛистПоставщика = Неопределено;
	КонецЕсли;
	
	Поставщик = Неопределено;
	Если Не ЗначениеЗаполнено(ПрайсЛистПоставщика) И ПараметрыПоиска.Свойство("Поставщик") Тогда
		Поставщик = ПараметрыПоиска.Поставщик;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПодразделениеКомпании") И ЗначениеЗаполнено(ПараметрыПоиска.ПодразделениеКомпании) Тогда
		ПодразделениеКомпании = ПараметрыПоиска.ПодразделениеКомпании;
	Иначе
		ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ТипЦен") И ЗначениеЗаполнено(ПараметрыПоиска.ТипЦен) Тогда
		ТипЦен = ПараметрыПоиска.ТипЦен;
	Иначе
		ТипЦен = обПраво("ОсновнойТипЦенПродажи");
	КонецЕсли;
	
	// С рабочим листом работаем только текущей датой!!
	ДатаЦен = ТекущаяДата();
	Если ПараметрыПоиска.Свойство("ДатаЦен") Тогда
		ДатаЦен = ПараметрыПоиска.ДатаЦен;
	КонецЕсли;
	
	// Курс валюты, в которой требуется вернуть цены
	Если ПараметрыПоиска.Свойство("Курс") Тогда
		КурсДокумента = ПараметрыПоиска.Курс;
	Иначе
		СтруктураКурс = обКурсДляВалюты(ВалютаДокумента, ДатаЦен);
		КурсДокумента = СтруктураКурс.Курс / ?(СтруктураКурс.Кратность=0, 1, СтруктураКурс.Кратность);
	КонецЕсли;
	
	ТаблицаСтарт = ПустаяТаблицаАналогов();
	ТаблицаСтарт.Колонки.Удалить("Производитель");
	ТаблицаСтарт.Колонки.Добавить("Производитель", Новый ОписаниеТипов("СправочникСсылка.Производители"));
	
	Для Каждого Артикул Из МассивАртикулов Цикл
		Если Не (ЗначениеЗаполнено(Артикул.Номенклатура) ИЛИ (ЗначениеЗаполнено(Артикул.АртикулДляПоиска) И ТипЗнч(Артикул.Производитель) = Тип("СправочникСсылка.Производители"))) Тогда
			// Нет номенклатуры и не заполнен Артикул для поиска или Производителя нет в нашей базе
			Продолжить;
		КонецЕсли;
		СтрокаСтарт = ТаблицаСтарт.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтарт, Артикул);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Подразделение", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ДатаЦен", ДатаЦен);
	Запрос.УстановитьПараметр("Таб", ТаблицаСтарт);
	Запрос.УстановитьПараметр("КурсДокумента", КурсДокумента);
	Запрос.УстановитьПараметр("ТипЦенЦенаВключаетНДС", ТипЦен.ЦенаВключаетНДС);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.Ссылка КАК ПрайсЛист
	|ПОМЕСТИТЬ втПодходящиеПрайсЛисты
	|ИЗ
	|	Справочник.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|ГДЕ
	|	(ПрайсЛистыКонтрагентов.ФайлИсточникДанных
	|			ИЛИ ПрайсЛистыКонтрагентов.ХранитьДанныеЛокально)
	|	И ПрайсЛистыКонтрагентов.ВидПрайсЛиста <> ЗНАЧЕНИЕ(Перечисление.ВидыПрайсЛистов.ВебПрайсЛист)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	Таб.ХешАналога КАК ХешАналога,
	|	Таб.Артикул КАК Артикул,
	|	Таб.АртикулДляПоиска КАК АртикулДляПоиска,
	|	Таб.Производитель КАК Производитель,
	|	Таб.Номенклатура КАК Номенклатура,
	|	Таб.Наименование КАК Наименование,
	|	Таб.Раздел КАК Раздел,
	|	Таб.ВидДополнения КАК ВидДополнения,
	|	Таб.ПроизводительКод КАК ПроизводительКод
	|ПОМЕСТИТЬ втТаблицаПолная
	|ИЗ
	|	&Таб КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаПолная.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	втТаблицаПолная.ХешАналога КАК ХешАналога,
	|	втТаблицаПолная.Артикул КАК Артикул,
	|	втТаблицаПолная.АртикулДляПоиска КАК АртикулДляПоиска,
	|	втТаблицаПолная.Производитель КАК Производитель,
	|	втТаблицаПолная.Номенклатура КАК Номенклатура,
	|	втТаблицаПолная.Наименование КАК Наименование,
	|	втТаблицаПолная.Раздел КАК Раздел,
	|	втТаблицаПолная.ВидДополнения КАК ВидДополнения,
	|	втТаблицаПолная.ПроизводительКод КАК ПроизводительКод,
	|	втПодходящиеПрайсЛисты.ПрайсЛист КАК ПрайсЛист
	|ПОМЕСТИТЬ втТаблицаПолнаяСПрайсЛистами
	|ИЗ
	|	втТаблицаПолная КАК втТаблицаПолная,
	|	втПодходящиеПрайсЛисты КАК втПодходящиеПрайсЛисты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.ПрайсЛист КАК ПрайсЛист,
	|	ПрайсЛистыКонтрагентов.АртикулДляПоиска КАК АртикулДляПоиска,
	|	ПрайсЛистыКонтрагентов.Производитель КАК Производитель,
	|	ПрайсЛистыКонтрагентов.ДатаЗаписи КАК ДатаЗаписи,
	|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
	|	ПрайсЛистыКонтрагентов.ТегПозиции КАК ТегПозиции,
	|	втТаблицаПолная.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	втТаблицаПолная.ХешАналога КАК ХешАналога,
	|	втТаблицаПолная.Артикул КАК Артикул,
	|	втТаблицаПолная.Номенклатура КАК Номенклатура,
	|	втТаблицаПолная.Наименование КАК Наименование,
	|	втТаблицаПолная.Раздел КАК Раздел,
	|	втТаблицаПолная.ВидДополнения КАК ВидДополнения,
	|	втТаблицаПолная.ПроизводительКод КАК ПроизводительКод
	|ПОМЕСТИТЬ втПодходящиеЗаписиИзПрайсЛистовКонтрагентов
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаПолная КАК втТаблицаПолная
	|		ПО ПрайсЛистыКонтрагентов.АртикулДляПоиска = втТаблицаПолная.АртикулДляПоиска
	|			И ПрайсЛистыКонтрагентов.Производитель = втТаблицаПолная.Производитель
	|ГДЕ
	|	(ПрайсЛистыКонтрагентов.ПрайсЛист, ПрайсЛистыКонтрагентов.АртикулДляПоиска, ПрайсЛистыКонтрагентов.Производитель) В
	|			(ВЫБРАТЬ
	|				втТаблицаПолнаяСПрайсЛистами.ПрайсЛист КАК ПрайсЛист,
	|				втТаблицаПолнаяСПрайсЛистами.АртикулДляПоиска КАК АртикулДляПоиска,
	|				втТаблицаПолнаяСПрайсЛистами.Производитель КАК Производитель
	|			ИЗ
	|				втТаблицаПолнаяСПрайсЛистами КАК втТаблицаПолнаяСПрайсЛистами)
	|	И НЕ ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика = """"
	|	И 77777 = 77777
	|	И 88888 = 88888
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ХешАналога КАК ХешАналога,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.Артикул КАК Артикул,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.АртикулДляПоиска КАК АртикулДляПоиска,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.Производитель КАК Производитель,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.Наименование КАК Наименование,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.Номенклатура КАК Номенклатура,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ПрайсЛист КАК ПрайсЛист,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ПрайсЛист.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ПрайсЛист.СтавкаНДС КАК СтавкаНДС,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ТегПозиции КАК ТегПозиции,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ДатаЗаписи КАК ДатаЗаписи,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.Раздел КАК Раздел,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ВидДополнения КАК ВидДополнения,
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ПроизводительКод КАК ПроизводительКод
	|ПОМЕСТИТЬ втПрайсЛисты
	|ИЗ
	|	втПодходящиеЗаписиИзПрайсЛистовКонтрагентов КАК втПодходящиеЗаписиИзПрайсЛистовКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
	|		ПО втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ПрайсЛист = ЗапрещенныеПодразделения.ПрайсЛист
	|			И (ЗапрещенныеПодразделения.ПодразделениеКомпании = &Подразделение
	|				ИЛИ ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи КАК НедоступныеЗаписи
	|		ПО втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ПрайсЛист = НедоступныеЗаписи.ПрайсЛист
	|			И втПодходящиеЗаписиИзПрайсЛистовКонтрагентов.ДатаЗаписи = НедоступныеЗаписи.ДатаЗаписи
	|ГДЕ
	|	ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL
	|	И НедоступныеЗаписи.ПрайсЛист ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТаблицаПолная.ХешАртикулПроизводитель,
	|	ТаблицаПолная.ХешАналога,
	|	ТаблицаПолная.Артикул,
	|	ТаблицаПолная.АртикулДляПоиска,
	|	ТаблицаПолная.Производитель,
	|	ТаблицаПолная.Наименование,
	|	ТаблицаПолная.Номенклатура,
	|	ПрайсЛистыКонтрагентов.ПрайсЛист,
	|	ПрайсЛистыКонтрагентов.ПрайсЛист.ЦенаВключаетНДС,
	|	ПрайсЛистыКонтрагентов.ПрайсЛист.СтавкаНДС,
	|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика,
	|	ПрайсЛистыКонтрагентов.ТегПозиции,
	|	ПрайсЛистыКонтрагентов.ДатаЗаписи,
	|	ТаблицаПолная.Раздел,
	|	ТаблицаПолная.ВидДополнения,
	|	ТаблицаПолная.ПроизводительКод
	|ИЗ
	|	втТаблицаПолная КАК ТаблицаПолная
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
	|		ПО (ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки))
	|			И (ПравилаЗагрузки.ВидПравила = ЗНАЧЕНИЕ(Перечисление.ВидыПравилЗагрузки.ПрисвоитьЗначение))
	|			И (ПравилаЗагрузки.ИмяРеквизитаПрайсЛиста = ""Номенклатура"")
	|			И ((ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура)) = ТаблицаПолная.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
	|		ПО (ПравилаЗагрузки.ПрайсЛист = ЗапрещенныеПодразделения.ПрайсЛист)
	|			И (ЗапрещенныеПодразделения.ПодразделениеКомпании = &Подразделение
	|				ИЛИ ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|		ПО (ПравилаЗагрузки.ПрайсЛист = ПрайсЛистыКонтрагентов.ПрайсЛист)
	|			И ((ВЫРАЗИТЬ(ПравилаЗагрузки.ОбъектПравила КАК СТРОКА(32))) = ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи КАК НедоступныеЗаписи
	|		ПО (ПрайсЛистыКонтрагентов.ПрайсЛист = НедоступныеЗаписи.ПрайсЛист)
	|			И (ПрайсЛистыКонтрагентов.ДатаЗаписи = НедоступныеЗаписи.ДатаЗаписи)
	|ГДЕ
	|	ТаблицаПолная.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL
	|	И НедоступныеЗаписи.ПрайсЛист ЕСТЬ NULL
	|	И 77777 = 77777
	|	И 88888 = 88888";
	
	
	Если ЗначениеЗаполнено(Поставщик) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "77777 = 77777", "ПрайсЛистыКонтрагентов.ПрайсЛист.Владелец = &Контрагент");
		Запрос.УстановитьПараметр("Контрагент", Поставщик);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПрайсЛистПоставщика) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "88888 = 88888", "ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист");
		Запрос.УстановитьПараметр("ПрайсЛист", ПрайсЛистПоставщика);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	ТекстЗапросаНаценки = "ВЫБОР";
	МассивПодразделений = Новый Массив;
	ТекущееПодразделение = ПодразделениеКомпании;
	ПеременнаяЦикла = 0;
	Пока Истина Цикл
		// Отбор подразделений
		МассивПодразделений.Добавить(ТекущееПодразделение);
		
		// Сортировка подразделений
		ТекстЗапросаНаценки = ТекстЗапросаНаценки + "
		|	КОГДА ЕСТЬNULL(Наценки.ПодразделениеКомпании, ЕСТЬNULL(НаценкиТегПозиции.ПодразделениеКомпании, ЕСТЬNULL(НаценкиПроизводитель.ПодразделениеКомпании, НаценкиБазовые.ПодразделениеКомпании))) = &Подразделение"+ПеременнаяЦикла+"
		|		ТОГДА "+ПеременнаяЦикла;
		
		Запрос.УстановитьПараметр("Подразделение"+ПеременнаяЦикла, ТекущееПодразделение);
		
		// Получаем родителя подразделения
		ТекущееПодразделение = ТекущееПодразделение.Родитель;
		Если Не ЗначениеЗаполнено(ТекущееПодразделение) Тогда
			// Добавим пустое подразделение
			ПеременнаяЦикла = ПеременнаяЦикла + 1;
			ТекущееПодразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
			МассивПодразделений.Добавить(ТекущееПодразделение);
			
			ТекстЗапросаНаценки = ТекстЗапросаНаценки + "
			|	КОГДА ЕСТЬNULL(Наценки.ПодразделениеКомпании, ЕСТЬNULL(НаценкиТегПозиции.ПодразделениеКомпании, ЕСТЬNULL(НаценкиПроизводитель.ПодразделениеКомпании, НаценкиБазовые.ПодразделениеКомпании))) = &Подразделение"+ПеременнаяЦикла+"
			|		ТОГДА "+ПеременнаяЦикла;
			
			Запрос.УстановитьПараметр("Подразделение"+ПеременнаяЦикла, ТекущееПодразделение);
			
			Прервать;
		КонецЕсли;
		
		ПеременнаяЦикла = ПеременнаяЦикла + 1;
	КонецЦикла;
	
	ТекстЗапросаНаценки = ТекстЗапросаНаценки + "
	|	ИНАЧЕ 999
	|КОНЕЦ";
	
	Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.ПрайсЛист,
	|	ВложенныйЗапрос.ТегПозиции,
	|	ВложенныйЗапрос.Производитель,
	|	ЕСТЬNULL(Скидки.Скидка, ЕСТЬNULL(СкидкиТегПозиции.Скидка, ЕСТЬNULL(СкидкиПроизводитель.Скидка, ЕСТЬNULL(СкидкиБазовые.Скидка, 0)))) КАК Скидка,
	|	ЕСТЬNULL(Наценки.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиТегПозиции.АлгоритмРасчетаЦены, ЕСТЬNULL(НаценкиПроизводитель.АлгоритмРасчетаЦены, НаценкиБазовые.АлгоритмРасчетаЦены))) КАК АлгоритмРасчетаЦены,
	|	ЕСТЬNULL(Наценки.Наценка, ЕСТЬNULL(НаценкиТегПозиции.Наценка, ЕСТЬNULL(НаценкиПроизводитель.Наценка, НаценкиБазовые.Наценка))) КАК Наценка,
	|	ЕСТЬNULL(Наценки.ОкруглятьДо, ЕСТЬNULL(НаценкиТегПозиции.ОкруглятьДо, ЕСТЬNULL(НаценкиПроизводитель.ОкруглятьДо, НаценкиБазовые.ОкруглятьДо))) КАК ОкруглятьДо,
	|	99999 КАК ПрядокСортировки
	|ПОМЕСТИТЬ втСкидкиНаценки
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПрайсЛисты.ПрайсЛист КАК ПрайсЛист,
	|		ПрайсЛисты.ТегПозиции КАК ТегПозиции,
	|		ПрайсЛисты.Производитель КАК Производитель
	|	ИЗ
	|		втПрайсЛисты КАК ПрайсЛисты) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|				&ДатаЦен,
	|				ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПрайсЛисты.ПрайсЛист
	|						ИЗ
	|							втПрайсЛисты КАК ПрайсЛисты)
	|					И ТегПозиции = """"
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиБазовые
	|		ПО (НЕ СкидкиБазовые.Отменена)
	|			И (СкидкиБазовые.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|				&ДатаЦен,
	|				ПрайсЛист В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ПрайсЛисты.ПрайсЛист
	|					ИЗ
	|						втПрайсЛисты КАК ПрайсЛисты)) КАК Скидки
	|		ПО (НЕ Скидки.Отменена)
	|			И (Скидки.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|			И (Скидки.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|			И (Скидки.Производитель = ВложенныйЗапрос.Производитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|				&ДатаЦен,
	|				ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПрайсЛисты.ПрайсЛист
	|						ИЗ
	|							втПрайсЛисты КАК ПрайсЛисты)
	|					И ТегПозиции <> """"
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиТегПозиции
	|		ПО (НЕ СкидкиТегПозиции.Отменена)
	|			И (СкидкиТегПозиции.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|			И (СкидкиТегПозиции.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|				&ДатаЦен,
	|				ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПрайсЛисты.ПрайсЛист
	|						ИЗ
	|							втПрайсЛисты КАК ПрайсЛисты)
	|					И ТегПозиции = """"
	|					И Производитель <> ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СкидкиПроизводитель
	|		ПО (НЕ СкидкиПроизводитель.Отменена)
	|			И (СкидкиПроизводитель.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|			И (СкидкиПроизводитель.Производитель = ВложенныйЗапрос.Производитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПрайсЛисты.ПрайсЛист
	|						ИЗ
	|							втПрайсЛисты КАК ПрайсЛисты)
	|					И ТегПозиции = """"
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиБазовые
	|		ПО (НЕ НаценкиБазовые.Отменена)
	|			И (НаценкиБазовые.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПрайсЛисты.ПрайсЛист
	|						ИЗ
	|							втПрайсЛисты КАК ПрайсЛисты)) КАК Наценки
	|		ПО (НЕ Наценки.Отменена)
	|			И (Наценки.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|			И (Наценки.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|			И (Наценки.Производитель = ВложенныйЗапрос.Производитель)
	|			И (Наценки.ТипЦен = НаценкиБазовые.ТипЦен)
	|			И (Наценки.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПрайсЛисты.ПрайсЛист
	|						ИЗ
	|							втПрайсЛисты КАК ПрайсЛисты)
	|					И Производитель = ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК НаценкиТегПозиции
	|		ПО (НЕ НаценкиТегПозиции.Отменена)
	|			И (НаценкиТегПозиции.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|			И (НаценкиТегПозиции.ТегПозиции = ВложенныйЗапрос.ТегПозиции)
	|			И (НаценкиТегПозиции.ТипЦен = НаценкиБазовые.ТипЦен)
	|			И (НаценкиТегПозиции.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНаценки.СрезПоследних(
	|				&ДатаЦен,
	|				ПодразделениеКомпании В (&МассивПодразделений)
	|					И ТипЦен = &ТипЦен
	|					И ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ПрайсЛисты.ПрайсЛист
	|						ИЗ
	|							втПрайсЛисты КАК ПрайсЛисты)
	|					И ТегПозиции = """") КАК НаценкиПроизводитель
	|		ПО (НЕ НаценкиПроизводитель.Отменена)
	|			И (НаценкиПроизводитель.ПрайсЛист = ВложенныйЗапрос.ПрайсЛист)
	|			И (НаценкиПроизводитель.Производитель = ВложенныйЗапрос.Производитель)
	|			И (НаценкиПроизводитель.ТипЦен = НаценкиБазовые.ТипЦен)
	|			И (НаценкиПроизводитель.ПодразделениеКомпании = НаценкиБазовые.ПодразделениеКомпании)
	|ГДЕ
	|	99999 < 999
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВложенныйЗапрос.ПрайсЛист
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкидкиНаценки.ПрайсЛист,
	|	СкидкиНаценки.ТегПозиции,
	|	СкидкиНаценки.Производитель,
	|	ЕСТЬNULL(СкидкиНаценки.Скидка, 0) КАК Скидка,
	|	СкидкиНаценки.АлгоритмРасчетаЦены КАК АлгоритмРасчетаЦены,
	|	СкидкиНаценки.Наценка КАК Наценка,
	|	ВЫБОР
	|		КОГДА СкидкиНаценки.ОкруглятьДо ЕСТЬ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ СкидкиНаценки.ОкруглятьДо
	|	КОНЕЦ КАК ОкруглятьДо
	|ПОМЕСТИТЬ втСкидкиНаценкиИтог
	|ИЗ
	|	втСкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	(СкидкиНаценки.ПрайсЛист, СкидкиНаценки.ПрядокСортировки) В
	|			(ВЫБРАТЬ
	|				СкидкиНаценки.ПрайсЛист,
	|				МИНИМУМ(СкидкиНаценки.ПрядокСортировки) КАК ПрядокСортировки
	|			ИЗ
	|				втСкидкиНаценки КАК СкидкиНаценки
	|			СГРУППИРОВАТЬ ПО
	|				СкидкиНаценки.ПрайсЛист)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛисты.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	ПрайсЛисты.ХешАналога КАК ХешАналога,
	|	ПрайсЛисты.ПрайсЛист КАК Размещение,
	|	ПрайсЛисты.Артикул КАК Артикул,
	|	ПрайсЛисты.АртикулДляПоиска КАК АртикулДляПоиска,
	|	ПрайсЛисты.Производитель КАК Производитель,
	|	ПрайсЛисты.ПроизводительКод КАК ПроизводительКод,
	|	ПрайсЛисты.Наименование КАК Наименование,
	|	ПрайсЛисты.Номенклатура КАК Номенклатура,
	|	ПрайсЛистыКонтрагентов.Артикул КАК АртикулВПрайсЛисте,
	|	ПрайсЛистыКонтрагентов.ПроизводительВПрайсЛисте КАК ПроизводительВПрайсЛисте,
	|	ПрайсЛистыКонтрагентов.Наименование КАК НаименованиеВПрайсЛисте,
	|	ПрайсЛистыКонтрагентов.СрокПоставки КАК СрокПоставки,
	|	ПрайсЛистыКонтрагентов.СрокПоставкиМинимальный КАК СрокПоставкиМинимальный,
	|	ПрайсЛистыКонтрагентов.СрокПоставкиМаксимальный КАК СрокПоставкиМаксимальный,
	|	ПрайсЛистыКонтрагентов.Количество КАК Наличие,
	|	0 КАК Количество,
	|	ПрайсЛисты.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ПрайсЛисты.СтавкаНДС КАК СтавкаНДС,
	|	ПрайсЛистыКонтрагентов.Цена КАК ЦенаВПрайсЛисте,
	|	ВЫБОР КОГДА ПрайсЛисты.ЦенаВключаетНДС = &ТипЦенЦенаВключаетНДС ТОГДА 1 
	|		КОГДА ПрайсЛисты.ЦенаВключаетНДС ТОГДА 100 / (100 + ЕСТЬNULL(ПрайсЛисты.СтавкаНДС.Ставка, 0)) 
	|		ИНАЧЕ (100 + ЕСТЬNULL(ПрайсЛисты.СтавкаНДС.Ставка, 0)) / 100 КОНЕЦ * ПрайсЛистыКонтрагентов.Цена * (1 - СкидкиНаценкиИтог.Скидка / 100) КАК ЦенаПокупки,
	|	ПрайсЛистыКонтрагентов.Валюта.Код КАК ВалютаКод,
	|	ПрайсЛистыКонтрагентов.Валюта КАК Валюта,
	|	(ЕСТЬNULL(КурсыВалют.Курс, 1) / ЕСТЬNULL(КурсыВалют.Кратность, 1)) / &КурсДокумента КАК КоэффициентПересчетаКурса,
	|	СкидкиНаценкиИтог.АлгоритмРасчетаЦены КАК АлгоритмРасчетаЦены,
	|	СкидкиНаценкиИтог.Наценка КАК Наценка,
	|	СкидкиНаценкиИтог.ОкруглятьДо КАК ОкруглятьДо,
	|	ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика КАК КлючСтрокиПоставщика,
	|	ПрайсЛисты.Раздел КАК Раздел,
	|	ПрайсЛисты.ВидДополнения КАК ВидДополнения,
	|	3 КАК ВидПредложения,
	|	ИСТИНА КАК ЕстьВПодразделении
	|ИЗ
	|	втПрайсЛисты КАК ПрайсЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСкидкиНаценкиИтог КАК СкидкиНаценкиИтог
	|		ПО ПрайсЛисты.ПрайсЛист = СкидкиНаценкиИтог.ПрайсЛист
	|			И ПрайсЛисты.ТегПозиции = СкидкиНаценкиИтог.ТегПозиции
	|			И ПрайсЛисты.Производитель = СкидкиНаценкиИтог.Производитель
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|		ПО ПрайсЛисты.ПрайсЛист = ПрайсЛистыКонтрагентов.ПрайсЛист
	|			И ПрайсЛисты.КлючСтрокиПоставщика = ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика
	|			И ПрайсЛисты.ДатаЗаписи = ПрайсЛистыКонтрагентов.ДатаЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаЦен, ) КАК КурсыВалют
	|		ПО (ПрайсЛистыКонтрагентов.Валюта = КурсыВалют.Валюта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втПрайсЛисты";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "99999", ТекстЗапросаНаценки);
	
	ТаблицаПредложений = Запрос.Выполнить().Выгрузить();
	ТаблицаПредложений.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	//Создадим Кеш для хранения установленных цен
	КешЦенНоменклатуры = Новый ТаблицаЗначений;
	КешЦенНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КешЦенНоменклатуры.Колонки.Добавить("АртикулДляПоиска", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(25)));
	КешЦенНоменклатуры.Колонки.Добавить("Производитель", Новый ОписаниеТипов("СправочникСсылка.Производители"));
	КешЦенНоменклатуры.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	Для Каждого СтрокаПредложения Из ТаблицаПредложений Цикл
		Если СтрокаПредложения.Наценка = Null Тогда
			СтрокаПредложения.Цена = 0;
		Иначе
			ЦенаВПрайсЛисте = СтрокаПредложения.ЦенаВПрайсЛисте;
			ЦенаПокупки = СтрокаПредложения.ЦенаПокупки;
			Цена = ЦенаПокупки;
			ЦенаНоменклатуры = 0;
			
			Если ЗначениеЗаполнено(СтрокаПредложения.АлгоритмРасчетаЦены) Тогда
				Если Найти(СтрокаПредложения.АлгоритмРасчетаЦены, "ЦенаНоменклатуры") > 0 Тогда
					Если ЗначениеЗаполнено(СтрокаПредложения.Номенклатура) Тогда
						СтрокаЦены = КешЦенНоменклатуры.Найти(СтрокаПредложения.Номенклатура, "Номенклатура");
						Если СтрокаЦены = Неопределено Тогда
							ЦенаПоТипуЦен = обПолучитьЦену(ТипЦен, СтрокаПредложения.Номенклатура, ДатаЦен,, ВалютаДокумента, КурсДокумента,,, ПодразделениеКомпании);
							СтрокаЦены = КешЦенНоменклатуры.Добавить();
							СтрокаЦены.Номенклатура = СтрокаПредложения.Номенклатура;
							СтрокаЦены.АртикулДляПоиска = СтрокаПредложения.АртикулДляПоиска;
							СтрокаЦены.Производитель = СтрокаПредложения.Производитель;
							СтрокаЦены.Цена = ЦенаПоТипуЦен;
						КонецЕсли;
						ЦенаНоменклатуры = СтрокаЦены.Цена;
					ИначеЕсли ЗначениеЗаполнено(СтрокаПредложения.АртикулДляПоиска) Тогда
						СтрокиЦен = КешЦенНоменклатуры.НайтиСтроки(Новый Структура("АртикулДляПоиска, Производитель", СтрокаПредложения.АртикулДляПоиска, СтрокаПредложения.Производитель));
						Если СтрокиЦен.Количество() = 0 Тогда
							Номенклатура = Справочники.Номенклатура.НайтиНоменклатуру(СтрокаПредложения.АртикулДляПоиска, СтрокаПредложения.Производитель);
							Если ЗначениеЗаполнено(Номенклатура) Тогда
								СтрокаЦены = КешЦенНоменклатуры.Найти(Номенклатура, "Номенклатура");
								Если СтрокаЦены = Неопределено Тогда
									СтрокаЦены = КешЦенНоменклатуры.Добавить();
									СтрокаЦены.Номенклатура = Номенклатура;
									СтрокаЦены.АртикулДляПоиска = СтрокаПредложения.АртикулДляПоиска;
									СтрокаЦены.Производитель = СтрокаПредложения.Производитель;
									СтрокаЦены.Цена = обПолучитьЦену(ТипЦен, Номенклатура, ДатаЦен,, ВалютаДокумента, КурсДокумента,,, ПодразделениеКомпании);
								КонецЕсли;
							Иначе
								СтрокаЦены = КешЦенНоменклатуры.Добавить();
								СтрокаЦены.АртикулДляПоиска = СтрокаПредложения.АртикулДляПоиска;
								СтрокаЦены.Производитель = СтрокаПредложения.Производитель;
								СтрокаЦены.Цена = 0;
							КонецЕсли;
						Иначе
							СтрокаЦены = СтрокиЦен[0];
						КонецЕсли;
						ЦенаНоменклатуры = СтрокаЦены.Цена;
					КонецЕсли;
				КонецЕсли;
				
				Если Найти(СтрокаПредложения.АлгоритмРасчетаЦены, "#Процедура") > 0 Тогда
					Попытка
						Выполнить(СтрЗаменить(СтрокаПредложения.АлгоритмРасчетаЦены, "#Процедура", ""));
					Исключение
						Цена = 0;
					КонецПопытки;
				Иначе
					Попытка
						Выполнить("Цена = Макс(0, " + СтрокаПредложения.АлгоритмРасчетаЦены + ")");
					Исключение
						Цена = 0;
					КонецПопытки;
				КонецЕсли;
			Иначе
				// Простая наценка
				Цена = МАКС(0, Цена + Цена * СтрокаПредложения.Наценка / 100);
			КонецЕсли;
			Цена = Цена * СтрокаПредложения.КоэффициентПересчетаКурса;
			//округляем
			Если НЕ СтрокаПредложения.ОкруглятьДо = 0 Тогда
				ДельтаОкругления = Цена / СтрокаПредложения.ОкруглятьДо;
				ДельтаОкругленияЦел = Цел(ДельтаОкругления);
				Если ДельтаОкругления <> ДельтаОкругленияЦел Тогда
					Цена = (ДельтаОкругленияЦел+1) * СтрокаПредложения.ОкруглятьДо;
				КонецЕсли; 
			КонецЕсли;
			СтрокаПредложения.Цена = Цена;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПолная.ХешАртикулПроизводитель,
	|	ТаблицаПолная.ХешАналога,
	|	ТаблицаПолная.АртикулДляПоиска,
	|	ТаблицаПолная.Производитель,
	|	ТаблицаПолная.Наименование,
	|	ТаблицаПолная.Раздел КАК Раздел,
	|	ТаблицаПолная.ВидДополнения КАК ВидДополнения,
	|	ТаблицаПолная.ПроизводительКод КАК ПроизводительКод
	|ИЗ
	|	втТаблицаПолная КАК ТаблицаПолная
	|ГДЕ
	|	ТаблицаПолная.АртикулДляПоиска <> """"";
	
	ТаблицаАртикулов = Запрос.Выполнить().Выгрузить();
	
	// получить список внешних прайс-листов контрагентов
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.Ссылка,
	|	ПрайсЛистыКонтрагентов.Производитель,
	|	ПрайсЛистыКонтрагентов.ЦенаВключаетНДС,
	|	ПрайсЛистыКонтрагентов.СтавкаНДС,
	|	ЕСТЬNULL(ПрайсЛистыКонтрагентов.СтавкаНДС.Ставка, 0) КАК Ставка
	|ИЗ
	|	Справочник.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовЗапрещенныеПодразделения КАК ЗапрещенныеПодразделения
	|		ПО (ЗапрещенныеПодразделения.ПрайсЛист = ПрайсЛистыКонтрагентов.Ссылка)
	|			И (ЗапрещенныеПодразделения.ПодразделениеКомпании = &Подразделение
	|				ИЛИ ЗапрещенныеПодразделения.ПодразделениеКомпании = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))
	|ГДЕ
	|	ПрайсЛистыКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	И ПрайсЛистыКонтрагентов.ФайлИсточникДанных = ЛОЖЬ
	|	И ПрайсЛистыКонтрагентов.ХранитьДанныеЛокально = ЛОЖЬ
	|	И ЗапрещенныеПодразделения.ПрайсЛист ЕСТЬ NULL 
	|	И ПрайсЛистыКонтрагентов.ВидПрайсЛиста = ЗНАЧЕНИЕ(Перечисление.ВидыПрайсЛистов.ПрайсЛистПоставщика) "
	+?(Не ЗначениеЗаполнено(ПрайсЛистПоставщика),"","И ПрайсЛистыКонтрагентов.Ссылка = &ПрайсЛист");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектПрайсЛиста = Выборка.Ссылка.ПолучитьОбъект();
		
		Для Каждого ТекАртикул Из ТаблицаАртикулов Цикл
			
			Если ЗначениеЗаполнено(Выборка.Производитель) И Выборка.Производитель <> ТекАртикул.Производитель Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураПараметров = ОбъектПрайсЛиста.ПолучитьПараметрыНоменклатуры(ТекАртикул.АртикулДляПоиска, ТекАртикул.Производитель, "Артикул", ТипЦен, ПодразделениеКомпании, ДатаЦен, Истина);
			
			Если СтруктураПараметров <> Неопределено Тогда
				
				Если ТипЗнч(СтруктураПараметров) = Тип("Массив") Тогда
					
					Для Каждого Позиция Из СтруктураПараметров Цикл
						
						СтрокаПрайсЛиста = СтруктураСтрокиЦен();
						
						ЗаполнитьЗначенияСвойств(СтрокаПрайсЛиста, Позиция);
						ЗаполнитьЗначенияСвойств(СтрокаПрайсЛиста, ТекАртикул, "ХешАртикулПроизводитель, Раздел, ВидДополнения, ПроизводительКод");
						СтрокаПрайсЛиста.Размещение = Выборка.Ссылка;
						СтрокаПрайсЛиста.ЦенаВПрайсЛисте = Позиция.Цена;
						СтрокаПрайсЛиста.ВидПредложения = 3;
						СтрокаПрайсЛиста.ЦенаВключаетНДС = Выборка.ЦенаВключаетНДС;
						СтрокаПрайсЛиста.СтавкаНДС = Выборка.СтавкаНДС;
						
						Если Выборка.ЦенаВключаетНДС = ТипЦен.ЦенаВключаетНДС Тогда
							СтрокаПрайсЛиста.Цена = обПересчет(Позиция.ЦенаПродажи, Позиция.Валюта, ДатаЦен, ВалютаДокумента, КурсДокумента);
						ИначеЕсли Выборка.ЦенаВключаетНДС Тогда
							СтрокаПрайсЛиста.Цена = обПересчет(Позиция.ЦенаПродажи * 100 / (100 + Выборка.Ставка), Позиция.Валюта, ДатаЦен, ВалютаДокумента, КурсДокумента);
						Иначе
							СтрокаПрайсЛиста.Цена = обПересчет(Позиция.ЦенаПродажи * (100 + Выборка.Ставка) / 100, Позиция.Валюта, ДатаЦен, ВалютаДокумента, КурсДокумента);
						КонецЕсли;
						
						СтрокаПредложения = ТаблицаПредложений.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаПредложения, СтрокаПрайсЛиста);
						
					КонецЦикла;
					
				Иначе // Структура
					СтрокаПрайсЛиста = СтруктураСтрокиЦен();
					
					ЗаполнитьЗначенияСвойств(СтрокаПрайсЛиста, СтруктураПараметров);
					ЗаполнитьЗначенияСвойств(СтрокаПрайсЛиста, ТекАртикул, "ХешАртикулПроизводитель, Раздел, ВидДополнения, ПроизводительКод");
					
					СтрокаПрайсЛиста.Размещение = Выборка.Ссылка;
					СтрокаПрайсЛиста.ЦенаВПрайсЛисте = СтруктураПараметров.Цена;
					СтрокаПрайсЛиста.ВидПредложения = 3;
					СтрокаПрайсЛиста.ЦенаВключаетНДС = Выборка.ЦенаВключаетНДС;
					СтрокаПрайсЛиста.СтавкаНДС = Выборка.СтавкаНДС;
					
					Если Выборка.ЦенаВключаетНДС = ТипЦен.ЦенаВключаетНДС Тогда
						СтрокаПрайсЛиста.Цена = обПересчет(СтруктураПараметров.ЦенаПродажи, СтруктураПараметров.Валюта, ДатаЦен, ВалютаДокумента, КурсДокумента);
					ИначеЕсли Выборка.ЦенаВключаетНДС Тогда
						СтрокаПрайсЛиста.Цена = обПересчет(СтруктураПараметров.ЦенаПродажи * 100 / (100 + Выборка.Ставка), СтруктураПараметров.Валюта, ДатаЦен, ВалютаДокумента, КурсДокумента);
					Иначе
						СтрокаПрайсЛиста.Цена = обПересчет(СтруктураПараметров.ЦенаПродажи * (100 + Выборка.Ставка) / 100, СтруктураПараметров.Валюта, ДатаЦен, ВалютаДокумента, КурсДокумента);
					КонецЕсли;
					
					СтрокаПредложения = ТаблицаПредложений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаПредложения, СтрокаПрайсЛиста);
					
				КонецЕсли;
				
			КонецЕсли; 
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаПредложений;

КонецФункции

Функция ПолучитьЦеныАртикуловСклады(Знач МассивАртикулов, Знач ПараметрыПоиска) Экспорт
	
	Если ПараметрыПоиска.Свойство("Валюта") И ЗначениеЗаполнено(ПараметрыПоиска.Валюта) Тогда
		ВалютаДокумента = ПараметрыПоиска.Валюта;
	Иначе
		ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПодразделениеКомпании") И ЗначениеЗаполнено(ПараметрыПоиска.ПодразделениеКомпании) Тогда
		ПодразделениеКомпании = ПараметрыПоиска.ПодразделениеКомпании;
	Иначе
		ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ТипЦен") И ЗначениеЗаполнено(ПараметрыПоиска.ТипЦен) Тогда
		ТипЦен = ПараметрыПоиска.ТипЦен;
	Иначе
		ТипЦен = обПраво("ОсновнойТипЦенПродажи");
	КонецЕсли;
	
	ДатаЦен = ТекущаяДата();
	
	// Курс валюты, в которой требуется вернуть цены
	Если ПараметрыПоиска.Свойство("Курс") Тогда
		КурсДокумента = ПараметрыПоиска.Курс;
	Иначе
		СтруктураКурс = обКурсДляВалюты(ВалютаДокумента, ДатаЦен);
		КурсДокумента = СтруктураКурс.Курс / ?(СтруктураКурс.Кратность=0, 1, СтруктураКурс.Кратность);
	КонецЕсли;
	
	ТаблицаСтарт = ПустаяТаблицаАналогов();
	ТаблицаСтарт.Колонки.Удалить("Производитель");
	ТаблицаСтарт.Колонки.Добавить("Производитель", Новый ОписаниеТипов("СправочникСсылка.Производители"));
	
	Для Каждого Артикул Из МассивАртикулов Цикл
		Если Не (ЗначениеЗаполнено(Артикул.Номенклатура) ИЛИ (ЗначениеЗаполнено(Артикул.АртикулДляПоиска) И ТипЗнч(Артикул.Производитель) = Тип("СправочникСсылка.Производители"))) Тогда
			// Нет номенклатуры и не заполнен Артикул для поиска или Производителя нет в нашей базе
			Продолжить;
		КонецЕсли;
		СтрокаСтарт = ТаблицаСтарт.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтарт, Артикул);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь); // Участвует при поиске на складах
	Запрос.УстановитьПараметр("Таб", ТаблицаСтарт);
	Запрос.УстановитьПараметр("ДатаЦен", ДатаЦен);
	Запрос.УстановитьПараметр("Подразделение", ПодразделениеКомпании);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб.ХешАртикулПроизводитель,
	|	Таб.ХешАналога,
	|	Таб.Артикул,
	|	Таб.АртикулДляПоиска КАК АртикулДляПоиска,
	|	Таб.Производитель КАК Производитель,
	|	Таб.Номенклатура КАК Номенклатура,
	|	Таб.Наименование,
	|	Таб.Раздел,
	|	Таб.ВидДополнения,
	|	Таб.ПроизводительКод
	|ПОМЕСТИТЬ втТаблицаПолная
	|ИЗ
	|	&Таб КАК Таб
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	АртикулДляПоиска,
	|	Производитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПолная.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	ТаблицаПолная.ХешАналога КАК ХешАналога,
	|	ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК Размещение,
	|	ОстаткиТоваров.Номенклатура.Артикул КАК Артикул,
	|	ОстаткиТоваров.Номенклатура.АртикулДляПоиска КАК АртикулДляПоиска,
	|	ТаблицаПолная.ПроизводительКод КАК ПроизводительКод,
	|	ОстаткиТоваров.Номенклатура.Производитель КАК Производитель,
	|	ОстаткиТоваров.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваров.Номенклатура.Наименование КАК Наименование,
	|	ТаблицаПолная.Раздел КАК Раздел,
	|	ТаблицаПолная.ВидДополнения КАК ВидДополнения,
	|	0 КАК ЦенаВПрайсЛисте,
	|	0 КАК ЦенаПокупки,
	|	"""" КАК ВалютаКод,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	ОстаткиТоваров.Наличие КАК Наличие,
	|	0 КАК Количество,
	|	ОстаткиТоваров.Номенклатура.Код КАК КлючСтрокиПоставщика,
	|	"""" КАК СрокПоставки,
	|	0 КАК СрокПоставкиМинимальный,
	|	0 КАК СрокПоставкиМаксимальный,
	|	1 КАК ВидПредложения,
	|	ОстаткиТоваров.ЕстьНаСкладеПодразделения КАК ЕстьВПодразделении
	|ИЗ
	|	(ВЫБРАТЬ
	|		Рег.Номенклатура КАК Номенклатура,
	|		СУММА(Рег.КоличествоОстаток - Рег.РезервОстаток) КАК Наличие,
	|		МАКСИМУМ(Рег.СкладКомпании.Подразделение = &Подразделение) КАК ЕстьНаСкладеПодразделения
	|	ИЗ
	|		РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|				&ДатаЦен,
	|				(Номенклатура В
	|						(ВЫБРАТЬ
	|							Таб.Номенклатура
	|						ИЗ
	|							втТаблицаПолная КАК Таб
	|						ГДЕ
	|							Таб.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.Пустаяссылка))
	|					ИЛИ (Номенклатура.АртикулДляПоиска, Номенклатура.Производитель) В
	|						(ВЫБРАТЬ
	|							Таб.АртикулДляПоиска,
	|							Таб.Производитель
	|						ИЗ
	|							втТаблицаПолная КАК Таб
	|						ГДЕ
	|							Таб.АртикулДляПоиска <> """"))
	|					И НЕ СкладКомпании В
	|							(ВЫБРАТЬ
	|								Рег.СкладКомпании
	|							ИЗ
	|								РегистрСведений.ЗапрещенныеСкладыКомпании КАК Рег
	|							ГДЕ
	|								Рег.Пользователь = &Пользователь)) КАК Рег
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Рег.Номенклатура) КАК ОстаткиТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаПолная КАК ТаблицаПолная
	|		ПО (ТаблицаПолная.Номенклатура = ОстаткиТоваров.Номенклатура
	|				ИЛИ ТаблицаПолная.АртикулДляПоиска <> """"
	|					И ТаблицаПолная.АртикулДляПоиска = ОстаткиТоваров.Номенклатура.АртикулДляПоиска
	|					И ТаблицаПолная.Производитель = ОстаткиТоваров.Номенклатура.Производитель)";
	
	ТаблицаПредложений = Запрос.Выполнить().Выгрузить();
	ТаблицаПредложений.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	//Создадим Кеш для хранения установленных цен
	КешЦенНоменклатуры = Новый ТаблицаЗначений;
	КешЦенНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КешЦенНоменклатуры.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	Для Каждого СтрокаНаличие Из ТаблицаПредложений Цикл
		
		СтрокаЦены = КешЦенНоменклатуры.Найти(СтрокаНаличие.Номенклатура, "Номенклатура");
		Если СтрокаЦены = Неопределено Тогда
			ЦенаПоТипуЦен = обПолучитьЦену(ТипЦен, СтрокаНаличие.Номенклатура, ДатаЦен,, ВалютаДокумента, КурсДокумента,,, ПодразделениеКомпании);
			СтрокаЦены = КешЦенНоменклатуры.Добавить();
			СтрокаЦены.Номенклатура = СтрокаНаличие.Номенклатура;
			СтрокаЦены.Цена = ЦенаПоТипуЦен;
		КонецЕсли;
		
		СтрокаНаличие.Цена = СтрокаЦены.Цена;
	КонецЦикла;
	
	Возврат ТаблицаПредложений;
	
КонецФункции

Функция ПолучитьЦеныАртикуловЗаказы(Знач МассивАртикулов, Знач ПараметрыПоиска) Экспорт
	
	Если ПараметрыПоиска.Свойство("Валюта") И ЗначениеЗаполнено(ПараметрыПоиска.Валюта) Тогда
		ВалютаДокумента = ПараметрыПоиска.Валюта;
	Иначе
		ВалютаДокумента = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПодразделениеКомпании") И ЗначениеЗаполнено(ПараметрыПоиска.ПодразделениеКомпании) Тогда
		ПодразделениеКомпании = ПараметрыПоиска.ПодразделениеКомпании;
	Иначе
		ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ТипЦен") И ЗначениеЗаполнено(ПараметрыПоиска.ТипЦен) Тогда
		ТипЦен = ПараметрыПоиска.ТипЦен;
	Иначе
		ТипЦен = обПраво("ОсновнойТипЦенПродажи");
	КонецЕсли;
	
	ДатаЦен = ТекущаяДата();
	
	// Курс валюты, в которой требуется вернуть цены
	Если ПараметрыПоиска.Свойство("Курс") Тогда
		КурсДокумента = ПараметрыПоиска.Курс;
	Иначе
		СтруктураКурс = обКурсДляВалюты(ВалютаДокумента, ДатаЦен);
		КурсДокумента = СтруктураКурс.Курс / ?(СтруктураКурс.Кратность=0, 1, СтруктураКурс.Кратность);
	КонецЕсли;
	
	ТаблицаСтарт = ПустаяТаблицаАналогов();
	ТаблицаСтарт.Колонки.Удалить("Производитель");
	ТаблицаСтарт.Колонки.Добавить("Производитель", Новый ОписаниеТипов("СправочникСсылка.Производители"));
	
	Для Каждого Артикул Из МассивАртикулов Цикл
		Если Не (ЗначениеЗаполнено(Артикул.Номенклатура) ИЛИ (ЗначениеЗаполнено(Артикул.АртикулДляПоиска) И ТипЗнч(Артикул.Производитель) = Тип("СправочникСсылка.Производители"))) Тогда
			// Нет номенклатуры и не заполнен Артикул для поиска или Производителя нет в нашей базе
			Продолжить;
		КонецЕсли;
		СтрокаСтарт = ТаблицаСтарт.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСтарт, Артикул);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Таб", ТаблицаСтарт);
	Запрос.УстановитьПараметр("ДатаЦен", ДатаЦен);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб.ХешАртикулПроизводитель,
	|	Таб.ХешАналога,
	|	Таб.Артикул,
	|	Таб.АртикулДляПоиска,
	|	Таб.Производитель,
	|	Таб.Номенклатура,
	|	Таб.Наименование,
	|	Таб.Раздел,
	|	Таб.ВидДополнения,
	|	Таб.ПроизводительКод
	|ПОМЕСТИТЬ втТаблицаПолная
	|ИЗ
	|	&Таб КАК Таб
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Таб.Номенклатура,
	|	Таб.АртикулДляПоиска,
	|	Таб.Производитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПолная.ХешАртикулПроизводитель КАК ХешАртикулПроизводитель,
	|	ТаблицаПолная.ХешАналога КАК ХешАналога,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка) КАК Размещение,
	|	ЗаказыПоставщикам.Номенклатура.Артикул КАК Артикул,
	|	ЗаказыПоставщикам.Номенклатура.АртикулДляПоиска КАК АртикулДляПоиска,
	|	ТаблицаПолная.ПроизводительКод КАК ПроизводительКод,
	|	ЗаказыПоставщикам.Номенклатура.Производитель КАК Производитель,
	|	ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	|	ЗаказыПоставщикам.Номенклатура.Наименование КАК Наименование,
	|	ТаблицаПолная.Раздел КАК Раздел,
	|	ТаблицаПолная.ВидДополнения КАК ВидДополнения,
	|	2 КАК ВидПредложения,
	|	0 КАК ЦенаВПрайсЛисте,
	|	0 КАК ЦенаПокупки,
	|	"""" КАК ВалютаКод,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	ЗаказыПоставщикам.Номенклатура.Код КАК КлючСтрокиПоставщика,
	|	""Свободный заказ"" КАК СрокПоставки,
	|	ЗаказыПоставщикам.ДатаПоставки КАК ДатаПоставки,
	|	0 КАК СрокПоставкиМинимальный,
	|	0 КАК СрокПоставкиМаксимальный,
	|	ЗаказыПоставщикам.СвободныйЗаказ КАК Наличие,
	|	0 КАК Количество,
	|	ЗаказыПоставщикам.ЕстьВЗаказахПодразделения КАК ЕстьВПодразделении
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	|		МИНИМУМ(ЗаказыПоставщикам.ЗаказПоставщику.СрокПоставки) КАК ДатаПоставки,
	|		СУММА(ЗаказыПоставщикам.ЗаказаноОстаток - ЕСТЬNULL(ЗаказыРаспределение.КоличествоОстаток, 0)) КАК СвободныйЗаказ,
	|		МАКСИМУМ(ЗаказыПоставщикам.ЗаказПоставщику.ПодразделениеКомпании = &ПодразделениеКомпании) КАК ЕстьВЗаказахПодразделения
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикам.Остатки(
	|				&ДатаЦен,
	|				ЗаказПоставщику.ПодразделениеКомпании В ИЕРАРХИИ (&ПодразделениеКомпании)
	|						И Номенклатура В
	|							(ВЫБРАТЬ
	|								Таб.Номенклатура
	|							ИЗ
	|								втТаблицаПолная КАК Таб
	|							ГДЕ
	|								Таб.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.Пустаяссылка))
	|					ИЛИ (Номенклатура.АртикулДляПоиска, Номенклатура.Производитель) В
	|						(ВЫБРАТЬ
	|							Таб.АртикулДляПоиска,
	|							Таб.Производитель
	|						ИЗ
	|							втТаблицаПолная КАК Таб
	|						ГДЕ
	|							Таб.АртикулДляПоиска <> """")) КАК ЗаказыПоставщикам
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыРаспределение.Остатки(
	|					&ДатаЦен,
	|					ЗаказПоставщика.ПодразделениеКомпании В ИЕРАРХИИ (&ПодразделениеКомпании)
	|							И Номенклатура В
	|								(ВЫБРАТЬ
	|									Таб.Номенклатура
	|								ИЗ
	|									втТаблицаПолная КАК Таб
	|								ГДЕ
	|									Таб.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.Пустаяссылка))
	|						ИЛИ (Номенклатура.АртикулДляПоиска, Номенклатура.Производитель) В
	|							(ВЫБРАТЬ
	|								Таб.АртикулДляПоиска,
	|								Таб.Производитель
	|							ИЗ
	|								втТаблицаПолная КАК Таб
	|							ГДЕ
	|								Таб.АртикулДляПоиска <> """")) КАК ЗаказыРаспределение
	|			ПО ЗаказыПоставщикам.ЗаказПоставщику = ЗаказыРаспределение.ЗаказПоставщика
	|				И ЗаказыПоставщикам.Номенклатура = ЗаказыРаспределение.Номенклатура
	|	ГДЕ
	|		ЗаказыПоставщикам.ЗаказаноОстаток - ЕСТЬNULL(ЗаказыРаспределение.КоличествоОстаток, 0) > 0
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗаказыПоставщикам.Номенклатура) КАК ЗаказыПоставщикам
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаПолная КАК ТаблицаПолная
	|		ПО (ТаблицаПолная.Номенклатура = ЗаказыПоставщикам.Номенклатура
	|				ИЛИ ТаблицаПолная.АртикулДляПоиска <> """"
	|					И ТаблицаПолная.АртикулДляПоиска = ЗаказыПоставщикам.Номенклатура.АртикулДляПоиска
	|					И ТаблицаПолная.Производитель = ЗаказыПоставщикам.Номенклатура.Производитель)";
	
	ТаблицаПредложений = Запрос.Выполнить().Выгрузить();
	ТаблицаПредложений.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	//Создадим Кеш для хранения установленных цен
	КешЦенНоменклатуры = Новый ТаблицаЗначений;
	КешЦенНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КешЦенНоменклатуры.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	
	Для Каждого СтрокаСвободныйЗаказ Из ТаблицаПредложений Цикл
		СтрокаЦены = КешЦенНоменклатуры.Найти(СтрокаСвободныйЗаказ.Номенклатура, "Номенклатура");
		Если СтрокаЦены = Неопределено Тогда
			ЦенаПоТипуЦен = обПолучитьЦену(ТипЦен, СтрокаСвободныйЗаказ.Номенклатура, ДатаЦен,, ВалютаДокумента, КурсДокумента,,, ПодразделениеКомпании);
			СтрокаЦены = КешЦенНоменклатуры.Добавить();
			СтрокаЦены.Номенклатура = СтрокаСвободныйЗаказ.Номенклатура;
			СтрокаЦены.Цена = ЦенаПоТипуЦен;
		КонецЕсли;
		СтрокаСвободныйЗаказ.Цена = СтрокаЦены.Цена;
		СтрокаСвободныйЗаказ.СрокПоставки = Формат(СтрокаСвободныйЗаказ.ДатаПоставки, "ДФ=dd.MM.yyyy");
		СтрокаСвободныйЗаказ.СрокПоставкиМинимальный = Макс(КонецДня(СтрокаСвободныйЗаказ.ДатаПоставки)-КонецДня(ДатаЦен), 0);
		СтрокаСвободныйЗаказ.СрокПоставкиМаксимальный = ?(СтрокаСвободныйЗаказ.СрокПоставкиМинимальный=0, 0, СтрокаСвободныйЗаказ.СрокПоставкиМинимальный+24*60*60);
	КонецЦикла;
	
	ТаблицаПредложений.Колонки.Удалить("ДатаПоставки");
	
	Возврат ТаблицаПредложений;
	
КонецФункции



/////////////////////////////////////////////////////////////////////////////////////////
////////////////////////РАБОЧИЙ ЛИСТ/////////////////////////////////////////////////////

Функция ПолучитьВариантыПоставкиРабочегоЛиста(Знач Объект, Знач Подразделение) Экспорт
	
	Если ЗначениеЗаполнено(Объект) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		Если ТипЗнч(Объект) = Тип("Строка") Тогда
			Запрос.УстановитьПараметр("Объект", "Клиент: " + Объект);
		Иначе
			Запрос.УстановитьПараметр("Объект", Объект);
		КонецЕсли;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	РабочиеЛисты.Дата,
			|	РабочиеЛисты.Значение
			|ИЗ
			|	РегистрСведений.атРабочиеЛисты КАК РабочиеЛисты
			|ГДЕ
			|	ВЫРАЗИТЬ(РабочиеЛисты.Автор КАК Справочник.ПодразделенияКомпании).Ссылка = &Подразделение
			|	И РабочиеЛисты.Объект = &Объект";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Структура = Выборка.Значение.Получить();
			Если ТипЗнч(Структура) = Тип("Структура") И Структура.Свойство("ВариантыПоставки") Тогда
				Структура.Вставить("Дата", Выборка.Дата);
				Если Не Структура.Свойство("Валюта") Тогда
					Структура.Вставить("Валюта");
				КонецЕсли;
				Возврат Структура;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("Дата, Валюта, ВариантыПоставки");
	
КонецФункции

Функция ПолучитьЛокальныйРабочийЛист(Знач ВидОбъекта, Знач Объект, Знач Автор=Неопределено, ПолучатьЗапросы=Истина, ПолучатьДетали=Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если Не (ПолучатьЗапросы ИЛИ ПолучатьДетали) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Автор", Автор);
	
	СписокОбъектов = Новый СписокЗначений;
	СписокОбъектов.Добавить(Объект);
	Если ТипЗнч(Объект) = Тип("Строка") Тогда
		СписокОбъектов.Добавить(ВидОбъекта + ": " + Объект);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Объект", СписокОбъектов);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РабочиеЛисты.Автор,
		|	РабочиеЛисты.Дата,
		|	РабочиеЛисты.Значение,
		|	РабочиеЛисты.Запросы
		|ИЗ
		|	РегистрСведений.атРабочиеЛисты КАК РабочиеЛисты
		|ГДЕ
		|	" + ?(Автор = Неопределено, "ИСТИНА", "(РабочиеЛисты.Автор = &Автор
		|			ИЛИ РабочиеЛисты.Автор = НЕОПРЕДЕЛЕНО)") + "
		|	" + ?(НЕ ПолучатьЗапросы, "И НЕ РабочиеЛисты.Запросы", "")+"
		|	" + ?(НЕ ПолучатьДетали, "И РабочиеЛисты.Запросы", "") + "
		|	И РабочиеЛисты.Объект В(&Объект)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	РабочийЛист = Новый Структура;
	РабочийЛист.Вставить("Дата", Неопределено);
	Если ПолучатьЗапросы Тогда
		РабочийЛист.Вставить("Запросы", ТаблицаЗапросы());
	КонецЕсли;
	Если ПолучатьДетали Тогда
		РабочийЛист.Вставить("НомераДеталей", ТаблицаНомераДеталей());
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		Структура = Выборка.Значение.Получить();
		
		Если Выборка.Автор <> Неопределено И Выборка.Автор = Автор Тогда
			// Рабочий лист пользователя
			РабочийЛист.Дата = Выборка.Дата;
			Если ТипЗнч(Структура) = Тип("Структура") Тогда
				Для Каждого КлючЗначение Из Структура Цикл
					Если КлючЗначение.Ключ = "Запросы" ИЛИ КлючЗначение.Ключ = "НомераДеталей" Тогда
						// Такие ключи нам не нужны
						Продолжить;
					КонецЕсли;
					Если Не РабочийЛист.Свойство(КлючЗначение.Ключ) Тогда
						РабочийЛист.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		// Запросы нам нужны общие и указанного автора
		Если ПолучатьЗапросы И Структура.Свойство("Запросы") И (Выборка.Автор = Неопределено ИЛИ Выборка.Автор = Автор) Тогда
			Для Каждого Запрос Из Структура.Запросы Цикл
				СтрокаЗапросы = РабочийЛист.Запросы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗапросы, Запрос);
			КонецЦикла;
		КонецЕсли;
		
		Если ПолучатьДетали Тогда
			// Номера деталей нужны все, попавшие в запрос
			Если Структура.Свойство("НомераДеталей") Тогда
				Для Каждого НомерДетали Из Структура.НомераДеталей Цикл
					СтрокаНомераДеталей = РабочийЛист.НомераДеталей.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаНомераДеталей, НомерДетали);
					Если Не ЗначениеЗаполнено(СтрокаНомераДеталей.Наименование) Тогда
						Попытка СтрокаНомераДеталей.Наименование = НомерДетали.Номенклатура;
						Исключение КонецПопытки;
					КонецЕсли;
					СтрокаНомераДеталей.Автор = Выборка.Автор;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РабочийЛист;
	
КонецФункции

Функция ОбновитьЗапросРабочегоЛиста(Знач ВидОбъекта, Знач Объект, Знач Сервис, Знач Каталог, Знач Марка="", Знач Модель="", Знач Модификация="", Параметры=Неопределено, ИдентификаторЗапроса="") Экспорт
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтборОбъект = Объект;
	
	СписокОбъектов = Новый СписокЗначений;
	СписокОбъектов.Добавить(ОтборОбъект);
	Если ТипЗнч(Объект) = Тип("Строка") Тогда
		ОтборОбъект = ВидОбъекта + ": " + Объект;
		СписокОбъектов.Добавить(ОтборОбъект);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", СписокОбъектов);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РабочиеЛисты.Значение
		|ИЗ
		|	РегистрСведений.атРабочиеЛисты КАК РабочиеЛисты
		|ГДЕ
		|	РабочиеЛисты.Автор = НЕОПРЕДЕЛЕНО
		|	И РабочиеЛисты.Объект В(&Объект)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Запросы = ТаблицаЗапросы();
	
	Пока Выборка.Следующий() Цикл
		
		Структура = Выборка.Значение.Получить();
		
		Если Структура.Свойство("Запросы") Тогда
			Для Каждого Запрос Из Структура.Запросы Цикл
				СтрокаЗапросы = Запросы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗапросы, Запрос);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	СервисКод = "";
	Если ТипЗнч(Сервис) = Тип("Структура") Тогда
		СервисКод = Сервис.Код;
		Сервис = Сервис.Наименование;
	КонецЕсли;
	КаталогКод = "";
	Если ТипЗнч(Каталог) = Тип("Структура") Тогда
		КаталогКод = Каталог.Код;
		Каталог = Каталог.Наименование;
	КонецЕсли;
	МодельКод = "";
	Если ТипЗнч(Модель) = Тип("Структура") Тогда
		МодельКод = Модель.Код;
		Модель = Модель.Наименование;
	КонецЕсли;
	МаркаКод = "";
	Если ТипЗнч(Марка) = Тип("Структура") Тогда
		МаркаКод = Марка.Код;
		Марка = Марка.Наименование;
	КонецЕсли;
	МодификацияКод = "";
	Если ТипЗнч(Модификация) = Тип("Структура") Тогда
		МодификацияКод = Модификация.Код;
		Модификация = Модификация.Наименование;
	КонецЕсли;
	
	Комментарий = Неопределено;
	СтрокаПараметры = Неопределено;
	Если ТипЗнч(Параметры) = Тип("Структура") Тогда
		Если Параметры.Свойство("Комментарий") Тогда
			Комментарий = Параметры.Комментарий;
			Параметры.Удалить("Комментарий");
		КонецЕсли;
		СтрокаПараметры = ЗначениеВСтрокуВнутр(Параметры);
	ИначеЕсли ТипЗнч(Параметры) = Тип("Массив") Тогда
		СтрокаПараметры = ЗначениеВСтрокуВнутр(Параметры);
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СервисКод", СервисКод);
	СтруктураПоиска.Вставить("Сервис", Сервис);
	СтруктураПоиска.Вставить("КаталогКод", КаталогКод);
	СтруктураПоиска.Вставить("Каталог", Каталог);
	СтруктураПоиска.Вставить("МаркаКод", МаркаКод);
	СтруктураПоиска.Вставить("Марка", Марка);
	СтруктураПоиска.Вставить("МодельКод", МодельКод);
	СтруктураПоиска.Вставить("Модель", Модель);
	СтруктураПоиска.Вставить("МодификацияКод", МодификацияКод);
	СтруктураПоиска.Вставить("Модификация", Модификация);
	
	СтрокиЗапросы = Запросы.НайтиСтроки(СтруктураПоиска);
	
	Если СтрокиЗапросы.Количество() = 0 Тогда
		СтрокаЗапросы = Запросы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапросы, СтруктураПоиска);
		СтрокаЗапросы.Идентификатор = СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
		НовыйЗапрос = Истина;
	Иначе
		НовыйЗапрос = Ложь;
		СтрокаЗапросы = СтрокиЗапросы[0];
		//СтрокаЗапросы.Идентификатор = СтрЗаменить(СтрокаЗапросы.Идентификатор, "-", "");
	КонецЕсли;
	
	ИдентификаторЗапроса = СтрокаЗапросы.Идентификатор;
	
	СтрокаЗапросы.ДатаОбращения = ТекущаяДата();
	Если Комментарий <> Неопределено Тогда
		СтрокаЗапросы.Комментарий = Комментарий;
	КонецЕсли;
	Если СтрокаПараметры <> Неопределено Тогда
		СтрокаЗапросы.Параметры = СтрокаПараметры;
	Иначе
		Если ЗначениеЗаполнено(СтрокаЗапросы.Параметры) Тогда
			// Передадим обратно параметры из найденной строки запроса
			Параметры = ЗначениеИзСтрокиВнутр(СтрокаЗапросы.Параметры);
		КонецЕсли;
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Запросы", Запросы);
	ХранилищеЗначения = Новый ХранилищеЗначения(Структура);
	
	НаборЗаписей = РегистрыСведений.атРабочиеЛисты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Автор.Установить(Неопределено, Истина);
	НаборЗаписей.Отбор.Объект.Установить(ОтборОбъект, Истина);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	СтрокаНабора = НаборЗаписей.Добавить();
	СтрокаНабора.Дата = ТекущаяДата();
	СтрокаНабора.Автор = Неопределено;
	СтрокаНабора.ВидОбъекта = ВидОбъекта;
	СтрокаНабора.Объект = ОтборОбъект;
	СтрокаНабора.Запросы = Истина;
	СтрокаНабора.Значение = ХранилищеЗначения;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Если НовыйЗапрос Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецПопытки;
	
	Возврат НовыйЗапрос;
	
КонецФункции

Функция УдалитьЗапросРабочегоЛиста(Знач ВидОбъекта, Знач Объект, Знач ИдентификаторЗапроса) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтборОбъект = Объект;
	
	СписокОбъектов = Новый СписокЗначений;
	СписокОбъектов.Добавить(ОтборОбъект);
	Если ТипЗнч(Объект) = Тип("Строка") Тогда
		ОтборОбъект = ВидОбъекта + ": " + Объект;
		СписокОбъектов.Добавить(ОтборОбъект);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", СписокОбъектов);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РабочиеЛисты.Значение
		|ИЗ
		|	РегистрСведений.атРабочиеЛисты КАК РабочиеЛисты
		|ГДЕ
		|	РабочиеЛисты.Автор = НЕОПРЕДЕЛЕНО
		|	И РабочиеЛисты.Объект В(&Объект)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Запросы = ТаблицаЗапросы();
	
	Пока Выборка.Следующий() Цикл
		
		Структура = Выборка.Значение.Получить();
		
		Если Структура.Свойство("Запросы") Тогда
			Для Каждого Запрос Из Структура.Запросы Цикл
				Если Запрос.Идентификатор = ИдентификаторЗапроса Тогда
					Продолжить;
				КонецЕсли;
				СтрокаЗапросы = Запросы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗапросы, Запрос);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Структура = Новый Структура;
	Структура.Вставить("Запросы", Запросы);
	ХранилищеЗначения = Новый ХранилищеЗначения(Структура);
	
	НаборЗаписей = РегистрыСведений.атРабочиеЛисты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Автор.Установить(Неопределено, Истина);
	НаборЗаписей.Отбор.Объект.Установить(ОтборОбъект, Истина);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	СтрокаНабора = НаборЗаписей.Добавить();
	СтрокаНабора.Дата = ТекущаяДата();
	СтрокаНабора.Автор = Неопределено;
	СтрокаНабора.ВидОбъекта = ВидОбъекта;
	СтрокаНабора.Объект = ОтборОбъект;
	СтрокаНабора.Запросы = Истина;
	СтрокаНабора.Значение = ХранилищеЗначения;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Процедура АвтомобильПриЗаписи(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) ИЛИ Не ЗначениеЗаполнено(Ссылка.VIN) Тогда
		Возврат;
	КонецЕсли;
	
	VIN = СокрЛП(Ссылка.VIN);
	
	// Перепишем корзины на ссылку
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Корзина.Номер,
		|	Корзина.Дата,
		|	Корзина.Клиент,
		|	Корзина.ПодразделениеКомпании
		|ИЗ
		|	РегистрСведений.Корзина КАК Корзина
		|ГДЕ
		|	(ВЫРАЗИТЬ(Корзина.Автомобиль КАК СТРОКА(50))) = &VIN";
	Запрос.УстановитьПараметр("VIN", VIN);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Менеджер = РегистрыСведений.Корзина.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Менеджер, Выборка);
		Менеджер.Прочитать();
		Если Менеджер.Выбран() Тогда
			Менеджер.Автомобиль = Ссылка;
			Менеджер.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	СписокОбъектов = Новый СписокЗначений;
	СписокОбъектов.Добавить(VIN);
	СписокОбъектов.Добавить("Автомобиль: " + VIN);
	
	Запрос.УстановитьПараметр("Объект", СписокОбъектов);
	
	// Сначала перельем Запросы
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РабочиеЛисты.Автор,
		|	РабочиеЛисты.Объект,
		|	РабочиеЛисты.Значение
		|ИЗ
		|	РегистрСведений.атРабочиеЛисты КАК РабочиеЛисты
		|ГДЕ
		|	РабочиеЛисты.Объект В(&Объект)
		|	И РабочиеЛисты.Запросы";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Запросы = ПолучитьЛокальныйРабочийЛист("Автомобиль", Ссылка,, Истина, Ложь).Запросы;
	
	Пока Выборка.Следующий() Цикл
		ТекРабочийЛист = Выборка.Значение.Получить();
		Если ТекРабочийЛист.Свойство("Запросы") Тогда
			Для Каждого ТекЗапрос Из ТекРабочийЛист.Запросы Цикл
				Если Запросы.Найти(ТекЗапрос.Идентификатор, "Идентификатор") = Неопределено Тогда
					НоваяСтрока = Запросы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекЗапрос);
				КонецЕсли;
			КонецЦикла;
			Менеджер = РегистрыСведений.атРабочиеЛисты.СоздатьМенеджерЗаписи();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Менеджер, Выборка);
		Менеджер.Прочитать();
		Если Менеджер.Выбран() Тогда
			Менеджер.Удалить();
		КонецЕсли;
	КонецЦикла;
	
	Если Запросы.Количество() > 0 Тогда
		НовоеЗначение = Новый Структура("Запросы", Запросы);
		Менеджер = РегистрыСведений.атРабочиеЛисты.СоздатьМенеджерЗаписи();
		Менеджер.Автор      = Неопределено;
		Менеджер.Объект     = Ссылка;
		Менеджер.Дата       = ТекущаяДата();
		Менеджер.Значение   = Новый ХранилищеЗначения(НовоеЗначение, Новый СжатиеДанных(9));
		Менеджер.Запросы    = Истина;
		Менеджер.ВидОбъекта = "Автомобиль";
		Менеджер.Записать();
	КонецЕсли;
	
	// Теперь перельем НомераДеталей
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РабочиеЛисты.Автор,
		|	РабочиеЛисты.Объект,
		|	РабочиеЛисты.Значение,
		|	РабочиеЛисты.Клиент
		|ИЗ
		|	РегистрСведений.атРабочиеЛисты КАК РабочиеЛисты
		|ГДЕ
		|	РабочиеЛисты.Объект В(&Объект)
		|	И Не РабочиеЛисты.Запросы";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		РабочийЛист = ПолучитьЛокальныйРабочийЛист("Автомобиль", Ссылка, Выборка.Автор, Ложь, Истина);
		
		ТекРабочийЛист = Выборка.Значение.Получить();
		Если ТекРабочийЛист.Свойство("НомераДеталей") Тогда
			Для Каждого ТекНомерДетали Из ТекРабочийЛист.НомераДеталей Цикл
				НоваяСтрока = РабочийЛист.НомераДеталей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекНомерДетали);
			КонецЦикла;
		КонецЕсли;
		
		Менеджер = РегистрыСведений.атРабочиеЛисты.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Менеджер, Выборка);
		Менеджер.Прочитать();
		Если Менеджер.Выбран() Тогда
			Менеджер.Удалить();
		КонецЕсли;
		
		Если РабочийЛист.НомераДеталей.Количество() > 0 Тогда
			
			НовоеЗначение = Новый Структура;
			НовоеЗначение.Вставить("НомераДеталей", РабочийЛист.НомераДеталей);
			
			Менеджер = РегистрыСведений.атРабочиеЛисты.СоздатьМенеджерЗаписи();
			Менеджер.Автор      = Выборка.Автор;
			Менеджер.Объект     = Ссылка;
			Менеджер.Дата       = ТекущаяДата();
			Менеджер.Значение   = Новый ХранилищеЗначения(НовоеЗначение, Новый СжатиеДанных(9));
			Менеджер.Запросы    = Ложь;
			Менеджер.ВидОбъекта = "Автомобиль";
			Менеджер.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьТег(Знач ИмяСловаря, Знач Код, Знач Тег, Ошибка="") Экспорт 
	
	СтруктураЗапроса = Новый Структура("Отбор, Данные", Новый Структура("Код", Код), Новый Структура("Тег",СокрЛП(Тег)));
	ТелоЗапроса = ПолучитьТекстСообщения(СтруктураЗапроса);
	ВыполнитьЗапрос("dictionaries", ИмяСловаря, ТелоЗапроса, "УстановитьТег",, Ошибка);
	
КонецПроцедуры

Процедура УдалитьТег(Знач ИмяСловаря, Знач Код, Знач Тег, Ошибка="") Экспорт 
	
	СтруктураЗапроса = Новый Структура("Отбор,Данные", Новый Структура("Код", Код), Новый Структура("Тег",СокрЛП(Тег)));
	ТелоЗапроса = ПолучитьТекстСообщения(СтруктураЗапроса);
	ВыполнитьЗапрос("dictionaries", ИмяСловаря, ТелоЗапроса, "УдалитьТег",, Ошибка);
	
КонецПроцедуры

Процедура ДобавитьТегАвтомобиля(Знач Отбор, Знач Данные, Ошибка="") Экспорт 
	
	Если ТипЗнч(Данные) = Тип("Строка") Тогда
		Данные = Новый Структура("Тег", СокрЛП(Данные));
	КонецЕсли;
	
	СтруктураЗапроса = Новый Структура("Отбор, Данные", Отбор, Данные);
	ТелоЗапроса = ПолучитьТекстСообщения(СтруктураЗапроса);
	ВыполнитьЗапрос("dictionaries", "Автомобили", ТелоЗапроса, "УстановитьТег",, Ошибка);
	
КонецПроцедуры

Процедура УдалитьТегАвтомобиля(Знач Отбор, Знач Тег, Ошибка="") Экспорт 
	
	СтруктураЗапроса = Новый Структура("Отбор, Данные", Отбор, Новый Структура("Тег",СокрЛП(Тег)));
	ТелоЗапроса = ПолучитьТекстСообщения(СтруктураЗапроса);
	ВыполнитьЗапрос("dictionaries", "Автомобили", ТелоЗапроса, "УдалитьТег",, Ошибка);
	
КонецПроцедуры

Функция ПолучитьПодключенныеУслуги(Ошибка="") Экспорт
	
	Возврат ВыполнитьЗапрос("partner", "ПодключенныеУслуги",,,, Ошибка);
	
КонецФункции // ПолучитьПодулюченныеУслуги(Логин, Пароль)

Функция ПодключитьВебПрайсЛист(Знач Код, Ошибка="") Экспорт
	
	ТелоЗапроса = СокрЛП(ПолучитьТекстСообщения(Новый Структура("Данные", Новый Структура("ВебПрайсЛист",СокрЛП(Код)))));
	Возврат ВыполнитьЗапрос("partner", "ПодключитьВебПрайсЛист", ТелоЗапроса,,, Ошибка);
	
КонецФункции // ПодключитьВебПрайсЛист(Код)

Функция ОтключитьВебПрайсЛист(Знач Код, Ошибка="") Экспорт
	
	ТелоЗапроса = СокрЛП(ПолучитьТекстСообщения(Новый Структура("Данные", Новый Структура("ВебПрайсЛист",СокрЛП(Код)))));
	Возврат ВыполнитьЗапрос("partner", "ОтключитьВебПрайсЛист", ТелоЗапроса,,, Ошибка);
	
КонецФункции // ОтключитьВебПрайсЛист(Код)

Функция ОбновитьДоступныеСервисы(Ошибка="") Экспорт
	
	Результат = ВыполнитьЗапрос("partner", "ДоступныеСервисы",,,, Ошибка);
	
	Если Результат <> Неопределено Тогда
		Результат.Вставить("ДоступностьСервисов", Новый Структура);
		Для Каждого КлючЗначение Из Результат.Сервисы Цикл
			Результат.ДоступностьСервисов.Вставить(КлючЗначение.ИмяСервиса, КлючЗначение.Доступен);
		КонецЦикла;
		
		ПараметрыСеанса.атДоступныеСервисы = ФиксированныеДанные(Результат);
	КонецЕсли;
	
КонецФункции



// Вычитает один массив элементов из другого массива. Возвращает результат вычитания.
//
// Параметры:
//  Массив - Массив - массив элементов, из которого необходимо выполнить вычитание;
//  МассивВычитания - Массив - массив элементов, который будет вычитаться.
// 
// Возвращаемое значение:
//  Массив - результат вычитания двух массивов.
//
Функция СократитьМассив(Массив, МассивВычитания) Экспорт
	
	Результат = Новый Массив;
	
	Для Каждого Элемент Из Массив Цикл
		
		Если МассивВычитания.Найти(Элемент) = Неопределено Тогда
			
			Результат.Добавить(Элемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//     - для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//     Е если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//  СокращатьНепечатаемыеСимволы - Булево - сокращать непечатаемые символы по краям каждой из найденных подстрок.
//
// Возвращаемое значение:
//  Массив - массив строк.
//
// Примеры:
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",") - возвратит массив из 5 элементов, три из которых  - пустые
//  строки;
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",", Истина) - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок(" один   два  ", " ") - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок("") - возвратит пустой массив;
//  РазложитьСтрокуВМассивПодстрок("",,Ложь) - возвратит массив с одним элементом "" (пустой строкой);
//  РазложитьСтрокуВМассивПодстрок("", " ") - возвратит массив с одним элементом "" (пустой строкой);
//
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь) Экспорт
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

// Вставляет параметры в строку, учитывая, что в параметрах могут использоваться подстановочные слова %1, %2 и т.д.
Функция ПодставитьПараметрыВСтрокуАльтернативныйАлгоритм(Знач СтрокаПодстановки,
	Знач Параметр1, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено)
	
	Результат = "";
	Позиция = Найти(СтрокаПодстановки, "%");
	Пока Позиция > 0 Цикл 
		Результат = Результат + Лев(СтрокаПодстановки, Позиция - 1);
		СимволПослеПроцента = Сред(СтрокаПодстановки, Позиция + 1, 1);
		ПодставляемыйПараметр = "";
		Если СимволПослеПроцента = "1" Тогда
			ПодставляемыйПараметр =  Параметр1;
		ИначеЕсли СимволПослеПроцента = "2" Тогда
			ПодставляемыйПараметр =  Параметр2;
		ИначеЕсли СимволПослеПроцента = "3" Тогда
			ПодставляемыйПараметр =  Параметр3;
		ИначеЕсли СимволПослеПроцента = "4" Тогда
			ПодставляемыйПараметр =  Параметр4;
		ИначеЕсли СимволПослеПроцента = "5" Тогда
			ПодставляемыйПараметр =  Параметр5;
		ИначеЕсли СимволПослеПроцента = "6" Тогда
			ПодставляемыйПараметр =  Параметр6;
		ИначеЕсли СимволПослеПроцента = "7" Тогда
			ПодставляемыйПараметр =  Параметр7
		ИначеЕсли СимволПослеПроцента = "8" Тогда
			ПодставляемыйПараметр =  Параметр8;
		ИначеЕсли СимволПослеПроцента = "9" Тогда
			ПодставляемыйПараметр =  Параметр9;
		КонецЕсли;
		Если ПодставляемыйПараметр = "" Тогда
			Результат = Результат + "%";
			СтрокаПодстановки = Сред(СтрокаПодстановки, Позиция + 1);
		Иначе
			Результат = Результат + ПодставляемыйПараметр;
			СтрокаПодстановки = Сред(СтрокаПодстановки, Позиция + 2);
		КонецЕсли;
		Позиция = Найти(СтрокаПодстановки, "%");
	КонецЦикла;
	Результат = Результат + СтрокаПодстановки;
	
	Возврат Результат;
КонецФункции

// Подставляет параметры в строку. Максимально возможное число параметров - 9.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров начинается с единицы.
//
// Параметры:
//  СтрокаПодстановки  - Строка - шаблон строки с параметрами (вхождениями вида "%ИмяПараметра");
//  Параметр<n>        - Строка - подставляемый параметр.
//
// Возвращаемое значение:
//  Строка   - текстовая строка с подставленными параметрами.
//
// Пример:
//  ПодставитьПараметрыВСтроку(НСтр("ru='%1 пошел в %2'"), "Вася", "Зоопарк") = "Вася пошел в Зоопарк".
//
Функция ПодставитьПараметрыВСтроку(Знач СтрокаПодстановки,
	Знач Параметр1, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	ИспользоватьАльтернативныйАлгоритм = 
		Найти(Параметр1, "%")
		Или Найти(Параметр2, "%")
		Или Найти(Параметр3, "%")
		Или Найти(Параметр4, "%")
		Или Найти(Параметр5, "%")
		Или Найти(Параметр6, "%")
		Или Найти(Параметр7, "%")
		Или Найти(Параметр8, "%")
		Или Найти(Параметр9, "%");
		
	Если ИспользоватьАльтернативныйАлгоритм Тогда
		СтрокаПодстановки = ПодставитьПараметрыВСтрокуАльтернативныйАлгоритм(СтрокаПодстановки, Параметр1,
			Параметр2, Параметр3, Параметр4, Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	Иначе
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%1", Параметр1);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%2", Параметр2);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%3", Параметр3);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%4", Параметр4);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%5", Параметр5);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%6", Параметр6);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%7", Параметр7);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%8", Параметр8);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%9", Параметр9);
	КонецЕсли;
	
	Возврат СтрокаПодстановки;
КонецФункции

// Функция выполняет сравнение двух коллекций строк, для которых доступен обход 
// посредством оператора Для каждого … Из … Цикл.
// Сравниваемые коллекции должны отвечать следующим требованиям:
//	- доступен обход посредством оператора Для каждого … Из … Цикл,
//	- наличие в обеих коллекциях всех колонок, перечисленных в параметре ИменаКолонок 
//  (если ИменаКолонок не заполнен - всех колонок).
//
// Параметры:
//	КоллекцияСтрок1 - КоллекцияЗначений - коллекция, отвечающая вышеописанным требованиям;
//	КоллекцияСтрок2 - КоллекцияЗначений - коллекция, отвечающая вышеописанным требованиям;
//	ИменаКолонок - Строка - имена колонок через запятую, 
//						по которым производится сравнение. 
//						Не является обязательным для коллекций, 
//						состав колонок которых можно выявить: 
//						ТаблицаЗначений, СписокЗначений, Соответствие, Структура,
//						если не указан - сравнение производится по всем колонкам.
//						Для коллекций других типов является обязательным.
//	ИсключаяКолонки - Строка - имена колонок, которые игнорируются при сравнении, необязательный.
//	УчитыватьПоследовательностьСтрок - Булево - если Истина, то коллекции признаются 
//						идентичными только если одинаковые строки размещены 
//						в коллекциях на одинаковых местах.
//
// Возвращаемое значение:
//  Булево.
//
Функция КоллекцииИдентичны(КоллекцияСтрок1, КоллекцияСтрок2, ИменаКолонок = "", ИсключаяКолонки = "", УчитыватьПоследовательностьСтрок = Ложь) Экспорт
	
	// Типы коллекций, для которых доступен состав колонок, 
	// и можно выявить, если он не задан.
	ТипыОсобыхКоллекций = Новый Массив;
	ТипыОсобыхКоллекций.Добавить(Тип("ТаблицаЗначений"));
	ТипыОсобыхКоллекций.Добавить(Тип("СписокЗначений"));
	
	ТипыКоллекцийКлючИЗначение = Новый Массив;
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("Соответствие"));
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("Структура"));
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("ФиксированноеСоответствие"));
	ТипыКоллекцийКлючИЗначение.Добавить(Тип("ФиксированнаяСтруктура"));
	
	Если ПустаяСтрока(ИменаКолонок) Тогда
		Если ТипыОсобыхКоллекций.Найти(ТипЗнч(КоллекцияСтрок1)) <> Неопределено 
			Или ТипыКоллекцийКлючИЗначение.Найти(ТипЗнч(КоллекцияСтрок1)) <> Неопределено Тогда
			СравниваемыеКолонки = Новый Массив;
			Если ТипЗнч(КоллекцияСтрок1) = Тип("ТаблицаЗначений") Тогда
				Для Каждого Колонка Из КоллекцияСтрок1.Колонки Цикл
					СравниваемыеКолонки.Добавить(Колонка.Имя);
				КонецЦикла;
			ИначеЕсли ТипЗнч(КоллекцияСтрок1) = Тип("СписокЗначений") Тогда
				СравниваемыеКолонки.Добавить("Значение");
				СравниваемыеКолонки.Добавить("Картинка");
				СравниваемыеКолонки.Добавить("Пометка");
				СравниваемыеКолонки.Добавить("Представление");
			ИначеЕсли ТипыКоллекцийКлючИЗначение.Найти(ТипЗнч(КоллекцияСтрок1)) <> Неопределено Тогда
				СравниваемыеКолонки.Добавить("Ключ");
				СравниваемыеКолонки.Добавить("Значение");
			КонецЕсли;
		Иначе
			ТекстИсключения = НСтр("ru = 'Для коллекции типа %1 необходимо указать имена полей, по которым производится сравнение'");
			ВызватьИсключение ПодставитьПараметрыВСтроку(ТекстИсключения, ТипЗнч(КоллекцияСтрок1));
		КонецЕсли;
	Иначе
		СравниваемыеКолонки = РазложитьСтрокуВМассивПодстрок(ИменаКолонок,,, Истина);
	КонецЕсли;

	// Вычитаем исключаемые поля
	СравниваемыеКолонки = СократитьМассив(СравниваемыеКолонки, 
						РазложитьСтрокуВМассивПодстрок(ИсключаяКолонки,,, Истина));
						
	Если УчитыватьПоследовательностьСтрок Тогда
		
		// Параллельный обход обеих коллекций.
		НомерСтрокиКоллекции1 = 0;
		Для Каждого СтрокаКоллекции1 Из КоллекцияСтрок1 Цикл
			// Спозиционируемся на аналогичную строку второй коллекции.
			НомерСтрокиКоллекции2 = 0;
			ЕстьСтрокиКоллекции2 = Ложь;
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				ЕстьСтрокиКоллекции2 = Истина;
				Если НомерСтрокиКоллекции2 = НомерСтрокиКоллекции1 Тогда
					Прервать;
				КонецЕсли;
				НомерСтрокиКоллекции2 = НомерСтрокиКоллекции2 + 1;
			КонецЦикла;
			Если Не ЕстьСтрокиКоллекции2 Тогда
				// Во второй коллекции вообще нет строк.
				Возврат Ложь;
			КонецЕсли;
			// Сравниваем значения полей двух строк.
			Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
				Если СтрокаКоллекции1[ИмяКолонки] <> СтрокаКоллекции2[ИмяКолонки] Тогда
					Возврат Ложь;
				КонецЕсли;
			КонецЦикла;
			НомерСтрокиКоллекции1 = НомерСтрокиКоллекции1 + 1;
		КонецЦикла;
		
		КоличествоСтрокКоллекции1 = НомерСтрокиКоллекции1;
		
		// Отдельно подсчитаем количество строк второй коллекции.
		КоличествоСтрокКоллекции2 = 0;
		Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
			КоличествоСтрокКоллекции2 = КоличествоСтрокКоллекции2 + 1;
		КонецЦикла;
		
		// Если в первой коллекции не оказалось строк, 
		// то их не должно быть и во второй.
		Если КоличествоСтрокКоллекции1 = 0 Тогда
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				Возврат Ложь;
			КонецЦикла;
			КоличествоСтрокКоллекции2 = 0;
		КонецЕсли;
		
		// Количество строк не должно отличаться.
		Если КоличествоСтрокКоллекции1 <> КоличествоСтрокКоллекции2 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
	
		// Проверяем идентичность состава одинаковых строк без учета их последовательности.
		
		// Строки отбора накапливаем по первой коллекции для того, чтобы:
		//  - повторно не искать одинаковые строки,
		//  - убедиться, что во второй коллекции ни одной такой строки, которой нет в накопленных.
		
		СтрокиОтбора = Новый ТаблицаЗначений;
		ПараметрыОтбора = Новый Структура;
		Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
			СтрокиОтбора.Колонки.Добавить(ИмяКолонки);
			ПараметрыОтбора.Вставить(ИмяКолонки);
		КонецЦикла;
		
		ЕстьСтрокиКоллекции1 = Ложь;
		Для Каждого СтрокаОтбора Из КоллекцияСтрок1 Цикл
			
			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаОтбора);
			Если СтрокиОтбора.НайтиСтроки(ПараметрыОтбора).Количество() > 0 Тогда
				// Строку с такими полями уже искали.
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтрокиОтбора.Добавить(), СтрокаОтбора);
			
			// Подсчитаем количество таких строк в первой коллекции.
			НайденоСтрокКоллекции1 = 0;
			Для Каждого СтрокаКоллекции1 Из КоллекцияСтрок1 Цикл
				СтрокаПодходит = Истина;
				Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
					Если СтрокаКоллекции1[ИмяКолонки] <> СтрокаОтбора[ИмяКолонки] Тогда
						СтрокаПодходит = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если СтрокаПодходит Тогда
					НайденоСтрокКоллекции1 = НайденоСтрокКоллекции1 + 1;
				КонецЕсли;
			КонецЦикла;
			
			// Подсчитаем количество таких строк во второй коллекции.
			НайденоСтрокКоллекции2 = 0;
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				СтрокаПодходит = Истина;
				Для Каждого ИмяКолонки Из СравниваемыеКолонки Цикл
					Если СтрокаКоллекции2[ИмяКолонки] <> СтрокаОтбора[ИмяКолонки] Тогда
						СтрокаПодходит = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если СтрокаПодходит Тогда
					НайденоСтрокКоллекции2 = НайденоСтрокКоллекции2 + 1;
					// Если количество таких строк во второй коллекции превысило количество в первой, 
					// то уже можно сделать вывод, что коллекции не идентичны.
					Если НайденоСтрокКоллекции2 > НайденоСтрокКоллекции1 Тогда
						Возврат Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			// Количество таких строк не должно отличаться.
			Если НайденоСтрокКоллекции1 <> НайденоСтрокКоллекции2 Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ЕстьСтрокиКоллекции1 = Истина;
			
		КонецЦикла;
		
		// Если в первой коллекции не оказалось строк, 
		// то их не должно быть и во второй.
		Если Не ЕстьСтрокиКоллекции1 Тогда
			Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
				Возврат Ложь;
			КонецЦикла;
		КонецЕсли;
		
		// Проверим, что во второй коллекции нет ни одной такой строки, которой нет в накопленных.
		Для Каждого СтрокаКоллекции2 Из КоллекцияСтрок2 Цикл
			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаКоллекции2);
			Если СтрокиОтбора.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Сравнивает данные сложной структуры с учетом вложенности.
//
// Параметры:
//  Данные1 - Структура ,   ФиксированнаяСтруктура -
//          - Соответствие, ФиксированноеСоответствие -
//          - Массив,       ФиксированныйМассив - 
//          - ХранилищеЗначения, ТаблицаЗначений -
//          - Простые типы - которые можно сравнивать на равно,
//            например, Строка, Число, Булево.
//
//  Данные2 - Произвольный - те же типы, что и для параметра Данные1.
//
// Возвращаемое значение:
//  Булево.
//
Функция ДанныеСовпадают(Данные1, Данные2) Экспорт
	
	Если ТипЗнч(Данные1) <> ТипЗнч(Данные2) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Данные1) = Тип("Структура")
	 ИЛИ ТипЗнч(Данные1) = Тип("ФиксированнаяСтруктура") Тогда
		
		Если Данные1.Количество() <> Данные2.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Для каждого КлючИЗначение Из Данные1 Цикл
			СтароеЗначение = Неопределено;
			
			Если НЕ Данные2.Свойство(КлючИЗначение.Ключ, СтароеЗначение)
			 ИЛИ НЕ ДанныеСовпадают(КлючИЗначение.Значение, СтароеЗначение) Тогда
			
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Данные1) = Тип("Соответствие")
	      ИЛИ ТипЗнч(Данные1) = Тип("ФиксированноеСоответствие") Тогда
		
		Если Данные1.Количество() <> Данные2.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		КлючиНовогоСоответствия = Новый Соответствие;
		
		Для каждого КлючИЗначение Из Данные1 Цикл
			КлючиНовогоСоответствия.Вставить(КлючИЗначение.Ключ, Истина);
			СтароеЗначение = Данные2.Получить(КлючИЗначение.Ключ);
			
			Если НЕ ДанныеСовпадают(КлючИЗначение.Значение, СтароеЗначение) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Для каждого КлючИЗначение Из Данные2 Цикл
			Если КлючиНовогоСоответствия[КлючИЗначение.Ключ] = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Данные1) = Тип("Массив")
	      ИЛИ ТипЗнч(Данные1) = Тип("ФиксированныйМассив") Тогда
		
		Если Данные1.Количество() <> Данные2.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Индекс = Данные1.Количество()-1;
		Пока Индекс >= 0 Цикл
			Если НЕ ДанныеСовпадают(Данные1.Получить(Индекс), Данные2.Получить(Индекс)) Тогда
				Возврат Ложь;
			КонецЕсли;
			Индекс = Индекс - 1;
		КонецЦикла;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Данные1) = Тип("ТаблицаЗначений") Тогда
		
		Если Данные1.Количество() <> Данные2.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если Данные1.Колонки.Количество() <> Данные2.Колонки.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Для каждого Колонка Из Данные1.Колонки Цикл
			Если Данные2.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Индекс = Данные1.Количество()-1;
			Пока Индекс >= 0 Цикл
				Если НЕ ДанныеСовпадают(Данные1[Индекс][Колонка.Имя], Данные2[Индекс][Колонка.Имя]) Тогда
					Возврат Ложь;
				КонецЕсли;
				Индекс = Индекс - 1;
			КонецЦикла;
		КонецЦикла;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Данные1) = Тип("ХранилищеЗначения") Тогда
	
		Если НЕ ДанныеСовпадают(Данные1.Получить(), Данные2.Получить()) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Данные1 = Данные2;
	
КонецФункции

// Фиксирует данные типов Структура, Соответствие, Массив с учетом вложенности.
//
// Параметры:
//  Данные - Структура, Соответствие, Массив - коллекции, значения которых являются примитивными типами,
//           хранилищем значения или не могут быть изменены. Поддерживаются типы значений:
//           Булево, Строка, Число, Дата, Неопределено, УникальныйИдентификатор, Null, Тип,
//           ХранилищеЗначения, ОбщийМодуль, ОбъектМетаданных, ТипЗначенияXDTO, ТипОбъектаXDTO,
//           ЛюбаяСсылка.
//
//  ВызыватьИсключение - Булево - начальное значение Истина. Когда установлено.
//                       Ложь, тогда в случае наличия нефиксируемых данных исключение не будет
//                       вызвано, при этом данные будут зафиксированы на сколько возможно.
//
// Возвращаемое значение:
//  Фиксированные данные, аналогичные переданным в параметре Данные.
// 
Функция ФиксированныеДанные(Данные, ВызыватьИсключение = Истина)
	
	Если ТипЗнч(Данные) = Тип("Массив") Тогда
		Массив = Новый Массив;
		
		Индекс = Данные.Количество() - 1;
		
		Для каждого Значение Из Данные Цикл
			
			Если ТипЗнч(Значение) = Тип("Структура")
			 ИЛИ ТипЗнч(Значение) = Тип("Соответствие")
			 ИЛИ ТипЗнч(Значение) = Тип("Массив") Тогда
				
				Массив.Добавить(ФиксированныеДанные(Значение, ВызыватьИсключение));
			Иначе
				Массив.Добавить(Значение);
			КонецЕсли;
		КонецЦикла;
		
		Возврат Новый ФиксированныйМассив(Массив);
		
	ИначеЕсли ТипЗнч(Данные) = Тип("Структура")
	      ИЛИ ТипЗнч(Данные) = Тип("Соответствие") Тогда
		
		Если ТипЗнч(Данные) = Тип("Структура") Тогда
			Коллекция = Новый Структура;
		Иначе
			Коллекция = Новый Соответствие;
		КонецЕсли;
		
		Для каждого КлючИЗначение Из Данные Цикл
			Значение = КлючИЗначение.Значение;
			
			Если ТипЗнч(Значение) = Тип("Структура")
			 ИЛИ ТипЗнч(Значение) = Тип("Соответствие")
			 ИЛИ ТипЗнч(Значение) = Тип("Массив") Тогда
				
				Коллекция.Вставить(
					КлючИЗначение.Ключ, ФиксированныеДанные(Значение, ВызыватьИсключение));
			Иначе
				Коллекция.Вставить(КлючИЗначение.Ключ, Значение);
			КонецЕсли;
		КонецЦикла;
		
		Если ТипЗнч(Данные) = Тип("Структура") Тогда
			Возврат Новый ФиксированнаяСтруктура(Коллекция);
		Иначе
			Возврат Новый ФиксированноеСоответствие(Коллекция);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции


Процедура ПрочитатьГодИМесяц(Знач ДатаСтрока, ГодДаты, МесяцДаты) Экспорт
	ДатаСтрока = СокрЛП(ДатаСтрока);
	ДлинаДаты = СтрДлина(ДатаСтрока);
	Если ДлинаДаты = 9 Тогда //Д.ММ.ГГГГ
		ДатаСтрока = "0"+ДатаСтрока;
	КонецЕсли;
	Если ДлинаДаты = 4 Тогда // ГГГГ
		ГодДаты = Число(ДатаСтрока);
	ИначеЕсли ДлинаДаты = 6 Тогда // ГГГГММ
		ГодДаты = Число(ЛЕВ(ДатаСтрока,4));
		МесяцДаты = Число(ПРАВ(ДатаСтрока,2));
		Если ГодДаты < 1900 ИЛИ ГодДаты > 2050 Тогда
			ГодДаты = Число(ЛЕВ(ДатаСтрока,4));
			МесяцДаты = Число(ПРАВ(ДатаСтрока,2));
		КонецЕсли;
	Иначе
		Поз_ = Найти(ДатаСтрока,".")+Найти(ДатаСтрока,"-"); // ГГГГ.ММ.ДД ГГГГ.ММ ММ.ГГГГ ДД.ММ.ГГГГ
		Если Поз_ > 0 Тогда
			Если Поз_ = 5 Тогда // 2004.10.30 2004.10
				ГодДаты = Число(ЛЕВ(ДатаСтрока,4));
				Если СтрЧислоВхождений(".",ДатаСтрока) = 1 Тогда
					МесяцДаты = Число(ПРАВ(ДатаСтрока,2));
				Иначе
					МесяцДаты = Число(СРЕД(ДатаСтрока,6,2));
				КонецЕсли;
			ИначеЕсли Поз_ = 3 Тогда // 10.2004 30.10.2004
				ГодДаты = Число(ПРАВ(ДатаСтрока,4));
				Если СтрЧислоВхождений(ДатаСтрока,".")+СтрЧислоВхождений(ДатаСтрока,"-") = 1 Тогда
					МесяцДаты = Число(ЛЕВ(ДатаСтрока,2));
				Иначе
					МесяцДаты = Число(СРЕД(ДатаСтрока,4,2));
				КонецЕсли;
			Иначе
				// непонятно что
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьТаблицуЦенНоменклатуры(Номенклатура) Экспорт
	ТаблицаЦен = ns_Переопределяемый.ПолучитьТаблицуЦенНоменклатуры(Номенклатура);
	
	Правила = ПолучитьПравилаСкидокНаценок();
	Если НЕ Правила = Неопределено Тогда
		ПреобразоватьЦеныПоПравилам(ТаблицаЦен, Правила);	
	КонецЕсли;
	
	Возврат ТаблицаЦен;
КонецФункции

Функция ПолучитьПравилаСкидокНаценок()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_СкидкиНаценкиПравила.Правило КАК Правило,
	|	ns_СкидкиНаценкиПравила.ЗначениеПроверки КАК ЗначениеПроверки,
	|	ns_СкидкиНаценкиПравила.Размер КАК Размер
	|ИЗ
	|	Справочник.ns_СкидкиНаценки.Правила КАК ns_СкидкиНаценкиПравила
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеПроверки УБЫВ";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Возврат РезультатЗапроса.Выгрузить();
КонецФункции

Процедура ПреобразоватьЦеныПоПравилам(ТаблицаЦен, Правила)
	
	Для каждого СтрЦена Из ТаблицаЦен Цикл
		Скидка = 0;
		Для каждого СтрПравило Из Правила Цикл
			Если СтрПравило.Правило = Перечисления.ns_ПравилаСкидокНаценок.ЦенаБольше Тогда
				Если СтрЦена.Значение > СтрПравило.ЗначениеПроверки Тогда
					Скидка = СтрПравило.Размер;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если Скидка > 0 Тогда
			ЦенаСоСкидкой = СтрЦена.Значение - (СтрЦена.Значение/100 * Скидка);
			ТаблицаЦен[СтрЦена.Ключ] = Окр(ЦенаСоСкидкой);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры



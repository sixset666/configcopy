
//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СПЕЦИАЛЬНОГО НАЗНАЧЕНИЯ
//////////////////////////////////////////////////////////////////////////////////

// Функция получает имя таблицы базы данных соответствующей объекту метаданных. Используется
// для формирования текста запроса.
//
// Параметры:
//  Класс  - "Строка" - строковое представление класса объекта-источника.
//                 
//  Имя  - "Строка" - имя объекта метаданных соответствующего объекту-источнику.
//                 
//  СсылочныйТип  - "Булево" - если, объект ссылочного типа, то возвращается Истина.
//
//  ЭтоКонстанта  - "Булево" - если объект-источник соответствует константе, то возвращается Истина.
//
// Возвращаемое значение:
//   "Строка"   - таблица БД.
//
Функция ПолучитьИмяТаблицыПоМетаданным(Знач Класс, Знач Имя, СсылочныйТип=Ложь, ЭтоКонстанта=Ложь)
	СсылочныйТип = Ложь;
	ЭтоКонстанта = Ложь;
	ИмяТаблицы   = "";
	
	Если Класс="Константы" Тогда  
		ИмяТаблицы = "Константы";
		ЭтоКонстанта = Истина;   
	ИначеЕсли Класс="Справочники" Тогда  
		ИмяТаблицы   = "Справочник." + Имя;
		СсылочныйТип = Истина;
	ИначеЕсли Класс="Документы" Тогда   
		ИмяТаблицы   = "Документ." + Имя; 
		СсылочныйТип = Истина;
	ИначеЕсли Класс="ПланыВидовХарактеристик" Тогда
		ИмяТаблицы   = "ПланВидовХарактеристик." + Имя;
		СсылочныйТип = Истина;
	ИначеЕсли Класс="ПланыСчетов" Тогда
		ИмяТаблицы   = "ПланСчетов." + Имя; 
		СсылочныйТип = Истина;
	ИначеЕсли Класс="ПланыВидовРасчета" Тогда
		ИмяТаблицы   = "ПланВидовРасчета." + Имя; 
		СсылочныйТип = Истина;
	ИначеЕсли Класс="БизнесПроцессы" Тогда 
		ИмяТаблицы   = "БизнесПроцесс." + Имя;
		СсылочныйТип = Истина;
	ИначеЕсли Класс="Задачи" Тогда 
		ИмяТаблицы   = "Задача." + Имя;  	
		СсылочныйТип = Истина;
	ИначеЕсли Класс="ПланыОбмена" Тогда 		
		ИмяТаблицы   = "ПланОбмена." + Имя;
		СсылочныйТип = Истина;			
	ИначеЕсли Класс="Последовательности" Тогда 			
		ИмяТаблицы   = "Последовательность." + Имя; 	
	ИначеЕсли Класс="РегистрыСведений" Тогда
		ИмяТаблицы   = "РегистрСведений." + Имя;
	ИначеЕсли Класс="РегистрыНакопления" Тогда
		ИмяТаблицы   = "РегистрНакопления." + Имя;
	ИначеЕсли Класс="РегистрыБухгалтерии" Тогда
		ИмяТаблицы   = "РегистрБухгалтерии." + Имя;
	ИначеЕсли Класс="РегистрыРасчета" Тогда 
		ИмяТаблицы   = "РегистрРасчета." + Имя;
	КонецЕсли;
	
	Возврат ИмяТаблицы;
КонецФункции // ПолучитьИмяТаблицыПоМетаданным()

// Функция возвращает список с классами метаданных
//
// Возвращаемое значение:
//   Список значений   - список значений с классами метаданных
//  
Функция ПолучитьСписокКлассовМетаданных(ТолькоСсылочныеКлассы=Ложь) Экспорт
	
	спКлассы = Новый СписокЗначений;
	Если НЕ ТолькоСсылочныеКлассы Тогда
		спКлассы.Добавить("Константы",               "Константы");
	КонецЕсли;
	спКлассы.Добавить("Справочники",             "Справочники");
	спКлассы.Добавить("Документы",               "Документы");
	Если НЕ ТолькоСсылочныеКлассы Тогда
		спКлассы.Добавить("Последовательности",      "Последовательности");
	КонецЕсли;
	спКлассы.Добавить("ПланыВидовХарактеристик", "Планы видов характеристик");
	спКлассы.Добавить("ПланыСчетов",             "Планы счетов");
	спКлассы.Добавить("ПланыВидовРасчета",       "Планы видов расчета");
	Если НЕ ТолькоСсылочныеКлассы Тогда	
		спКлассы.Добавить("РегистрыСведений",        "Регистры сведений");
		спКлассы.Добавить("РегистрыНакопления",      "Регистры накопления");
		спКлассы.Добавить("РегистрыБухгалтерии",     "Регистры бухгалтерии");
		спКлассы.Добавить("РегистрыРасчета",         "Регистры расчета");	
	КонецЕсли;
	спКлассы.Добавить("БизнесПроцессы",          "Бизнес-процессы");
	спКлассы.Добавить("Задачи",                  "Задачи");
	спКлассы.Добавить("ПланыОбмена",             "Планы обмена");	
	
	Возврат спКлассы;
КонецФункции // ПолучитьСписокКлассовМетаданных()  

// Функция позволяет определить для объекта метаданных класс, к которому он принадлежит.
//
// Параметры:
//  ОбъектМетаданных  - "ОбъектМетаданных", либо "Строка", либо "Тип" - объект метаданных, для которого
//					необходимо определить класс. Если тип значения не "ОбъектМетаданных", тогда предварительно
//					ищется соответствующий объект метаданных (по типу, либо по полному имени)
//                 
//  ВозврОбъект  - "ОбъектМетаданных" - возвращает найденный объект метаданных
//                 Используется, если тип значения первого параметра не "ОбъектМетаданных". 
//
// Возвращаемое значение:
//   "Строка     - имя класса объекта метаданных
//
Функция ПолучитьКлассОбъектаМетаданных(Знач ОбъектМетаданных, ВозврОбъект=Неопределено) Экспорт
	
	// Если переданный параметр не "ОбъектМетаданных", тогда найдем его.
	Если ТипЗнч(ОбъектМетаданных)=Тип("Строка") Тогда
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ОбъектМетаданных); 		
	ИначеЕсли ТипЗнч(ОбъектМетаданных)=Тип("Тип") Тогда
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ОбъектМетаданных); 
	КонецЕсли;
	
	Если ОбъектМетаданных=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВозврОбъект = ОбъектМетаданных;
	
	Класс = "";
	спКлассы = ПолучитьСписокКлассовМетаданных();   // Получили список всевозможных классов объектов
	Для Каждого ТекКласс Из спКлассы Цикл  	
		Попытка
			Результат = Метаданные[ТекКласс.Значение].Содержит(ОбъектМетаданных);
		Исключение
			Возврат Неопределено;
		КонецПопытки;
		
		Если Результат Тогда
			Возврат ТекКласс.Значение;	
		КонецЕсли;
	КонецЦикла;
		
КонецФункции // ПолучитьКлассОбъектаМетаданных() 

// Функция получает список возможных регистрируемых событий для конкретного класса объектов метаданных
//
// Параметры:
//  КлассОбъекта  - "Строка" - имя класса объектов метаданных
//                
// Возвращаемое значение:
//   "СписокЗначений"   - список значений с событиями.
//
Функция ПолучитьСписокВозможныхСобытий(КлассОбъекта) Экспорт
	
	спСобытия = Новый СписокЗначений;
	спСобытия.Добавить("ПриЗаписи");
	
	Если КлассОбъекта="Константы" Тогда 		
				
	ИначеЕсли КлассОбъекта="Справочники" Тогда
		спСобытия.Добавить("ПередУдалением");
				
	ИначеЕсли КлассОбъекта="Документы" Тогда
		спСобытия.Добавить("ОбработкаПроведения");
		спСобытия.Добавить("ОбработкаУдаленияПроведения");
		спСобытия.Добавить("ПередУдалением");
		
	ИначеЕсли КлассОбъекта="Последовательности" Тогда
		//спСобытия.Добавить("ПередЗаписью");
		
	ИначеЕсли КлассОбъекта="ПланыВидовХарактеристик" Тогда
		спСобытия.Добавить("ПередУдалением");
		
	ИначеЕсли КлассОбъекта="ПланыСчетов" Тогда
		спСобытия.Добавить("ПередУдалением");
		
	ИначеЕсли КлассОбъекта="ПланыВидовРасчета" Тогда
		спСобытия.Добавить("ПередУдалением");

	ИначеЕсли КлассОбъекта="РегистрыСведений" Тогда 		
				
	ИначеЕсли КлассОбъекта="РегистрыНакопления" Тогда
				
	ИначеЕсли КлассОбъекта="РегистрыБухгалтерии" Тогда
			
	ИначеЕсли КлассОбъекта="РегистрыРасчета" Тогда
				
	ИначеЕсли КлассОбъекта="БизнесПроцессы" Тогда
		спСобытия.Добавить("ПередУдалением");

	ИначеЕсли КлассОбъекта="Задачи" Тогда
		спСобытия.Добавить("ОбработкаИнтерактивнойАктивации");
		спСобытия.Добавить("ОбработкаПроверкиВыполнения");
		спСобытия.Добавить("ПередВыполнением");
       	спСобытия.Добавить("ПередИнтерактивнымВыполнением");
		спСобытия.Добавить("ПередУдалением");
        спСобытия.Добавить("ПриВыполнении");
		
	ИначеЕсли КлассОбъекта="ПланыОбмена" Тогда
		спСобытия.Добавить("ПередУдалением");
		спСобытия.Добавить("ПриОтправкеДанныхГлавному");
		спСобытия.Добавить("ПриОтправкеДанныхПодчиненному");
        спСобытия.Добавить("ПриОтправкеДанныхУзлаПодчиненному");
		спСобытия.Добавить("ПриПолученииДанныхОтГлавного");
		спСобытия.Добавить("ПриПолученииДанныхОтПодчиненного");
        спСобытия.Добавить("ПриПолученииДанныхУзлаОтГлавного");    
	КонецЕсли;  	
	
	Возврат спСобытия;
КонецФункции // ПолучитьСписокВозможныхСобытий() 

// Функция формирует дерево доступных реквизитов объекта метаданных для выбора.
// Для формирования используется построитель запроса.	
//
// Параметры:
//  Источник  - "Произвольный объект" - объект конфигурации, для которого необходимо сформировать дерево.
//                 
//  ОграниченияНаПоля  - "Структура" - используется для наложения ограничений на формируемое дерево 
//
//  ИсключаемыеКлассы  - "Строка" - через запятую перечисляются имена классов объектов метаданных, для
//									которых строить дерево не нужно. 
// Возвращаемое значение:
//   "ДеревоЗначений"     - заполненное доступными полями для выбора дерево значений
//  
Функция ПолучитьДеревоДоступныхРеквизитовДляВыбора(Источник, ОграниченияНаПоля=Неопределено, ИсключаемыеКлассы=Неопределено)  Экспорт
	
	// Сначала получим класса объекта
	ОбъектМетаданных = Неопределено;
	Класс = ПолучитьКлассОбъектаМетаданных(Источник, ОбъектМетаданных);
	Если Класс=Неопределено Тогда // Имеем дело с коллекцией
		Класс = Источник;
	КонецЕсли;
	
	ДеревоПолей = Новый ДеревоЗначений;
	ДеревоПолей.Колонки.Добавить("Имя");
	ДеревоПолей.Колонки.Добавить("ПолноеИмя");
	ДеревоПолей.Колонки.Добавить("ТипЗначения");
	ДеревоПолей.Колонки.Добавить("Представление");
	ДеревоПолей.Колонки.Добавить("ЭтоТЧ", Новый ОписаниеТипов("Булево"));
	
	Если ОграниченияНаПоля=Неопределено Тогда
		ОграниченияНаПоля = Новый Структура;
	КонецЕсли;
	
	// Теперь установим ограничения на формируемое дерево
	Если НЕ ОграниченияНаПоля.Свойство("ОграничениеТипов") Тогда 
		// В дерево попадут только поля, тип которых соответствует передаваемому Описанию Типов
		
		мсвТипов = Новый Массив;
		мсвТипов.Добавить(Тип("Число"));
		мсвТипов.Добавить(Тип("Строка"));
		мсвТипов.Добавить(Тип("Булево"));
		мсвТипов.Добавить(Тип("Дата"));
		мсвТипов.Добавить(Тип("Строка"));		
		
		Для Каждого ТекТип Из Справочники.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;
		Для Каждого ТекТип Из Документы.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;
		Для Каждого ТекТип Из Перечисления.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;
		Для Каждого ТекТип Из ПланыВидовХарактеристик.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;
		Для Каждого ТекТип Из ПланыСчетов.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;   
		Для Каждого ТекТип Из ПланыВидовРасчета.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;
		Для Каждого ТекТип Из ПланыОбмена.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;
		Для Каждого ТекТип Из БизнесПроцессы.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;
		Для Каждого ТекТип Из Задачи.ТипВсеСсылки().Типы() Цикл  	
			мсвТипов.Добавить(ТекТип);		 	
		КонецЦикла;   		
		
		ОграниченияНаПоля.Вставить("ОграничениеТипов",  Новый ОписаниеТипов(мсвТипов));
	КонецЕсли;
	
	Если НЕ ОграниченияНаПоля.Свойство("ВключатьТЧ") Тогда
		// Включать/Не включать табличные части в дерево.
		ОграниченияНаПоля.Вставить("ВключатьТЧ", Истина);
	КонецЕсли;
	Если НЕ ОграниченияНаПоля.Свойство("МаксУровень") Тогда
		// Ограничивает максимальный уровень формируемого дерева.
		ОграниченияНаПоля.Вставить("МаксУровень", 1);
	КонецЕсли;
	
	// Если для класса объекта метаданных строить дерево не надо (т.е. он находится в исключении), 
	// то сразу вернем Неопределено
	Если ИсключаемыеКлассы<>Неопределено Тогда
		Если Найти(ИсключаемыеКлассы, Класс)<>0 Тогда
			Возврат Неопределено;	
		КонецЕсли;
	КонецЕсли;
	
	// Формируем дерево с доступными полями для выбора, используя построитель запроса.
	// Для каждого класса объектов метаданных, построитель запроса заполняется по-разному.
	Если Класс="Константы" Тогда
		Возврат ПолучитьСписокДоступныхПолейКонстанты(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="Справочники" Тогда	
		Возврат ПолучитьСписокДоступныхПолейСправочника(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="Документы" Тогда	
		Возврат ПолучитьСписокДоступныхПолейДокумента(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="Последовательности" Тогда	
		Возврат ПолучитьСписокДоступныхПолейПоследовательности(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="ПланыВидовХарактеристик" Тогда	
		Возврат ПолучитьСписокДоступныхПолейПВХ(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="ПланыСчетов" Тогда	
		Возврат ПолучитьСписокДоступныхПолейПланыСчетов(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="ПланыВидовРасчета" Тогда	
		Возврат ПолучитьСписокДоступныхПолейПланаВидовРасчета(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="РегистрыСведений" Тогда	
		Возврат ПолучитьСписокДоступныхПолейРегистраСведений(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="РегистрыНакопления" Тогда	
		Возврат ПолучитьСписокДоступныхПолейРегистраНакопления(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="РегистрыБухгалтерии" Тогда	
		Возврат ПолучитьСписокДоступныхПолейРегистраБухгалтерии(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="РегистрыРасчета" Тогда	
		Возврат ПолучитьСписокДоступныхПолейРегистраРасчета(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="БизнесПроцессы" Тогда	
		Возврат ПолучитьСписокДоступныхПолейБизнесПроцесса(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="Задачи" Тогда
		Возврат ПолучитьСписокДоступныхПолейЗадачи(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля);
		
	ИначеЕсли Класс="ПланыОбмена" Тогда	
		Возврат ПолучитьСписокДоступныхПолейПланаОбмена(ОбъектМетаданных, ДеревоПолей, ОграниченияНаПоля); 		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьДеревоДоступныхРеквизитовДляВыбора() 

// Рекурсивная функция. По построителю запроса строит соответствующее дерево, учитывая налагаемые ограничения
//
// Параметры:
//  ДоступныеПоля  - "ДоступныеПоля" - доступные поля на текущем уровне построителя.
//
//  ТекСтрока  - "СтрокаДереваЗначений" - текущая строка дерева значений.
//
//  Ограничения  - "Структура" - ограничения на строящееся дерево.
// 
Процедура СформироватьДЗПоПостроителюЗапроса(ДоступныеПоля, ТекСтрока, Ограничения=Неопределено)	
	// Если ограничения не были заданы "выше", тогда используем ограничения по-умолчанию
	Если Ограничения=Неопределено Тогда
		Ограничения = Новый Структура;
		Ограничения.Вставить("ОграничениеТипов",  Неопределено);
		Ограничения.Вставить("ВключатьТЧ",        Истина);
		Ограничения.Вставить("МаксУровень",       1);
	КонецЕсли;
	
	// Если текущая строка не задана, значит находимся на верхнем уровне.
	// Создаем дерево значений.
	Если ТекСтрока=Неопределено Тогда
		ТекСтрока = Новый ДеревоЗначений;
		ТекСтрока.Колонки.Добавить("Имя");
		ТекСтрока.Колонки.Добавить("ПолноеИмя");
		ТекСтрока.Колонки.Добавить("ТипЗначения");
		ТекСтрока.Колонки.Добавить("Представление");
	КонецЕсли;
	
	Для Каждого ТекПоле Из ДоступныеПоля Цикл		
		// Отсеем лишнее
		мсвОтсеянныеТипы = Новый Массив;
		Для Каждого ТекТип Из ТекПоле.ТипЗначения.Типы() Цикл 					
			Если Ограничения.ВключатьТЧ Тогда
				Если Найти(НРег(Строка(ТекТип)), "табличная часть")=0 Тогда 
					// Если это не табличная часть, то будем проверять на ограничение по типу
					Если Ограничения.ОграничениеТипов<>Неопределено Тогда
						Если НЕ Ограничения.ОграничениеТипов.СодержитТип(ТекТип) Тогда
							Продолжить;
						КонецЕсли;				
					КонецЕсли; 
				КонецЕсли;		
			Иначе
				// Поскольку табличные части нам не нужны будем проверять на ограничение по типу сразу. 
				// Если Ограничение типа - определено, то ТЧ-и пролетят, т.к. туда не входят.
				Если Ограничения.ОграничениеТипов<>Неопределено Тогда
					Если НЕ Ограничения.ОграничениеТипов.СодержитТип(ТекТип) Тогда
						Продолжить;
					КонецЕсли;
				Иначе
					Если Найти(НРег(Строка(ТекТип)), "табличная часть")<>0 Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			мсвОтсеянныеТипы.Добавить(ТекТип);
		КонецЦикла;
		
		Если мсвОтсеянныеТипы.Количество()=0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ТекПоле.ТипЗначения = Новый ОписаниеТипов(мсвОтсеянныеТипы);
		
		// Добавляем новую строку в дерево значений, в соответствующую ветку
		НоваяСтрока = ТекСтрока.Строки.Добавить();
		НоваяСтрока.Имя           = ТекПоле.Имя;
		НоваяСтрока.ПолноеИмя     = ТекПоле.ПутьКДанным;
		НоваяСтрока.ТипЗначения   = ТекПоле.ТипЗначения;   		
		НоваяСтрока.Представление = ТекПоле.Представление; 
		
		Если Найти(НРег(Строка(ТекТип)), "табличная часть")<>0 Тогда
			НоваяСтрока.ЭтоТЧ = Истина;	
		Иначе
			НоваяСтрока.ЭтоТЧ = Ложь;
		КонецЕсли;
		
		Если НоваяСтрока.Уровень()<=Ограничения.МаксУровень Тогда			
			СформироватьДЗПоПостроителюЗапроса(ТекПоле.Поля, НоваяСтрока, Ограничения);			
		КонецЕсли;
	КонецЦикла;	 
	
	ТекСтрока.Строки.Сортировать("Представление");
КонецПроцедуры // СформироватьДЗПоПостроителюЗапроса()


#Если Клиент Тогда	
// Функция для интерактивного выбора значения реквизита.
// Если реквизит составного типа, то сначала предлагается выбрать тип, а затем значение.
//
// Параметры:
//  мсвТипы  - "Массив" - массив типов реквизита. Если количество элементов массива равно одному, то
// 						  выбор типа не производится.
//                 
//  ЭтаФорма  - "Форма" - форма, на которой расположен реквизит.
//
//  Элемент  - "ЭлементФормы" - элемент формы, с помощью которого редактируется значение реквизита.
// 
//  СтандартнаяОбработка  - "Булево" - признак выполнения стандартной обработки.
// 
Процедура ВыбратьЗначениеРеквизитаТабличнойЧасти(мсвТипы, ЭтаФорма, Элемент, СтандартнаяОбработка, ПутьКДанным="") Экспорт
	
	// Формируем список значений с возможными типами реквизита для выбора.
	спТипы = Новый СписокЗначений;
	Для Каждого ТекТип Из мсвТипы Цикл
		Попытка
			ПустоеЗначение = Новый (ТекТип);
			ОбМетаданные   = ПустоеЗначение.Метаданные();
			спТипы.Добавить(Новый Структура("обМетаданные, Тип",  ОбМетаданные, ТекТип), ПустоеЗначение.Метаданные().Синоним);
		Исключение
			спТипы.Добавить(Новый Структура("Имя, Тип",  "", ТекТип), ТекТип);
		КонецПопытки;	
	КонецЦикла;	
	
	спТипы.СортироватьПоПредставлению();
	
	// выбираем вид
	ВыбВид=Неопределено;
	Если спТипы.Количество()>1 Тогда
		ВыбВид = ЭтаФорма.ВыбратьИзСписка(спТипы, Элемент);
	Иначе
		ВыбВид = спТипы[0];
	КонецЕсли;
	
	// проверим что выбрали
	Если ВыбВид=Неопределено Тогда 
		Возврат; 
	КонецЕсли;
	
	ТипВыбранногоЭлемента = Неопределено;
		
	Если Документы.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		ФормаВыбора = Документы[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		ТипВыбранногоЭлемента = Документы[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();		
		
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		Если (ПутьКДанным="Ссылка.Родитель") И (ВыбВид.Значение.обМетаданные.ВидИерархии=Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов) Тогда
			ФормаВыбора = Справочники[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбораГруппы(,Элемент,);	
		Иначе
			ФормаВыбора = Справочники[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		КонецЕсли;
		
		// Здесь посложнее. Возможно справочник является подчиненным.		
		Владельцы = ВыбВид.Значение.обМетаданные.Владельцы;
		Если Владельцы.Количество()<>0 Тогда 						
			ЭлементВладелец = ВыполнитьВыборВладельца(Владельцы, ЭтаФорма, Элемент);
			Если ЭлементВладелец=Неопределено Тогда
				Возврат;
			КонецЕсли;  			
			ФормаВыбора.ПараметрОтборПоВладельцу = ЭлементВладелец;
		КонецЕсли;
				
		ТипВыбранногоЭлемента = Справочники[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();
				
	ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		ФормаВыбора = ПланыВидовХарактеристик[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		Если (ПутьКДанным="Ссылка.Родитель") И (ВыбВид.Значение.обМетаданные.Иерархический) Тогда
			ФормаВыбора = ПланыВидовХарактеристик[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбораГруппы(,Элемент,);	
		Иначе
			ФормаВыбора = ПланыВидовХарактеристик[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		КонецЕсли;

		ТипВыбранногоЭлемента = ПланыВидовХарактеристик[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();
		
	ИначеЕсли ПланыСчетов.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		ФормаВыбора = ПланыСчетов[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		ТипВыбранногоЭлемента = ПланыСчетов[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();
		
	ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		ФормаВыбора = ПланыВидовРасчета[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		ТипВыбранногоЭлемента = ПланыВидовРасчета[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();
		
	ИначеЕсли БизнесПроцессы.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		ФормаВыбора = БизнесПроцессы[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		ТипВыбранногоЭлемента = БизнесПроцессы[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();
		
	ИначеЕсли Задачи.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		ФормаВыбора = Задачи[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		ТипВыбранногоЭлемента = Задачи[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();
		
	ИначеЕсли ПланыОбмена.ТипВсеСсылки().СодержитТип(ВыбВид.Значение.Тип) Тогда
		ФормаВыбора = ПланыОбмена[ВыбВид.Значение.обМетаданные.Имя].ПолучитьФормуВыбора(,Элемент,);
		ТипВыбранногоЭлемента = ПланыОбмена[ВыбВид.Значение.обМетаданные.Имя].ПустаяСсылка();      
		
	ИначеЕсли ВыбВид.Значение.Тип=Тип("Строка") Тогда
		Если ТипЗнч(Элемент.Значение)<>Тип("Строка") 
			Тогда Элемент.Значение=""; 
		КонецЕсли; 
		СтандартнаяОбработка = Истина;
		Возврат;
		
	ИначеЕсли ВыбВид.Значение.Тип=Тип("Число") Тогда
		Если ТипЗнч(Элемент.Значение)<>Тип("Число") Тогда 
			Элемент.Значение=0.00;
		КонецЕсли;
		СтандартнаяОбработка = Истина;   
		Возврат;
		
	ИначеЕсли ВыбВид.Значение.Тип=Тип("Дата") Тогда
		Если ТипЗнч(Элемент.Значение)<>Тип("Дата") Тогда
			Элемент.Значение='00010101'; 
		КонецЕсли; 
		СтандартнаяОбработка = Истина;
		Возврат;
		
	ИначеЕсли ВыбВид.Значение.Тип=Тип("Булево") Тогда
		Если ТипЗнч(Элемент.Значение)<>Тип("Булево") Тогда
			Элемент.Значение=Ложь; 					
		КонецЕсли; 
		СтандартнаяОбработка = Истина;   
		Возврат;
		
	Иначе
		Попытка
			//Попробуем выбрать значение перечисления
			СписокЗначенийПеречисления = Новый СписокЗначений;
			Для Каждого ЗначениеПеречисления Из Перечисления[ВыбВид.Значение.обМетаданные.Имя] Цикл
				СписокЗначенийПеречисления.Добавить(ЗначениеПеречисления);
			КонецЦикла; 
			ВыбЗначение = ЭтаФорма.ВыбратьИзСписка(СписокЗначенийПеречисления,Элемент);
			Если ВыбЗначение<>Неопределено Тогда
				Элемент.Значение=ВыбЗначение.Значение;
			КонецЕсли;
			Возврат;
		Исключение
			Возврат;
		КонецПопытки; 		
	КонецЕсли; 			
	
	// открываем формы выбора
	Если ТипЗнч(ТипВыбранногоЭлемента)=ТипЗнч(Элемент.Значение) Тогда
		ФормаВыбора.НачальноеЗначениеВыбора=Элемент.Значение;
	КонецЕсли; 
	
	// Открываем форму в режиме выбора.
	ФормаВыбора.Открыть();		
КонецПроцедуры // ВыбратьЗначениеРеквизитаТабличнойЧасти()

// Функция используется в случае, когда выбирается значение реквизита, являющегося ссылкой на
// элемент ПОДЧИНЕННОГО справочника. В этом случае предлагается выбрать вид владельца, затем значение владельца.
//
// Параметры:
//  Владельцы  - "КоллекцияОбъектовМетаданных" - коллекция объектов метаданных (владельцы справочника)
//
//  ЭтаФорма  - "Форма" - форма.
//
//  ЭлементФормы  - "ЭлементФормы" - элемент формы, в котором производится выбор значения.
//
// Возвращаемое значение:
//   возвращает элемент-владелец (может быть элемент справочника, элемент ПВХ и т.д.)
//
Функция ВыполнитьВыборВладельца(Владельцы, ЭтаФорма, ЭлементФормы)

	// Предлагаем выбрать вид владельца
	спВладельцы = Новый СписокЗначений; 	
	Для Каждого ТекВладелец Из Владельцы Цикл
		спВладельцы.Добавить(ТекВладелец, ТекВладелец.Представление());		
	КонецЦикла;			
	Если спВладельцы.Количество()>1 Тогда
		ВыбВладелец = ЭтаФорма.ВыбратьИзСписка(спВладельцы, ЭлементФормы);
		Если ВыбВладелец=Неопределено Тогда
			Возврат Неопределено;	
		КонецЕсли;
	Иначе
		ВыбВладелец = спВладельцы[0];
	КонецЕсли;
	
	ОбъектМетаданных = ВыбВладелец.Значение;
	Класс = ПолучитьКлассОбъектаМетаданных(ОбъектМетаданных);
	
	// Получаем нужную форму для выбора, в зависимости от выбранного ранее вида.
	Если Класс="Справочники" Тогда 						
		ФормаВыбора = Справочники[ОбъектМетаданных.Имя].ПолучитьФормуВыбора();
				
	ИначеЕсли Класс="ПланыВидовХарактеристик" Тогда
		ФормаВыбора = ПланыВидовХарактеристик[ОбъектМетаданных.Имя].ПолучитьФормуВыбора();
				
	ИначеЕсли Класс="ПланыСчетов" Тогда
		ФормаВыбора = ПланыСчетов[ОбъектМетаданных.Имя].ПолучитьФормуВыбора();
				
	ИначеЕсли Класс="ПланыВидовРасчета" Тогда
		ФормаВыбора = ПланыВидовРасчета[ОбъектМетаданных.Имя].ПолучитьФормуВыбора();
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
	
	ФормаВыбора.Заголовок = "Выбор владельца (" + ОбъектМетаданных.Представление() + ")";
	
	// Открываем форму для выбора модально.
	Возврат ФормаВыбора.ОткрытьМодально();
КонецФункции // ВыполнитьВыборВладельца()   
#КонецЕсли

// Функция формирует текст для построителя запроса.
//
// Параметры:
//  ТаблицаБазы  - "Строка" - имя таблицы базы данных
//
//  мсвПоляВыбора  - "Массив" - массив имен полей таблицы БД, которые нужно получить.
//
// Возвращаемое значение:
//   "Строка"   - сформированный текст для построителя запроса
//
Функция СформироватьТекстЗапросаДляПостроителяЗапроса(ТаблицаБазы, мсвПоляВыбора)
	Текст = "ВЫБРАТЬ 1	{ВЫБРАТЬ ";
	
	РазмерМассива = мсвПоляВыбора.Количество()-1;
	Для Сч=0 По РазмерМассива Цикл 
		Текст = Текст + "" + мсвПоляВыбора[Сч] + ".*" + ?(Сч=РазмерМассива, "", ", ");	
	КонецЦикла; 	
	
	Текст = Текст + "}
	|ИЗ
	|	" + ТаблицаБазы;
	
	Возврат Текст;
КонецФункции // СформироватьТекстЗапросаДляПостроителяЗапроса()

// Функция создает пустой объект по передаваемому объекту метаданных.
//
// Параметры:
//  Источник  - "ОбъектМетаднных", "Строка" - объект метаданных (либо его полное имя), для
// 											  которого необходимо создать объект.
//
// Возвращаемое значение:
//   Пустой объект. Тип объекта зависит от передаваемого объекта метаданных
//   Для не ссылочных типов данных (регистров и последовательностей) создаются соответствующие наборы записей.
//   Для констант создаются соответствующие менеджеры значения.
//
Функция СоздатьПустоеЗначениеПоОбъектуМетаданных(Источник) Экспорт
	Если ТипЗнч(Источник)<>Тип("ОбъектМетаданных") Тогда
		ОбъектМетаданные = Метаданные.НайтиПоПолномуИмени(Источник);
	Иначе
		ОбъектМетаданные = Источник;
	КонецЕсли;
	
	Если ОбъектМетаданные=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	ИмяОбъекта = ОбъектМетаданные.Имя;	
	Класс      = ПолучитьКлассОбъектаМетаданных(ОбъектМетаданные);
		
	Если Класс="Константы" Тогда
		Возврат Константы[ИмяОбъекта].СоздатьМенеджерЗначения();
		
	ИначеЕсли Класс="Справочники" Тогда	
		Возврат Справочники[ИмяОбъекта].СоздатьЭлемент();
		
	ИначеЕсли Класс="Документы" Тогда	
		Возврат Документы[ИмяОбъекта].СоздатьДокумент();;
		
	ИначеЕсли Класс="Последовательности" Тогда
		Возврат Неопределено;
		//Возврат Последовательности[ИмяОбъекта].СоздатьНаборЗаписей();
		
	ИначеЕсли Класс="ПланыВидовХарактеристик" Тогда	
		Возврат ПланыВидовХарактеристик[ИмяОбъекта].СоздатьЭлемент();
		
	ИначеЕсли Класс="ПланыСчетов" Тогда	
		Возврат ПланыСчетов[ИмяОбъекта].СоздатьСчет();;
		
	ИначеЕсли Класс="ПланыВидовРасчета" Тогда	
		Возврат ПланыВидовРасчета[ИмяОбъекта].СоздатьВидРасчета();;
		
	ИначеЕсли Класс="РегистрыСведений" Тогда	
		Возврат РегистрыСведений[ИмяОбъекта].СоздатьНаборЗаписей();
		
	ИначеЕсли Класс="РегистрыНакопления" Тогда	
		Возврат РегистрыНакопления[ИмяОбъекта].СоздатьНаборЗаписей();
		
	ИначеЕсли Класс="РегистрыБухгалтерии" Тогда	
		Возврат РегистрыБухгалтерии[ИмяОбъекта].СоздатьНаборЗаписей();
		
	ИначеЕсли Класс="РегистрыРасчета" Тогда	
		Возврат РегистрыРасчета[ИмяОбъекта].СоздатьНаборЗаписей();
		
	ИначеЕсли Класс="БизнесПроцессы" Тогда	
		Возврат БизнесПроцессы[ИмяОбъекта].СоздатьБизнесПроцесс();
		
	ИначеЕсли Класс="Задачи" Тогда
		Возврат Задачи[ИмяОбъекта].СоздатьЗадачу();
		
	ИначеЕсли Класс="ПланыОбмена" Тогда	
		Возврат ПланыОбмена[ИмяОбъекта].СоздатьУзел(); 		
	Иначе
		Возврат Неопределено;
	КонецЕсли; 	
	
КонецФункции // СоздатьПустоеЗначениеПоОбъектуМетаданных() 


//////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ПОЛУЧЕНИЯ СПИСКА ДОСТУПНЫХ ПОЛЕЙ МЕТАДАННЫХ
//////////////////////////////////////////////////////////////////////////////////

// Функция возвращает список доступных полей Константы
//
Функция ПолучитьСписокДоступныхПолейКонстанты(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "Константы".
	// У констант нет общих полей, поэтому дерево будет пустое
	Если ОбъектМетаданных=Неопределено Тогда
		ДеревоДоступныхПолей = Неопределено;	
	Иначе
		Текст = "ВЫБРАТЬ
		|	Константы." + ОбъектМетаданных.Имя + "
		|{ВЫБРАТЬ
		|	" + ОбъектМетаданных.Имя + ".*}
		|ИЗ
		|	Константы КАК Константы";
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля, ДеревоДоступныхПолей, Ограничения);
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей;
КонецФункции // ПолучитьСписокДоступныхПолейКонстанты() 

// Функция возвращает список доступных полей Справочника
//
Функция ПолучитьСписокДоступныхПолейСправочника(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "Справочники".
	// Поэтому дерево доступных полей будет состоять только из общих свойств	
	Если ОбъектМетаданных=Неопределено Тогда
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Наименование";
		НоваяСтрока.ПолноеИмя     = "Наименование";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Строка");
		НоваяСтрока.Представление = "Наименование";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Предопределенный";
		НоваяСтрока.ПолноеИмя     = "Предопределенный";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Предопределенный";
					
	Иначе
		Текст = 		
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	Справочник." + ОбъектМетаданных.Имя + "";
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("СправочникСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 	
КонецФункции // ПолучитьСписокДоступныхПолейСправочника() 

// Функция возвращает список доступных полей Документа
//
Функция ПолучитьСписокДоступныхПолейДокумента(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "Документы".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.
	Если ОбъектМетаданных=Неопределено Тогда
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Дата";
		НоваяСтрока.ПолноеИмя     = "Дата";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Дата");
		НоваяСтрока.Представление = "Дата";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Проведен";
		НоваяСтрока.ПолноеИмя     = "Проведен";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Проведен"; 		
			
	Иначе
		Текст = 		
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	Документ." + ОбъектМетаданных.Имя + "";
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("ДокументСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 		
	
КонецФункции // ПолучитьСписокДоступныхПолейДокумента()

// Функция возвращает список доступных полей Последовательности
//
Функция ПолучитьСписокДоступныхПолейПоследовательности(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "Последовательности".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.
	Если ОбъектМетаданных=Неопределено Тогда
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Период";
		НоваяСтрока.ПолноеИмя     = "Период";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Дата");
		НоваяСтрока.Представление = "Период";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Регистратор";
		НоваяСтрока.ПолноеИмя     = "Регистратор";
		НоваяСтрока.ТипЗначения   = Документы.ТипВсеСсылки();
		НоваяСтрока.Представление = "Регистратор";		
		
	Иначе	
		
		// Получим список измерений 		
		мсвИзмерений = Новый Массив;		
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Измерения Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		мсвИзмерений.Добавить("Период");
		мсвИзмерений.Добавить("Регистратор");
		
		// Сформируем текст запроса для построителя
		Текст = СформироватьТекстЗапросаДляПостроителяЗапроса("Последовательность."+ОбъектМетаданных.Имя, мсвИзмерений);
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля, ДеревоДоступныхПолей, Ограничения);
		        
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 	
КонецФункции // ПолучитьСписокДоступныхПолейПоследовательности()

// Функция возвращает список доступных полей ПВХ
//
Функция ПолучитьСписокДоступныхПолейПВХ(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "ПланыВидовХарактеристик".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.
	Если ОбъектМетаданных=Неопределено Тогда 		
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Наименование";
		НоваяСтрока.ПолноеИмя     = "Наименование";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Строка");
		НоваяСтрока.Представление = "Наименование";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Предопределенный";
		НоваяСтрока.ПолноеИмя     = "Предопределенный";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Предопределенный";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ЭтоГруппа";
		НоваяСтрока.ПолноеИмя     = "ЭтоГруппа";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Это группа"; 
		
	Иначе
		Текст =	
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	ПланВидовХарактеристик." + ОбъектМетаданных.Имя + "";
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 	
КонецФункции // ПолучитьСписокДоступныхПолейПВХ()

// Функция возвращает список доступных полей ПланаСчетов
//
Функция ПолучитьСписокДоступныхПолейПланыСчетов(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "ПланыСчетов".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.
	Если ОбъектМетаданных=Неопределено Тогда 		
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Наименование";
		НоваяСтрока.ПолноеИмя     = "Наименование";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Строка");
		НоваяСтрока.Представление = "Наименование";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Предопределенный";
		НоваяСтрока.ПолноеИмя     = "Предопределенный";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Предопределенный";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Забалансовый";
		НоваяСтрока.ПолноеИмя     = "Забалансовый";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Забалансовый";   		
	Иначе
		Текст =	
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	ПланСчетов." + ОбъектМетаданных.Имя + "";
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("ПланСчетовСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 	
КонецФункции // ПолучитьСписокДоступныхПолейПланыСчетов()

// Функция возвращает список доступных полей ВидовРасчета
//
Функция ПолучитьСписокДоступныхПолейПланаВидовРасчета(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "ПланыВидовРасчета".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.
	Если ОбъектМетаданных=Неопределено Тогда 		
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Наименование";
		НоваяСтрока.ПолноеИмя     = "Наименование";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Строка");
		НоваяСтрока.Представление = "Наименование";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Предопределенный";
		НоваяСтрока.ПолноеИмя     = "Предопределенный";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Предопределенный";  	   		
	Иначе
		Текст =	
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	ПланВидовРасчета." + ОбъектМетаданных.Имя + "";
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("ПланВидовРасчетаСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей;	
	
КонецФункции // ПолучитьСписокДоступныхПолейПланаВидовРасчета()

// Функция возвращает список доступных полей РегистраСведений
//
Функция ПолучитьСписокДоступныхПолейРегистраСведений(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "РегистрыСведений".
	// Для регистров сведений нет общих свойств, поэтому дерево будет пустое.
	Если ОбъектМетаданных=Неопределено Тогда		
		ДеревоДоступныхПолей = Неопределено;	
	Иначе			
		// Получим список измерений 		
		мсвИзмерений = Новый Массив;		
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Измерения Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Ресурсы Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Реквизиты Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;    
		
		Если ОбъектМетаданных.ПериодичностьРегистраСведений<>Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			мсвИзмерений.Добавить("Период");
		КонецЕсли;
		Если ОбъектМетаданных.РежимЗаписи<>Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			мсвИзмерений.Добавить("Регистратор");
			мсвИзмерений.Добавить("Активность");
		КонецЕсли;  		  		
		
		// Сформируем текст запроса для построителя
		Текст = СформироватьТекстЗапросаДляПостроителяЗапроса("РегистрСведений."+ОбъектМетаданных.Имя, мсвИзмерений);
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля, ДеревоДоступныхПолей, Ограничения);  		
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 	
КонецФункции // ПолучитьСписокДоступныхПолейРегистраСведений()

// Функция возвращает список доступных полей РегистраНакопления
//
Функция ПолучитьСписокДоступныхПолейРегистраНакопления(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "РегистрыНакопления".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.
	Если ОбъектМетаданных=Неопределено Тогда		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Период";
		НоваяСтрока.ПолноеИмя     = "Период";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Дата");
		НоваяСтрока.Представление = "Период";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Регистратор";
		НоваяСтрока.ПолноеИмя     = "Регистратор";
		НоваяСтрока.ТипЗначения   = Документы.ТипВсеСсылки();
		НоваяСтрока.Представление = "Регистратор";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Активность";
		НоваяСтрока.ПолноеИмя     = "Активность";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Активность";  	
	Иначе			
		// Получим список измерений 		
		мсвИзмерений = Новый Массив;		
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Измерения Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Ресурсы Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Реквизиты Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;    		
		
		мсвИзмерений.Добавить("Период"); 		
		мсвИзмерений.Добавить("Регистратор");
		мсвИзмерений.Добавить("Активность");  		
		
		// Сформируем текст запроса для построителя
		Текст = СформироватьТекстЗапросаДляПостроителяЗапроса("РегистрНакопления."+ОбъектМетаданных.Имя, мсвИзмерений);
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля, ДеревоДоступныхПолей, Ограничения);  		
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей;	
КонецФункции // ПолучитьСписокДоступныхПолейРегистраНакопления()

// Функция возвращает список доступных полей РегистраБухгалтерии
//
Функция ПолучитьСписокДоступныхПолейРегистраБухгалтерии(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "РегистрыБухгалтерии".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.  
	Если ОбъектМетаданных=Неопределено Тогда		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Период";
		НоваяСтрока.ПолноеИмя     = "Период";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Дата");
		НоваяСтрока.Представление = "Период";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Регистратор";
		НоваяСтрока.ПолноеИмя     = "Регистратор";
		НоваяСтрока.ТипЗначения   = Документы.ТипВсеСсылки();
		НоваяСтрока.Представление = "Регистратор";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Активность";
		НоваяСтрока.ПолноеИмя     = "Активность";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Активность";  	
	Иначе			
		// Получим список измерений 		
		мсвИзмерений = Новый Массив;		
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Измерения Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Ресурсы Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Реквизиты Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;    		
		
		мсвИзмерений.Добавить("Период"); 		
		мсвИзмерений.Добавить("Регистратор");
		мсвИзмерений.Добавить("Активность");  		
		
		// Сформируем текст запроса для построителя
		Текст = СформироватьТекстЗапросаДляПостроителяЗапроса("РегистрБухгалтерии."+ОбъектМетаданных.Имя, мсвИзмерений);
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля, ДеревоДоступныхПолей, Ограничения);  		
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей;		
КонецФункции // ПолучитьСписокДоступныхПолейРегистраБухгалтерии()

// Функция возвращает список доступных полей РегистраРасчета
//
Функция ПолучитьСписокДоступныхПолейРегистраРасчета(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "РегистрыРасчета".
	// Поэтому дерево доступных полей будет состоять только из общих свойств. 
	Если ОбъектМетаданных=Неопределено Тогда		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Период";
		НоваяСтрока.ПолноеИмя     = "Период";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Дата");
		НоваяСтрока.Представление = "Период";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Регистратор";
		НоваяСтрока.ПолноеИмя     = "Регистратор";
		НоваяСтрока.ТипЗначения   = Документы.ТипВсеСсылки();
		НоваяСтрока.Представление = "Регистратор";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Активность";
		НоваяСтрока.ПолноеИмя     = "Активность";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Активность";  	
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Сторно";
		НоваяСтрока.ПолноеИмя     = "Сторно";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Сторно";  	
	Иначе			
		// Получим список измерений 		
		мсвИзмерений = Новый Массив;		
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Измерения Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Ресурсы Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;
		Для Каждого ТекИзмерение Из ОбъектМетаданных.Реквизиты Цикл
			мсвИзмерений.Добавить(ТекИзмерение.Имя);	
		КонецЦикла;    		
		
		мсвИзмерений.Добавить("Период"); 		
		мсвИзмерений.Добавить("Регистратор");
		мсвИзмерений.Добавить("Активность");  		
		мсвИзмерений.Добавить("Сторно");  
		мсвИзмерений.Добавить("ПериодРегистрации"); 
		
		// Сформируем текст запроса для построителя
		Текст = СформироватьТекстЗапросаДляПостроителяЗапроса("РегистрРасчета."+ОбъектМетаданных.Имя, мсвИзмерений);
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля, ДеревоДоступныхПолей, Ограничения);  		
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей;		
КонецФункции // ПолучитьСписокДоступныхПолейРегистраРасчета()

// Функция возвращает список доступных полей БизнесПроцесса
//
Функция ПолучитьСписокДоступныхПолейБизнесПроцесса(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "БизнесПроцессы".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.   
	Если ОбъектМетаданных=Неопределено Тогда
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Дата";
		НоваяСтрока.ПолноеИмя     = "Дата";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Дата");
		НоваяСтрока.Представление = "Дата";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Стартован";
		НоваяСтрока.ПолноеИмя     = "Стартован";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Стартован";

		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Завершен";
		НоваяСтрока.ПолноеИмя     = "Завершен";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Завершен";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";		
				
	Иначе
		Текст = 		
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	БизнесПроцесс." + ОбъектМетаданных.Имя + "";
				
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("БизнесПроцессСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 	 	
КонецФункции // ПолучитьСписокДоступныхПолейБизнесПроцесса()

// Функция возвращает список доступных полей Задачи
//
Функция ПолучитьСписокДоступныхПолейЗадачи(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "Задачи".
	// Поэтому дерево доступных полей будет состоять только из общих свойств. 
	Если ОбъектМетаданных=Неопределено Тогда
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Дата";
		НоваяСтрока.ПолноеИмя     = "Дата";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Дата");
		НоваяСтрока.Представление = "Дата";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Выполнена";
		НоваяСтрока.ПолноеИмя     = "Выполнена";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Выполнена";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Наименование";
		НоваяСтрока.ПолноеИмя     = "Наименование";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Строка");
		НоваяСтрока.Представление = "Наименование";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";		
		
	Иначе
		Текст = 		
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	Задача." + ОбъектМетаданных.Имя + "";
		
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("ЗадачаСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 	
КонецФункции // ПолучитьСписокДоступныхПолейЗадачи()

// Функция возвращает список доступных полей ПланаОбмена
//
Функция ПолучитьСписокДоступныхПолейПланаОбмена(ОбъектМетаданных, ДеревоДоступныхПолей, Ограничения) 	
	
	// Если объект метаданных не задан, то значит в качестве источника события выступает класс "ПланыОбмена".
	// Поэтому дерево доступных полей будет состоять только из общих свойств.   
	Если ОбъектМетаданных=Неопределено Тогда
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Наименование";
		НоваяСтрока.ПолноеИмя     = "Наименование";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Строка");
		НоваяСтрока.Представление = "Наименование";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "НомерОтправленного";
		НоваяСтрока.ПолноеИмя     = "НомерОтправленного";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Число");
		НоваяСтрока.Представление = "Номер отправленного";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "НомерПринятого";
		НоваяСтрока.ПолноеИмя     = "НомерПринятого";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Число");
		НоваяСтрока.Представление = "Номер принятого";
		
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "ПометкаУдаления";
		НоваяСтрока.ПолноеИмя     = "ПометкаУдаления";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("Булево");
		НоваяСтрока.Представление = "Пометка удаления";
		
	Иначе
		Текст = 		
		"ВЫБРАТЬ
		|	Ссылка
		|{ВЫБРАТЬ
		|	Ссылка.*}
		|ИЗ
		|	ПланОбмена." + ОбъектМетаданных.Имя + "";
			
		ПостроительЗапроса = Новый ПостроительЗапроса(Текст);
		СформироватьДЗПоПостроителюЗапроса(ПостроительЗапроса.ДоступныеПоля.Ссылка.Поля, ДеревоДоступныхПолей, Ограничения);
		
		// Добавим поле "Ссылка"
		НоваяСтрока = ДеревоДоступныхПолей.Строки.Добавить();
		НоваяСтрока.Имя           = "Ссылка";
		НоваяСтрока.ПолноеИмя     = "Ссылка";
		НоваяСтрока.ТипЗначения   = Новый ОписаниеТипов("ПланОбменаСсылка." + ОбъектМетаданных.Имя);
		НоваяСтрока.Представление = "Ссылка";
	КонецЕсли;
	
	Возврат ДеревоДоступныхПолей; 
КонецФункции // ПолучитьСписокДоступныхПолейПланаОбмена()

                    
//////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ПОДПИСОК
//////////////////////////////////////////////////////////////////////////////////

// Процедура обработчик события ПриЗаписи Константы
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбКонстантыПриЗаписи(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
КонецПроцедуры

// Процедура обработчик события ПередЗаписью Справочника
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбСправочникПередЗаписью(Источник, Отказ) Экспорт
	
	//->Diginova _Настройка базы_ 03.01.2022
	Если Источник.ОбменДанными.Загрузка = Истина тогда
		Возврат;
	КонецЕсли;
	//<-Diginova _FlaiM_

	// Необходимо проверить, новый ли это объект. Если да,то необходимо сохранить это признак, 
	// чтобы можно его проанализировать в последующих событиях.
	
	// Особенность. У вновь созданных объектов возможен двойной вызов обработчиков "ПередЗаписью" и "ПриЗаписи", 
	// т.е. двойная запись. При этом в первый раз, объект будет считаться новым, а уже во второй раз нет.
	// Нас будет интерисовать только первый проход.
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ЭтоНовый());
	КонецЕсли;
КонецПроцедуры

// Процедура обработчик события ПриЗаписи Справочника
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбСправочникиПриЗаписи(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");		
	
	// Обмен с сайтом автосервиса
	Если (НЕ Отказ) И Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса.Состав.Содержит(Источник.Метаданные()) Тогда
		ОбменССайтомАвтосервиса = Обработки.ОбменССайтомАвтосервиса.Создать();
		ОбменССайтомАвтосервиса.ВыполнитьРегистрациюИзмененияОбъектовОбмен(Источник, Отказ);
	КонецЕсли;
КонецПроцедуры

// Процедура обработчик события ПередУдалением Справочника
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбСправочникиПередУдалением(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПередУдалением");
КонецПроцедуры

// Процедура обработчик события ПередЗаписью Документа
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбДокументыПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	// Необходимо проверить, новый ли это объект. Если да,то необходимо сохранить это признак, 
	// чтобы можно его проанализировать в последующих событиях.
	
	// Особенность. У вновь созданных объектов возможен двойной вызов обработчиков "ПередЗаписью" и "ПриЗаписи", 
	// т.е. двойная запись. При этом в первый раз, объект будет считаться новым, а уже во второй раз нет.
	// Нас будет интерисовать только первый проход.
	
	//->Diginova _Настройка базы_ 03.01.2022
	Если Источник.ОбменДанными.Загрузка = Истина тогда
		Возврат;
	КонецЕсли;
	//<-Diginova _FlaiM_   

	//БизТех 12.09.2024г. <<
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения или РежимЗаписи = РежимЗаписиДокумента.Запись  Тогда  
		Если НЕ Источник.ЭтоНовый() Тогда
			ДокСФ = БТ_ПроцедурыДокументовСервер.ВернутьСФ(Источник);
			Если ДокСФ <> Неопределено Тогда 
				ДокСФОбъект = ДокСФ.ПолучитьОбъект();
				ДокСФОбъект.Проведен = ?(РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения, Ложь, Источник.Проведен);
				ДокСФОбъект.ПометкаУдаления = Источник.ПометкаУдаления;
				ДокСФОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	//БизТех 12.09.2024г. >>
    
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ЭтоНовый());
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработчик события ПриЗаписи Документа
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбДокументыПриЗаписи(Источник, Отказ) Экспорт
	
	//->Diginova _Настройка базы_ 03.01.2022
	Если Источник.ОбменДанными.Загрузка = Истина тогда
		Возврат;
	КонецЕсли;
	//<-Diginova _FlaiM_

	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
	
	// Обмен с сайтом автосервиса
	Если (НЕ Отказ) И Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса.Состав.Содержит(Источник.Метаданные()) Тогда
		ОбменССайтомАвтосервиса = Обработки.ОбменССайтомАвтосервиса.Создать();
		ОбменССайтомАвтосервиса.ВыполнитьРегистрациюИзмененияОбъектовОбмен(Источник, Отказ);
	КонецЕсли;
КонецПроцедуры

// Процедура обработчик события ОбработкаПроведения Документа
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбДокументыОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ОбработкаПроведения");
	// решаем как быть с утверждением документа
	#Если Клиент Тогда
	ВыполнитьУтверждениеПослеПроведения(Источник, "ОбработкаПроведения");
	#КонецЕсли 
КонецПроцедуры

// Процедура обработчик события ОбработкаУдаленияПроведения Документа
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбДокументыОбработкаУдаленияПроведения(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ОбработкаУдаленияПроведения");
	// решаем как быть с утверждением документа
	#Если Клиент Тогда
	ВыполнитьУтверждениеПослеПроведения(Источник, "ОбработкаУдаленияПроведения");
	#КонецЕсли
КонецПроцедуры

// Процедура обработчик события ПередУдалением Документа
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбДокументыПередУдалением(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПередУдалением");
КонецПроцедуры

// Процедура обработчик события ПриЗаписи Последовательности
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПоследовательностьПриЗаписи(Источник, Отказ, Замещение) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
КонецПроцедуры

// Процедура обработчик события ПередЗаписью ПВХ
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПВХПередЗаписью(Источник, Отказ) Экспорт
	
	//->Diginova _Настройка базы_ 03.01.2022
	Если Источник.ОбменДанными.Загрузка = Истина тогда
		Возврат;
	КонецЕсли;
	//<-Diginova _FlaiM_

	// Необходимо проверить, новый ли это объект. Если да,то необходимо сохранить это признак, 
	// чтобы можно его проанализировать в последующих событиях.
	
	// Особенность. У вновь созданных объектов возможен двойной вызов обработчиков "ПередЗаписью" и "ПриЗаписи", 
	// т.е. двойная запись. При этом в первый раз, объект будет считаться новым, а уже во второй раз нет.
	// Нас будет интерисовать только первый проход.
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ЭтоНовый());
	КонецЕсли;
КонецПроцедуры

// Процедура обработчик события ПриЗаписи ПВХ
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПВХПриЗаписи(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
КонецПроцедуры

// Процедура обработчик события ПередУдалением ПВХ
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПВХПередУдалением(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПередУдалением");
КонецПроцедуры

// Процедура обработчик события ПередЗаписью ПланаСчетов
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПланыСчетовПередЗаписью(Источник, Отказ) Экспорт
	// Необходимо проверить, новый ли это объект. Если да,то необходимо сохранить это признак, 
	// чтобы можно его проанализировать в последующих событиях.
	
	// Особенность. У вновь созданных объектов возможен двойной вызов обработчиков "ПередЗаписью" и "ПриЗаписи", 
	// т.е. двойная запись. При этом в первый раз, объект будет считаться новым, а уже во второй раз нет.
	// Нас будет интерисовать только первый проход.
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ЭтоНовый());
	КонецЕсли;
КонецПроцедуры

// Процедура обработчик события ПриЗаписи ПланаСчетов
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПланыСчетовПриЗаписи(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
КонецПроцедуры

// Процедура обработчик события ПередУдалением ПланаСчетов
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПланыСчетовПередУдалением(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПередУдалением");
КонецПроцедуры

// Процедура обработчик события ПриЗаписи ПВР
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПланВидовРасчетаПриЗаписи(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
КонецПроцедуры

// Процедура обработчик события ПриЗаписи РегистраСведений
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбРегистрСведенийПриЗаписи(Источник, Отказ, Замещение) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
	
	// Обмен с сайтом автосервиса
	Если (НЕ Отказ) И Метаданные.ПланыОбмена.ОбменССайтомАвтосервиса.Состав.Содержит(Источник.Метаданные()) Тогда
		ОбменССайтомАвтосервиса = Обработки.ОбменССайтомАвтосервиса.Создать();
		ОбменССайтомАвтосервиса.ВыполнитьРегистрациюИзмененияОбъектовОбмен(Источник, Отказ, Замещение);
	КонецЕсли;
КонецПроцедуры

// Процедура обработчик события ПриЗаписи РегистраСведений без фоновых заданий
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбРегистрСведенийПриЗаписиБезФоновыхЗаданий(Источник, Отказ, Замещение) Экспорт
	
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи", Истина);
	
КонецПроцедуры

// Процедура обработчик события ПриЗаписи РегистраНакопления
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбРегистрНакопленияПриЗаписи(Источник, Отказ, Замещение) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
КонецПроцедуры

// Процедура обработчик события ПередЗаписью ПланаОбмена
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПланыОбменаПередЗаписью(Источник, Отказ) Экспорт
	// Необходимо проверить, новый ли это объект. Если да,то необходимо сохранить это признак, 
	// чтобы можно его проанализировать в последующих событиях.
	
	// Особенность. У вновь созданных объектов возможен двойной вызов обработчиков "ПередЗаписью" и "ПриЗаписи", 
	// т.е. двойная запись. При этом в первый раз, объект будет считаться новым, а уже во второй раз нет.
	// Нас будет интерисовать только первый проход.
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Источник.ЭтоНовый());
	КонецЕсли;
КонецПроцедуры

// Процедура обработчик события ПриЗаписи ПланаОбмена
// Параметры:
//	Источник - источник события
//	Отказ - флаг отказа
//
Процедура сбПланОбменаПриЗаписи(Источник, Отказ) Экспорт
	РеакцияНаЗначимоеСобытие(Источник, "ПриЗаписи");
КонецПроцедуры

// Процедура выполняет резервирование заказа покупателей
Процедура сбЗаказыПокупателейРезервирование(Источник, Отказ, Замещение) Экспорт
	//При поступлении товаров на склад и резервировании его в заказах покупателей 
	//сформируем напоминания авторам заказов
	
	// Если происходит загрузка объекта механизмом обменов, то обрабатывать такое событие не нужно.
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КодВыполняетсяНаСервере=обЭтоКлиентСервер();
	// Проверим настройку, возможно обрабатывать события не нужно.
	Если НЕ КодВыполняетсяНаСервере Тогда
		#Если Клиент Тогда
			ТекПраво=глПрава;			
		#КонецЕсли  			
	Иначе
		ТекПраво=Неопределено;
	КонецЕсли;
	Если ТекПраво<>Неопределено Тогда
		АктивностьЗначимыхСобытий=обПраво("АктивностьЗначимыхСобытий",ТекПраво);
	Иначе
		АктивностьЗначимыхСобытий=обПолучитьПраваИНастройкиПользователя(Неопределено,ПланыВидовХарактеристик.ПраваИНастройки.АктивностьЗначимыхСобытий);
	КонецЕсли; 
	//KamikadZe begin add 27.08.2015 15:41:11
	АктивностьЗначимыхСобытий = Истина;
	//KamikadZe end add
	Если НЕ АктивностьЗначимыхСобытий Тогда
		Возврат;
	КонецЕсли;
	
	//Получим регистратор записей в регистр
	ОтборРегистратор=Источник.Отбор.Найти("Регистратор");
	Если ОтборРегистратор=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Регистратор=ОтборРегистратор.Значение;
	
	Попытка
		Если НЕ Источник.ИнтерактивноеПроведение Тогда
			Возврат;
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	
	Если ТипЗнч(Регистратор)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		Возврат;
	КонецЕсли;
	РегистраторАвтор=Регистратор.Автор;
	
	//Получим заказы и их авторов, которым требуется создать напоминание
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
	             |	ВЫРАЗИТЬ(ЗаказыПокупателей.Заказ КАК Документ.ЗаказПокупателя) КАК ЗаказПокупателя,
	             |	ПРЕДСТАВЛЕНИЕ(ЗаказыПокупателей.Заказ) КАК ЗаказПокупателяПредставление,
	             |	ЕСТЬNULL(Пользователи.Ссылка, ВЫРАЗИТЬ(ЗаказыПокупателей.Заказ КАК Документ.ЗаказПокупателя).Автор) КАК Автор,
	             |	Пользователи.Ссылка,
	             |	ЗначенияСвойствОбъектов.Значение КАК СпособУведомления
	             |ИЗ
	             |	РегистрНакопления.ЗаказыПокупателей КАК ЗаказыПокупателей
	             |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	             |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	             |ПО
	             |	(ЗначенияСвойствОбъектов.Свойство = &Свойство) И 
	             |	((ВЫРАЗИТЬ(ЗаказыПокупателей.Заказ КАК Документ.ЗаказПокупателя)) = ЗначенияСвойствОбъектов.Объект)
	             |ЛЕВОЕ СОЕДИНЕНИЕ
	             |	Справочник.Пользователи КАК Пользователи
	             |ПО
	             |	(ВЫРАЗИТЬ(ЗаказыПокупателей.Заказ КАК Документ.ЗаказПокупателя).Менеджер = Пользователи.Сотрудник) И
	             |	(НЕ Пользователи.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка))
	             |ГДЕ
	             |	ЗаказыПокупателей.ВидДвижения = &ВидДвиженияПриход
	             |	И ЗаказыПокупателей.Регистратор = &Регистратор
	             |	И ЗаказыПокупателей.Заказ ССЫЛКА Документ.ЗаказПокупателя
	             |	И ЕСТЬNULL(Пользователи.Ссылка, ВЫРАЗИТЬ(ЗаказыПокупателей.Заказ КАК Документ.ЗаказПокупателя).Автор) <> &ПустойАвтор
	             |	И ЗаказыПокупателей.Резерв > 0
	             |	И ЗначенияСвойствОбъектов.Значение > 0";
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("Регистратор",       Регистратор);
	Запрос.УстановитьПараметр("ПустойАвтор",       Справочники.Пользователи.ПустаяСсылка());
	Запрос.УстановитьПараметр("Свойство",          ПланыВидовХарактеристик.СвойстваОбъектов.УведомлениеОРезервировании);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗаказов = РезультатЗапроса.Выгрузить();
	Если ТаблицаЗаказов.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВложенныЙЗапрос.Заказ КАК Заказ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказыПокупателейОстатки.Заказ КАК Заказ,
		|		ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|		ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		ВЫБОР
		|			КОГДА ЗаказыПокупателейОстатки.ЗаказаноОстаток = ЗаказыПокупателейОстатки.РезервОстаток ТОГДА
		|				1
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК СовпадаетЗаказИРезерв
		|	ИЗ
		|		РегистрНакопления.ЗаказыПокупателей.Остатки(&МоментВремени, Заказ В (&Заказы)) КАК ЗаказыПокупателейОстатки
		|	ГДЕ
		|		ЗаказыПокупателейОстатки.ЗаказаноОстаток>0) КАК ВложенныЙЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ВложенныЙЗапрос.Заказ
		|ИМЕЮЩИЕ
		|	МИНИМУМ(ВложенныЙЗапрос.СовпадаетЗаказИРезерв)>0
		|";
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(Регистратор.МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Заказы",        ТаблицаЗаказов.ВыгрузитьКолонку("ЗаказПокупателя"));
	ТаблицаПолныхРезервов = Запрос.Выполнить().Выгрузить();
	ТаблицаПолныхРезервов.Индексы.Добавить("Заказ");
	
	//KamikadZe begin add 27.02.2019 11:30:09
	обОбщие.обСоздатьНапоминанияПоЗаказу(ТаблицаЗаказов,ТаблицаПолныхРезервов,РегистраторАвтор);
	Возврат;
	//KamikadZe end add
	
	НаборЗаписейНапоминания=РегистрыСведений.Напоминания.СоздатьНаборЗаписей();
	НаборЗаписейНапоминания.Отбор.Автор.Установить(РегистраторАвтор);
	НаборЗаписейНапоминания.Отбор.Завершено.Установить(Ложь);
	НаборЗаписейНапоминания.Прочитать();
	ДатаОповещения=ТекущаяДата();
	
	ЗаписатьНапоминания = Ложь;
	Для Каждого Выборка Из ТаблицаЗаказов Цикл
		
		ЧастичноеРезервирование = (ТаблицаПолныхРезервов.Найти(Выборка.ЗаказПокупателя, "Заказ") = Неопределено);
		
		Если Выборка.СпособУведомления = 2 И ЧастичноеРезервирование Тогда
			Продолжить;
		КонецЕсли;
		
		//Для каждого автора создадим напоминание
		НоваяЗапись = НаборЗаписейНапоминания.Добавить();
		НоваяЗапись.Пользователь            = Выборка.Автор;
		НоваяЗапись.Автор                   = РегистраторАвтор;
		НоваяЗапись.Завершено               = Ложь;
		НоваяЗапись.РеальнаяДатаОповещения  = ДатаОповещения;
		НоваяЗапись.Объект                  = Выборка.ЗаказПокупателя;
		НоваяЗапись.ДатаНачала              = ДатаОповещения;
		НоваяЗапись.ДатаОповещения          = ДатаОповещения;
		Если ЧастичноеРезервирование Тогда
			НоваяЗапись.Описание            = СокрЛП(РегистраторАвтор)+"! Детали по заказу <"+Выборка.ЗаказПокупателяПредставление+"> для <"+СокрЛП(?(ТипЗнч(Выборка.ЗаказПокупателя)=Тип("ДокументСсылка.ЗаказВнутренний"),Выборка.ЗаказПокупателя.ПодразделениеПолучатель,Выборка.ЗаказПокупателя.Контрагент))+"> частично зарезервированы.";
		Иначе
			НоваяЗапись.Описание            = СокрЛП(РегистраторАвтор)+"! Детали по заказу <"+Выборка.ЗаказПокупателяПредставление+"> для <"+СокрЛП(?(ТипЗнч(Выборка.ЗаказПокупателя)=Тип("ДокументСсылка.ЗаказВнутренний"),Выборка.ЗаказПокупателя.ПодразделениеПолучатель,Выборка.ЗаказПокупателя.Контрагент))+"> полностью зарезервированы.";
		КонецЕсли;
		НоваяЗапись.Редактирование          = Ложь;
		НоваяЗапись.Тема                    = "Резервирование товаров"; 
		НоваяЗапись.ТипПериода              = "00"; 
		НоваяЗапись.УдалитьПоИстеченииСрока = Истина; 
		
		ЗаписатьНапоминания = Истина;
		
	КонецЦикла;
	
	Попытка
		НаборЗаписейНапоминания.Записать(Истина);
	Исключение
		Сообщить("Ошибка создания напоминаний о резервировании товаров: "+ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА ЗНАЧИМОГО СОБЫТИЯ
//////////////////////////////////////////////////////////////////////////////////
     
// Процедура вызывается из всех обработчиков подписок.
//
// Параметры:
//  Источник  - произвольный объект - объект вызвавший событие.
//
//  Событие  - "Строка" - вид события.
// 
Процедура РеакцияНаЗначимоеСобытие(Источник, Событие="ПриЗаписи", НеИспользоватьФоновыеЗадания = Ложь)
	Попытка
		// Если происходит загрузка объекта механизмом обменов, то обрабатывать такое событие не нужно.
		Если Источник.ОбменДанными.Загрузка Тогда
			Возврат;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	КодВыполняетсяНаСервере = обЭтоКлиентСервер();
	
	// Проверим настройку, возможно обрабатывать события не нужно.
	Если НЕ КодВыполняетсяНаСервере Тогда
		#Если Клиент Тогда
			ТекПраво = глПрава;
		#КонецЕсли
	Иначе
		ТекПраво = Неопределено;
	КонецЕсли;
	
	Если ТекПраво <> Неопределено Тогда
		АктивностьЗначимыхСобытий = обПраво("АктивностьЗначимыхСобытий", ТекПраво);
	Иначе
		АктивностьЗначимыхСобытий = обПолучитьПраваИНастройкиПользователя(Неопределено,ПланыВидовХарактеристик.ПраваИНастройки.АктивностьЗначимыхСобытий);
	КонецЕсли;
	
	Если НЕ АктивностьЗначимыхСобытий Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим свойство "ДополнительныеСвойства" объекта-источника.
	// Если в этой структуре присутствует ключ "СозданПоЗначимомуСобытию", значит этот объект создан
	// механизмом значимых событий и его обрабатывать не нужно, иначе возникнет бесконечная рекурсия.
	Если Источник.ДополнительныеСвойства.Свойство("СозданПоЗначимомуСобытию") Тогда
		Если Источник.ДополнительныеСвойства.СозданПоЗначимомуСобытию Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Поищем подходящие элементы справочника "Значимые события"
	ОбъектМетаданных = Источник.Метаданные();
	Имя              = ОбъектМетаданных.Имя;
	ПолноеИмя        = ОбъектМетаданных.ПолноеИмя();
	Класс            = ПолучитьКлассОбъектаМетаданных(ОбъектМетаданных);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗначимыеСобытия.Ссылка,
	|	ЗначимыеСобытия.ТолькоНаКлиенте
	|ИЗ
	|	Справочник.ЗначимыеСобытия КАК ЗначимыеСобытия
	|ГДЕ
	|	(НЕ ЗначимыеСобытия.ЭтоГруппа)
	|	И (НЕ ЗначимыеСобытия.ПометкаУдаления)
	|	И (ЗначимыеСобытия.Источник = &ПолноеИмя
	|			ИЛИ ЗначимыеСобытия.Источник = &Класс)
	|	И ЗначимыеСобытия.Событие = &Событие
	|	И ЗначимыеСобытия.Активность
	|УПОРЯДОЧИТЬ ПО
	|	ЗначимыеСобытия.Код";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПолноеИмя", ПолноеИмя);
	Запрос.УстановитьПараметр("Класс",     Класс);
	Запрос.УстановитьПараметр("Событие",   Событие);
	
	МассивЗначимыхСобытий       = Новый Массив;
	МассивЗначимыхСобытийКлиент = Новый Массив;
	
	ВыборкаЗначимыхСобытий = Запрос.Выполнить().Выбрать();
	
	Если КодВыполняетсяНаСервере Тогда
		Пока ВыборкаЗначимыхСобытий.Следующий() Цикл
			Если ПроверитьУсловияЗначимогоСобытия(ВыборкаЗначимыхСобытий.Ссылка, Источник, Класс, Имя) Тогда
				Если ВыборкаЗначимыхСобытий.ТолькоНаКлиенте Тогда
					МассивЗначимыхСобытийКлиент.Добавить(ВыборкаЗначимыхСобытий.Ссылка);
				Иначе
					МассивЗначимыхСобытий.Добавить(ВыборкаЗначимыхСобытий.Ссылка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Пока ВыборкаЗначимыхСобытий.Следующий() Цикл
			Если ПроверитьУсловияЗначимогоСобытия(ВыборкаЗначимыхСобытий.Ссылка, Источник, Класс, Имя) Тогда
				МассивЗначимыхСобытий.Добавить(ВыборкаЗначимыхСобытий.Ссылка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если МассивЗначимыхСобытий.Количество() = 0 И МассивЗначимыхСобытийКлиент.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТипИсточника     = "";
	ИсточникНаСервер = ПреобразоватьИсточник(Источник, ТипИсточника);
	
	Если ТипИсточника = "СсылочныйТип" Тогда
		Ключ = СтрЗаменить(Источник.Ссылка.УникальныйИдентификатор(),"-","_");
		Ключ = Ключ + "_" + Событие;
	Иначе
		Ключ = "";
	КонецЕсли;
	
	// В зависимости от режима работы с базой (файловый вариант или серверный) обрабатываться значимые
	// события будут по-разному.
	// Либо при помощи механизма фоновых заданий (серверный вариант) или при помощи обработчика ожидания
	// (файловый вариант).
	Если КодВыполняетсяНаСервере Тогда
		// Задействуем механизм фоновых заданий
		// Т.к. функция фонового задания будет выполнятся на сервере, на параметры этой функции
		// накладываются жесткие ограничения. Поэтому в чистом виде объект передавать нельзя,
		// придется произвести преобразование.
		// Для этого вместо объектов ссылочного типа будем передавать ссылку, а вместо наборов записей структуру параметров,
		// позволяющую воссоздать этот набор записей.
		
		Если МассивЗначимыхСобытий.Количество()>0 Тогда
			
			Параметры = Новый Массив;
			Параметры.Добавить(ИсточникНаСервер);
			Параметры.Добавить(МассивЗначимыхСобытий);
			Параметры.Добавить(ТипИсточника);
			
			СтруктураПараметровСеанса = Новый Структура;
			Попытка
				СтруктураПараметровСеанса.Вставить("Компьютер",ПараметрыСеанса.Компьютер);
			Исключение
				СтруктураПараметровСеанса.Вставить("Компьютер",Неопределено);
			КонецПопытки;
			Попытка
				СтруктураПараметровСеанса.Вставить("Организация",ПараметрыСеанса.Организация);
			Исключение
				СтруктураПараметровСеанса.Вставить("Организация",Неопределено);
			КонецПопытки;
			Попытка
				СтруктураПараметровСеанса.Вставить("ПодразделениеКомпании",ПараметрыСеанса.ПодразделениеКомпании);
			Исключение
				СтруктураПараметровСеанса.Вставить("ПодразделениеКомпании",Неопределено);
			КонецПопытки;
			Попытка
				СтруктураПараметровСеанса.Вставить("Пользователь",ПараметрыСеанса.Пользователь);
			Исключение
				СтруктураПараметровСеанса.Вставить("Пользователь",Неопределено);
			КонецПопытки;
			
			Параметры.Добавить(СтруктураПараметровСеанса);
			
			// Так же необходимо передать на сервер признак того, что объект новый.
			Если Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
				Параметры.Добавить(Источник.ДополнительныеСвойства.ЭтоНовый);
			Иначе
				Параметры.Добавить(Ложь);
			КонецЕсли;
			
			//Параметры.Добавить(МассивЗначимыхСобытий);
			// для ссылочного типа добавим ключ
			Если ТипИсточника = "СсылочныйТип" Тогда
				
				Попытка
					Фоновые = ФоновыеЗадания.ПолучитьФоновыеЗадания(Новый Структура("Ключ", Ключ));
					Если Фоновые.Количество()>0 Тогда
						Возврат;
					КонецЕсли;
				Исключение
				КонецПопытки;
				
				Если НеИспользоватьФоновыеЗадания Тогда
					ОбработатьЗначимоеСобытие(
						ИсточникНаСервер,
						МассивЗначимыхСобытий,
						ТипИсточника,
						СтруктураПараметровСеанса,
						Параметры[Параметры.ВГраница()]);
				Иначе
					ФоновыеЗадания.Выполнить("сбСобытия.ОбработатьЗначимоеСобытие", Параметры, Ключ, "ЗначимоеСобытие");
				КонецЕсли;
			Иначе
				Если НеИспользоватьФоновыеЗадания Тогда
					ОбработатьЗначимоеСобытие(
						ИсточникНаСервер,
						МассивЗначимыхСобытий,
						ТипИсточника,
						СтруктураПараметровСеанса,
						Параметры[Параметры.ВГраница()]);
				Иначе
					ФоновыеЗадания.Выполнить("сбСобытия.ОбработатьЗначимоеСобытие", Параметры,, "ЗначимоеСобытие");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		#Если Клиент Тогда
			Если МассивЗначимыхСобытийКлиент.Количество()>0 Тогда
				// Т.к. обработчик ожидания выполняется только в "свободное время", то к времени
				// его выполнения может произойти несколько различных событий. Чтобы не потерять события,
				// используется очередь событий, которая очищается в момент отработки обработчиком ожидания.
				
				Если ТипЗнч(глОчередьЗначимыхСобытий) = Тип("ТаблицаЗначений") Тогда
					Если ТипИсточника = "СсылочныйТип" Тогда
						СтрокаСобытия = глОчередьЗначимыхСобытий.Найти(Ключ, "Ключ");
						Если СтрокаСобытия = Неопределено Тогда
							НоваяСтрока = глОчередьЗначимыхСобытий.Добавить();
							НоваяСтрока.Источник        = Источник;
							НоваяСтрока.Ключ            = Ключ;
							НоваяСтрока.ЗначимыеСобытия = МассивЗначимыхСобытийКлиент;
							ПодключитьОбработчикОжидания("глОбработатьЗначимоеСобытие", 1, Истина);
						КонецЕсли;
					Иначе
						НоваяСтрока = глОчередьЗначимыхСобытий.Добавить();
						НоваяСтрока.Источник        = Источник;
						НоваяСтрока.Ключ            = Ключ;
						НоваяСтрока.ЗначимыеСобытия = МассивЗначимыхСобытийКлиент;
						ПодключитьОбработчикОжидания("глОбработатьЗначимоеСобытие", 1, Истина);
					КонецЕсли;
				Иначе
					глОчередьЗначимыхСобытий = Новый ТаблицаЗначений;
					глОчередьЗначимыхСобытий.Колонки.Добавить("Ключ");
					глОчередьЗначимыхСобытий.Колонки.Добавить("Источник");
					глОчередьЗначимыхСобытий.Колонки.Добавить("ЗначимыеСобытия");
					глОчередьЗначимыхСобытий.Индексы.Добавить("Ключ");
					
					НоваяСтрока = глОчередьЗначимыхСобытий.Добавить();
					НоваяСтрока.Источник        = Источник;
					НоваяСтрока.Ключ            = Ключ;
					НоваяСтрока.ЗначимыеСобытия = МассивЗначимыхСобытийКлиент;
					
					ПодключитьОбработчикОжидания("глОбработатьЗначимоеСобытие", 1, Истина);
				КонецЕсли;
			КонецЕсли;
		#КонецЕсли
		
	Иначе
		// Если работа производится в файловом режиме, то подключим "одноразовый" обработчик ожидания.
		#Если Клиент Тогда
			// Т.к. обработчик ожидания выполняется только в "свободное время", то к времени
			// его выполнения может произойти несколько различных событий. Чтобы не потерять события,
			// используется очередь событий, которая очищается в момент отработки обработчиком ожидания.
			
			Если ТипЗнч(глОчередьЗначимыхСобытий) = Тип("ТаблицаЗначений") Тогда
				Если ТипИсточника = "СсылочныйТип" Тогда
					СтрокаСобытия = глОчередьЗначимыхСобытий.Найти(Ключ, "Ключ");
					Если СтрокаСобытия = Неопределено Тогда
						НоваяСтрока = глОчередьЗначимыхСобытий.Добавить();
						НоваяСтрока.Источник        = Источник;
						НоваяСтрока.Ключ            = Ключ;
						НоваяСтрока.ЗначимыеСобытия = МассивЗначимыхСобытий;
						ПодключитьОбработчикОжидания("глОбработатьЗначимоеСобытие", 1, Истина);
					КонецЕсли;
				Иначе
					НоваяСтрока = глОчередьЗначимыхСобытий.Добавить();
					НоваяСтрока.Источник        = Источник;
					НоваяСтрока.Ключ            = Ключ;
					НоваяСтрока.ЗначимыеСобытия = МассивЗначимыхСобытий;
					ПодключитьОбработчикОжидания("глОбработатьЗначимоеСобытие", 1, Истина);
				КонецЕсли;
			Иначе
				глОчередьЗначимыхСобытий = Новый ТаблицаЗначений;
				глОчередьЗначимыхСобытий.Колонки.Добавить("Ключ");
				глОчередьЗначимыхСобытий.Колонки.Добавить("Источник");
				глОчередьЗначимыхСобытий.Колонки.Добавить("ЗначимыеСобытия");
				глОчередьЗначимыхСобытий.Индексы.Добавить("Ключ");
				
				НоваяСтрока = глОчередьЗначимыхСобытий.Добавить();
				НоваяСтрока.Источник        = Источник;
				НоваяСтрока.Ключ            = Ключ;
				НоваяСтрока.ЗначимыеСобытия = МассивЗначимыхСобытий;
				
				ПодключитьОбработчикОжидания("глОбработатьЗначимоеСобытие", 1, Истина);
			КонецЕсли;
		#КонецЕсли
	КонецЕсли;
КонецПроцедуры // РеакцияНаЗначимоеСобытие()

// Функция предназначения для преобразования источника в подходящий тип данных, для передачи на сервер.
//
// Параметры:
//  Источник  - Произвольный объект - объект-источник события.
//
//  ТипИсточника  - "Строка" - возвращает строковое представление типа источника.
//							   Используется для обратного преобразования на сервере.
//
// Возвращаемое значение:
//   Возвращает преобразованный источник события. 
//   Для ссылочных данных возвращается соответствующая ссылка. Для наборов записей возвращается
//	 структура, позволяющая восстановить этот набор записей.
//   
Функция ПреобразоватьИсточник(Источник, ТипИсточника="")	
	ОбъектМетаданных = Источник.Метаданные();
	Класс            = ПолучитьКлассОбъектаМетаданных(ОбъектМетаданных);
	
	Если Класс="Константы" Тогда  
		// КонстантуМенеджерЗначения можно передавать на сервер.
		ТипИсточника = "Константы";
		Возврат Источник; 
		
	ИначеЕсли Класс="Справочники" Или  		
		// Для ссылочных типов передаем ссылки, как это не странно.
		Класс="Документы" Или 		
		Класс="ПланыВидовХарактеристик" Или
		Класс="ПланыСчетов" Или 		
		Класс="ПланыВидовРасчета" Или 		 
		Класс="БизнесПроцессы" Или 	
		Класс="Задачи" Или   		 	
		Класс="ПланыОбмена" Тогда 	
		
		ТипИсточника = "СсылочныйТип";
		Возврат Источник.Ссылка;
		
	ИначеЕсли Класс="Последовательности" Или 			
		Класс="РегистрыСведений" Или    		
		Класс="РегистрыНакопления" Или
		Класс="РегистрыБухгалтерии" Или 		
		Класс="РегистрыРасчета" Тогда  		
			
		ТипИсточника = "НаборЗаписей";		
		СтруктураИсточника = Новый Структура;
		СтруктураИсточника.Вставить("ОбъектМетаданных", ОбъектМетаданных.ПолноеИмя());
		СтруктураИсточника.Вставить("Отбор",            Источник.Отбор); 		
				
		Возврат СтруктураИсточника;
	КонецЕсли;
	
КонецФункции // ПреобразоватьИсточник() 

// Функция обрабатывает значимое событие. Проверяет условия и выполняет действия значимого события.
// Если происходит работа в клиент-серверном режиме, то данная функция выполняется на сервере.
//
// Параметры:
//  Источник  - произвольный - преобразованный объект-источник события
//
//  Событие  - "Строка" - строковое представление события
//
//  ТипИсточника  - "Строка" - строковое представление типа источника ("СсылочныйТип" либо "НаборЗаписей")
//
Процедура ОбработатьЗначимоеСобытие(Источник, МассивЗначимыхСобытий, ТипИсточника="", СтруктураПараметровСеанса=Неопределено, ЭтоНовый=Ложь) Экспорт
	
	// Проверим, в каком режиме работает предприятие. Если в клиент-серверном, то значит задействован механизм
	// значимых событий
	// Соответственно необходимо выполнить инициализацию параметров сеанса и получить объект-источник.
	Если обЭтоКлиентСервер() Тогда
		// Если код выполняется на сервере, значит задействован механизм 
		// фоновых заданий. В таком случае, параметры сеанса могут быть не инициализированы.
		Если СтруктураПараметровСеанса<>Неопределено Тогда
			ПараметрыСеанса.Компьютер              = СтруктураПараметровСеанса.Компьютер;
			ПараметрыСеанса.Организация            = СтруктураПараметровСеанса.Организация;
			ПараметрыСеанса.ПодразделениеКомпании  = СтруктураПараметровСеанса.ПодразделениеКомпании;
			ПараметрыСеанса.Пользователь           = СтруктураПараметровСеанса.Пользователь;
		КонецЕсли;
		
		// Необходимо получить объект-источник, т.к. объект передавать на сервер нельзя, то он предварительно преобразуется.
		Если ТипИсточника="СсылочныйТип" Тогда
			Источник = Источник.ПолучитьОбъект();
		ИначеЕсли ТипИсточника="НаборЗаписей" Тогда
			Отбор    = Источник.Отбор;
			Источник = СоздатьПустоеЗначениеПоОбъектуМетаданных(Источник.ОбъектМетаданных);
			Для Каждого ЭлементОтбора Из Отбор Цикл
				НовыйЭлемент = Источник.Отбор.Найти(ЭлементОтбора.ПутьКДанным);
				Если НовыйЭлемент=Неопределено Тогда
					НовыйЭлемент = Источник.Отбор.Добавить(ЭлементОтбора.ПутьКДанным);
				КонецЕсли;
				НовыйЭлемент.Установить(ЭлементОтбора.Значение);
			КонецЦикла; 
			Источник.Прочитать();
		КонецЕсли;
		
		// Теперь восстановим признак "ЭтоНовый".
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ИсторияКомандМобильногоПриложения") Тогда
		ЗаписьЖурналаРегистрации(
			"ОбработкаЗначимогоСобытия",
			УровеньЖурналаРегистрации.Информация,
			Метаданные.РегистрыСведений.ИсторияКомандМобильногоПриложения,
			"Середина записи");
	КонецЕсли;
	
	Если (Источник=Неопределено) ИЛИ МассивЗначимыхСобытий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Поищем подходящие элементы справочника "Значимые события"
	ОбъектМетаданных = Источник.Метаданные();
	Имя              = ОбъектМетаданных.Имя;
	Класс            = ПолучитьКлассОбъектаМетаданных(ОбъектМетаданных);
	
	КоличествоСообщений = МассивЗначимыхСобытий.Количество();
	Счетчик = 1;
	Для Каждого ТекСобытие Из МассивЗначимыхСобытий Цикл
		Если ВыполнитьДействияЗначимогоСобытия(ТекСобытие, Источник, Класс, Имя) И Счетчик<КоличествоСообщений Тогда
		// Для разделения информации о значимых событиях
		#Если Клиент Тогда
			Сообщить(" ");
		#КонецЕсли
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
КонецПроцедуры // ОбработатьЗначимоеСобытие()


//////////////////////////////////////////////////////////////////////////////////
// ПРОВЕРКА УСЛОВИЙ ЗНАЧИМОГО СОБЫТИЯ
//////////////////////////////////////////////////////////////////////////////////

// Функция проверяет условия значимого события.
//
// Параметры:
//  ЭлементСсылка  - "СправочникСсылка.ЗначимыеСобытия" - ссылка на элемент справочника,
//					 условия которого нужно проверить.
//
//  Источник  - произвольный объект - объект-источник события.
//
//  Класс  - "Строка" - класс объектов метаданных, которому принадлежит текущий объект-источник.
//
//  Имя  - "Строка" - имя объекта метаданных соответствующего объекту-источнику.
//
//  Событие  - "Строка" - обрабатываемое событие.
//
// Возвращаемое значение:
//   "Булево"   - Если все проверки успешно пройдены, то возвращается Истина, иначе Ложь.
// 
Функция ПроверитьУсловияЗначимогоСобытия(Знач ЭлементСсылка, Знач Источник, Знач Класс, Знач Имя)
	
	// Если у элемента справочника "Значимые события" установлен флаг "Обрабатывать события только для новых объектов", 
	// то необходимо произвести эту проверку.
	Если ЭлементСсылка.ТолькоНовыйОбъект Тогда
		Если Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
			Если НЕ Источник.ДополнительныеСвойства.ЭтоНовый Тогда
				Возврат Ложь;			
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СсылочныйТип = Ложь;
	ЭтоКонстанта = Ложь;
	ИмяТаблицы   = ПолучитьИмяТаблицыПоМетаданным(Класс, Имя, СсылочныйТип, ЭтоКонстанта);
	    
	// Сначала проверяем простые условия.
	// Проверять будем при помощи запроса. В зависимости от класса объекта-источника, текст запроса будет различен.
	РезультатПроверки = Истина;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Использование", Истина);
	СтруктураПоиска.Вставить("ВидУсловия",    Ложь);
	мсвСтрок = ЭлементСсылка.Условия.НайтиСтроки(СтруктураПоиска); 	
	Если мсвСтрок.Количество()<>0 Тогда 	
		Если ЭтоКонстанта Тогда			
			РезультатПроверки = ПроверитьПростыеУсловияКонстанты(мсвСтрок, Имя);	
		Иначе
			Если СсылочныйТип Тогда
				РезультатПроверки = ПроверитьПростыеУсловияСсылочногоТипа(мсвСтрок, Источник, ИмяТаблицы);				
			Иначе
				РезультатПроверки = ПроверитьПростыеУсловияНеСсылочногоТипа(мсвСтрок, Источник, ИмяТаблицы);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
	Если НЕ РезультатПроверки Тогда  // Если уже Ложь, то проверять дальше смысла нет.
		Возврат Ложь;
	КонецЕсли;
		
	// Теперь проверяем произвольные условия
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Использование", Истина);
	СтруктураПоиска.Вставить("ВидУсловия",    Истина);
	мсвСтрок = ЭлементСсылка.Условия.НайтиСтроки(СтруктураПоиска);
	Если мсвСтрок.Количество()<>0 Тогда
		Для Каждого ТекУсловие Из мсвСтрок Цикл			
			Результат = Неопределено;
			Объект    = Источник; 						
			Попытка
				Выполнить(ТекУсловие.ТекстПроизвольногоУсловия);
			Исключение	
				Результат = Неопределено
			КонецПопытки;
			Если ТипЗнч(Результат)=Тип("Булево") Тогда
				Если НЕ Результат Тогда
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;  			
		КонецЦикла;
	КонецЕсли;
	
	Возврат Истина;	
КонецФункции // ПроверитьУсловияЗначимогоСобытия() 

// Функция проверяет простые условия объекта-источника вида "Константа".
// Проверка выполняется посредством запроса.
//
// Параметры:
//  мсвСтрок  - "Массив" - массив строк табличной части "Условия" элемента справочника "Значимые события",
//						   соответствующих простым условиям.                   
// 
//  ИмяКонстанты  - "Строка" - имя константы.
//
// Возвращаемое значение:
//   "Булево" - в случае успешной проверки всех условий возвращается Истина.
//
Функция ПроверитьПростыеУсловияКонстанты(мсвСтрок, ИмяКонстанты)
	
	ТекстЗапроса = "";	
	
	Для Сч=0 По (мсвСтрок.Количество()-1) Цикл  	
		ТекУсловие = мсвСтрок[Сч];
		
		Если ПустаяСтрока(ТекУсловие.ПутьКДанным) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекстЗапроса="" Тогда
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	" + ИмяКонстанты +  "
			|ИЗ
			|	Константы 
			|{ГДЕ
			|";			
		Иначе
			ТекстЗапроса = ТекстЗапроса + "," + Символы.ПС; 			
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ТекУсловие.ПутьКДанным + ".*";		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "}";
	Запрос = Новый ПостроительЗапроса(ТекстЗапроса);
	Запрос.ЗаполнитьНастройки();
	
	Для Сч=0 По (мсвСтрок.Количество()-1) Цикл
		ОтборИмя = СтрЗаменить(мсвСтрок[Сч].ПутьКДанным,".","");
		
		Если НЕ Запрос.ДоступныеПоля[ОтборИмя] = Неопределено И
			Запрос.ДоступныеПоля[ОтборИмя].ТипЗначения = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)) Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос.Отбор.Добавить(ОтборИмя);
		Запрос.Отбор[ОтборИмя].Использование = Истина;
		Запрос.Отбор[ОтборИмя].Значение 	 = мсвСтрок[Сч].ЗначениеСравнения;
		
		Сравнение = ВидСравнения.Равно;
		Если мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.Больше Тогда
			Сравнение = ВидСравнения.Больше;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.БольшеИлиРавно Тогда
			Сравнение = ВидСравнения.БольшеИлиРавно;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.Меньше Тогда
			Сравнение = ВидСравнения.Меньше;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.МеньшеИлиРавно Тогда
			Сравнение = ВидСравнения.МеньшеИлиРавно;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.НеРавно Тогда
			Сравнение = ВидСравнения.НеРавно;
		КонецЕсли;			
		
		Запрос.Отбор[ОтборИмя].ВидСравнения  = Сравнение;
	КонецЦикла;
	
	Запрос.Выполнить();
	
	Если Запрос.Результат.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 
КонецФункции // ПроверитьПростыеУсловияКонстанты()

// Функция проверяет простые условия объекта-источника ссылочного вида.
//
// Параметры:
//  мсвСтрок  - "Массив" - массив строк табличной части "Условия" элемента справочника "Значимые события",
//                 		   соответствующих простым условиям.
//  Источник  - произвольный объект - объект-источник события.
//
//  ИмяТаблицы  - "Строка" - имя таблицы БД, соответствующей объекту-источнику.
//
// Возвращаемое значение:
//   "Булево" - в случае успешной проверки всех условий возвращается Истина.
//
Функция ПроверитьПростыеУсловияСсылочногоТипа(мсвСтрок, Источник, ИмяТаблицы)
	
	ТекстЗапроса = "";	
	
	Для Сч=0 По (мсвСтрок.Количество()-1) Цикл  	
		ТекУсловие = мсвСтрок[Сч];
		
		Если ПустаяСтрока(ТекУсловие.ПутьКДанным) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекстЗапроса="" Тогда
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	Ссылка
			|ИЗ
			|	" + ИмяТаблицы + " 
			|{ГДЕ
			|	Ссылка.*,
			|";			
		Иначе
			ТекстЗапроса = ТекстЗапроса + "," + Символы.ПС; 			
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ТекУсловие.ПутьКДанным + ".* КАК " + СтрЗаменить(мсвСтрок[Сч].ПутьКДанным,".","");		
	КонецЦикла;
	ТекстЗапроса = ТекстЗапроса + "}"; 	      	
	Запрос = Новый ПостроительЗапроса(ТекстЗапроса);
	Запрос.ЗаполнитьНастройки();
	
	Запрос.Отбор.Добавить("Ссылка");
	Запрос.Отбор["Ссылка"].Использование = Истина;
	Запрос.Отбор["Ссылка"].Значение 	 = Источник.Ссылка;
	Запрос.Отбор["Ссылка"].ВидСравнения  = ВидСравнения.Равно;
	
	Для Сч=0 По (мсвСтрок.Количество()-1) Цикл
		ОтборИмя = СтрЗаменить(мсвСтрок[Сч].ПутьКДанным,".","");
		
		Если НЕ Запрос.ДоступныеПоля[ОтборИмя] = Неопределено И
			Запрос.ДоступныеПоля[ОтборИмя].ТипЗначения = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)) Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос.Отбор.Добавить(ОтборИмя);
		Запрос.Отбор[ОтборИмя].Использование = Истина;
		Запрос.Отбор[ОтборИмя].Значение 	 = мсвСтрок[Сч].ЗначениеСравнения;
		
		Сравнение = ВидСравнения.Равно;
		Если мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.Больше Тогда
			Сравнение = ВидСравнения.Больше;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.БольшеИлиРавно Тогда
			Сравнение = ВидСравнения.БольшеИлиРавно;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.Меньше Тогда
			Сравнение = ВидСравнения.Меньше;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.МеньшеИлиРавно Тогда
			Сравнение = ВидСравнения.МеньшеИлиРавно;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.НеРавно Тогда
			Сравнение = ВидСравнения.НеРавно;
		КонецЕсли;			
		
		Запрос.Отбор[ОтборИмя].ВидСравнения  = Сравнение;
	КонецЦикла;
	
	Запрос.Выполнить();
	
	Если Запрос.Результат.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 
	
КонецФункции // ПроверитьПростыеУсловияСсылочногоТипа() 

// Функция проверяет простые условия объекта-источника ссылочного вида. Только для событий вид "ПередЗаписью".
//
// Параметры:
//  мсвСтрок  - "Массив" - массив строк табличной части "Условия" элемента справочника "Значимые события",
//                 		   соответствующих простым условиям.
//  Источник  - произвольный объект ссылочного типа - объект-источник события.
//
// Возвращаемое значение:
//   "Булево" - в случае успешной проверки всех условий возвращается Истина.
//
Функция ПроверитьПростыеУсловияСсылочногоТипаПередЗаписью(мсвСтрок, Источник)
		
	// Для начала подготовим список условий.
	СписокУсловий = Новый ТаблицаЗначений;
	СписокУсловий.Колонки.Добавить("ПутьКДанным",  Новый ОписаниеТипов("Строка"));
	СписокУсловий.Колонки.Добавить("ВидСравнения");
	СписокУсловий.Колонки.Добавить("ЗначениеСравнения");
	СписокУсловий.Колонки.Добавить("Проверено", Новый ОписаниеТипов("Булево"));
	
	Для Каждого ТекУсловие Из мсвСтрок Цикл  	
		НоваяСтрока = СписокУсловий.Добавить();
		НоваяСтрока.ПутьКДанным       = ТекУсловие.ПутьКДанным;
		НоваяСтрока.ВидСравнения      = ТекУсловие.ВидСравнения; 
		НоваяСтрока.ЗначениеСравнения = ТекУсловие.ЗначениеСравнения;
		НоваяСтрока.Проверено         = Ложь;
	КонецЦикла;
	
	Для Каждого ТекУсловие Из СписокУсловий Цикл
		Если ТекУсловие.Проверено Тогда
			Продолжить;
		КонецЕсли;
		
		// Разбираемся с путем к данным.
		Путь = ТекУсловие.ПутьКДанным;
		Путь = СтрЗаменить(Путь, "Ссылка.", "");
		ВремЗначение = Источник;
		Пока Истина Цикл
			ПозицияТочки = Найти(Путь, ".");
			Если ПозицияТочки=0 Тогда // Значит дошли до конца пути.
				ВремЗначение = ВремЗначение[Путь];
				
				Если ТекУсловие.ВидСравнения=Перечисления.ВидСравнения.Равно Тогда
					Если НЕ (ВремЗначение=ТекУсловие.ЗначениеСравнения) Тогда
						Возврат Ложь;		
					КонецЕсли;
				ИначеЕсли ТекУсловие.ВидСравнения=Перечисления.ВидСравнения.Больше Тогда
					Если НЕ (ВремЗначение>ТекУсловие.ЗначениеСравнения) Тогда
						Возврат Ложь;		
					КонецЕсли;
				ИначеЕсли ТекУсловие.ВидСравнения=Перечисления.ВидСравнения.БольшеИлиРавно Тогда
					Если НЕ (ВремЗначение>=ТекУсловие.ЗначениеСравнения) Тогда
						Возврат Ложь;		
					КонецЕсли;
				ИначеЕсли ТекУсловие.ВидСравнения=Перечисления.ВидСравнения.Меньше Тогда
					Если НЕ (ВремЗначение<ТекУсловие.ЗначениеСравнения) Тогда
						Возврат Ложь;		
					КонецЕсли;
				ИначеЕсли ТекУсловие.ВидСравнения=Перечисления.ВидСравнения.МеньшеИлиРавно Тогда
					Если НЕ (ВремЗначение<=ТекУсловие.ЗначениеСравнения) Тогда
						Возврат Ложь;		
					КонецЕсли;
				ИначеЕсли ТекУсловие.ВидСравнения=Перечисления.ВидСравнения.НеРавно Тогда
					Если НЕ (ВремЗначение<>ТекУсловие.ЗначениеСравнения) Тогда
						Возврат Ложь;		
					КонецЕсли;
				КонецЕсли;
				
				ТекУсловие.Проверено = Истина;
				Прервать;
			Иначе
				ПромежуточныйРеквизит = Лев(Путь, ПозицияТочки-1);
				ВремЗначение = ВремЗначение[ПромежуточныйРеквизит];				
				Путь = Сред(Путь, ПозицияТочки+1);	
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции // ПроверитьПростыеУсловияСсылочногоТипаПередЗаписью() 

// Функция проверяет простые условия объекта-источника не ссылочного вида (наборы записей).
//
// Параметры:
//  мсвСтрок  - "Массив" - массив строк табличной части "Условия" элемента справочника "Значимые события",
//                 		   соответствующих простым условиям.
//  Источник  - произвольный объект - объект-источник события.
//
//  ИмяТаблицы  - "Строка" - имя таблицы БД, соответствующей объекту-источнику.
//
// Возвращаемое значение:
//   "Булево" - в случае успешной проверки всех условий возвращается Истина.
// 
Функция ПроверитьПростыеУсловияНеСсылочногоТипа(мсвСтрок, Источник, ИмяТаблицы)
	
	ТекстЗапроса = "
	| ВЫБРАТЬ *
	| ИЗ
	|" + ИмяТаблицы + "
	|";
	
	ТекстОграничений = ""; 	
	Для Сч=0 По (мсвСтрок.Количество()-1) Цикл  			
		ТекУсловие = мсвСтрок[Сч];
		
		Если ПустаяСтрока(ТекУсловие.ПутьКДанным) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекстОграничений="" Тогда
			ТекстОграничений = "{ ГДЕ ";
		Иначе
			ТекстОграничений = ТекстОграничений + "," + Символы.ПС; 			
		КонецЕсли;
		ТекстОграничений = ТекстОграничений + ТекУсловие.ПутьКДанным + ".* КАК " + СтрЗаменить(мсвСтрок[Сч].ПутьКДанным,".","");	
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ТекстОграничений) Тогда
		ТекстЗапроса = ТекстЗапроса + ТекстОграничений + "}";
	КонецЕсли;
	
	Запрос = Новый ПостроительЗапроса(ТекстЗапроса);
	Запрос.ЗаполнитьНастройки();
	
	Для Сч=0 По (мсвСтрок.Количество()-1) Цикл
		ОтборИмя = СтрЗаменить(мсвСтрок[Сч].ПутьКДанным,".","");
		
		Если НЕ Запрос.ДоступныеПоля[ОтборИмя] = Неопределено И
			Запрос.ДоступныеПоля[ОтборИмя].ТипЗначения = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0)) Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос.Отбор.Добавить(ОтборИмя);
		Запрос.Отбор[ОтборИмя].Использование = Истина;
		Запрос.Отбор[ОтборИмя].Значение 	 = мсвСтрок[Сч].ЗначениеСравнения;
		
		Сравнение = ВидСравнения.Равно;
		Если мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.Больше Тогда
			Сравнение = ВидСравнения.Больше;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.БольшеИлиРавно Тогда
			Сравнение = ВидСравнения.БольшеИлиРавно;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.Меньше Тогда
			Сравнение = ВидСравнения.Меньше;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.МеньшеИлиРавно Тогда
			Сравнение = ВидСравнения.МеньшеИлиРавно;
		ИначеЕсли мсвСтрок[Сч].ВидСравнения = Перечисления.ВидСравнения.НеРавно Тогда
			Сравнение = ВидСравнения.НеРавно;
		КонецЕсли;			
		
		Запрос.Отбор[ОтборИмя].ВидСравнения  = Сравнение;		
	КонецЦикла; 	
	
	Запрос.Выполнить();
	
	Если Запрос.Результат.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 	
	
КонецФункции // ПроверитьПростыеУсловияНеСсылочногоТипа()


//////////////////////////////////////////////////////////////////////////////////
// ВЫПОЛНЕНИЕ ДЕЙСТВИЙ ЗНАЧИМОГО СОБЫТИЯ, ПРОШЕДШЕГО ПРОВЕРКУ.
//////////////////////////////////////////////////////////////////////////////////
                                    
// СОЗДАНИЕ ОБЪЕКТА 

// Функция выполняет действия значимого события. 
//
// Параметры:
//  СобытиеСсылка  - "СправочникСсылка.ЗначимыеСобытия" - ссылка на элемент справочника "Значимые события", 
//														  действия которого необходимо выполнить.
//  Источник  - Произвольный объект - объект-источник события
//
//  КлассИсточника  - "Строка" - строковое представление класса объектов метаданных,
//					  которому принадлежит объект-источник
//
//  ИмяИсточника  - "Строка" - имя объекта метаданных, соответствующего объекту-источнику.
//
Функция ВыполнитьДействияЗначимогоСобытия(СобытиеСсылка, Источник, КлассИсточника, ИмяИсточника)
	// Получим список действий на данное событие и упорядочим его по реквизиту "Порядок"
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДействияНаЗначимыеСобытия.Ссылка,
	|	ДействияНаЗначимыеСобытия.ВидДействия,
	|	ДействияНаЗначимыеСобытия.Порядок КАК Порядок,
	|	ДействияНаЗначимыеСобытия.ПровестиДокумент,
	|	ДействияНаЗначимыеСобытия.ОтправитьSMS,
	|	ДействияНаЗначимыеСобытия.ЗадействоватьВводНаОсновании,
	|   ДействияНаЗначимыеСобытия.ЗаполнитьРеквизитыПоУмолчанию,
	|	ДействияНаЗначимыеСобытия.ТипОбъекта,
	|	ДействияНаЗначимыеСобытия.ПредставлениеТипаОбъекта,
	|	ДействияНаЗначимыеСобытия.Соответствия.(
	|		Ссылка,
	|		ВидПравила,
	|		ПредставлениеРеквизита,
	|		РеквизитОбъекта,
	|		Правило,
	|		ПутьКДанным,
	|		ПроизвольныйКод
	|	),
	|	ДействияНаЗначимыеСобытия.Представление,
	|	ДействияНаЗначимыеСобытия.Наименование,
	|	ДействияНаЗначимыеСобытия.ДатаНапоминания,
	|	ДействияНаЗначимыеСобытия.ДатаНапоминанияПутьКДанным,
	|	ДействияНаЗначимыеСобытия.ДатаНапоминанияПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.ТемаНапоминания,
	|	ДействияНаЗначимыеСобытия.ТемаНапоминанияПутьКДанным,
	|	ДействияНаЗначимыеСобытия.ТемаНапоминанияПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.ПрикрепленныйОбъект,
	|	ДействияНаЗначимыеСобытия.ПрикрепленныйОбъектПутьКДанным,
	|	ДействияНаЗначимыеСобытия.ПрикрепленныйОбъектПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.Содержание,
	|	ДействияНаЗначимыеСобытия.СодержаниеПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.ДатаАктуальности,
	|	ДействияНаЗначимыеСобытия.СрокДоНачала,
	|	ДействияНаЗначимыеСобытия.ТипПериода,
	|	ДействияНаЗначимыеСобытия.УдалитьПоИстеченииСрока,
	|	ДействияНаЗначимыеСобытия.ПолучателиНапоминания.(
	|		Ссылка,
	|		ВидПравила,
	|		Значение,
	|		ПутьКДанным,
	|		ПроизвольныйКод
	|	),
	|	ДействияНаЗначимыеСобытия.жрСобытие,
	|	ДействияНаЗначимыеСобытия.жрСобытиеПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.жрУровеньВажности,
	|	ДействияНаЗначимыеСобытия.жрОбъектМетаданных,
	|	ДействияНаЗначимыеСобытия.жрОбъектМетаданныхИмяОбъекта,
	|	ДействияНаЗначимыеСобытия.жрОбъектМетаданныхПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.жрКомментарий,
	|	ДействияНаЗначимыеСобытия.жрКомментарийПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.жрДанные,
	|	ДействияНаЗначимыеСобытия.жрДанныеПутьКДанным,
	|	ДействияНаЗначимыеСобытия.жрДанныеПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.эпАвтор,
	|	ДействияНаЗначимыеСобытия.эпАвторПутьКДанным,
	|	ДействияНаЗначимыеСобытия.эпАвторПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.эпТема,
	|	ДействияНаЗначимыеСобытия.эпТемаПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.эпВажность,
	|	ДействияНаЗначимыеСобытия.эпВажностьПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.эпУчетнаяЗапись,
	|	ДействияНаЗначимыеСобытия.эпУчетнаяЗаписьПутьКДанным,
	|	ДействияНаЗначимыеСобытия.эпУчетнаяЗаписьПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.эпКомментарий,
	|	ДействияНаЗначимыеСобытия.эпКомментарийПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.эпФорматТекста,
	|	ДействияНаЗначимыеСобытия.эпТекстПисьма,
	|	ДействияНаЗначимыеСобытия.эпТекстПисьмаПроизвольныйКод,
	|	ДействияНаЗначимыеСобытия.КонецПериода,
	|	ДействияНаЗначимыеСобытия.ВидПериода,
	|	ДействияНаЗначимыеСобытия.ПолучателиПисьма.(
	|		Ссылка,
	|		НомерСтроки,
	|		ВидПравила,
	|		АдресЭлектроннойПочты,
	|		ПроизвольныйКод,
	|		Представление,
	|		КодГруппыАдреса
	|	),
	|	ДействияНаЗначимыеСобытия.эпВариантЗаписи,
	|	ДействияНаЗначимыеСобытия.ШаблонОбъект,
	|	ДействияНаЗначимыеСобытия.ШаблонИдентификатор,
	|	ДействияНаЗначимыеСобытия.ПроизвольныйКод
	|ИЗ
	|	Справочник.ДействияНаЗначимыеСобытия КАК ДействияНаЗначимыеСобытия
	|ГДЕ
	|	ДействияНаЗначимыеСобытия.ПометкаУдаления = ЛОЖЬ
	|	И ДействияНаЗначимыеСобытия.Владелец = &Владелец
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Владелец", СобытиеСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество()=0 Тогда
		Возврат Ложь;
	КонецЕсли;

	Параметры = Новый Структура;
	Параметры.Вставить("ОбъектИсточникСобытия", Источник); 	
	Параметры.Вставить("КлассИсточника",        КлассИсточника);
	Параметры.Вставить("ИмяИсточника",          ИмяИсточника);
	Параметры.Вставить("ВыполненныеДействия",   Новый Массив);
	Параметры.Вставить("Ошибки",                Новый Массив);
	Параметры.Вставить("СозданныеОбъекты",      Новый Массив);
	
	РежимВыполнения       = СобытиеСсылка.РежимВыполнения;
	ОшибкаСозданияОбъекта = Ложь;
	
	// Если задан режим выполнения "в транзакции", то инициализируем транзакцию.
	Если СобытиеСсылка.РежимВыполнения=Перечисления.РежимыВыполненияДействий.ВТранзакции Тогда
		НачатьТранзакцию();	
	КонецЕсли; 	
	
	Пока Выборка.Следующий() Цикл	
				
		Если Выборка.ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьИЗаписатьОбъект Тогда 			
			// СОЗДАНИЕ ПРОИЗВОЛЬНОГО ОБЪЕКТА	
			СозданныйОбъект = СоздатьИЗаписатьОбъект(Выборка, Параметры);	
			
		ИначеЕсли Выборка.ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьНапоминание Тогда
			// СОЗДАТЬ НАПОМИНАНИЕ			
			СозданныйОбъект = СоздатьНапоминание(Выборка, Параметры);	
			
		ИначеЕсли Выборка.ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.СоздатьЗаписьЖурналаРегистрации Тогда
			// СОЗДАТЬ ЗАПИСЬ В ЖУРНАЛЕ РЕГИСТРАЦИИ				
			СозданныйОбъект = СоздатьЗаписьЖурналаРегистрации(Выборка, Параметры);	
			
		ИначеЕсли Выборка.ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.НаписатьЭлектронноеПисьмо Тогда
			// НАПИСАТЬ ЭЛЕКТРОННОЕ ПИСЬМО			
			СозданныйОбъект = СоздатьЭлектронноеПисьмо(Выборка, Параметры);
		ИначеЕсли Выборка.ВидДействия=Перечисления.ВидДействияНаЗначимоеСобытие.Прочее Тогда
			// ПРОЧЕЕ ДЕЙСТВИЕ			
			ВыполнитьПрочееДействие(Выборка, Параметры);
		КонецЕсли;
		
		// Созданный объект сохраняем в массиве для доступа к нему в последующих действиях.
		// Объект "вставляется" в массив под номером равным его порядку.
		// Если текущий объект создать не удалось, то соот-но запишется "Неопределено"		
		Параметры.СозданныеОбъекты.Вставить(Выборка.Порядок, СозданныйОбъект); 
		
		Если СозданныйОбъект=Неопределено Тогда
			ОшибкаСозданияОбъекта = Истина;
			Если РежимВыполнения=Перечисления.РежимыВыполненияДействий.ВТранзакции Тогда					
				Прервать;
			ИначеЕсли РежимВыполнения=Перечисления.РежимыВыполненияДействий.Прерывать Тогда
				Прервать;	
			КонецЕсли;
		КонецЕсли;   		
	КонецЦикла;	
	
	// Если действия выполнялись в транзакции, то зафиксируем ее (или откатим)
	ОткатТранзакции = Ложь;	
	Если РежимВыполнения=Перечисления.РежимыВыполненияДействий.ВТранзакции Тогда
		Если ОшибкаСозданияОбъекта Тогда
			ОтменитьТранзакцию();
			// устанавливаем признак отмены транзакции и очищаем массив сообщений об
			// успешно выполненных действиях.
			ОткатТранзакции = Истина;
			Параметры.ВыполненныеДействия.Очистить();
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЕсли;	
	
	// Теперь выводим сообщения с информацией о созданных объектах, а также ругательства.
	// Вывод возможен только в файловом режиме работы.
	#Если Клиент Тогда 
		Если Параметры.Ошибки.Количество()<>0 Тогда
			Если ОткатТранзакции Тогда
				Сообщить("При выполнении действий значимого события <" + СобытиеСсылка + "> обнаружены ошибки. Все действия отменены.", СтатусСообщения.Важное);
			Иначе
				Сообщить("При выполнении действий значимого события <" + СобытиеСсылка + "> обнаружены ошибки.", СтатусСообщения.Важное);
			КонецЕсли;
			
			Для Каждого ТекОшибка Из Параметры.Ошибки Цикл
				Сообщить(" - " + ТекОшибка, СтатусСообщения.Важное);	
			КонецЦикла;
		КонецЕсли;
		
		Если Параметры.ВыполненныеДействия.Количество()<>0 Тогда 
			Сообщить("Выполнены следующие действия значимого события <" + СобытиеСсылка + ">:", СтатусСообщения.Информация);
			Для Каждого ТекРезультат Из Параметры.ВыполненныеДействия Цикл
				Сообщить(" - " + ТекРезультат, СтатусСообщения.Информация);
			КонецЦикла;
		КонецЕсли;
	#КонецЕсли
	
	Возврат Истина;
	
КонецФункции // ВыполнитьДействияЗначимогоСобытия() 

// Функция выполняет создание объекта произвольного вида.
// А также ряд дополнительных действий. Например, проведение документа.
//
// Параметры:
//  Выборка  - "ВыборкаИзРезультатЗапроса" - спозиционированная выборка из запроса.
//
//  Параметры  - "Структура" - структура необходимых параметров.
//				 ОбъектИсточникСобытия - Произвольный объект - объект-источник.
//				 КлассИсточника        - "Строка" - строковое представление класса объектов метаданных,
//				 к которому принадлежит объект-источник.
//               ИмяИсточника		   - "Строка" - имя объекта метаданных, соответствующего объекту-источнику.
//               ВыполненныеДействия   - "Массив" - массив информационных строк о выполненных действиях.  
//               Ошибки				   - "Массив" - массив информационных строк о произошедших ошибках. 
//               ВыполненныеДействия   - "Массив" - массив созданных в предыдущих действиях объектов. 
//													Индексы элементов в массиве соответствуют значениям реквизита "Порядок".
//
// Возвращаемое значение:
//   В случае успешного создания объекта возвращает созданный объект. 
//	 Если произошла ошибка, что возвращается НЕОПРЕДЕЛЕНО.
//
Функция СоздатьИЗаписатьОбъект(Выборка, Параметры)
	
	ТипОбъекта = Выборка.ТипОбъекта;
	Если ПустаяСтрока(ТипОбъекта) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Получаем класс объекта метаданных, к которому принадлежит СОЗДАВАЕМЫЙ объект.
	ОбъектМетаданных = Неопределено;
	КлассОбъекта     = ПолучитьКлассОбъектаМетаданных(ТипОбъекта, ОбъектМетаданных);
	
	Если ОбъектМетаданных=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если КлассОбъекта="Константы" Тогда
		НовыйОбъект = Константы[ОбъектМетаданных.Имя].СоздатьМенеджерЗначения();
		
	ИначеЕсли КлассОбъекта="Справочники" Тогда
		НовыйОбъект = Справочники[ОбъектМетаданных.Имя].СоздатьЭлемент();
		
	ИначеЕсли КлассОбъекта="Документы" Тогда
		НовыйОбъект = Документы[ОбъектМетаданных.Имя].СоздатьДокумент();   		
		
	ИначеЕсли КлассОбъекта="ПланыВидовХарактеристик" Тогда
		НовыйОбъект = ПланыВидовХарактеристик[ОбъектМетаданных.Имя].СоздатьЭлемент();
		
	ИначеЕсли КлассОбъекта="ПланыСчетов" Тогда
		НовыйОбъект = ПланыСчетов[ОбъектМетаданных.Имя].СоздатьСчет();           
		
	ИначеЕсли КлассОбъекта="ПланыВидовРасчета" Тогда
		НовыйОбъект = ПланыВидовРасчета[ОбъектМетаданных.Имя].СоздатьВидРасчета(); 		
		
	ИначеЕсли КлассОбъекта="РегистрыСведений" Тогда
		НаборЗаписей = РегистрыСведений[ОбъектМетаданных.Имя].СоздатьНаборЗаписей();
		НовыйОбъект  = НаборЗаписей.Добавить();
		
	ИначеЕсли КлассОбъекта="БизнесПроцессы" Тогда
		НовыйОбъект = БизнесПроцессы[ОбъектМетаданных.Имя].СоздатьБизнесПроцесс(); 		
		
	ИначеЕсли КлассОбъекта="Задачи" Тогда
		НовыйОбъект = Задачи[ОбъектМетаданных.Имя].СоздатьЗадачу();
		
	ИначеЕсли КлассОбъекта="ПланыОбмена" Тогда
		НовыйОбъект = ПланыОбмена[ОбъектМетаданных.Имя].СоздатьУзел(); 		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	// Если нужно, задействуем механизм ввода на основании.
	Попытка
		Если Выборка.ЗаполнитьРеквизитыПоУмолчанию Тогда
			НовыйОбъект.Заполнить(Неопределено);	
		ИначеЕсли Выборка.ЗадействоватьВводНаОсновании Тогда
			НовыйОбъект.Заполнить(Параметры.ОбъектИсточникСобытия.Ссылка);			
		КонецЕсли;	
	Исключение
	КонецПопытки;
	
	// ТОЧНЫЕ ЗНАЧЕНИЯ
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ВидПравила", Перечисления.ВидыПравил.ТочноеЗначение);
	тзСоответствия = Выборка.Соответствия.Выгрузить();	
	мсвПравил      = тзСоответствия.НайтиСтроки(СтруктураПоиска);
	Для Каждого ТекПравило Из мсвПравил Цикл
		Попытка
			Если КлассОбъекта="Константы" Тогда
				НовыйОбъект.Значение = ТекПравило.Правило;
				Прервать;
			Иначе
				НовыйОбъект[СтрЗаменить(ТекПравило.РеквизитОбъекта, "Ссылка.", "")] = ТекПравило.Правило;
			КонецЕсли;
		Исключение 			
		КонецПопытки;
	КонецЦикла;
	
	// РЕКВИЗИТЫ ОБЪЕКТА-ИСТОЧНИКА
	мсвПоляВыборки = Новый Массив;	
	СтруктураПоиска.Вставить("ВидПравила", Перечисления.ВидыПравил.РеквизитОбъектаИсточника);
	мсвПравил = тзСоответствия.НайтиСтроки(СтруктураПоиска);
	Для Каждого ТекПравило Из мсвПравил Цикл
		Если НЕ ПустаяСтрока(ТекПравило.ПутьКДанным) Тогда
			Если КлассОбъекта="Константы" Тогда
				мсвПоляВыборки.Добавить("" + ТекПравило.ПутьКДанным + " КАК " + "Значение");
				Прервать;
			Иначе
				РеквизитОбъектаИсточника = СтрЗаменить(ТекПравило.ПутьКДанным, "Ссылка.", "");
				РеквизитНовогоОбъекта    = СтрЗаменить(ТекПравило.РеквизитОбъекта, "Ссылка.", "");
				мсвПоляВыборки.Добавить("" + РеквизитОбъектаИсточника + " КАК " + РеквизитНовогоОбъекта);	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если мсвПоляВыборки.Количество()<>0 Тогда
		СсылочныйТип = Ложь;
		ЭтоКонстанта = Ложь;
		ИмяТаблицы = ПолучитьИмяТаблицыПоМетаданным(Параметры.КлассИсточника, Параметры.ИмяИсточника, СсылочныйТип, ЭтоКонстанта);
		Если ЭтоКонстанта или СсылочныйТип Тогда 			
			ТекстЗапроса = "ВЫБРАТЬ ";
			Для Сч=0 По (мсвПоляВыборки.Количество()-1) Цикл
				ТекстЗапроса = ТекстЗапроса + " " + мсвПоляВыборки[Сч];
				Если Сч<(мсвПоляВыборки.Количество()-1) Тогда
					ТекстЗапроса = ТекстЗапроса + ",";		
				КонецЕсли;
			КонецЦикла;
			ТекстЗапроса = ТекстЗапроса + " ИЗ ";
			ТекстЗапроса = ТекстЗапроса + ИмяТаблицы;			
			Если СсылочныйТип Тогда
				ТекстЗапроса = ТекстЗапроса + " ГДЕ Ссылка=&Ссылка";			
			КонецЕсли;
			
			Запрос = Новый Запрос(ТекстЗапроса);
			Если СсылочныйТип Тогда
				Запрос.УстановитьПараметр("Ссылка", Параметры.ОбъектИсточникСобытия.Ссылка);			
			КонецЕсли;
			
			ТекВыборка = Запрос.Выполнить().Выбрать();
			
			Если ТекВыборка.Следующий() Тогда  				
				Для Каждого ТекПравило Из мсвПравил Цикл					
					Реквизит = СтрЗаменить(ТекПравило.РеквизитОбъекта, "Ссылка.", "");
					Попытка
						Если КлассОбъекта="Константы" Тогда
							НовыйОбъект.Значение = ТекВыборка.Значение;
							Прервать;
						Иначе	
							НовыйОбъект[Реквизит] = ТекВыборка[Реквизит];	
						КонецЕсли;
					Исключение
						Если КлассОбъекта="Константы" Тогда
							Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установке значения константы. Неверное значение реквизита объекта-источника: "+ОписаниеОшибки());	
						Иначе
							Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установке реквизита <" + ТекПравило.ПредставлениеРеквизита + ">. Неверное значение реквизита объекта-источника: "+ОписаниеОшибки());
						КонецЕсли;						
					КонецПопытки;					
				КонецЦикла; 				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	// ПРОИЗВОЛЬНЫЙ КОД
	СтруктураПоиска.Вставить("ВидПравила", Перечисления.ВидыПравил.ПроизвольныйКод);
	мсвПравил = тзСоответствия.НайтиСтроки(СтруктураПоиска);
	Для Каждого ТекПравило Из мсвПравил Цикл 
		ЗначениеРеквизита = Неопределено;
		ОбъектИсточник    = Параметры.ОбъектИсточникСобытия;
		ТекущийОбъект     = НовыйОбъект;
		СозданныеОбъекты  = Параметры.СозданныеОбъекты;
		
		Реквизит = СтрЗаменить(ТекПравило.РеквизитОбъекта, "Ссылка.", "");
		Попытка
			Выполнить(ТекПравило.ПроизвольныйКод);			
			Попытка
				Если КлассОбъекта="Константы" Тогда
					НовыйОбъект.Значение = ЗначениеРеквизита;
					Прервать;
				Иначе
					НовыйОбъект[Реквизит] = ЗначениеРеквизита;
				КонецЕсли;   
			Исключение
				Если КлассОбъекта="Константы" Тогда
					Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установке значения константы. Не соответствие типов: "+ОписаниеОшибки());	
				Иначе
					Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установке реквизита <" + ТекПравило.ПредставлениеРеквизита + ">. Не соответствие типов: "+ОписаниеОшибки());
				КонецЕсли;
			КонецПопытки;  			
		Исключение 	
			Если КлассОбъекта="Константы" Тогда
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установке значения константы. Ошибка выполнения произвольного кода: "+ОписаниеОшибки());
			Иначе  				
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установке реквизита <" + ТекПравило.ПредставлениеРеквизита + ">. Ошибка выполнения произвольного кода: "+ОписаниеОшибки());
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;	
	
	//Если был указан шаблон для заполнения то применим его
	Если НЕ(Выборка.ШаблонИдентификатор = "") Тогда
		ПараметрыИмени = Новый Структура("Объект,Идентификатор");
		ПараметрыИмени.Вставить("Объект",Выборка.ШаблонОбъект);
		ПараметрыИмени.Вставить("Идентификатор",Выборка.ШаблонИдентификатор);
		дкЗагрузитьРеквизитыДокументаИзШаблона(НовыйОбъект,ПараметрыИмени);
	КонецЕсли;
	
	// ЗАПИСЬ ОБЪЕКТА
	// Пометим объект, чтобы исключить бесконечную рекурсию.
	Если КлассОбъекта="РегистрыСведений" Тогда
		НаборЗаписей.ДополнительныеСвойства.Вставить("СозданПоЗначимомуСобытию", Истина);
	Иначе
		НовыйОбъект.ДополнительныеСвойства.Вставить("СозданПоЗначимомуСобытию", Истина);
	КонецЕсли; 
	
	// Если записывается объект ссылочного типа, то установим номер\код (если он не указан)
	Если КлассОбъекта="Документы" Тогда
		// У документов уже прописано автоматическая нумерация в процедуре общего модуля.
		
	ИначеЕсли КлассОбъекта="Справочники"       или 
		КлассОбъекта="ПланыВидовХарактеристик" или
		КлассОбъекта="ПланыСчетов"             или
		КлассОбъекта="ПланыВидовРасчета"       или
		КлассОбъекта="БизнесПроцессы"          или
		КлассОбъекта="Задачи"                  или
		КлассОбъекта="ПланыОбмена" Тогда
		
		Если обЗначениеНеЗаполнено(НовыйОбъект.Код) Тогда
			Попытка
				НовыйОбъект.УстановитьНовыйКод();	
			Исключение
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установке номера: "+ОписаниеОшибки());
				Возврат Неопределено; 
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
	Если КлассОбъекта="Документы" Тогда
		Попытка 
			НовыйОбъект.Записать(РежимЗаписиДокумента.Запись);
			Параметры.ВыполненныеДействия.Добавить("Записан документ: " + НовыйОбъект);	
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при записи документа: "+ОписаниеОшибки());
			Возврат Неопределено; 			
		КонецПопытки;
		
		Если Выборка.ПровестиДокумент Тогда
			Попытка 				
				НовыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
				Параметры.ВыполненныеДействия.Добавить("Проведен документ: " + НовыйОбъект);				
			Исключение
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при проведении документа <" + НовыйОбъект + ">: "+ОписаниеОшибки());
				Возврат Неопределено;	
			КонецПопытки;
		КонецЕсли; 
		
		
		Если Выборка.ОтправитьSMS Тогда
			//отправка SMS получателям
			Попытка
				
				Если НЕ ЗначениеЗаполнено(НовыйОбъект.НачалоОтправки) Тогда
					НовыйОбъект.НачалоОтправки = ТекущаяДата();
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(НовыйОбъект.НомерОтправителя) Тогда
					
					НовыйОбъект.НомерОтправителя = Константы.смсИмяПользователяПоУмолчанию.Получить();
					Если НЕ ЗначениеЗаполнено(НовыйОбъект.НомерОтправителя) Тогда
						//Формирование списка номеров всех доступных отправителей
						СписокНомеровОтправителя = Новый СписокЗначений;
						СтруктураПараметров = смсРаботаССообщениями.ПолучитьПараметры();
						Если СтруктураПараметров.Свойство("НомераОтправителя") Тогда
							СписокНомеровОтправителя = смсКоммуникатор.ПолучитьСписокНомеровИзСтроки(СтруктураПараметров.НомераОтправителя);
						КонецЕсли;
						Если СписокНомеровОтправителя.Количество()> 0 Тогда
							НовыйОбъект.НомерОтправителя = СписокНомеровОтправителя[0];
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
				Для Каждого ТекЗначение Из НовыйОбъект.Получатели Цикл
					Текст = ТекЗначение.ТекстСообщения;
					Если СокрЛП(Текст)="" Тогда
						Шаблон                 = НовыйОбъект.ТекстСообщения;
						Контрагент = ТекЗначение.Контрагент;
						СтрокаОшибкиФункции = "";
						Текст = НовыйОбъект.СформироватьСообщениеПоШаблону(Шаблон, Контрагент, СтрокаОшибкиФункции);
						ТекЗначение.ТекстСообщения = Текст;
						Если НЕ СтрокаОшибкиФункции = "" Тогда
							Параметры.ВыполненныеДействия.Добавить(СтрокаОшибкиФункции);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				НовыйОбъект.Записать();
				СтрокаОшибки = НовыйОбъект.ОтправитьSMS();
				Если СтрокаОшибки = "" Тогда
					Параметры.ВыполненныеДействия.Добавить("Отправлен SMS: " + СокрЛП(НовыйОбъект));
					//кмПолучитьСтатусSMS(НовыйОбъект);
					НовыйОбъект.Записать(РежимЗаписиДокумента.Запись);
				Иначе
					Параметры.ВыполненныеДействия.Добавить("Не отправлен SMS: " + СокрЛП(НовыйОбъект) + ". По причине: " + СтрокаОшибки);
				КонецЕсли;
				
			Исключение
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при отправке SMS <" + НовыйОбъект + ">: "+ОписаниеОшибки());
				Возврат Неопределено;
			КонецПопытки;
		КонецЕсли;
		
	ИначеЕсли КлассОбъекта="РегистрыСведений" Тогда 				
		// Реквизиты записи соответсвующие измерениям должны быть обязательно заполнены, 
		// чтобы по ним можно было установить отбор набора записей.		
		Если ОбъектМетаданных.РежимЗаписи<>Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			Если обЗначениеНеЗаполнено(НовыйОбъект.Регистратор) Тогда
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не заполнен реквизит ""Регистратор""");
			Иначе
				НаборЗаписей.Отбор["Регистратор"].Установить(НовыйОбъект.Регистратор);
			КонецЕсли;			
		Иначе 			
			Для Каждого ТекИзмерение Из ОбъектМетаданных.Измерения Цикл
				Если обЗначениеНеЗаполнено(НовыйОбъект[ТекИзмерение.Имя]) Тогда
					Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не заполнен реквизит """ + ТекИзмерение.Представление() + """");
				Иначе
					НаборЗаписей.Отбор[ТекИзмерение.Имя].Установить(НовыйОбъект[ТекИзмерение.Имя]);				
				КонецЕсли;						
			КонецЦикла;	
			
			Если ОбъектМетаданных.ПериодичностьРегистраСведений<>Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
				Если обЗначениеНеЗаполнено(НовыйОбъект.Период) Тогда
					Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не заполнен реквизит ""Период""");
				Иначе
					НаборЗаписей.Отбор["Период"].Установить(НовыйОбъект.Период);
				КонецЕсли;
			КонецЕсли; 			
		КонецЕсли;  		
		
		Если Параметры.Ошибки.Количество()<>0 Тогда
			Возврат Неопределено;
		Иначе
			Попытка
				НаборЗаписей.Записать();
				Параметры.ВыполненныеДействия.Добавить("Создана запись регистра сведений <" + ОбъектМетаданных.Имя + ">");
			Исключение
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при записи объекта <" + НовыйОбъект + ">: "+ОписаниеОшибки());
				Возврат Неопределено;				
			КонецПопытки;
		КонецЕсли;
	Иначе
		Попытка
			НовыйОбъект.Записать();
			Если КлассОбъекта="Константы" Тогда
				Параметры.ВыполненныеДействия.Добавить("Установлено значение константы: " + НовыйОбъект.Метаданные().Представление());	
			Иначе
				Параметры.ВыполненныеДействия.Добавить("Создан объект: " + НовыйОбъект);
			КонецЕсли;
		Исключение
			Если КлассОбъекта="Константы" Тогда
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при установлении значения константы <" + НовыйОбъект.Метаданные().Представление() + ">: "+ОписаниеОшибки());
			Иначе
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при записи объекта <" + НовыйОбъект + ">: "+ОписаниеОшибки());
			КонецЕсли;
			
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Возврат НовыйОбъект;
КонецФункции // СоздатьИЗаписатьОбъект()
                             
// СОЗДАНИЕ НАПОМИНАНИЯ

// Функция выполняет создание записи регистра сведений "Напоминания".
//
// Параметры:
//  Выборка  - "ВыборкаИзРезультатЗапроса" - спозиционированная выборка из запроса.
//
//  Параметры  - "Структура" - структура необходимых параметров.
//				 ОбъектИсточникСобытия - Произвольный объект - объект-источник.
//				 КлассИсточника        - "Строка" - строковое представление класса объектов метаданных,
//				 к которому принадлежит объект-источник.
//               ИмяИсточника		   - "Строка" - имя объекта метаданных, соответствующего объекту-источнику.
//               ВыполненныеДействия   - "Массив" - массив информационных строк о выполненных действиях.  
//               Ошибки				   - "Массив" - массив информационных строк о произошедших ошибках. 
//               ВыполненныеДействия   - "Массив" - массив созданных в предыдущих действиях объектов. 
//													Индексы элементов в массиве соответствуют значениям реквизита "Порядок".
//
// Возвращаемое значение:
//   В случае успешного создания напоминания возвращает созданный набор записей. 
//	 Если произошла ошибка, что возвращается НЕОПРЕДЕЛЕНО.
//
Функция СоздатьНапоминание(ВыборкаИзРезультатЗапроса, Параметры) 	
		
	мсвПоляВыборки        = Новый Массив;
	ДатаНапоминания       = Дата("00010101");
	ТемаНапоминания       = "";
	ПрикрепленныйОбъект   = Неопределено;
	Содержание            = "";
	Результат             = Истина;
	
	// Сначала вычислим значение даты начала напоминания
	Если НЕ ПустаяСтрока(ВыборкаИзРезультатЗапроса.ДатаНапоминанияПутьКДанным) Тогда
		РеквизитОбъектаИсточника = СтрЗаменить(ВыборкаИзРезультатЗапроса.ДатаНапоминанияПутьКДанным, "Ссылка.", "");
		мсвПоляВыборки.Добавить(РеквизитОбъектаИсточника + " КАК ДатаНапоминания");
		
	ИначеЕсли НЕ ПустаяСтрока(ВыборкаИзРезультатЗапроса.ДатаНапоминанияПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			ДатаНапоминания  = Неопределено;
			Выполнить(ВыборкаИзРезультатЗапроса.ДатаНапоминанияПроизвольныйКод);
			Если ТипЗнч(ДатаНапоминания)<>Тип("Дата") Тогда
				ДатаНапоминания = Дата("00010101");
			КонецЕсли;
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - ошибка при заполнении реквизита <Дата начала>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Если ТипЗнч(ВыборкаИзРезультатЗапроса.ДатаНапоминания)=Тип("Дата") Тогда
			ДатаНапоминания = ВыборкаИзРезультатЗапроса.ДатаНапоминания;		
		КонецЕсли;
	КонецЕсли;
	
	
	// Теперь тема напоминания
	Если НЕ ПустаяСтрока(ВыборкаИзРезультатЗапроса.ТемаНапоминанияПутьКДанным) Тогда
		РеквизитОбъектаИсточника = СтрЗаменить(ВыборкаИзРезультатЗапроса.ТемаНапоминанияПутьКДанным, "Ссылка.", "");
		мсвПоляВыборки.Добавить(РеквизитОбъектаИсточника + " КАК ТемаНапоминания");	
		
	ИначеЕсли НЕ ПустаяСтрока(ВыборкаИзРезультатЗапроса.ТемаНапоминанияПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			ТемаНапоминания  = Неопределено;
			Выполнить(ВыборкаИзРезультатЗапроса.ТемаНапоминанияПроизвольныйКод);
			// Возможно переменной "ТемаНапоминания" присвоено не строковое значение. Преобразуем в строку.
			ТемаНапоминания = Строка(ТемаНапоминания);			
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - ошибка при заполнении реквизита <Тема напоминания>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе		
		ТемаНапоминания = ВыборкаИзРезультатЗапроса.ТемаНапоминания;				
	КонецЕсли;
	
	
	// Теперь прикрепляемый объект
	Если НЕ ПустаяСтрока(ВыборкаИзРезультатЗапроса.ПрикрепленныйОбъектПутьКДанным) Тогда		
		РеквизитОбъектаИсточника = СтрЗаменить(ВыборкаИзРезультатЗапроса.ПрикрепленныйОбъектПутьКДанным, "Ссылка.", "");
		мсвПоляВыборки.Добавить(РеквизитОбъектаИсточника + " КАК ПрикрепленныйОбъект");
		
	ИначеЕсли НЕ ПустаяСтрока(ВыборкаИзРезультатЗапроса.ПрикрепленныйОбъектПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник      = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты    = Параметры.СозданныеОбъекты;
			ПрикрепленныйОбъект = Неопределено;
			Выполнить(ВыборкаИзРезультатЗапроса.ПрикрепленныйОбъектПроизвольныйКод);			
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - ошибка при заполнении реквизита <Объект>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе		
		ПрикрепленныйОбъект = ВыборкаИзРезультатЗапроса.ПрикрепленныйОбъект;				
	КонецЕсли;	
	
	
	// Содержание напоминания
	Если НЕ ПустаяСтрока(ВыборкаИзРезультатЗапроса.СодержаниеПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник      = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты    = Параметры.СозданныеОбъекты;
			Содержание          = Неопределено;
			Выполнить(ВыборкаИзРезультатЗапроса.СодержаниеПроизвольныйКод);			
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - ошибка при заполнении реквизита ""Содержание"". Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе		
		Содержание = ВыборкаИзРезультатЗапроса.Содержание;				
	КонецЕсли; 	
	
		
	// Теперь пользователи-получатели
	тзПолучателиРезультат = Новый ТаблицаЗначений;
	тзПолучателиРезультат.Колонки.Добавить("Пользователь", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	
	тзПолучателиВыгрузка = ВыборкаИзРезультатЗапроса.ПолучателиНапоминания.Выгрузить();
	Для Каждого ТекСтрока Из тзПолучателиВыгрузка Цикл  	
		Если ТекСтрока.ВидПравила=Перечисления.ВидыПравил.ТочноеЗначение Тогда
			Если ТипЗнч(ТекСтрока.Значение)=Тип("СправочникСсылка.Пользователи") Тогда
				Если НЕ ТекСтрока.Значение.Пустая() Тогда
					НоваяСтрока = тзПолучателиРезультат.Добавить();	
					НоваяСтрока.Пользователь = ТекСтрока.Значение;
				КонецЕсли;
			КонецЕсли;
				
		ИначеЕсли ТекСтрока.ВидПравила=Перечисления.ВидыПравил.ПроизвольныйКод Тогда
			Попытка
				ОбъектИсточник      = Параметры.ОбъектИсточникСобытия;
				СозданныеОбъекты    = Параметры.СозданныеОбъекты;
				Пользователь        = Неопределено;
				Выполнить(ТекСтрока.ПроизвольныйКод);
				Если ТипЗнч(Пользователь)=Тип("СправочникСсылка.Пользователи") Тогда
					Если НЕ Пользователь.Пустая() Тогда
						НоваяСтрока = тзПолучателиРезультат.Добавить();	
						НоваяСтрока.Пользователь = Пользователь; 						
					КонецЕсли;
					
				ИначеЕсли ТипЗнч(Пользователь)=Тип("Массив") Тогда
					Для Каждого ТекПользователь Из Пользователь Цикл  	
						Если ТипЗнч(ТекПользователь)=Тип("СправочникСсылка.Пользователи") Тогда
							Если НЕ ТекПользователь.Пустая() Тогда
								НоваяСтрока = тзПолучателиРезультат.Добавить();	
								НоваяСтрока.Пользователь = ТекПользователь; 						
							КонецЕсли;		
						КонецЕсли;		
					КонецЦикла;	
				КонецЕсли;					
			Исключение
				Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - ошибка при заполнении реквизита <Пользователи>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
			КонецПопытки;				
		Иначе
			РеквизитОбъектаИсточника = СтрЗаменить(ТекСтрока.ПутьКДанным, "Ссылка.", "");
			мсвПоляВыборки.Добавить(РеквизитОбъектаИсточника + " КАК Пользователь_" + мсвПоляВыборки.Количество());   		
		КонецЕсли;
	КонецЦикла;
	
	// Заполняем реквизиты, которые заполняются по объекту-источнику
	Если мсвПоляВыборки.Количество()<>0 Тогда
		СсылочныйТип = Ложь;
		ЭтоКонстанта = Ложь;
		ИмяТаблицы = ПолучитьИмяТаблицыПоМетаданным(Параметры.КлассИсточника, Параметры.ИмяИсточника, СсылочныйТип, ЭтоКонстанта);
		Если ЭтоКонстанта или СсылочныйТип Тогда
			ТекстЗапроса = "ВЫБРАТЬ ";
			Для Сч=0 По (мсвПоляВыборки.Количество()-1) Цикл
				ТекстЗапроса = ТекстЗапроса + " " + мсвПоляВыборки[Сч];
				Если Сч<(мсвПоляВыборки.Количество()-1) Тогда
					ТекстЗапроса = ТекстЗапроса + ",";
				КонецЕсли;
			КонецЦикла;
			ТекстЗапроса = ТекстЗапроса + " ИЗ ";
			ТекстЗапроса = ТекстЗапроса + ИмяТаблицы;
			Если СсылочныйТип Тогда
				ТекстЗапроса = ТекстЗапроса + " ГДЕ Ссылка=&Ссылка";
			КонецЕсли;
			
			Запрос = Новый Запрос(ТекстЗапроса);
			Если СсылочныйТип Тогда
				Запрос.УстановитьПараметр("Ссылка", Параметры.ОбъектИсточникСобытия.Ссылка);
			КонецЕсли;
			
			РезультатЗапроса = Запрос.Выполнить();
			ТекВыборка = Запрос.Выполнить().Выбрать();
			
			Если ТекВыборка.Следующий() Тогда 
				Для Каждого ТекКолонка Из РезультатЗапроса.Колонки Цикл  	
					Если Найти(ТекКолонка.Имя, "Пользователь")=0 Тогда
						Выполнить(ТекКолонка.Имя + " = ТекВыборка[ТекКолонка.Имя];");	
					Иначе
						НоваяСтрока = тзПолучателиРезультат.Добавить();	
						НоваяСтрока.Пользователь = ТекВыборка[ТекКолонка.Имя]; 	
					КонецЕсли;
				КонецЦикла; 				
			КонецЕсли;			
		КонецЕсли; 		
	КонецЕсли;  	
	
	// Возможно после выполнения запроса, типы значений переменных не соответствуют их назначению.
	Если ТипЗнч(ДатаНапоминания)<>Тип("Дата") Тогда
		ДатаНапоминания = Дата("00010101");
	КонецЕсли;
	Если ТипЗнч(ТемаНапоминания)<>Тип("Строка") Тогда
		ТемаНапоминания = Строка(ТемаНапоминания);
	КонецЕсли;
	Если ТипЗнч(Содержание)<>Тип("Строка") Тогда
		Содержание = "";
	КонецЕсли;  
	
	// Если не заполнены обязательные реквизиты, то поднимем флаг ошибки, а также добавим описание ошибки в массив ошибок.
	Если обЗначениеНеЗаполнено(ДатаНапоминания) Тогда
		Результат = Ложь;
		Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - не заполнен реквизит <Дата начала>.");
	КонецЕсли;
	
	Если ПустаяСтрока(ТемаНапоминания) Тогда
		Результат = Ложь;
		Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - не заполнен реквизит <Тема напоминания>.");
	КонецЕсли;  
	
	тзПолучателиРезультат.Свернуть("Пользователь");
	Если тзПолучателиРезультат.Количество()=0 Тогда
		Результат = Ложь;
		Параметры.Ошибки.Добавить("Действие <" + ВыборкаИзРезультатЗапроса.Наименование + "> - не выбрано ни одного пользователя.");
	КонецЕсли; 
	
	Если НЕ Результат Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Все необходимые реквизиты заполнены. Можно создавать напоминание.
	ПараметрыНапоминания = Новый Структура;
	
	ДатаАктуальности = Неопределено;
	Если ЗначениеЗаполнено(ВыборкаИзРезультатЗапроса.ДатаАктуальности) Тогда
		ДатаАктуальности = ВыборкаИзРезультатЗапроса.ДатаАктуальности;
	ИначеЕсли ЗначениеЗаполнено(ВыборкаИзРезультатЗапроса.КонецПериода) Тогда
			Итератор = 0; Индекс = ВыборкаИзРезультатЗапроса.ВидПериода;
			
			Если Индекс = 0 Тогда
				Итератор = 60;
			ИначеЕсли Индекс = 1 Тогда
				Итератор = 3600;
			ИначеЕсли Индекс = 2 Тогда
				Итератор = 86400;
			ИначеЕсли Индекс = 3 Тогда
				Итератор = 604800;
			ИначеЕсли Индекс = 4 Тогда //месяцев
				Итератор = -1;
				ДатаАктуальности = ДобавитьМесяц(ДатаНапоминания,ВыборкаИзРезультатЗапроса.КонецПериода);
			ИначеЕсли Индекс = 5 Тогда //лет
				Итератор = -1;
				Год = Год(ДатаНапоминания) + ВыборкаИзРезультатЗапроса.КонецПериода;
				Попытка
					ДатаАктуальности = Дата(Год, Месяц(ДатаНапоминания), День(ДатаНапоминания), Час(ДатаНапоминания), Минута(ДатаНапоминания), Секунда(ДатаНапоминания));
				Исключение
				КонецПопытки;
				Если ТипЗнч(ДатаАктуальности) <> Тип("Дата") Тогда //(29 февраля)
					Попытка
						ДатаАктуальности = Дата(Год, 3, 1, Час(ДатаНапоминания), Минута(ДатаНапоминания), Секунда(ДатаНапоминания));
					Исключение
					КонецПопытки;
				КонецЕсли;
			Иначе 
				Итератор = 0;
			КонецЕсли;
			
			Если Итератор > 0 Тогда
				ДатаАктуальности = ДатаНапоминания + Итератор*ВыборкаИзРезультатЗапроса.КонецПериода;
			КонецЕсли;
	КонецЕсли;
	
	ПараметрыНапоминания.Вставить("ДатаАктуальности", ДатаАктуальности);
	ПараметрыНапоминания.Вставить("ДатаНачала",       ДатаНапоминания);
	ПараметрыНапоминания.Вставить("ДатаОповещения",   ДатаНапоминания);
	ПараметрыНапоминания.Вставить("Описание",         Содержание);
	ПараметрыНапоминания.Вставить("Редактирование",   Ложь);
	ПараметрыНапоминания.Вставить("СрокДоНачала",     ВыборкаИзРезультатЗапроса.СрокДоНачала);
	ПараметрыНапоминания.Вставить("Тема",             ТемаНапоминания);
	ПараметрыНапоминания.Вставить("ТипПериода",       ВыборкаИзРезультатЗапроса.ТипПериода);
	ПараметрыНапоминания.Вставить("УдалитьПоИстеченииСрока", ВыборкаИзРезультатЗапроса.УдалитьПоИстеченииСрока);
	
	ПараметрыНапоминания.Вставить("Пользователи",     тзПолучателиРезультат);
	ПараметрыНапоминания.Вставить("Автор",            ПараметрыСеанса.Пользователь);
	ПараметрыНапоминания.Вставить("Завершено",        Ложь);
	ПараметрыНапоминания.Вставить("РеальнаяДатаОповещения", ДатаНапоминания);
	ПараметрыНапоминания.Вставить("Объект",           ПрикрепленныйОбъект);  
	
	НаборЗаписейНапоминания = СформироватьЗаписьРСНапоминания(ПараметрыНапоминания, ВыборкаИзРезультатЗапроса, Параметры);
	Возврат НаборЗаписейНапоминания;
КонецФункции // СоздатьНапоминание()

// Выполнение произвольного действия
//
// Параметры:
//  Выборка  - "ВыборкаИзРезультатЗапроса" - спозиционированная выборка из запроса.
//
//  Параметры  - "Структура" - структура необходимых параметров.
//				 ОбъектИсточникСобытия - Произвольный объект - объект-источник.
//				 КлассИсточника        - "Строка" - строковое представление класса объектов метаданных,
//				 к которому принадлежит объект-источник.
//               ИмяИсточника		   - "Строка" - имя объекта метаданных, соответствующего объекту-источнику.
//               ВыполненныеДействия   - "Массив" - массив информационных строк о выполненных действиях.  
//               Ошибки				   - "Массив" - массив информационных строк о произошедших ошибках. 
//               ВыполненныеДействия   - "Массив" - массив созданных в предыдущих действиях объектов. 
//													Индексы элементов в массиве соответствуют значениям реквизита "Порядок".
Процедура ВыполнитьПрочееДействие(ВыборкаИзРезультатЗапроса, Параметры)
	
	Попытка
		Выполнить(ВыборкаИзРезультатЗапроса.ПроизвольныйКод);
	Исключение
		#Если Клиент Тогда
		Сообщить(ОписаниеОшибки());
		#КонецЕсли		
	КонецПопытки;
	
КонецПроцедуры


// Сформировать запись регистра сведений(Напоминания)
Функция СформироватьЗаписьРСНапоминания(ПараметрыНапоминания, Выборка, Параметры)
		
	// Произведем расчет даты напоминания.
	НоваяДата = ПараметрыНапоминания.ДатаНачала;
	Рез       = опНайтиБлижайшуюДату(ПараметрыНапоминания, НоваяДата);		
	Если Рез Тогда 
		ПараметрыНапоминания.ДатаОповещения = НоваяДата;
		ПараметрыНапоминания.ДатаНачала     = НоваяДата;
		ПараметрыНапоминания.РеальнаяДатаОповещения = Макс(ТекущаяДата(), ПараметрыНапоминания.ДатаНачала - ПараметрыНапоминания.СрокДоНачала) ;
		// Проверим
		Если НЕ обЗначениеНеЗаполнено(ПараметрыНапоминания.ДатаАктуальности) и (ПараметрыНапоминания.РеальнаяДатаОповещения>=ПараметрыНапоминания.ДатаАктуальности) Тогда
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - неправильно установлена дата актуальности!");
			Возврат Неопределено;
		КонецЕсли;			
	Иначе  			
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не удалось определить дату оповещения.");
		Возврат Неопределено; 	
	КонецЕсли; 	
	
	// Создаем набор записей
	НаборЗаписей = РегистрыСведений.Напоминания.СоздатьНаборЗаписей();
	
	// Исключаем бесконечный цикл
	НаборЗаписей.ДополнительныеСвойства.Вставить("СозданПоЗначимомуСобытию", Истина);
	
	НаборЗаписей.Отбор.Автор.Установить(ПараметрыНапоминания.Автор);
	НаборЗаписей.Отбор.Завершено.Установить(ПараметрыНапоминания.Завершено);
	НаборЗаписей.Отбор.РеальнаяДатаОповещения.Установить(ПараметрыНапоминания.РеальнаяДатаОповещения); 	
	НаборЗаписей.Отбор.Объект.Установить(ПараметрыНапоминания.Объект);	
	
	// Количество записей будет равно количеству пользователей, которые получат напоминание.
	Для Каждого ТекСтрока Из ПараметрыНапоминания.Пользователи Цикл		
		НоваяЗапись = НаборЗаписей.Добавить();  		
		НоваяЗапись.Пользователь = ТекСтрока.Пользователь;
		НоваяЗапись.Автор        = ПараметрыНапоминания.Автор;
		НоваяЗапись.Завершено    = ПараметрыНапоминания.Завершено;
		НоваяЗапись.РеальнаяДатаОповещения = ПараметрыНапоминания.РеальнаяДатаОповещения;
		НоваяЗапись.Объект       = ПараметрыНапоминания.Объект;
		
		// Теперь ресурсы
		НоваяЗапись.ДатаНачала              = ПараметрыНапоминания.ДатаНачала; 
		НоваяЗапись.ДатаОповещения          = ПараметрыНапоминания.ДатаОповещения; 
		НоваяЗапись.СрокДоНачала            = ПараметрыНапоминания.СрокДоНачала; 
		НоваяЗапись.Тема                    = ПараметрыНапоминания.Тема; 
		НоваяЗапись.ТипПериода              = ПараметрыНапоминания.ТипПериода; 
		НоваяЗапись.ДатаАктуальности        = ПараметрыНапоминания.ДатаАктуальности; 
		НоваяЗапись.Описание                = ПараметрыНапоминания.Описание; 
		НоваяЗапись.УдалитьПоИстеченииСрока = ПараметрыНапоминания.УдалитьПоИстеченииСрока; 
		НоваяЗапись.Редактирование = Ложь;					                           		
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать(Истина);
		Параметры.ВыполненныеДействия.Добавить("Создано напоминание.");
	Исключение
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при записи: "+ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НаборЗаписей;
КонецФункции // СформироватьЗаписьРСНапоминания() 
                        
// ЗАПИСЬ СОБЫТИЯ В ЖУРНАЛ РЕГИСТРАЦИИ

// Функция выполняет создание записи в журнале регистрации.
//
// Параметры:
//  Выборка  - "ВыборкаИзРезультатЗапроса" - спозиционированная выборка из запроса.
//
//  Параметры  - "Структура" - структура необходимых параметров.
//				 ОбъектИсточникСобытия - Произвольный объект - объект-источник.
//				 КлассИсточника        - "Строка" - строковое представление класса объектов метаданных,
//				 к которому принадлежит объект-источник.
//               ИмяИсточника		   - "Строка" - имя объекта метаданных, соответствующего объекту-источнику.
//               ВыполненныеДействия   - "Массив" - массив информационных строк о выполненных действиях.  
//               Ошибки				   - "Массив" - массив информационных строк о произошедших ошибках. 
//               ВыполненныеДействия   - "Массив" - массив созданных в предыдущих действиях объектов. 
//													Индексы элементов в массиве соответствуют значениям реквизита "Порядок".
//
// Возвращаемое значение:
//   В случае успешного создания записи в журнале регистрации возвращает пустую строку. 
//	 Если произошла ошибка, что возвращается НЕОПРЕДЕЛЕНО.
//
Функция СоздатьЗаписьЖурналаРегистрации(Выборка, Параметры)
	Событие          = "";
	УровеньВажности  = Неопределено;
	ОбъектМетаданных = Неопределено;
	Данные           = Неопределено;  
	Комментарий      = "";
	Результат        = Истина;
	 	
	// Определяем регистрируемое событие
	Если НЕ ПустаяСтрока(Выборка.жрСобытиеПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			Событие          = Неопределено;
			Выполнить(Выборка.жрСобытиеПроизвольныйКод);
			Событие = Строка(Событие); 			
		Исключение
			Событие = "";
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Регистрируемое событие>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе   		
		Событие = Выборка.жрСобытие;		
	КонецЕсли;
	
	Если ПустаяСтрока(Событие) Тогда
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - обязательный реквизит <Регистрируемое событие> - не заполнен.");
		Результат = Ложь;		
	КонецЕсли;
	
	// Определяем уровень важности
	Если Выборка.жрУровеньВажности="Информация" Тогда
		УровеньВажности	= УровеньЖурналаРегистрации.Информация;
	ИначеЕсли  Выборка.жрУровеньВажности="Ошибка" Тогда
		УровеньВажности	= УровеньЖурналаРегистрации.Ошибка
	ИначеЕсли  Выборка.жрУровеньВажности="Предупреждение" Тогда
		УровеньВажности	= УровеньЖурналаРегистрации.Предупреждение
	ИначеЕсли  Выборка.жрУровеньВажности="Примечание" Тогда
		УровеньВажности	= УровеньЖурналаРегистрации.Примечание
	Иначе
		УровеньВажности = Неопределено;
	КонецЕсли;
	
	// Определяем объект метаданных
	Если НЕ ПустаяСтрока(Выборка.жрОбъектМетаданныхИмяОбъекта) Тогда
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Выборка.жрОбъектМетаданныхИмяОбъекта);		
	ИначеЕсли НЕ ПустаяСтрока(Выборка.жрОбъектМетаданныхПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			ОбъектМетаданных = Неопределено;
			Выполнить(Выборка.жрОбъектМетаданныхПроизвольныйКод);
			Если ТипЗнч(ОбъектМетаданных)<>Тип("ОбъектМетаданных") Тогда
				ОбъектМетаданных = Неопределено;
			КонецЕсли;
		Исключение
			ОбъектМетаданных = Неопределено;
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Объект метаданных>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки; 
	Иначе
		ОбъектМетаданных = Неопределено;	
	КонецЕсли;   	
	
	// Теперь данные
	Если НЕ ПустаяСтрока(Выборка.жрДанныеПутьКДанным) Тогда
		// Запрос   				
		СсылочныйТип = Ложь;
		ЭтоКонстанта = Ложь;
		ИмяТаблицы = ПолучитьИмяТаблицыПоМетаданным(Параметры.КлассИсточника, Параметры.ИмяИсточника, СсылочныйТип, ЭтоКонстанта);
		Если ЭтоКонстанта или СсылочныйТип Тогда  			
			ТекстЗапроса = "ВЫБРАТЬ 
			| " + СтрЗаменить(Выборка.жрДанныеПутьКДанным, "Ссылка.", "") + " КАК Данные ";
			ТекстЗапроса = ТекстЗапроса + " ИЗ ";
			ТекстЗапроса = ТекстЗапроса + ИмяТаблицы;			
			Если СсылочныйТип Тогда
				ТекстЗапроса = ТекстЗапроса + " ГДЕ Ссылка=&Ссылка";			
			КонецЕсли;
			
			Запрос = Новый Запрос(ТекстЗапроса);
			Если СсылочныйТип Тогда
				Запрос.УстановитьПараметр("Ссылка", Параметры.ОбъектИсточникСобытия.Ссылка);			
			КонецЕсли;
			
			РезультатЗапроса = Запрос.Выполнить();
			ТекВыборка = Запрос.Выполнить().Выбрать();
			
			Если ТекВыборка.Следующий() Тогда 
				Данные = ТекВыборка.Данные;				
			КонецЕсли;			
		КонецЕсли;		

	ИначеЕсли НЕ ПустаяСтрока(Выборка.жрДанныеПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			Данные           = Неопределено;
			Выполнить(Выборка.жрДанныеПроизвольныйКод);
		Исключение
			Данные = Неопределено;
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Данные>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки; 
	Иначе
		Данные = Выборка.жрДанные;	
	КонецЕсли; 

	// Комментарий
	Если НЕ ПустаяСтрока(Выборка.жрКомментарийПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			Комментарий      = Неопределено;
			Выполнить(Выборка.жрКомментарийПроизвольныйКод);			
			Комментарий = Строка(Комментарий);			
		Исключение
			Комментарий = "";
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Комментарий>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки; 
	Иначе
		Комментарий = Выборка.жрКомментарий;	
	КонецЕсли;
	
	
	// СОЗДАНИЕ ЗАПИСИ
	Попытка
		ЗаписьЖурналаРегистрации(Событие, УровеньВажности, ОбъектМетаданных, Данные, Комментарий, РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
		Параметры.ВыполненныеДействия.Добавить("Записано событие <" + Событие + "> в журнал регистрации."); 
	Исключение
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка записи события в журнал регистрации: "+ОписаниеОшибки()); 
	КонецПопытки;
	
	// Т.к. ссылаться на запись в журнале регистрации нельзя, то просто вернем
	// пустую строку (а не НЕОПРЕДЕЛЕНО), тогда ошибка не будет зафиксирована.
	Возврат "";	
КонецФункции // СоздатьЗаписьЖурналаРегистрации() 
                        
// ЭЛЕКТРОННОЕ ПИСЬМО

// Функция выполняет создание документа "Электронное письмо" и его отправку.
//
// Параметры:
//  Выборка  - "ВыборкаИзРезультатЗапроса" - спозиционированная выборка из запроса.
//
//  Параметры  - "Структура" - структура необходимых параметров.
//				 ОбъектИсточникСобытия - Произвольный объект - объект-источник.
//				 КлассИсточника        - "Строка" - строковое представление класса объектов метаданных,
//				к которому принадлежит объект-источник.
//               ИмяИсточника		   - "Строка" - имя объекта метаданных, соответствующего объекту-источнику.
//               ВыполненныеДействия   - "Массив" - массив информационных строк о выполненных действиях.  
//               Ошибки				   - "Массив" - массив информационных строк о произошедших ошибках. 
//               ВыполненныеДействия   - "Массив" - массив созданных в предыдущих действиях объектов. 
//													Индексы элементов в массиве соответствуют значениям реквизита "Порядок".
//
// Возвращаемое значение:
//   В случае успешного создания документа, возвращает объект-документ. 
//	 Если произошла ошибка, что возвращается НЕОПРЕДЕЛЕНО.
//
Функция СоздатьЭлектронноеПисьмо(Выборка, Параметры)
	
	НовыйДокумент = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
	ТекущийОбъект = НовыйДокумент;
	
	Автор         = Справочники.Пользователи.ПустаяСсылка();
	УчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка();
	Тема          = "";
	Комментарий   = "";
	ТекстПисьма   = "";
	Важность      = Перечисления.Важность.Средняя;
	ФорматТекста  = Перечисления.ФорматТекста.ПростойТекст;
	
	мсвПоляВыборки = Новый Массив;
	
	// Вычисляем значение реквизита Автор
	Если НЕ ПустаяСтрока(Выборка.эпАвторПутьКДанным) Тогда
		РеквизитОбъектаИсточника = СтрЗаменить(Выборка.эпАвторПутьКДанным, "Ссылка.", "");
		мсвПоляВыборки.Добавить(РеквизитОбъектаИсточника + " КАК Автор");
		
	ИначеЕсли НЕ ПустаяСтрока(Выборка.эпАвторПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			Автор            = Неопределено; 
			Выполнить(Выборка.эпАвторПроизвольныйКод);
			Если ТипЗнч(Автор)<>Тип("СправочникСсылка.Пользователи") Тогда
				Автор =  Справочники.Пользователи.ПустаяСсылка();
			КонецЕсли;
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Автор электронного письма>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Если ТипЗнч(Выборка.эпАвтор)=Тип("СправочникСсылка.Пользователи") Тогда
			Автор = Выборка.эпАвтор;		
		КонецЕсли;
	КонецЕсли;
	
	// Вычисляем значение реквизита Учетная запись
	Если НЕ ПустаяСтрока(Выборка.эпУчетнаяЗаписьПутьКДанным) Тогда
		РеквизитОбъектаИсточника = СтрЗаменить(Выборка.эпУчетнаяЗаписьПутьКДанным, "Ссылка.", "");
		мсвПоляВыборки.Добавить(РеквизитОбъектаИсточника + " КАК УчетнаяЗапись");
		
	ИначеЕсли НЕ ПустаяСтрока(Выборка.эпУчетнаяЗаписьПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			УчетнаяЗапись    = Неопределено; 
			Выполнить(Выборка.эпУчетнаяЗаписьПроизвольныйКод);
			Если ТипЗнч(УчетнаяЗапись)<>Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
				УчетнаяЗапись =  Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка();
			КонецЕсли;
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Учетная запись>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Если ТипЗнч(Выборка.эпУчетнаяЗапись)=Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
			УчетнаяЗапись = Выборка.эпУчетнаяЗапись;		
		КонецЕсли;
	КонецЕсли; 	

	// Вычисляем значение реквизита Тема
	Если НЕ ПустаяСтрока(Выборка.эпТемаПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			Тема             = Неопределено; 
			Выполнить(Выборка.эпТемаПроизвольныйКод);
			Если ТипЗнч(Тема)<>Тип("Строка") Тогда
				Тема =  Строка(Тема);
			КонецЕсли;
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Тема>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Если ТипЗнч(Выборка.эпТема)=Тип("Строка") Тогда
			Тема = Выборка.эпТема;		
		КонецЕсли;
	КонецЕсли;
     	
	// Вычисляем значение реквизита Комментарий
	Если НЕ ПустаяСтрока(Выборка.эпКомментарийПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			Комментарий      = Неопределено; 
			Выполнить(Выборка.эпКомментарийПроизвольныйКод);
			Если ТипЗнч(Комментарий)<>Тип("Строка") Тогда
				Комментарий =  Строка(Комментарий);
			КонецЕсли;
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Комментарий>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Если ТипЗнч(Выборка.эпКомментарий)=Тип("Строка") Тогда
			Комментарий = Выборка.эпКомментарий;		
		КонецЕсли;
	КонецЕсли; 
	
	// Вычисляем значение реквизита Важность
	Если НЕ ПустаяСтрока(Выборка.эпВажностьПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			Важность         = Неопределено; 
			Выполнить(Выборка.эпВажностьПроизвольныйКод);
			Если ТипЗнч(Важность)<>Тип("ПеречислениеСсылка.Важность") Тогда
				Важность =  Перечисления.Важность.Средняя;
			КонецЕсли;
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Важность>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Если ТипЗнч(Выборка.эпВажность)=Тип("ПеречислениеСсылка.Важность") Тогда
			Важность = Выборка.эпВажность;		
		КонецЕсли;
	КонецЕсли; 
	
	// Вычисляем значение реквизита Текст письма
	Если НЕ ПустаяСтрока(Выборка.эпТекстПисьмаПроизвольныйКод) Тогда
		Попытка
			ОбъектИсточник   = Параметры.ОбъектИсточникСобытия;
			СозданныеОбъекты = Параметры.СозданныеОбъекты;
			ТекстПисьма      = Неопределено; 
			Выполнить(Выборка.эпТекстПисьмаПроизвольныйКод);
			Если ТипЗнч(ТекстПисьма)<>Тип("Строка") Тогда
				ТекстПисьма =  Строка(ТекстПисьма);
			КонецЕсли;
		Исключение
			Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении реквизита <Текст письма>. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
		КонецПопытки;
	Иначе
		Если ТипЗнч(Выборка.эпТекстПисьма)=Тип("Строка") Тогда
			ТекстПисьма = Выборка.эпТекстПисьма;		
		КонецЕсли;
	КонецЕсли;

	// Вычисляем значение реквизита Формат текста
	Если НЕ Выборка.эпФорматТекста.Пустая() Тогда
		ФорматТекста = Выборка.эпФорматТекста;	
	КонецЕсли;
	
	
	// Заполняем реквизиты, которые заполняются по объекту-источнику
	Если мсвПоляВыборки.Количество()<>0 Тогда
		СсылочныйТип = Ложь;
		ЭтоКонстанта = Ложь;
		ИмяТаблицы = ПолучитьИмяТаблицыПоМетаданным(Параметры.КлассИсточника, Параметры.ИмяИсточника, СсылочныйТип, ЭтоКонстанта);
		Если ЭтоКонстанта или СсылочныйТип Тогда  			
			ТекстЗапроса = "ВЫБРАТЬ ";
			Для Сч=0 По (мсвПоляВыборки.Количество()-1) Цикл
				ТекстЗапроса = ТекстЗапроса + " " + мсвПоляВыборки[Сч];
				Если Сч<(мсвПоляВыборки.Количество()-1) Тогда
					ТекстЗапроса = ТекстЗапроса + ",";		
				КонецЕсли;
			КонецЦикла;
			ТекстЗапроса = ТекстЗапроса + " ИЗ ";
			ТекстЗапроса = ТекстЗапроса + ИмяТаблицы;			
			Если СсылочныйТип Тогда
				ТекстЗапроса = ТекстЗапроса + " ГДЕ Ссылка=&Ссылка";			
			КонецЕсли;
			
			Запрос = Новый Запрос(ТекстЗапроса);
			Если СсылочныйТип Тогда
				Запрос.УстановитьПараметр("Ссылка", Параметры.ОбъектИсточникСобытия.Ссылка);			
			КонецЕсли;
			
			РезультатЗапроса = Запрос.Выполнить();
			ТекВыборка = Запрос.Выполнить().Выбрать();
			
			Если ТекВыборка.Следующий() Тогда 
				Для Каждого ТекКолонка Из РезультатЗапроса.Колонки Цикл  	
					Если ТекКолонка.Имя="Автор" Тогда   						
						Если ТипЗнч(ТекВыборка[ТекКолонка.Имя])=Тип("СправочникСсылка.Пользователи") Тогда		
							Автор = ТекВыборка[ТекКолонка.Имя];
						КонецЕсли;
					ИначеЕсли ТекКолонка.Имя="УчетнаяЗапись" Тогда
						Если ТипЗнч(ТекВыборка[ТекКолонка.Имя])=Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда		
							УчетнаяЗапись = ТекВыборка[ТекКолонка.Имя]; 
						КонецЕсли;
					КонецЕсли;
				КонецЦикла; 				
			КонецЕсли;			
		КонецЕсли; 		
	КонецЕсли;
	
	// Заполняем получателей письма
	тзПолучателиПисьма  = Новый ТаблицаЗначений;
	тзПолучателиПисьма.Колонки.Добавить("АдресЭлектроннойПочты");
	тзПолучателиПисьма.Колонки.Добавить("Представление");
	тзПолучателиПисьма.Колонки.Добавить("КодГруппыАдреса");
	
	тзПолучателиВыгрузка = Выборка.ПолучателиПисьма.Выгрузить();
	Для Каждого ТекСтрока Из тзПолучателиВыгрузка Цикл  	
		Если ТекСтрока.ВидПравила=Перечисления.ВидыПравил.ТочноеЗначение Тогда  			
			Если НЕ ПустаяСтрока(ТекСтрока.АдресЭлектроннойПочты) Тогда
				НоваяСтрока = тзПолучателиПисьма.Добавить();	
				НоваяСтрока.АдресЭлектроннойПочты = ТекСтрока.АдресЭлектроннойПочты;
				НоваяСтрока.Представление         = ТекСтрока.Представление;
				НоваяСтрока.КодГруппыАдреса       = ТекСтрока.КодГруппыАдреса;
			КонецЕсли;  			
			
		ИначеЕсли ТекСтрока.ВидПравила=Перечисления.ВидыПравил.ПроизвольныйКод Тогда
			Попытка
				ОбъектИсточник        = Параметры.ОбъектИсточникСобытия;
				СозданныеОбъекты      = Параметры.СозданныеОбъекты;
				АдресЭлектроннойПочты = Неопределено;
				Выполнить(ТекСтрока.ПроизвольныйКод);
				Если ТипЗнч(АдресЭлектроннойПочты)=Тип("Строка") Тогда
					Если НЕ ПустаяСтрока(АдресЭлектроннойПочты) Тогда
						НоваяСтрока = тзПолучателиПисьма.Добавить();	
						НоваяСтрока.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
						НоваяСтрока.Представление         = "";
						НоваяСтрока.КодГруппыАдреса       = ТекСтрока.КодГруппыАдреса;						
					КонецЕсли;
				КонецЕсли;					
			Исключение
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при заполнении получателей электронного письма. Ошибка при выполнении произвольного кода: "+ОписаниеОшибки());
			КонецПопытки;			
		КонецЕсли;
	КонецЦикла;

	// Теперь проверим заполненность обязательных реквизитов
	Результат = Истина;
	Если Автор.Пустая() Тогда
		Результат = Ложь;
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не заполнен реквизит <Автор>.");
	КонецЕсли;
	
	Если УчетнаяЗапись.Пустая() Тогда
		Результат = Ложь;
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не заполнен реквизит <Учетная запись>.");
	КонецЕсли;
	
	Если ПустаяСтрока(Тема) Тогда
		Результат = Ложь;
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не заполнен реквизит <Тема>.");
	КонецЕсли;
	
	Если тзПолучателиПисьма.Количество()=0 Тогда
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не выбрано ни одного получателя электронного письма.");
		Результат = Ложь;		
	КонецЕсли;
	
	Если НЕ Результат Тогда
		Возврат Неопределено;
	КонецЕсли;
	

	// Найдем группу писем, которой будет принадлежать письмо.
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ГруппыПисемЭлектроннойПочты.Ссылка
	|ИЗ
	|	Справочник.ГруппыПисемЭлектроннойПочты КАК ГруппыПисемЭлектроннойПочты
	|ГДЕ
	|	ГруппыПисемЭлектроннойПочты.Владелец = &Владелец
	|	И ГруппыПисемЭлектроннойПочты.Наименование = ""Исходящие""";
	ЗапросПоискГруппы = Новый Запрос(ТекстЗапроса);
	ЗапросПоискГруппы.УстановитьПараметр("Владелец", УчетнаяЗапись);
	ВремВыборка = ЗапросПоискГруппы.Выполнить().Выбрать();
	Если ВремВыборка.Следующий() Тогда
		ГруппаПисьма = ВремВыборка.Ссылка;	
	Иначе
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не найдена папка писем учетной записи ""Исходящие"".");
		Возврат Неопределено;
	КонецЕсли;
	
	// Получим представление отправителя
	ОтправительПредставление = ?(СокрЛП(УчетнаяЗапись.Пользователь) = "", "<", УчетнаяЗапись.Пользователь + " <") + УчетнаяЗапись.АдресЭлектроннойПочты + ">" ;
	
	// Получаем вариант записи электронного письма
	ВариантЗаписи = Выборка.эпВариантЗаписи;
	
	
	// СОЗДАНИЕ ДОКУМЕНТА "ЭЛЕКТРОННОЕ ПИСЬМО"
	//НовыйДокумент = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
	// Исключаем бесконечный цикл
	НовыйДокумент.ДополнительныеСвойства.Вставить("СозданПоЗначимомуСобытию", Истина);
	
	НовыйДокумент.Дата                     = ТекущаяДата();
	НовыйДокумент.УстановитьНовыйНомер();
	НовыйДокумент.Автор	                   = Автор;
	НовыйДокумент.ПодразделениеКомпании    = Автор.Подразделение;
	НовыйДокумент.Организация              = Автор.Организация;
	НовыйДокумент.Важность                 = ?(Важность.Пустая(), Перечисления.Важность.Средняя, Важность);
	НовыйДокумент.ГруппаПисем          	   = ГруппаПисьма;
	НовыйДокумент.ЕстьВложения             = Ложь;
	НовыйДокумент.Комментарий              = Комментарий;
	НовыйДокумент.НеРассмотрено            = Ложь;
	НовыйДокумент.Ответ                    = Ложь;
	НовыйДокумент.ОтправительПредставление = ОтправительПредставление;
	НовыйДокумент.Переадресация            = Ложь;
	НовыйДокумент.ТекстПисьма              = ТекстПисьма;
	НовыйДокумент.Тема                     = Тема;
	НовыйДокумент.УчетнаяЗапись            = УчетнаяЗапись;   
	НовыйДокумент.ФорматТекста             = ФорматТекста;	
	НовыйДокумент.ХозОперация              = Справочники.ХозОперации.ЭлектронноеПисьмо;
	
	
	// Теперь заполняем табличную часть документа "Получатели"
	// За одно сформируем представление всех получателей.
	ПредставлениеПолучателейПисьма = "";  	
	Для Каждого ТекПолучатель Из тзПолучателиПисьма Цикл   
		Если НЕ ПустаяСтрока(ПредставлениеПолучателейПисьма) Тогда
			ПредставлениеПолучателейПисьма = ПредставлениеПолучателейПисьма + ", ";
		КонецЕсли;
		ПредставлениеПолучателейПисьма = ПредставлениеПолучателейПисьма + ?(СокрЛП(ТекПолучатель.Представление) = "", "<", ТекПолучатель.Представление + " <") + ТекПолучатель.АдресЭлектроннойПочты + ">" ;
		
		НоваяСтрока = НовыйДокумент.Получатели.Добавить();
		НоваяСтрока.АдресЭлектроннойПочты = ТекПолучатель.АдресЭлектроннойПочты;
		НоваяСтрока.Представление         = ТекПолучатель.Представление;
		
		НоваяСтрока.КодГруппыАдреса = ТекПолучатель.КодГруппыАдреса;
	КонецЦикла;
	НовыйДокумент.ПолучателиПредставление  = ПредставлениеПолучателейПисьма;
	
	
	// Выполняем запись созданного документа
	Попытка
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		Параметры.ВыполненныеДействия.Добавить("Создано электронное письмо: " + НовыйДокумент);	
		
		// Если нужно, пробуем отправить письмо.
		Если ВариантЗаписи=Перечисления.ВариантЗаписиЭлектронногоПисьма.ОтправитьНемедленно Тогда
			Попытка
				Если УчетнаяЗапись=Справочники.УчетныеЗаписиЭлектроннойПочты.Поддержка Тогда
					Если обЭтоКлиентСервер() Тогда
						// Отправка письма через встроенный SMTP сервер производится в защищенном модуле, который не создается на сервере.
						// Поэтому отправка письма в этом случае не возможна.
						Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - электронное письмо не может быть отправлено через стандартный SMTP автоматически.");
						Возврат Неопределено; 
					Иначе
						#Если Клиент Тогда
							ВремМассивОшибок = Новый Массив;
							МассивПисем = Новый Массив;
							МассивПисем.Добавить(НовыйДокумент.Ссылка);	
							
							зфОтправитьПисьмаЧерезСтандартныйSMTP(УчетнаяЗапись, МассивПисем,, ВремМассивОшибок);
							Если ВремМассивОшибок.Количество()<>0 Тогда
								Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при отправке электронного письма. Ошибка почтового сервера.");
								Возврат Неопределено; 								
							КонецЕсли;
						#КонецЕсли
					КонецЕсли;
				Иначе			
					СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Ложь, Истина);
					СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(УчетнаяЗапись, СтруктураРежимовОбмена);
					Если СоединениеСПочтовымСервером<>Неопределено Тогда
						Если эпОтправитьПисьмо(НовыйДокумент, СоединениеСПочтовымСервером) Тогда
							эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
						Иначе
							Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при отправке электронного письма. Ошибка почтового сервера.");
							Возврат Неопределено; 
						КонецЕсли;
					Иначе
						Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - не удалось подключиться к почтовому серверу");
						Возврат Неопределено; 
					КонецЕсли;
				КонецЕсли; 
				
				Параметры.ВыполненныеДействия.Добавить("Отправлено электронное письмо: " + НовыйДокумент);
				
				// Если работа идет в файловом режиме, то оповестим АРМ Коммуникатор.
				#Если Клиент Тогда
					Оповестить("ОбновитьДеревоГрупп", "Пересчитать");		
				#КонецЕсли
			Исключение
				Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при отправке электронного письма: "+ОписаниеОшибки());
				Возврат Неопределено; 	
			КонецПопытки;
		КонецЕсли;
	Исключение
		Параметры.Ошибки.Добавить("Действие <" + Выборка.Наименование + "> - ошибка при записи документа ""Электронное письмо"": "+ОписаниеОшибки());
		Возврат Неопределено; 			
	КонецПопытки;
	
	Возврат НовыйДокумент;
КонецФункции // СоздатьЭлектронноеПисьмо() 

 

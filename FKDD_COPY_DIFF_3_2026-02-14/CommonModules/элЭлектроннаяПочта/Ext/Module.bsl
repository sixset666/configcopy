////////////////////////////////////////////////////////////////////////////////
// В ДАННЫЙ МОДУЛЬ ВЫНЕСЕНЫ ФУНКЦИИ, СВЯЗАННЫЕ С ПОДСИСТЕМОЙ
// 						"ЭЛЕКТРОННАЯ ПОЧТА"
////////////////////////////////////////////////////////////////////////////////

// Вывод картинок для предопределенных групп писем электронной почты
//
// Параметры:
//  Оформление строки - ОформлениеСтроки - содержит оформление строки 
//                      (шрифт, цвет) и коллекцию оформлений ячеек. 
//  Данные строки     - Данные выводимой строки - параметр соответствует 
//                      свойству ТекущиеДанные для выводимой строки. 
//
Процедура эпГруппыПисемПриВыводеСтроки(ОформлениеСтроки, ДанныеСтроки) Экспорт

	ИндексКартинки = 1;
	
	Если НЕ ДанныеСтроки = Неопределено  Тогда 
		 Если ДанныеСтроки.Наименование = "Исходящие" Тогда	 
			 ИндексКартинки  = 5;
		 ИначеЕсли  ДанныеСтроки.Наименование = "Входящие" Тогда	 
			 ИндексКартинки  = 3;
		 ИначеЕсли  ДанныеСтроки.Наименование = "Отправленные" Тогда	 
			 ИндексКартинки  = 2;
			 
		 ИначеЕсли  ДанныеСтроки.Наименование = "Удаленные" Тогда	 
			 ИндексКартинки  = 0;
		 Иначе
			 Если ДанныеСтроки.ПометкаУдаления Тогда
				 ИндексКартинки  = 4;
			 Иначе
				 ИндексКартинки  = 1;
			 КонецЕсли;
		 КонецЕсли;
	 КонецЕсли; 
	 
	 ОформлениеСтроки.Ячейки.Наименование.ИндексКартинки = ИндексКартинки;
	 ОформлениеСтроки.Ячейки.Наименование.ОтображатьКартинку = Истина;
	 
	 
КонецПроцедуры // эпГруппыПисемПриВыводеСтроки()

#Если Клиент Тогда
// Вывод картинок для учетных записей электронной почты
//
// Параметры:
//  Оформление строки - ОформлениеСтроки - содержит оформление строки 
//   (шрифт, цвет) и коллекцию оформлений ячеек. 
//  Данные строки - Данные выводимой строки - параметр соответствует 
//   свойству ТекущиеДанные для выводимой строки. 
//
Процедура эпУчетныеЗаписиПриВыводеСтроки(ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки.Ссылка.ЭтоУчетнаяЗаписьНовостей Тогда
		ОформлениеСтроки.Ячейки.Наименование.Картинка = БиблиотекаКартинок.НовостиRSS;		
	Иначе 
		Если ДанныеСтроки.Ссылка.ОбщийДоступ Тогда
			Если ЭпПолучитьУчетнуюЗаписьПоУмолчанию() <> ДанныеСтроки.Ссылка Тогда
				ОформлениеСтроки.Ячейки.Наименование.Картинка = БиблиотекаКартинок.УчетнаяЗаписьОбщая;	
			Иначе
				ОформлениеСтроки.Ячейки.Наименование.Картинка = БиблиотекаКартинок.УчетнаяЗаписьОбщаяПоУмолчанию;	
			КонецЕсли;
		Иначе	
			Если ЭпПолучитьУчетнуюЗаписьПоУмолчанию() <> ДанныеСтроки.Ссылка Тогда
				ОформлениеСтроки.Ячейки.Наименование.Картинка = БиблиотекаКартинок.УчетнаяЗапись;	
			Иначе
				ОформлениеСтроки.Ячейки.Наименование.Картинка = БиблиотекаКартинок.УчетнаяЗаписьПоУмолчанию;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ОформлениеСтроки.Ячейки.Наименование.ОтображатьКартинку = Истина; 
КонецПроцедуры

// Оформление строк табличной части "ПрикрепленныеФайлы" документа "Электронное письмо" 
//
// Параметры:
//  Оформление строки - ОформлениеСтроки - содержит оформление строки 
//                      (шрифт, цвет) и коллекцию оформлений ячеек. 
//  Данные строки     - Данные выводимой строки - параметр соответствует 
//                      свойству ТекущиеДанные для выводимой строки. 
//
Процедура эпПрикрепленныеФайлыПриВыводеСтроки(ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки <> Неопределено Тогда
		Размер = ДанныеСтроки.Размер;
		Если Размер < 1024 Тогда
			ОформлениеСтроки.Ячейки.Размер.УстановитьТекст("" + Размер + " байт");
		ИначеЕсли Размер < 1024*1024 Тогда	
			ОформлениеСтроки.Ячейки.Размер.УстановитьТекст(Формат(Размер/1024, "ЧДЦ=2") + " Кб");
		Иначе 
			ОформлениеСтроки.Ячейки.Размер.УстановитьТекст(Формат(Размер/1024/1024, "ЧДЦ=2") + " Мб");
		КонецЕсли;
	КонецЕсли;
	// выведем картинки основных форматов файлов
	ОформлениеСтроки.Ячейки.Имя.ИндексКартинки = рфПолучитьИндексПиктограммыФайла(рфПолучитьРасширениеФайла(ДанныеСтроки.Имя));
	ОформлениеСтроки.Ячейки.Имя.ОтображатьКартинку = Истина;
КонецПроцедуры // эпПрикрепленныеФайлыПриВыводеСтроки()

#КонецЕсли


// Получение для текущего пользователя учетной записи по умолчанию
//
// Возвращаемое значение:
//  СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - найденная учетная запись по умолчанию
//  Или Неопределено, если у пользователя вообще нет учетных записей
//
Функция эпПолучитьУчетнуюЗаписьПоУмолчанию() Экспорт

	// Выведем доступные учетные записи электронной почты
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка КАК УчетнаяЗапись
	               |ИЗ
	               |	Справочник.УчетныеЗаписиЭлектроннойПочты.Пользователи КАК УчетныеЗаписиЭлектроннойПочтыПользователи
	               |ГДЕ
	               |	УчетныеЗаписиЭлектроннойПочтыПользователи.Пользователь = &Пользователь
	               |	И УчетныеЗаписиЭлектроннойПочтыПользователи.УчетнаяЗаписьПоУмолчанию
	               |	И (НЕ УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ЭтоУчетнаяЗаписьНовостей)";
					   
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.УчетнаяЗапись;
	Иначе	
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // эпПолучитьУчетнуюЗаписьПоУмолчанию()

// Получение для текущего пользователя списка доступных учетных записей по праву
// если право не указано, возвращается полный список доступных учетных записей 
//
// Параметры:
//  Право - Строка - название права из табличной части "Пользователи" спр. УчетныеЗаписиЭлектроннойПочты 
//          может принимать значения "АДМИНИСТРИРОВАНИЕ", "РЕДАКТИРОВАНИЕ", "ТРАНСПОРТ" либо по умолчанию
//			"" (пустая строка).
//          Регистр значение не имеет, другие значения игнорируются.
//  ВключатьПомеченныеНаУдаление - Булево - признак включения в список помеченных на удаление записей
//
//Возвращаемое значение:
//   СписокЗначений - список доступных учетных записей с указанным правом
//   
Функция эпПолучитьДоступныеУчетныеЗаписи(Право = "", ВключатьПомеченныеНаУдаление = Ложь, ИсключитьНовости = Ложь, ИсключитьТехподдержку = Ложь) Экспорт
	
	СписокУчетныхЗаписей = Новый СписокЗначений;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.УчетныеЗаписиЭлектроннойПочты) Тогда
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		               |	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка КАК УчетнаяЗапись,
		               |	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ЭтоУчетнаяЗаписьНовостей КАК УчетнаяЗаписьНовостей
		               |ИЗ
		               |	Справочник.УчетныеЗаписиЭлектроннойПочты.Пользователи КАК УчетныеЗаписиЭлектроннойПочтыПользователи";
					   
		Условия = "";			   
		Если НЕ ВключатьПомеченныеНаУдаление Тогда
			Условия = Условия + "
					   |	НЕ УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ПометкаУдаления";
		КонецЕсли;      
		Если ИсключитьНовости Тогда	
			Условия = Условия + ?(Условия = "", "", " И") +  "
			  		   | 	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ЭтоУчетнаяЗаписьНовостей=Ложь";
		КонецЕсли;
		Если ИсключитьТехподдержку Тогда	
			Условия = Условия + ?(Условия = "", "", " И") +  "
			  		   | 	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка<>&УчетнаяЗаписьТехПоддержка";
		КонецЕсли;
		
		УсловиеПрава = "";
		Если ВРег(Право) = "ЧТЕНИЕ" Тогда
			УсловиеПрава = "	(УчетныеЗаписиЭлектроннойПочтыПользователи.Пользователь = &Пользователь ИЛИ УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ОбщийДоступ)";
		ИначеЕсли ВРег(Право) = "АДМИНИСТРИРОВАНИЕ" Тогда
			УсловиеПрава = "	(УчетныеЗаписиЭлектроннойПочтыПользователи.Администрирование И УчетныеЗаписиЭлектроннойПочтыПользователи.Пользователь = &Пользователь)";
		ИначеЕсли ВРег(Право) = "РЕДАКТИРОВАНИЕ" Тогда	
			УсловиеПрава = "	((УчетныеЗаписиЭлектроннойПочтыПользователи.Редактирование И УчетныеЗаписиЭлектроннойПочтыПользователи.Пользователь = &Пользователь) ИЛИ УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ОбщийДоступ)";
		ИначеЕсли ВРег(Право) = "ТРАНСПОРТ" Тогда	
			УсловиеПрава = "	((УчетныеЗаписиЭлектроннойПочтыПользователи.Транспорт И УчетныеЗаписиЭлектроннойПочтыПользователи.Пользователь = &Пользователь) ИЛИ УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ОбщийДоступ)";
		КонецЕсли;					   
		
		Условия = Условия + ?(УсловиеПрава = "", "", " И" + Символы.ПС + УсловиеПрава);
		Условия = ?(Условия = "", "", Символы.ПС + "ГДЕ" + Условия);
				   
		Запрос.Текст = Запрос.Текст + Условия + "
		               |УПОРЯДОЧИТЬ ПО
		               |	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка.ЭтоУчетнаяЗаписьНовостей,
					   |    УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка";
		
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь);
		
		Если ИсключитьТехподдержку Тогда 
			Запрос.УстановитьПараметр("УчетнаяЗаписьТехПоддержка", Справочники.УчетныеЗаписиЭлектроннойПочты.Поддержка);
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ И", "ГДЕ ");
		Выборка = Запрос.Выполнить().Выбрать();
		
		СписокУчетныхЗаписей.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УчетнаяЗапись"));
	КонецЕсли;					   
		
	Возврат СписокУчетныхЗаписей;
	
КонецФункции 

//Функция проверяет корректность электронного адреса
//
Функция эпКорректностьЭлектронногоАдреса(ОтправительПредставление,ОписаниеОшибки) Экспорт
	// доделать позже	
  Возврат Истина;
КонецФункции // эпКорректностьЭлектронногоАдреса()


/////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ УСТАНОВЛЕНИЯ СВЯЗИ С ПОЧТОВЫМ СЕРВЕРОМ

// Функция формирует почтовый профиль 
// 
// Параметры:
//	 УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - почтовый профиль; 
//   РежимыОбмена  - Структура - содержит два элемента с ключами "Получить" и "Отправить". Значения элементов
// 	   				имеют тип Булево: Истина, если режим будет использоваться во время обмена, иначе - Ложь.
//
// Возвращаемое значение:
//   ИнтернетПочтовыйПрофиль - Почтовый профиль для отправки/получения электронных писем
//
Функция эпПолучитьПочтовыйПрофиль(УчетнаяЗапись, РежимыОбмена) Экспорт
	
	ПочтовыйПрофиль=Новый ИнтернетПочтовыйПрофиль;
	
	Если УчетнаяЗапись.ВремяОжиданияСервера > 0 Тогда
		ПочтовыйПрофиль.ВремяОжидания = УчетнаяЗапись.ВремяОжиданияСервера;
	КонецЕсли; 
	
	Если РежимыОбмена.Получить Тогда
		Если УчетнаяЗапись.ПротоколВходящейПочты = "IMAP" Тогда
			ПочтовыйПрофиль.АдресСервераIMAP = УчетнаяЗапись.POP3Сервер;
			ПочтовыйПрофиль.ПортIMAP         = УчетнаяЗапись.ПортPOP3;
			ПочтовыйПрофиль.ПользовательIMAP = УчетнаяЗапись.Логин;
			ПочтовыйПрофиль.ПарольIMAP       = УчетнаяЗапись.Пароль;
			ПочтовыйПрофиль.ИспользоватьSSLIMAP = УчетнаяЗапись.ИспользоватьPOPSSL;
		Иначе
			ПочтовыйПрофиль.АутентификацияPOP3 = эпПреобразоватьВСпособPOP3Аутентификации(УчетнаяЗапись.АутентификацияPOP3);
			ПочтовыйПрофиль.АдресСервераPOP3   = УчетнаяЗапись.POP3Сервер;	
			ПочтовыйПрофиль.ПортPOP3           = УчетнаяЗапись.ПортPOP3;		
			ПочтовыйПрофиль.Пароль             = УчетнаяЗапись.Пароль;
			ПочтовыйПрофиль.Пользователь       = УчетнаяЗапись.Логин;
			ПочтовыйПрофиль.ИспользоватьSSLPOP3= УчетнаяЗапись.ИспользоватьPOPSSL;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимыОбмена.Отправить Тогда
		ПочтовыйПрофиль.АдресСервераSMTP    = УчетнаяЗапись.SMTPСервер;
		ПочтовыйПрофиль.ПортSMTP            = УчетнаяЗапись.ПортSMTP;
		ПочтовыйПрофиль.АутентификацияSMTP  = эпПреобразоватьВСпособSMTPАутентификации(УчетнаяЗапись.АутентификацияSMTP);
		ПочтовыйПрофиль.ИспользоватьSSLSMTP = УчетнаяЗапись.ИспользоватьSMTPSSL;
		Если УчетнаяЗапись.АутентификацияSMTP <> Перечисления.СпособАутентификацииSMTP.БезАутентификации Тогда
			ПочтовыйПрофиль.ПарольSMTP         = УчетнаяЗапись.ПарольSMTP;
			ПочтовыйПрофиль.ПользовательSMTP   = УчетнаяЗапись.ЛогинSMTP;
			ПочтовыйПрофиль.POP3ПередSMTP	   = УчетнаяЗапись.POP3ПередSMTP;
		КонецЕсли; 
	КонецЕсли;
	
	УдалятьСообщения = УчетнаяЗапись.УдалятьСообщенияССервера;
	
	Возврат ПочтовыйПрофиль;
	
КонецФункции // эпПолучитьПочтовыйПрофиль()

// Функция выполняет отправку писем.
//
// Параметры:
//  ТекущееСоединение - ИнтернетПочта - текущее соединение с почтовым сервером
//  Сообщение         - ИнтернетПочтовоеСообщение - отправляемое сообщение электронной почты
//
// Возвращаемое значение:
//  Возвращает Истина при успешном отправлении письма и Ложь при не удачном.
//
Функция  эпПослать(ТекущееСоединение, Сообщение) Экспорт
	#Если Клиент Тогда
	Состояние("Ждите, отправка письма(ем)...");	
	#КонецЕсли
	Попытка
		ТекущееСоединение.Послать(Сообщение);
		Возврат Истина;
	Исключение
		СообщениеОбОшибке=элРазобратьОшибкуПочтовогоСоединения();
		Если обЗначениеНеЗаполнено(СообщениеОбОшибке) Тогда
			СообщениеОбОшибке=ОписаниеОшибки();
		КонецЕсли; 
		Сообщить("Ошибка при отправке электронного письма: "+СообщениеОбОшибке, СтатусСообщения.Важное); 
		Возврат Ложь;
	КонецПопытки;	
	#Если Клиент Тогда
	Состояние("");	
	#КонецЕсли
КонецФункции // эпПослать()

// Функция отсылает электронное письмо 
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты, учетная запись,
//					 от имени которой создается подключение
//    				 к почтовому серверу
//   РежимыОбмена - Структура - содержит два элемента с ключами "Получить" и "Отправить". Значения элементов
// 					 имеют тип Булево: Истина, если режим будет использоваться во время обмена, иначе - Ложь.
//
// Возвращаемое значение:
//   ИнтернетПочта - созданное соединение или Неопределено, если соединение установить не удалось
//
Функция эпПодключитьсяКПочтовомуСерверу(УчетнаяЗапись, РежимыОбмена) Экспорт
	#Если Клиент Тогда
	Состояние("Выполняется подключение к почтовому серверу");
	#КонецЕсли
	Соединение = Новый ИнтернетПочта;
	
	Если УчетнаяЗапись.ПротоколВходящейПочты = "IMAP" Тогда
		Протокол = ПротоколИнтернетПочты.IMAP;
	Иначе
		Протокол = ПротоколИнтернетПочты.POP3;
	КонецЕсли;
	Попытка
		Соединение.Подключиться(эпПолучитьПочтовыйПрофиль(УчетнаяЗапись, РежимыОбмена),Протокол);
	Исключение
		Сообщить(элРазобратьОшибкуПочтовогоСоединения(), СтатусСообщения.Важное); 
		Сообщить("Не удалось подключиться к почтовому серверу. Проверьте параметры учетной записи """+УчетнаяЗапись+""".", СтатусСообщения.Важное); 
		Возврат Неопределено;
	КонецПопытки; 

	Возврат Соединение;
	
КонецФункции // эпПодключитьсяКПочтовомуСерверу()

// Процедура выполняет отключение от почтового сервера
//
// Параметры:
//   Соединение - ИнтернетПочта, подключение к почтовому серверу
//
Процедура эпОтключитьсяОтПочтовогоСервера(Соединение) Экспорт
	
	Соединение.Отключиться();
	
КонецПроцедуры // эпОтключитьсяОтПочтовогоСервера()

// Функция отсылает электронное письмо 
//
// Параметры:
//   Объект            - ДокументОбъект.ЭлектронноеПисьмо, отсылаемое электронное письмо
//   ТекущееСоединение - ИнтернетПочта, соединение с почтовым сервером
// 
// Возвращаемое значение:
//   Булево - признак, отправлено письмо или нет
//
Функция эпОтправитьПисьмо(Объект, ТекущееСоединение) Экспорт
	
	Сообщение = Новый ИнтернетПочтовоеСообщение;
	Сообщение.Тема = Объект.Тема;
	
	ИмяОтправителя = эпВыделитьАдресИПредставление(Объект.ОтправительПредставление);
	ИмяОтправителя = ИмяОтправителя[0].Представление;
	
	Если ИмяОтправителя = ""  Тогда
		ИмяОтправителя = Объект.УчетнаяЗапись.Пользователь;
	КонецЕСли; 
	Сообщение.ИмяОтправителя = ИмяОтправителя;
	Сообщение.Отправитель = Объект.УчетнаяЗапись.АдресЭлектроннойПочты;	
	
	эпДобавитьТекст(Сообщение, Объект.ТекстПисьма, Объект.ФорматТекста); 
	
	Для каждого СтрокаВложения из Объект.ПрикрепленныеФайлы Цикл 
		Сообщение.Вложения.Добавить(СтрокаВложения.Файл.Получить(), СтрокаВложения.Имя);
	КонецЦикла;
	
	Для каждого Адрес из Объект.Получатели Цикл
		эпДобавитьАдрес(Сообщение, Адрес); 
	КонецЦикла;
	
	Сообщение.Важность = ?(Объект.Важность=Перечисления.Важность.Высокая,ВажностьИнтернетПочтовогоСообщения.Высокая,?(Объект.Важность=Перечисления.Важность.Низкая,ВажностьИнтернетПочтовогоСообщения.Низкая,ВажностьИнтернетПочтовогоСообщения.Обычная));
	
	АдресУведомления = Сообщение.Отправитель.Адрес;
	// Свойство устарело и более не поддерживается.
	//Если Объект.УведомитьОДоставке тогда
	//	Сообщение.УведомитьОДоставке = Объект.УведомитьОДоставке;
	//	Сообщение.АдресаУведомленияОДоставке.Добавить(АдресУведомления);
	//КонецЕсли;
	Сообщение.УведомитьОДоставке = Ложь;
	Если Объект.УведомитьОПрочтении тогда
		Сообщение.УведомитьОПрочтении = Объект.УведомитьОПрочтении;
		Сообщение.АдресаУведомленияОПрочтении.Добавить(АдресУведомления);
	КонецЕсли;
	
	Если эпПослать(ТекущееСоединение, Сообщение) Тогда 
		Объект.СтатусПисьма = Перечисления.СтатусыСообщений.Отправленное;
		Объект.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Отправленные",Истина,,Объект.УчетнаяЗапись);
		Объект.ДатаОтправления = ТекущаяДата();
		Объект.ДатаПолучения   = '00010101000000';
		Объект.Записать();
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // эпОтправитьПисьмо()

// Процедура выполняет отправку созданных писем из указанной учетной записи
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты, учетная запись, 
//   				 из которой отправляются письма
//
Процедура эпОтправитьЭлектронныеПисьма(УчетнаяЗапись) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
					|	ЭлектронноеПисьмо.Ссылка
					|ИЗ
					|	Документ.ЭлектронноеПисьмо КАК ЭлектронноеПисьмо
					|
					|ГДЕ
					|	ЭлектронноеПисьмо.УчетнаяЗапись = &УчетнаяЗапись И
					|	ЭлектронноеПисьмо.ГруппаПисем.Наименование = &Наименование И
					|	ЭлектронноеПисьмо.СтатусПисьма = &СтатусПисьма";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.УстановитьПараметр("Наименование", "Исходящие");
	Запрос.УстановитьПараметр("СтатусПисьма", Перечисления.СтатусыСообщений.Исходящее);
	РезультатЗапроса = Запрос.Выполнить();	
	
	Если НЕ РезультатЗапроса.Пустой() ТОгда
		Если УчетнаяЗапись=Справочники.УчетныеЗаписиЭлектроннойПочты.Поддержка Тогда			
			#Если Клиент Тогда
				Состояние("Отправка письма через стандартный сервер SMTP");				
				
				мсвОшибки = Новый Массив;
				зфОтправитьПисьмаЧерезСтандартныйSMTP(УчетнаяЗапись, РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"),, мсвОшибки);
				
				Если мсвОшибки.Количество()<>0 Тогда
					Для каждого ТекОшибка Из мсвОшибки Цикл 
						Сообщить("" + ТекОшибка, СтатусСообщения.Важное);
					КонецЦикла;	
				КонецЕсли;
				Оповестить("ОбновитьДеревоГрупп", "Пересчитать");
			#КонецЕсли 
		Иначе
			СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Ложь, Истина);
			СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(УчетнаяЗапись, СтруктураРежимовОбмена);
			Если СоединениеСПочтовымСервером=Неопределено Тогда
				Возврат;
			КонецЕсли;
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Документ = Выборка.Ссылка.ПолучитьОбъект();
				эпОтправитьПисьмо(Документ, СоединениеСПочтовымСервером);
			КонецЦикла;			
			эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);			
		КонецЕсли; 		
	Иначе
		Возврат;
	КОнецЕсли;
	
КонецПроцедуры

// Функция преобразовывает перечисление СпособАутентификацииPOP3 в значение типа СпособPOP3Аутентификации
// Параметры:
//   СпособАутентификаци - Перечисление.СпособАутентификацииPOP3 - нужный способ аутентификации
//
Функция эпПреобразоватьВСпособPOP3Аутентификации(СпособАутентификаци) Экспорт
	Если СпособАутентификаци = Перечисления.СпособАутентификацииPOP3.Обычная тогда
		Результат = СпособPOP3Аутентификации.Обычная;
	ИначеЕсли СпособАутентификаци = Перечисления.СпособАутентификацииPOP3.APOP тогда
		Результат = СпособPOP3Аутентификации.APOP;
	ИначеЕсли СпособАутентификаци = Перечисления.СпособАутентификацииPOP3.CramMD5 тогда
		Результат = СпособPOP3Аутентификации.CramMD5;
	Иначе	
		Результат = СпособPOP3Аутентификации.Обычная;
	КонецЕсли;
	Возврат Результат;
КонецФункции

// Функция преобразовывает перечисление СпособАутентификацииSMTP в значение типа СпособSMTPАутентификации
// Параметры:
//   СпособАутентификаци - Перечисление.СпособАутентификацииSMTP - нужный способ аутентификации
//
Функция эпПреобразоватьВСпособSMTPАутентификации(СпособАутентификации) Экспорт
	Если СпособАутентификации = Перечисления.СпособАутентификацииSMTP.БезАутентификации тогда
		Результат = СпособSMTPАутентификации.БезАутентификации;
	ИначеЕсли СпособАутентификации = Перечисления.СпособАутентификацииSMTP.Login тогда
		Результат = СпособSMTPАутентификации.Login;
	ИначеЕсли СпособАутентификации = Перечисления.СпособАутентификацииSMTP.Plain тогда
		Результат = СпособSMTPАутентификации.Plain;
	ИначеЕсли СпособАутентификации = Перечисления.СпособАутентификацииSMTP.CramMD5 тогда
		Результат = СпособSMTPАутентификации.CramMD5;
	ИначеЕсли СпособАутентификации = Перечисления.СпособАутентификацииSMTP.ПоУмолчанию тогда
		Результат = СпособSMTPАутентификации.ПоУмолчанию;
	Иначе	
		Результат = СпособSMTPАутентификации.БезАутентификации;
	КонецЕсли;
	Возврат Результат;
КонецФункции

///////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ СТРУКТУРЫ ПИСЬМА И ЕГО ОТПРАВКА

#Если Клиент Тогда 
// Процедура добавляет файлы в табличную часть письма "ПрикрепленныеФайлы"
// 
// Параметры:
//  ТЧПрикрепленныеФайлы - Табличная часть - табличная часть документа "Электронное письмо".
//
Процедура  эпДобавитьФайлыВТЧ(ТЧПрикрепленныеФайлы) Экспорт
	
	Файлы = "";
	Если Не рфВыбратьФайлы(Файлы) Тогда
		Возврат;
	КонецЕсли;
	
	эпДобавитьВложенияВПисьмо(ТЧПрикрепленныеФайлы, Файлы);
	
КонецПроцедуры // эпДобавитьФайлыВТЧ()
 #КонецЕсли

// Функция выделяет из переданной строки электронный адрес и его представление
//
// Параметры:
//  Текст - Строка, переданная для разбора строка
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица электронных адресов и их представлений
// 
Функция эпВыделитьАдресИПредставление(Знач Текст) Экспорт
	
	ТаблицаЗначенийАдресов = Новый ТаблицаЗначений;
	ТаблицаЗначенийАдресов.Колонки.Добавить("Представление");
	ТаблицаЗначенийАдресов.Колонки.Добавить("АдресЭлектроннойПочты");
	ТаблицаЗначенийАдресов.Колонки.Добавить("АдресСПредставлением");
	
	Текст = СокрЛП(СтрЗаменить(Текст,";",","));
	Запятая = Найти(Текст,",");
	
	Пока Запятая > 0 Цикл 
		АдресСПредставлением = СокрЛП(Лев(Текст, Запятая - 1));
		
		ЛеваяКавычка  = Найти(АдресСПредставлением, "<");
		ПраваяКавычка = Найти(АдресСПредставлением, ">");
		
		Адрес = СокрЛП(Сред(АдресСПредставлением, ЛеваяКавычка+1,ПраваяКавычка-ЛеваяКавычка-1));
		Представление = СокрЛП(Лев(АдресСПредставлением, ЛеваяКавычка - 1));
		
		Если Адрес <> "" Тогда
			НовыйАдрес = ТаблицаЗначенийАдресов.Добавить();
			НовыйАдрес.Представление = Представление;
			НовыйАдрес.АдресЭлектроннойПочты = Адрес;
			НовыйАдрес.АдресСПредставлением = АдресСПредставлением;
		КонецЕсли;
		
		Текст   = СокрЛП(Прав(Текст,СтрДлина(Текст) - Запятая)); 
		Запятая = Найти(Текст,",");
		
	КонецЦикла;	
	
	Если Запятая = 0 и НЕ Текст = "" Тогда
		ДлинаСтроки =  СтрДлина(Текст);
		АдресСПредставлением = СокрЛП(Лев(Текст, ДлинаСтроки));
		
		ЛеваяКавычка  = Найти(АдресСПредставлением, "<");
		ПраваяКавычка = Найти(АдресСПредставлением, ">");
		
		Адрес = СокрЛП(Сред(АдресСПредставлением, ЛеваяКавычка+1,ПраваяКавычка-ЛеваяКавычка-1));
		Представление = СокрЛП(Лев(АдресСПредставлением, ЛеваяКавычка - 1));
		
		НовыйАдрес = ТаблицаЗначенийАдресов.Добавить();
		НовыйАдрес.Представление = Представление;
		НовыйАдрес.АдресЭлектроннойПочты = Адрес;
		НовыйАдрес.АдресСПредставлением = АдресСПредставлением;
	КонецЕсли;	
	
	Возврат ТаблицаЗначенийАдресов;
	
КонецФункции // эпВыделитьАдресИПредставление()

// Функция анализирует начало темы на наличие специальных префиксов 
// и увеличивает индекс темы (RE: -> RE[2] и т.д.). Если префикс не найден - вставляет его
//
// Параметры:
//   Тема - строка, тема письма 
//   Префикс - строка, префикс темы письма  ("Re","Fwd")
//
// Возвращаемое значение:
//   Тема - Строка - тема с измененным префиксом
//
Функция эпУвеличитьИндексТемы(Знач Тема, Префикс) Экспорт
	
	ДлинаПрефикса = СтрДлина(Префикс);
	Тема = СокрЛП(Тема);
	Если ВРег(Лев(Тема, ДлинаПрефикса)) <> ВРег(Префикс) Тогда
		Тема = Префикс + ": " + Тема;
	Иначе
		Если Сред(Тема, ДлинаПрефикса+1, 1) =  ":" Тогда
			Тема = Префикс + "[2]" + Сред(Тема, ДлинаПрефикса+1);
		ИначеЕсли Сред(Тема, ДлинаПрефикса+1, 1) = "[" Тогда // пробуем увеличить индекс	
			Попытка
				НовыйИндекс = Число(Сред(Тема, ДлинаПрефикса+2, Найти(Тема, "]") - ДлинаПрефикса-2)) + 1;
				Тема = Префикс + "[" + НовыйИндекс + Сред(Тема, Найти(Тема, "]"));
			Исключение
				Тема = Префикс + ": " + Тема;
			КонецПопытки;
		Иначе	
			Тема = Префикс + ": " + Тема;
		КонецЕсли;	
	КонецЕсли;

	Возврат Тема;
	
КонецФункции // эпУвеличитьИндексТемы()

// Функция формирует текст письма при ответе
//
// Параметры:
//   ПисьмоОснование - ДокументСсылка.ЭлектронноеПисьмо, 
//
// Возвращаемое значение:
//   ТекстПисьма - Строка - сформированный текст письма
//
Функция эпСформироватьТекстПисьмаПриОтвете(ПисьмоОснование) Экспорт
	Шаблон = "Вы писали %ДатаОтправления :
	         |
	         |
			 |%ЦитированныйТекст
			 |";
	Возврат эпСформироватьТекстПисьмаПоШаблону(Шаблон, ПисьмоОснование);;
КонецФункции // эпСформироватьТекстПисьмаПриОтвете()

// Функция формирует и возвращает новое почтовое сообщение как "ответ"
//
// Параметры:
//   ПисьмоОснование - ДокументСсылка.ЭлектронноеПисьмо, письмо-основание
//
// Возвращаемое значение:
//   НовоеПисьмо - ДокументОбъект.ЭлектронноеПисьмо - Созданное письмо-ответ
//
Функция эпСоздатьПисьмоОтвет(ПисьмоОснование) Экспорт
	
	НовоеПисьмо = ПисьмоОснование.Скопировать();
	
	НовоеПисьмо.Тема = эпУвеличитьИндексТемы(НовоеПисьмо.Тема, "Re");
	
	НовоеПисьмо.ТекстПисьма = эпСформироватьТекстПисьмаПриОтвете(ПисьмоОснование);
	
    НовоеПисьмо.Получатели.Очистить();
	ТаблицаЗначенийАдресов = эпВыделитьАдресИПредставление(ПисьмоОснование.ОтправительПредставление);
	НовоеПисьмо.ОбновитьТабличнуюЧасть(ТаблицаЗначенийАдресов, Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому);
	
	НовоеПисьмо.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие",Истина,,ПисьмоОснование.УчетнаяЗапись);
	НовоеПисьмо.ДокументОснование = ПисьмоОснование;
	НовоеПисьмо.СтатусПисьма = Перечисления.СтатусыСообщений.ПустаяСсылка();
	НовоеПисьмо.Ответ = Истина;
	
	// удалим вложения
	Если  НовоеПисьмо.ЕстьВложения Тогда
		НовоеПисьмо.ЕстьВложения = Ложь;
		НовоеПисьмо.ПрикрепленныеФайлы.Очистить();
	КонецЕсли;	 
	
	Возврат НовоеПисьмо;
	
КонецФункции // эпСоздатьПисьмоОтвет()
	
// Функция формирует и возвращает новое почтовое сообщение как "Ответ всем"
//
// Параметры:
//   ПисьмоОснование - ДокументСсылка.ЭлектронноеПисьмо, письмо-основание
//
// Возвращаемое значение:
//   НовоеПисьмо - ДокументОбъект.ЭлектронноеПисьмо - Созданное письмо-ответ
//
Функция эпСоздатьПисьмоОтветВсем(ПисьмоОснование) Экспорт
	
	НовоеПисьмо = ПисьмоОснование.Скопировать();
	
	НовоеПисьмо.Тема = эпУвеличитьИндексТемы(НовоеПисьмо.Тема, "Re");
	
	НовоеПисьмо.ТекстПисьма = эпСформироватьТекстПисьмаПриОтвете(ПисьмоОснование);

    НовоеПисьмо.Получатели.Очистить();
	ТаблицаЗначенийАдресов = эпВыделитьАдресИПредставление(ПисьмоОснование.ОтправительПредставление);
	НовоеПисьмо.ОбновитьТабличнуюЧасть(ТаблицаЗначенийАдресов, Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому);
	
	// заменим в ТЧ "Получатели" "Кому" на "Копии" и удалим "СкрытыеКопии"
	Для каждого Получатель Из ПисьмоОснование.Получатели Цикл
		Если (Получатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому Или Получатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Копии) И  
			  НЕ ВРег(Получатель.АдресЭлектроннойПочты) = ВРег(НовоеПисьмо.УчетнаяЗапись.АдресЭлектроннойПочты) Тогда
			Строка = НовоеПисьмо.Получатели.Добавить();
			Строка.АдресЭлектроннойПочты = Получатель.АдресЭлектроннойПочты;
			Строка.Представление = Получатель.Представление;
			Строка.КодГруппыАдреса = Получатель.КодГруппыАдреса;
		КонецЕсли;	
	КонецЦикла;  	
	
	НовоеПисьмо.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие",Истина,,ПисьмоОснование.УчетнаяЗапись);
	НовоеПисьмо.ДокументОснование = ПисьмоОснование;
	НовоеПисьмо.СтатусПисьма = Перечисления.СтатусыСообщений.ПустаяСсылка();
	НовоеПисьмо.Ответ = Истина;
	
	// удалим вложения
	Если  НовоеПисьмо.ЕстьВложения Тогда
		НовоеПисьмо.ЕстьВложения = Ложь;
		НовоеПисьмо.ПрикрепленныеФайлы.Очистить();
	КонецЕсли;	 
	
	Возврат НовоеПисьмо;
	
КонецФункции // эпСоздатьПисьмоОтветВсем()

// Функция создает новое письмо для пересылки  на основании переданного
//
// Параметры:
//   ПисьмоОснование - ДокументСсылка.ЭлектронноеПисьмо, письмо-основание
//
// Возвращаемое значение:
//   НовоеПисьмо - ДокументОбъект.ЭлектронноеПисьмо - Созданное письмо для пересылки
//
Функция эпПереслатьПисьмо(ПисьмоОснование) Экспорт
	
	НовоеПисьмо = ПисьмоОснование.Скопировать();
	
	НовоеПисьмо.Тема = эпУвеличитьИндексТемы(НовоеПисьмо.Тема, "Fwd");
	
	Шаблон = 
	"---------- Пересылаемое письмо -----------------
	|Отправитель:         %Отправитель
	|Получатель:          %Получатель
	|Создано:             %ДатаОтправления
	|Тема:                %Тема
	|Прикрепленные файлы: %ПрикрепленныеФайлы
	|--------------------------------------------
	|%Текст
	|---------- Конец пересылаемого письма ----------
	|";
	
	НовоеПисьмо.ТекстПисьма = эпСформироватьТекстПисьмаПоШаблону(Шаблон, ПисьмоОснование); 
	
	НовоеПисьмо.Получатели.Очистить(); 
	НовоеПисьмо.Переадресация = Истина;
	НовоеПисьмо.Ответ = Ложь;
	НовоеПисьмо.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие",Истина,,ПисьмоОснование.УчетнаяЗапись);
	НовоеПисьмо.ДокументОснование = ПисьмоОснование;
	НовоеПисьмо.СтатусПисьма = Перечисления.СтатусыСообщений.ПустаяСсылка();
	
	Возврат НовоеПисьмо;
	
КонецФункции // эпПереслатьПисьмо()

// Функция новое письмо, как подтверждение о прочтении письма-основания
//
// Параметры:
//   ПисьмоОснование - ДокументСсылка.ЭлектронноеПисьмо, письмо-основание
//
// Возвращаемое значение:
//   НовоеПисьмо - ДокументОбъект.ЭлектронноеПисьмо - Созданное письмо-подтверждение
//
Функция эпСоздатьПисьмоПодтверждениеПрочтения(ПисьмоОснование) Экспорт
	
	НовоеПисьмо = ПисьмоОснование.Скопировать();
	
	НовоеПисьмо.Тема = "Подтверждение прочтения";
	
	Шаблон = 
	"От Вас было получено письмо с запросом на подтверждение прочтения.
	|Информируем Вас, что письмо было прочитано и, возможно, понято получателем.
	|
	|---------- Данные исходного письма -----------------
	|Отправитель:         %Отправитель
	|Получатель:          %Получатель
	|Создано:             %ДатаОтправления
	|Тема:                %Тема
	|Прикрепленные файлы: %ПрикрепленныеФайлы
	|----------------------------------------------------
	|";
	
	НовоеПисьмо.ТекстПисьма = эпСформироватьТекстПисьмаПоШаблону(Шаблон, ПисьмоОснование);
	
    НовоеПисьмо.Получатели.Очистить();
	ТаблицаЗначенийАдресов = эпВыделитьАдресИПредставление(ПисьмоОснование.ОтправительПредставление);
	НовоеПисьмо.ОбновитьТабличнуюЧасть(ТаблицаЗначенийАдресов, Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому);
	
	НовоеПисьмо.ГруппаПисем = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие",Истина,,ПисьмоОснование.УчетнаяЗапись);
	НовоеПисьмо.ДокументОснование = ПисьмоОснование;
	НовоеПисьмо.СтатусПисьма = Перечисления.СтатусыСообщений.ПустаяСсылка();
	НовоеПисьмо.Ответ = Истина;
	
	// удалим вложения
	Если  НовоеПисьмо.ЕстьВложения Тогда
		НовоеПисьмо.ЕстьВложения = Ложь;
		НовоеПисьмо.ПрикрепленныеФайлы.Очистить();
	КонецЕсли;	 
	
	Возврат НовоеПисьмо;
	
КонецФункции // эпСоздатьПисьмоПодтверждениеПрочтения()

// Функция формирует текст письма по заданному шаблону
//
// Параметры:
//   Шаблон    - Строка. Содержит шаблон по которому формируется текст
//   Письмо    - ДокументСсылка.ЭлектронноеПисьмо, письмо-основание
//
// Возвращаемое значение:
//   ТекстПисьма - Строка - сформированный текст письма
//
Функция эпСформироватьТекстПисьмаПоШаблону(Шаблон, Письмо)
	Результат = Шаблон;
	
	Замены = Новый СписокЗначений;
	Замены.Добавить("%Отправитель", 		"СтрокаЗамены=Письмо.ОтправительПредставление");
	Замены.Добавить("%Получатель", 			"СтрокаЗамены=Письмо.ПолучателиПредставление");
	Замены.Добавить("%ДатаОтправления", 	"СтрокаЗамены=Формат(Письмо.ДатаОтправления, ""ДЛФ=DDT"")");
	Замены.Добавить("%ДатаПолучения", 		"СтрокаЗамены=Формат(Письмо.ДатаПолучения, ""ДЛФ=DDT"")");
	Замены.Добавить("%Тема", 				"СтрокаЗамены=Письмо.Тема");
	Замены.Добавить("%ПрикрепленныеФайлы", 	"СтрокаЗамены=СформироватьПредставлениеПрикрепленныхФайлов(Письмо)");
	//Замены.Добавить("%", Письмо.);
	
	Для каждого Замена из Замены цикл
		Если Найти(Шаблон, Замена.Значение) > 0 тогда
			СтрокаЗамены = "";
			Выполнить(Замена.Представление);
			Результат = СтрЗаменить(Результат, Замена.Значение, СтрокаЗамены);
		КонецЕсли;
	КонецЦикла;
	
	//Шаблон = 
	//"---------- Исходное письмо -----------------
	//|Отправитель:         %Отправитель
	//|Получатель:          %Получатель
	//|Создано:             %ДатаОтправления
	//|Тема:                %Тема
	//|Прикрепленные файлы: %ПрикрепленныеФайлы
	//|--------------------------------------------
	//|%Текст
	//|%ЦитированныйТекст
	//|---------- Конец исходного письма ----------
	//|";
	
	ТекстИсходный = Письмо.ТекстПисьма;
	Если Письмо.ФорматТекста = Перечисления.ФорматТекста.HTML тогда
		ТелоНачало = Найти(НРег(ТекстИсходный), "<body>"); 
		ТелоКонец = Найти(НРег(ТекстИсходный), "</body>"); 
		Если ТелоНачало = 0 тогда ТелоНачало = 1 Иначе ТелоНачало = ТелоНачало + 6; КонецЕсли;
		Если ТелоКонец = 0 тогда ТелоКонец = СтрДлина(ТекстИсходный)+1; КонецЕсли;
		ТекстИсходныйДоТела = Лев(ТекстИсходный, ТелоНачало-1);
		ТекстИсходныйПослеТела = Сред(ТекстИсходный, ТелоКонец);
		ТекстИсходный = Сред(ТекстИсходный, ТелоНачало, ТелоКонец-ТелоНачало);
		Результат = ТекстИсходныйДоТела + "<p>" + обПреобразоватьТекстВHTML(Результат) + "</p>" + ТекстИсходныйПослеТела;
	КонецЕсли;
	
	
	Если Найти(Шаблон, "%Текст") > 0 тогда
		Если Письмо.ФорматТекста = Перечисления.ФорматТекста.HTML тогда
			Текст = "</p><p>" + ТекстИсходный + "</p><p>";
		Иначе
			Текст = ТекстИсходный;
		КонецЕсли;
		Результат = СтрЗаменить(Результат, "%Текст", Текст);
	КонецЕсли;
	
	Если Найти(Шаблон, "%ЦитированныйТекст") > 0 тогда
		Если Письмо.ФорматТекста = Перечисления.ФорматТекста.HTML тогда
			Текст = "</p><p><div><table border=0 cellpadding=1 cellspacing=2><tr valign=top>" + 
					"<td width=7><p>&gt;</p></td>" + 
					"<td>" + ТекстИсходный + "</td>" + 
					"</tr></table></div></p><p>";
		Иначе
			Текст = ">> " + СтрЗаменить(ТекстИсходный,Символы.ПС,Символы.ПС+">> ");
		КонецЕсли;
		Результат = СтрЗаменить(Результат, "%ЦитированныйТекст", Текст);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // эпСформироватьТекстПисьмаПоШаблону()

// Функция возвращает строку содержащую список имен файлов, прикрепленных к письму
// (динамически подключаемый обработчик)
// Параметры:
//   Письмо    - ДокументСсылка.ЭлектронноеПисьмо, письмо-основание
//
// Возвращаемое значение:
//   Список    - Строка - сформированный список имен файлов
//
Функция СформироватьПредставлениеПрикрепленныхФайлов(Письмо)
	Результат = "";
	Для каждого Файл из Письмо.ПрикрепленныеФайлы цикл
		Если Результат <> "" тогда
			Результат = Результат + ", ";
		КонецЕсли;
		Результат = Результат + Файл.Имя;
	КонецЦикла;
	Возврат Результат;
КонецФункции // СформироватьПредставлениеПрикрепленныхФайлов()
	
// Процедура добавляет  к сообщению текст письма
//
// Параметры:
//  Сообщение - ИнтернетПочтовоеСообщение - подготавливаемое к отсылке сообщение электронной почты
//  Текст     - Строка, добавляемый текст сообщения.
//  ТипТекста - ПеречислениеСсылка.ФорматТекста или Пустая строка - тип текста письма
//
Процедура эпДобавитьТекст(Сообщение, Текст,ТипТекста = "") Экспорт
	
	Если  ТипТекста = ""  Тогда
		ФорматТекста = Строка(Перечисления.ФорматТекста.ПростойТекст);
	Иначе	
		Если  ТипТекста = Перечисления.ФорматТекста.ПростойТекст Тогда
			ФорматТекста = "ПростойТекст";
		ИначеЕсли  ТипТекста = Перечисления.ФорматТекста.HTML Тогда
			ФорматТекста = "HTML";
		КонецЕсли;
	КонецЕсли;	

	ТипТекстаСообщения = ТипТекстаПочтовогоСообщения[ФорматТекста];
	Сообщение.Тексты.Добавить(Текст ,ТипТекстаСообщения);
	
КонецПроцедуры // эпДобавитьТекст()

// Процедура добавляет адрес получателя сообщения. 
// 
// Параметры:
//  Сообщение        - ИнтернетПочтовоеСообщение - подготавливаемое к отсылке сообщение электронной почты
//  СтрокаПолучатели - СтрокаТабличнойЧасти - строка ТЧ "Получатели"
//
Процедура эпДобавитьАдрес(Сообщение, СтрокаПолучатели) Экспорт
	
	Если СтрокаПолучатели.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому Тогда // Кому
		ПочтовыеАдресаКому = Сообщение.Получатели.Добавить();
		ПочтовыеАдресаКому.Адрес = СтрокаПолучатели.АдресЭлектроннойПочты;
		ПочтовыеАдресаКому.ОтображаемоеИмя = СтрокаПолучатели.Представление;

	ИначеЕсли СтрокаПолучатели.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Копии Тогда // Копии
		ПочтовыеАдресаКопии = Сообщение.Копии.Добавить();
		ПочтовыеАдресаКопии.Адрес = СтрокаПолучатели.АдресЭлектроннойПочты;
		ПочтовыеАдресаКопии.ОтображаемоеИмя = СтрокаПолучатели.Представление;
		
	ИначеЕсли СтрокаПолучатели.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.СкрытыеКопии Тогда // Скрытые копии
		ПочтовыеАдресаСкрытыеКопии = Сообщение.СлепыеКопии.Добавить();
		ПочтовыеАдресаСкрытыеКопии.Адрес = СтрокаПолучатели.АдресЭлектроннойПочты;
		ПочтовыеАдресаСкрытыеКопии.ОтображаемоеИмя = СтрокаПолучатели.Представление;
	КонецЕсли;
	
КонецПроцедуры // эпДобавитьАдрес()

// Добавляет переданные файлы во вложение
// Параметры
//  ТЧПрикрепленныеФайлы           - Табличная часть - табличная часть документа "Электронное письмо".
//  МассивИменФайлов               - массив строк, полные имена файлов-вложения, существующих на диске.
//									 Массив используется в связи с тем что он 
//                                   возвращается свойством "ВыбранныеФайлы" объекта "ДиалогВыбораФайла"
//  УдалятьФайлыПослеДобавления    - Булево, признак нужно ли удалить переданные файлы после добавления в письмо
//                                   Внимание!! Файлы будут удалены не зависимо от того удалась отправка письма или нет
//  ЗадаватьВопросДляБольшихФайлов - Булево, признак нужно ли задавать вопрос на добавление файлов больше 25 мб
//
Процедура эпДобавитьВложенияВПисьмо(ТЧПрикрепленныеФайлы, МассивИменФайлов, УдалятьФайлыПослеДобавления = Ложь, ЗадаватьВопросДляБольшихФайлов = Истина) Экспорт
	
	Для каждого ПолученноеИмяФайла Из МассивИменФайлов Цикл
		Файл = Новый Файл(ПолученноеИмяФайла);
		ИмяФайла = рфПолучитьИмяФайлаИзПолногоИмени(ПолученноеИмяФайла);
		
		Если Файл.Размер() > 26214400 И ЗадаватьВопросДляБольшихФайлов Тогда
			#Если Клиент Тогда 
			Ответ = Вопрос("Размер добавляемого файла """ + ИмяФайла + """
						   |больше 25 Mb. Добавить ?", РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Нет,"Внимание!");
			   Если Ответ = КодВозвратаДиалога.Нет Тогда
				Продолжить;			   
			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда				   
				Возврат;
			КонецЕсли; 
			#КонецЕсли
			#Если Сервер Тогда 
				Продолжить;			   
			#КонецЕсли
		КонецЕсли; 
		НоваяСтрока = ТЧПрикрепленныеФайлы.Добавить();
		НоваяСтрока.Имя = рфОбрезатьИмяФайла(ИмяФайла, 100, Истина);
		#Если Клиент Тогда 
		Состояние("Добавляется файл: " + НоваяСтрока.Имя);
		#КонецЕсли
		НоваяСтрока.Файл = Новый ХранилищеЗначения(Новый ДвоичныеДанные(Файл.ПолноеИмя), Новый СжатиеДанных());
		НоваяСтрока.Размер = Файл.Размер();
		
		Если УдалятьФайлыПослеДобавления Тогда
			Попытка
				УдалитьФайлы(ПолученноеИмяФайла);
			Исключение
				Сообщить("Не удалось удалить файл: " +	ПолученноеИмяФайла);
			КонецПопытки
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры // эпДобавитьВложенияВПисьмо()

//Функция возвращает строковое представление для отдельного адреса.
//
// Параметры
//  АдресЭлектроннойПочты - Строка - E-Mail
//  НаименованиеОбъекта   - Строка - имя получателя
//
// Возвращаемое значение:
//   Строка   – Представление адреса
//
Функция элСформироватьПредставлениеАдресаЭлектроннойПочты(АдресЭлектроннойПочты, НаименованиеОбъекта) Экспорт
	Результат = "<" + СокрЛП(АдресЭлектроннойПочты) + ">";
	Если СокрЛП(НаименованиеОбъекта) <> "" тогда
		Результат = СокрЛП(НаименованиеОбъекта)+ " " + Результат;
	КонецЕсли;
	Возврат Результат;
КонецФункции


////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОЛУЧЕНИЯ ПОЧТЫ

// Функция выполняет выборку структуры сообщений.
//
// Параметры:
//  Соединение    - ИнтернетПочта, подключение к почтовому серверу
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты, учетная запись, 
// 				    для которой осуществляется выборка
//
// Возвращаемое значение:
//  Массив - массив структур описывающих полученные сообщения. 
//  КЛЮЧИ СТРУКТУРЫ
//		- ИмяОтправителя
//		- АдресОтправителя
//		- Тема
//		- ТипТекста
//		- Текст
//		- КоличествоВложений
//		- Вложения -содержит коллекцию объектов ИнтернетПочтовоеВложение. 
//		- Получатели - содержит коллекцию объектов ИнтернетПочтовыйАдрес. 
//		- Копии - Содержит коллекцию объектов ИнтернетПочтовыйАдрес. 
//		- ДатаОтправления
//		- ДатаПолучения
//		- ИдентификаторСообщения
//
Функция эпВыбратьМассивСообщений(Соединение, УчетнаяЗапись, ПолучениеПисемПриСтартеСистемы = Ложь) Экспорт 
	#Если Клиент Тогда
	Состояние("Выполняется обмен с почтовым сервером");
	#КонецЕсли

	МассивСообщений = Новый Массив();
	
	// получим массив заголовков полученных писем
	ПолученныеПисьмаУчетнойЗаписи = РегистрыСведений.ЗаголовкиПолученныхПисем.СоздатьМенеджерЗаписи();
	ПолученныеПисьмаУчетнойЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	ПолученныеПисьмаУчетнойЗаписи.Прочитать();
	Если ПолученныеПисьмаУчетнойЗаписи.Выбран() Тогда
		МассивПолученныхПисем = ПолученныеПисьмаУчетнойЗаписи.ЗаголовкиПисем.Получить();
	Иначе
		МассивПолученныхПисем = Новый Массив();
	КонецЕсли;
	
	// выгрузим массив заголовков полученных писем в список
	СписокПолученныхПисем = Новый СписокЗначений;
	СписокПолученныхПисем.ЗагрузитьЗначения(МассивПолученныхПисем);
	
	СтруктураЗаголовков = Новый Структура;
	Если ПолучениеПисемПриСтартеСистемы Тогда
		Если УчетнаяЗапись.КоличествоДнейПриемаНовойПочты > 0 Тогда
			СтруктураЗаголовков.Вставить("ПослеДатыОтправления", НачалоДня((ТекущаяДата() - (УчетнаяЗапись.КоличествоДнейПриемаНовойПочты -1)*60*60*24)));
		КонецЕсли;
	КонецЕсли;
	
	// получим массив заголовков писем на сервере
	Попытка
		МассивПисемНаСервере = Соединение.ПолучитьЗаголовки(СтруктураЗаголовков);
	Исключение
		обОписаниеОшибки();
		Возврат МассивСообщений;
	КонецПопытки;
	
	МассивПолученныхПисем.Очистить();
	НомерПисьмаВМассиве = 0;
	Пока НомерПисьмаВМассиве < МассивПисемНаСервере.Количество() Цикл
		ПисьмоНаСервере = МассивПисемНаСервере.Получить(НомерПисьмаВМассиве);
		МассивПолученныхПисем.Добавить(ПисьмоНаСервере.ИдентификаторСообщения);
		ПолученноеПисьмо = СписокПолученныхПисем.НайтиПоЗначению(ПисьмоНаСервере.ИдентификаторСообщения);
		// если заголовка нет в списке полученных, добавим заголовок в массив полученных
		Если ПолученноеПисьмо = Неопределено Тогда
			НомерПисьмаВМассиве = НомерПисьмаВМассиве + 1;
		// если заголовок есть в списке полученных, удалим заголовок из массива писем на сервере
		Иначе
			МассивПисемНаСервере.Удалить(НомерПисьмаВМассиве);
			СписокПолученныхПисем.Удалить(ПолученноеПисьмо);
		КонецЕсли;
	КонецЦикла;
	
	Если Не МассивПисемНаСервере.Количество() = 0 Тогда
		Попытка
			Сообщения = Соединение.Выбрать(УчетнаяЗапись.УдалятьСообщенияССервера, МассивПисемНаСервере);
		Исключение
			обОписаниеОшибки();
			Возврат МассивСообщений;
		КонецПопытки;
		
		// сохраним  массив полученных писем
		ХранилищеПолученныхПисем = Новый ХранилищеЗначения(МассивПолученныхПисем);
		ПолученныеПисьмаУчетнойЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		ПолученныеПисьмаУчетнойЗаписи.ЗаголовкиПисем = ХранилищеПолученныхПисем;
		ПолученныеПисьмаУчетнойЗаписи.Записать();
		
		КоличествоСообщений = Сообщения.Количество();
		
		Для каждого Сообщение Из Сообщения Цикл
			
			СтруктураСообщения = Новый Структура;

			СтруктураСообщения.Вставить("ИмяОтправителя",Сообщение.ИмяОтправителя);
			СтруктураСообщения.Вставить("АдресОтправителя",Сообщение.Отправитель.Адрес);	
			СтруктураСообщения.Вставить("Тема",Сообщение.Тема);
			
			ХТМЛ_Текст = "";
			Простой_Текст = "";
			Прочий_Текст = "";
			
			Для каждого ТекстПочтовогоСообщения Из Сообщение.Тексты Цикл
				Если ТекстПочтовогоСообщения.ТипТекста = ТипТекстаПочтовогоСообщения.HTML Тогда
					ХТМЛ_Текст = ХТМЛ_Текст + элЗаменитьНедопустимыеСимволыXML(ТекстПочтовогоСообщения.Текст);
				ИначеЕсли ТекстПочтовогоСообщения.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст Тогда
					Простой_Текст = ТекстПочтовогоСообщения.Текст;
				Иначе
					Прочий_Текст = ТекстПочтовогоСообщения.Текст;
				КонецЕсли; 
			КонецЦикла;
		
			Если НЕ обЗначениеНеЗаполнено(ХТМЛ_Текст) Тогда
				ТекстПисьма = ХТМЛ_Текст;
				ТипТекста   = Перечисления.ФорматТекста.HTML;
			ИначеЕсли НЕ обЗначениеНеЗаполнено(Простой_Текст) Тогда
				ТекстПисьма = СтрЗаменить(Простой_Текст, Символ(13), "");
				ТипТекста   = Перечисления.ФорматТекста.ПростойТекст;
			ИначеЕсли НЕ обЗначениеНеЗаполнено(Прочий_Текст) Тогда
				ТекстПисьма = Прочий_Текст;
				ТипТекста   = Перечисления.ФорматТекста.ПростойТекст;
			КонецЕсли; 
				
			СтруктураСообщения.Вставить("ТипТекста" , ТипТекста);
			СтруктураСообщения.Вставить("Текст" , ТекстПисьма);
			СтруктураСообщения.Вставить("КоличествоВложений", Сообщение.Вложения.Количество());
			
			СтруктураСообщения.Вставить("Вложения",Сообщение.Вложения);
			СтруктураСообщения.Вставить("Получатели",Сообщение.Получатели);
			СтруктураСообщения.Вставить("Копии", Сообщение.Копии);
			
			СтруктураСообщения.Вставить("ДатаОтправления", Сообщение.ДатаОтправления);
			СтруктураСообщения.Вставить("ДатаПолучения", Сообщение.ДатаПолучения);

	        СтруктураСообщения.Вставить("ИдентификаторСообщения",Сообщение.ИдентификаторСообщения);
			
			СтруктураСообщения.Вставить("Важность",Сообщение.Важность);
			СтруктураСообщения.Вставить("УведомитьОПрочтении",Сообщение.УведомитьОПрочтении);
			
			
			МассивСообщений.Добавить(СтруктураСообщения);
			
		КонецЦикла; 
	КонецЕсли;
	
	Возврат МассивСообщений;
КонецФункции // эпВыбратьМассивСообщений()

// Процедура создает документ "Электронное письмо" и заполняет его реквизиты на основании полученного сообщения
//
// Параметры:
//  Сообщение     - Структура, содержит частично преобразованные данные полученного электронного сообщения
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты, учетная запись эл. почты
//
Процедура эпСоздатьПолученноеПисьмо(Сообщение, УчетнаяЗапись) Экспорт
	#Если Клиент Тогда
	Состояние("Запись полученных писем");
	#КонецЕсли
	
	НовоеПисьмо = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
	
	// Заполним типовые реквизиты документа
	дкОбработкаЗаполненияПоУмолчанию(НовоеПисьмо);
	НовоеПисьмо.ХозОперация = Справочники.ХозОперации.ЭлектронноеПисьмо;
	НовоеПисьмо.Дата = ТекущаяДата();
	НовоеПисьмо.УстановитьНовыйНомер();
	

	// адресаты
	НовоеПисьмо.ОтправительПредставление = Сообщение.ИмяОтправителя + " <" + Сообщение.АдресОтправителя + ">";
	
	Если обЗначениеНеЗаполнено(Сообщение.Тема) Тогда
		НовоеПисьмо.Тема = "<Тема не указана>"; 
	Иначе
		НовоеПисьмо.Тема = Сообщение.Тема;
	КонецЕсли;
	
	НовоеПисьмо.ДатаОтправления = Сообщение.ДатаОтправления;
	НовоеПисьмо.ДатаПолучения   = Сообщение.ДатаПолучения;
	
	НовоеПисьмо.ТекстПисьма 	   = Сообщение.Текст;
	НовоеПисьмо.ФорматТекста    = Сообщение.ТипТекста;
	
	НовоеПисьмо.ИдентификаторПисьма = Сообщение.ИдентификаторСообщения;
	
	// Заполним табличную часть "Получатели"
	Для каждого Получатель из Сообщение.Получатели Цикл
		НовыйПолучатель = НовоеПисьмо.Получатели.Добавить();
		НовыйПолучатель.Представление = Получатель.Пользователь;
		НовыйПолучатель.АдресЭлектроннойПочты = Получатель.Адрес;
		НовыйПолучатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
	КонецЦикла; 
	
	Для каждого Получатель из Сообщение.Копии Цикл
		НовыйПолучатель = НовоеПисьмо.Получатели.Добавить();
		НовыйПолучатель.Представление = Получатель.Пользователь;
		НовыйПолучатель.АдресЭлектроннойПочты = Получатель.Адрес;
		НовыйПолучатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Копии;
	КонецЦикла; 
	
	НовоеПисьмо.ПолучателиПредставление = НовоеПисьмо.ПредставлениеПолучателей();
	
	НовоеПисьмо.УчетнаяЗапись = УчетнаяЗапись;
	НовоеПисьмо.ГруппаПисем   = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Входящие",Истина,,УчетнаяЗапись);
	НовоеПисьмо.СтатусПисьма  = Перечисления.СтатусыСообщений.Входящее;
	НовоеПисьмо.НеРассмотрено = Истина;
	
	НовоеПисьмо.ЕстьВложения    = ?(Сообщение.КоличествоВложений > 0,Истина,Ложь);
	
	Если НовоеПисьмо.ЕстьВложения Тогда
		эпЗаполнитьПрикрепленныеФайлы(Сообщение, НовоеПисьмо);
	КонецЕсли;	
	
	Если Сообщение.Важность = ВажностьИнтернетПочтовогоСообщения.Высокая ИЛИ Сообщение.Важность = ВажностьИнтернетПочтовогоСообщения.Наивысшая
		тогда НовоеПисьмо.Важность = Перечисления.Важность.Высокая;
	ИначеЕсли Сообщение.Важность = ВажностьИнтернетПочтовогоСообщения.Низкая ИЛИ Сообщение.Важность = ВажностьИнтернетПочтовогоСообщения.Наименьшая
		тогда НовоеПисьмо.Важность = Перечисления.Важность.Низкая;
	Иначе НовоеПисьмо.Важность = Перечисления.Важность.Средняя;
	КонецЕсли;
	
	НовоеПисьмо.УведомитьОПрочтении = Сообщение.УведомитьОПрочтении;
	
	Попытка
		НовоеПисьмо.Записать();
	Исключение
		Сообщить("При записи объекта <"+Строка(НовоеПисьмо)+"> вознакли ошибки: "+ОписаниеОшибки()+";",СтатусСообщения.Важное);
	КонецПопытки;
	
КонецПроцедуры // эпСоздатьПолученноеПисьмо()

// Функция получает письма с почтового сервера
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты, учетная запись, 
// 				     для которой получаются письма
//   АвтоПолучение - Булево - Если не установлен, вызываем обновление дерева групп в АРМКоммуникатор
// Возвращаемое значение:
//	 Число - Количество новых полученных писем
//
Функция эпПолучитьЭлектронныеПисьма(УчетнаяЗапись, АвтоПолучение = Ложь, ПолучениеПисемПриСтартеСистемы = Ложь) Экспорт
	// Проверим, а указаны для учетной записи параметры POP3 сервера.
	// Если нет, то информируем об этом пользователя и прерываем получение.
	Если ПустаяСтрока(УчетнаяЗапись.POP3Сервер) Или ПустаяСтрока(УчетнаяЗапись.Логин) Тогда
		#Если Клиент Тогда
			Сообщить("Для учетной записи <" + УчетнаяЗапись + "> не указаны параметры POP3 сервера.
			|Получение почты невозможно.", СтатусСообщения.Информация);	
		#КонецЕсли
		Возврат 0;
	КонецЕсли;
	
	КоличествоНовыхПисем = 0;
	СтруктураРежимовОбмена = Новый Структура("Получить, Отправить", Истина, Ложь);
	СоединениеСПочтовымСервером = эпПодключитьсяКПочтовомуСерверу(УчетнаяЗапись, СтруктураРежимовОбмена);
	Если СоединениеСПочтовымСервером = Неопределено Тогда
		Возврат КоличествоНовыхПисем;
	КонецЕсли;
	
	Сообщения = эпВыбратьМассивСообщений(СоединениеСПочтовымСервером, УчетнаяЗапись, ПолучениеПисемПриСтартеСистемы);
	
	Если Сообщения.Количество() = 0 Тогда
		Возврат КоличествоНовыхПисем;
	КонецЕсли;
	
	Для каждого сообщение из  сообщения Цикл
		// если письмо уже загружено, тогда не будем его загружать.
		ПустаяСсылка = Документы.ЭлектронноеПисьмо.ПустаяСсылка();
		Если НЕ Документы.ЭлектронноеПисьмо.НайтиПоРеквизиту("ИдентификаторПисьма",Сообщение.ИдентификаторСообщения) = ПустаяСсылка Тогда 
			Продолжить;
		КонецЕсли;	
		эпСоздатьПолученноеПисьмо(Сообщение, УчетнаяЗапись);
		КоличествоНовыхПисем = КоличествоНовыхПисем + 1;
	КонецЦикла;
	
	эпОтключитьсяОтПочтовогоСервера(СоединениеСПочтовымСервером);
	
	Если Не АвтоПолучение Тогда
		#Если Клиент Тогда
		Если КоличествоНовыхПисем > 0 Тогда
			Оповестить("ОбновитьДеревоГрупп", "Пересчитать");
		КонецЕсли;
		#КонецЕсли
	КонецЕсли;

	Возврат КоличествоНовыхПисем;
КонецФункции // эпПолучитьЭлектронныеПисьма()

// Функция создает новый объект Документ.ЭлектронноеПисьмо и заполняет
// его данными из объекта ИнтернетПочтовоеСообщение, только без аттачей
//
// Параметры
//  ИнтернетПисьмо       - ИнтернетПочтовоеСообщение
//  УчетнаяЗапись        - СправочникСсылка.УчетныеЗаписи, учетная запись письма
//  ТекущийПользователь  - СправочникСсылка.Пользователи
//  мРазделительАдресов  - Строка, разделитель адресов
//  ИсключениеТранзакции - булево, флаг ошибки
//
// Возвращаемое значение:
//   Письмо - ДокументОбъект.ЭлектронноеПисьмо.
//
Функция эпПолучитьДокументОбъектИзИнтернетПочтовогоСообщения(ИнтернетПисьмо, УчетнаяЗапись = Неопределено, ТекущийПользователь, мРазделительАдресов = ",", ИсключениеТранзакции = Ложь) Экспорт

	Письмо = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
	
	Если ТипЗнч(УчетнаяЗапись) <> Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
		Письмо.УчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.СоздатьЭлемент();
	Иначе
		Письмо.УчетнаяЗапись = УчетнаяЗапись;
	КонецЕсли; 
	
	Для каждого Получатель из ИнтернетПисьмо.Получатели Цикл
		НовыйПолучатель = Письмо.Получатели.Добавить();
		НовыйПолучатель.Представление = Получатель.Пользователь;
		НовыйПолучатель.АдресЭлектроннойПочты = Получатель.Адрес;
		НовыйПолучатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому;
	КонецЦикла; 
	
	Для каждого Получатель из ИнтернетПисьмо.Копии Цикл
		НовыйПолучатель = Письмо.Получатели.Добавить();
		НовыйПолучатель.Представление = Получатель.Пользователь;
		НовыйПолучатель.АдресЭлектроннойПочты = Получатель.Адрес;
		НовыйПолучатель.КодГруппыАдреса = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Копии;
	КонецЦикла;
	
	Письмо.ПолучателиПредставление = Письмо.ПредставлениеПолучателей();
	
	ГруппыПисем = Справочники.ГруппыПисемЭлектроннойПочты.Выбрать(,УчетнаяЗапись,);
	Пока ГруппыПисем.Следующий() Цикл
		Если Найти(ВРег(ГруппыПисем.Наименование),"ВХОДЯЩИЕ") > 0 Тогда
			Письмо.ГруппаПисем = ГруппыПисем.Ссылка;
			Прервать
		КонецЕсли;	
	КонецЦикла;	
	
	Письмо.Тема = ИнтернетПисьмо.Тема;
	Письмо.СтатусПисьма = Перечисления.СтатусыСообщений.Входящее;
	
	Письмо.Дата            = ТекущаяДата();
	Письмо.ДатаОтправления = ИнтернетПисьмо.ДатаОтправления;
	Письмо.ДатаПолучения   = ИнтернетПисьмо.ДатаПолучения;
	
	эпУстановитьТекстПисьма(ИнтернетПисьмо, Письмо);
	
	Если ИнтернетПисьмо.Вложения.Количество() > 0 Тогда
		Письмо.ЕстьВложения = Истина;
	КонецЕсли; 
	
	Письмо.НеРассмотрено = Ложь;
	
	Возврат Письмо;
	
КонецФункции // эпПолучитьДокументОбъектИзИнтернетПочтовогоСообщения()

// Функция в уже созданный объект Документ.ЭлектронноеПисьмо устанавливает
// текст письма из объекта ИнтернетПочтовоеСообщение
//
// Параметры
//  ИнтернетПисьмо  – ИнтернетПочтовоеСообщение,
//  ПисьмоОбъект    – ДокументОбъект.ЭлектронноеПисьмо - Электронное письмо в котором устанавливаем текст.
//
Процедура эпУстановитьТекстПисьма(ИнтернетПисьмо, ПисьмоОбъект) Экспорт
	
	ХТМЛ_Текст = "";
	Простой_Текст = "";
	Прочий_Текст = "";
	
	Для каждого ТекстПочтовогоСообщения Из ИнтернетПисьмо.Тексты Цикл
		Если ТекстПочтовогоСообщения.ТипТекста = ТипТекстаПочтовогоСообщения.HTML Тогда
			ХТМЛ_Текст = ХТМЛ_Текст + элЗаменитьНедопустимыеСимволыXML(ТекстПочтовогоСообщения.Текст);
		ИначеЕсли ТекстПочтовогоСообщения.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст Тогда
			Простой_Текст = ТекстПочтовогоСообщения.Текст;
		Иначе
			Прочий_Текст = ТекстПочтовогоСообщения.Текст;
		КонецЕсли; 
	КонецЦикла;
	
	Если НЕ обЗначениеНеЗаполнено(ХТМЛ_Текст) Тогда
		ПисьмоОбъект.ФорматТекста = Перечисления.ФорматТекста.HTML;
		ПисьмоОбъект.ТекстПисьма = ХТМЛ_Текст;
	ИначеЕсли НЕ обЗначениеНеЗаполнено(Простой_Текст) Тогда
		ПисьмоОбъект.ФорматТекста = Перечисления.ФорматТекста.ПростойТекст;
		ПисьмоОбъект.ТекстПисьма = СтрЗаменить(Простой_Текст, Символ(13), "");
	ИначеЕсли НЕ обЗначениеНеЗаполнено(Прочий_Текст) Тогда
		ПисьмоОбъект.ФорматТекста   = Перечисления.ФорматТекста.ПростойТекст;		
		ТекстПисьма = Прочий_Текст;
	КонецЕсли; 
	
КонецПроцедуры // ПолучитьТекстПисьма()

// Отвечает за прикрепленные в теле письма файлы
//
// Параметры:
//  Сообщение     - Структура, содержит частично преобразованные данные полученного электронного сообщения
//  Письмо        - – ДокументОбъект.ЭлектронноеПисьмо - Электронное письмо.
//
Процедура эпЗаполнитьПрикрепленныеФайлы(Сообщение, Письмо)
	ИмяКаталога = КаталогВременныхФайлов();
	НомерСообщения = 0;
	Для Каждого Вложение из Сообщение.Вложения  Цикл
		
		Если ТипЗнч(Вложение.Данные) = Тип("ДвоичныеДанные") Тогда
			
			
			НовоеВложение = Письмо.ПрикрепленныеФайлы.Добавить();
			НовоеВложение.Имя = рфОбрезатьИмяФайла(Вложение.Имя, 100);
			НовоеВложение.Файл = Новый ХранилищеЗначения(Вложение.Данные, Новый СжатиеДанных());
			
			// сохраним файл во временной папке
			СпособПерезаписи = "ДА";	
			ИмяФайла = рфПолучитьИмяФайла(ИмяКаталога, Вложение.Имя);
			Если НЕ рфСохранитьФайлНаДиске(Вложение.Данные, ИмяФайла, , СпособПерезаписи) Тогда
				Прервать;
			КонецЕсли;
			
			Файл = Новый Файл(ИмяФайла);
			НовоеВложение.Размер = Файл.Размер();
			
			УдалитьФайлы(ИмяФайла,);
			
		ИначеЕсли ТипЗнч(Вложение.Данные) = Тип("ИнтернетПочтовоеСообщение") Тогда
			
			НовоеВложение = Письмо.ПрикрепленныеФайлы.Добавить();
			
			Если ПустаяСтрока(Вложение.Имя) Тогда
				НомерСообщения = НомерСообщения + 1;
				ИмяПочтовогоСообщения = "Message" + НомерСообщения;
			Иначе
				ИмяПочтовогоСообщения = Вложение.Имя;
			КонецЕсли;	
			ИмяПочтовогоСообщения = ИмяПочтовогоСообщения + ".msg";
			
			НовоеВложение.Имя = рфОбрезатьИмяФайла(ИмяПочтовогоСообщения, 100);
			НовоеВложение.Файл = Новый ХранилищеЗначения(Вложение.Данные, Новый СжатиеДанных());
			
		КонецЕсли;
			
	КонецЦикла;
КонецПроцедуры// эпЗаполнитьПрикрепленныеФайлы()

// Процедура открывает файл из хранилища с учетом расширения.
//
// Параметры:
//  Данные - ДвоичныеДанные - файл сохраненный в хранилище значений.
//
Процедура эпОткрытьВложенноеСообщениеИзХранилища(Данные) Экспорт
	
	ПочтовоеСообщение = Данные.Получить();
	Если ТипЗнч(ПочтовоеСообщение) <> Тип("ИнтернетПочтовоеСообщение") Тогда
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
		
	Если ТипЗнч(ПочтовоеСообщение.Отправитель) = Тип("Строка") Тогда
		Запрос.УстановитьПараметр("АдресЭлектроннойПочты", ПочтовоеСообщение.Отправитель);
	Иначе
		Запрос.УстановитьПараметр("АдресЭлектроннойПочты", ПочтовоеСообщение.Отправитель.Адрес);
	КонецЕсли;
		
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.АдресЭлектроннойПочты = &АдресЭлектроннойПочты
	|";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		УчетнаяЗапись = Выборка.Ссылка;
	КонецЕсли; 
		
	ДокументПисьмо = эпПолучитьДокументОбъектИзИнтернетПочтовогоСообщения(ПочтовоеСообщение, УчетнаяЗапись, ПараметрыСеанса.Пользователь,,);
		
	ДокументПисьмо.СтатусПисьма = Перечисления.СтатусыСообщений.Отправленное;
		
	ФормаПисьма = ДокументПисьмо.ПолучитьФорму();
	ФормаПисьма.ТолькоПросмотр = Истина;
		
	Если ДокументПисьмо.ЕстьВложения Тогда
		эпЗаполнитьПрикрепленныеФайлы(ПочтовоеСообщение, ДокументПисьмо);
	КонецЕсли;
		
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить(УчетнаяЗапись, УчетнаяЗапись);
	ФормаПисьма.ЭлементыФормы.УчетнаяЗапись.СписокВыбора = СписокВыбора;
		
	ФормаПисьма.Открыть();

КонецПроцедуры// эпОткрытьВложенноеСообщениеИзХранилища()

// Заменяет недопустимые символы в XML-строке на заданные символы.
//
// Параметры:
//   Текст - Строка - строка, в которой требуется выполнить замену недопустимых символов.
//   СимволЗамены - Строка - строка, на которую требуется выполнить замену недопустимого символа в XML-строке.
// 
//  Возвращаемое значение:
//    Строка - строка, полученная заменой недопустимых символов в XML-строке.
//
Функция элЗаменитьНедопустимыеСимволыXML(Знач Текст, СимволЗамены = " ") Экспорт
	
	ПозицияНачала = 1;
	Позиция = НайтиНедопустимыеСимволыXML(Текст, ПозицияНачала);
	Пока Позиция > 0 Цикл
		НедопустимыйСимвол = Сред(Текст, Позиция, 1);
		Текст = СтрЗаменить(Текст, НедопустимыйСимвол, СимволЗамены);
		ПозицияНачала = Позиция + СтрДлина(СимволЗамены);
		Если ПозицияНачала > СтрДлина(Текст) Тогда
			Прервать;
		КонецЕсли;
		Позиция = НайтиНедопустимыеСимволыXML(Текст, ПозицияНачала);
	КонецЦикла;
	
	Возврат Текст;
	
КонецФункции

////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ АВТОПОЛУЧЕНИЯ ПОЧТЫ


// Функция формирует таблицу учетных записей для отправки/получения писем
// текущим пользователем.
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   ТаблицаЗначений
//
Функция ПолучитьТаблицуУчетныхЗаписейДляТранспорта()

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.Пользователь);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка                                 КАК УчетнаяЗапись,
	|	УчетныеЗаписиЭлектроннойПочты.АдресЭлектроннойПочты                  КАК АдресЭлектроннойПочты,
	|	УчетныеЗаписиЭлектроннойПочты.ИнтервалАвтоПолученияОтправкиСообщений КАК ИнтервалОбновления,
	|	УчетныеЗаписиЭлектроннойПочты.ДействиеАвтополученияОтправкиСообщений КАК Действие
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.АвтоПолучениеОтправкаСообщений
	|	И
	|	УчетныеЗаписиЭлектроннойПочты.ОтветственныйЗаАвтоПолучениеОтправкуСообщений = &ТекущийПользователь
	|	И
	|	НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // эпЗаполнитьПрикрепленныеФайлы()

// Процедура обновляет список необходимых для транспорта текущим пользователем
// учетных записей.
//
// Параметры:
//  УчетныеЗаписиАвтоматическогоТранспортаПисем - ТаблицаЗначений - таблица учетных записей автоматического
//																	транспорта электронной почты.
//  ПервоеЗаполнение                            - Булево - если это автополучение при начале работы программы,
//														   тогда получает письма всех учетных записей.
//
Процедура эпОбновитьТаблицуУчетныхЗаписейАвтоматическогоТранспортаПочты(УчетныеЗаписиАвтоматическогоТранспортаПисем, ПервоеЗаполнение = Ложь) Экспорт
	
	ТаблицаУчетныхЗаписей = ПолучитьТаблицуУчетныхЗаписейДляТранспорта();

	#Если Клиент Тогда
		СохраненнаяТаблица = ВосстановитьЗначение("ТаблицаУчетныхЗаписейАвтоПолученияОтправки");
	#Иначе
		СохраненнаяТаблица = Неопределено;
	#КонецЕсли

	Если ТипЗнч(СохраненнаяТаблица) = Тип("ТаблицаЗначений") Тогда
		Для каждого СтрокаТаблицы Из СохраненнаяТаблица Цикл
			НайденнаяСтрока = ТаблицаУчетныхЗаписей.Найти(СтрокаТаблицы.УчетнаяЗапись, "УчетнаяЗапись");
			Если НайденнаяСтрока <> Неопределено Тогда
				Если НайденнаяСтрока.ИнтервалОбновления <> СтрокаТаблицы.ИнтервалОбновления Тогда
					Сообщить("Изменился интервал автополучения/отправки писем для учетной записи """ + НайденнаяСтрока.УчетнаяЗапись + """. Новое значение " + НайденнаяСтрока.ИнтервалОбновления + " мин.");
				КонецЕсли;
				Если НайденнаяСтрока.Действие <> СтрокаТаблицы.Действие Тогда
					Сообщить("Изменилось действие автополучения/отправки писем для учетной записи """ + НайденнаяСтрока.УчетнаяЗапись + """. Новое значение " + НайденнаяСтрока.Действие + ".");
				КонецЕсли;
			Иначе
				Сообщить("Из списка автополучения/отправки удалена учетная запись """ + СтрокаТаблицы.УчетнаяЗапись + """.");
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	УчетныеЗаписиАвтоматическогоТранспортаПисем.Очистить();
	
	Для каждого СтрокаТаблицы Из ТаблицаУчетныхЗаписей Цикл
		
		НоваяСтрокаТаблицыУчетныхЗаписей = УчетныеЗаписиАвтоматическогоТранспортаПисем.Добавить();
		НоваяСтрокаТаблицыУчетныхЗаписей.УчетнаяЗапись            = СтрокаТаблицы.УчетнаяЗапись;
		НоваяСтрокаТаблицыУчетныхЗаписей.ИнтервалОбновления       = СтрокаТаблицы.ИнтервалОбновления;
		НоваяСтрокаТаблицыУчетныхЗаписей.АдресЭлектроннойПочты    = СтрокаТаблицы.АдресЭлектроннойПочты;
		НоваяСтрокаТаблицыУчетныхЗаписей.Действие                 = СтрокаТаблицы.Действие;
		Если ПервоеЗаполнение Тогда
			// если это автополучение при начале работы программы, тогда получим письма всех учетных записей
			НоваяСтрокаТаблицыУчетныхЗаписей.ВремяПоследнегоПолучения = '00010101';
		Иначе
			НоваяСтрокаТаблицыУчетныхЗаписей.ВремяПоследнегоПолучения = ТекущаяДата();
		КонецЕсли;	
		
		Если ТипЗнч(СохраненнаяТаблица) = Тип("ТаблицаЗначений") Тогда
			НайденнаяСтрока = СохраненнаяТаблица.Найти(СтрокаТаблицы.УчетнаяЗапись, "УчетнаяЗапись");
		Иначе
			НайденнаяСтрока = Неопределено;
		КонецЕсли;
		
		Если НайденнаяСтрока = Неопределено Тогда
			Сообщить("В список автополучения/отправки добавлена учетная запись """ + СтрокаТаблицы.УчетнаяЗапись + """. Интервал обновления " + СтрокаТаблицы.ИнтервалОбновления + " мин. Действие: " + СтрокаТаблицы.Действие);
		КонецЕсли; 
	
	КонецЦикла;
	
	#Если Клиент Тогда
		СохранитьЗначение("ТаблицаУчетныхЗаписейАвтоПолученияОтправки", ТаблицаУчетныхЗаписей.Скопировать());
	#КонецЕсли
	
КонецПроцедуры // эпОбновитьТаблицуУчетныхЗаписейАвтоматическогоТранспортаПочты()

#Если Клиент Тогда
	
// Процедура обновляет список учетных записей автоотправки/получения
// и время последнего транспорта данным пользователем.
//
// Параметры:
//  УчетныеЗаписиАвтоматическогоТранспортаПисем - ТаблицаЗначений - таблица учетных записей автоматического
//												  транспорта электронной почты.
//
Процедура эпПодключитьАвтоматическийТранспортПочты(УчетныеЗаписиАвтоматическогоТранспортаПисем) Экспорт
	
	эпОбновитьТаблицуУчетныхЗаписейАвтоматическогоТранспортаПочты(УчетныеЗаписиАвтоматическогоТранспортаПисем, Истина);
	Если УчетныеЗаписиАвтоматическогоТранспортаПисем.Количество() > 0 Тогда
		ПодключитьОбработчикОжидания("глАвтоматическаяПроверкаПисем", 1);
	Иначе
		СохранитьЗначение("ПолучениеПисемПриСтартеСистемы", Ложь);
	КонецЕсли;
	
КонецПроцедуры // эпПодключитьАвтоматическийТранспортПочты()

#КонецЕсли

///////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С RSS НОВОСТЯМИ И ВСТРОЕННЫМИ УЧЕТНЫМИ ЗАПИСЯМИ

// Функция формирует адрес к RSS ленте текущего программного продукта.
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Строка - сформированный адрес.
//
Функция элСформироватьАдресНаНовостнойПотокПродукта() Экспорт

	Адрес = "http://update.rarus.ru/public/release-news/product" + СокрЛП(орПолучитьИнформациюОПродукте().КодПродукта) + ".xml";
	Возврат Адрес;

КонецФункции // СформироватьАдресНаНовостнойПотокПродукта()

// Функция разбирает переданный адрес URL на составляющие: адрес сервера и адрес ресурса.
//
// Параметры
//  УРЛ - "Строка" - адрес на ресурс в интернете.
//
// Возвращаемое значение:
//   Структура: Сервер - адрес сервера
//				Ресурс - адрес ресурса на сервере.
// 
Функция эпПолучитьАдресСервераИРесурса(Знач УРЛ) Экспорт
	// Ищем www
	НомерПозицииWWW = Найти(УРЛ, "www.");
	Если НомерПозицииWWW=0 Тогда
		НомерПозицииWWW = Найти(УРЛ, "http://");
		Если НомерПозицииWWW=0 Тогда
			Возврат Ложь;
		Иначе
			УРЛ = Сред(УРЛ, НомерПозицииWWW+7);
			УРЛ = "www." + УРЛ;
		КонецЕсли;
	Иначе
		УРЛ = Сред(УРЛ, НомерПозицииWWW);		
	КонецЕсли;
	
	
	НомерПозицииСлэша = Найти(УРЛ, "/");
	Если НомерПозицииСлэша=0 Тогда
		Возврат Ложь;	  
	КонецЕсли;
	
	Сервер = Лев(УРЛ, НомерПозицииСлэша-1); 
	Ресурс = Сред(УРЛ, НомерПозицииСлэша+1);
	
	Если Ресурс="" Тогда
		Возврат Ложь;	  
	КонецЕсли;
	
	Возврат Новый Структура("Сервер,Ресурс", Сервер, Ресурс);  
	
КонецФункции // элПолучитьАдресСервераИРесурса()

// Функция проверяет возможность подключения к указанному новостному RSS потоку.
//
// Параметры
//  Ссылка - "СправочникСсылка.УчетныеЗаписиЭлектроннойПочты" - ссылка  на учетную запись новости.
//
// Возвращаемое значение:
//   Строка - если все прошло успешно, возвращает пустую строку. Иначе возвращает описание ошибки.
//
Функция эпПроверитьПодключениеКRSS(Ссылка) Экспорт
	// Сначала разберем адрес на сервер и ресурс
	Структура = эпПолучитьАдресСервераИРесурса(Ссылка.АдресЭлектроннойПочты);
	Если ТипЗнч(Структура)<>Тип("Структура") Тогда
		Возврат "Неправильно задан адрес новости! Пример правильного формата адреса: www.1c.ru/news/rss-2.0.jsp";	
	КонецЕсли;
	
	// Пробуем получить XML файл новости с сервера
	Попытка 
		HTTPСоединение = Новый HTTPСоединение(Структура.Сервер);
		HTTPСоединение.Получить(Структура.Ресурс, КаталогВременныхФайлов() + "News.xml");
		HTTPСоединение = "";
	Исключение
		Возврат РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке()); 
	КонецПопытки;
	
	Возврат "";
КонецФункции

// Функция получает новости RSS, разбирает их и создает электронные письма (входящие)
//
// Параметры
//  УчетнаяЗапись - "СправочникСсылка.УчетныеЗаписиЭлектроннойПочты" - ссылка на новостную учетную запись.
//
//  Параметры - "Строка" - здесь указываются дополнительные параметры http соединения.
//
//  ПолучитьВсе - "Булево" - признак получения всех новостей, без учета уже полученных.
// 
// Возвращаемое значение:
//   НЕТ
// 
Процедура эпПолучитьНовости(УчетнаяЗапись, Параметры="", ПолучитьВсе = Ложь) Экспорт
	Если Не УчетнаяЗапись.ЭтоУчетнаяЗаписьНовостей Тогда
		Возврат;	
	КонецЕсли; 	
	ЕстьНовыеПисьма = Ложь;
	
	Структура = эпПолучитьАдресСервераИРесурса(УчетнаяЗапись.АдресЭлектроннойПочты);
	Если ТипЗнч(Структура)<>Тип("Структура") Тогда
		#Если Клиент Тогда
			Сообщить("Неправильно задан адрес новости! Пример правильного формата адреса: www.1c.ru/news/rss-2.0.jsp", СтатусСообщения.Важное);	
		#КонецЕсли 
		Возврат;	
	КонецЕсли;
	
	// Пробуем получить XML файл новости с сервера	
	#Если Клиент Тогда 
		Состояние("Получение файла новости...");		
	#КонецЕсли 
	
	Попытка 
		HTTPСоединение = Новый HTTPСоединение(Структура.Сервер);
		HTTPСоединение.Получить(Структура.Ресурс, КаталогВременныхФайлов() + "News.xml", Параметры);		
		HTTPСоединение = "";
	Исключение
		Попытка 
			HTTPСоединение = Новый HTTPСоединение(СтрЗаменить(Структура.Сервер, "www.",""));
			HTTPСоединение.Получить(Структура.Ресурс, КаталогВременныхФайлов() + "News.xml", Параметры);		
			HTTPСоединение = "";	
		Исключение
			#Если Клиент Тогда 
				Сообщить("" + РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке()), СтатусСообщения.Важное);
			#КонецЕсли 
			Возврат;  			
		КонецПопытки;
	КонецПопытки;  	
	
	// Теперь разберем XML, который получили.
	#Если Клиент Тогда 
		Состояние("Разбор полученного файла...");  		
	#КонецЕсли 
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(КаталогВременныхФайлов() + "News.xml");
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Имя");
	Дерево.Колонки.Добавить("Значение");
	Дерево.Колонки.Добавить("Атрибуты");
	
	// Пробуем прочитать полученный файл. Если ошибка, значит неправильный формат полученного файла.
	Попытка
		эпЗаполнитьДеревоЗначенийИзXML(ЧтениеXML, Дерево);
		ЧтениеXML = "";
	Исключение
		ЧтениеXML = "";
		#Если Клиент Тогда 
			Сообщить("Ошибка при чтении файла с новостями! 
			|Проверьте настройки интернет соединения и адрес новостного потока RSS.", СтатусСообщения.Важное);  		
		#КонецЕсли 
		Возврат;
	КонецПопытки;
	
		
	// Обрабатываем дерево.
	Если Дерево.Строки.Количество()=0 Тогда
		#Если Клиент Тогда
			Сообщить("Файл пустой!", СтатусСообщения.Важное);	
		#КонецЕсли
		Возврат;  		
	КонецЕсли;
	
	Версия   = 0;
	Атрибуты = Дерево.Строки[0].Атрибуты;
	Имя      = Дерево.Строки[0].Имя;
	Если НЕ Атрибуты.Свойство("version", Версия) и Имя<>"RDF" Тогда
		#Если Клиент Тогда
			Сообщить("Не указана версия формата новостей!", СтатусСообщения.Важное);	
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	тзНовости = Новый ТаблицаЗначений;
	тзНовости.Колонки.Добавить("Заголовок");
	тзНовости.Колонки.Добавить("Содержание");
	тзНовости.Колонки.Добавить("Ссылка");
	тзНовости.Колонки.Добавить("КопиРайт");
	тзНовости.Колонки.Добавить("ВебМастер");
	тзНовости.Колонки.Добавить("ДатаПубликации");
	тзНовости.Колонки.Добавить("ЭлПочтаАвтора");  
	
	Если Версия="2.0" Тогда
		
		эпОбработатьНовостиФормата_2_0(Дерево.Строки[0].Строки[0], тзНовости); // Переходим к каналу
		
	ИначеЕсли (Версия="1.0" или Имя="RDF") Тогда
		
		эпОбработатьНовостиФормата_1_0(Дерево.Строки[0], тзНовости); // Переходим к каналу
		
	ИначеЕсли Версия="0.91" Тогда
		
		эпОбработатьНовостиФормата_2_0(Дерево.Строки[0].Строки[0], тзНовости); // Переходим к каналу
		
	Иначе
		#Если Клиент Тогда
			Сообщить("Данная версия новостей не поддерживается.", СтатусСообщения.Важное);	
		#КонецЕсли  	
	КонецЕсли;  	
	
	Если тзНовости.Количество()=0 Тогда
		#Если Клиент Тогда
			Сообщить("Новостей нет.", СтатусСообщения.Важное);	
		#КонецЕсли  			
	КонецЕсли;
	
	// Читаем новости, проверяем на уже созданные
	#Если Клиент Тогда 
		Состояние("Запись полученных новостей...");
	#КонецЕсли  

	НаборЗаписей = РегистрыСведений.ЗаголовкиПолученныхПисем.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
	НаборЗаписей.Прочитать();
	
	СписокЗаголовков = Новый СписокЗначений;
	Если (Не ПолучитьВсе) и НаборЗаписей.Количество()<>0 Тогда
		Структура = НаборЗаписей[0].ЗаголовкиПисем.Получить();		
		Если ТипЗнч(Структура)=Тип("Структура") Тогда
			СписокЗаголовков = Структура.СписокЗаголовков;
		КонецЕсли;
	КонецЕсли;
	
	ПерваяСтрока = тзНовости[0]; // Первая строка - главная.
	тзНовости.Сортировать("ДатаПубликации");
	
	Для каждого ТекСтрока Из тзНовости Цикл
		Если ТекСтрока=ПерваяСтрока Тогда
			Продолжить;
		КонецЕсли;
		
		Заголовок = ТекСтрока.Заголовок;
		Если СписокЗаголовков.НайтиПоЗначению(Заголовок)=Неопределено Тогда
			СписокЗаголовков.Добавить(Заголовок);
			СоздатьНовость(УчетнаяЗапись, ТекСтрока, ПерваяСтрока);
			ЕстьНовыеПисьма = Истина;
		КонецЕсли;		
	КонецЦикла;
	
	НаборЗаписей.Очистить();
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.УчетнаяЗапись  = УчетнаяЗапись;
	
	Структура = Новый Структура;
	Структура.Вставить("СписокЗаголовков", СписокЗаголовков);
	Структура.Вставить("ДатаПубликации",   ПерваяСтрока.ДатаПубликации);
	
	НоваяЗапись.ЗаголовкиПисем = Новый ХранилищеЗначения(Структура);
	НаборЗаписей.Записать();
	
	ЧтениеXML = "";
	#Если Клиент Тогда
		Если ЕстьНовыеПисьма Тогда
			Оповестить("ОбновитьДеревоГрупп", "Пересчитать");
		Иначе
			Сообщить("Новостей нет!");
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

// Процедура формирует дерево по XML файлу.
//
// Параметры
//  ЧтениеXML - "ЧтениеXML" - объект ЧтениеXML, по которому строится дерево.
//
//  Дерево - "ДеревоЗначений" - строящееся дерево.
//  
// Возвращаемое значение:
//   НЕТ
//
Процедура эпЗаполнитьДеревоЗначенийИзXML(ЧтениеXML, Дерево) Экспорт
	ВремДерево = Дерево;
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла=ТипУзлаXML.НачалоЭлемента Тогда
			ВремДерево      = ВремДерево.Строки;
			НоваяСтрока     = ВремДерево.Добавить();
			НоваяСтрока.Имя = ЧтениеXML.ЛокальноеИмя;
			
			// Пройдемся по атрибутам
			СтруктураАтрибутов = Новый Структура;
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
				СтруктураАтрибутов.Вставить(ЧтениеXML.ЛокальноеИмя, ЧтениеXML.Значение);	
			КонецЦикла;	
			НоваяСтрока.Атрибуты = СтруктураАтрибутов;
			
			ВремДерево = НоваяСтрока; 			
			
		ИначеЕсли ЧтениеXML.ТипУзла=ТипУзлаXML.КонецЭлемента Тогда
			ВремДерево = ВремДерево.Родитель;
			
		ИначеЕсли ЧтениеXML.ТипУзла=ТипУзлаXML.Текст Тогда
			ВремДерево.Значение = ЧтениеXML.Значение;			
		КонецЕсли;			
	КонецЦикла;
КонецПроцедуры

// Процедура разбирает новости RSS в формате 2.0. Рекурсивная. 
//
// Параметры
//  ТекСтрокаДЗ - "СтрокаДереваЗначений" - текущая строка дерева значений с новостями.
//
//  тзНовости - "ТаблицаЗначений" - заполняемая таблица значений, каждая строка соответствует одной новости.
// 
// Возвращаемое значение:
//   НЕТ
//  
Процедура эпОбработатьНовостиФормата_2_0(ТекСтрокаДЗ, тзНовости) Экспорт
	
	НоваяСтрока = тзНовости.Добавить();
	Для каждого ТекСтрока Из ТекСтрокаДЗ.Строки Цикл
		Если ТекСтрока.Имя="title" Тогда
			НоваяСтрока.Заголовок = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="description" Тогда
			НоваяСтрока.Содержание = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="link" Тогда
			НоваяСтрока.Ссылка = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="copyright" Тогда
			НоваяСтрока.КопиРайт = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="webMaster" Тогда
			НоваяСтрока.ВебМастер = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="pubDate" Тогда
			НоваяСтрока.ДатаПубликации = эпПреобразоватьДатуИзRFC822(СокрЛП(ТекСтрока.Значение));
			
		ИначеЕсли ТекСтрока.Имя="author" Тогда
			НоваяСтрока.ЭлПочтаАвтора = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="item" Тогда
			эпОбработатьНовостиФормата_2_0(ТекСтрока, тзНовости);	
		КонецЕсли;			
	КонецЦикла;	
	
КонецПроцедуры

// Процедура разбирает новости RSS в формате 1.0. Рекурсивная. 
//
// Параметры
//  ТекСтрокаДЗ - "СтрокаДереваЗначений" - текущая строка дерева значений с новостями.
//
//  тзНовости - "ТаблицаЗначений" - заполняемая таблица значений, каждая строка соответствует одной новости.
// 
// Возвращаемое значение:
//   НЕТ
//
Процедура эпОбработатьНовостиФормата_1_0(ТекСтрокаДЗ, тзНовости, НоваяСтрока=Неопределено)Экспорт
	
	Для каждого ТекСтрока Из ТекСтрокаДЗ.Строки Цикл
		Если ТекСтрока.Имя="title" Тогда
			НоваяСтрока.Заголовок = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="description" Тогда
			НоваяСтрока.Содержание = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="link" Тогда
			НоваяСтрока.Ссылка = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="copyright" Тогда
			НоваяСтрока.КопиРайт = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="webMaster" Тогда
			НоваяСтрока.ВебМастер = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="pubDate" Тогда
			НоваяСтрока.ДатаПубликации = эпПреобразоватьДатуИзRFC822(СокрЛП(ТекСтрока.Значение));
			
		ИначеЕсли ТекСтрока.Имя="author" Тогда
			НоваяСтрока.ЭлПочтаАвтора = СокрЛП(ТекСтрока.Значение);
			
		ИначеЕсли ТекСтрока.Имя="item" или ТекСтрока.Имя="channel" Тогда
			эпОбработатьНовостиФормата_1_0(ТекСтрока, тзНовости, тзНовости.Добавить());
		КонецЕсли;			
	КонецЦикла;	
	
КонецПроцедуры

// Функция преобразует дату формата RFC822 в дату 1С
//
// Параметры
//  СтрДата - "Строка" - дата в формате RFC822
//
// Возвращаемое значение:
//   Дата - результат преобразований.
// 
Функция эпПреобразоватьДатуИзRFC822(Знач СтрДата)Экспорт
	// уберем лишнее начало
	Номер = Найти(СтрДата, ",");
	Если Номер<>0 Тогда
		СтрДата = СокрЛП(Сред(СтрДата, Номер+1));	
	КонецЕсли;
	
	НомерМесяца = элПолучитьНомерМесяца(СтрДата, Номер);  		
	Если НомерМесяца=0 Тогда
		Возврат "";	
	КонецЕсли;
	
	// Получим день
	НомерДня = Число(СокрЛП(Лев(СтрДата, Номер-1))); 
	Если НомерДня=0 Тогда
		Возврат "";	
	КонецЕсли;
	
	// Получим год
	СтрДата = СокрЛП(Сред(СтрДата, Номер+3));
	
	// Номер года
	Номер = Найти(СтрДата, " ");
	Если Номер=0 и (СтрДлина(СтрДата)>4) Тогда
		Возврат "";
	КонецЕсли;
	
	НомерГода = Число(Лев(СтрДата, Номер-1));
	
	СтрДата = СокрЛП(Сред(СтрДата, Номер+1));
	// Теперь разбираем дату
	Часы    = Сред(СтрДата, 1, 2);
	Минуты  = Сред(СтрДата, 4, 2);
	Секунды = Сред(СтрДата, 7, 2);
	
	Возврат Дата(НомерГода, НомерМесяца, НомерДня, Часы, Минуты, Секунды);
КонецФункции // ПреобразоватьДату()

// Вспомогательная функция преобразования даты из формата RFC822
// Получает номер месяца.
//
// Параметры
//  СтрДата - "Строка" - дата в формате RFC822
//
//  ПозицияСимвола - "Число" - возвращает позицию названия месяца в дате RFC822
//
// Возвращаемое значение:
//   Число - номер месяца.
//  
Функция элПолучитьНомерМесяца(СтрДата, ПозицияСимвола=0) Экспорт

	ПозицияСимвола = Найти(СтрДата, "Jan");
	Если ПозицияСимвола<>0 Тогда
		Возврат 1;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Feb");
	Если ПозицияСимвола<>0 Тогда
		Возврат 2;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Mar");
	Если ПозицияСимвола<>0 Тогда
		Возврат 3;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Apr");
	Если ПозицияСимвола<>0 Тогда
		Возврат 4;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "May");
	Если ПозицияСимвола<>0 Тогда
		Возврат 5;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Jun");
	Если ПозицияСимвола<>0 Тогда
		Возврат 6;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Jul");
	Если ПозицияСимвола<>0 Тогда
		Возврат 7;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Aug");
	Если ПозицияСимвола<>0 Тогда
		Возврат 8;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Sep");
	Если ПозицияСимвола<>0 Тогда
		Возврат 9;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Oct");
	Если ПозицияСимвола<>0 Тогда
		Возврат 10;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Nov");
	Если ПозицияСимвола<>0 Тогда
		Возврат 10;
	КонецЕсли;
	
	ПозицияСимвола = Найти(СтрДата, "Dec");
	Если ПозицияСимвола<>0 Тогда
		Возврат 12;
	КонецЕсли;
	              	   	             
    ПозицияСимвола = 0;
	Возврат 0;
КонецФункции // элПолучитьНомерМесяца()

// Процедура создания входящего электронного письма типа "Новость".
//
// Параметры
//  УчетнаяЗапись - "СправочникСсылка.УчетныеЗаписиЭлектроннойПочты" - ссылка на новостную учетную запись.
//
//  ТекСтрока - "СтрокаТаблицыЗначений" - текущая строка таблицы значений с новостями.
//										  Каждая строка - отдельная новость.
//
// ПерваяСтрока - "строкаТаблицыЗначений" - первая строка таблицы значений с новостями. В первой строке 
//											содержится общий заголовок для всех новостей.
//
// Возвращаемое значение:
//   НЕТ
//  
Процедура СоздатьНовость(УчетнаяЗапись, ТекСтрока, ПерваяСтрока)
	
	#Если Клиент Тогда
		Состояние("Запись полученных писем");
	#КонецЕсли
	
	НовоеПисьмо = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
	
	// Заполним типовые реквизиты документа
	дкОбработкаЗаполненияПоУмолчанию(НовоеПисьмо);
	НовоеПисьмо.ХозОперация = Справочники.ХозОперации.ЭлектронноеПисьмо;
	НовоеПисьмо.Дата = ТекущаяДата();
	НовоеПисьмо.УстановитьНовыйНомер();
	
	
	// адресаты
	НовоеПисьмо.ОтправительПредставление = ПерваяСтрока.Заголовок;
	НовоеПисьмо.ПолучателиПредставление  = ПерваяСтрока.Содержание;
	
	НовоеПисьмо.Тема = ТекСтрока.Заголовок; 
	
	НовоеПисьмо.ДатаОтправления = ТекСтрока.ДатаПубликации;
	НовоеПисьмо.ДатаПолучения   = ТекущаяДата();
	
	Текст = ""; 	
	Текст = "<H3>" + ТекСтрока.Заголовок + "</H3>";
	Текст = Текст + "<BR>";
	Текст = Текст + "<P>";
	Текст = Текст + ТекСтрока.Содержание;
	Текст = Текст + "</P>";
	
	Текст = Текст + "<BR>";
	Текст = Текст + "<A href=" + ТекСтрока.Ссылка + ">" + "Ссылка на полную версию..." + "</A>";
	
	Текст = Текст + "<HR>"; 		
	Текст = Текст + "<P align=right>"; 
	Текст = Текст + Формат(ТекСтрока.ДатаПубликации, "ДЛФ=DDT");
	
	Если Не обЗначениеНеЗаполнено(ПерваяСтрока.КопиРайт) Тогда
		Текст = Текст + "<BR>";
		Текст = Текст + "<A href=" + ПерваяСтрока.Ссылка + ">" + ПерваяСтрока.КопиРайт + "</A>";
	КонецЕсли;
	
	Если Не обЗначениеНеЗаполнено(ПерваяСтрока.ВебМастер) Тогда
		Текст = Текст + "<BR>";
		Текст = Текст + "" + ПерваяСтрока.ВебМастер;
	КонецЕсли;
	
	Текст = Текст + "</P>";
	
	НовоеПисьмо.ФорматТекста  = Перечисления.ФорматТекста.HTML;
	
	НовоеПисьмо.ТекстПисьма   = Текст;
	
	НовоеПисьмо.УчетнаяЗапись = УчетнаяЗапись;
	НовоеПисьмо.ГруппаПисем   = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Входящие",Истина,,УчетнаяЗапись);
	НовоеПисьмо.СтатусПисьма  = Перечисления.СтатусыСообщений.Входящее;
	НовоеПисьмо.НеРассмотрено = Истина;
	
	НовоеПисьмо.ЕстьВложения  = Ложь;
	
	НовоеПисьмо.Записать();
	
КонецПроцедуры // СоздатьНовость()

// Функция получает электронный адрес технической службы поддержки текущего программного продукта.
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Строка - электронный адрес техподдержки.
//
Функция элПолучитьАдресТехПоддержки() Экспорт
	Информация = орПолучитьИнформациюОПродукте();
	Возврат Информация.АдресТехПоддержкиПродукта;
КонецФункции // элПолучитьАдресТехПоддержки()

#Если Клиент Тогда

// Процедура формирует электронное письмо-ответ.
//
// Параметры
//  ТекУчетнаяЗаписьНовости - "СправочникСсылка.УчетныеЗаписиЭлектроннойПочты" - ссылка на новостную уч. запись.
//
//  ТекНовость - "ДокументСсылка.ЭлектронноеПисьмо" - новость.
//
// Возвращаемое значение:
//   НЕТ
//
Процедура элСоздатьПисьмоОтветНаНовость(ТекУчетнаяЗаписьНовости, ТекНовость) Экспорт
	// В качестве учетной записи для отправки новости будем использовать учетную запись по-умолчанию
	УчетнаяЗаписьДляОтправки = ЭпПолучитьУчетнуюЗаписьПоУмолчанию();
	Если УчетнаяЗаписьДляОтправки=Неопределено Тогда  // Используем встроенную учетную запись.
		УчетнаяЗаписьДляОтправки = Справочники.УчетныеЗаписиЭлектроннойПочты.Поддержка; 		
	КонецЕсли;
	
	// АдресЭлектроннойПочтыДляОтвета
	СписокДоступныхЗаписей = эпПолучитьДоступныеУчетныеЗаписи("РЕДАКТИРОВАНИЕ");
	Если СписокДоступныхЗаписей.НайтиПоЗначению(УчетнаяЗаписьДляОтправки) = Неопределено Тогда
		Предупреждение("У пользователя нет прав на создание писем учетной записи <" + УчетнаяЗаписьДляОтправки +  ">!
		|Обратитесь к администратору.",, "Внимание!");
		Возврат;
	КонецЕсли;
	
	Тема = "";
	Если ТекНовость<>Неопределено Тогда
		Тема = ТекНовость.Тема;		
	КонецЕсли;
	
	ПисьмоОтвет = Документы.ЭлектронноеПисьмо.СоздатьДокумент();
	ПисьмоОтвет.Тема = эпУвеличитьИндексТемы(Тема, "Re");
	
	ПисьмоОтвет.Получатели.Очистить();
	ТаблицаЗначенийАдресов = эпВыделитьАдресИПредставление(ТекУчетнаяЗаписьНовости.АдресЭлектроннойПочтыДляОтвета);
	ПисьмоОтвет.ОбновитьТабличнуюЧасть(ТаблицаЗначенийАдресов, Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому);

	ПисьмоОтвет.ГруппаПисем       = Справочники.ГруппыПисемЭлектроннойПочты.НайтиПоНаименованию("Исходящие", Истина,, УчетнаяЗаписьДляОтправки);
	ПисьмоОтвет.ДокументОснование = ?(ТекНовость=Неопределено, Документы.ЭлектронноеПисьмо.ПустаяСсылка(), ТекНовость);
	ПисьмоОтвет.СтатусПисьма      = Перечисления.СтатусыСообщений.ПустаяСсылка();
	ПисьмоОтвет.Ответ             = Истина;  
	
	ПисьмоОтвет.ПолучитьФорму(,,ПисьмоОтвет).Открыть();
	
КонецПроцедуры // элСоздатьПисьмоОтветНаНовость()


////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ СОЗДАНИЯ ЭЛЕКТРОННОГО ПИСЬМА ДЛЯ ТЕХПОДДЕРЖКИ

// Процедура формирует электронное письмо в службу техподдержки. Расставляет необходимые комментарии,
// получает информацию о системе, о текущем объекте, о системе защиты и т.д. 
//
// Параметры
//  ЭтаФорма - "Форма" - если формирование письма вызвано из меню "Действия", то
//  					 эта переменная содержит текущую форму.
//
//  Ссылка - "ЛюбаяСсылка" - если формирование письма вызвано в форме списка, то данная
//							 переменная содержит ссылку на текущий объект.
//
// Возвращаемое значение:
//   НЕТ
// 
Процедура элНаписатьПисьмоВСлужбуТехПоддержки(ЭтаФорма=Неопределено, Ссылка=Неопределено) Экспорт 
	
	НовоеПисьмо = Документы.ЭлектронноеПисьмо.СоздатьДокумент();		
	
	// Получим учетную запись, от которой будет отправлено письмо.
	УчетнаяЗапись = Неопределено;
	Если ПравоДоступа("Чтение", Метаданные.Справочники.УчетныеЗаписиЭлектроннойПочты) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭлектроннойПочтыПользователи.Ссылка,
		|	УчетныеЗаписиЭлектроннойПочтыПользователи.УчетнаяЗаписьПоУмолчанию
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты.Пользователи КАК УчетныеЗаписиЭлектроннойПочтыПользователи
		|ГДЕ
		|	УчетныеЗаписиЭлектроннойПочтыПользователи.Пользователь = &Пользователь
		|	И УчетныеЗаписиЭлектроннойПочтыПользователи.УчетнаяЗаписьПоУмолчанию";
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.Пользователь);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			УчетнаяЗапись = Выборка.Ссылка;	
		КонецЕсли;
	КонецЕсли;
	
	Если УчетнаяЗапись=Неопределено Тогда
		УчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.Поддержка;		
	КонецЕсли;
	
	НовоеПисьмо.УчетнаяЗапись = УчетнаяЗапись;
	
	// Подставим в качестве получателя адрес техподдержки.
	ТехПоддержка = элПолучитьАдресТехПоддержки();		
	НоваяСтрока  = НовоеПисьмо.Получатели.Добавить();
	НоваяСтрока.Представление         = "" + ТехПоддержка.Представление;
	НоваяСтрока.АдресЭлектроннойПочты = ТехПоддержка.Адрес;	
	НоваяСтрока.КодГруппыАдреса       = Перечисления.КодГруппыАдресаЭлектронногоПисьма.Кому; 
		
	Текст = "/ЗДЕСЬ НЕОБХОДИМО  ПОДРОБНО ОПИСАТЬ СУТЬ
	|ПРОБЛЕМЫ ИЛИ ПРЕДЛОЖЕНИЯ. ПИСЬМА БЕЗ ДЕТАЛЬНОГО ОПИСАНИЯ БУДУТ ИГНОРИРОВАНЫ/";	
	
	// Оставляем место для текста пользователя.
	Для Сч=0 По 5 Цикл
		Текст = Текст + Символы.ПС;	
	КонецЦикла;

	ФормаДляСбораИнформацииОСистеме = Обработки.Поддержка.ПолучитьФорму("ПолучениеИнформацииОСистеме", ЭтаФорма);
	ФормаДляСбораИнформацииОСистеме.ТекущийОбъект = Ссылка;
	
	Результат = ФормаДляСбораИнформацииОСистеме.ОткрытьМодально();
	// Результом будет структура с ключам: "Текст", "СписокФайлов", "СписокОшибок".
	Если Результат<>Неопределено Тогда
		Текст = Текст + Результат.Текст;
		
		// Теперь прикрепим файлы.
		Если Результат.СписокФайлов.Количество()<>0 Тогда
			эпДобавитьВложенияВПисьмо(НовоеПисьмо.ПрикрепленныеФайлы, Результат.СписокФайлов, Истина); // Прикрепляем файл к письму и удаляем его	
		КонецЕсли;
		
		// Возможно при получении информации произошли ошибки.		
		Для каждого ТекОшибка Из  Результат.СписокОшибок Цикл  	
			Сообщить("ТекОшибка", СтатусСообщения.Важное);	
		КонецЦикла;	
		
	Иначе
		Возврат; // Отказался пользователь.		
	КонецЕсли;
	
	НовоеПисьмо.ТекстПисьма = Текст;
	
	Если НЕ обПраво("ИспользоватьВстроенныйКлиент") тогда
		НовоеПисьмо.ДополнительныеСвойства.Вставить("ОтправитьВнешнимКлиентом",Истина);
	КонецЕсли;
	
	НовоеПисьмо.ПолучитьФорму().Открыть();  		
	
КонецПроцедуры // элНаписатьПисьмоВСлужбуТехПоддержки()


////////////////////////////////////////////////////////////////
// РАБОТА С HTTP 

// Функция выполняет POST запрос по указанному адресу.
//
// Параметры
//  ТелоЗапроса - "Строка" - текст запроса.
//
//  АдресСервера - "Строка" - электронный адрес сервера.
//
//  АдресРесурса - "Строка" - адрес ресурса на сервере.
//
//  Логин - "Строка" - логин, если необходима авторизация.
//
//  Пароль - "Строка" - пароль.
//
//  ИмяПолученногоФайла - "Строка" - возвращает полное имя файла, с результатами запроса.
//  
// Возвращаемое значение:
//   Строка - в случае успешного выполнения запроса, возвращает пустую строку. Иначе текст ошибки.
//
Функция элPostЗапрос(ТелоЗапроса, АдресСервера, АдресРесурса, Логин="", Пароль="", ИмяПолученногоФайла="") Экспорт
	
	Попытка
		ИсходныйФайлИмя = ПолучитьИмяВременногоФайла();
		ИсходныйФайл    = Новый ЗаписьТекста;
		ИсходныйФайл.Открыть(ИсходныйФайлИмя, КодировкаТекста.ANSI, , Ложь);
		ИсходныйФайл.Записать(ТелоЗапроса);
		ИсходныйФайл.Закрыть();
		
		ВыходнойФайлИмя = ПолучитьИмяВременногоФайла();
	Исключение		
		Возврат "Невозможно создать временный файл";
	КонецПопытки;
	
	Состояние("Установление соединения..");
	Попытка
		Соединение=Новый HTTPСоединение(АдресСервера, "80", Логин, Пароль);
	Исключение
		Возврат РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке());
	КонецПопытки;
	
	Состояние("Получение ресурса...");
	Попытка
		// +BasY
		//Соединение.ОтправитьДляОбработки(ИсходныйФайлИмя, АдресРесурса, ВыходнойФайлИмя, "Content-Type: application/x-www-form-urlencoded");
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type: application/x-www-form-urlencoded");
		ЗапросHTTP				= Новый HTTPЗапрос;
		ЗапросHTTP.АдресРесурса	= АдресРесурса;
		ЗапросHTTP.Заголовки	= Заголовки;
		ЗапросHTTP.УстановитьИмяФайлаТела(ИсходныйФайлИмя);
		Соединение.ОтправитьДляОбработки(ЗапросHTTP, ВыходнойФайлИмя);
		// -BasY
		ИмяПолученногоФайла = ВыходнойФайлИмя;	
	Исключение  				
		Возврат РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке());		
	КонецПопытки; 
	
	// Удаляем временный файл с телом запроса
	Попытка
		УдалитьФайлы(ИсходныйФайлИмя);		
	Исключение 		
	КонецПопытки;
	
	Возврат ""; 	
КонецФункции // PostЗапрос()

#КонецЕсли

// Функция разбирает ошибки, возникшие в ходе выполнения HTTP соединения.
//
// Параметры
//  ИнформацияОбОшибке - "ИнформацияОбОшибке" - структура с информацией об ошибке.
//
// Возвращаемое значение:
//   Строка - строка с описанием ошибки.
//
Функция РазобратьОшибкуHTTPСоединения(ИнформацияОбОшибке) Экспорт
	стрОшибка = "";
	Если ТипЗнч(ИнформацияОбОшибке)=Тип("ИнформацияОбОшибке") Тогда
		Если ИнформацияОбОшибке.Причина<>Неопределено Тогда
			стрОшибка = СокрЛП(ИнформацияОбОшибке.Причина.Описание);		
			Если Найти(стрОшибка, "Ошибка аутентификации при доступе к ресурсу")<>0 Тогда
				стрОшибка = "Неверное имя пользователя или пароль";
				
			ИначеЕсли Найти(стрОшибка, "Ошибка работы с Интернет")<>0 Тогда
				стрОшибка = "Внутренняя ошибка Сервера обновлений. Попробуйте повторить попытку позже";				
			Иначе
				стрОшибка = "Невозможно соединиться с Сервером обновлений. 
				|Проверьте настройки интернет-соединения и корректность указанного адреса.";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат стрОшибка;
КонецФункции // РазобратьОшибку()

// Функция возвращает удобочитаемое представление ошибки, при работе с почтой
//
// Параметры:
//  НЕТ
//
// Возвращаемое значение:
//   Строка - описание ошибки.
// 
Функция элРазобратьОшибкуПочтовогоСоединения()  Экспорт
	
	ИнформацияОбОшибке = ИнформацияОбОшибке();
	Если ИнформацияОбОшибке=Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Если ИнформацияОбОшибке.Причина=Неопределено Тогда
		Возврат ИнформацияОбОшибке.Описание;
	Иначе
		Возврат ИнформацияОбОшибке.Причина.Описание;
	КонецЕсли;	
КонецФункции // элРазобратьОшибкуПочтовогоСоединения() 

// Функция преобразует доменные логины вида "домен\пользователь".
// Используется из-за не корректной работы конструктора HTTP соединения.
// Заменяет знак "\" на последовательность символов "%5C" 
//
// Параметры
//  Логин - "Строка" - логин.
//
// Возвращаемое значение:
//   Строка - преобразованный логин.
//
Функция СформироватьЛогин(Знач Логин) Экспорт
	Логин = СокрЛП(Логин);
	
	НомерСлэша = Найти(Логин, "\");
	Если НомерСлэша<>0 Тогда
		Логин = Сред(Логин, 1, НомерСлэша-1) + "%5C" + Сред(Логин, НомерСлэша+1); 	
	Иначе
		Логин = "UPDATE%5C" + Логин;
	КонецЕсли;
	
	Возврат Логин;
КонецФункции // СформироватьЛогин()



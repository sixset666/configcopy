
// Процедура инициализирует параметр сеанса  "ТекущийПользователь".
// Параметр содержит ссылку на элемент справочника "Пользователи", 
// соответствующий текущему пользователю информационной базы.
//
// Параметры:
//  Нет.
//
Функция ОпределитьТекущегоПользователя() Экспорт

	Если ПустаяСтрока(ИмяПользователя()) Тогда
		ИмяПользователя           = "НеАвторизован";
		ПолноеИмяПользователя     = "Не авторизован";
	Иначе
		ИмяПользователя           = ИмяПользователя();

		Если ПустаяСтрока(ПолноеИмяПользователя()) Тогда
			ПолноеИмяПользователя = ИмяПользователя;
		Иначе
			ПолноеИмяПользователя = ПолноеИмяПользователя();
		КонецЕсли;
	КонецЕсли;

	ПараметрыСеанса.Пользователь = Справочники.Пользователи.НайтиПоКоду(ИмяПользователя);

	Если ПараметрыСеанса.Пользователь = Справочники.Пользователи.ПустаяСсылка() Тогда
		ОбъектПользователь = Справочники.Пользователи.СоздатьЭлемент();

		ОбъектПользователь.Код          = ИмяПользователя;
		ОбъектПользователь.Наименование = ПолноеИмяПользователя;

		ОбъектПользователь.Записать();

		//Сообщить("Пользователь зарегистрирован в справочнике пользователей.");
	КонецЕсли;
    
    Возврат ПараметрыСеанса.Пользователь;
    
КонецФункции // ОпределитьТекущегоПользователя()

// Процедура устанавливает параметры номер сессии и имя и пароль пользователя
//
Процедура УстановитьПараметры() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	смсПараметры.НомерСессии КАК НомерСессии
	|ИЗ
	|	РегистрСведений.смсПараметры КАК смсПараметры";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() > 0 Тогда
		ПараметрыСеанса.смсНомерСессии = РезультатЗапроса[0].НомерСессии;
	Иначе 
		ПараметрыСеанса.смсНомерСессии = 0;
	КонецЕсли; 
	
	смсКоммуникатор.УстановитьИмяПользователяИПароль(Константы.смсИмяПользователя.Получить(), Константы.смсПарольПользователя.Получить());
	
КонецПроцедуры

// Функция читает параметры с регистра сведений "смсПараматеры"
// 
// Возвращаемое значение (Структура):
// 
// Возвращает стуктуру параметров
//
Функция ПолучитьПараметры() Экспорт
	
	Структура = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	смсПараметры.НомераОтправителя,
	|	смсПараметры.ОстатокСМС,
	|	смсПараметры.МаксДлительностьДоставки,
	|	смсПараметры.МаксАктуальностьДоставки,
	|	смсПараметры.ДатаПолученияСМС,
	|	смсПараметры.Миллисекунда,
	|	смсПараметры.КоличествоНомеров
	|ИЗ
	|	РегистрСведений.смсПараметры КАК смсПараметры";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() > 0 Тогда
		Строка = РезультатЗапроса[0];
		Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
			Структура.Вставить(Колонка.Имя, Строка[Колонка.Имя]);
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат Структура;
	
КонецФункции

// Процедура записывает параметры сессии в регист сведений "смсПараметры"
//
// Параметры:
//
//  ПараметрыСессии (структура)- содержит параметры которые необходимо записать в регистр.
//							
Процедура ЗаписатьПараметры(ПараметрыСессии) Экспорт 
	
	НоваяЗапись = РегистрыСведений.смсПараметры.СоздатьМенеджерЗаписи();
	НоваяЗапись.Прочитать();
	Если ПараметрыСессии.Свойство("Номера") Тогда
		НоваяЗапись.НомераОтправителя = ПараметрыСессии.Номера;
	КонецЕсли;	
	Если ПараметрыСессии.Свойство("ОстатокСМС") Тогда
		НоваяЗапись.ОстатокСМС = ПараметрыСессии.ОстатокСМС;
	КонецЕсли;	
	Если ПараметрыСессии.Свойство("МаксАктуальностьДоставки") Тогда
		НоваяЗапись.МаксАктуальностьДоставки = ПараметрыСессии.МаксАктуальностьДоставки;
	КонецЕсли;	
	Если ПараметрыСессии.Свойство("МаксДлительностьДоставки") Тогда
		НоваяЗапись.МаксДлительностьДоставки = ПараметрыСессии.МаксДлительностьДоставки;
	КонецЕсли;	
	Если ПараметрыСессии.Свойство("КолНомеров") Тогда
		НоваяЗапись.КоличествоНомеров = ПараметрыСессии.КолНомеров;
	КонецЕсли;	
	Если ПараметрыСессии.Свойство("ДатаПолученияСМС") Тогда
		НоваяЗапись.ДатаПолученияСМС = ПараметрыСессии.ДатаПолученияСМС;
	КонецЕсли;	
	НоваяЗапись.НомерСессии = смсКоммуникатор.ПолучитьНомерСессии();
	НоваяЗапись.Записать();
	
КонецПроцедуры

// Функция предназначена для перевода числового кода статуса в перечисление 
//
// Параметры:
//
//  КодСтатуса (число)- числовой код статуса.
// 
// Возвращаемое значение (Перечисления.смсСостоянияСообщений):
// 
// Возвращает представление статуса
//
Функция КодВСтатус(КодСтатуса) Экспорт
	
	Если КодСтатуса = 1 Тогда
		Статус = Перечисления.смсСостоянияСообщений.Доставка;
	ИначеЕсли КодСтатуса = 2 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.ВОчереди;
	ИначеЕсли КодСтатуса = 3 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.Отправлено;
	ИначеЕсли КодСтатуса = 4 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.НеОтправлено;
	ИначеЕсли КодСтатуса = 5 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.Доставлено;
	ИначеЕсли КодСтатуса = 6 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.НеДоставлено;
	ИначеЕсли КодСтатуса = 7 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.Ошибка;
	ИначеЕсли КодСтатуса = 8 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.Получено;
	ИначеЕсли КодСтатуса = 9 Тогда 
		Статус = Перечисления.смсСостоянияСообщений.ПолученоЧастично;
	КонецЕсли; 
	
	Возврат Статус;
	
КонецФункции

// Функция предназначена для перевода статуса в числовое представление  
//
// Параметры:
//
//  Статус (Перечисления.смсСостоянияСообщений)- Статус.
// 
// Возвращаемое значение (число):
// 
// Возвращает числово представление статуса
//
Функция СтатусВКод(Статус) Экспорт
	
	Если Статус = Перечисления.смсСостоянияСообщений.Доставка Тогда
		КодСтатуса = 1;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.ВОчереди Тогда
		КодСтатуса = 2;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.Отправлено Тогда
		КодСтатуса = 3;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.НеОтправлено Тогда
		КодСтатуса = 4;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.Доставлено Тогда
		КодСтатуса = 5;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.НеДоставлено Тогда
		КодСтатуса = 6;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.Ошибка Тогда
		КодСтатуса = 7;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.Получено Тогда
		КодСтатуса = 8;
	ИначеЕсли Статус = Перечисления.смсСостоянияСообщений.ПолученоЧастично Тогда
		КодСтатуса = 9;
	КонецЕсли; 
	
	Возврат КодСтатуса;
	
КонецФункции

// Функция убирает лишнии символы и разделители из строки с номером телефона
// 
// Параметры:
//	Телефон	- Строка	- Номер телефона
//
// Возвращаемое значение:
//	Строка	- Номер телефона после обработки
//
Функция УбратьЛишниеСимволыТелефона(Телефон) Экспорт
	// Убираем все ненужные символы
	ДлинаНомера = СтрДлина(Телефон);
	ЕстьПлюс	= (Лев(Телефон, 1) = "+");
	НовыйТелефон = "";
	Для а = 1 По ДлинаНомера Цикл
		Симв 		= Сред(Телефон, а, 1);
		КодСимвола 	= КодСимвола(Симв);
		Если КодСимвола < 58 И КодСимвола > 47 Тогда
			НовыйТелефон = НовыйТелефон + Симв;
		КонецЕсли;	
	КонецЦикла;
	// Проверяем полученный номер телефона
	Если НЕ ПустаяСтрока(НовыйТелефон) Тогда
		Если СтрДлина(НовыйТелефон) > 7 Тогда
			Если Лев(НовыйТелефон, 1) = "8" Тогда 
				Если НЕ ЕстьПлюс Тогда
					НовыйТелефон = "7" + Сред(НовыйТелефон, 2, СтрДлина(НовыйТелефон));
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат НовыйТелефон;
КонецФункции // УбратьЛишниеСимволыТелефона()

// Функция расчитывает длину сообщения в символах и число частей, на которое будет разбито сообщения при отправке 
//
// Параметры:
//	Сообщение	- Строка	- Текст сообщения
//
// Возвращаемое значение:
//	Структура	- Структура, содержащая длину сообщения и количество частей
//
Функция ВернутьДлинуТекстаСМС(Сообщение) Экспорт
	
	СтруктураВозврата = Новый Структура;
	// Максимальная длинна T-UD (User Data) в октетах
	МаксДлинаСообщения  =  140;
	// Длинна заголовка пользовательских данных в октетах
	ДлинаЗаголовка = 6;
	// Максимальная длинна данных в UCS2 (16 бит представляются 2 октктами)
	МаксДлинаСМС_UCS2 = Цел(МаксДлинаСообщения / 2); // 70;
	// Максимальная длинна данных в UCS2 минус заголовок пользовательских данных
	МаксДлинаСМС_UCS2_БезUDH =  Цел((МаксДлинаСообщения  - ДлинаЗаголовка - 1) / 2);
	// Максимальная длинна данных в 7 bit
	МаксДлинаСМС_7bit = МаксДлинаСообщения + Цел(МаксДлинаСообщения / 7); // 160
	// Максимальная длинна данных в  8 bit минус заголовок пользовательских данных
	МаксДлинаСМС_7bit_БезUDH = МаксДлинаСМС_7bit - (ДлинаЗаголовка + 1);
	// Определяем из скольких частей будет состоять SMS
	ДлинаСообщения = СтрДлина(Сообщение);
	
	СтруктураВозврата.Вставить("ДлинаСообщения", ДлинаСообщения);
	
	Кодировка = смсКоммуникатор.Использовать7БитСообщение(Сообщение);
	Если Кодировка = 0 Тогда
		Если ДлинаСообщения > МаксДлинаСМС_7bit Тогда 
			МаксДлина = МаксДлинаСМС_7bit_БезUDH;
			КолвоЧастей = Цел(ДлинаСообщения / МаксДлина);
			Если (ДлинаСообщения / МаксДлина - Цел(ДлинаСообщения / МаксДлина) > 0) Тогда 
				КолвоЧастей = КолвоЧастей + 1; 
			КонецЕсли;
		Иначе
			МаксДлина = МаксДлинаСМС_7bit;
			КолвоЧастей = 1;			
		КонецЕсли;
	Иначе
		Если ДлинаСообщения > МаксДлинаСМС_UCS2 Тогда
			МаксДлина = МаксДлинаСМС_UCS2_БезUDH;
			КолвоЧастей = Цел(ДлинаСообщения / МаксДлина);
			Если (ДлинаСообщения / МаксДлина - Цел(ДлинаСообщения / МаксДлина) > 0) Тогда
				КолвоЧастей = КолвоЧастей + 1;
			КонецЕсли;
		Иначе
			МаксДлина = МаксДлинаСМС_UCS2;
			КолвоЧастей = 1;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураВозврата.Вставить("КоличествоЧастей", КолвоЧастей);
	
	Возврат СтруктураВозврата;
	
КонецФункции // кмВернутьДлинуТекстаSMS()

// Функция возвращает список элементов справочника "ВидыКонтактнойИнформации", с видом мобильный или сотовый телефон 
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	СписокЗначений	- Список элементов справочника "ВидыКонтактнойИнформации"
//
Функция ПолучитьСписокВидовТелефонов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	(НЕ ВидыКонтактнойИнформации.ПометкаУдаления)
	|	И ВидыКонтактнойИнформации.Тип = &Тип
	|	И (ВидыКонтактнойИнформации.Наименование ПОДОБНО ""%мобильный%""
	|			ИЛИ ВидыКонтактнойИнформации.Наименование ПОДОБНО ""%сотовый%"")";
	СписокВидовТелефонов = Новый СписокЗначений;
	СписокВидовТелефонов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат СписокВидовТелефонов;
	
КонецФункции // ПолучитьСписокВидовТелефонов()

// Функция проверяет заполнение констант для подключения к сервису SMS
//
// Параметры:
//	СтруктураКонстант	- Структура	- Структура, содержащая значения констант
//
// Возвращаемое значение:
//	Булево	- Заполненность констант 
//
Функция ПроверитьНастройкиПодключения() Экспорт
	НетОшибок = Истина;
	Если ПустаяСтрока(Константы.смсИмяПользователя.Получить()) Тогда
		НетОшибок = Ложь;
		смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не указано имя пользователя для подключения к серверу SMS4B.'"), СтатусСообщения.Важное,
		"Подключение SMS", УровеньЖурналаРегистрации.Предупреждение);
	КонецЕсли;	
	Если ПустаяСтрока(Константы.смсПарольПользователя.Получить()) Тогда
		НетОшибок = Ложь;
		смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не указан пароль пользователя для подключения к серверу SMS4B.'"), СтатусСообщения.Важное,
		"Подключение SMS", УровеньЖурналаРегистрации.Предупреждение);
	КонецЕсли;
	
	Возврат НетОшибок;
	
КонецФункции // кмПроверитьНастройкиПодключения()	

// Функция получает сообщений из регистра сведений, предназначенные для отправки
//
// Возвращаемое значение:
//	Результат запроса - сообщения для отправки 
//
Функция ПолучитьСообщенияДляОтправки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	смсСостоянияСообщений.Сообщение КАК Сообщение,
	|	смсСостоянияСообщений.НомерСтрокиДокумента,
	|	смсСообщениеПолучатели.НомерТелефона КАК НомерПолучателя,
	|	смсСообщениеПолучатели.ТекстСообщения КАК ТекстСообщения,
	|	смсСостоянияСообщений.GUID,
	|	0 КАК КодОшибки
	|ИЗ
	|	РегистрСведений.смсСостоянияСообщений КАК смсСостоянияСообщений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.SMS.Получатели КАК смсСообщениеПолучатели
	|		ПО смсСостоянияСообщений.Сообщение = смсСообщениеПолучатели.Ссылка
	|			И смсСостоянияСообщений.НомерСтрокиДокумента = смсСообщениеПолучатели.НомерСтроки
	|ГДЕ
	|	смсСостоянияСообщений.Статус = ЗНАЧЕНИЕ(Перечисление.смсСостоянияСообщений.Доставка)
	|ИТОГИ ПО
	|	Сообщение";
	
	Возврат Запрос.Выполнить();
	 
КонецФункции

// Функция проверяет есть в базе дубли сообщений
//
// Параметры:
//	СписокГУИД	- массив - Массив GUID сообщений
//	ДатаВхСообщения	- Дата	- Дата входящего сообщений
//
// Возвращаемое значение:
//	ТаблицаЗначений	- Найденные в базе сообщения 
//
Функция ПроверитьДублиСообщений(СписокГУИД, ДатаВхСообщения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	смсСообщениеПолучатели.GUID_SMS,
	|	смсСообщениеПолучатели.Статус,
	|	смсСообщениеПолучатели.ТекстСообщения,
	|	смсСообщениеПолучатели.Ссылка.Ссылка КАК ДокументСсылка
	|ИЗ
	|	Документ.SMS.Получатели КАК смсСообщениеПолучатели
	|ГДЕ
	|	смсСообщениеПолучатели.GUID_SMS В(&Массив)
	|	И смсСообщениеПолучатели.Ссылка.Дата > &Дата";
	Запрос.УстановитьПараметр("Массив", СписокГУИД);
	Запрос.УстановитьПараметр("Дата", ДатаВхСообщения-86400);
	
	Возврат Запрос.Выполнить().Выгрузить();
	 
КонецФункции

// Функция получает сообщения статусы которых нужно обновить
//
Функция ПолучитьСообщенияДляОбновления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	смсСостоянияСообщений.GUID КАК GUID,
	|	смсСостоянияСообщений.Статус КАК СтатусСообщения,
	|	смсСостоянияСообщений.ОписаниеОшибки КАК ОписаниеОшибки,
	|	смсСостоянияСообщений.Сообщение,
	|	смсСостоянияСообщений.НомерСтрокиДокумента
	|ИЗ
	|	РегистрСведений.смсСостоянияСообщений КАК смсСостоянияСообщений
	|ГДЕ
	|	(смсСостоянияСообщений.Статус = ЗНАЧЕНИЕ(Перечисление.смсСостоянияСообщений.ВОчереди)
	|			ИЛИ смсСостоянияСообщений.Статус = ЗНАЧЕНИЕ(Перечисление.смсСостоянияСообщений.Отправлено))";
	
	Возврат Запрос.Выполнить().Выгрузить();
	 
КонецФункции

// Проверяет заполение констант имя и пароль ползователя
//
Функция ПроверитьЗаполнениеКонстант(ИзКонстант=Ложь) Экспорт

	ТекстСооб = ?(ИзКонстант, НСтр("ru = 'Не'"), НСтр("ru = 'В константах не'"));
	
	// Проверка заполнения констант
	Если НЕ ЗначениеЗаполнено(Константы.смсИмяПользователя.Получить()) Тогда 
		смсРаботаССообщениями.ВывестиСообщение(ТекстСооб+НСтр("ru = ' заполнен логин для подключения к sms4b.ru из внешних программ!'"), СтатусСообщения.Важное,
		"Отправка сообщений", УровеньЖурналаРегистрации.Предупреждение);
		Возврат Ложь;
	КонецЕсли;	
	Если НЕ ЗначениеЗаполнено(Константы.смсПарольПользователя.Получить()) Тогда 
		смсРаботаССообщениями.ВывестиСообщение(ТекстСооб+НСтр("ru = ' заполнен пароль для подключения к sms4b.ru из внешних программ!'"), СтатусСообщения.Важное,
		"Отправка сообщений", УровеньЖурналаРегистрации.Предупреждение);
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

// Процедура регламентного задания для получения/отправки/обновления статусов сообщений         
Процедура РегламентноеЗаданиеПроверкаСМС(Параметры = Неопределено) Экспорт
	
	Если НЕ ПроверитьЗаполнениеКонстант() Тогда
		Возврат;
	КонецЕсли; 
	ТолькоОтправка = Ложь;
	Если Тип("Булево") = ТипЗнч(Параметры) Тогда
		ТолькоОтправка = Параметры;
	КонецЕсли;
	
	// Обновляем параметры сессии
	ПараметрыСессии = Новый Структура;
	смсКоммуникатор.ПолучитьПараметрыСессии(ПараметрыСессии);
	смсРаботаССообщениями.ЗаписатьПараметры(ПараметрыСессии);
	
	СтруктураПараметров = ПолучитьПараметры();
	
	КодОшибки = 0;
	ПрерватьОбработку = Ложь;											
	
	//////////////////////////////////////////////////////////////
	// Отправка сообщений
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("НомерПолучателя");
	Таблица.Колонки.Добавить("GUID");
	Таблица.Колонки.Добавить("ТекстСообщения");
	Таблица.Колонки.Добавить("КодОшибки");
	Таблица.Колонки.Добавить("НомерСтрокиДокумента");
	
	// Выбираем сообщения для отправки
	РезультатЗапроса = ПолучитьСообщенияДляОтправки();
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		ВыборкаРегистра = Выборка.Выбрать();
		Таблица.Очистить();
		Пока ВыборкаРегистра.Следующий() Цикл
			НовСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ВыборкаРегистра,,);
			НовСтрока.НомерПолучателя = УбратьЛишниеСимволыТелефона(НовСтрока.НомерПолучателя);
		КонецЦикла;  
		// Заполняем параметры отправки сообщений
		СписокСообщений = смсКоммуникатор.ТаблицаЗначенийВМассивСтруктур(Таблица);
		ДокументСсылка 	 = Выборка.Сообщение;
		НомерОтправителя = ДокументСсылка.НомерОтправителя;
		ТекстСообщенияПоУмолчанию = ДокументСсылка.ТекстСообщения;
		ДатаОтправки			  = ДокументСсылка.НачалоОтправки;
		Актуальность 	 		  = ДокументСсылка.Актуальность;
		НачалоПериодаЗапрета 	  = ДокументСсылка.НачалоПериодаЗапрета;
		КонецПериодаЗапрета 	  = ДокументСсылка.КонецПериодаЗапрета;
		РавномернаяРассылка 	  = ДокументСсылка.РавномернаяРассылка;
		
		// Отправляем сообщения
		КодОшибки = смсКоммуникатор.ОтправитьСообщения(СписокСообщений, НомерОтправителя, ТекстСообщенияПоУмолчанию, ДатаОтправки,
													Актуальность, НачалоПериодаЗапрета, КонецПериодаЗапрета, РавномернаяРассылка);
													
		Если КодОшибки < -100000 Тогда //Перешли на другой сервер
			ПрерватьОбработку = Истина;
			КодОшибки = КодОшибки + 100000;
		КонецЕсли;
		
		// Если метод вернул ошибку
		Если КодОшибки < 0 Тогда
			смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Документ №"+ ДокументСсылка.Номер + ". Не удалось отправить сообщения. "+смсКоммуникатор.ОписаниеОшибокВебСервиса(КодОшибки)+"'"), СтатусСообщения.Важное,
			"Отправка сообщений", УровеньЖурналаРегистрации.Предупреждение);
		КонецЕсли;	
		
		// Может быть что отправились не все сообщения, а только часть из них
		КолОтправленных=0;
		ВсегоСообщений = СписокСообщений.Количество();
		Для Каждого Строка Из СписокСообщений Цикл
			Если Строка.КодОшибки >= 1 Тогда // Отправленное(ые) сообщение(я), части сообщений
				КолОтправленных = КолОтправленных + 1;
			КонецЕсли; 
		КонецЦикла; 
		
		// Меняем состояния сообщений в регистре сведений "смсСостоянияСообщений"
		НаборЗаписей = РегистрыСведений.смсСостоянияСообщений.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Сообщение.Значение = ДокументСсылка;
		НаборЗаписей.Отбор.Сообщение.Использование = Истина;
		НаборЗаписей.Прочитать();
		ТаблицаЗаписей = НаборЗаписей.Выгрузить();
		Для Каждого Строка Из СписокСообщений Цикл
			НайденнаяСтрока = ТаблицаЗаписей.Найти(Строка.GUID, "GUID");
			Если НЕ НайденнаяСтрока = Неопределено Тогда
				Если Строка.КодОшибки = 0 Тогда // Сообщение не отправилось
					НайденнаяСтрока.Статус = Перечисления.смсСостоянияСообщений.Доставка;
				ИначеЕсли Строка.КодОшибки < 0 Тогда // Ошибка отправки
					НайденнаяСтрока.Статус = Перечисления.смсСостоянияСообщений.Ошибка;
					НайденнаяСтрока.ОписаниеОшибки = смсКоммуникатор.ОписаниеОшибокВебСервиса(Строка.КодОшибки);
				ИначеЕсли Строка.КодОшибки >= 1 Тогда // Успешно Отправленное(ые) сообщение(я), части сообщений
					НайденнаяСтрока.Статус = Перечисления.смсСостоянияСообщений.ВОчереди;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
		НаборЗаписей.Загрузить(ТаблицаЗаписей);
		НаборЗаписей.Записать();
		
		//Изменим статус документа если произошла ошибка
		Если КодОшибки < 0 И НЕ ТолькоОтправка тогда
			Попытка
				ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
				СтруктураСтатуса = ДокументОбъект.ПолучитьСтатусДокумента();
				ДокументОбъект.СтатусСтрокой = СтруктураСтатуса.СтатусСтрокой;
				ДокументОбъект.Статус        = СтруктураСтатуса.Статус;
				ДокументОбъект.Записать();
			Исключение
				смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не удалось записать документ: "+ДокументОбъект.Номер+"'"), СтатусСообщения.Важное,
				"Запись документа", УровеньЖурналаРегистрации.Предупреждение);
			КонецПопытки; 
		КонецЕсли;
		
		// Если отправились не все сообщения
		Если КолОтправленных < ВсегоСообщений Тогда
			смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Документ №"+ ДокументСсылка.Номер + " Отправлено: "+КолОтправленных+" из "+ВсегоСообщений+"'") , СтатусСообщения.Важное,
			"Отправка сообщений", УровеньЖурналаРегистрации.Предупреждение);
		КонецЕсли;
		Если ПрерватьОбработку тогда //перешли на другой сервер, прерываем рагламентное задание
			Возврат;	
		КонецЕсли;
	КонецЦикла;
	
	Если ТолькоОтправка Тогда 
		Возврат;
	КонецЕсли;
	
	
	//////////////////////////////////////////////////////////////
	// Получение сообщений
	Если СтруктураПараметров.КоличествоНомеров > 0 Тогда
		СписокСообщений = Новый ТаблицаЗначений;
		// Получаем входящие сообщения
		КодОшибки = смсКоммуникатор.ПолучитьСообщения(СписокСообщений, СтруктураПараметров.ДатаПолученияСМС, 
										СтруктураПараметров.Миллисекунда, Константы.смсПолучатьТолькоПолныеСообщения.Получить());
		ЕстьВходящиеСооб = Ложь;
		
		Если КодОшибки < -100000 Тогда //Перешли на другой сервер
			ПрерватьОбработку = Истина;
			КодОшибки = КодОшибки + 100000;
		КонецЕсли;
		
		// Если есть полученные сообщения
		Если КодОшибки = 1 Тогда 
			МассивГУИД = Новый Массив;
			Для Каждого Строка Из СписокСообщений Цикл
				МассивГУИД.Добавить(Строка.GUID);
			КонецЦикла;
			// Ищем уже полученные сообщения
			ДублиСообщений = ПроверитьДублиСообщений(МассивГУИД, СтруктураПараметров.ДатаПолученияСМС);
			Для Каждого Строка Из СписокСообщений Цикл
				НайденноеСооб = ДублиСообщений.Найти(Строка.GUID);
				Если НЕ НайденноеСооб = Неопределено Тогда
					Если НайденноеСооб.Статус = "Получено" Тогда
						Продолжить;
					ИначеЕсли НайденноеСооб.Статус = "Получено частично" Тогда 
						// Записываем части сообщений в существующий документ
						Попытка
							ДокОбъект = НайденноеСооб.ДокументСсылка.ПолучитьОбъект();
							ТекстСообщения = ДокОбъект.ТекстСообщения;
							Если ДокОбъект.Статус = Перечисления.смсСостоянияСообщений.Получено Тогда   
								ДокОбъект.Получатели[0].ТекстСообщения = Строка.ТекстСообщения;
								ДокОбъект.ТекстСообщения = Строка.ТекстСообщения;
							ИначеЕсли ДокОбъект.Статус = Перечисления.смсСостоянияСообщений.ПолученоЧастично
								И Число(Строка.ТекущаяЧасть) = Число(Строка.ВсегоЧастей) Тогда 
								ДокОбъект.Получатели[0].ТекстСообщения = ТекстСообщения + Строка.ТекстСообщения;
								ДокОбъект.ТекстСообщения = ТекстСообщения + Строка.ТекстСообщения;
								ДокОбъект.Статус = Перечисления.смсСостоянияСообщений.Получено;
								ДатаДок = Лев(СтрЗаменить(Строка.ДатаПолучения, " ", ""), 16);
								ДатаДок = СтрЗаменить(ДатаДок, ":", "");
								ДатаПослСообщения = Дата(ДатаДок);
								Миллисекунда  	  = Строка.Миллисекунда;
								ЕстьВходящиеСооб = Истина;
							КонецЕсли; 
							ДокОбъект.Записать();
						Исключение
							смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не удалось записать документ: "+ДокОбъект.Номер+"'"), СтатусСообщения.Важное,
							"Запись документа", УровеньЖурналаРегистрации.Предупреждение);
						КонецПопытки; 
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				ЕстьВходящиеСооб = Истина;
				// Создаем документ для входящего сообщения
				ВходящееСообщение = Документы.SMS.СоздатьДокумент();
				ВходящееСообщение.Дата = ТекущаяДата();
				ВходящееСообщение.Входящее_Исходящее = Перечисления.ТипыСообщений.Входящее;
				ВходящееСообщение.НомерОтправителя = Строка.Отправитель;
				ВходящееСообщение.Организация = ПараметрыСеанса.Организация;
				ВходящееСообщение.ХозОперация = Справочники.ХозОперации.SMS;
				ВходящееСообщение.ПодразделениеКомпании = ПараметрыСеанса.ПодразделениеКомпании;
				#Если Сервер Тогда
					ВходящееСообщение.Автор = Константы.смсРоботРегламентныхЗаданий.Получить();
				#Иначе
					ВходящееСообщение.Автор = ПараметрыСеанса.Пользователь;
				#Конецесли
				ВходящееСообщение.ТекстСообщения = Строка.ТекстСообщения;
				НовСтрока = ВходящееСообщение.Получатели.Добавить();
				НовСтрока.GUID_SMS = Строка.GUID;
				ДатаДок = Лев(СтрЗаменить(Строка.ДатаПолучения, " ", ""), 16);
				ДатаДок = СтрЗаменить(ДатаДок, ":", "");
				НовСтрока.ДатаЗавершения = Дата(ДатаДок);
				ДатаПослСообщения = Дата(ДатаДок);
				Миллисекунда  	  = Строка.Миллисекунда;
				НовСтрока.НомерТелефона = Строка.Получатель;
				НовСтрока.ТекстСообщения = Строка.ТекстСообщения;
				НовСтрока.Миллисекунда   = Строка.Миллисекунда;
				Если Число(Строка.ТекущаяЧасть) < Число(Строка.ВсегоЧастей) Тогда
					НовСтрока.Статус = Перечисления.смсСостоянияСообщений.ПолученоЧастично;
					ВходящееСообщение.Статус = Перечисления.смсСостоянияСообщений.ПолученоЧастично;
					ВходящееСообщение.СтатусСтрокой = Перечисления.смсСостоянияСообщений.ПолученоЧастично;
				Иначе
					НовСтрока.Статус = Перечисления.смсСостоянияСообщений.Получено;
					ВходящееСообщение.Статус = Перечисления.смсСостоянияСообщений.Получено;
					ВходящееСообщение.СтатусСтрокой = Перечисления.смсСостоянияСообщений.Получено;
				КонецЕсли; 
				ВходящееСообщение.Записать();
			КонецЦикла;
		ИначеЕсли КодОшибки < 0 Тогда  
			смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не удалось получить сообщения. "+смсКоммуникатор.ОписаниеОшибокВебСервиса(КодОшибки)+"'"), СтатусСообщения.Важное,
			"Получение сообщений", УровеньЖурналаРегистрации.Предупреждение);
		КонецЕсли;
		// Устаном дату получения сообщения равной дате последнего полученного сообщения
		Если ЕстьВходящиеСооб Тогда
			НоваяЗапись = РегистрыСведений.смсПараметры.СоздатьМенеджерЗаписи();
			НоваяЗапись.Прочитать();
			НоваяЗапись.ДатаПолученияСМС = ДатаПослСообщения;
			НоваяЗапись.Миллисекунда     = Миллисекунда;
			НоваяЗапись.Записать();
		КонецЕсли; 
		Если ПрерватьОбработку тогда //перешли на другой сервер, прерываем рагламентное задание
			Возврат;	
		КонецЕсли;
	КонецЕсли;	
	
	
	//////////////////////////////////////////////////////////////	
	// Обновление статусов сообщений
	
	// Получаем сообщения для обновления статусов
	СписокСообщений = смсКоммуникатор.ТаблицаЗначенийВМассивСтруктур(ПолучитьСообщенияДляОбновления());
	Для Каждого Строка Из СписокСообщений Цикл
		Строка.СтатусСообщения = СтатусВКод(Строка.СтатусСообщения);
	КонецЦикла; 
	
	// Запрашиваем на сервере состояния сообщений
	КодОшибки = смсКоммуникатор.ОбновитьСтатусы(СписокСообщений);

	Если КодОшибки < -100000 Тогда //Перешли на другой сервер
		ПрерватьОбработку = Истина;
		КодОшибки = КодОшибки + 100000;
	КонецЕсли;
	
	Если КодОшибки = 1 Тогда
		Для Каждого Строка Из СписокСообщений Цикл
			МенеджерЗаписи = РегистрыСведений.смсСостоянияСообщений.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Сообщение = Строка.Сообщение;
			МенеджерЗаписи.НомерСтрокиДокумента = Строка.НомерСтрокиДокумента;
			МенеджерЗаписи.GUID	= Строка.GUID;
			Если Число(Строка.СтатусСообщения) > 0 Тогда
				МенеджерЗаписи.Статус  = КодВСтатус(Строка.СтатусСообщения);
			Иначе
				МенеджерЗаписи.Статус = Перечисления.смсСостоянияСообщений.Ошибка;
				МенеджерЗаписи.ОписаниеОшибки = смсКоммуникатор.ОписаниеОшибокВебСервиса(Число(Строка.СтатусСообщения));
			КонецЕсли; 
			МенеджерЗаписи.Записать();
		КонецЦикла; 
	ИначеЕсли КодОшибки = -21 или КодОшибки = -22 Тогда   //Глобальная ошибка из которой ясно, что в первой строке ошибка.  
		Строка = СписокСообщений[0];
		МенеджерЗаписи = РегистрыСведений.смсСостоянияСообщений.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Сообщение = Строка.Сообщение;
		МенеджерЗаписи.НомерСтрокиДокумента = Строка.НомерСтрокиДокумента;
		МенеджерЗаписи.GUID	= Строка.GUID;
		МенеджерЗаписи.Статус = Перечисления.смсСостоянияСообщений.Ошибка;
		МенеджерЗаписи.ОписаниеОшибки = смсКоммуникатор.ОписаниеОшибокВебСервиса(КодОшибки);
		
		МенеджерЗаписи.Записать();
		
	ИначеЕсли КодОшибки < 0 Тогда 
		смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не удалось обновить статусы сообщений. "+смсКоммуникатор.ОписаниеОшибокВебСервиса(КодОшибки)+"'"), СтатусСообщения.Важное,
		"Обновление статусов сообщений", УровеньЖурналаРегистрации.Предупреждение);
	КонецЕсли;
	
	ЗаписанныйДок = "";
	// обновляем статус документов
	Для Каждого Строка Из СписокСообщений Цикл
		ТекущийДок = Строка.Сообщение;
		
		Если НЕ ТекущийДок = ЗаписанныйДок Тогда
			Попытка
				ДокументОбъект = ТекущийДок.ПолучитьОбъект();
				СтруктураСтатуса = ДокументОбъект.ПолучитьСтатусДокумента();
				ДокументОбъект.СтатусСтрокой = СтруктураСтатуса.СтатусСтрокой;
				ДокументОбъект.Статус        = СтруктураСтатуса.Статус;
				ДокументОбъект.Записать();
			Исключение
				смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не удалось записать документ: "+ДокументОбъект.Номер+"'"), СтатусСообщения.Важное,
				"Запись документа", УровеньЖурналаРегистрации.Предупреждение);
			КонецПопытки; 
			ЗаписанныйДок = ТекущийДок;
		КонецЕсли;
	КонецЦикла; 	
	
КонецПроцедуры

// Функция ИнформационнаяБазаФайловая определяет режим эксплуатации
// информационной базы файловый (Истина) или Серверный (Ложь).
//  При проверке используется СтрокаСоединенияИнформационнойБазы, которую
// можно указать явно.
//
// Параметры:
//  СтрокаСоединенияИнформационнойБазы - Строка - параметр используется, если
//                 нужно проверить строку соединения не текущей информационной базы.
//
// Возвращаемое значение:
//  Булево.
//
Функция ИнформационнаяБазаФайловая(Знач СтрокаСоединенияИнформационнойБазы = "") Экспорт
            
    Если ПустаяСтрока(СтрокаСоединенияИнформационнойБазы) Тогда
        СтрокаСоединенияИнформационнойБазы =  СтрокаСоединенияИнформационнойБазы();
    КонецЕсли;
    Возврат Найти(Врег(СтрокаСоединенияИнформационнойБазы), "FILE=") = 1;
    
КонецФункции // ИнформационнаяБазаФайловая()

// Процедура выводит переданное сообщение
//
// Параметры:
//	Сообщение	- Строка					- Текст сообщения
//	Статус		- СтатусСообщения			- Статус сообщения
//	Заголовок	- Строка					- Событие для журнала регистрации
//	Уровень		- УровеньЖурналаРегистрации	- Уровень для журнала регистрации
//
Процедура ВывестиСообщение(Сообщение, Статус, Заголовок = "Сообщение", Уровень) Экспорт
	
	ЗаписьЖурналаРегистрации(Заголовок, Уровень,,, Сообщение);
	#Если НаКлиенте Тогда
		Сообщить(Сообщение, Статус);
	#КонецЕсли
	
КонецПроцедуры	// ВывестиСообщение()

// Установить константы по умолчанию
//
Процедура УстановитьКонстанты() Экспорт
#Если Сервер Тогда
	
	Если Константы.смсСрокЖизниSMS.Получить() = 0 Тогда
		Константы.смсСрокЖизниSMS.Установить(24);
	КонецЕсли; 
	
#КонецЕсли

КонецПроцедуры

&НаСервере
Функция ПолучитьРегламентноеЗаданиеПроверкаСМС() Экспорт
    Возврат РегламентныеЗадания.НайтиПредопределенное("ПроверкаСМС");
КонецФункции

&НаСервере
Процедура ОбработатьРеглЗадание() Экспорт
    
    Попытка
        Задание = ПолучитьРегламентноеЗаданиеПроверкаСМС();
        ОбработатьРегламентноеЗадание = Задание.Расписание.ТребуетсяВыполнение();
        Если ОбработатьРегламентноеЗадание Тогда
            Выполнить("РегламентноеЗаданиеПроверкаСМС()");
        КонецЕсли; 
    Исключение
        смсРаботаССообщениями.ВывестиСообщение(НСтр("ru = 'Не удалось обработать регламентное задание.'"), СтатусСообщения.Важное,
        "Регламентное задание", УровеньЖурналаРегистрации.Предупреждение);
    КонецПопытки; 

КонецПроцедуры

 // Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  ДокументОбъект             - объект проводимого документа, 
//  СтруктураОбязательныхПолей - структура, содержащая имена полей, которые собственно и надо проверить.
//  Отказ                      - флаг отказа в проведении.
//  Заголовок                  - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапкиДокумента(ДокументОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок) Экспорт
	
	МетаданныеРеквизиты = ДокументОбъект.Метаданные().Реквизиты;
	
	Для каждого КлючЗначение Из СтруктураОбязательныхПолей Цикл

		Значение = ДокументОбъект[КлючЗначение.Ключ];
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			Если НЕ ЗначениеЗаполнено(КлючЗначение.Значение) Тогда
				ПредставлениеРеквизита = МетаданныеРеквизиты[КлючЗначение.Ключ].Представление();
				СтрокаСообщения = НСтр("ru = 'Не заполнено значение реквизита """ + СокрЛП(ПредставлениеРеквизита) + """!'");
			Иначе
				СтрокаСообщения = НСтр("ru = '"+КлючЗначение.Значение+"'");
			КонецЕсли;
			смсРаботаССообщениями.ВывестиСообщение(СтрокаСообщения, СтатусСообщения.Важное,
			"Проверка реквизитов документа", УровеньЖурналаРегистрации.Предупреждение);
			Отказ = Истина;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеШапкиДокумента()


#Если Клиент Тогда
// Процедура выполняет сворачивание/разворачивание элементов форм
//
// Параметры:
//  Видимость         - Булево, признак "развернутости" элемент. 
//  ГлавныйЭлемент    - Элемент формы который сворачиваем.
//  Главный разделить - Разделитель.
//  Панель формы      - панель или главная панель формы.
//  Направление       - Строка, направление сворачивания.
//  Пропорционально   - Булево - Истина, если разделитель привязывается пропорционально
//
Процедура УправлениеВидимостиЭлементов(Видимость, ГлавныйЭлемент, ГлавныйРазделитель, ПанельФормы, Направление, Пропорционально = Истина) Экспорт
	Если ВРег(Направление) =  "ЛЕВО" Тогда
		Если Видимость Тогда
			// откроем 
			ГлавныйРазделитель.Свертка=РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка=РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Лево);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Право,ГлавныйРазделитель,ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.Ширина = 4;
		Иначе	
			// скроем 
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйЭлемент,ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право);
			ГлавныйЭлемент.Свертка=РежимСверткиЭлементаУправления.Лево;
			ГлавныйРазделитель.Свертка=РежимСверткиЭлементаУправления.Лево;
		КонецЕсли;
	ИначеЕсли ВРег(Направление) = "ПРАВО" Тогда
		
		Если Видимость Тогда
			// Показать
			ГлавныйРазделитель.Свертка = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка = РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право,  ПанельФормы, ГраницаЭлементаУправления.Лево);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельФормы, ГраницаЭлементаУправления.Право);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ГлавныйРазделитель, ГраницаЭлементаУправления.Право);
			ГлавныйРазделитель.Ширина = 4;
		Иначе 
			//скрыть 
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Право, ГлавныйЭлемент, ГраницаЭлементаУправления.Лево);
			ГлавныйЭлемент.Свертка = РежимСверткиЭлементаУправления.Право;
			ГлавныйРазделитель.Свертка = РежимСверткиЭлементаУправления.Право;
		КонецЕсли;	
		
	ИначеЕсли ВРег(Направление) = "НИЗ" Тогда
		
		Если Видимость Тогда
			// Откроем
			ГлавныйРазделитель.Свертка  = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка		= РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Верх);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ПанельФормы, ГраницаЭлементаУправления.Низ);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			ГлавныйРазделитель.Высота 	= 4;
		Иначе // Скроем 
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх);
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Верх);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ГлавныйЭлемент,ГраницаЭлементаУправления.Верх);
			ГлавныйЭлемент.Свертка 		= РежимСверткиЭлементаУправления.Низ;		
			ГлавныйРазделитель.Свертка	= РежимСверткиЭлементаУправления.Низ;		
		КонецЕсли;
		
	ИначеЕсли ВРег(Направление) = "ВЕРХ" Тогда
		Если Видимость Тогда
			// Откроем
			ГлавныйРазделитель.Свертка  = РежимСверткиЭлементаУправления.Нет;
			ГлавныйЭлемент.Свертка		= РежимСверткиЭлементаУправления.Нет;
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйРазделитель,ГраницаЭлементаУправления.Низ);
			Если Пропорционально Тогда
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Верх);
			Иначе
				ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельФормы, ГраницаЭлементаУправления.Низ);
			КонецЕсли;	
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ГлавныйРазделитель,ГраницаЭлементаУправления.Верх);
			ГлавныйРазделитель.Высота 	= 4;
		Иначе // Скроем 
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Низ);
			ГлавныйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Низ);
			ГлавныйРазделитель.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ГлавныйЭлемент, ГраницаЭлементаУправления.Низ);
			ГлавныйЭлемент.Свертка 		= РежимСверткиЭлементаУправления.Верх;		
			ГлавныйРазделитель.Свертка	= РежимСверткиЭлементаУправления.Верх;		
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры	// обУправлениеВидимостиЭлементов()
#КонецЕсли


Функция ПолучитьОбязательныеПоля() Экспорт
	ОбязательныеПоля = Новый Массив;
	ОбязательныеПоля.Добавить("Category");
	ОбязательныеПоля.Добавить("Title");
	ОбязательныеПоля.Добавить("Condition");
	
	Возврат ОбязательныеПоля;
КонецФункции

Функция ПолучитьОбязательныеПоляШаблона() Экспорт
	ОбязательныеПоля = Новый Массив;
	ОбязательныеПоля.Добавить("Address");
	ОбязательныеПоля.Добавить("Category");
	ОбязательныеПоля.Добавить("Description");
	
	Возврат ОбязательныеПоля;
КонецФункции

Функция ПолучитьДополнительныеОбязательныеПоля(Category, GoodsType) Экспорт
	ОбязательныеПоля = Новый Массив;
	
	Если НЕ Category = ns_БытоваяЭлектроникаПовтИсп.ПолучитьНастольныеКомпьютеры() Тогда
		ОбязательныеПоля.Добавить("GoodsType");
	КонецЕсли; 
	
	Если Category = ns_БытоваяЭлектроникаПовтИсп.ПолучитьАудиоИВидео() Тогда
		Если GoodsType = ns_БытоваяЭлектроникаПовтИсп.ПолучитьGoodsType_ТелевизорыИПроекторы() Тогда
			ОбязательныеПоля.Добавить("ProductsType");
		КонецЕсли; 
	КонецЕсли;
	
	Возврат ОбязательныеПоля;
КонецФункции

Функция СнятьБезОстатковСВыгрузки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_БытоваяЭлектроника.Ссылка КАК Ссылка,
	|	ns_БытоваяЭлектроника.Номенклатура КАК Номенклатура,
	|	ns_БытоваяЭлектроника.Характеристика КАК Характеристика
	|ИЗ
	|	Справочник.ns_БытоваяЭлектроника КАК ns_БытоваяЭлектроника
	|ГДЕ
	|	ns_БытоваяЭлектроника.Выгружать = ИСТИНА";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Выгрузка = РезультатЗапроса.Выгрузить();
	Номенклатура = Выгрузка.ВыгрузитьКолонку("Номенклатура");
	Остатки = ns_Переопределяемый.ПолучитьТаблицуОстатков(Номенклатура);
	
	Для каждого Стр Из Выгрузка Цикл
		Если ЗначениеЗаполнено(Стр.Характеристика) Тогда
			Идентификатор = Строка(Стр.Номенклатура.УникальныйИдентификатор()) + "#" + Строка(Стр.Характеристика.УникальныйИдентификатор());
		Иначе
			Идентификатор = Строка(Стр.Номенклатура.УникальныйИдентификатор());
		КонецЕсли; 
		
		Если Остатки[Идентификатор] = Неопределено Тогда
			ОбъектЗапчасти = Стр.Ссылка.ПолучитьОбъект();
			ОбъектЗапчасти.Выгружать = Ложь;
			ОбъектЗапчасти.Записать();
		КонецЕсли;	
	КонецЦикла;
	
КонецФункции

Функция ПроверитьОшибкиПриВыгрузке(Данные, ТаблицаОшибок, Картинки = Неопределено) Экспорт
	ЕстьОшибка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Данные.Category) Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле Category", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Данные.GoodsType) И НЕ Строка(Данные.Category) = "Настольные компьютеры" Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле GoodsType", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Данные.Title) Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле Title", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Данные.Condition) Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле Condition", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	КонецЕсли;
	
	
	Если Картинки = Неопределено Тогда
		ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не указана картинка товара", Данные, ТаблицаОшибок);
		ЕстьОшибка = Истина;
	Иначе
		КартинкиТовара = Картинки[Данные.Ссылка];
		Если КартинкиТовара = Неопределено Тогда
			ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не указана картинка товара", Данные, ТаблицаОшибок);
			ЕстьОшибка = Истина; 
		ИначеЕсли КартинкиТовара.Количество() = 0 Тогда	
			ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не указана картинка товара", Данные, ТаблицаОшибок);
			ЕстьОшибка = Истина; 
		КонецЕсли;
	КонецЕсли;	
	
	ДопПоля = ПолучитьДополнительныеОбязательныеПоля(Данные.Category, Данные.GoodsType);
	Для каждого ТекПоле Из ДопПоля Цикл
		Если НЕ ЗначениеЗаполнено(Данные[ТекПоле]) Тогда
			ns_АвитоОбработкаОшибок.ДобавитьОшибку("Не заполнено поле " + ТекПоле, Данные, ТаблицаОшибок);
			ЕстьОшибка = Истина;
		КонецЕсли;
	КонецЦикла;        
	
	Возврат ЕстьОшибка;
КонецФункции

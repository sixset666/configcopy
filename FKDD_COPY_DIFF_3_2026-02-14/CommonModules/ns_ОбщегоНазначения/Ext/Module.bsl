//Область Интерфейс

Процедура СообщениеПользователю(СтрокаСообщение) Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрокаСообщение;
	Сообщение.Сообщить(); 
КонецПроцедуры

Процедура ДобавитьКнопкуНаФорму(Элементы, Команды, ИмяКнопки, Синоним, ИмяДействия, ГруппаРодитель = Неопределено, ТолькоВоВсехДействиях = Ложь, Картинка = Неопределено, Пометка = Ложь) Экспорт

    ИмяКоманды = "Команда" + ИмяКнопки;
    
    Команда = Команды.Добавить(ИмяКоманды);
    Команда.Действие = ИмяДействия;
    Если Картинка <> Неопределено Тогда
        Команда.Картинка = Картинка;
    КонецЕсли;
    
    Если ГруппаРодитель = Неопределено Тогда
        НоваяКнопка = Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"));
    Иначе    
        НоваяКнопка = Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), ГруппаРодитель);
    КонецЕсли;      
    НоваяКнопка.Вид                     = ВидКнопкиФормы.КнопкаКоманднойПанели;
    НоваяКнопка.ИмяКоманды              = ИмяКоманды;
    НоваяКнопка.Заголовок               = Синоним;
    НоваяКнопка.ТолькоВоВсехДействиях   = ТолькоВоВсехДействиях;
    НоваяКнопка.Пометка                 = Пометка;

КонецПроцедуры

//КонецОбласти 

//Область СтроковыеФункциии

Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",",	Знач ПропускатьПустыеСтроки = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЕстьКириллица(ИсходнаяСтрока) Экспорт
	ЕстьКириллица = Ложь;
	ИсходнаяСтрока = СокрЛП(ИсходнаяСтрока);
	Если СтрДлина(ИсходнаяСтрока)>0 Тогда
		Для Сч = 1 по СтрДлина(ИсходнаяСтрока) Цикл
			Символ = НРег(Сред(ИсходнаяСтрока, Сч, 1));
			Если Найти("абвгдеёжзийклмнопрстуфхцчшщъыьэюя", Символ) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции 

//КонецОбласти

//Область Разное

Функция ЭтоГрафическийФайл(Расширение) Экспорт
	НРегРасширение = НРег(Расширение);
	РасширенияИзображений = ".jpg.jpeg.png.bmp.ico.gif.tif.emf.wmf";
	Возврат Найти(РасширенияИзображений, НРегРасширение) > 0;
КонецФункции

//КонецОбласти 

//Область Производительность

Функция НачатьЗамер() Экспорт
	Возврат ТекущаяДата();
КонецФункции

Функция ЗакончитьЗамер(Замер) Экспорт
	
	КонВремя = ТекущаяДата();
	КонДни      = 0;
	КонЧасы     = 0;
	КонМинуты   = 0;
	КонСекунды = 0;
	
	ОстатокВремени = КонВремя - Замер;
	КонДни          = Цел(ОстатокВремени / 86400); //++ 86400 - Количество секунд в сутках
	ОстатокВремени = ОстатокВремени - КонДни * 86400;
	КонЧасы         = Цел(ОстатокВремени / 3600); //++ 3600 - Количество секунд в часе
	ОстатокВремени  = ОстатокВремени - КонЧасы * 3600;
	КонМинуты       = Цел(ОстатокВремени / 60); //++ 60 - Количество секунд в минуте
	ОстатокВремени  = ОстатокВремени - КонМинуты * 60;
	КонСекунды      = ОстатокВремени;
	ТекСтрока       = Формат(КонЧасы,"ЧЦ=2; ЧН=00; ЧВН=") + ":" + Формат(КонМинуты,"ЧЦ=2; ЧН=00; ЧВН=") + ":" + Формат(КонСекунды,"ЧЦ=2; ЧН=00; ЧВН=");
	
	Возврат ТекСтрока;
КонецФункции

//КонецОбласти
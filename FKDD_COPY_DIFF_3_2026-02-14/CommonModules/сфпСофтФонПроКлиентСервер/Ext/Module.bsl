//////////////////////////////////////////////////////////////
// ВЫНЕСЕННЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция формирует список объектов для перезаполнения по номерам
//
// Параметры:
//	Нет.
//
Функция сфпПолучитьСписокОбъектовДляПерезаполнения() Экспорт
	СписокОбъектов = Новый СписокЗначений;
	УстановитьПривилегированныйРежим(Истина); 
	Для Каждого ТипСправочника Из Метаданные.РегистрыСведений.сфпНомераТелефоновДляПоиска.Измерения.Объект.Тип.Типы() Цикл
		СправочникСсылка = Новый(ТипСправочника);
		// +Софтфон_УТ 10.3
		//Если СправочникСсылка.Метаданные().ТабличныеЧасти.Найти("КонтактнаяИнформация") = Неопределено Тогда Продолжить; КонецЕсли;
		МассивТипов = Метаданные.РегистрыСведений.КонтактнаяИнформация.Измерения.Объект.Тип.Типы();
		Если МассивТипов.Найти(ТипСправочника) = Неопределено Тогда Продолжить; КонецЕсли;
		// -Софтфон_УТ 10.3
		Выборка = Справочники[СправочникСсылка.Метаданные().Имя].Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокОбъектов.Добавить(Выборка.Ссылка);;
		КонецЦикла;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь); 
	Возврат СписокОбъектов;
КонецФункции // сфпПолучитьСписокОбъектовДляПерезаполнения()

// Функция формирует список объектов для выбора пользователем среди найденного массива номеров
//
// Параметры:
//	МассивЗвонящих	- Массив - Массив найденных объектов по номеру телефона
//  ТолькоПартнеры  - Булево - В список добавлять только партнёров и КЛ
//
// Возвращаемое значение:
//	СписокКонтактов - СписокЗначений - Список значений для выбора
//
Функция сфпСформироватьСписокОбъектовДляВыбораПоМассивуЗвонящих(МассивЗвонящих, ТолькоПартнеры = Ложь) Экспорт
	СписокКонтактов = Новый СписокЗначений;
	Для Каждого Контакт Из МассивЗвонящих Цикл
		Если Найти(Строка(Контакт), НСтр("ru = '<Объект не найден>'")) > 0 Тогда
			// работаем под неполными правами, контакт нам не доступен, в список выбора не подставляем
			Продолжить;
		КонецЕсли;
		Если Тип("СправочникСсылка.Сотрудники") = ТипЗнч(Контакт) И НЕ ТолькоПартнеры Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru = ' (сотрудник)'"));
		ИначеЕсли Тип("СправочникСсылка.ПодразделенияКомпании") = ТипЗнч(Контакт) И НЕ ТолькоПартнеры Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru = ' (подразделение компании)'"));
		ИначеЕсли Тип("СправочникСсылка.Контрагенты") = ТипЗнч(Контакт) Тогда
			Если Контакт.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
				СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru = ' (контрагент, частное лицо)'"));
			Иначе
				СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru = ' (контрагент, юр. лицо)'"));
			КонецЕсли;
		ИначеЕсли Тип("СправочникСсылка.Организации") = ТипЗнч(Контакт) И НЕ ТолькоПартнеры Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru = ' (организация)'"));
		Иначе
			// в список найденных не добавляем
		КонецЕсли;
	КонецЦикла;
	Возврат СписокКонтактов;
КонецФункции

// Функция преобразовывает входящий массив в список значений
//
// Параметры:
//	МассивЗвонящих	- Массив - Массив звонящих
//
// Возвращаемое значение:
//	СписокЗвонящих - Список значений - Преобразованынй список значений
//
Функция сфпПреобразоватьМассивЗвонящихВСписокЗначений(МассивЗвонящих) Экспорт
	СписокЗвонящих = Новый СписокЗначений;
	Для Каждого ЭлементМассива Из МассивЗвонящих Цикл
		СписокЗвонящих.Добавить(ЭлементМассива);
	КонецЦикла;
	Возврат СписокЗвонящих;
КонецФункции	

// Сравнить две строки версий.
//
// Параметры
//  СтрокаВерсии1  – Строка – номер версии в формате РР.{П|ПП}.ЗЗ.СС
//  СтрокаВерсии2  – Строка – второй сравниваемый номер версии
//
// Возвращаемое значение:
//   Число   – больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
//
Функция сфпСравнитьВерсии(Знач СтрокаВерсии1, Знач СтрокаВерсии2) Экспорт
	
	Строка1 = ?(ПустаяСтрока(СтрокаВерсии1), "0.0.0.0", СтрокаВерсии1);
	Строка2 = ?(ПустаяСтрока(СтрокаВерсии2), "0.0.0.0", СтрокаВерсии2);
	Версия1 = атСервер.РазложитьСтрокуВМассивПодстрок(Строка1, ".");
	Если Версия1.Количество() <> 4 Тогда
		ВызватьИсключение атСервер.ПодставитьПараметрыВСтроку(
		                    НСтр("ru = 'Неправильный формат строки версии: %1'"), СтрокаВерсии1);
	КонецЕсли;
	Версия2 = атСервер.РазложитьСтрокуВМассивПодстрок(Строка2, ".");
	Если Версия2.Количество() <> 4 Тогда
		ВызватьИсключение атСервер.ПодставитьПараметрыВСтроку(
	                         НСтр("ru = 'Неправильный формат строки версии: %1'"), СтрокаВерсии2);
	КонецЕсли;
	
	Результат = 0;
	Для Разряд = 0 По 3 Цикл
		Результат = Число(Версия1[Разряд]) - Число(Версия2[Разряд]);
		Если Результат <> 0 Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции


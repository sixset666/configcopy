
// Производит непосредственное чтение сообщения обмена из файла
//
// Параметры
//  ИмяФайлаСообщения  – Строка – имя файла сообщения обмена
//  УзелОбмена  – ПланОбменаСсылка.УдаленныеПодразделения – ссылка на узел обмена
//  КоличествоЭлементовВТранзакции - Число
//
// Возвращаемое значение:
//	Число - номер сообщения обмена
//
Функция ПрочитатьСообщениеОбмена(ИмяФайлаСообщения,УзелОбмена,КоличествоЭлементовВТранзакции) Экспорт
	ЕстьПрава = ПроверитьПользователя(); // враг не пройдет..
	Если НЕ ЕстьПрава Тогда
		ТекстОшибки = "Недостаточно прав для чтения сообщения обмена";
	    Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		ПрефиксЛога = "Обмен." + ?(УзелОбмена.Пустая(),"","Узел_" + СокрЛП(УзелОбмена.Префикс) + ".");
		ЗаписьЖурналаРегистрации(ПрефиксЛога + "Чтение", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		Возврат 0;
	КонецЕсли; 
	
    НомерСообщения = 0;
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайлаСообщения);
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
	НомерСообщения = ЧтениеСообщения.НомерСообщения;
	Если ЧтениеСообщения.Отправитель <> УзелОбмена Тогда
		ВызватьИсключение "Сообщение обмена предназначено не для этого узла!";  // прерываем сразу - легче от попыток не станет
	КонецЕсли;
	
	ПланыОбмена.ПрочитатьИзменения(ЧтениеСообщения, КоличествоЭлементовВТранзакции);
	
	ЧтениеСообщения.ЗакончитьЧтение();
	// закроем xml-файл
	Попытка 
		ЧтениеXML.Закрыть();
	Исключение 
		СтрСообщения = ИнформацияОбОшибке().Описание;
		ВызватьИсключение СтрСообщения; // не смогли закрыть - прерываем 
	КонецПопытки;
	
	Возврат НомерСообщения;
	
КонецФункции // пвПрочитатьСообщениеОбмена()

// Производит непосредственную запись сообщения обмена из файла
//
// Параметры
//  ИмяФайлаСообщения  – Строка – имя файла сообщения обмена
//  УзелОбмена  – ПланОбменаСсылка.УдаленныеПодразделения – ссылка на узел обмена
//  КоличествоЭлементовВТранзакции - Число
//
// Возвращаемое значение:
//	Число - номер сообщения обмена
//
Функция ЗаписатьСообщениеОбмена(ИмяФайлаСообщения,УзелОбмена,КоличествоЭлементовВТранзакции) Экспорт
	ЕстьПрава = ПроверитьПользователя(); // враг не пройдет..
	Если НЕ ЕстьПрава Тогда
		ТекстОшибки = "Недостаточно прав для записи сообщения обмена";
	    Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		ПрефиксЛога = "Обмен." + ?(УзелОбмена.Пустая(),"","Узел_" + СокрЛП(УзелОбмена.Префикс) + ".");
		ЗаписьЖурналаРегистрации(ПрефиксЛога + "Запись", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		Возврат 0;
	КонецЕсли; 
	
    НомерСообщения = 0;
	
	// Создаем объект записи XML
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайлаСообщения);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	// Создаем новое сообщение
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, УзелОбмена);
	
	НомерСообщения = ЗаписьСообщения.НомерСообщения;
	// сразу определим соответствие пространства имен
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsd", "http://www.w3.org/2001/XMLSchema");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8",  "http://v8.1c.ru/data");
	
	ПланыОбмена.ЗаписатьИзменения(ЗаписьСообщения, КоличествоЭлементовВТранзакции);
	ЗаписьСообщения.ЗакончитьЗапись();
	
	// закроем xml-файл
	Попытка 
		ЗаписьXML.Закрыть();
	Исключение 
		СтрСообщения = ИнформацияОбОшибке().Описание;
		ВызватьИсключение СтрСообщения; // не смогли закрыть - прерываем
	КонецПопытки;
	
	Возврат НомерСообщения;
	
КонецФункции // ЗаписатьСообщениеОбмена()

//Rarus Alkoko 24.07.2015 {

// Производит проверку существования файла на сервере
//
// Параметры
//  АдресФайла  – Строка – Полное имя файла
//
// Возвращаемое значение:
//	Булево - Признак существования файла
//
Функция ФайлСуществуетНаСервере(АдресФайла) Экспорт 
	Файл = Новый Файл(АдресФайла);
	Возврат Файл.Существует();
КонецФункции //ФайлСуществуетНаСервере()	
//Rarus Alkoko 24.07.2015 }

// БЛОКИРОВКИ

//Снятие блокировки соединений
Процедура СнятьБлокировкуСоединений() Экспорт
	ЕстьПрава = ПроверитьПользователя(); // враг не пройдет..
	Если НЕ ЕстьПрава Тогда
		ТекстОшибки = "Недостаточно прав для снятия блокировки установки соединений";
	    Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		ЗаписьЖурналаРегистрации("Блокировка работы с базой", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Блокировка = ПолучитьБлокировкуУстановкиСоединений();
	БылаУстановлена = Блокировка.Установлена;
	Если БылаУстановлена Тогда
		Блокировка.КодРазрешения = "";
		Блокировка.Установлена = Ложь;
		УстановитьБлокировкуУстановкиСоединений(Блокировка);
		ЗаписьЖурналаРегистрации("Блокировка работы с базой", УровеньЖурналаРегистрации.Предупреждение,
		, , "Блокировка соединений с базой снята");
	Иначе
		ЗаписьЖурналаРегистрации("Блокировка работы с базой", УровеньЖурналаРегистрации.Информация, , , "Не было зафиксировано режима блокировки соединений с базой");
	КонецЕсли; 
КонецПроцедуры // сфСнятьБлокировкуСоединений

// Устанавливает блокировку соединений к информационной базе
//
Функция УстановитьБлокировкуСоединений(Знач ТекстСообщения="",Знач КодРазрешения="",Начало=НЕОПРЕДЕЛЕНО,Конец=НЕОПРЕДЕЛЕНО,Параметр="180") Экспорт
	Результат = Ложь;
	ЕстьПрава = ПроверитьПользователя(); // враг не пройдет..
	Если НЕ ЕстьПрава Тогда
		ТекстОшибки = "Недостаточно прав для установления блокировки установки соединений";
	    Сообщить(ТекстОшибки,СтатусСообщения.Важное);
		ЗаписьЖурналаРегистрации("Блокировка работы с базой", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;

	Если ПустаяСтрока(ТекстСообщения) Тогда
		ТекстСообщения = 	"
							| База данных ЗАКРЫТА на регламентное обслуживание.
							|
							|ВНИМАНИЮ тех, кто еще пока продолжает cвою работу:
							|Администратор просит выйти из базы самостоятельно.
							|  Через несколько минут ваш сеанс будет завершен
							|автоматически в принудительном режиме!
							|";
	КонецЕсли;
	
	Если ПустаяСтрока(Параметр) Тогда
		Параметр = "180"; // В параметре будем устанавливать таймаут ожидания завершения работы пользователей (он строковый)
	КонецЕсли;
	
	Попытка ИнтервалОжидания = Число(Параметр); Исключение ИнтервалОжидания = 180; КонецПопытки;
						
	Если ПустаяСтрока(КодРазрешения) Тогда
		КодРазрешения = СокрЛП(Константы.КодРазрешенияВходаПриБлокировке.Получить()); // пароль для входа в заблокированную базу должен быть заполнен
		Если ПустаяСтрока(КодРазрешения) Тогда
			КодРазрешения = "AllowRobotLogon";	// поставим тогда наш любимый :)  (ключ /ucAllowRobotLogon)
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Начало) ИЛИ (ТипЗнч(Начало) <> Тип("Дата")) Тогда
		Начало = ТекущаяДата();
	КонецЕсли;
	ВремяОтключенияСессий = Начало + ИнтервалОжидания;
	// Сохраним время начала жесткого отрубания соединений в параметре сеанса "РежимРаботы"
	Попытка СтрРежимРаботы = СокрЛП(ПараметрыСеанса.РежимРаботы); Исключение СтрРежимРаботы = ""; КонецПопытки;
	ПозицияМаркера1 = Найти(СтрРежимРаботы,"T");
	ПозицияМаркера2 = Найти(СтрРежимРаботы,";");
	Если (ПозицияМаркера1 > 0) И (ПозицияМаркера2 > (ПозицияМаркера1+18))  Тогда 
		ПараметрыСеанса.РежимРаботы = Лев(СтрРежимРаботы,ПозицияМаркера1-1) + "T" + Строка(ВремяОтключенияСессий) + ";" + Сред(СтрРежимРаботы,ПозицияМаркера2+1);
	Иначе
		ПараметрыСеанса.РежимРаботы = СтрРежимРаботы + "T" + Строка(ВремяОтключенияСессий) + ";";
	КонецЕсли;
	
	Попытка	
		Блокировка = Новый БлокировкаУстановкиСоединений;
		Блокировка.Установлена 	= Истина;
		Блокировка.Начало 		= Начало;
		Если (ТипЗнч(Конец) = Тип("Дата")) И ЗначениеЗаполнено(Конец) Тогда
			Блокировка.Конец  		= Конец;
		КонецЕсли;
		Блокировка.КодРазрешения= КодРазрешения;
		Блокировка.Параметр		= Параметр;
		Блокировка.Сообщение 	= ТекстСообщения;	
			
		УстановитьБлокировкуУстановкиСоединений(Блокировка);
		
		Результат = Истина;
		ЗаписьЖурналаРегистрации("Блокировка работы с базой", УровеньЖурналаРегистрации.Предупреждение, , , "Успешно установлена блокировка соединений с базой данных."+?(ЗначениеЗаполнено(Блокировка.Начало)," Начало : "+Блокировка.Начало,""));

	Исключение
		СтрОшибка=ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("Блокировка работы с базой", УровеньЖурналаРегистрации.Ошибка, , , обСформироватьТекстСообщения(СтрОшибка));
	КонецПопытки;
	
	Возврат Результат;
КонецФункции // сфУстановитьБлокировкуСоединений()

// Выполняет проверку прав пользователя
//
// Параметры
//  Нет
//
// Возвращаемое значение:
//   Булево   – Истина, если у пользователя есть права на привилегии
//
Функция ПроверитьПользователя()

	ПравоАдминистрирования = ПравоДоступа("Администрирование",Метаданные);
	ПравоОбновлениеИОбменФЗ = НЕ ПравоДоступа("Администрирование", Метаданные) И ПравоДоступа("ОбновлениеКонфигурацииБазыДанных", Метаданные);

	Возврат ПравоАдминистрирования ИЛИ ПравоОбновлениеИОбменФЗ;
	
КонецФункции // ПроверитьПользователя()
 

///////////////////////////////////////////////////////////////////////////////

// Функция публикации файла с сообщением обмена
//
// Параметры:
//  ИмяФайла 		- имя файла, содержащего сообщение обмена
//  НомерСообщения  - номер сообщения отправляемых данных
//
Функция ОтправитьФайл_ЛокСеть(ИмяОтправляемогоФайла = "",УзелОбмена,НастройкаДоставки, 
	КаталогФайловСообщений,  КаталогТемпФайлов, ТемпФайлыДляУдаления) Экспорт
	
	// ПРОВЕРКИ
	Рез = Ложь; // все плохо - пока :-)
	
	ПараметрыОбмена 		= одОбменДанными.ПолучитьПараметрыСеансаОбмена();
	КомментироватьХодОбмена = ПараметрыОбмена.КомментироватьХодОбмена;
	ВестиЖурнал 			= ПараметрыОбмена.ВестиЖурналРегистрации;
	ПрефиксЛога 			= ПараметрыОбмена.ПрефиксЛога;
			
	// Проверка на существование файла-источника в выбранном каталоге
	ФайлИсточник = Новый Файл(ИмяОтправляемогоФайла);
	Если НЕ ФайлИсточник.Существует() Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Файл сообщения не обнаружен <" + ИмяОтправляемогоФайла + ">",,
			Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка, КомментироватьХодОбмена);
		Возврат Ложь;
	КонецЕсли;
	
	// в массив для удаления
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	
	// Проверка на существование каталога приемника (корневой каталог)
	Если НайтиФайлы(КаталогФайловСообщений).Количество() = 0 Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Каталог локальной сети не обнаружен <"
			+ КаталогФайловСообщений + ">", , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка, КомментироватьХодОбмена);
		Возврат Ложь;	
	КонецЕсли;
	
	// ОТПРАВКА
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Начало отправки файла <" + ИмяОтправляемогоФайла + ">");

	// Определим полное имя файла-приемника
	ИмяФайлаСообщения = ФайлИсточник.Имя;
	ПолноеИмяФайлаОбменаПриемник = КаталогФайловСообщений + ИмяФайлаСообщения;
	
	// Упакуем сообщение если оно запакованное во временную папку 
	Если НастройкаДоставки.АрхивироватьСообщенияОбмена Тогда
		ПолноеИмяФайлаОбменаПриемник = КаталогФайловСообщений + ФайлИсточник.ИмяБезРасширения + ".zip";
		Рез = ЗапаковатьОтправляемыйФайл(ФайлИсточник, НастройкаДоставки, КаталогТемпФайлов, 
			КомментироватьХодОбмена, ПрефиксЛога);
		Если Рез = Ложь Тогда
			Возврат Рез;
		КонецЕсли;
		ИмяОтправляемогоФайла = ФайлИсточник.ПолноеИмя; //теперь это zip вместо xml
	КонецЕсли;
    	
	// Копируем файл в каталог, определенный для данного узла
	// если необходимо - делаем копию уже отправленного сообщения
	Попытка
	    // копирование из темп-каталога в каталог-приемник
		ПереместитьФайл(ИмяОтправляемогоФайла, ПолноеИмяФайлаОбменаПриемник);  // файл отправлен
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Успешно отправлен файл <" + ИмяОтправляемогоФайла + "> в локальный каталог <" + КаталогФайловСообщений + ">"
			,,,КомментироватьХодОбмена);
		Рез = Истина;
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка отправки файла  <" + ИмяОтправляемогоФайла + "> в локальный каталог <" + КаталогФайловСообщений + ">: "
			+ ИнформацияОбОшибке().Описание, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка, КомментироватьХодОбмена);
		Рез = Ложь;
	КонецПопытки;
	
	// Локальный темп каталог чистим всегда от ВСЕХ наших действий (в т.ч. и следы архивации)
	// для лок.сети копии не нужны - чистим сразу
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;
    
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Конец отправки файла <" + ИмяОтправляемогоФайла + ">", , , КомментироватьХодОбмена);
	
	Возврат Рез;
	
КонецФункции // ОтправитьФайл_ЛокСеть()

// Функция публикации файла с сообщением обмена
//
// Параметры:
//  ИмяФайла 		- имя файла, содержащего сообщение обмена
//  НомерСообщения  - номер сообщения отправляемых данных
//
Функция ОтправитьФайл_FTP(ИмяОтправляемогоФайла = "",УзелОбмена,НастройкаДоставки, 
	КаталогФайловСообщений,  КаталогТемпФайлов, ТемпФайлыДляУдаления) Экспорт
	
	ПараметрыОбмена 		= одОбменДанными.ПолучитьПараметрыСеансаОбмена();
	КомментироватьХодОбмена = ПараметрыОбмена.КомментироватьХодОбмена;
	ВестиЖурнал 			= ПараметрыОбмена.ВестиЖурналРегистрации;
	ПрефиксЛога 			= ПараметрыОбмена.ПрефиксЛога;

	// ПРОВЕРКИ

	//проверка на существование файла-источника
	ФайлИсточник = Новый Файл(ИмяОтправляемогоФайла);
	Если НЕ ФайлИсточник.Существует() Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Файл сообщения не обнаружен <" + ИмяОтправляемогоФайла + ">",,
			Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	ПолноеИмяИсходногоФайла = ФайлИсточник.ПолноеИмя;
	ИмяИсходногоФайла = ФайлИсточник.Имя;
	// в массив для удаления
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	
	// Упакуем сообщение 
	Если НастройкаДоставки.АрхивироватьСообщенияОбмена Тогда
		Рез = ЗапаковатьОтправляемыйФайл(ФайлИсточник, НастройкаДоставки, КаталогТемпФайлов, 
			КомментироватьХодОбмена, ПрефиксЛога); // теперь это zip
		Если Рез = Ложь Тогда
			Возврат Рез;
		КонецЕсли;
		ИмяОтправляемогоФайла = ФайлИсточник.ПолноеИмя;
		// в массив для удаления
		ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	КонецЕсли;
	
	// транслит...
	Транслит = НастройкаДоставки.ВыполнятьТранслитерацию;
	ИмяФайлаСообщения = ФайлИсточник.Имя;
	Если Транслит Тогда
		ИмяФайлаСообщения = одОбменДанными.ВыполнитьТранслит(ИмяФайлаСообщения);
	КонецЕсли;

	КаталогОбменаFTP = НастройкаДоставки.КаталогОбменаFTP;
	Если Прав(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = КаталогОбменаFTP + "/";
	КонецЕсли;
	Если Лев(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = "/" + КаталогОбменаFTP;
	КонецЕсли;
	ПолноеИмяФайлаОбменаПриемник = КаталогОбменаFTP + ИмяФайлаСообщения;
	
	// каталог-источник на FTP - еще раз проверили (с момента послед. проверки связь могла пропасть
	СтрОшибкиFTP = "";
	FTP = ПодключитьсяКFTP(НастройкаДоставки, СтрОшибкиFTP);
	Если FTP = Неопределено Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрОшибкиFTP,,	Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		СтрОшибки = СтрОшибки + СтрОшибкиFTP + Символы.ПС;
		Возврат Ложь;
	КонецЕсли;
	
	// ОТПРАВКА
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Начало отправки файла <" + ИмяОтправляемогоФайла + ">");
	Рез = Ложь; // все плохо

	//пишем в каталог обмена файл
	Попытка
		FTP.Записать(ИмяОтправляемогоФайла, ПолноеИмяФайлаОбменаПриемник);
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Отправлен файл на <ftp://" + НастройкаДоставки.СерверFTP  + ПолноеИмяФайлаОбменаПриемник + ">",
			, Перечисления.СтатусыСобытийСистемногоПротокола.Информация);
		Рез = Истина; // отправили
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка отправки файла на <ftp://" + НастройкаДоставки.СерверFTP + ПолноеИмяФайлаОбменаПриемник + ">: "
			+ ИнформацияОбОшибке().Описание, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Рез = Ложь;
	КонецПопытки;
	
	Если Рез Тогда // Почистим файлы устаревших сообщений если есть там
		ПозРазделителя = Найти(ПолноеИмяФайлаОбменаПриемник,".");
		НомерОтправленного = Сред(ПолноеИмяФайлаОбменаПриемник,ПозРазделителя-10,10);
		МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Истина, "??????????", "*", , Транслит);
		FTPФайлыДляУдаления = FTP.НайтиФайлы(КаталогОбменаFTP, МаскаФайловУдаления);
		Для Каждого FTPФайл Из FTPФайлыДляУдаления Цикл
			ИмяFTPФайла = FTPФайл.Имя;
			ПозРазделителя = Найти(ИмяFTPФайла,".");
			Номер = Сред(ИмяFTPФайла,ПозРазделителя-10,10);
			Если Номер < НомерОтправленного Тогда
				Попытка // Пытаемся удалить файл со старыми сообщением
					FTP.Удалить(FTPФайл.ПолноеИмя, );
					СтрСообщения = "Удалено устаревшее сообщение <" + ИмяFTPФайла + ">";
					ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения);
				Исключение
					СтрОшибкиFTP = ОписаниеОшибки();
					СтрСообщения = "Ошибка удаления устаревшего сообщения <" + ИмяFTPФайла + "> - "+СтрОшибкиFTP;
					ЗаписьЛога(ПрефиксЛога + "Получение", СтрСообщения,, Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение);
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// перемещение из темп-каталога в каталог-отправ. сообщений
	Если Рез И НастройкаДоставки.СохранятьОтправленные Тогда 
		Попытка
		    ПолноеИмяОтправленногоФайла = КаталогФайловСообщений + "SENT\" + ИмяИсходногоФайла;
			ПереместитьФайл(ПолноеИмяИсходногоФайла, ПолноеИмяОтправленногоФайла);
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Сохранена копия файла <" + ПолноеИмяОтправленногоФайла + ">");
		Исключение
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка при сохранении копии файла <" + ПолноеИмяОтправленногоФайла + ">: "
			+ ИнформацияОбОшибке().Описание, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		КонецПопытки;
	КонецЕсли;
	
	// Локальный темп каталог чистим всегда от ВСЕХ наших действий (в т.ч. и следы архивации)
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	FTP = Неопределено;
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Конец отправки файла <" + ИмяОтправляемогоФайла + ">");
	
	Возврат Рез;

КонецФункции // ОтправитьФайл_FTP()

// Функция подключения к FTP
//Параметры:
//	ПараметрыСоединения - любая коллекция значений с именованными полями (структура, строка ТЗ и т.п.). Поля:
//		- СерверFTP
//		- ПортFTP
//		- ПользовательFTP
//		- ПарольFTP
//		- ПассивныйРежим
// Возвращает: объект FTPСоединение, если не установлено - Неопределено
Функция ПодключитьсяКFTP(НастройкаДоставки, СтрОшибки = "")
	FTP = Неопределено;
	
	//получаем параметры
	Попытка
		Сервер = НастройкаДоставки.СерверFTP;
		Порт = НастройкаДоставки.ПортFTP;
		Логин = НастройкаДоставки.ПользовательFTP;
		Пароль = НастройкаДоставки.ПарольFTP;
		ПассивныйРежим = НастройкаДоставки.ПассивныйРежим;
	Исключение
		СтрОшибки = СтрОшибки + "Некорректные параметры FTP: " + ИнформацияОбОшибке().Описание;
		Возврат неопределено;
	КонецПопытки;
	
	// Проверим реквизиты подключения к FTP
	Если FTP = НЕОПРЕДЕЛЕНО  Тогда 
		#Если Клиент Тогда
			Состояние("Попытка подключения к FTP...");
		#КонецЕсли
		Попытка 
			FTP = Новый FTPСоединение(Сервер, 
			Порт, 
			Логин, 
			Пароль,
			, 
			ПассивныйРежим);
			#Если Клиент Тогда
				Состояние("Попытка подключения к FTP - ОК");
			#КонецЕсли
		Исключение
			ТекстОшибки = ИнформацияОбОшибке().Описание;
			Если Найти(ВРег(ТекстОшибки),ВРег("couldn't connect to server")) Тогда
			    ТекстОшибки = " на указанном сервере не запущен FTP-сервер";
			ИначеЕсли Найти(ВРег(ТекстОшибки),ВРег("couldn't resolve host name")) Тогда 	
			    ТекстОшибки = " указанный сервер не доступен";
			КонецЕсли; 
			стрОшибки = стрОшибки + "Ошибка подключения к FTP серверу <ftp://" + НастройкаДоставки.СерверFTP + ">: " + ТекстОшибки;
			Возврат неопределено;
		КонецПопытки;
	КонецЕсли;
	
	// Проверим существование "нашего" каталога 		
	Попытка 
		#Если Клиент Тогда
			Состояние("Попытка установить каталог на FTP...");
		#КонецЕсли
		КаталогОбменаFTP = "";
		Если Прав(НастройкаДоставки.КаталогОбменаFTP,1) <> "/" Тогда
			КаталогОбменаFTP = НастройкаДоставки.КаталогОбменаFTP + "/";
		КонецЕсли;
		FTP.УстановитьТекущийКаталог(КаталогОбменаFTP);
	Исключение
		#Если Клиент Тогда
			Состояние("Попытка создать каталог на FTP...");
		#КонецЕсли
		КаталогОбменаFTP = НастройкаДоставки.КаталогОбменаFTP;
		Попытка // попробуем создать "нужный" каталог
			FTP.СоздатьКаталог(КаталогОбменаFTP);
			FTP.УстановитьТекущийКаталог(КаталогОбменаFTP);
		Исключение
			СтрОшибки = "Ошибка: не удалось создать на FTP заданный каталог обмена """ + КаталогОбменаFTP + """";
			Возврат неопределено;
		КонецПопытки;
	КонецПопытки;
	
	#Если Клиент Тогда
		Состояние("Проверяем текущий каталог на FTP...");
	#КонецЕсли
	СтрТекущийКаталог = FTP.ТекущийКаталог();
	Если Прав(СтрТекущийКаталог,1) <> "/" Тогда
		СтрТекущийКаталог = СтрТекущийКаталог + "/";
	КонецЕсли;
	Если Лев(КаталогОбменаFTP,1) <> "/" Тогда
		КаталогОбменаFTP = "/" + КаталогОбменаFTP;
	КонецЕсли;
	Если ВРЕГ(СтрТекущийКаталог) = ВРЕГ(КаталогОбменаFTP) Тогда
		#Если Клиент Тогда
			Состояние("Проверяем текущий каталог на FTP - ОК");
		#КонецЕсли
		Возврат FTP;
	Иначе
		#Если Клиент Тогда
			Состояние("Проверяем текущий каталог на FTP - Ошибка!");
		#КонецЕсли
		СтрОшибки = "Ошибка: не удалось установить на FTP в качестве текущего заданный каталог обмена """ + КаталогОбменаFTP + """";
		Возврат неопределено;
	КонецЕсли;
	
КонецФункции

// Отправка файла по электронной почте
Функция ОтправитьФайл_Почта(ИмяОтправляемогоФайла = "",УзелОбмена,НастройкаДоставки, 
	КаталогФайловСообщений,  КаталогТемпФайлов, ТемпФайлыДляУдаления) Экспорт
	
	ПараметрыОбмена 		= одОбменДанными.ПолучитьПараметрыСеансаОбмена();
	КомментироватьХодОбмена = ПараметрыОбмена.КомментироватьХодОбмена;
	ВестиЖурнал 			= ПараметрыОбмена.ВестиЖурналРегистрации;
	ПрефиксЛога 			= ПараметрыОбмена.ПрефиксЛога;
     
	// ПРОВЕРКА
	//проверка на существование файла-источника
	ФайлИсточник = Новый Файл(ИмяОтправляемогоФайла);
	Если НЕ ФайлИсточник.Существует() Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Файл сообщения не обнаружен <" + ИмяОтправляемогоФайла + ">",,
			Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Возврат Ложь;
	КонецЕсли;
	ПолноеИмяИсходногоФайла = ФайлИсточник.ПолноеИмя;
	ИмяИсходногоФайла = ФайлИсточник.Имя;
	// в массив для удаления
	ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	
	// Упакуем сообщение 
	Если НастройкаДоставки.АрхивироватьСообщенияОбмена Тогда
		Рез = ЗапаковатьОтправляемыйФайл(ФайлИсточник, НастройкаДоставки, КаталогТемпФайлов, 
		КомментироватьХодОбмена, ПрефиксЛога); // теперь это zip
		Если Рез = Ложь Тогда
			Возврат Рез;
		КонецЕсли;
		ИмяОтправляемогоФайла = ФайлИсточник.ПолноеИмя;
		// в массив для удаления
		ТемпФайлыДляУдаления = ТемпФайлыДляУдаления + Символы.ПС + ИмяОтправляемогоФайла;
	КонецЕсли;
	
	// транслит...
	Транслит = НастройкаДоставки.ВыполнятьТранслитерацию;
	ИмяФайлаСообщения = ФайлИсточник.Имя;
	Если Транслит Тогда
		ИмяФайлаСообщения = одОбменДанными.ВыполнитьТранслит(ИмяФайлаСообщения);
	КонецЕсли;

    // проверили почту
	СтрОшибкиПочта = "";
	Почта = ПодключитьсяКПочте(НастройкаДоставки, СтрОшибкиПочта);
	Если Почта = Неопределено Тогда
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрОшибкиПочта, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		СтрОшибки = СтрОшибки + СтрОшибкиПочта + Символы.ПС;
		Возврат Ложь;
	КонецЕсли;
	
	// ОТПРАВКА
	Рез = Ложь; // все плохо
	
	// MAPI
	Если НастройкаДоставки.СпособОбмена = 1 Тогда
		#Если Клиент Тогда
			ПочтовоеСообщение = Новый ПочтовоеСообщение;
		#Иначе
			ЗаписьЛога(ПрефиксЛога + "Отправка", 
				"Почтовый профиль пользователя недоступен в режиме внешнего соединения или на сервере!", , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
			Возврат Ложь;
		#КонецЕсли
		// SMTP/POP3   
	Иначе	
		ПочтовоеСообщение = Новый ИнтернетПочтовоеСообщение;
		ПочтовоеСообщение.Кодировка = "windows-1251";
	КонецЕсли;
	
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Начало отправки файла <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">");
	Рез = Истина;
	
	// Формируем сообщение обмена
	ПочтовоеСообщение.Тема = "1С: СООБЩЕНИЕ ОБМЕНА: " + ИмяФайлаСообщения;
	ПочтовоеСообщение.Получатели.Добавить(НастройкаДоставки.АдресЭлектроннойПочты);
	ПочтовоеСообщение.Отправитель = НастройкаДоставки.АдресЭлектроннойПочты;
			
	// Создаем вложение с файлом обмена
	ПочтовоеСообщение.Вложения.Добавить(ИмяОтправляемогоФайла, ИмяФайлаСообщения);
	
	// Отправляем сообщение с файлом обмена
	Попытка
		Почта.Послать(ПочтовоеСообщение);
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Отправлен файл <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">.",
			,Перечисления.СтатусыСобытийСистемногоПротокола.Информация);
		Рез = Истина; // отправили
	Исключение
		ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка при отправке файла <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">: "
			+ ИнформацияОбОшибке().Описание, , Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		Рез = Ложь;
	КонецПопытки;
	
	// перемещение из темп-каталога в каталог-отправ. сообщений
	Если Рез И НастройкаДоставки.СохранятьОтправленные Тогда 
		Попытка
		    ПолноеИмяОтправленногоФайла = КаталогФайловСообщений + "SENT\" + ИмяИсходногоФайла;
			ПереместитьФайл(ПолноеИмяИсходногоФайла, ПолноеИмяОтправленногоФайла);
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Сохранена копия файла <" + ПолноеИмяОтправленногоФайла + ">");
		Исключение
			ЗаписьЛога(ПрефиксЛога + "Отправка", "Ошибка при сохранении копии файла <" + ПолноеИмяОтправленногоФайла + ">: "
			+ ИнформацияОбОшибке().Описание, ,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
		КонецПопытки;
	КонецЕсли;
	
	// Локальный темп каталог чистим всегда от ВСЕХ наших действий (в т.ч. и следы архивации)
	МаскаФайловУдаления = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Ложь, "??????????", "*");
	Файлы = НайтиФайлы(КаталогТемпФайлов, МаскаФайловУдаления);
	Для Каждого Файл Из Файлы Цикл
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
		КонецПопытки;
	КонецЦикла;

	Попытка
		Почта.Отключиться();
	Исключение
	КонецПопытки;
	
	ЗаписьЛога(ПрефиксЛога + "Отправка", "Конец отправки файла <" + ИмяОтправляемогоФайла + ", на адрес " + НастройкаДоставки.АдресЭлектроннойПочты + ">");
	
	Возврат Истина;
КонецФункции

// Функция подключения к почтовым серверам или почтовому
// клиенту для инициализации переменной "Почта"
// Возвращает: Соединение с почтой, если не установлено - Неопределено
Функция ПодключитьсяКПочте(НастройкаДоставки, СтрОшибки = "")
	Рез = Неопределено;
	
	Если НастройкаДоставки.СпособОбмена = 1 Тогда //MAPI
		#Если Клиент Тогда
			Почта = Новый Почта();
			Попытка
				Почта.Подключиться(); 
				Рез = Почта;
			Исключение
				СтрОшибки = СтрОшибки + "Ошибка при подключении к почтовому профилю пользователя: " + ИнформацияОбОшибке().Описание;
						 
				Возврат Рез;
			КонецПопытки;
		#Иначе
			СтрОшибки = СтрОшибки + "Почтовый профиль пользователя недоступен в режиме внешнего соединения или на сервере!";
			
			Возврат рез;
		#КонецЕсли
	
	Иначе //e-Mail
		ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
		ПочтовыйПрофиль.АдресСервераPOP3 = НастройкаДоставки.СерверPOP3;
		ПочтовыйПрофиль.ПортPOP3         = НастройкаДоставки.ПортPOP3;
		ПочтовыйПрофиль.Пользователь     = НастройкаДоставки.ПользовательPOP3;
		ПочтовыйПрофиль.Пароль           = НастройкаДоставки.ПарольPOP3;
		ПочтовыйПрофиль.АдресСервераSMTP = НастройкаДоставки.СерверSMTP;
		ПочтовыйПрофиль.ПортSMTP         = НастройкаДоставки.ПортSMTP;
		ПочтовыйПрофиль.ПользовательSMTP = НастройкаДоставки.ПользовательSMTP;
		ПочтовыйПрофиль.ПарольSMTP       = НастройкаДоставки.ПарольSMTP;
		Если НастройкаДоставки.Таймаут > 0 Тогда
			ПочтовыйПрофиль.ВремяОжидания = НастройкаДоставки.Таймаут;
		КонецЕсли; 
		   
		Почта = Новый ИнтернетПочта(); 
		Попытка
			Почта.Подключиться(ПочтовыйПрофиль);
			Рез = Почта;
		Исключение 
			СтрОшибки = СтрОшибки + "Ошибка при подключении к почтовому серверу SMTP: " + ИнформацияОбОшибке().Описание;
			
			Возврат Рез; 
		КонецПопытки;
		
	КонецЕсли;
		
	Возврат Рез;
КонецФункции	//	ПодключитьсяКПочте()

// Пишет системный протокол
Функция ЗаписьЛога(ТипСобытия, СтрСообщения, ОбъектЛога="", Знач СтатусСобытия = Неопределено, КомментироватьХодОбмена = Ложь) Экспорт
	
	СтатусСобытия = ?(СтатусСобытия = Неопределено, Перечисления.СтатусыСобытийСистемногоПротокола.Информация, СтатусСобытия);
	
	одОбменДанными.ЗафиксироватьСобытие(ТипСобытия, СтрСообщения, ОбъектЛога, СтатусСобытия, , , );
	#Если Клиент Тогда
		Если КомментироватьХодОбмена Тогда
			ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
			СтрИнтСообщения = ТекДата + СтрСообщения;
			Сообщить(СтрИнтСообщения, ?(СтатусСобытия = Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка, СтатусСообщения.Внимание, СтатусСообщения.Информация));
		КонецЕсли;
	#КонецЕсли
КонецФункции

// Выполняет архивацию файла обмена
//Параметры:
//	ПолноеИмяФайла - полное имя файла обмена
//	стрОшибки - строка ошибок, накопление ошибок при архивации
//	УдалитьИсточник - Булево - если Истина, то удалить после архивации исходный файл
Функция ЗапаковатьОтправляемыйФайл(ФайлСообщения, НастройкаДоставки, КаталогТемпФайлов, 
		КомментироватьХодОбмена, ПрефиксЛога = "")
	Рез = Ложь;
	// Сначала почистим временный каталог от прошлых архивов ???
	СтарыеФайлы = НайтиФайлы(КаталогТемпФайлов + ФайлСообщения.ИмяБезРасширения + ".zip");
	Для Каждого СтарыйФайл Из СтарыеФайлы Цикл
		УдалитьФайлы(СтарыйФайл.ПолноеИмя);
	КонецЦикла;

	Попытка
		ИмяФайлаZIP = ФайлСообщения.ИмяБезРасширения + ".zip";
		ПолноеИмяФайлаZIP = КаталогТемпФайлов + ИмяФайлаZIP;
		
		ФайлZIP = Новый ЗаписьZipФайла(ПолноеИмяФайлаZIP,НастройкаДоставки.ПарольАрхивации);
		ФайлZIP.Добавить(ФайлСообщения.ПолноеИмя);
		#Если Клиент Тогда
			Состояние("Упаковка файла сообщения в ZIP-архив...");
		#КонецЕсли
		ФайлZIP.Записать();
		#Если Клиент Тогда
			Состояние("Упаковка файла сообщения в ZIP-архив - Ок");
		#КонецЕсли
		ФайлZIP = Неопределено;

	Исключение
		СтрСообщения = "Не удалось запаковать файл <" + ФайлСообщения.ПолноеИмя + "> в каталог <" + КаталогТемпФайлов + "> " + ОписаниеОшибки();
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения,, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка, КомментироватьХодОбмена);
        Возврат Ложь;
	КонецПопытки;
	
	// Попробуем найти результат
	ФайлСообщения = Новый Файл(ПолноеИмяФайлаZIP);
	Если НЕ (ФайлСообщения.Существует() И ФайлСообщения.ЭтоФайл()) Тогда
		СтрСообщения = "Не удалось запаковать файл обмена  <" + ФайлСообщения.ПолноеИмя + ">";
		ЗаписьЛога(ПрефиксЛога + "Отправка", СтрСообщения,, Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка, КомментироватьХодОбмена);
		Возврат Ложь;
	Иначе
		Рез = Истина;
	КонецЕсли;
	
	Возврат Рез;

КонецФункции

// Удаляет файлы по маске (MessageExchange_Ц_П1_??????????.*)
//
// Параметры
//	НастройкаДоставки 	- СправочникСсылка.НастройкиДоставки. Настройка
//	Выгрузка 			- Булево. Флаг выгрузки сообщения
//	МаскаНомер 			- Строка. Маска номера файла сообщения обмена
//	МаскаРасш 			- Строка. Маска расширения файла сообщения обмена
//	ИсключатьТекущее 	- Булево. Флаг неудаления текущего сообщения
//	мсвИсключения 		- Массив. Массив файлов-исключений
//
// Возвращаемое значение:
//   Булево. Флаг успешного удаления файлов
//
Функция УдалитьФайлыПоМаске(УзелОбмена,НастройкаДоставки,Выгрузка = Истина, МаскаНомер = "??????????", МаскаРасш = "*", 
	ИсключатьТекущее = Истина, мсвИсключения = Неопределено) Экспорт
	
	Рез = Истина;
	КаталогСообщенийОбмена = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(КаталогСообщенийОбмена) Тогда
	    КаталогСообщенийОбмена = НастройкаДоставки.КаталогОбменов;
	КонецЕсли;
	
	Если ПустаяСтрока(КаталогСообщенийОбмена) Тогда
		КаталогСообщенийОбмена = КаталогВременныхФайлов();
	КонецЕсли;
	
	Маска = орМаскаФайлаДоставки(УзелОбмена,НастройкаДоставки, Выгрузка); //отраслевое переопределение маски
	Если ПустаяСтрока(Маска) Тогда
		ЭтотУзел = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
		ИмяФайлаТекСообщения = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Выгрузка, 
			?(Выгрузка, УзелОбмена.НомерОтправленного, УзелОбмена.НомерПринятого), , НастройкаДоставки.АрхивироватьСообщенияОбмена);
	
		Маска = одОбменДанными.ИмяФайлаДоставки(УзелОбмена, Выгрузка, МаскаНомер, МаскаРасш);
	КонецЕсли;	
	
	мсвФайлыОбмена = НайтиФайлы(КаталогСообщенийОбмена, Маска);
	ЕстьИсключенияКоторыеНельзяУдалять = (ТипЗнч(мсвИсключения) = Тип("Массив")) И (мсвИсключения.Количество() > 0);
	Для Каждого ТекФайл Из мсвФайлыОбмена Цикл
		Если ИсключатьТекущее И ТекФайл.Имя = ИмяФайлаТекСообщения Тогда
			Продолжить;
		КонецЕсли;
		Если ЕстьИсключенияКоторыеНельзяУдалять И (мсвИсключения.Найти(ТекФайл.ПолноеИмя) <> Неопределено) Тогда
			Продолжить;
		КонецЕсли;
		
		// Пытаемся удалить файл со старым сообщением
		Попытка
			УдалитьФайлы(ТекФайл.ПолноеИмя);
		Исключение
			Рез = Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Рез;
КонецФункции  // УдалитьФайлыПоМаске()

// Удаляет временные файлы (не сообщений) из темп-каталога обменов,
// оставшиеся в результате сбоев обмена
//
Процедура УдалитьВременныеФайлы(УзелОбмена, НастройкаОбмена) Экспорт
	
	ПараметрыОбмена 		= одОбменДанными.ПолучитьПараметрыСеансаОбмена();
	ПрефиксЛога 			= ПараметрыОбмена.ПрефиксЛога;

	// определимся с местонахождением временных файлов
	КорневойКаталог = Константы.СлужебныйКаталогРегламентныхОпераций.Получить();
	Если ПустаяСтрока(КорневойКаталог) Тогда
		Если НастройкаОбмена.Пустая() Тогда
			НастройкаДоставкиДоп = УзелОбмена.ИнформационнаяБаза.НастройкаДоставкиПоУмолчанию;
		Иначе
			НастройкаДоставкиДоп = НастройкаОбмена;
		КонецЕсли;
		КорневойКаталог = НастройкаДоставкиДоп.КаталогОбменов;
	КонецЕсли;	
	
	Если НастройкаОбмена.ИспользоватьВременныйКаталог Тогда 
		КаталогТемпФайлов = КорневойКаталог + "TEMP\";
	Иначе
		КаталогТемпФайлов = КаталогВременныхФайлов();
	КонецЕсли;
	
	// мусор мог остаться и в самом корневом каталоге
	МассивКаталоговВремФайлов = Новый Массив;
	МассивКаталоговВремФайлов.Добавить(КаталогТемпФайлов);
	МассивКаталоговВремФайлов.Добавить(КорневойКаталог);
	
	Для Сч = 0 По МассивКаталоговВремФайлов.ВГраница() Цикл
		КаталогТемпФайлов = МассивКаталоговВремФайлов[Сч];
		ТекстОшибки = "";
		Если НЕ ЕстьКаталог(КаталогТемпФайлов,ТекстОшибки) Тогда
			СтрОшибки = "Ошибка при удалении файлов из временного каталога <" + КаталогТемпФайлов + ">: " + ТекстОшибки;
			ЗаписьЛога(ПрефиксЛога + "УдалениеВремФайлов", СтрОшибки,
				,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
			Продолжить;
		КонецЕсли;
		
		МассивМасокФайлов = Новый Массив;
		МассивМасокФайлов.Добавить("_TstWRAcc_*.*");
		МассивМасокФайлов.Добавить("test_exch_*.*");
		Для Сч2 = 0 По МассивМасокФайлов.ВГраница() Цикл
			МаскаФайлов = МассивМасокФайлов[Сч2];
			УдалятьВремФайлы = Ложь;
			мсвФайлов = НайтиФайлы(КаталогТемпФайлов,МаскаФайлов);
			Если мсвФайлов.Количество()<>0 Тогда
				УдалятьВремФайлы = Истина;
			КонецЕсли;
			
			Если УдалятьВремФайлы Тогда
				ДатаУдаления = ТекущаяДата() - 3600*2; // создали 2 часа назад
				Для Каждого ТекФайл Из мсвФайлов Цикл
					Попытка
						Если ТекФайл.ПолучитьВремяИзменения() > ДатаУдаления Тогда
							Продолжить;
						КонецЕсли;
						УдалитьФайлы(ТекФайл.ПолноеИмя);
					Исключение
					КонецПопытки;
				КонецЦикла;
			КонецЕсли; 
		КонецЦикла;
		
	КонецЦикла;	// 	Для Сч = 0
	
КонецПроцедуры

// Проверят наличие указанного в параметре каталога
//если отсутствует, то пытается создать новый
//Добавляет заключительный знак "\" к переданному пути
//Возвращает:
//-	Истина если каталог есть (или был создан) 
//-	Ложь в противном случае
//Во втором (необязательном) параметре возвращает текст сообщения об ошибке
Функция ЕстьКаталог(Путь,ТекстОшибки="") Экспорт
	Если НЕ ЗначениеЗаполнено(Путь) ИЛИ (ТипЗнч(Путь) <> Тип("Строка")) Тогда
		ТекстОшибки = "Ошибка: Неверный параметр";
		Возврат Ложь;
	КонецЕсли;
	Если Прав(Путь,1) <> "\" Тогда
		Путь = Путь + "\";
	КонецЕсли;
    Попытка
		ПроверкаКаталога = Новый Файл(Путь);
		Если НЕ ПроверкаКаталога.Существует() Тогда
			СоздатьКаталог(Путь);
			ТекстОшибки = "Создан каталог: " + Путь;
			ПроверкаКаталога = Новый Файл(Путь);
		КонецЕсли;			
		Если ПроверкаКаталога.Существует() Тогда
			Если ПроверкаКаталога.ЭтоКаталог() Тогда
				Результат = Истина;
			Иначе
				ТекстОшибки = "Ошибка: заданный путь <"+Путь+"> не является каталогом (возможно это файл?)";
				Результат = Ложь;
			КонецЕсли;
		Иначе
			ТекстОшибки = "Ошибка: не удалось создать каталог: <"+Путь+">";
			Результат = Ложь;
		КонецЕсли;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Результат = Ложь;
	КонецПопытки;
	// Возвращаем что у нас получилось
	Возврат Результат;
КонецФункции // ЕстьКаталог

///////////////////////////////////////////////////////////////////////////////
// MICROCAT

// Блок авторизации пользователя MicroCat

// Выполняет авторизацию пользователя, и возвращает статус авторизации
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя , при авторизации из MicroCat(15 символов)
//  Пароль  - Строка - Пароль пользователя , при авторизации из MicroCat
//
// Возвращаемое значение:
//  СтатусАвторизации   - Строка, содержащая статус авторизации
//	"OK" - Авторизация прошла успешно
//  "INVU" - Пользователь не найден
//  "INVL" - Неправильный логин
//	"INVP" - Неправильный пароль
//
Функция пвСтатусАвторизацииПользователяMicroCat(Пользователь,Логин,Пароль) Экспорт

	Возврат РегистрыСведений.ПользователиMicroCat.СоздатьНаборЗаписей().СтатусАвторизацииПользователяMicroCat(Пользователь,Логин,Пароль);

КонецФункции // СтатусАвторизацииПользователя()

// Выполняет смену старого пароля пользователя MicroCat, на новый
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя , при авторизации из MicroCat(15 символов)
//  Пароль  - Строка - Пароль пользователя , при авторизации из MicroCat
//
// Возвращаемое значение:
//  СтатусСменаПароля   - Строка, содержащая статус авторизации
//	"OK" - Авторизация прошла успешно
//  "INVU" - Пользователь не найден
//  "INVL" - Неправильный логин
//	"INVP" - Неправильный старый пароль
//
Функция пвСменитьПарольПользователяMicroCat(Пользователь,Логин,ПарольСтарый,ПарольНовый) Экспорт

	Возврат РегистрыСведений.ПользователиMicroCat.СоздатьНаборЗаписей().СменитьПарольПользователяMicroCat(Пользователь,Логин,ПарольСтарый,ПарольНовый);	

КонецФункции // пвСменитьПарольПользователяMicroCat()

// Восстанавливает утерянный пароль пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя , при авторизации из MicroCat(15 символов)
//  Пароль  - Строка - Пароль пользователя , при авторизации из MicroCat
//
// Возвращаемое значение:
//  СтатусВосстановитьПароль   - Строка, содержащая статус восстановления пароля
//	"OK" - Пароль успешно восстановлен
//  "INVU" - Пользователь не найден
//  "INVL" - Неправильный логин
//  Неопределено - Нарушение прав доступа
//
Функция пвВосстановитьПарольПользователяMicroCat(Пользователь,Логин,ПарольНовый) Экспорт

	Возврат РегистрыСведений.ПользователиMicroCat.СоздатьНаборЗаписей().ВосстановитьПарольПользователяMicroCat(Пользователь,Логин,ПарольНовый);	

КонецФункции // пвВосстановитьПарольПользователяMicroCat()

// Добавляет нового пользователя в список пользователей MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//  Логин  - Строка - Логин пользователя , при авторизации из MicroCat(15 символов)
//  Пароль  - Строка - Пароль пользователя , при авторизации из MicroCat
//
// Возвращаемое значение:
//  СтатусНовыйПользователь   - Строка, содержащая статус добавления нового пользователя MicroCat
//	"OK" - Пользователь добавлен
//  "UOK" - Пользователь уже имеется в списке пользователей MicroCat
//
Функция пвДобавитьПользователяMicroCat(Пользователь,Логин,Пароль) Экспорт

	Возврат РегистрыСведений.ПользователиMicroCat.СоздатьНаборЗаписей().ДобавитьПользователяMicroCat(Пользователь,Логин,Пароль);	

КонецФункции // пвДобавитьПользователяMicroCat()

// Возвращает логин пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//
// Возвращаемое значение:
//  ЛогинПользователя   - Строка, содержащая логин пользователя MicroCat, иначе пустая строка
//
Функция пвПолучитьЛогинПользователяMicroCat(Пользователь) Экспорт

	Возврат РегистрыСведений.ПользователиMicroCat.СоздатьНаборЗаписей().ПолучитьЛогинПользователяMicroCat(Пользователь);	

КонецФункции // пвДобавитьПользователяMicroCat()

// Блок настройки параметров пользователя MicroCat

// Устанавливает параметры пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//
// Возвращаемое значение:
//  Флаг   - Булево, истина, если параметры успешно записаны, иначе ложь
//
Функция пвЗаписатьПараметрыПользователяMicroCat(Пользователь,СтруктураПараметров) Экспорт

	Возврат РегистрыСведений.ПользователиMicroCat.СоздатьНаборЗаписей().ЗаписатьПараметрыПользователяMicroCat(Пользователь,СтруктураПараметров);	

КонецФункции // пвДобавитьПользователяMicroCat()

// Возвращает параметры пользователя MicroCat
//
// Параметры
//  Пользователь  - СправочникСсылка.Пользователи - Пользователь системы 1С:Предприятие
//
// Возвращаемое значение:
//  СтруктураПараметров   - Параметры пользователя MicroCat
//
Функция пвПолучитьПараметрыПользователяMicroCat(Пользователь) Экспорт

	Возврат РегистрыСведений.ПользователиMicroCat.СоздатьНаборЗаписей().ПолучитьПараметрыПользователяMicroCat(Пользователь);	

КонецФункции // пвДобавитьПользователяMicroCat()


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ //////////////////////////////////////////

Функция ПоискПоляВСтруктуре(Знач ПолеПоиска, СтруктураПоиска, ТочноеСоответствие = Ложь)
	
	Результат  = Неопределено;
	ПолеПоиска = СокрЛП(ПолеПоиска);
	Если ТочноеСоответствие Тогда
		Для Каждого ТекПоле Из СтруктураПоиска Цикл
			Попытка
				ПутьКДанным = СокрЛП(ТекПоле.Ключ);
				Если ПутьКДанным = ПолеПоиска Тогда
					Результат = ТекПоле.Значение;
					Прервать;
				КонецЕсли;
			Исключение
				ПутьКДанным = СокрЛП(ТекПоле);
				Если ПутьКДанным = ПолеПоиска Тогда
					Результат = ТекПоле;
					Прервать;
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	Иначе
		Для Каждого ТекПоле Из СтруктураПоиска Цикл
			Попытка
				ПутьКДанным = СокрЛП(ТекПоле.Ключ);
				Если Найти(ПутьКДанным, ПолеПоиска)=1 Тогда
					Результат = ТекПоле.Значение;
					Прервать;
				КонецЕсли;
			Исключение
				ПутьКДанным = СокрЛП(ТекПоле);
				Если Найти(ПутьКДанным, ПолеПоиска)=1 Тогда
					Результат = ТекПоле;
					Прервать;
				КонецЕсли;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПоискВГруппировке(ПолеПоиска, Группировка)
	
	Результат = Ложь;
	Элементы = Группировка.ПоляГруппировки.Элементы;
	Для Каждого ТекГруппировка Из Элементы Цикл
		Если Не ТекГруппировка.Использование Тогда
			Продолжить;
		КонецЕсли;
		Если ТекГруппировка.Поле = ПолеПоиска Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПоискВГруппировке()

Функция ПолучитьГраницуПериода(Значение) Экспорт
	
	ЗначениеГраницы = Новый Граница(КонецДня(Значение), ВидГраницы.Включая);
	
	Возврат ЗначениеГраницы;
	
КонецФункции

Функция ПолучитьСтрокуПредставленияТипа(ПередаваемыйТип) Экспорт
	
	Результат = Неопределено;
	
	ТекущийТип = ?(Тип("ОписаниеТипов") = ТипЗнч(ПередаваемыйТип), ПередаваемыйТип.Типы()[0], ПередаваемыйТип);
	
	Если ТекущийТип=Тип("Строка") Тогда
		Результат = "Строка";
	ИначеЕсли ТекущийТип=Тип("Число") Тогда
		Результат = "Число";
	ИначеЕсли ТекущийТип=Тип("Дата") Тогда
		Результат = "Дата";
	ИначеЕсли ТекущийТип=Тип("Булево") Тогда
		Результат = "Булево";
	Иначе
		Результат = Метаданные.НайтиПоТипу(ТекущийТип).ПолноеИмя();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьСтрокуПредставленияТипа()

Функция ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, СвойствоОбъекта) Экспорт
	
	ИмяПВХ = ПланыВидовХарактеристик.СвойстваОбъектов.ПолучитьИмяПредопределенного(СвойствоОбъекта);
	
	ЗначениеСвойства     = "ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов."+ИмяПВХ+")";
	
	Результат = "
	|{ХАРАКТЕРИСТИКИ
	|ТИП("+СтрокаТипа+")
	|ВИДЫХАРАКТЕРИСТИК (ВЫБРАТЬ
	|		СвойстваОбъектов.Ссылка КАК Свойство,
	|		""Свойство.("" + СвойстваОбъектов.Код + "") "" + СвойстваОбъектов.Наименование КАК Наименование,
	|		СвойстваОбъектов.ТипЗначения КАК ТипЗначенияСвойства
	|	ИЗ
	|		ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
	|	ГДЕ
	|		СвойстваОбъектов.ПометкаУдаления = ЛОЖЬ
	|		И СвойстваОбъектов.Ссылка = " + ЗначениеСвойства + ")
	|ПОЛЕКЛЮЧА Свойство
	|ПОЛЕИМЕНИ Наименование
	|ПОЛЕТИПАЗНАЧЕНИЯ ТипЗначенияСвойства
	|ЗНАЧЕНИЯХАРАКТЕРИСТИК РегистрСведений.ЗначенияСвойствОбъектов
	|ПОЛЕОБЪЕКТА Объект
	|ПОЛЕВИДА Свойство
	|ПОЛЕЗНАЧЕНИЯ Значение }";
	
	Возврат Результат;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С КОМПОНОВЩИКОМ ДАННЫХ //////////////////////////////////////

Функция КомпоновщикПолучитьПолеКомпоновки(ПолеКомопоновки) Экспорт
	
	Результат = Неопределено;
	Если ТипЗнч(ПолеКомопоновки) = Тип("Строка") Тогда
		Результат = Новый ПолеКомпоновкиДанных(ПолеКомопоновки);
	ИначеЕсли ТипЗнч(ПолеКомопоновки) = Тип("ПолеКомпоновкиДанных") Тогда
		Результат = ПолеКомопоновки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция КомпоновщикПолучитьНастройки(Знач КомпоновщикНастроек) Экспорт
	
	Возврат КомпоновщикНастроек.ПолучитьНастройки();
	
КонецФункции

Функция КомпоновщикПолучитьДоступноеПоле(Знач ПолеПоиска, НастройкиКомпоновщика, ВВыборке = Истина, ВГруппировке = Истина, ВОтборе = Истина, ВПорядке = Истина) Экспорт
	
	Результат      = Неопределено;
	ПолеКомпоновки = КомпоновщикПолучитьПолеКомпоновки(ПолеПоиска);
	
	Если ВВыборке Тогда
		Результат = НастройкиКомпоновщика.ДоступныеПоляВыбора.НайтиПоле(ПолеКомпоновки);
	КонецЕсли;
	
	Если Результат = Неопределено И ВГруппировке Тогда
		Результат = НастройкиКомпоновщика.ДоступныеПоляГруппировок.НайтиПоле(ПолеКомпоновки);
	КонецЕсли;
	
	Если Результат = Неопределено И ВОтборе Тогда
		Результат = НастройкиКомпоновщика.ДоступныеПоляОтбора.НайтиПоле(ПолеКомпоновки);
	КонецЕсли;
	
	Если Результат = Неопределено И ВПорядке Тогда
		Результат = НастройкиКомпоновщика.ДоступныеПоляПорядка.НайтиПоле(ПолеКомпоновки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция КомпоновщикЕстьПолеВСтруктуре(Знач ПолеПоиска, СтруктураОтчета) Экспорт
	
	ПолеПоиска = КомпоновщикПолучитьПолеКомпоновки(ПолеПоиска);
	
	Результат = Ложь;
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	Для Каждого ТекЭлемент Из СтруктураОтчета Цикл
		Если Не ТекЭлемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипСтруктурыТаблицы Тогда
			Для Каждого Строка Из ТекЭлемент.Строки Цикл
				Если Не Строка.Использование Тогда
					Продолжить;
				КонецЕсли;
				Если ПоискВГруппировке(ПолеПоиска, Строка) ИЛИ КомпоновщикЕстьПолеВСтруктуре(ПолеПоиска, Строка.Структура) Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
			Для Каждого Колонка Из ТекЭлемент.Колонки Цикл
				Если Не Колонка.Использование Тогда
					Продолжить;
				КонецЕсли;
				Если ПоискВГруппировке(ПолеПоиска, Колонка) ИЛИ КомпоновщикЕстьПолеВСтруктуре(ПолеПоиска, Колонка.Структура) Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТипЭлемента = ТипСтруктурыДиаграммы Тогда
			Для Каждого Серия Из ТекЭлемент.Серии Цикл
				Если Не Серия.Использование Тогда
					Продолжить;
				КонецЕсли;
				Если ПоискВГруппировке(ПолеПоиска, Серия) ИЛИ КомпоновщикЕстьПолеВСтруктуре(ПолеПоиска, Серия.Структура) Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
			Для Каждого Точка Из ТекЭлемент.Точки Цикл
				Если Не Точка.Использование Тогда
					Продолжить;
				КонецЕсли;
				Если ПоискВГруппировке(ПолеПоиска, Точка) ИЛИ КомпоновщикЕстьПолеВСтруктуре(ПолеПоиска, Точка.Структура) Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если ПоискВГруппировке(ПолеПоиска, ТекЭлемент) ИЛИ КомпоновщикЕстьПолеВСтруктуре(ПолеПоиска, ТекЭлемент.Структура) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // КомпоновщикЕстьПолеВСтруктуре()

Функция КомпоновщикСчетчикУсловийПоля(ПолеПоиска, ОтборКомпоновщика, ТипГруппы = Неопределено) Экспорт
	
	Счетчик = 0;
	Для Каждого ТекОтбор Из ОтборКомпоновщика.Элементы Цикл
		Если Не ТекОтбор.Использование Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Счетчик = Счетчик + КомпоновщикСчетчикУсловийПоля(ПолеПоиска, ТекОтбор, ТекОтбор.ТипГруппы);
		ИначеЕсли ТекОтбор.ЛевоеЗначение = ПолеПоиска Тогда
			Если ТипГруппы = Неопределено И ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				Счетчик = Счетчик + 1;
			Иначе
				Счетчик = Счетчик + 2;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Счетчик;
	
КонецФункции

Функция КомпоновщикПолучитьЗначениеПараметра(Знач ПараметрКомпоновки, Настройки) Экспорт
	
	Результат = Неопределено;
	
	Если НЕ ТипЗнч(ПараметрКомпоновки) = Тип("ПараметрКомпоновкиДанных") Тогда
		ПараметрКомпоновки = Новый ПараметрКомпоновкиДанных(СокрЛП(ПараметрКомпоновки));
	КонецЕсли;
	
	ЗначениеПараметра = Настройки.ПараметрыДанных.Элементы.Найти(ПараметрКомпоновки);
	
	Если ЗначениеПараметра = Неопределено Тогда
		Возврат Результат;
	ИначеЕсли ТипЗнч(ЗначениеПараметра.Значение) = Тип("СтандартныйПериод") Тогда
		Результат = Новый Структура;
		Результат.Вставить("ДатаНачала",    ЗначениеПараметра.Значение.ДатаНачала);
		Результат.Вставить("ДатаОкончания", ЗначениеПараметра.Значение.ДатаОкончания);
	ИначеЕсли ТипЗнч(ЗначениеПараметра.Значение) = Тип("СтандартнаяДатаНачала") Тогда
		Результат = ЗначениеПараметра.Значение.Дата;
	ИначеЕсли ТипЗнч(ЗначениеПараметра.Значение) = Тип("СписокЗначений") И ЗначениеПараметра.Значение.Количество()=1 Тогда
		Результат = ЗначениеПараметра.Значение[0].Значение;
	ИначеЕсли ТипЗнч(ЗначениеПараметра.Значение) = Тип("Граница") Тогда
		Результат = ЗначениеПараметра.Значение.Значение;
	Иначе
		Результат = ЗначениеПараметра.Значение;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьПолеВПоследнююГруппировку(ПолеДобавления, СтруктураОтчета) Экспорт
	
	Если СтруктураОтчета.Количество() = 0 Тогда
		
		НоваяГруппировка = СтруктураОтчета.Добавить();
		НоваяГруппировка.Использование = Истина;
		
		ПолеГруппировки = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Использование  = Истина;
		ПолеГруппировки.Поле           = ПолеДобавления;
		ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
		
		НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		НовоеПоле.Использование = Истина;
		
		НовоеПоле = НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		НовоеПоле.Использование = Истина;
		
		Возврат НоваяГруппировка;
		
	КонецЕсли;
	
	Результат = Неопределено;
	
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	Для Каждого ТекЭлемент Из СтруктураОтчета Цикл
		Если Не ТекЭлемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипСтруктурыТаблицы Тогда
			
			ПоследняяСтрока = Неопределено;
			Для Каждого Строка Из ТекЭлемент.Строки Цикл
				Если Не Строка.Использование Тогда
					Продолжить;
				КонецЕсли;
				ПоследняяСтрока = Строка;
			КонецЦикла;
			
			Если НЕ ПоследняяСтрока = Неопределено Тогда
				Результат = ДобавитьПолеВПоследнююГруппировку(ПолеДобавления, ПоследняяСтрока.Структура);
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = ТипСтруктурыДиаграммы Тогда
			ПоследняяСтрока = Неопределено;
			Для Каждого Строка Из ТекЭлемент.Серии Цикл
				Если Не Строка.Использование Тогда
					Продолжить;
				КонецЕсли;
				ПоследняяСтрока = Строка;
			КонецЦикла;
			
			Если НЕ ПоследняяСтрока = Неопределено Тогда
				Результат = ДобавитьПолеВПоследнююГруппировку(ПолеДобавления, ПоследняяСтрока.Структура);
			КонецЕсли;
		Иначе
			Результат = ДобавитьПолеВПоследнююГруппировку(ПолеДобавления, ТекЭлемент.Структура);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура КомпоновщикУстановитьОтборПоПолямГруппировки(Группировка, Поля) Экспорт
	
	Элементы = Группировка.ПоляГруппировки.Элементы;
	Для Каждого ТекГруппировка Из Элементы Цикл
		Если Не ТекГруппировка.Использование Тогда
			Продолжить;
		КонецЕсли;
		ПутьКДанным = СокрЛП(ТекГруппировка.Поле);
		Для Каждого ТекПоле Из Поля Цикл
			Если Найти(ПутьКДанным, ТекПоле) = 1 Тогда
				НовыйОтбор = Неопределено;
				Для Каждого ТекОтбор Из Группировка.Отбор.Элементы Цикл
					Если ТекОтбор.ЛевоеЗначение = ТекГруппировка.Поле И 
						ТекОтбор.ВидСравнения  = ВидСравненияКомпоновкиДанных.Заполнено Тогда
						НовыйОтбор = ТекОтбор;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если НовыйОтбор = Неопределено Тогда 
					НовыйОтбор = Группировка.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					НовыйОтбор.ЛевоеЗначение = ТекГруппировка.Поле;
					НовыйОтбор.ВидСравнения  = ВидСравненияКомпоновкиДанных.Заполнено;
				КонецЕсли;
				НовыйОтбор.Использование = Истина;
				//НовыйОтбор.Применение    = ТипПримененияОтбораКомпоновкиДанных.Иерархия;
				ПараметрОтображения = Группировка.ПараметрыВывода.Элементы.Найти("ВыводитьОтбор");
				Если Не ПараметрОтображения = Неопределено Тогда
					ПараметрОтображения.Значение      = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
					ПараметрОтображения.Использование = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция КомпоновщикУстановитьОтбор(Настройки, Знач ПолеКомпоновки, ЗначениеОтбора = Неопределено, ВидСравнения = Неопределено) Экспорт
	
	НовыйОтбор     = Неопределено;
	ПолеКомпоновки = КомпоновщикПолучитьПолеКомпоновки(ПолеКомпоновки);
	Отборы         = Настройки.Отбор;
	НастройкиБезСхемы = Настройки.ДоступныеПоляВыбора.Элементы.Количество()=0;
	Если НастройкиБезСхемы ИЛИ (НЕ КомпоновщикПолучитьДоступноеПоле(ПолеКомпоновки, Настройки, Ложь, Ложь, Истина, Ложь) = Неопределено) Тогда
		Для Каждого ТекОтбор Из Отборы.Элементы Цикл
			Если ТекОтбор.ЛевоеЗначение = ПолеКомпоновки Тогда
				НовыйОтбор = ТекОтбор;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НовыйОтбор = Неопределено Тогда
			НовыйОтбор = Отборы.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйОтбор.ЛевоеЗначение = ПолеКомпоновки;
		КонецЕсли;
		
		НовыйОтбор.Использование  = Истина;
		Если ВидСравнения = Неопределено Тогда
			НовыйОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Иначе
			НовыйОтбор.ВидСравнения = ВидСравнения;
		КонецЕсли;
		
		Если НЕ ЗначениеОтбора = Неопределено Тогда
			НовыйОтбор.ПравоеЗначение = ЗначениеОтбора;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НовыйОтбор;
	
КонецФункции

Процедура КомпоновщикУстановитьОтборПустыхГруппировок(СтруктураОтчета, Поля) Экспорт
	
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	Для Каждого ТекЭлемент Из СтруктураОтчета Цикл
		
		Если Не ТекЭлемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипСтруктурыТаблицы Тогда
			Для Каждого Строка Из ТекЭлемент.Строки Цикл
				Если Не Строка.Использование Тогда
					Продолжить;
				КонецЕсли;
				КомпоновщикУстановитьОтборПоПолямГруппировки(Строка, Поля);
				КомпоновщикУстановитьОтборПустыхГруппировок(Строка.Структура, Поля);
			КонецЦикла;
			Для Каждого Колонка Из ТекЭлемент.Колонки Цикл
				Если Не Колонка.Использование Тогда
					Продолжить;
				КонецЕсли;
				КомпоновщикУстановитьОтборПоПолямГруппировки(Колонка, Поля);
				КомпоновщикУстановитьОтборПустыхГруппировок(Колонка.Структура, Поля);
			КонецЦикла;
		ИначеЕсли ТипЭлемента = ТипСтруктурыДиаграммы Тогда
			Для Каждого Серия Из ТекЭлемент.Серии Цикл
				Если Не Серия.Использование Тогда
					Продолжить;
				КонецЕсли;
				КомпоновщикУстановитьОтборПоПолямГруппировки(Серия, Поля);
				КомпоновщикУстановитьОтборПустыхГруппировок(Серия.Структура, Поля);
			КонецЦикла;
			Для Каждого Точка Из ТекЭлемент.Точки Цикл
				Если Не Точка.Использование Тогда
					Продолжить;
				КонецЕсли;
				КомпоновщикУстановитьОтборПоПолямГруппировки(Точка, Поля);
				КомпоновщикУстановитьОтборПустыхГруппировок(Точка.Структура, Поля);
			КонецЦикла;
		Иначе
			КомпоновщикУстановитьОтборПоПолямГруппировки(ТекЭлемент, Поля);
			КомпоновщикУстановитьОтборПустыхГруппировок(ТекЭлемент.Структура, Поля);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СКД_ДобавитьГруппировку(СтруктураОтчета, Поле) Экспорт
	
	ПолеКомпоновки = КомпоновщикПолучитьПолеКомпоновки(Поле);
	
	НоваяГруппировка = СтруктураОтчета.Добавить();
	НоваяГруппировка.Использование = Истина;
	
	Группировка = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	Группировка.Использование  = Истина;
	Группировка.Поле           = ПолеКомпоновки;
	Группировка.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
	
	НовоеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НовоеПоле.Использование = Истина;
	
	НовоеПоле = НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	НовоеПоле.Использование = Истина;
	
	Возврат НоваяГруппировка;
	
КонецФункции

Функция КомпоновщикНайтиОтбор(Знач ПолеПоиска, Отборы, ВИерархии = Ложь, ПоискВГруппе = Ложь) Экспорт
	
	Если НЕ ПоискВГруппе Тогда
		ПолеПоиска = КомпоновщикПолучитьПолеКомпоновки(ПолеПоиска);
	КонецЕсли;
	
	РезультатПоиска = Неопределено;
	Для Каждого ТекОтбор Из Отборы.Элементы Цикл
		
		Если ТипЗнч(ТекОтбор) = Тип("ЭлементОтбораКомпоновкиДанных") И ТекОтбор.ЛевоеЗначение = ПолеПоиска Тогда
			РезультатПоиска = ТекОтбор;
		ИначеЕсли ВИерархии И ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			РезультатПоиска = КомпоновщикНайтиОтбор(ПолеПоиска, ТекОтбор, Истина, Истина);
		КонецЕсли;
		
		Если НЕ РезультатПоиска = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатПоиска;
	
КонецФункции

Процедура УстановитьПорядокПоУмолчанию(Настройки, СтрокаГруппировки) Экспорт
	
	ПолеГруппировки      = СтрокаГруппировки.Поле;
	ЗаголовокГруппировки = СтрокаГруппировки.Заголовок;
	
	СписокСортировки = Новый СписокЗначений;
	
	ПолеПорядка = Настройки.ДоступныеПоляПорядка.НайтиПоле(ПолеГруппировки);
	Если НЕ ПолеПорядка = Неопределено И ПолеПорядка.Элементы.Количество()>0 Тогда
		ПолеСортировки = Новый ПолеКомпоновкиДанных(СокрЛП(ПолеПорядка.Поле)+".Наименование");
		ДоступныеДанные = ПолеПорядка.Элементы.Найти(ПолеСортировки);
		Если НЕ ДоступныеДанные = Неопределено Тогда
			СписокСортировки.Добавить(ПолеСортировки, СтрЗаменить(ДоступныеДанные.Заголовок, ЗаголовокГруппировки+".", ""));
		КонецЕсли;
	КонецЕсли;
	
	Если СписокСортировки.Количество() = 0 Тогда
		ПредставлениеПорядка = "Авто";
	Иначе
		ПредставлениеПорядка = "";
		Для Каждого ТекСтрока Из СписокСортировки Цикл
			НоваяСтрока = СтрокаГруппировки.Порядок.Добавить();
			НоваяСтрока.Поле              = ТекСтрока.Значение;
			НоваяСтрока.Заголовок         = ТекСтрока.Представление;
			НоваяСтрока.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
			
			ПредставлениеПорядка  = ПредставлениеПорядка + ?(ПредставлениеПорядка = "", "", ", ") + НоваяСтрока.Заголовок + " Возр";
		КонецЦикла;
	КонецЕсли;
	
	СтрокаГруппировки.ПредставлениеПорядка = ПредставлениеПорядка;
	
КонецПроцедуры

Функция ОтборПоСписку(ВидСравненияОтбора) Экспорт
	
	ТекРезультат = Ложь;
	Если ВидСравненияОтбора = ВидСравненияКомпоновкиДанных.ВСписке ИЛИ 
		ВидСравненияОтбора = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии ИЛИ
		ВидСравненияОтбора = ВидСравненияКомпоновкиДанных.НеВСписке ИЛИ 
		ВидСравненияОтбора= ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		ТекРезультат = Истина;
	КонецЕсли;
	
	Возврат ТекРезультат;
	
КонецФункции

// Процедура добавления значения к списку владельцев
//
// Параметры:
//	ТекЗначение      - Ссылка         - Ссылка на объект
//	СписокВладельцев - СписокЗначений - Список владельцев 
//	ИмяОбъекта       - Строка         - Имя объекта
//	ТипВладельца     - Произвольный   - Тип владельца
//
Процедура ДобавитьЗначениеКСпискуВладельцевПоИерархии(ТекЗначение, СписокВладельцев, ИмяОбъекта, ТипВладельца)
	
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипВладельца) Тогда
		ВидМетаданных = "Справочник";
	ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипВладельца) Тогда
		ВидМетаданных = "ПланыВидовХарактеристик";
	ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ТипВладельца) Тогда
		ВидМетаданных = "ПланыВидовРасчета";
	Иначе
		Возврат;
	КонецЕсли;
	
	Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка КАК ТекЭлемент
	|ИЗ
	|	"+ВидМетаданных+"."+ИмяОбъекта+" КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В ИЕРАРХИИ(&Ссылка)
	|	И Таблица.ЭтоГруппа = ЛОЖЬ";
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ТекЗначение);
	Запрос.Текст = Текст;
	Выборка = Запрос.Выполнить().Выгрузить();
	Для Каждого ТекСтрока Из Выборка Цикл
		СписокВладельцев.Добавить(ТекСтрока.ТекЭлемент);
	КонецЦикла;
	
КонецПроцедуры // ДобавитьЗначениеКСпискуВладельцевПоИерархии()

Процедура ЗаполнитьСписокВладельцев(ЭлементыОтбора, ДоступныеПоляОтбора, МетаданныеЗначения, ЭтоХарактеристика, ЭтоЕдиницаИзмерения, СписокВладельцев)
	
	ТипСсылкиНоменклатуры     = Тип("СправочникСсылка.Номенклатура");
	ТипСсылкиТипаНоменклатуры = Тип("СправочникСсылка.ТипыНоменклатуры");
	
	Для Каждого ТекОтбор Из ЭлементыОтбора Цикл
		
		Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ЗаполнитьСписокВладельцев(ТекОтбор.Элементы, ДоступныеПоляОтбора, МетаданныеЗначения, ЭтоХарактеристика, ЭтоЕдиницаИзмерения, СписокВладельцев);
			
		КонецЕсли;
		
		ДоступноеПоле = ДоступныеПоляОтбора.НайтиПоле(ТекОтбор.ЛевоеЗначение);
		
		Если ДоступноеПоле = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		МассивТипов = ДоступноеПоле.ТипЗначения.Типы();
		
		СписокДоступныхТипов = Новый СписокЗначений;
		Для Каждого ТекТип Из МассивТипов Цикл
			
			МетаданныеВладельца = Метаданные.НайтиПоТипу(ТекТип);
			Если МетаданныеВладельца = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если МетаданныеЗначения.Владельцы.Содержит(МетаданныеВладельца) Тогда
				СписокДоступныхТипов.Добавить(ТекТип);
			КонецЕсли;
			
		КонецЦикла;
		
		Если СписокДоступныхТипов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ТекОтбор.ПравоеЗначение) = Тип("СписокЗначений") Тогда
			
			Для Каждого ТекЗначениеВладельца Из ТекОтбор.ПравоеЗначение Цикл
				
				ТекЗначение    = ТекЗначениеВладельца.Значение;
				ТекТипЗначения = ТипЗнч(ТекЗначение);
				
				Если СписокДоступныхТипов.НайтиПоЗначению(ТекТипЗначения) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если ЭтоХарактеристика Тогда
					//1 - Характеристики подчинены данному типу номенклатуры,
					//2 - Характеристики подчинены номенклатурным позициям, 
					//3 (и любой другой) - Учет по характеристикам не ведется
					Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
						Если ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик = 1 Тогда
							ТекЗначение = ТекЗначение.ТипНоменклатуры;
						ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик > 2 Тогда
							Продолжить;
						КонецЕсли;
					ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
						Если ТекЗначение.ИспользованиеХарактеристик > 1 Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ЭтоЕдиницаИзмерения Тогда
					//1 - Единицы измерения подчинены данному типу номенклатуры,
					//2 - Единицы подчинены номенклатурным позициям
					Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
						Если ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
							ТекЗначение = ТекЗначение.ТипНоменклатуры;
						ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения > 2 Тогда
							Продолжить;
						КонецЕсли;
					ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
						Если ТекЗначение.ИспользованиеЕдиницИзмерения > 1 Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ЭтоГруппа = Ложь;
				Попытка
					ЭтоГруппа = ТекЗначение.ЭтоГруппа;
				Исключение
				КонецПопытки;
				
				Если ЭтоГруппа Тогда
					ДобавитьЗначениеКСпискуВладельцевПоИерархии(ТекЗначение, СписокВладельцев, ТекЗначение.Метаданные().Имя, ТипЗнч(ТекЗначение));
				Иначе
					Если СписокВладельцев.НайтиПоЗначению(ТекЗначение) = Неопределено Тогда
						СписокВладельцев.Добавить(ТекЗначение);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			ТекЗначение    = ТекОтбор.ПравоеЗначение;
			ТекТипЗначения = ТипЗнч(ТекЗначение);
			
			Если СписокДоступныхТипов.НайтиПоЗначению(ТекТипЗначения) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭтоХарактеристика Тогда
				//1 - Характеристики подчинены данному типу номенклатуры,
				//2 - Характеристики подчинены номенклатурным позициям, 
				//3 (и любой другой) - Учет по характеристикам не ведется
				Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
					Если ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик = 1 Тогда
						ТекЗначение = ТекЗначение.ТипНоменклатуры;
					ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеХарактеристик > 2 Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
					Если ТекЗначение.ИспользованиеХарактеристик > 1 Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ЭтоЕдиницаИзмерения Тогда
				//1 - Единицы измерения подчинены данному типу номенклатуры,
				//2 - Единицы подчинены номенклатурным позициям
				Если ТекТипЗначения = ТипСсылкиНоменклатуры Тогда
					Если ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
						ТекЗначение = ТекЗначение.ТипНоменклатуры;
					ИначеЕсли ТекЗначение.ТипНоменклатуры.ИспользованиеЕдиницИзмерения > 2 Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли ТекТипЗначения = ТипСсылкиТипаНоменклатуры Тогда
					Если ТекЗначение.ИспользованиеЕдиницИзмерения > 1 Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ЭтоГруппа = Ложь;
			Попытка
				ЭтоГруппа = ТекЗначение.ЭтоГруппа;
			Исключение
			КонецПопытки;
			
			Если ЭтоГруппа Тогда
				ДобавитьЗначениеКСпискуВладельцевПоИерархии(ТекЗначение, СписокВладельцев, ТекЗначение.Метаданные().Имя, ТипЗнч(ТекЗначение));
			Иначе
				Если СписокВладельцев.НайтиПоЗначению(ТекЗначение) = Неопределено Тогда
					СписокВладельцев.Добавить(ТекЗначение);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСписокВладельцев(Настройки, ТипЗначенияОтбора) Экспорт
	
	СписокВладельцев = Новый СписокЗначений;
	
	МетаданныеЗначения = Метаданные.НайтиПоТипу(ТипЗначенияОтбора);
	
	Если МетаданныеЗначения = Неопределено Тогда
		Возврат СписокВладельцев;
	КонецЕсли;
	
	Попытка
		Если МетаданныеЗначения.Владельцы.Количество() = 0 Тогда
			Возврат СписокВладельцев;
		КонецЕсли;
	Исключение
		Возврат СписокВладельцев;
	КонецПопытки;
	
	ЭтоХарактеристика   = (ТипЗначенияОтбора = Тип("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ЭтоЕдиницаИзмерения = (ТипЗначенияОтбора = Тип("СправочникСсылка.ЕдиницыИзмерения"));
	
	ЗаполнитьСписокВладельцев(Настройки.Отбор.Элементы, Настройки.ДоступныеПоляОтбора, МетаданныеЗначения, ЭтоХарактеристика, ЭтоЕдиницаИзмерения, СписокВладельцев);
	
	Возврат СписокВладельцев;
	
КонецФункции

Функция ПолучитьВладельцаЗначенияСвойств(ПолеВладельца) Экспорт
	
	Результат = ПланыВидовХарактеристик.СвойстваОбъектов.ПустаяСсылка();
	
	ПредставлениеВладельца = СокрЛП(ПолеВладельца);
	
	ПозицияКода = Найти(ПредставлениеВладельца, ".[Свойство.(");
	ПредставлениеВладельца = Сред(ПредставлениеВладельца, ПозицияКода+12);
	
	ПозицияНаименования = Найти(ПредставлениеВладельца, ")");
	
	КодСвойства          = СокрЛП(Лев(ПредставлениеВладельца, ПозицияНаименования-1));
	НаименованиеСвойства = СокрЛП(Сред(ПредставлениеВладельца, ПозицияНаименования+1, СтрДлина(ПредставлениеВладельца)-ПозицияНаименования-1));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СвойстваОбъектов.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
	|ГДЕ
	|	СвойстваОбъектов.Код = &Код И 
	|	СвойстваОбъектов.Наименование = &Наименование И 
	|	СвойстваОбъектов.ПометкаУдаления = ЛОЖЬ
	|";
	Запрос.УстановитьПараметр("Код",          КодСвойства);
	Запрос.УстановитьПараметр("Наименование", НаименованиеСвойства);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////// ПОЛУЧЕНИЕ ТЕКСТА ЗАПРОСА ИЗ НАСТРОЕК КОМПОНОВКИ /////////////////////////////////////////

Функция КомпоновщикПолучитьТекстОтбораЗаполнено(ТипПоля, ПутьКДанным, Заполнено = Истина) Экспорт
	
	Если ТипЗнч(ТипПоля) = Тип("ОписаниеТипов") Тогда
		ТипыПоля = ТипПоля.Типы();
	ИначеЕсли ТипЗнч(ТипПоля) = Тип("Массив") Тогда
		ТипыПоля = ТипПоля;
	Иначе
		ТипыПоля = Новый Массив;
		ТипыПоля.Добавить(ТипПоля);
	КонецЕсли;
	
	ТекстУсловия = "";
	Для Каждого ТекущийТип Из ТипыПоля Цикл
		
		ПустоеЗначение = """";
		Если ТекущийТип      = Тип("Строка") Тогда
			ПустоеЗначение = """";
		ИначеЕсли ТекущийТип = Тип("Число") Тогда
			ПустоеЗначение = "0";
		ИначеЕсли ТекущийТип = Тип("Дата") Тогда
			ПустоеЗначение = "ДАТАВРЕМЯ(1,1,1)";
		ИначеЕсли ТекущийТип = Тип("Булево") Тогда
			ПустоеЗначение = "ЛОЖЬ";
		Иначе
			ПустоеЗначение = "ЗНАЧЕНИЕ(" + Метаданные.НайтиПоТипу(ТекущийТип).ПолноеИмя() + ".ПустаяСсылка)";
		КонецЕсли;
		ТекстУсловия = ТекстУсловия + ?(ТекстУсловия = "", "", " ИЛИ " + Символы.ПС) + ПутьКДанным + " = " + ПустоеЗначение;
		
	КонецЦикла;
	
	Если ТипыПоля.Количество() > 1 Тогда
		ТекстУсловия = ТекстУсловия + " ИЛИ " + Символы.ПС + ПутьКДанным + " = Неопределено";
	КонецЕсли;
	
	Если Заполнено Тогда
		ТекстУсловия = "НЕ ("+ТекстУсловия + ?(ТекстУсловия = "", "", " ИЛИ " + Символы.ПС) + ПутьКДанным + " ЕСТЬ NULL)";
	Иначе
		ТекстУсловия = ТекстУсловия + ?(ТекстУсловия = "", "", " ИЛИ " + Символы.ПС) + ПутьКДанным + " ЕСТЬ NULL";
	КонецЕсли;
	
	Возврат ТекстУсловия;
	
КонецФункции // КомпоновщикПолучитьТекстОтбораЗаполнено()

Функция КомпоновщикПолучитьТекстСравнения(ДоступныеПоляОтбора, ТекОтбор, Параметры, ПутьКДанным) Экспорт
	
	ИмяПараметра = "Параметр" + Параметры.Количество();
	ТипПараметра = 1;
	ТекстУсловия = "";
	
	Если ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше Тогда
		ТекстУсловия = ПутьКДанным+">&"+ИмяПараметра;
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
		ТекстУсловия = ПутьКДанным+">=&"+ИмяПараметра;
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии Тогда
		ТекстУсловия = ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+")";	
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
		ТекстУсловия = ПутьКДанным+" В (&"+ИмяПараметра+")";
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
		ТекстУсловия = ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+")";
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено Тогда
		ДоступноеПолеОтбора = ДоступныеПоляОтбора.НайтиПоле(ТекОтбор.ЛевоеЗначение);
		ТекстУсловия = КомпоновщикПолучитьТекстОтбораЗаполнено(ДоступноеПолеОтбора.ТипЗначения.Типы(), ПутьКДанным, Истина);
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше Тогда
		ТекстУсловия = ПутьКДанным+"<&"+ИмяПараметра;
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
		ТекстУсловия = ПутьКДанным+"<=&"+ИмяПараметра;
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
		ТекстУсловия = "(НЕ "+ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+"))";
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке Тогда
		ТекстУсловия = "(НЕ "+ПутьКДанным+" В (&"+ИмяПараметра+"))";
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		ТекстУсловия = "(НЕ "+ПутьКДанным+" В ИЕРАРХИИ (&"+ИмяПараметра+"))";
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
		ДоступноеПолеОтбора = ДоступныеПоляОтбора.НайтиПоле(ТекОтбор.ЛевоеЗначение);
		ТекстУсловия = КомпоновщикПолучитьТекстОтбораЗаполнено(ДоступноеПолеОтбора.ТипЗначения.Типы(), ПутьКДанным, Ложь);
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно Тогда
		ТекстУсловия = ПутьКДанным+" <> &"+ИмяПараметра;
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеСодержит Тогда
		ТекстУсловия = "(НЕ "+ПутьКДанным+" ПОДОБНО &"+ИмяПараметра+")";
		ТипПараметра  = 2;
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
		ТекстУсловия = ПутьКДанным+" = &"+ИмяПараметра;
	ИначеЕсли ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит Тогда
		ТекстУсловия = ПутьКДанным+" ПОДОБНО &"+ИмяПараметра;
		ТипПараметра  = 2;
	КонецЕсли;
	
	Если ТипПараметра=1 Тогда
		Если ТекОтбор.ПравоеЗначение = Неопределено Тогда
			ДоступноеПолеОтбора = ДоступныеПоляОтбора.НайтиПоле(ТекОтбор.ЛевоеЗначение);
			ТипПоля = ДоступноеПолеОтбора.ТипЗначения.Типы();
			Если ТипПоля.Количество() = 0 Тогда
				Значение = Неопределено;
			Иначе
				ПредставлениеСвойства = ПолучитьСтрокуПредставленияТипа(ДоступноеПолеОтбора.ТипЗначения);
				Если ПредставлениеСвойства="Строка" Тогда
					Значение = "";
				ИначеЕсли ПредставлениеСвойства="Число" Тогда
					Значение = 0;
				ИначеЕсли ПредставлениеСвойства="Дата" Тогда
					Значение = Дата(1,1,1);
				ИначеЕсли ПредставлениеСвойства="Булево" Тогда
					Значение = Ложь;
				Иначе
					Запрос = Новый Запрос;
					Запрос.Текст = "
					|ВЫБРАТЬ
					|	ЗНАЧЕНИЕ("+ПредставлениеСвойства+".ПустаяСсылка) КАК ПустоеЗначение";
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						Значение = Выборка.ПустоеЗначение;
					Иначе
						Значение = Неопределено;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ТекОтбор.ПравоеЗначение) = Тип("СтандартнаяДатаНачала") Тогда
			Значение = ТекОтбор.ПравоеЗначение.Дата;
		Иначе
			Значение = ТекОтбор.ПравоеЗначение;
		КонецЕсли;
		Параметры.Вставить(ИмяПараметра, Значение);
	ИначеЕсли ТипПараметра=2 Тогда
		Параметры.Вставить(ИмяПараметра, "%"+Строка(ТекОтбор.ПравоеЗначение)+"%");
	КонецЕсли;
	
	Возврат ТекстУсловия;
	
КонецФункции // КомпоновщикПолучитьТекстСравнения()

// функция возвращает текст отбора
//Параметры:
// ЭлементыОтбора      - Список отборов настроки компоновщика данных
// ДоступныеПоляОтбора - Список доступных полей для отбора
// ИмяТаблицыЗапроса   - Строка имени таблицы в запросе, для которой нужно получить отбор
// Параметры           - Структура, в которую помещаются значения параметров отбора
// СоответствиеЗамен   - Соответствие, содержит текст замены реального выражения в запросе 
//			для определенного поля компоновщика. Например: замена для поля Организация будет Склад.Организация.
//			ключем соответствия будет или поле компоновки или имя поле в виде строки.
// ПоляОграничения - массив или строка или поле компоновки. Список полей, для которых 
//			нужно получить отбор, все остальные поля в текст запроса не попадут.
// ПоляИсключения - массив или строка или поле компоновки. Список полей, которые 
//			нужно исключить из текст отбора.
//
Функция КомпоновщикПолучитьТекстОтбора(ЭлементыОтбора, ДоступныеПоляОтбора, ИмяТаблицыЗапроса, Параметры, СоответствиеЗамен = Неопределено, ПоляОграничения = Неопределено, ПоляИсключения = Неопределено, Союз = Неопределено, Уровень = 0) Экспорт
	
	Если Союз = Неопределено Тогда
		Союз = " И ";
	КонецЕсли;
	
	УдалитьТаблицу = Ложь;
	Если Уровень = 0 Тогда
		
		Замены = Новый Соответствие;
		Если ТипЗнч(СоответствиеЗамен) = Тип("Соответствие") Тогда
			Для Каждого ТекПоле ИЗ СоответствиеЗамен Цикл
				НовыйКлюч = СокрЛП(ТекПоле.Ключ);
				Замены.Вставить(НовыйКлюч, ТекПоле.Значение);
			КонецЦикла;
		КонецЕсли;
		
		Ограничения = Новый Массив;
		Если ТипЗнч(ПоляОграничения) = Тип("Массив") Тогда
			Для Каждого ТекПоле Из ПоляОграничения Цикл
				Ограничения.Добавить(СокрЛП(ТекПоле));
			КонецЦикла;
		ИначеЕсли НЕ ПоляОграничения = Неопределено Тогда
			Ограничения.Добавить(СокрЛП(ПоляОграничения));
		КонецЕсли;
		
		Исключения = Новый Массив;
		Если ТипЗнч(ПоляИсключения) = Тип("Массив") Тогда
			Для Каждого ТекПоле Из ПоляИсключения Цикл
				Исключения.Добавить(СокрЛП(ТекПоле));
			КонецЦикла;
		ИначеЕсли НЕ ПоляИсключения = Неопределено Тогда
			Исключения.Добавить(СокрЛП(ПоляИсключения));
		КонецЕсли;
		
		Если ИмяТаблицыЗапроса = "" Тогда
			ИмяТаблицыЗапроса = "ТаблицаУдаления";
			УдалитьТаблицу    = Истина;
		КонецЕсли;
		
	Иначе
		Замены      = СоответствиеЗамен;
		Ограничения = ПоляОграничения;
		Исключения  = ПоляИсключения;
	КонецЕсли;
	
	ЕстьОграничение = (Ограничения.Количество()>0);
	
	ТекстОтбора = "";
	Для Каждого ТекОтбор Из ЭлементыОтбора Цикл
		
		Если НЕ ТекОтбор.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ПредварительныйТекстОтбора = "";
			Если ТекОтбор.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ Тогда
				ПредварительныйТекстОтбора = "(" + КомпоновщикПолучитьТекстОтбора(ТекОтбор.Элементы, ДоступныеПоляОтбора, ИмяТаблицыЗапроса, Параметры, Замены, Ограничения, Исключения, " И ", Уровень + 1) + ")";
			ИначеЕсли ТекОтбор.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли Тогда
				ПредварительныйТекстОтбора = "(" + КомпоновщикПолучитьТекстОтбора(ТекОтбор.Элементы, ДоступныеПоляОтбора, ИмяТаблицыЗапроса, Параметры, Замены, Ограничения, Исключения, " ИЛИ ", Уровень + 1) + ")";
			ИначеЕсли ТекОтбор.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе Тогда
				ПредварительныйТекстОтбора = "НЕ (" + КомпоновщикПолучитьТекстОтбора(ТекОтбор.Элементы, ДоступныеПоляОтбора, ИмяТаблицыЗапроса, Параметры, Замены, Ограничения, Исключения, " ИЛИ ", Уровень + 1) + ")";
			КонецЕсли;
			
			Если Найти(ПредварительныйТекстОтбора, ИмяТаблицыЗапроса+".")>0 Тогда
				Если НЕ ТекстОтбора = "" Тогда
					ТекстОтбора = ТекстОтбора + Союз + Символы.ПС;
				КонецЕсли;
				ТекстОтбора = ТекстОтбора + ПредварительныйТекстОтбора;
			КонецЕсли;
			
		Иначе
			
			ИмяОтбора = СокрЛП(ТекОтбор.ЛевоеЗначение);
			
			// Свойства пропускаем
			Если Найти(ИмяОтбора, ".[Свойства")>0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Если отбор по реквизиту поля
			Позиция = Найти(ИмяОтбора, ".");
			Если Позиция>0 Тогда
				ИмяОтбора = Лев(ИмяОтбора, Позиция-1);
			КонецЕсли;
			
			Если (ИмяОтбора = "") ИЛИ (ТипЗнч(ТекОтбор.ПравоеЗначение) = Тип("СписокЗначений") И ТекОтбор.ПравоеЗначение.Количество()=0) Тогда
				ТекОтбор.Использование = Ложь;
			ИначеЕсли ЕстьОграничение И ПоискПоляВСтруктуре(ИмяОтбора, Ограничения) = Неопределено Тогда
				Продолжить;
			ИначеЕсли НЕ ПоискПоляВСтруктуре(ИмяОтбора, Исключения) = Неопределено Тогда
				Продолжить;
			Иначе
				ВыражениеПоля = ПоискПоляВСтруктуре(ИмяОтбора, Замены);
				ВыражениеПоля = СокрЛП(?(ВыражениеПоля = Неопределено, ИмяОтбора, ВыражениеПоля));
				// Если отбор по реквизиту поля, то добавим его после выражения замены
				Если Позиция>0 Тогда
					ВыражениеПоля = ВыражениеПоля + Сред(ТекОтбор.ЛевоеЗначение, Позиция);
				КонецЕсли;
				Если НЕ ТекстОтбора = "" Тогда
					ТекстОтбора = ТекстОтбора + Союз + Символы.ПС;
				КонецЕсли;
				ПутьКДанным = ИмяТаблицыЗапроса + "." + ВыражениеПоля;
				ТекстОтбора = ТекстОтбора + КомпоновщикПолучитьТекстСравнения(ДоступныеПоляОтбора, ТекОтбор, Параметры, ПутьКДанным);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если УдалитьТаблицу Тогда
		ТекстОтбора = СтрЗаменить(ТекстОтбора, "ТаблицаУдаления.", "");
	КонецЕсли;
	
	Возврат ТекстОтбора;
	
КонецФункции

Процедура ПолучитьУникальныеПоляВГруппировке(Группировка, УникальныеПоля)
	
	Элементы = Группировка.ПоляГруппировки.Элементы;
	Для Каждого ТекГруппировка Из Элементы Цикл
		Если Не ТекГруппировка.Использование Тогда
			Продолжить;
		КонецЕсли;
		ПутьКДанным = СокрЛП(ТекГруппировка.Поле);
		Если Найти(ПутьКДанным, ".") > 0 ИЛИ УникальныеПоля.Свойство(ПутьКДанным) Тогда
			Продолжить;
		КонецЕсли;
		УникальныеПоля.Вставить(ПутьКДанным);
	КонецЦикла;
	
КонецПроцедуры // ПолучитьУникальныеПоляВГруппировке()

Процедура КомпоновщикПолучитьПоляСтруктуры(СтруктураОтчета, УникальныеПоля = Неопределено) Экспорт
	
	Если УникальныеПоля = Неопределено Тогда
		УникальныеПоля = Новый Структура;
	КонецЕсли;
	
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	Для Каждого ТекЭлемент Из СтруктураОтчета Цикл
		
		Если Не ТекЭлемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипСтруктурыТаблицы Тогда
			Для Каждого Строка Из ТекЭлемент.Строки Цикл
				Если Не Строка.Использование Тогда
					Продолжить;
				КонецЕсли;
				ПолучитьУникальныеПоляВГруппировке(Строка, УникальныеПоля);
				КомпоновщикПолучитьПоляСтруктуры(Строка.Структура, УникальныеПоля);
			КонецЦикла;
			Для Каждого Колонка Из ТекЭлемент.Колонки Цикл
				Если Не Колонка.Использование Тогда
					Продолжить;
				КонецЕсли;
				ПолучитьУникальныеПоляВГруппировке(Колонка, УникальныеПоля);
				КомпоновщикПолучитьПоляСтруктуры(Колонка.Структура, УникальныеПоля);
			КонецЦикла;
		ИначеЕсли ТипЭлемента = ТипСтруктурыДиаграммы Тогда
			Для Каждого Серия Из ТекЭлемент.Серии Цикл
				Если Не Серия.Использование Тогда
					Продолжить;
				КонецЕсли;
				ПолучитьУникальныеПоляВГруппировке(Серия, УникальныеПоля);
				КомпоновщикПолучитьПоляСтруктуры(Серия.Структура, УникальныеПоля);
			КонецЦикла;
			Для Каждого Точка Из ТекЭлемент.Точки Цикл
				Если Не Точка.Использование Тогда
					Продолжить;
				КонецЕсли;
				ПолучитьУникальныеПоляВГруппировке(Точка, УникальныеПоля);
				КомпоновщикПолучитьПоляСтруктуры(Точка.Структура, УникальныеПоля);
			КонецЦикла;
		Иначе
			ПолучитьУникальныеПоляВГруппировке(ТекЭлемент, УникальныеПоля);
			КомпоновщикПолучитьПоляСтруктуры(ТекЭлемент.Структура, УникальныеПоля);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // КомпоновщикПолучитьПоляСтруктуры()

Функция ПроверитьКорректностьСтроки(Строка, ТаблицаПолей, НомерПериодаПоСтруктуре, ВложенныйНомерПериода, НомерОсновногоПериодаПоСтруктуре, ВложенныйНомерОсновногоПериода)
	
	Отказ = Ложь;
	
	ПолеИерархии          = Неопределено;
	ПревПоле              = Неопределено;
	НомерПериода          = 0;
	НомерОсновногоПериода = 0;
	Для Каждого ТекПолеГруппировки Из Строка.ПоляГруппировки.Элементы Цикл
		Если Не ТекПолеГруппировки.Использование ИЛИ (НЕ ТипЗнч(ТекПолеГруппировки) = Тип("ПолеГруппировкиКомпоновкиДанных")) Тогда
			Продолжить;
		КонецЕсли;
		
		ПолеПериода = ТаблицаПолей.Найти(ТекПолеГруппировки.Поле, "Поле");
		Если ПолеПериода = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия ИЛИ ТекПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия Тогда
			Если ПолеИерархии = Неопределено Тогда
				ПолеИерархии = ПолеПериода.Заголовок;
			Иначе
				Сообщение       = Новый СообщениеПользователю;
				Сообщение.Текст = "Иерархическая группировка одновременно по нескольким полям: <" + ПолеИерархии + "> и <"+ПолеПериода.Заголовок+"> не допустима!";
				Сообщение.Сообщить();
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ПолеПериода.НомерПериода>0 Тогда
			
			Если НЕ ПревПоле = Неопределено Тогда
				Сообщение       = Новый СообщениеПользователю;
				Сообщение.Текст = "Совместная группировка по периодам с другими выражениями запрещена! Поля: <" + ПревПоле.Заголовок + "> и <"+ПолеПериода.Заголовок+">.";
				Сообщение.Сообщить();
				Возврат Истина;
			КонецЕсли;
			
			НомерПериода = Макс(НомерПериода, ПолеПериода.НомерПериода);
			Если ПолеПериода.ТипПериода = ТипПериодаКомпоновкиДанных.Основной Тогда
				Если НомерОсновногоПериода = 0 Тогда
					НомерОсновногоПериода = ПолеПериода.НомерПериода;
				Иначе
					НомерОсновногоПериода = Мин(НомерОсновногоПериода, ПолеПериода.НомерПериода);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли НомерПериода>0 Тогда
			
			ВремПоле = ТаблицаПолей.Найти(НомерПериода, "НомерПериода");
			
			Сообщение       = Новый СообщениеПользователю;
			Сообщение.Текст = "Совместная группировка по периодам с другими выражениями запрещена! Поля: <" + ВремПоле.Заголовок + "> и <"+ПолеПериода.Заголовок+">.";
			Сообщение.Сообщить();
			Возврат Истина;
		Иначе
			
			ПревПоле = ПолеПериода;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НомерОсновногоПериода>0 И НомерПериодаПоСтруктуре>0 Тогда
		
		ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериода, "НомерПериода");
		ВторойПериод = ТаблицаПолей.Найти(НомерПериодаПоСтруктуре, "НомерПериода");
		
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
		Сообщение.Сообщить();
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если НомерОсновногоПериодаПоСтруктуре>0 И НомерПериода>0 Тогда
		
		ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериодаПоСтруктуре, "НомерПериода");
		ВторойПериод = ТаблицаПолей.Найти(НомерПериода, "НомерПериода");
		
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
		Сообщение.Сообщить();
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если НомерОсновногоПериода>0 И ВложенныйНомерПериода>НомерОсновногоПериода Тогда
		
		ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериода, "НомерПериода");
		ВторойПериод = ТаблицаПолей.Найти(ВложенныйНомерПериода, "НомерПериода");
		
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Периодическая группировка: <"+ПервыйПериод.Заголовок+"> должна находится ниже группировки: <"+ВторойПериод.Заголовок+">!";
		Сообщение.Сообщить();
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если НомерОсновногоПериодаПоСтруктуре>0 И ВложенныйНомерПериода>0 Тогда
		
		ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериодаПоСтруктуре, "НомерПериода");
		ВторойПериод = ТаблицаПолей.Найти(ВложенныйНомерПериода, "НомерПериода");
		
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
		Сообщение.Сообщить();
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если ВложенныйНомерОсновногоПериода>0 И НомерПериодаПоСтруктуре>0 Тогда
		
		ПервыйПериод = ТаблицаПолей.Найти(ВложенныйНомерОсновногоПериода, "НомерПериода");
		ВторойПериод = ТаблицаПолей.Найти(НомерПериодаПоСтруктуре, "НомерПериода");
		
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
		Сообщение.Сообщить();
		
		Возврат Истина;
		
	КонецЕсли;
	
	НомерПериодаПоСтруктуре          = Макс(НомерПериодаПоСтруктуре, НомерПериода, ВложенныйНомерПериода);
	НомерОсновногоПериодаПоСтруктуре = Макс(НомерОсновногоПериодаПоСтруктуре, НомерОсновногоПериода, ВложенныйНомерОсновногоПериода);
	
	Возврат Отказ;
	
КонецФункции

Функция ПроверитьКорректностьГруппировки(Группировка, ТаблицаПолей, МаксНомерПериода = 0, МаксНомерОсновногоПериода = 0)
	
	Отказ = Ложь;
	
	НомерПериодаПоСтруктуре          = 0;
	НомерОсновногоПериодаПоСтруктуре = 0;
	Для Каждого ТекГруппировка Из Группировка.Структура Цикл
		
		Если НЕ ТекГруппировка.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ВложенныйНомерПериода          = 0;
		ВложенныйНомерОсновногоПериода = 0;
		Отказ = ПроверитьКорректностьГруппировки(ТекГруппировка, ТаблицаПолей, ВложенныйНомерПериода, ВложенныйНомерОсновногоПериода);
		Если НЕ Отказ Тогда
			Отказ = ПроверитьКорректностьСтроки(ТекГруппировка, ТаблицаПолей, НомерПериодаПоСтруктуре, ВложенныйНомерПериода, НомерОсновногоПериодаПоСтруктуре, ВложенныйНомерОсновногоПериода);
			Если Отказ Тогда
				Возврат Истина
			КонецЕсли;
			//ПолеИерархии = Неопределено;
			//НомерПериода      = 0;
			//НомерОсновногоПериода = 0;
			//Для Каждого ТекПолеГруппировки Из ТекГруппировка.ПоляГруппировки.Элементы Цикл
			//	Если Не ТекПолеГруппировки.Использование ИЛИ (НЕ ТипЗнч(ТекПолеГруппировки) = Тип("ПолеГруппировкиКомпоновкиДанных")) Тогда
			//		Продолжить;
			//	КонецЕсли;
			//	
			//	ПолеПериода = ТаблицаПолей.Найти(ТекПолеГруппировки.Поле, "Поле");
			//	Если ПолеПериода = Неопределено Тогда
			//		Продолжить;
			//	КонецЕсли;
			//	
			//	Если ТекПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия ИЛИ ТекПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия Тогда
			//		Если ПолеИерархии = Неопределено Тогда
			//			ПолеИерархии = ПолеПериода.Заголовок;
			//		Иначе
			//			Сообщение       = Новый СообщениеПользователю;
			//			Сообщение.Текст = "Иерархическая группировка одновременно по нескольким полям: <" + ПолеИерархии + "> и <"+ПолеПериода.Заголовок+"> не допустима!";
			//			Сообщение.Сообщить();
			//			Возврат Истина;
			//		КонецЕсли;
			//	КонецЕсли;
			//	
			//	Если ПолеПериода.НомерПериода>0 Тогда
			//		НомерПериода = Макс(НомерПериода, ПолеПериода.НомерПериода);
			//		Если ПолеПериода.ТипПериода = ТипПериодаКомпоновкиДанных.Основной Тогда
			//			Если НомерОсновногоПериода = 0 Тогда
			//				НомерОсновногоПериода = ПолеПериода.НомерПериода;
			//			Иначе
			//				НомерОсновногоПериода = Мин(НомерОсновногоПериода, ПолеПериода.НомерПериода);
			//			КонецЕсли;
			//		КонецЕсли;
			//	КонецЕсли;
			//	
			//КонецЦикла;
			//
			//Если НомерОсновногоПериода>0 И НомерПериодаПоСтруктуре>0 Тогда
			//	
			//	ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериода, "НомерПериода");
			//	ВторойПериод = ТаблицаПолей.Найти(НомерПериодаПоСтруктуре, "НомерПериода");
			//	
			//	Сообщение       = Новый СообщениеПользователю;
			//	Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
			//	Сообщение.Сообщить();
			//	
			//	Возврат Истина;
			//	
			//КонецЕсли;
			//
			//Если НомерОсновногоПериодаПоСтруктуре>0 И НомерПериода>0 Тогда
			//	
			//	ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериодаПоСтруктуре, "НомерПериода");
			//	ВторойПериод = ТаблицаПолей.Найти(НомерПериода, "НомерПериода");
			//	
			//	Сообщение       = Новый СообщениеПользователю;
			//	Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
			//	Сообщение.Сообщить();
			//	
			//	Возврат Истина;
			//	
			//КонецЕсли;
			//
			//Если НомерОсновногоПериода>0 И ВложенныйНомерПериода>НомерОсновногоПериода Тогда
			//	
			//	ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериода, "НомерПериода");
			//	ВторойПериод = ТаблицаПолей.Найти(ВложенныйНомерПериода, "НомерПериода");
			//	
			//	Сообщение       = Новый СообщениеПользователю;
			//	Сообщение.Текст = "Периодическая группировка: <"+ПервыйПериод.Заголовок+"> должна находится ниже группировки: <"+ВторойПериод.Заголовок+">!";
			//	Сообщение.Сообщить();
			//	
			//	Возврат Истина;
			//	
			//КонецЕсли;
			//
			//Если НомерОсновногоПериодаПоСтруктуре>0 И ВложенныйНомерПериода>0 Тогда
			//	
			//	ПервыйПериод = ТаблицаПолей.Найти(НомерОсновногоПериодаПоСтруктуре, "НомерПериода");
			//	ВторойПериод = ТаблицаПолей.Найти(ВложенныйНомерПериода, "НомерПериода");
			//	
			//	Сообщение       = Новый СообщениеПользователю;
			//	Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
			//	Сообщение.Сообщить();
			//	
			//	Возврат Истина;
			//	
			//КонецЕсли;
			//
			//Если ВложенныйНомерОсновногоПериода>0 И НомерПериодаПоСтруктуре>0 Тогда
			//	
			//	ПервыйПериод = ТаблицаПолей.Найти(ВложенныйНомерОсновногоПериода, "НомерПериода");
			//	ВторойПериод = ТаблицаПолей.Найти(НомерПериодаПоСтруктуре, "НомерПериода");
			//	
			//	Сообщение       = Новый СообщениеПользователю;
			//	Сообщение.Текст = "Периодические группировки: <"+ПервыйПериод.Заголовок+"> и <"+ВторойПериод.Заголовок+"> должны находится в одной ветви!";
			//	Сообщение.Сообщить();
			//	
			//	Возврат Истина;
			//	
			//КонецЕсли;
			
			//НомерПериодаПоСтруктуре = Макс(НомерПериодаПоСтруктуре, НомерПериода, ВложенныйНомерПериода);
			МаксНомерПериода        = Макс(НомерПериодаПоСтруктуре, МаксНомерПериода);
			
			//НомерОсновногоПериодаПоСтруктуре = Макс(НомерОсновногоПериодаПоСтруктуре, НомерОсновногоПериода, ВложенныйНомерОсновногоПериода);
			МаксНомерОсновногоПериода        = Макс(НомерОсновногоПериодаПоСтруктуре, МаксНомерОсновногоПериода);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Отказ;
	
КонецФункции

Функция ПроверитьКорректностьСтруктуры(СтруктураОтчета, ТаблицаПолей)
	
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	
	Отказ = Ложь;
	Для Каждого ТекЭлемент Из СтруктураОтчета Цикл
		
		Если Не ТекЭлемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипСтруктурыТаблицы Тогда
			НомерПериодаПоСтруктуре          = 0;
			НомерОсновногоПериодаПоСтруктуре = 0;
			Для Каждого Строка Из ТекЭлемент.Строки Цикл
				
				Если НЕ Строка.Использование Тогда
					Продолжить;
				КонецЕсли;
				
				ВложенныйНомерПериода          = 0;
				ВложенныйНомерОсновногоПериода = 0;
			
				Отказ = ПроверитьКорректностьГруппировки(Строка, ТаблицаПолей, ВложенныйНомерПериода, ВложенныйНомерОсновногоПериода);
				
				Если НЕ Отказ Тогда
					Отказ = ПроверитьКорректностьСтроки(Строка, ТаблицаПолей, НомерПериодаПоСтруктуре, ВложенныйНомерПериода, НомерОсновногоПериодаПоСтруктуре, ВложенныйНомерОсновногоПериода);
				КонецЕсли;
				
				Если Отказ Тогда
					Возврат Истина
				КонецЕсли;
				
			КонецЦикла;
			
			Если ТекЭлемент.Колонки.Количество()>0 Тогда
				
				//ОсновныеПериоды       = Новый Массив;
				ОсновнойПериод        = Неопределено;
				ДополнительныеПериоды = Новый Массив;
				Для Каждого ТекСтрока Из ТаблицаПолей Цикл
					Если ТекСтрока.НомерПериода>0 Тогда
						Если ТекСтрока.ТипПериода = ТипПериодаКомпоновкиДанных.Основной Тогда 
							//ОсновныеПериоды.Добавить(ТекСтрока);
							Если КомпоновщикЕстьПолеВСтруктуре(ТекСтрока.Поле, ТекЭлемент.Колонки) и не ТекСтрока.Заголовок = "Детал месяц" Тогда  //БизТех Аляль 17.12.24г. не формировался АДДС по месяцам
								Сообщение       = Новый СообщениеПользователю;
								Сообщение.Текст = "Использовать группировку: <"+ТекСтрока.Заголовок + "> в колонках запрешено!";
								Сообщение.Сообщить();
								Возврат Истина;
							КонецЕсли;
							
							Если КомпоновщикЕстьПолеВСтруктуре(ТекСтрока.Поле, ТекЭлемент.Строки) Тогда
								ОсновнойПериод = ТекСтрока.Заголовок;
							КонецЕсли;
							
						Иначе
							ДополнительныеПериоды.Добавить(ТекСтрока);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ ОсновнойПериод = Неопределено Тогда
					Для Каждого ТекСтрока Из ДополнительныеПериоды Цикл
						Если КомпоновщикЕстьПолеВСтруктуре(ТекСтрока.Поле, ТекЭлемент.Колонки) Тогда
							Сообщение       = Новый СообщениеПользователю;
							Сообщение.Текст = "В строках используется группировка: <"+ОсновнойПериод+">. Использовать группировку: <"+ТекСтрока.Заголовок + "> в колонках запрешено!";
							Сообщение.Сообщить();
							Возврат Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Для Каждого Колонка Из ТекЭлемент.Колонки Цикл
					
					Если НЕ Колонка.Использование Тогда
						Продолжить;
					КонецЕсли;
					
					ВложенныйНомерПериода          = 0;
					ВложенныйНомерОсновногоПериода = 0;
					
					Отказ = ПроверитьКорректностьГруппировки(Колонка, ТаблицаПолей, ВложенныйНомерПериода, ВложенныйНомерОсновногоПериода);
					
					Если НЕ Отказ Тогда
						Отказ = ПроверитьКорректностьСтроки(Колонка, ТаблицаПолей, НомерПериодаПоСтруктуре, ВложенныйНомерПериода, НомерОсновногоПериодаПоСтруктуре, ВложенныйНомерОсновногоПериода);
					КонецЕсли;
					
					Если Отказ Тогда
						Возврат Истина
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
				
		ИначеЕсли ТипЭлемента = ТипСтруктурыДиаграммы Тогда
			
			НомерПериодаПоСтруктуре          = 0;
			НомерОсновногоПериодаПоСтруктуре = 0;
			Для Каждого Серия Из ТекЭлемент.Серии Цикл
				
				Если НЕ Серия.Использование Тогда
					Продолжить;
				КонецЕсли;
				
				ВложенныйНомерПериода          = 0;
				ВложенныйНомерОсновногоПериода = 0;
				
				Отказ = ПроверитьКорректностьГруппировки(Серия, ТаблицаПолей, ВложенныйНомерПериода, ВложенныйНомерОсновногоПериода);
				
				Если НЕ Отказ Тогда
					Отказ = ПроверитьКорректностьСтроки(Серия, ТаблицаПолей, НомерПериодаПоСтруктуре, ВложенныйНомерПериода, НомерОсновногоПериодаПоСтруктуре, ВложенныйНомерОсновногоПериода);
				КонецЕсли;
				
				Если Отказ Тогда
					Возврат Истина
				КонецЕсли;
				
			КонецЦикла;
			
			Если ТекЭлемент.Точки.Количество()>0 Тогда
				
				//ОсновныеПериоды       = Новый Массив;
				ОсновнойПериод        = Неопределено;
				ДополнительныеПериоды = Новый Массив;
				Для Каждого ТекСтрока Из ТаблицаПолей Цикл
					Если ТекСтрока.НомерПериода>0 Тогда
						Если ТекСтрока.ТипПериода = ТипПериодаКомпоновкиДанных.Основной Тогда
							//ОсновныеПериоды.Добавить(ТекСтрока);
							Если КомпоновщикЕстьПолеВСтруктуре(ТекСтрока.Поле, ТекЭлемент.Точки) Тогда
								Сообщение       = Новый СообщениеПользователю;
								Сообщение.Текст = "Использовать группировку: <"+ТекСтрока.Заголовок + "> в точках запрешено!";
								Сообщение.Сообщить();
								Возврат Истина;
							КонецЕсли;
							
							Если КомпоновщикЕстьПолеВСтруктуре(ТекСтрока.Поле, ТекЭлемент.Серии) Тогда
								ОсновнойПериод = ТекСтрока.Заголовок;
							КонецЕсли;
							
						Иначе
							ДополнительныеПериоды.Добавить(ТекСтрока);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ ОсновнойПериод = Неопределено Тогда
					Для Каждого ТекСтрока Из ДополнительныеПериоды Цикл
						Если КомпоновщикЕстьПолеВСтруктуре(ТекСтрока.Поле, ТекЭлемент.Точки) Тогда
							Сообщение       = Новый СообщениеПользователю;
							Сообщение.Текст = "В строках используется группировка: <"+ОсновнойПериод+">. Использовать группировку: <"+ТекСтрока.Заголовок + "> в точках запрешено!";
							Сообщение.Сообщить();
							Возврат Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Для Каждого Колонка Из ТекЭлемент.Точки Цикл
					
					Если НЕ Колонка.Использование Тогда
						Продолжить;
					КонецЕсли;
					
					ВложенныйНомерПериода          = 0;
					ВложенныйНомерОсновногоПериода = 0;
					
					Отказ = ПроверитьКорректностьГруппировки(Колонка, ТаблицаПолей, ВложенныйНомерПериода, ВложенныйНомерОсновногоПериода);
					
					Если НЕ Отказ Тогда
						Отказ = ПроверитьКорректностьСтроки(Колонка, ТаблицаПолей, НомерПериодаПоСтруктуре, ВложенныйНомерПериода, НомерОсновногоПериодаПоСтруктуре, ВложенныйНомерОсновногоПериода);
					КонецЕсли;
					
					Если Отказ Тогда
						Возврат Истина
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		ИначеЕсли ТекЭлемент.Использование Тогда
			Отказ = ПроверитьКорректностьГруппировки(ТекЭлемент, ТаблицаПолей);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Отказ;
	
КонецФункции // ПроверитьКорректностьСтруктуры()

Процедура СкопироватьСтруктуру(СтруктураНастроек, СтруктураКопирования, ОбщийПорядок = Неопределено) Экспорт
	
	ТипСтруктурыОтчета = ТипЗнч(СтруктураКопирования);
	
	Если ТипСтруктурыОтчета = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		НоваяТаблица = СтруктураНастроек.Добавить(Тип("ТаблицаКомпоновкиДанных"));
		
		СкопироватьСтруктуру(НоваяТаблица.Строки, СтруктураКопирования.Строки, ОбщийПорядок);
		СкопироватьСтруктуру(НоваяТаблица.Колонки, СтруктураКопирования.Колонки, ОбщийПорядок);
		СкопироватьСтруктуру(НоваяТаблица.Выбор.Элементы, СтруктураКопирования.Выбор.Элементы);
		СкопироватьСтруктуру(НоваяТаблица.ПараметрыВывода.Элементы, СтруктураКопирования.ПараметрыВывода.Элементы);
		СкопироватьСтруктуру(НоваяТаблица.УсловноеОформление.Элементы, СтруктураКопирования.УсловноеОформление.Элементы);
		
	ИначеЕсли ТипСтруктурыОтчета = Тип("ДиаграммаКомпоновкиДанных") Тогда
		
		НоваяДиаграмма = СтруктураНастроек.Добавить(Тип("ДиаграммаКомпоновкиДанных"));
		
		СкопироватьСтруктуру(НоваяДиаграмма.Серии, СтруктураКопирования.Серии, ОбщийПорядок);
		СкопироватьСтруктуру(НоваяДиаграмма.Точки, СтруктураКопирования.Точки, ОбщийПорядок);
		СкопироватьСтруктуру(НоваяДиаграмма.Выбор.Элементы, СтруктураКопирования.Выбор.Элементы);
		СкопироватьСтруктуру(НоваяДиаграмма.ПараметрыВывода.Элементы, СтруктураКопирования.ПараметрыВывода.Элементы);
		СкопироватьСтруктуру(НоваяДиаграмма.УсловноеОформление.Элементы, СтруктураКопирования.УсловноеОформление.Элементы);
		
	// Группировка таблицы отчета
	ИначеЕсли ТипСтруктурыОтчета = Тип("ГруппировкаКомпоновкиДанных") Тогда
		
		ВетвьСтруктуры = СтруктураНастроек;
		Если СтруктураКопирования.Использование Тогда
			
			НоваяГруппировка = СтруктураНастроек.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
		
			// Заполнение полей группировки
			СкопироватьСтруктуру(НоваяГруппировка.ПоляГруппировки.Элементы, СтруктураКопирования.ПоляГруппировки.Элементы);
			
			// Заполнение Реквизитов группировки
			СкопироватьСтруктуру(НоваяГруппировка.Выбор.Элементы, СтруктураКопирования.Выбор.Элементы);
			
			// Заполнение Порядка группировки
			СкопироватьСтруктуру(НоваяГруппировка.Порядок.Элементы, СтруктураКопирования.Порядок.Элементы, ОбщийПорядок);
			
			// Заполнение Отборов группировки
			СкопироватьСтруктуру(НоваяГруппировка.Отбор.Элементы, СтруктураКопирования.Отбор.Элементы);
			
			// Заполнение Условного оформления группировки
			СкопироватьСтруктуру(НоваяГруппировка.УсловноеОформление.Элементы, СтруктураКопирования.УсловноеОформление.Элементы);
			
			// Заполнение Параметров вывода группировки
			СкопироватьСтруктуру(НоваяГруппировка.ПараметрыВывода.Элементы, СтруктураКопирования.ПараметрыВывода.Элементы);
			
			ВетвьСтруктуры = НоваяГруппировка.Структура;
			
		КонецЕсли;
		// Заполнение вложенных группировок
		СкопироватьСтруктуру(ВетвьСтруктуры, СтруктураКопирования.Структура, ОбщийПорядок);
		
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаКопирования Из СтруктураКопирования Цикл
			
			СкопироватьСтруктуру(СтруктураНастроек, СтрокаКопирования, ОбщийПорядок);
			
		КонецЦикла;
		
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаКопирования Из СтруктураКопирования Цикл
			
			ВетвьСтруктуры = СтруктураНастроек;
			
			Если СтрокаКопирования.Использование Тогда
				
				ЭлементСтруктуры = ВетвьСтруктуры.Добавить();
				ЭлементСтруктуры.Использование = СтрокаКопирования.Использование;
				
				СкопироватьСтруктуру(ЭлементСтруктуры, СтрокаКопирования, ОбщийПорядок);
				
				ВетвьСтруктуры = ЭлементСтруктуры.Структура;
				
			КонецЕсли;
			
			СкопироватьСтруктуру(ВетвьСтруктуры, СтрокаКопирования.Структура, ОбщийПорядок);
			
		КонецЦикла;
		
	// Элементы диаграммы
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаКопирования Из СтруктураКопирования Цикл
			
			ВетвьСтруктуры = СтруктураНастроек;
			
			Если СтрокаКопирования.Использование Тогда
				
				ЭлементСтруктуры = ВетвьСтруктуры.Добавить();
				ЭлементСтруктуры.Использование = СтрокаКопирования.Использование;
				
				СкопироватьСтруктуру(ЭлементСтруктуры, СтрокаКопирования, ОбщийПорядок);
				
				ВетвьСтруктуры = ЭлементСтруктуры.Структура;
				
			КонецЕсли;
			
			СкопироватьСтруктуру(ВетвьСтруктуры, СтрокаКопирования.Структура, ОбщийПорядок);
			
		КонецЦикла;
		
	// Группировка таблицы отчета
	ИначеЕсли ТипСтруктурыОтчета = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		
		// Заполнение полей группировки
		СкопироватьСтруктуру(СтруктураНастроек.ПоляГруппировки.Элементы, СтруктураКопирования.ПоляГруппировки.Элементы);
		
		// Заполнение Реквизитов группировки
		СкопироватьСтруктуру(СтруктураНастроек.Выбор.Элементы, СтруктураКопирования.Выбор.Элементы);
		
		// Заполнение Порядка группировки
		СкопироватьСтруктуру(СтруктураНастроек.Порядок.Элементы, СтруктураКопирования.Порядок.Элементы, ОбщийПорядок);
		
		// Заполнение Отборов группировки
		СкопироватьСтруктуру(СтруктураНастроек.Отбор.Элементы, СтруктураКопирования.Отбор.Элементы);
		
		// Заполнение Условного оформления группировки
		СкопироватьСтруктуру(СтруктураНастроек.УсловноеОформление.Элементы, СтруктураКопирования.УсловноеОформление.Элементы);
		
		// Заполнение Параметров вывода группировки
		СкопироватьСтруктуру(СтруктураНастроек.ПараметрыВывода.Элементы, СтруктураКопирования.ПараметрыВывода.Элементы);
		
	// Группировка диаграммы отчета
	ИначеЕсли ТипСтруктурыОтчета = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		// Заполнение полей группировки
		СкопироватьСтруктуру(СтруктураНастроек.ПоляГруппировки.Элементы, СтруктураКопирования.ПоляГруппировки.Элементы);
		
		// Заполнение Реквизитов группировки
		СкопироватьСтруктуру(СтруктураНастроек.Выбор.Элементы, СтруктураКопирования.Выбор.Элементы);
		
		// Заполнение Порядка группировки
		СкопироватьСтруктуру(СтруктураНастроек.Порядок.Элементы, СтруктураКопирования.Порядок.Элементы, ОбщийПорядок);
		
		// Заполнение Отборов группировки
		СкопироватьСтруктуру(СтруктураНастроек.Отбор.Элементы, СтруктураКопирования.Отбор.Элементы);
		
		// Заполнение Условного оформления группировки
		СкопироватьСтруктуру(СтруктураНастроек.УсловноеОформление.Элементы, СтруктураКопирования.УсловноеОформление.Элементы);
		
		// Заполнение Параметров вывода группировки
		СкопироватьСтруктуру(СтруктураНастроек.ПараметрыВывода.Элементы, СтруктураКопирования.ПараметрыВывода.Элементы, ОбщийПорядок);
		
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаПараметров Из СтруктураКопирования Цикл
			
			Если НЕ СтрокаПараметров.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрСКД = СтруктураНастроек.Найти(СтрокаПараметров.Параметр);
			Если ПараметрСКД = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПараметрСКД.Использование = Истина;
			ПараметрСКД.Значение      = СтрокаПараметров.Значение;
			
			Если ПараметрСКД.ЗначенияВложенныхПараметров.Количество()>0 Тогда
				СкопироватьСтруктуру(ПараметрСКД.ЗначенияВложенныхПараметров, СтрокаПараметров.ЗначенияВложенныхПараметров);
			КонецЕсли;
			
		КонецЦикла;
		
	// Показатели и реквизиты
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияВыбранныхПолейКомпоновкиДанных") Тогда
		
		Для Каждого ВыбранноеПоле Из СтруктураКопирования Цикл
			
			ТипПоля = ТипЗнч(ВыбранноеПоле);
			
			НовоеВыбранноеПоле = СтруктураНастроек.Добавить(ТипПоля);
			ЗаполнитьЗначенияСвойств(НовоеВыбранноеПоле, ВыбранноеПоле);
			
			Если ТипПоля = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
				СкопироватьСтруктуру(НовоеВыбранноеПоле.Элементы, ВыбранноеПоле.Элементы);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияПолейГруппировкиКомпоновкиДанных") Тогда
		
		// Состав группировок строки
		Для Каждого ПолеГруппировки Из СтруктураКопирования Цикл
			
			НовоеПолеГруппировки = СтруктураНастроек.Добавить(ТипЗнч(ПолеГруппировки));
			ЗаполнитьЗначенияСвойств(НовоеПолеГруппировки, ПолеГруппировки);
			
		КонецЦикла;
		
	// Копирование отбора
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
		
		ТипЭлементаОтбора = Тип("ЭлементОтбораКомпоновкиДанных");
		
		Для Каждого СтрокаОтбора Из СтруктураКопирования Цикл
			
			Если ТипЗнч(СтрокаОтбора) = ТипЭлементаОтбора Тогда
				ЭлементОтбора = СтруктураНастроек.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.Использование  = СтрокаОтбора.Использование;
				ЭлементОтбора.ЛевоеЗначение  = СтрокаОтбора.ЛевоеЗначение;
				ЭлементОтбора.ВидСравнения   = СтрокаОтбора.ВидСравнения;
				ЭлементОтбора.ПравоеЗначение = СтрокаОтбора.ПравоеЗначение;
			Иначе
				ГруппаОтбора = СтруктураНастроек.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтбора.Использование = СтрокаОтбора.Использование;
				ГруппаОтбора.ТипГруппы     = СтрокаОтбора.ТипГруппы;
				
				СкопироватьСтруктуру(ГруппаОтбора.Элементы, СтрокаОтбора.Элементы);
			КонецЕсли;
			
		КонецЦикла;
		
	// Порядок
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияЭлементовПорядкаКомпоновкиДанных") Тогда
		
		ТипАвтоЭлемента = Тип("АвтоЭлементПорядкаКомпоновкиДанных");
		// 1. Если есть только автоматическая, то добавим автоматическую
		// 2. Если задана сотрировка, то уберем из нее автоматическую и добавим общую сортировку
		Если СтруктураКопирования.Количество()=1 И ТипЗнч(СтруктураКопирования[0]) = ТипАвтоЭлемента Тогда
			НовоеПолеПорядка = СтруктураНастроек.Добавить(ТипАвтоЭлемента);
			НовоеПолеПорядка.Использование = Истина;
		Иначе
			
			Для Каждого ПолеПорядка Из ОбщийПорядок.Элементы Цикл
				НовоеПолеПорядка = СтруктураНастроек.Добавить(ТипЗнч(ПолеПорядка));
				ЗаполнитьЗначенияСвойств(НовоеПолеПорядка, ПолеПорядка);
			КонецЦикла;
			
			// Порядок группировок
			Для Каждого ПолеПорядка Из СтруктураКопирования Цикл
				
				Если ТипЗнч(ПолеПорядка) = ТипАвтоЭлемента Тогда
					Продолжить;
				КонецЕсли;
				
				НовоеПолеПорядка = СтруктураНастроек.Добавить(ТипЗнч(ПолеПорядка));
				ЗаполнитьЗначенияСвойств(НовоеПолеПорядка, ПолеПорядка);
			КонецЦикла;
			
		КонецЕсли;
		
		Если СтруктураНастроек.Количество() = 0 Тогда
			НовоеПолеПорядка = СтруктураНастроек.Добавить(ТипАвтоЭлемента);
			НовоеПолеПорядка.Использование = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияОформляемыхПолейКомпоновкиДанных") Тогда
		
		Для Каждого ПолеОформления Из СтруктураКопирования Цикл
			
			Если НЕ ПолеОформления.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			НовоеПолеОформления = СтруктураНастроек.Добавить();
			НовоеПолеОформления.Использование = ПолеОформления.Использование;
			НовоеПолеОформления.Поле          = ПолеОформления.Поле;
			
		КонецЦикла;
		
	ИначеЕсли ТипСтруктурыОтчета = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
		
		// Заполнение Условного оформления группировки
		Для Каждого ЭлементОформления Из СтруктураКопирования Цикл
			
			Если Не ЭлементОформления.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйЭлементОформления = СтруктураНастроек.Добавить();
			
			ЗаполнитьЗначенияСвойств(НовыйЭлементОформления, ЭлементОформления);
			
			СкопироватьСтруктуру(НовыйЭлементОформления.Отбор.Элементы, ЭлементОформления.Отбор.Элементы);
			СкопироватьСтруктуру(НовыйЭлементОформления.Поля.Элементы, ЭлементОформления.Поля.Элементы);
			СкопироватьСтруктуру(НовыйЭлементОформления.Оформление.Элементы, ЭлементОформления.Оформление.Элементы);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////// СОБЫТИЯ ОТЧЕТА /////////////////////////////////////////

Процедура ВывестиОтчет(ОтчетОбъект, СхемаКомпоновкиДанных, Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, ВнешниеНаборыДанных = Неопределено, ИспользоватьВнешниеФункции = Ложь) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ДокументРезультат.Очистить();
	
	НеПравильныйФормат = Истина;
	Попытка
		НеПравильныйФормат = ТипЗнч(ОтчетОбъект) = Тип("СхемаКомпоновкиДанных");
	Исключение
	КонецПопытки;
	
	Если НеПравильныйФормат Тогда
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Старая версия отчета. Используйте отчеты, начиная от версии 5.0.06!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, Настройки, "dataCompositionScheme", "http://v8.1c.ru/8.1/data-composition-system/scheme");
	ТекстЗапроса = ЗаписьXML.Закрыть();
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстЗапроса);
	КопияНастроек = СериализаторXDTO.ПрочитатьXML(ЧтениеXML, Тип("НастройкиКомпоновкиДанных"));
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(КопияНастроек);
	
	КомпоновщикНастроек.Настройки.Структура.Очистить();
	
	Для Каждого ТекущаяТаблица Из Настройки.Структура Цикл
		
		Если ТекущаяТаблица.Использование Тогда
			СкопироватьСтруктуру(КомпоновщикНастроек.Настройки.Структура, ТекущаяТаблица, Настройки.Порядок);
		КонецЕсли;
		
	КонецЦикла;
	
	Попытка
		ОтчетОбъект.ПередФормированиемМакетаКомпоновки(СхемаКомпоновкиДанных, КомпоновщикНастроек);
	Исключение
	КонецПопытки;
	
	//ПолеДобавления = Новый ПолеКомпоновкиДанных("ПериодРегистратор");
	//Если НЕ ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре(ПолеДобавления, ВариантОтчета.Настройки.Структура) Тогда
	//	ГруппировкаРегистратора = ОтчетыСервер.ДобавитьПолеВПоследнююГруппировку(ПолеДобавления, ВариантОтчета.Настройки.Структура);
	//	Поля = Новый Массив;
	//	Поля.Добавить("ПериодРегистратор");
	//	ОтчетыСервер.КомпоновщикУстановитьОтборПоПолямГруппировки(ГруппировкаРегистратора, Поля);
	//	//ОтчетыСервер.КомпоновщикУстановитьОтборПустыхГруппировок(КомпоновщикНастроек.Настройки.Структура, Поля);
	//КонецЕсли;
	
	
	//Для Каждого ТекДоступноеПолеПараметра Из Настройки.ПараметрыДанных.ДоступныеПараметры.Элементы Цикл
	//	ТипыПоля = ТекДоступноеПолеПараметра.Тип.Типы();
	//	Если ТипыПоля.Количество()>0 И ТипыПоля[0] = Тип("СписокЗначений") Тогда
	//		Если (НЕ ТекДоступноеПолеПараметра.ДоступныеЗначения = Неопределено) И ТекДоступноеПолеПараметра.ДоступныеЗначения.Количество()>0 Тогда
	//							
	//			ЗначениеПараметра = КомпоновщикПолучитьЗначениеПараметра(ТекДоступноеПолеПараметра.Параметр, Настройки);
	//			ЭлементСписка = ТекДоступноеПолеПараметра.ДоступныеЗначения.НайтиПоЗначению(ЗначениеПараметра);
	//			Если НЕ ЭлементСписка = Неопределено Тогда
	//				ЗначениеПараметра = Настройки.ПараметрыДанных.Элементы.Найти(ТекДоступноеПолеПараметра.Параметр);
	//				ЗначениеПараметра.Значение[0].Представление = ЭлементСписка.Представление;
	//			КонецЕсли;
	//		КонецЕсли;
	//	КонецЕсли;
	//КонецЦикла;
	
	ТаблицаПолей = Новый ТаблицаЗначений;
	ТаблицаПолей.Колонки.Добавить("Поле");
	ТаблицаПолей.Колонки.Добавить("Заголовок");
	ТаблицаПолей.Колонки.Добавить("НомерПериода");
	ТаблицаПолей.Колонки.Добавить("ТипПериода");
	
	ДоступныеПоляГруппировок = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.Элементы;
	Для Каждого ТекНабор Из СхемаКомпоновкиДанных.НаборыДанных Цикл
		Для Каждого ТекПоле Из ТекНабор.Поля Цикл
			
			Если НЕ ТипЗнч(ТекПоле) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") ИЛИ ДоступныеПоляГруппировок.Найти(ТекПоле.Поле) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаПолей.Добавить();
			НоваяСтрока.Заголовок    = ТекПоле.Заголовок;
			НоваяСтрока.Поле         = Новый ПолеКомпоновкиДанных(ТекПоле.Поле);
			НоваяСтрока.НомерПериода = ТекПоле.Роль.НомерПериода;
			НоваяСтрока.ТипПериода   = ТекПоле.Роль.ТипПериода;
			
		КонецЦикла;
	КонецЦикла;
	
	Отказ = ПроверитьКорректностьСтруктуры(КомпоновщикНастроек.Настройки.Структура, ТаблицаПолей);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОткорректироватьНастройкиСКД(КомпоновщикНастроек.Настройки);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		КомпоновщикНастроек.Настройки,
		ДанныеРасшифровки
	);
	
	Попытка
		ОтчетОбъект.ПослеФормированияМакетаКомпоновки(МакетКомпоновки);
	Исключение
	КонецПопытки;
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, ИспользоватьВнешниеФункции);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	ПроцессорВывода.ЗакончитьВывод();
	
	Если ДокументРезультат.ФиксацияСлева>0 Тогда
		ДокументРезультат.ФиксацияСлева = 1;
	КонецЕсли;
	
КонецПроцедуры // ВывестиОтчет()

Функция НайтиДоступноеПоле(ДоступныеПоля, ПолеПоиска)
	
	Результат = Неопределено;
	Родитель  = ДоступныеПоля.Поле;
	
	// Если поле поиска это родитель то и искать ничего не нужно
	Если Родитель = ПолеПоиска Тогда
		Возврат ДоступныеПоля;
	КонецЕсли;
	// Проверка, что поле поиска пренадлежит родителю
	Если НЕ Найти(ПолеПоиска, Родитель) = 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ДоступныеПоля.Элементы.Найти(ПолеПоиска);
	Если НЕ Результат = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПутьКДанным  = СтрЗаменить(ПолеПоиска, СокрЛП(Родитель)+".", "");
	
	// Свойство или пользовательское поле
	Если Лев(ПутьКДанным, 1) = "[" Тогда
		ПозицияТочки = Найти(ПутьКДанным, "].");
	Иначе
		ПозицияТочки = Найти(ПутьКДанным, ".")-1;
	КонецЕсли;
	
	// Если поле нахоится на первом уровне найдем его обычным поиском
	Если ПозицияТочки > 0 Тогда
		// Если поле нахоится ниже, чем на первом уровне найдем его ветвь и перейдем на уровень ниже
		ПутьКДанным = СокрЛП(Родитель)+"."+Лев(ПутьКДанным, ПозицияТочки);
		ПолеВетви = Новый ПолеКомпоновкиДанных(ПутьКДанным);
		ВетвьПоиска = ДоступныеПоля.Элементы.Найти(ПолеВетви);
		// Если найдена ветвь поиска переходим на следующий уровень
		Если НЕ ВетвьПоиска = Неопределено Тогда
			Результат = НайтиДоступноеПоле(ВетвьПоиска, ПолеПоиска);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ОткорректироватьГруппировкуСКД(Группировка)
	
	Если НЕ Группировка.Использование Тогда
		Возврат;
	КонецЕсли;
	
	// Детальные записи не проверяем
	Если Группировка.ПоляГруппировки.Элементы.Количество() = 0 Тогда
		ОткорректироватьСтруктуруСКД(Группировка.Структура);
		Возврат;
	КонецЕсли;
	
	ДоступныеПоляВыбора  = Новый Массив;
	ДоступныеПоляПорядка = Новый Массив;
	Для Каждого ТекГруппировка Из Группировка.ПоляГруппировки.Элементы Цикл
		Если Не ТекГруппировка.Использование ИЛИ (НЕ ТипЗнч(ТекГруппировка) = Тип("ПолеГруппировкиКомпоновкиДанных")) Тогда
			Продолжить;
		КонецЕсли;
		ДоступныеПоля = Группировка.Выбор.ДоступныеПоляВыбора.НайтиПоле(ТекГруппировка.Поле);
		Если НЕ ДоступныеПоля = Неопределено Тогда
			ДоступныеПоляВыбора.Добавить(ДоступныеПоля);
		КонецЕсли;
		ДоступныеПоля = Группировка.Порядок.ДоступныеПоляПорядка.НайтиПоле(ТекГруппировка.Поле);
		Если НЕ ДоступныеПоля = Неопределено Тогда
			ДоступныеПоляПорядка.Добавить(ДоступныеПоля);
		КонецЕсли;
	КонецЦикла;
	
	СистемныеПоля = Группировка.Выбор.ДоступныеПоляВыбора.НайтиПоле(Новый ПолеКомпоновкиДанных("СистемныеПоля"));
	
	МассивДляУдаления = Новый Массив;
	Для Каждого Элемент Из Группировка.Выбор.Элементы Цикл
		Если НЕ Элемент.Использование ИЛИ (НЕ ТипЗнч(Элемент) = Тип("ВыбранноеПолеКомпоновкиДанных")) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ СистемныеПоля = Неопределено И (НЕ СистемныеПоля.Элементы.Найти(Элемент.Поле) = Неопределено) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПоля = Группировка.Выбор.ДоступныеПоляВыбора.НайтиПоле(Элемент.Поле);
		
		УдалятьПоле = Истина;
		Если ПараметрыПоля = Неопределено Тогда
			УдалятьПоле = Истина;
		ИначеЕсли ПараметрыПоля.Ресурс Тогда
			УдалятьПоле = Ложь;
		Иначе
			Для Каждого ДоступныеПоля Из ДоступныеПоляВыбора Цикл
				Если НЕ НайтиДоступноеПоле(ДоступныеПоля, Элемент.Поле) = Неопределено Тогда
					УдалятьПоле = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если УдалятьПоле Тогда
			МассивДляУдаления.Добавить(Элемент);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Элемент Из МассивДляУдаления Цикл
		Группировка.Выбор.Элементы.Удалить(Элемент);
	КонецЦикла;
	
	Если Группировка.Выбор.Элементы.Количество() = 0 Тогда
		НовоеПоле = Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		НовоеПоле.Использование = Истина;
	КонецЕсли;
	
	МассивДляУдаления.Очистить();
	Для Каждого Элемент Из Группировка.Порядок.Элементы Цикл
		Если НЕ Элемент.Использование ИЛИ (НЕ ТипЗнч(Элемент) = Тип("ЭлементПорядкаКомпоновкиДанных")) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПоля = Группировка.Порядок.ДоступныеПоляПорядка.НайтиПоле(Элемент.Поле);
		
		УдалятьПоле = Истина;
		Если ПараметрыПоля = Неопределено Тогда
			УдалятьПоле = Истина;
		ИначеЕсли ПараметрыПоля.Ресурс Тогда
			УдалятьПоле = Ложь;
		Иначе
			Для Каждого ДоступныеПоля Из ДоступныеПоляПорядка Цикл
				Если НЕ НайтиДоступноеПоле(ДоступныеПоля, Элемент.Поле) = Неопределено Тогда
					УдалятьПоле = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
			
		Если УдалятьПоле Тогда
			МассивДляУдаления.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из МассивДляУдаления Цикл
		Группировка.Порядок.Элементы.Удалить(Элемент);
	КонецЦикла;
	
	Если Группировка.Порядок.Элементы.Количество() = 0 Тогда
		НовоеПоле = Группировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		НовоеПоле.Использование = Истина;
	КонецЕсли;
	
	ОткорректироватьСтруктуруСКД(Группировка.Структура);
	
КонецПроцедуры

Процедура ОткорректироватьСтруктуруСКД(СтруктураОтчета)
	
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	Для Каждого ТекЭлемент Из СтруктураОтчета Цикл
		
		Если Не ТекЭлемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипСтруктурыТаблицы Тогда
			Для Каждого Строка Из ТекЭлемент.Строки Цикл
				ОткорректироватьГруппировкуСКД(Строка);
			КонецЦикла;
			Для Каждого Колонка Из ТекЭлемент.Колонки Цикл
				ОткорректироватьГруппировкуСКД(Колонка);
			КонецЦикла;
		ИначеЕсли ТипЭлемента = ТипСтруктурыДиаграммы Тогда
			Для Каждого Серия Из ТекЭлемент.Серии Цикл
				ОткорректироватьГруппировкуСКД(Серия);
			КонецЦикла;
			Для Каждого Точка Из ТекЭлемент.Точки Цикл
				ОткорректироватьГруппировкуСКД(Точка);
			КонецЦикла;
		Иначе
			ОткорректироватьГруппировкуСКД(ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // КомпоновщикЕстьПолеВСтруктуре()

Процедура ОткорректироватьОтборСКД(ОтборОтчета, ДоступныеПоляОтбора)
	
	ТипГруппаЭлементовОтбора = Тип("ГруппаЭлементовОтбораКомпоновкиДанных");
	Для Каждого ТекЭлемент Из ОтборОтчета.Элементы Цикл
		
		Если Не ТекЭлемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипГруппаЭлементовОтбора Тогда
			ОткорректироватьОтборСКД(ТекЭлемент, ДоступныеПоляОтбора)
		Иначе
			ДоступныеПоля = ДоступныеПоляОтбора.НайтиПоле(ТекЭлемент.ЛевоеЗначение);
			Если ДоступныеПоля = Неопределено Тогда
				ТекЭлемент.Использование = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // КомпоновщикЕстьПолеВСтруктуре()

Процедура ОткорректироватьНастройкиСКД(НастройкиКомпоновки)
	
	// Настройки выбора
	
	// Настройки порядка
	
	// Настройки структуры
	ОткорректироватьСтруктуруСКД(НастройкиКомпоновки.Структура);
	
	// Настройки отбора
	ОткорректироватьОтборСКД(НастройкиКомпоновки.Отбор, НастройкиКомпоновки.Отбор.ДоступныеПоляОтбора);
	
КонецПроцедуры

Функция ПроверитьКорректностьПоказателей(Показатели)
	
	Для Каждого ТекПоказатель Из Показатели Цикл
		Если ТекПоказатель.Использование = Истина Тогда
			Если ТипЗнч(ТекПоказатель) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
				Если НЕ СокрЛП(ТекПоказатель.Поле) = "" Тогда
					Возврат Истина;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ТекПоказатель) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") И ПроверитьКорректностьПоказателей(ТекПоказатель.Элементы) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ПроверитьКорректностьОтборов(Отборы, Уровень = "")
	
	Результат = Истина;
	Для Каждого ТекОтбор Из Отборы Цикл
		Если ТекОтбор.Использование = Истина Тогда
			Если ТипЗнч(ТекОтбор) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				Если СокрЛП(ТекОтбор.ЛевоеЗначение) = "" Тогда
					Сообщение       = Новый СообщениеПользователю;
					Если Уровень = "" Тогда
						Сообщение.Текст = "Отборы: Элемент в строке № "+ Уровень + Строка(Отборы.Индекс(ТекОтбор)+1) + " не заполнен!";
					Иначе
						Сообщение.Текст = "Отборы: Элемент в строке № ("+ Уровень + ") " + Строка(Отборы.Индекс(ТекОтбор)+1) + " не заполнен!";
					КонецЕсли;
					Сообщение.Сообщить();
					Результат = Ложь;
				КонецЕсли;
			Иначе
				Результат = ПроверитьКорректностьОтборов(ТекОтбор.Элементы, Уровень + ?(Уровень="", "", "->") + Строка(Отборы.Индекс(ТекОтбор)+1) + "->") И Результат;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьКорректность(ОтчетОбъект, КомпоновщикНастроек)
	
	Результат = Истина;
	
	//ДатаНачала = КомпоновщикПолучитьЗначениеПараметра("ДатаНачала",    КомпоновщикНастроек.Настройки);
	//ДатаКонца  = КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", КомпоновщикНастроек.Настройки);
	//
	//Если (НЕ ДатаКонца = Неопределено) Тогда
	//	Если НачалоДня(ДатаКонца) = Дата(1, 1, 1) Тогда
	//		Сообщение       = Новый СообщениеПользователю;
	//		Сообщение.Текст = "Не заполнена дата окончания";
	//		Сообщение.Сообщить();
	//		Результат = Ложь;
	//	КонецЕсли;
	//КонецЕсли;
	//
	//Если (НЕ ДатаНачала = Неопределено) И (НЕ ДатаКонца = Неопределено) Тогда
	//	Если ДатаНачала > ДатаКонца Тогда
	//		Сообщение       = Новый СообщениеПользователю;
	//		Сообщение.Текст = "Дата начала периода не должна превышать дату окончания";
	//		Сообщение.Сообщить();
	//		Результат = Ложь;
	//	КонецЕсли;
	//КонецЕсли;
	
	Если НЕ ОтчетОбъект.ПроверитьЗаполнение() Тогда
		Результат = Ложь;
	КонецЕсли;
	
	//Если НЕ ПроверитьКорректностьПоказателей(КомпоновщикНастроек.Настройки.Выбор.Элементы) Тогда
	//	Сообщение       = Новый СообщениеПользователю;
	//	Сообщение.Текст = "Не выбрано ни одного показателя!";
	//	Сообщение.Сообщить();
	//	Результат = Ложь;
	//КонецЕсли;
	
	Если НЕ ПроверитьКорректностьОтборов(КомпоновщикНастроек.Настройки.Отбор.Элементы) Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СформироватьОтчет(ОтчетОбъект, ТабличныйДокумент, СхемаКомпоновки = Неопределено, КомпоновщикНастроек = Неопределено, Заголовок = "", ДанныеРасшифровки = Неопределено, УникальныйИдентификатор = Неопределено) Экспорт
	
	Если КомпоновщикНастроек = Неопределено Тогда
		КомпоновщикНастроек = ОтчетОбъект.КомпоновщикНастроек;
	КонецЕсли;
	
	Если НЕ СхемаКомпоновки = Неопределено Тогда
		ОтчетОбъект.СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(СхемаКомпоновки);
	КонецЕсли;
	
	ДатаОкончания        = КомпоновщикНастроек.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаОкончания"));
	ДатаОкончанияГраница = КомпоновщикНастроек.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаОкончанияГраница"));
	Если (НЕ ДатаОкончания = Неопределено) Тогда
		ДатаКонца = КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", КомпоновщикНастроек.Настройки);
		ДатаКонца = КонецДня(ДатаКонца);
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", ДатаКонца);
		ПараметрФормата = ОтчетОбъект.СхемаКомпоновкиДанных.Параметры.ДатаОкончания.ПараметрыРедактирования.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ФорматРедактирования"));
		Если (НЕ ПараметрФормата = Неопределено) И (НЕ ПараметрФормата.Использование) Тогда
			ПараметрФормата.Использование = Истина;
			ПараметрФормата.Значение = "ДФ=dd.MM.yyyy";
		КонецЕсли;
		
		Если (НЕ ДатаОкончанияГраница = Неопределено) Тогда
			КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончанияГраница", Новый Граница(ДатаКонца, ВидГраницы.Включая));
		КонецЕсли;
	КонецЕсли;
	
	ДатаНачала = КомпоновщикНастроек.Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаНачала"));
	Если (НЕ ДатаНачала = Неопределено) Тогда
		ДатаНач = КомпоновщикПолучитьЗначениеПараметра("ДатаНачала", КомпоновщикНастроек.Настройки);
		ДатаНач = НачалоДня(ДатаНач);
		ПараметрФормата = ОтчетОбъект.СхемаКомпоновкиДанных.Параметры.ДатаНачала.ПараметрыРедактирования.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ФорматРедактирования"));
		Если (НЕ ПараметрФормата = Неопределено) И (НЕ ПараметрФормата.Использование) Тогда
			ПараметрФормата.Использование = Истина;
			ПараметрФормата.Значение = "ДФ=dd.MM.yyyy";
		КонецЕсли;
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала", ДатаНач);
	КонецЕсли;
	
	Если НЕ ПроверитьКорректность(ОтчетОбъект, КомпоновщикНастроек) Тогда
		Возврат;
	КонецЕсли;
	
	ВыводитьЗаголовок = ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьЗаголовок");
	Если НЕ ВыводитьЗаголовок.Использование Тогда
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьЗаголовок").Значение        = ТипВыводаТекстаКомпоновкиДанных.Выводить;
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьЗаголовок").Использование   = Истина;
	
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("Заголовок").Значение                = Заголовок;
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("Заголовок").Использование           = Истина;
	КонецЕсли;
	
	ВыводитьОтбор = ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьОтбор");
	Если НЕ ВыводитьОтбор.Использование Тогда
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьОтбор").Значение            = ТипВыводаТекстаКомпоновкиДанных.Выводить;
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьОтбор").Использование       = Истина;
	КонецЕсли;
	
	ВыводитьПараметры = ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьПараметрыДанных");
	Если НЕ ВыводитьПараметры.Использование Тогда
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьПараметрыДанных").Значение      = ТипВыводаТекстаКомпоновкиДанных.Выводить;
		ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьПараметрыДанных").Использование = Истина;
	КонецЕсли;
	
	Расшифровка = Неопределено;
	//Попытка
	ОтчетОбъект.СкомпоноватьРезультат(ТабличныйДокумент, Расшифровка);
	//Исключение
	//	ПроцедурыОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	//КонецПопытки;
	
	Если (НЕ ДанныеРасшифровки = Неопределено) И (НЕ УникальныйИдентификатор = Неопределено) Тогда
		ДанныеРасшифровки = ПоместитьВоВременноеХранилище(Расшифровка, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНачальныеОстатки(ОтчетОбъект, НаборыДанных = Неопределено, СтруктураСвойств = Неопределено) Экспорт
	
	Если НаборыДанных = Неопределено Тогда
		НаборыДанных = ОтчетОбъект.СхемаКомпоновкиДанных.НаборыДанных;
	КонецЕсли;
	
	ДопСвойства = ОтчетОбъект.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	
	Если СтруктураСвойств = Неопределено Тогда
		Попытка
			СтруктураСвойств = ОтчетОбъект.ПолучитьСвойства();
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураСвойств) = Тип("Структура") Тогда
		
		Для Каждого ТекНабор Из НаборыДанных Цикл
			
			Если ТипЗнч(ТекНабор) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
				ЗаполнитьНачальныеОстатки(ОтчетОбъект, ТекНабор.Элементы, СтруктураСвойств);
				Продолжить;
			ИначеЕсли НЕ ТипЗнч(ТекНабор) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			ДоступныеПоля = Новый Структура;
			//СтруктураСвойств.Количество();
			Для Каждого ТекПоле Из СтруктураСвойств Цикл
				ДоступноеПоле = ТекНабор.Поля.Найти(ТекПоле.Ключ);
				Если НЕ ДоступноеПоле = Неопределено Тогда
					// у поля набора не назначен тип значения
					Если ДоступноеПоле.ТипЗначения.Типы().Количество() = 0 Тогда
						Если НЕ ДоступноеПоле.ОграничениеИспользованияРеквизитов.Поле Тогда
							ПолеПоиска = ОтчетОбъект.КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ТекПоле.Ключ));
							Если НЕ ПолеПоиска = Неопределено Тогда
								ДоступныеПоля.Вставить(ТекПоле.Ключ, ПолеПоиска.ТипЗначения);
							КонецЕсли;
						ИначеЕсли НЕ ДоступноеПоле.ОграничениеИспользованияРеквизитов.Условие Тогда
							ПолеПоиска = ОтчетОбъект.КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ТекПоле.Ключ));
							Если НЕ ПолеПоиска = Неопределено Тогда
								ДоступныеПоля.Вставить(ТекПоле.Ключ, ПолеПоиска.ТипЗначения);
							КонецЕсли;
						КонецЕсли;
					Иначе
						ДоступныеПоля.Вставить(ТекПоле.Ключ, ДоступноеПоле.ТипЗначения);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если ДоступныеПоля.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ИскатьСвойстваДляХарактеристики = ДоступныеПоля.Свойство("ХарактеристикаНоменклатуры");
			
			Запрос = Новый Запрос();
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	НазначенияСвойств.Ссылка КАК Назначение,
			|	НазначенияСвойств.ТипЗначения КАК ТипЗначенияНазначения,
			|	ВЫБОР
			|		КОГДА НазначенияСвойств.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ХАРАКТЕРИСТИКИ) ТОГДА
			|			ИСТИНА
			|		ИНАЧЕ
			|			ЛОЖЬ
			|	КОНЕЦ КАК ЭтоХарактеристика
			|ИЗ
			|	ПланВидовХарактеристик.НазначенияСвойствОбъектов КАК НазначенияСвойств
			|ГДЕ
			|	НазначенияСвойств.ПометкаУдаления = Ложь И 
			|	НазначенияСвойств.БазовоеНазначение = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ПустаяСсылка) И 
			|	НазначенияСвойств.ЭтоГруппа = Ложь
			|УПОРЯДОЧИТЬ ПО
			|	НазначенияСвойств.Наименование";
			
			ТекстХарактеристик = "";
			СоответствиеНазначений = Новый Соответствие;
			
			Справочник_Номенклатура = ПланыВидовХарактеристик.НазначенияСвойствОбъектов.Справочник_Номенклатура;
			
			СвойстваВыборка = Запрос.Выполнить().Выбрать();
			Пока СвойстваВыборка.Следующий() Цикл
				СтрокаТипа = "";
				ИмяПВХ     = "";
				Если НЕ СвойстваВыборка.ЭтоХарактеристика Тогда
					ТипНазначения = СвойстваВыборка.ТипЗначенияНазначения.Типы()[0];
					Для Каждого ТекПоле Из ДоступныеПоля Цикл
						Если ТекПоле.Значение.СодержитТип(ТипНазначения) И СоответствиеНазначений[СвойстваВыборка.Назначение] = Неопределено Тогда
							СоответствиеНазначений.Вставить(СвойстваВыборка.Назначение);
							МетаданныеТипа = Метаданные.НайтиПоТипу(ТипНазначения);
							СтрокаТипа     = МетаданныеТипа.ПолноеИмя();
							ИмяПВХ         = ПланыВидовХарактеристик.НазначенияСвойствОбъектов.ПолучитьИмяПредопределенного(СвойстваВыборка.Назначение);
						КонецЕсли;
					КонецЦикла;
				ИначеЕсли ИскатьСвойстваДляХарактеристики И СоответствиеНазначений[СвойстваВыборка.Назначение] = Неопределено Тогда
					СоответствиеНазначений.Вставить(СвойстваВыборка.Назначение);
					СтрокаТипа = "Справочник.ХарактеристикиНоменклатуры";
					ИмяПВХ     = "ХарактеристикиНоменклатуры";
				КонецЕсли;
				
				Если НЕ ИмяПВХ = "" Тогда
					
					ТекстХарактеристик = ТекстХарактеристик + "
					|{ХАРАКТЕРИСТИКИ
					|ТИП(" + СтрокаТипа + ")
					|ВИДЫХАРАКТЕРИСТИК (ВЫБРАТЬ
					|		НазначенияСвойств.Свойство КАК Свойство,
					|		""Свойство.("" + НазначенияСвойств.Свойство.Код + "") "" + НазначенияСвойств.Свойство.Наименование КАК Наименование,
					|		НазначенияСвойств.Свойство.ТипЗначения КАК ТипЗначенияСвойства
					|	ИЗ
					|		ПланВидовХарактеристик.НазначенияСвойствОбъектов.ВидыСвойств КАК НазначенияСвойств
					|	ГДЕ
					|		НазначенияСвойств.Ссылка.ПометкаУдаления = ЛОЖЬ
					|		И НазначенияСвойств.Ссылка.ЭтоГруппа = ЛОЖЬ
					|		И ВЫБОР
					|				КОГДА НазначенияСвойств.Ссылка.БазовоеНазначение = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов.ПустаяСсылка)
					|					ТОГДА НазначенияСвойств.Ссылка
					|				ИНАЧЕ НазначенияСвойств.Ссылка.БазовоеНазначение
					|			КОНЕЦ = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствОбъектов."+ИмяПВХ+"))
					|ПОЛЕКЛЮЧА Свойство
					|ПОЛЕИМЕНИ Наименование
					|ПОЛЕТИПАЗНАЧЕНИЯ ТипЗначенияСвойства
					|ЗНАЧЕНИЯХАРАКТЕРИСТИК РегистрСведений.ЗначенияСвойствОбъектов
					|ПОЛЕОБЪЕКТА Объект
					|ПОЛЕВИДА Свойство
					|ПОЛЕЗНАЧЕНИЯ Значение }";
					
					Если ИмяПВХ = "Справочник_Номенклатура" Тогда
						
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныШирина);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныВысота);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныРадиус);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныПрофиль);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныНазначение);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныКонструкция);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСезонность);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексСкорости);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныИндексНагрузки);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ШиныСпособГерметизации);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ДискиШирина);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ДискиМодель);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ДискиТип);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ДискиКоличествоОтверстий);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ДискиРасстояниеМеждуОтверстиями);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ДискиВылет);
						ТекстХарактеристик = ТекстХарактеристик + ПолучитьЗапросХарактеристикиДляСвойства(СтрокаТипа, ПланыВидовХарактеристик.СвойстваОбъектов.ДискиДиаметрСтупицы);
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если НЕ ТекстХарактеристик = "" И СтрНайти(ТекНабор.Запрос, ТекстХарактеристик) = 0 Тогда
				ТекНабор.Запрос = ТекНабор.Запрос + Символы.ПС + ТекстХарактеристик;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция КомпоновщикПолучитьПервуюСтрокуСтруктурыПоПолю(СтруктураОтчета) Экспорт
	
	Результат = Неопределено;
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	Для Каждого ТекЭлемент Из СтруктураОтчета Цикл
		ТипЭлемента = ТипЗнч(ТекЭлемент);
		Если ТипЭлемента = ТипСтруктурыТаблицы Тогда
			Для Каждого Строка Из ТекЭлемент.Строки Цикл
				Возврат Строка;
			КонецЦикла;
			Для Каждого Колонка Из ТекЭлемент.Колонки Цикл
				Возврат Колонка;
			КонецЦикла;
		ИначеЕсли ТипЭлемента = ТипСтруктурыДиаграммы Тогда
			Для Каждого Серия Из ТекЭлемент.Серии Цикл
				Возврат Серия;
			КонецЦикла;
			Для Каждого Точка Из ТекЭлемент.Точки Цикл
				Возврат Точка;
			КонецЦикла;
		Иначе
			Возврат ТекЭлемент;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // КомпоновщикПолучитьПервуюСтрокуСтруктурыПоПолю()

Процедура УстановитьПериодСКДПоВарианту(Настройки, ВариантЗагрузкиПериода) Экспорт
	
	Если ЗначениеЗаполнено(ВариантЗагрузкиПериода) И (НЕ ВариантЗагрузкиПериода = "ВариантСтандартногоПериода.ПроизвольныйПериод") Тогда
		
		СтандартныйПериодОтчета = Новый СтандартныйПериод(ПредопределенноеЗначение(ВариантЗагрузкиПериода));
		
		ДатаНачала = Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаНачала"));
		Если НЕ ДатаНачала = Неопределено Тогда
			Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала", СтандартныйПериодОтчета.ДатаНачала);
		КонецЕсли;
		
		ДатаОкончания = Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаОкончания"));
		Если НЕ ДатаОкончания = Неопределено Тогда
			Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", СтандартныйПериодОтчета.ДатаОкончания);
		КонецЕсли;
		
		ДатаОкончанияГраница = Настройки.ПараметрыДанных.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных("ДатаОкончанияГраница"));
		Если НЕ ДатаОкончанияГраница = Неопределено Тогда
			Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончанияГраница", Новый Граница(СтандартныйПериодОтчета.ДатаОкончания, ВидГраницы.Включая));
		КонецЕсли;
		
	КонецЕсли;
	
	ДатаОкончания = КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", Настройки);
	Если НЕ ДатаОкончания = Неопределено И НачалоДня(ДатаОкончания) = Дата(1, 1, 1) Тогда
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ТекущаяДата()));
	КонецЕсли;
	
	ДатаОкончанияГраница = КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", Настройки);
	Если НЕ ДатаОкончанияГраница = Неопределено И НачалоДня(ДатаОкончанияГраница) = Дата(1, 1, 1) Тогда
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончанияГраница", Новый Граница(КонецДня(ТекущаяДата()), ВидГраницы.Включая));
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьНастройкиВСКД(Настройки, РежимВыводаОтчета, ИзмеренияСтроки, ИзмеренияКолонки, ВариантЗагрузкиПериода = "", ОтсекатьГруппировки = Истина) Экспорт
	
	Если Настройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПериодСКДПоВарианту(Настройки, ВариантЗагрузкиПериода);
	
	СтруктураОтчета = Настройки.Структура;
	СтруктураОтчета.Очистить();
	Если РежимВыводаОтчета = 1 Тогда
		РодительГруппировки = СтруктураОтчета;
		// Группировки
		Для Каждого ТекСтрока Из ИзмеренияСтроки Цикл
			
			// Нельзя добавлять пустые группировки иначе СКД отсечет остальные
			Если ОтсекатьГруппировки И (НЕ ТекСтрока.Использование) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = РодительГруппировки.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
			НоваяСтрока.Использование = ТекСтрока.Использование;
			
			// Основная группировка
			Если НЕ СокрЛП(ТекСтрока.Поле) = "" Тогда
				НовоеПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				НовоеПолеГруппировки.Использование  = Истина;
				НовоеПолеГруппировки.Поле           = ТекСтрока.Поле;
				НовоеПолеГруппировки.ТипГруппировки = ТекСтрока.ТипГруппировки;
				
				// Дополнительные группировки
				Для Каждого ТекПоле Из ТекСтрока.ДопГруппировки Цикл
					
					Если СокрЛП(ТекПоле.Поле) = "" ИЛИ (НЕ ТекПоле.Использование) Тогда
						Продолжить;
					КонецЕсли;
					
					НовоеДопПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
					НовоеДопПолеГруппировки.Использование  = Истина;
					НовоеДопПолеГруппировки.Поле           = ТекПоле.Поле;
					НовоеДопПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
				КонецЦикла;
				
			КонецЕсли;
			
			// Поля группировки
			НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Использование     = Истина;
			Для Каждого ТекПоле Из ТекСтрока.ДопПоля Цикл
				НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				НовоеПоле.Использование = Истина;
				НовоеПоле.Поле          = ТекПоле.Поле;
			КонецЦикла;
			
			// Автоматический порядок
			НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			НовоеПоле.Использование = Истина;
			
			// Порядок группировки
			Для Каждого ТекПоле Из ТекСтрока.Порядок Цикл
				НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
				НовоеПоле.Использование     = Истина;
				НовоеПоле.Поле              = ТекПоле.Поле;
				НовоеПоле.ТипУпорядочивания = ТекПоле.ТипУпорядочивания;
			КонецЦикла;
			
			РодительГруппировки = НоваяСтрока.Структура;
			
		КонецЦикла;
		
	ИначеЕсли РежимВыводаОтчета = 2 Тогда
		
		ТаблицаСКД = СтруктураОтчета.Добавить(Тип("ТаблицаКомпоновкиДанных"));
		НовоеПоле = ТаблицаСКД.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		НовоеПоле.Использование     = Истина;
		РодительГруппировки = ТаблицаСКД.Строки;
		// Строки
		Для Каждого ТекСтрока Из ИзмеренияСтроки Цикл
			
			// Нельзя добавлять пустые группировки иначе СКД отсечет остальные
			Если ОтсекатьГруппировки И (НЕ ТекСтрока.Использование) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = РодительГруппировки.Добавить();
			НоваяСтрока.Использование = ТекСтрока.Использование;
			
			Если НЕ СокрЛП(ТекСтрока.Поле) = "" Тогда
				НовоеПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				НовоеПолеГруппировки.Использование  = Истина;
				НовоеПолеГруппировки.Поле           = ТекСтрока.Поле;
				НовоеПолеГруппировки.ТипГруппировки = ТекСтрока.ТипГруппировки;
				
				// Дополнительные группировки
				Для Каждого ТекПоле Из ТекСтрока.ДопГруппировки Цикл
					
					Если СокрЛП(ТекПоле.Поле) = "" ИЛИ (НЕ ТекПоле.Использование) Тогда
						Продолжить;
					КонецЕсли;
					
					НовоеДопПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
					НовоеДопПолеГруппировки.Использование  = Истина;
					НовоеДопПолеГруппировки.Поле           = ТекПоле.Поле;
					НовоеДопПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
				КонецЦикла;
				
			КонецЕсли;
			
			// Поля группировки
			НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Использование     = Истина;
			Для Каждого ТекПоле Из ТекСтрока.ДопПоля Цикл
				НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				НовоеПоле.Использование = Истина;
				НовоеПоле.Поле          = ТекПоле.Поле;
			КонецЦикла;
			
			// Автоматический порядок
			НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			НовоеПоле.Использование     = Истина;
			
			// Порядок группировки
			Для Каждого ТекПоле Из ТекСтрока.Порядок Цикл
				НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
				НовоеПоле.Использование     = Истина;
				НовоеПоле.Поле              = ТекПоле.Поле;
				НовоеПоле.ТипУпорядочивания = ТекПоле.ТипУпорядочивания;
			КонецЦикла;
			
			РодительГруппировки = НоваяСтрока.Структура;
			
		КонецЦикла;
		
		// Колонки
		РодительГруппировки = ТаблицаСКД.Колонки;
		Для Каждого ТекСтрока Из ИзмеренияКолонки Цикл
			
			// Нельзя добавлять пустые группировки иначе СКД отсечет остальные
			Если ОтсекатьГруппировки И (НЕ ТекСтрока.Использование) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = РодительГруппировки.Добавить();
			НоваяСтрока.Использование = ТекСтрока.Использование;
			
			Если НЕ СокрЛП(ТекСтрока.Поле) = "" Тогда
				НовоеПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				НовоеПолеГруппировки.Использование  = Истина;
				НовоеПолеГруппировки.Поле           = ТекСтрока.Поле;
				НовоеПолеГруппировки.ТипГруппировки = ТекСтрока.ТипГруппировки;
			КонецЕсли;
			
			НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Использование = Истина;
			
			// Автоматический порядок
			НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			НовоеПоле.Использование = Истина;
			
			РодительГруппировки = НоваяСтрока.Структура;
			
		КонецЦикла;
		
	Иначе
		
		ТаблицаСКД = СтруктураОтчета.Добавить(Тип("ДиаграммаКомпоновкиДанных"));
		НовоеПоле = ТаблицаСКД.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		НовоеПоле.Использование = Истина;
		РодительГруппировки = ТаблицаСКД.Серии;
		// Серии
		Для Каждого ТекСтрока Из ИзмеренияСтроки Цикл
			
			// Нельзя добавлять пустые группировки иначе СКД отсечет остальные
			Если ОтсекатьГруппировки И (НЕ ТекСтрока.Использование) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = РодительГруппировки.Добавить();
			НоваяСтрока.Использование = ТекСтрока.Использование;
			
			Если НЕ СокрЛП(ТекСтрока.Поле) = "" Тогда
				НовоеПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				НовоеПолеГруппировки.Использование  = Истина;
				НовоеПолеГруппировки.Поле           = ТекСтрока.Поле;
				НовоеПолеГруппировки.ТипГруппировки = ТекСтрока.ТипГруппировки;
				
				// Дополнительные группировки
				Для Каждого ТекПоле Из ТекСтрока.ДопГруппировки Цикл
					
					Если СокрЛП(ТекПоле.Поле) = "" ИЛИ (НЕ ТекПоле.Использование) Тогда
						Продолжить;
					КонецЕсли;
					
					НовоеДопПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
					НовоеДопПолеГруппировки.Использование  = Истина;
					НовоеДопПолеГруппировки.Поле           = ТекПоле.Поле;
					НовоеДопПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
				КонецЦикла;
				
			КонецЕсли;
			
			// Поля группировки
			НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Использование     = Истина;
			Для Каждого ТекПоле Из ТекСтрока.ДопПоля Цикл
				НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				НовоеПоле.Использование = Истина;
				НовоеПоле.Поле          = ТекПоле.Поле;
			КонецЦикла;
			
			// Автоматический порядок
			НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			НовоеПоле.Использование     = Истина;
			
			// Порядок группировки
			Для Каждого ТекПоле Из ТекСтрока.Порядок Цикл
				НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
				НовоеПоле.Использование     = Истина;
				НовоеПоле.Поле              = ТекПоле.Поле;
				НовоеПоле.ТипУпорядочивания = ТекПоле.ТипУпорядочивания;
			КонецЦикла;
			
			РодительГруппировки = НоваяСтрока.Структура;
			
		КонецЦикла;
		
		// Точки
		РодительГруппировки = ТаблицаСКД.Точки;
		Для Каждого ТекСтрока Из ИзмеренияКолонки Цикл
			// Нельзя добавлять пустые группировки иначе СКД отсечет остальные
			Если ОтсекатьГруппировки И (НЕ ТекСтрока.Использование) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = РодительГруппировки.Добавить();
			НоваяСтрока.Использование = ТекСтрока.Использование;
			
			Если НЕ СокрЛП(ТекСтрока.Поле) = "" Тогда
				НовоеПолеГруппировки = НоваяСтрока.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				НовоеПолеГруппировки.Использование  = Истина;
				НовоеПолеГруппировки.Поле           = ТекСтрока.Поле;
				НовоеПолеГруппировки.ТипГруппировки = ТекСтрока.ТипГруппировки;
			КонецЕсли;
			
			НовоеПоле = НоваяСтрока.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Использование = Истина;
			
			// Автоматический порядок
			НовоеПоле = НоваяСтрока.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			НовоеПоле.Использование = Истина;
			
			РодительГруппировки = НоваяСтрока.Структура;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьГруппировкиСтроки(Настройки, СтруктураГруппировок, Группировка)
	
	СписокСотрировки = Новый СписокЗначений;
	Для Каждого ТекПоле Из Группировка.Порядок.Элементы Цикл
		Если ТипЗнч(ТекПоле) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") ИЛИ (НЕ ТекПоле.Использование) Тогда
			Продолжить;
		КонецЕсли;
		СписокСотрировки.Добавить(ТекПоле.ТипУпорядочивания, СокрЛП(ТекПоле.Поле));
	КонецЦикла;
	
	СписокПолей = Новый СписокЗначений;
	Для Каждого ТекПоле Из Группировка.Выбор.Элементы Цикл
		Если ТипЗнч(ТекПоле) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") ИЛИ (НЕ ТекПоле.Использование) Тогда
			Продолжить;
		КонецЕсли;
		СписокПолей.Добавить(ТекПоле.Поле, СокрЛП(ТекПоле.Поле));
	КонецЦикла;
	
	ЭлементыГруппировки = Группировка.ПоляГруппировки.Элементы;
	
	Если ЭлементыГруппировки.Количество() = 0 Тогда
		// Детальные записи
		СтрокаГруппировки = СтруктураГруппировок.Добавить();
		СтрокаГруппировки.Использование  = Группировка.Использование;
		СтрокаГруппировки.Поле           = Новый ПолеКомпоновкиДанных("");
		СтрокаГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
		СтрокаГруппировки.Заголовок      = "Детальные записи";
		СтрокаГруппировки.Картинка       = БиблиотекаКартинок.ГруппировкаКомпоновкиДанных;
		
		ПредставлениеПорядка = "";
		Для Каждого ТекПоле Из СписокСотрировки Цикл
			
			ПолеСортировки = Новый ПолеКомпоновкиДанных(ТекПоле.Представление);
			ДоступныеДанные = Настройки.ДоступныеПоляПорядка.НайтиПоле(ПолеСортировки);
			Если НЕ ДоступныеДанные = Неопределено Тогда
				НоваяСтрока = СтрокаГруппировки.Порядок.Добавить();
				НоваяСтрока.Поле              = ПолеСортировки;
				НоваяСтрока.Заголовок         = СтрЗаменить(ДоступныеДанные.Заголовок, СтрокаГруппировки.Заголовок+".", "");
				НоваяСтрока.ТипУпорядочивания = ТекПоле.Значение;
				
				ПредставлениеПорядка  = ПредставлениеПорядка + ?(ПредставлениеПорядка = "", "", ", ") + НоваяСтрока.Заголовок;
				Если ТекПоле.Значение = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
					ПредставлениеПорядка = ПредставлениеПорядка + " Возр";
				Иначе
					ПредставлениеПорядка = ПредставлениеПорядка + " Убыв";
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		СтрокаГруппировки.ПредставлениеПорядка = ПредставлениеПорядка;
		
		ПредставлениеПолей = "";
		Для Каждого ТекПоле Из СписокПолей Цикл
			ДоступныеДанные = Настройки.ДоступныеПоляПорядка.НайтиПоле(ТекПоле.Значение);
			Если НЕ ДоступныеДанные = Неопределено Тогда
				НоваяСтрока = СтрокаГруппировки.ДопПоля.Добавить();
				НоваяСтрока.Поле              = ТекПоле.Значение;
				НоваяСтрока.Заголовок         = СтрЗаменить(ДоступныеДанные.Заголовок, СтрокаГруппировки.Заголовок+".", "");
				
				ПредставлениеПолей = ПредставлениеПолей + ?(ПредставлениеПолей = "", "", ", ") + НоваяСтрока.Заголовок;
			КонецЕсли;
		КонецЦикла;
		
		СтрокаГруппировки.ПредставлениеПолей = ПредставлениеПолей;
		
	Иначе
		
		ТекГруппировка = ЭлементыГруппировки[0];
		ДоступноеПоле  = Настройки.ДоступныеПоляГруппировок.НайтиПоле(ТекГруппировка.Поле);
		
		//Если (НайтиГруппировку(ТекГруппировка.Поле) = Неопределено) И (НЕ ДоступноеПоле = Неопределено) Тогда
		Если (НЕ ДоступноеПоле = Неопределено) Тогда
			
			СтрокаГруппировки = СтруктураГруппировок.Добавить();
			СтрокаГруппировки.Использование  = (ТекГруппировка.Использование И Группировка.Использование);
			СтрокаГруппировки.Поле           = ТекГруппировка.Поле;
			СтрокаГруппировки.ТипГруппировки = ТекГруппировка.ТипГруппировки;
			СтрокаГруппировки.Заголовок      = ДоступноеПоле.Заголовок;
			СтрокаГруппировки.Картинка       = БиблиотекаКартинок.ГруппировкаКомпоновкиДанных;
			
			ПредставлениеПорядка = "";
			Для Каждого ТекПоле Из СписокСотрировки Цикл
				Если Найти(ТекПоле.Представление, СокрЛП(ТекГруппировка.Поле)) = 1 Тогда
					ПолеСортировки = Новый ПолеКомпоновкиДанных(ТекПоле.Представление);
					ДоступныеДанные = Настройки.ДоступныеПоляПорядка.НайтиПоле(ПолеСортировки);
					Если НЕ ДоступныеДанные = Неопределено Тогда
						НоваяСтрока = СтрокаГруппировки.Порядок.Добавить();
						НоваяСтрока.Поле              = ПолеСортировки;
						НоваяСтрока.Заголовок         = СтрЗаменить(ДоступныеДанные.Заголовок, СтрокаГруппировки.Заголовок+".", "");
						НоваяСтрока.ТипУпорядочивания = ТекПоле.Значение;
						
						ПредставлениеПорядка  = ПредставлениеПорядка + ?(ПредставлениеПорядка = "", "", ", ") + НоваяСтрока.Заголовок;
						Если ТекПоле.Значение = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
							ПредставлениеПорядка = ПредставлениеПорядка + " Возр";
						Иначе
							ПредставлениеПорядка = ПредставлениеПорядка + " Убыв";
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если ПредставлениеПорядка = "" Тогда
				УстановитьПорядокПоУмолчанию(Настройки, СтрокаГруппировки);
			Иначе
				СтрокаГруппировки.ПредставлениеПорядка = ПредставлениеПорядка;
			КонецЕсли;
			
			ПредставлениеПолей = "";
			Для Каждого ТекПоле Из СписокПолей Цикл
				Если Найти(ТекПоле.Представление, СокрЛП(ТекГруппировка.Поле)) = 1 Тогда
					//ПолеСортировки = Новый ПолеКомпоновкиДанных(ТекПоле.Представление);
					ДоступныеДанные = Настройки.ДоступныеПоляПорядка.НайтиПоле(ТекПоле.Значение);
					Если НЕ ДоступныеДанные = Неопределено Тогда
						НоваяСтрока = СтрокаГруппировки.ДопПоля.Добавить();
						НоваяСтрока.Поле              = ТекПоле.Значение;
						НоваяСтрока.Заголовок         = СтрЗаменить(ДоступныеДанные.Заголовок, СтрокаГруппировки.Заголовок+".", "");
						
						ПредставлениеПолей = ПредставлениеПолей + ?(ПредставлениеПолей = "", "", ", ") + НоваяСтрока.Заголовок;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			СтрокаГруппировки.ПредставлениеПолей = ПредставлениеПолей;
			
			Счетчик                  = 0;
			ПредставлениеГруппировок = "";
			Для Каждого ДопГруппировка Из ЭлементыГруппировки Цикл
				// Пропустим первую строку
				Счетчик = Счетчик + 1;
				Если Счетчик = 1 Тогда
					Продолжить;
				КонецЕсли;
				
				ДоступныеДанные = Настройки.ДоступныеПоляГруппировок.НайтиПоле(ДопГруппировка.Поле);
				Если ДоступныеДанные = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = СтрокаГруппировки.ДопГруппировки.Добавить();
				НоваяСтрока.Использование = ДопГруппировка.Использование;
				НоваяСтрока.Поле          = ДопГруппировка.Поле;
				НоваяСтрока.Заголовок     = ДоступныеДанные.Заголовок;
				
				ПредставлениеГруппировок = ПредставлениеГруппировок + ?(ПредставлениеГруппировок = "", "", ", ") + НоваяСтрока.Заголовок;
				
			КонецЦикла;
			
			СтрокаГруппировки.ПредставлениеГруппировок = ПредставлениеГруппировок;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Группировка.Структура.Количество()>0 Тогда
		СтруктураОтчета = Группировка.Структура[0];
		Если ТипЗнч(СтруктураОтчета) = Тип("ГруппировкаКомпоновкиДанных") ИЛИ 
			ТипЗнч(СтруктураОтчета) = Тип("ГруппировкаТаблицыКомпоновкиДанных") ИЛИ 
			ТипЗнч(СтруктураОтчета) = Тип("ГруппировкиДиаграммыМакетаКомпоновкиДанных") Тогда
			ЗаполнитьГруппировкиСтроки(Настройки, СтруктураГруппировок, СтруктураОтчета);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьГруппировкиКолонки(Настройки, СтруктураГруппировок, Группировка)
	
	ЭлементыГруппировки = Группировка.ПоляГруппировки.Элементы;
	Для Каждого ТекГруппировка Из ЭлементыГруппировки Цикл
		
		//Если НЕ НайтиГруппировку(ТекГруппировка.Поле) = Неопределено Тогда
		//	Продолжить;
		//КонецЕсли;
		
		ДоступноеПоле = Настройки.ДоступныеПоляГруппировок.НайтиПоле(ТекГруппировка.Поле);
		Если ДоступноеПоле = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаГруппировки = СтруктураГруппировок.Добавить();
		СтрокаГруппировки.Использование  = (ТекГруппировка.Использование И Группировка.Использование);
		СтрокаГруппировки.Поле           = ТекГруппировка.Поле;
		СтрокаГруппировки.ТипГруппировки = ТекГруппировка.ТипГруппировки;
		СтрокаГруппировки.Заголовок      = ДоступноеПоле.Заголовок;
		СтрокаГруппировки.Картинка       = БиблиотекаКартинок.ГруппировкаКомпоновкиДанных;
		
	КонецЦикла;
	//СоздатьИзмеренияОтчета(Группировка.Структура, СтруктураГруппировок, Ложь);
	Если Группировка.Структура.Количество()>0 Тогда
		СтруктураОтчета = Группировка.Структура[0];
		Если ТипЗнч(СтруктураОтчета) = Тип("ГруппировкаКомпоновкиДанных") ИЛИ 
			ТипЗнч(СтруктураОтчета) = Тип("ГруппировкаТаблицыКомпоновкиДанных") ИЛИ 
			ТипЗнч(СтруктураОтчета) = Тип("ГруппировкиДиаграммыМакетаКомпоновкиДанных") Тогда
			ЗаполнитьГруппировкиКолонки(Настройки, СтруктураГруппировок, СтруктураОтчета);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьНастройкиИзСКД(Настройки, ИзмеренияСтроки, ИзмеренияКолонки) Экспорт
	
	СтруктураОтчета = Настройки.Структура[0];
	
	ИзмеренияСтроки.Очистить();
	ИзмеренияКолонки.Очистить();
	
	ТипСтруктурыТаблицы   = Тип("ТаблицаКомпоновкиДанных");
	ТипСтруктурыДиаграммы = Тип("ДиаграммаКомпоновкиДанных");
	
	ТипСтруктуры = ТипЗнч(СтруктураОтчета);
	Если ТипСтруктуры = ТипСтруктурыТаблицы Тогда
		
		Для Каждого Строка Из СтруктураОтчета.Строки Цикл
			ЗаполнитьГруппировкиСтроки(Настройки, ИзмеренияСтроки, Строка);
		КонецЦикла;
		
		Для Каждого Колонка Из СтруктураОтчета.Колонки Цикл
			ЗаполнитьГруппировкиКолонки(Настройки, ИзмеренияКолонки, Колонка);
		КонецЦикла;
		
	ИначеЕсли ТипСтруктуры = ТипСтруктурыДиаграммы Тогда
		
		Для Каждого Серия Из СтруктураОтчета.Серии Цикл
			ЗаполнитьГруппировкиСтроки(Настройки, ИзмеренияСтроки, Серия);
		КонецЦикла;
		
		Для Каждого Точка Из СтруктураОтчета.Точки Цикл
			ЗаполнитьГруппировкиКолонки(Настройки, ИзмеренияКолонки, Точка);
		КонецЦикла;
		
	Иначе
		
		ЗаполнитьГруппировкиСтроки(Настройки, ИзмеренияСтроки, СтруктураОтчета);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьПоказатели(ПоказателиСКД, ПоказателиЗапрещены = Ложь)
	
	Результат = ПоказателиЗапрещены;
	Для Каждого ТекущийПоказатель Из ПоказателиСКД.Элементы Цикл
		
		Если НЕ ТекущийПоказатель.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ТекущийПоказатель) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ТекущийПоказатель) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Результат = ЕстьПоказатели(ТекущийПоказатель, ПоказателиЗапрещены);
		Иначе
			Результат = Истина;
		КонецЕсли;
		
		Если Результат Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСтруктуруОтчета(СтрокиСтруктуры, РежимЭксперт, Уровень) Экспорт
	
	СчетчикСтруктур  = 0;
	ТекущаяСтруктура = Неопределено;
	Для Каждого СтрокаСтруктуры Из СтрокиСтруктуры Цикл
		
		//Если НЕ СтрокаСтруктуры.Использование Тогда
		//	Продолжить;
		//КонецЕсли;
		
		// Проверка на вложенные таблицы
		ТипСтруктурыОтчета = ТипЗнч(СтрокаСтруктуры);
		Если Уровень>0 И (ТипСтруктурыОтчета = Тип("ТаблицаКомпоновкиДанных") ИЛИ 
			ТипСтруктурыОтчета = Тип("ДиаграммаКомпоновкиДанных") ИЛИ 
			ТипСтруктурыОтчета = Тип("ВстроеннаяТаблица")) Тогда
			РежимЭксперт = Истина;
			Прервать;
		КонецЕсли;
		
		ТекущаяСтруктура = СтрокаСтруктуры;
		СчетчикСтруктур  = СчетчикСтруктур + 1;
		
	КонецЦикла;
	
	// Больше одной вертикальной группировки
	Если СчетчикСтруктур > 1 Тогда
		РежимЭксперт = Истина;
		ТекущаяСтруктура = Неопределено;
	//Иначе
	//	ГруппировкиЗапрещены = Ложь;
	КонецЕсли;
	
	Возврат ТекущаяСтруктура;
	
КонецФункции

Функция ПолучитьПоказатель(Показатели) Экспорт
	
	Результат = Неопределено;
	ТипГруппы = Тип("ГруппаВыбранныхПолейКомпоновкиДанных");
	Для Каждого ТекушаяСтрока Из Показатели Цикл
		
		Если НЕ ТекушаяСтрока.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ТекушаяСтрока) = ТипГруппы Тогда
			Результат = ПолучитьПоказатель(ТекушаяСтрока.Элементы);
		Иначе
			Результат = ТекушаяСтрока;
		КонецЕсли;
		
		Если НЕ Результат = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьСписокВариантовОтчета(ФормаОтчета) Экспорт
	
	ТаблицаВариантов = ФормаОтчета.ТаблицаВариантов;
	Элементы         = ФормаОтчета.Элементы;
	Команды          = ФормаОтчета.Команды;
	
	Для Каждого ТекущийВариант Из ТаблицаВариантов Цикл
		Элементы.Удалить(Элементы[ТекущийВариант.Имя]);
	КонецЦикла;
	
	ТаблицаВариантов.Очистить();
	
	Счетчик = 1;
	СписокВариантов = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьСписок(ФормаОтчета.КлючОтчета);
	Для Каждого ТекЭлемент Из СписокВариантов Цикл
		
		ИмяВарианта = "ВариантОтчета_" + Счетчик;
		
		ВариантНастроек = ТаблицаВариантов.Добавить();
		ВариантНастроек.Имя           = ИмяВарианта;
		ВариантНастроек.Идентификатор = ТекЭлемент.Значение;
		ВариантНастроек.Представление = ТекЭлемент.Представление;
		
		ИмяКоманды = "ВыбратьВариант"+ИмяВарианта;
		НоваяКоманда = Команды.Найти(ИмяКоманды);
		Если НоваяКоманда = Неопределено Тогда
			НоваяКоманда = Команды.Добавить(ИмяКоманды);
			НоваяКоманда.Действие = "ВыбратьВариант";
		КонецЕсли;
		
		НовыйВариант = Элементы.Найти(ИмяВарианта);
		Если НовыйВариант = Неопределено Тогда
			НовыйВариант = Элементы.Добавить(ИмяВарианта, Тип("КнопкаФормы"), Элементы.МенюВыборНастройки);
			НовыйВариант.ИмяКоманды = НоваяКоманда.Имя;
		КонецЕсли;
		НовыйВариант.Заголовок  = ТекЭлемент.Представление;
		
		Если ТекЭлемент.Значение = ФормаОтчета.КлючТекущегоВарианта Тогда
			НовыйВариант.Пометка = Истина;
			ЗаголовокМеню = ПолучитьЗаголовокМенюНастроекВарианта(НовыйВариант.Заголовок);
			Элементы.МенюВыборНастройки.Заголовок = "Вариант настройки: " + ЗаголовокМеню;
			Элементы.МенюВыборНастройки.Подсказка = "Вариант настройки: " + НовыйВариант.Заголовок;
		Иначе
			НовыйВариант.Пометка = Ложь;
		КонецЕсли;
		
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(ФормаОтчета.СхемаКомпоновки);
	Для Каждого ВариантНастроекКД Из СхемаКомпоновкиДанных.ВариантыНастроек Цикл
		
		ВариантыНастроекКД = ТаблицаВариантов.НайтиСтроки(Новый Структура("Идентификатор", ВариантНастроекКД.Имя));
		Если ВариантыНастроекКД.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяВарианта = "ВариантОтчета_" + Счетчик;
		
		ВариантНастроек = ТаблицаВариантов.Добавить();
		ВариантНастроек.Имя           = ИмяВарианта;
		ВариантНастроек.Идентификатор = ВариантНастроекКД.Имя;
		ВариантНастроек.Представление = ВариантНастроекКД.Представление;
		
		ИмяКоманды = "ВыбратьВариант"+ИмяВарианта;
		НоваяКоманда = Команды.Найти(ИмяКоманды);
		Если НоваяКоманда = Неопределено Тогда
			НоваяКоманда = Команды.Добавить(ИмяКоманды);
			НоваяКоманда.Действие = "ВыбратьВариант";
		КонецЕсли;
		
		НовыйВариант = Элементы.Найти(ИмяВарианта);
		Если НовыйВариант = Неопределено Тогда
			НовыйВариант = Элементы.Добавить(ИмяВарианта, Тип("КнопкаФормы"), Элементы.МенюВыборНастройки);
			НовыйВариант.ИмяКоманды = НоваяКоманда.Имя;
			НовыйВариант.Заголовок  = ВариантНастроекКД.Представление;
		КонецЕсли;
		
		Если ВариантНастроекКД.Имя = ФормаОтчета.КлючТекущегоВарианта Тогда
			НовыйВариант.Пометка = Истина;
			ЗаголовокМеню = ПолучитьЗаголовокМенюНастроекВарианта(НовыйВариант.Заголовок);
			Элементы.МенюВыборНастройки.Заголовок = "Вариант настройки: " + ЗаголовокМеню;
			Элементы.МенюВыборНастройки.Подсказка = "Вариант настройки: " + НовыйВариант.Заголовок;
		Иначе
			НовыйВариант.Пометка = Ложь;
		КонецЕсли;
		
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВидимостьПериода(ФормаОтчета) Экспорт
	
	Произвольный = (ФормаОтчета.ВидПериода = "Произвольный");
	
	Элементы = ФормаОтчета.Элементы;
	
	Если (НЕ ФормаОтчета.ЕстьКонецПериода) И (НЕ ФормаОтчета.ЕстьНачалоПериода) Тогда
		Элементы.ПодменюВариантовПериода.Видимость = Ложь;
		Элементы.ВариантПериода.Видимость = Ложь;
		Элементы.ПериодНазад.Видимость = Ложь;
		Элементы.ПериодВперед.Видимость = Ложь;
		Элементы.НачалоПериода.Видимость = Ложь;
		Элементы.КонецПериода.Видимость = Ложь;
		Элементы.УстановитьИнтервал.Видимость = Ложь;
	Иначе
		Элементы.ПодменюВариантовПериода.Видимость = Истина;
		
		Если ФормаОтчета.ЕстьНачалоПериода Тогда
			Элементы.ПериодПроизвольный.Заголовок = "Период с";
			Элементы.КонецПериода.Заголовок = "по";
			Элементы.КонецПериода.Подсказка = "Окончание периода отчета";
		Иначе
			Элементы.ПериодПроизвольный.Заголовок = "Период";
			Элементы.КонецПериода.Заголовок = "На дату";
			Элементы.КонецПериода.Подсказка = "Дата формирования отчета";
		КонецЕсли;
		
		Если Произвольный Тогда
			Элементы.ПодменюВариантовПериода.Заголовок = Элементы.ПериодПроизвольный.Заголовок;
		КонецЕсли;
		
		Элементы.ВариантПериода.Видимость     = (НЕ Произвольный);
		Элементы.ПериодНазад.Видимость        = (НЕ Произвольный);
		Элементы.ПериодВперед.Видимость       = (НЕ Произвольный);
		Элементы.НачалоПериода.Видимость      = ФормаОтчета.ЕстьНачалоПериода И (Произвольный);
		Элементы.КонецПериода.Видимость       = ФормаОтчета.ЕстьКонецПериода И (Произвольный);
		Элементы.УстановитьИнтервал.Видимость = (ФормаОтчета.ЕстьНачалоПериода ИЛИ ФормаОтчета.ЕстьКонецПериода) И (Произвольный);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗаголовокМенюНастроекВарианта(ПредставлениеТекущегоВарианта)
	
	//ЗаголовокМеню = ПредставлениеТекущегоВарианта;
	
	ДлинаПредставления = СтрДлина(ПредставлениеТекущегоВарианта);
	Если ДлинаПредставления >= 20 Тогда
		ЗаголовокМеню = Лев(ПредставлениеТекущегоВарианта, 16)+"...";
	Иначе
		КоличествоПробелов = 20 - ДлинаПредставления;
		ЗаголовокМеню = ПредставлениеТекущегоВарианта;
		Для Инд = 0 По КоличествоПробелов Цикл
			ЗаголовокМеню = ЗаголовокМеню + Символы.НПП;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЗаголовокМеню;
	
КонецФункции

Процедура УстановитьЗаголовокВарианта(ФормаОтчета, ИмяВарианта) Экспорт
	
	ПредставлениеТекущегоВарианта = ФормаОтчета.ПредставлениеТекущегоВарианта;
	Элементы = ФормаОтчета.Элементы;
	
	Для Каждого ТекВариант Из Элементы.МенюВыборНастройки.ПодчиненныеЭлементы Цикл
		Если НЕ ТипЗнч(ТекВариант) = Тип("КнопкаФормы") Тогда
			Продолжить;
		КонецЕсли;
		ТекВариант.Пометка = (ТекВариант.Имя = ИмяВарианта);
	КонецЦикла;
	
	Если (НЕ ИмяВарианта = "") И (НЕ ПредставлениеТекущегоВарианта = "") Тогда
		
		ЗаголовокМеню = ПолучитьЗаголовокМенюНастроекВарианта(ПредставлениеТекущегоВарианта);
		
		Элементы.МенюВыборНастройки.Заголовок = "Вариант настройки: " + ЗаголовокМеню;
		Элементы.МенюВыборНастройки.Подсказка = "Вариант настройки: " + ПредставлениеТекущегоВарианта;
	КонецЕсли;
	
КонецПроцедуры

Функция ДобавитьГруппировкуДиаграммы(ТаблицаГруппировок, ПолеКомопоновки, ДоступныеНастройки, КоличествоЗаписей) Экспорт
	
	// Добавим точки диаграммы
	ПолеДиаграммы = КомпоновщикПолучитьПолеКомпоновки(ПолеКомопоновки);
	Если НЕ ДоступныеНастройки.НайтиПоле(ПолеДиаграммы) = Неопределено Тогда
		
		НоваяГруппировка = ТаблицаГруппировок.Добавить();
		
		НовоеПолеГруппировки = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		НовоеПолеГруппировки.Использование = Истина;
		НовоеПолеГруппировки.Поле          = ПолеДиаграммы;
		
		// Автоматическое поле
		ПолеРеквизитаСКД = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		ПолеРеквизитаСКД.Использование = Истина;
		
		// Автоматический порядок
		ПолеПорядкаСКД = НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		ПолеПорядкаСКД.Использование = Истина;
		
		Если КоличествоЗаписей > 0 Тогда
			НоваяГруппировка.ПараметрыВывода.УстановитьЗначениеПараметра("КоличествоЗаписей", КоличествоЗаписей);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции


#Область Краткая_форма

// Заполнение элемента формы настроек из настройки СКД для краткой формы отчета
Процедура ЗаполнитьСтруктуруОтчета(ФормаОтчета, ОбъектНастройки, КоллекцияЭлементов = Неопределено, РежимЭксперт = Ложь, Уровень = 0) Экспорт
	
	Если РежимЭксперт Тогда
		Возврат;
	КонецЕсли;
	
	Отчет    = ФормаОтчета.Отчет;
	Элементы = ФормаОтчета.Элементы;
	
	УровеньПодчиненных = Уровень + 1;
	
	ТипНастройки = ТипЗнч(ОбъектНастройки);
	
	Если ТипНастройки = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") Тогда
		
		Для Каждого ТекущаяСтруктура Из ОбъектНастройки Цикл
			
			Если НЕ ТекущаяСтруктура.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяТаблица = Неопределено;
			
			ТипСтруктурыОтчета = ТипЗнч(ТекущаяСтруктура);
			Если ТипСтруктурыОтчета = Тип("ТаблицаКомпоновкиДанных") Тогда
				
				ИдентификаторСтроки = ДобавитьТаблицуОтчета(ФормаОтчета, "Таблица", Ложь);
				НоваяТаблица = ФормаОтчета.СписокТаблиц.НайтиПоИдентификатору(ИдентификаторСтроки);
				
				ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура.Строки,  НоваяТаблица.ГруппировкиСтрок, РежимЭксперт, УровеньПодчиненных);
				ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура.Колонки, НоваяТаблица.ГруппировкиКолонок, РежимЭксперт, УровеньПодчиненных);
				ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяТаблица.ПараметрыВывода.ПолучитьЭлементы(), ТекущаяСтруктура.ПараметрыВывода.Элементы, ТекущаяСтруктура.ПараметрыВывода.ДоступныеПараметры);
				// Условное оформление
				НоваяТаблица.УсловноеОформление = ТекущаяСтруктура.УсловноеОформление;
				
			ИначеЕсли ТипСтруктурыОтчета = Тип("ДиаграммаКомпоновкиДанных") Тогда
				
				ИдентификаторСтроки = ДобавитьТаблицуОтчета(ФормаОтчета, "Диаграмма", Ложь);
				
				НоваяТаблица = ФормаОтчета.СписокТаблиц.НайтиПоИдентификатору(ИдентификаторСтроки);
				
				ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура.Серии, НоваяТаблица.ГруппировкиКолонок, РежимЭксперт, УровеньПодчиненных);
				ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура.Точки, НоваяТаблица.ГруппировкиСтрок, РежимЭксперт, УровеньПодчиненных);
				ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяТаблица.ПараметрыВывода.ПолучитьЭлементы(), ТекущаяСтруктура.ПараметрыВывода.Элементы, ТекущаяСтруктура.ПараметрыВывода.ДоступныеПараметры);
				// Условное оформление
				НоваяТаблица.УсловноеОформление = ТекущаяСтруктура.УсловноеОформление;
				
			Иначе
				
				ИдентификаторСтроки = ДобавитьТаблицуОтчета(ФормаОтчета, "Таблица", Ложь);
				
				НоваяТаблица = ФормаОтчета.СписокТаблиц.НайтиПоИдентификатору(ИдентификаторСтроки);
				
				ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура, НоваяТаблица.ГруппировкиСтрок, РежимЭксперт, УровеньПодчиненных);
				ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяТаблица.ПараметрыВывода.ПолучитьЭлементы(), ТекущаяСтруктура.ПараметрыВывода.Элементы, ТекущаяСтруктура.ПараметрыВывода.ДоступныеПараметры);
				// Условное оформление
				НоваяТаблица.УсловноеОформление = ТекущаяСтруктура.УсловноеОформление;
				
			КонецЕсли;
			
			ПараметрОформления = ТекущаяСтруктура.ПараметрыВывода.Элементы.Найти("РасположениеРеквизитов");
			Если (НЕ ПараметрОформления = Неопределено) И ПараметрОформления.Использование Тогда
				НоваяТаблица.РеквизитыОтдельно = (ПараметрОформления.Значение = РасположениеРеквизитовКомпоновкиДанных.Отдельно);
				ФормаОтчета.Элементы.ТаблицаРеквизитыОтдельно.Пометка = НоваяТаблица.РеквизитыОтдельно;
			КонецЕсли;
			
			ПараметрОформления = ТекущаяСтруктура.ПараметрыВывода.Элементы.Найти("МакетОформления");
			Если (НЕ ПараметрОформления = Неопределено) И ПараметрОформления.Использование Тогда
				НоваяТаблица.ОформлениеТаблицы = ПараметрОформления.Значение;
			Иначе
				ПараметрОформления = Отчет.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("МакетОформления");
				Если (НЕ ПараметрОформления = Неопределено) И ПараметрОформления.Использование Тогда
					НоваяТаблица.ОформлениеТаблицы = ПараметрОформления.Значение;
				Иначе
					НоваяТаблица.ОформлениеТаблицы = БиблиотекаМакетовОформленияКомпоновкиДанных[1].Имя;
				КонецЕсли;
			КонецЕсли;
			
			ПараметрТипДиаграммы = ТекущаяСтруктура.ПараметрыВывода.Элементы.Найти("ТипДиаграммы");
			Если НЕ ПараметрТипДиаграммы = Неопределено Тогда
				НоваяТаблица.ТипДиаграммы            = СтрЗаменить(ПолучитьПолноеИмяПредопределенногоЗначения(ПараметрТипДиаграммы.Значение), "ТипДиаграммы.", "");
				НоваяТаблица.ОтображатьТаблицуДанных = ПараметрТипДиаграммы.ЗначенияВложенныхПараметров.Найти("ТипДиаграммы.ОтображатьТаблицуДанных").Значение;
				НоваяТаблица.ВидПодписей             = ПолучитьПолноеИмяПредопределенногоЗначения(ПараметрТипДиаграммы.ЗначенияВложенныхПараметров.Найти("ТипДиаграммы.ВидПодписей").Значение);
			Иначе
				ПараметрОформления = Отчет.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("ТипДиаграммы");
				Если (НЕ ПараметрОформления = Неопределено) И ПараметрОформления.Использование Тогда
					НоваяТаблица.ТипДиаграммы            = СтрЗаменить(ПолучитьПолноеИмяПредопределенногоЗначения(ПараметрОформления.Значение), "ТипДиаграммы.", "");
					НоваяТаблица.ОтображатьТаблицуДанных = ПараметрОформления.ЗначенияВложенныхПараметров.Найти("ТипДиаграммы.ОтображатьТаблицуДанных").Значение;
					НоваяТаблица.ВидПодписей             = ПолучитьПолноеИмяПредопределенногоЗначения(ПараметрОформления.ЗначенияВложенныхПараметров.Найти("ТипДиаграммы.ВидПодписей").Значение);
				Иначе
					НоваяТаблица.ТипДиаграммы            = "Гистограмма";
					НоваяТаблица.ОтображатьТаблицуДанных = Ложь;
					НоваяТаблица.ВидПодписей             = ПолучитьПолноеИмяПредопределенногоЗначения(ВидПодписейКДиаграмме.Значение);
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ НоваяТаблица = Неопределено Тогда
				
				ЕстьРесурсы = ЕстьПоказатели(ТекущаяСтруктура.Выбор);
				Если ЕстьРесурсы Тогда
					ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяТаблица.Показатели, ТекущаяСтруктура.Выбор.Элементы, ТекущаяСтруктура.Выбор.ДоступныеПоляВыбора);
				Иначе
					ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяТаблица.Показатели, Отчет.КомпоновщикНастроек.Настройки.Выбор.Элементы, Отчет.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора);
				КонецЕсли;
				
				СтрокаПоказателя = ПолучитьПоказатель(НоваяТаблица.Показатели.Элементы);
				Если НЕ СтрокаПоказателя = Неопределено Тогда
					
					ДоступноеПолеПоказателя = ТекущаяСтруктура.Выбор.ДоступныеПоляВыбора.НайтиПоле(СтрокаПоказателя.Поле);
					Если ДоступноеПолеПоказателя = Неопределено Тогда
						ПоказательЗаголовок = СтрокаПоказателя.Заголовок;
					Иначе
						ПоказательЗаголовок = ОтчетыКлиентСервер.ПолучитьЗаголовокПоля(ДоступноеПолеПоказателя);
					КонецЕсли;
					
					НоваяТаблица.Показатель          = СтрокаПоказателя.Поле;
					НоваяТаблица.ПоказательЗаголовок = ПоказательЗаголовок;
					
				КонецЕсли;
				
				Для Каждого ТекущаяСтрока Из НоваяТаблица.ГруппировкиСтрок Цикл
					
					Если НЕ ТекущаяСтрока.Использование Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаГруппировки = Неопределено;
					Для Каждого ТекущееПоле Из ТекущаяСтрока.ПоляГруппировки.Элементы Цикл
						Если ТекущееПоле.Использование Тогда
							СтрокаГруппировки = ТекущееПоле;
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ СтрокаГруппировки = Неопределено Тогда
						НоваяТаблица.Точки                  = СтрокаГруппировки.Поле;
						НоваяТаблица.КоличествоЗаписейТочек = ТекущаяСтрока.КоличествоЗаписей;
						
						ДоступноеПоле = Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.Элементы.Найти(СтрокаГруппировки.Поле);
						Если ДоступноеПоле = Неопределено Тогда
							НоваяТаблица.ТочкиЗаголовок     = Строка(СтрокаГруппировки.Поле);
						Иначе
							НоваяТаблица.ТочкиЗаголовок     = ДоступноеПоле.Заголовок;
						КонецЕсли;
						
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Для Каждого ТекущаяСтрока Из НоваяТаблица.ГруппировкиКолонок Цикл
					
					Если НЕ ТекущаяСтрока.Использование Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаГруппировки = Неопределено;
					Для Каждого ТекущееПоле Из ТекущаяСтрока.ПоляГруппировки.Элементы Цикл
						Если ТекущееПоле.Использование Тогда
							СтрокаГруппировки = ТекущееПоле;
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ СтрокаГруппировки = Неопределено Тогда
						НоваяТаблица.Серии                  = СтрокаГруппировки.Поле;
						НоваяТаблица.КоличествоЗаписейСерий = ТекущаяСтрока.КоличествоЗаписей;
						
						ДоступноеПоле = Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.Элементы.Найти(СтрокаГруппировки.Поле);
						Если ДоступноеПоле = Неопределено Тогда
							НоваяТаблица.СерииЗаголовок     = Строка(СтрокаГруппировки.Поле);
						Иначе
							НоваяТаблица.СерииЗаголовок     = ДоступноеПоле.Заголовок;
						КонецЕсли;
						
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
				Если НоваяТаблица.СерииЗаголовок = "" Тогда
					СчетчикГруппировки = 0;
					Для Каждого ТекущаяСтрока Из НоваяТаблица.ГруппировкиСтрок Цикл
						
						Если НЕ ТекущаяСтрока.Использование Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокаГруппировки = Неопределено;
						Для Каждого ТекущееПоле Из ТекущаяСтрока.ПоляГруппировки.Элементы Цикл
							Если ТекущееПоле.Использование Тогда
								СтрокаГруппировки = ТекущееПоле;
								Прервать;
							КонецЕсли;
							
						КонецЦикла;
						
						Если НЕ СтрокаГруппировки = Неопределено Тогда
							Если СчетчикГруппировки = 0 Тогда
								СчетчикГруппировки = 1;
							Иначе
								НоваяТаблица.Серии                  = СтрокаГруппировки.Поле;
								НоваяТаблица.КоличествоЗаписейСерий = ТекущаяСтрока.КоличествоЗаписей;
								ДоступноеПоле = Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.Элементы.Найти(СтрокаГруппировки.Поле);
								Если ДоступноеПоле = Неопределено Тогда
									НоваяТаблица.СерииЗаголовок     = Строка(СтрокаГруппировки.Поле);
								Иначе
									НоваяТаблица.СерииЗаголовок     = ДоступноеПоле.Заголовок;
								КонецЕсли;
								
								Прервать;
								
							КонецЕсли;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		
		ТекущаяСтруктура = ПолучитьСтруктуруОтчета(ОбъектНастройки, РежимЭксперт, Уровень);
		Если РежимЭксперт Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура, КоллекцияЭлементов, РежимЭксперт, УровеньПодчиненных);
		
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") Тогда
		
		ТекущаяСтруктура = ПолучитьСтруктуруОтчета(ОбъектНастройки, РежимЭксперт, Уровень);
		Если РежимЭксперт Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура, КоллекцияЭлементов, РежимЭксперт, УровеньПодчиненных);
		
	ИначеЕсли ТипНастройки = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		
		НоваяГруппировка = КоллекцияЭлементов.Добавить();
		НоваяГруппировка.Использование = ОбъектНастройки.Использование;
		НоваяГруппировка.КартинкаПоля  = БиблиотекаКартинок.Реквизит;
		
		ПараметрВывода = ОбъектНастройки.ПараметрыВывода.Элементы.Найти("КоличествоЗаписей");
		Если НЕ ПараметрВывода = Неопределено И ПараметрВывода.Использование Тогда
			НоваяГруппировка.КоличествоЗаписей = ПараметрВывода.Значение;
		КонецЕсли;
		
		// Параметры вывода группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.ПараметрыВывода.ПолучитьЭлементы(), ОбъектНастройки.ПараметрыВывода.Элементы, ОбъектНастройки.ПараметрыВывода.ДоступныеПараметры);
		// Поля группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.ПоляГруппировки, ОбъектНастройки.ПоляГруппировки.Элементы, ОбъектНастройки.ПоляГруппировки.ДоступныеПоляПолейГруппировок, НоваяГруппировка);
		// Реквизиты группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.Выбор, ОбъектНастройки.Выбор.Элементы, ОбъектНастройки.Выбор.ДоступныеПоляВыбора, НоваяГруппировка);
		// Порядок группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.Порядок, ОбъектНастройки.Порядок.Элементы, ОбъектНастройки.Порядок.ДоступныеПоляПорядка, НоваяГруппировка);
		// Условное оформление
		НоваяГруппировка.УсловноеОформление = ОбъектНастройки.УсловноеОформление;
		
		НоваяГруппировка.Отбор = ОбъектНастройки.Отбор;
		
		ТекущаяСтруктура = ПолучитьСтруктуруОтчета(ОбъектНастройки.Структура, РежимЭксперт, Уровень);
		Если РежимЭксперт Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура, КоллекцияЭлементов, РежимЭксперт, УровеньПодчиненных);
		
	ИначеЕсли ТипНастройки = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		НоваяГруппировка = КоллекцияЭлементов.Добавить();
		НоваяГруппировка.Использование = ОбъектНастройки.Использование;
		
		ПараметрВывода = ОбъектНастройки.ПараметрыВывода.Элементы.Найти("КоличествоЗаписей");
		Если НЕ ПараметрВывода = Неопределено И ПараметрВывода.Использование Тогда
			НоваяГруппировка.КоличествоЗаписей = ПараметрВывода.Значение;
		КонецЕсли;
		
		// Параметры вывода группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.ПараметрыВывода.ПолучитьЭлементы(), ОбъектНастройки.ПараметрыВывода.Элементы, ОбъектНастройки.ПараметрыВывода.ДоступныеПараметры);
		// Поля группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.ПоляГруппировки, ОбъектНастройки.ПоляГруппировки.Элементы, ОбъектНастройки.ПоляГруппировки.ДоступныеПоляПолейГруппировок, НоваяГруппировка);
		// Реквизиты группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.Выбор, ОбъектНастройки.Выбор.Элементы, ОбъектНастройки.Выбор.ДоступныеПоляВыбора, НоваяГруппировка);
		// Порядок группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.Порядок, ОбъектНастройки.Порядок.Элементы, ОбъектНастройки.Порядок.ДоступныеПоляПорядка, НоваяГруппировка);
		// Условное оформление
		НоваяГруппировка.УсловноеОформление = ОбъектНастройки.УсловноеОформление;
		
		НоваяГруппировка.Отбор = ОбъектНастройки.Отбор;
		
		ТекущаяСтруктура = ПолучитьСтруктуруОтчета(ОбъектНастройки.Структура, РежимЭксперт, Уровень);
		Если РежимЭксперт Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура, КоллекцияЭлементов, РежимЭксперт, УровеньПодчиненных);
		
	ИначеЕсли ТипНастройки = Тип("ГруппировкаКомпоновкиДанных") Тогда
		
		НоваяГруппировка = КоллекцияЭлементов.Добавить();
		НоваяГруппировка.Использование = ОбъектНастройки.Использование;
		
		ПараметрВывода = ОбъектНастройки.ПараметрыВывода.Элементы.Найти("КоличествоЗаписей");
		Если НЕ ПараметрВывода = Неопределено И ПараметрВывода.Использование Тогда
			НоваяГруппировка.КоличествоЗаписей = ПараметрВывода.Значение;
		КонецЕсли;
		
		// Параметры вывода группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.ПараметрыВывода.ПолучитьЭлементы(), ОбъектНастройки.ПараметрыВывода.Элементы, ОбъектНастройки.ПараметрыВывода.ДоступныеПараметры);
		// Поля группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.ПоляГруппировки, ОбъектНастройки.ПоляГруппировки.Элементы, ОбъектНастройки.ПоляГруппировки.ДоступныеПоляПолейГруппировок, НоваяГруппировка);
		// Реквизиты группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.Выбор, ОбъектНастройки.Выбор.Элементы, ОбъектНастройки.Выбор.ДоступныеПоляВыбора, НоваяГруппировка);
		// Порядок группировки
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НоваяГруппировка.Порядок, ОбъектНастройки.Порядок.Элементы, ОбъектНастройки.Порядок.ДоступныеПоляПорядка, НоваяГруппировка);
		// Условное оформление
		НоваяГруппировка.УсловноеОформление = ОбъектНастройки.УсловноеОформление;
		
		НоваяГруппировка.Отбор = ОбъектНастройки.Отбор;
		
		ТекущаяСтруктура = ПолучитьСтруктуруОтчета(ОбъектНастройки.Структура, РежимЭксперт, Уровень);
		Если РежимЭксперт Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьСтруктуруОтчета(ФормаОтчета, ТекущаяСтруктура, КоллекцияЭлементов, РежимЭксперт, УровеньПодчиненных);
		
	Иначе
		
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, КоллекцияЭлементов, ОбъектНастройки);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняем настройки таблицы из несвязанных элементов формы отчета
Процедура ЗаполнитьНастройкиТаблицыИзСтраницы(ФормаОтчета) Экспорт
	
	Элементы = ФормаОтчета.Элементы;
	
	ТекущаяСтрока = Элементы.СписокТаблиц.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиТаблицы = ФормаОтчета.СписокТаблиц.НайтиПоИдентификатору(ТекущаяСтрока);
	
	//// Заполним показатели
	ДеревоПоказателей = ФормаОтчета.Показатели.ПолучитьЭлементы();
	//Если ДеревоПоказателей.Количество()>0 Тогда
	//	ПоказателиТаблицы = НастройкиТаблицы.Показатели.ПолучитьЭлементы();
	//	ПоказателиТаблицы.Очистить();
	//	ЗаполнитьНастройкиОтчета(ФормаОтчета, ПоказателиТаблицы, ДеревоПоказателей);
	//КонецЕсли;
	// Отображение показателей на форме отчета
	//ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НастройкиТаблицы.Показатели, ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки.Выбор.Элементы, ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора);
	//ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, НастройкиТаблицы.Показатели, ДеревоПоказателей);
	ОтчетыКлиентСервер.ЗаполнитьНастройкиСКД(НастройкиТаблицы.Показатели.Элементы, ДеревоПоказателей);
	
	Для Каждого ТекущееОформление Из Элементы.ГруппаОформлениеТаблицы.ПодчиненныеЭлементы Цикл
		Если ТекущееОформление.Пометка Тогда
			НастройкиТаблицы.ОформлениеТаблицы = СтрЗаменить(ТекущееОформление.Имя, "Оформление", "");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	НастройкиТаблицы.РеквизитыОтдельно = Элементы.ТаблицаРеквизитыОтдельно.Пометка;
	
КонецПроцедуры // ЗаполнитьНастройкиТаблицыИзСтраницы()

// Заполняем несвязанные элементы формы отчета из настроек таблицы
Процедура ЗаполнитьНастройкиСтраницыИзТаблицы(ФормаОтчета)
	
	Элементы = ФормаОтчета.Элементы;
	
	СтрокиТаблицы = ФормаОтчета.СписокТаблиц.НайтиСтроки(Новый Структура("Идентификатор", ФормаОтчета.ИдентификаторТаблицы));
	Если СтрокиТаблицы.Количество()>0 Тогда
		ТекущиеДанные = СтрокиТаблицы[0];
		// Отображение показателей на форме отчета
		ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, ФормаОтчета.Показатели.ПолучитьЭлементы(), ТекущиеДанные.Показатели.Элементы, ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора);
		
		Если ТекущиеДанные.ОформлениеТаблицы = "" Тогда
			МакетОформления = БиблиотекаМакетовОформленияКомпоновкиДанных[1];
		Иначе
			МакетОформления = БиблиотекаМакетовОформленияКомпоновкиДанных[ТекущиеДанные.ОформлениеТаблицы];
		КонецЕсли;
		
		ИмяОформления = "Оформление" + МакетОформления.Имя;
		Для Каждого ТекущееОформление Из Элементы.ГруппаОформлениеТаблицы.ПодчиненныеЭлементы Цикл
			ТекущееОформление.Пометка = Ложь;
			Если ТекущееОформление.Имя = ИмяОформления Тогда
				
				ТекущееОформление.Пометка = Истина;
				Элементы.ГруппаОформлениеТаблицы.Заголовок = МакетОформления.Представление;
				
			КонецЕсли;
		КонецЦикла;
		
		Элементы.ТаблицаРеквизитыОтдельно.Пометка = ТекущиеДанные.РеквизитыОтдельно;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьНастройкиСтраницыИзТаблицы()

// При переходе на следующую страницу перемещаем таблицы структуры отчета
Процедура ПереместитьТаблицуОтчета(ФормаОтчета, ИндексСтраницы) Экспорт
	
	// сохраняем несвязанные поля формы в таблицу отчета
	ЗаполнитьНастройкиТаблицыИзСтраницы(ФормаОтчета);
	
	Элементы = ФормаОтчета.Элементы;
	
	ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы[ИндексСтраницы];
	
	Для Каждого ТекущийЭлемент Из ТекущаяСтраница.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(ТекущийЭлемент) = Тип("ДекорацияФормы") Тогда
			
			// Если прошлая страница это шаблон, то не будем на нее перемещать декорацию
			Если Элементы.Страницы.ПодчиненныеЭлементы.Индекс(Элементы.ГруппаСтруктураТаблицы.Родитель) = 0 Тогда
				Элементы.Удалить(ТекущийЭлемент);
			Иначе
				Элементы.Переместить(ТекущийЭлемент, Элементы.ГруппаСтруктураТаблицы.Родитель);
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьНастройкиСтраницыИзТаблицы(ФормаОтчета);
	
	Элементы.Переместить(Элементы.ГруппаСтруктураТаблицы, ТекущаяСтраница);
	
КонецПроцедуры // ПереместитьТаблицуОтчета()

// Добавляем новую таблицу отчета
Функция ДобавитьТаблицуОтчета(ФормаОтчета, ВидТаблицы = "Таблица", УстановитьТекущуюСтраницу = Истина) Экспорт
	
	ИдентификаторТаблицы = "Таблица" + Строка(ФормаОтчета.КоличествоТаблиц);
	
	ФормаОтчета.ИдентификаторТаблицы = ИдентификаторТаблицы;
	
	Элементы = ФормаОтчета.Элементы;
	КоличествоТаблиц = ФормаОтчета.КоличествоТаблиц;
	
	НоваяТаблица = ФормаОтчета.СписокТаблиц.Добавить();
	НоваяТаблица.Использование = Истина;
	НоваяТаблица.Идентификатор = ИдентификаторТаблицы;
	НоваяТаблица.ВидТаблицы    = ВидТаблицы;
	НоваяТаблица.Заголовок     = ВидТаблицы + " №" + Строка(КоличествоТаблиц);
	НоваяТаблица.НомерТаблицы  = КоличествоТаблиц;
	
	НоваяСтраница = Элементы.Добавить(ИдентификаторТаблицы, Тип("ГруппаФормы"), Элементы.Страницы);
	НоваяСтраница.Вид       = ВидГруппыФормы.Страница;
	НоваяСтраница.Заголовок = НоваяТаблица.Заголовок;
	
	Если ВидТаблицы = "Таблица" Тогда
		НоваяСтраница.Картинка = БиблиотекаКартинок.Таблица;
	ИначеЕсли ВидТаблицы = "Диаграмма" Тогда
		НоваяСтраница.Картинка = БиблиотекаКартинок.Диаграмма;
	КонецЕсли;
	
	Если УстановитьТекущуюСтраницу Тогда
		
		ЗаполнитьНастройкиТаблицыИзСтраницы(ФормаОтчета);
		
		ДеревоПоказателей = ФормаОтчета.Показатели.ПолучитьЭлементы();
		ДеревоПоказателей.Очистить();
		//ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
		
		Элементы.Переместить(НоваяСтраница, Элементы.Страницы, Элементы.ДобавитьТаблицу);
		Элементы.Страницы.ТекущаяСтраница = НоваяСтраница;
		
		ПустаяДекорация = Элементы.Добавить("Декорация"+ИдентификаторТаблицы, Тип("ДекорацияФормы"), Элементы.ГруппаСтруктураТаблицы.Родитель);
		
		Элементы.Переместить(Элементы.ГруппаСтруктураТаблицы, НоваяСтраница);
		
	Иначе
		
		ПустаяДекорация = Элементы.Добавить("Декорация"+ИдентификаторТаблицы, Тип("ДекорацияФормы"), НоваяСтраница);
		
	КонецЕсли;
	
	ФормаОтчета.КоличествоТаблиц = КоличествоТаблиц + 1;
	
	Возврат НоваяТаблица.ПолучитьИдентификатор();
	
КонецФункции

// Загрузка настроек из СКД в краткую форму отчета
Процедура ОбновитьНастройкиОтчета(ФормаОтчета, Настройки, РежимЭксперт) Экспорт
	
	Элементы = ФормаОтчета.Элементы;
	
	// Переместим структуру отчета на страницу шаблона
	ПереместитьТаблицуОтчета(ФормаОтчета, 0);
	
	ФормаОтчета.КоличествоТаблиц = 1;
	КоличествоСтраниц = Элементы.Страницы.ПодчиненныеЭлементы.Количество()-2;
	Пока КоличествоСтраниц > 0 Цикл
		Элементы.Удалить(Элементы.Страницы.ПодчиненныеЭлементы[КоличествоСтраниц]);
		КоличествоСтраниц = КоличествоСтраниц - 1;
	КонецЦикла;
	
	//ДеревоОтбор       = ФормаОтчета.Отбор.ПолучитьЭлементы();
	ДеревоПоказателей = ФормаОтчета.Показатели.ПолучитьЭлементы();
	//ДеревоПараметрыДанных = ФормаОтчета.ПараметрыДанных.ПолучитьЭлементы();
	
	//ДеревоОтбор.Очистить();
	//ДеревоПоказателей.Очистить();
	//ДеревоПараметрыДанных.Очистить();
	
	ФормаОтчета.СписокТаблиц.Очистить();
	
	//ЗаполнитьНастройкиОтчета(ФормаОтчета, ДеревоОтбор, Настройки.Отбор.Элементы, Настройки.Отбор.ДоступныеПоляОтбора);
	//ЗаполнитьНастройкиОтчета(ФормаОтчета, ДеревоПараметрыДанных, Настройки.ПараметрыДанных.Элементы, Настройки.ПараметрыДанных.ДоступныеПараметры);
	//ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, ДеревоПоказателей, Настройки.Выбор.Элементы, Настройки.Выбор.ДоступныеПоляВыбора);
	ЗаполнитьСтруктуруОтчета(ФормаОтчета, Настройки.Структура,, РежимЭксперт);
	
	Элементы.Переместить(Элементы.ДобавитьТаблицу, Элементы.Страницы);
	
	// Очистим вспомогательное дерево
	ДеревоПоказателей.Очистить();
	Если Элементы.Страницы.ПодчиненныеЭлементы.Количество()>2 Тогда
		
		// Установим идентификатор от первой таблицы
		Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы[1];
		
		ИдентификаторТаблицы = Элементы.Страницы.ТекущаяСтраница.Имя;
		Если ИдентификаторТаблицы = "СтраницаШаблон" Тогда
			ИдентификаторТаблицы = "";
		КонецЕсли;
		
		ФормаОтчета.ИдентификаторТаблицы = ИдентификаторТаблицы;
		
		// Установим строку списка таблиц по идентификатору
		СтрокиТаблицы = ФормаОтчета.СписокТаблиц.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТаблицы));
		Если СтрокиТаблицы.Количество()>0 Тогда
			ТекущаяСтрока = СтрокиТаблицы[0];
			Элементы.СписокТаблиц.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
			
			ОтчетыКлиентСервер.ЗаполнитьНастройкиОтчета(ФормаОтчета, ДеревоПоказателей, ТекущаяСтрока.Показатели.Элементы, Настройки.Выбор.ДоступныеПоляВыбора);
			
			ВыбратьВидТаблицы(ФормаОтчета, ТекущаяСтрока.ВидТаблицы, ТекущаяСтрока.НомерТаблицы);
			
			МакетОформления = ?(ТекущаяСтрока.ОформлениеТаблицы = "", БиблиотекаМакетовОформленияКомпоновкиДанных[0], БиблиотекаМакетовОформленияКомпоновкиДанных[ТекущаяСтрока.ОформлениеТаблицы]);
			ИмяОформления = "Оформление" + МакетОформления.Имя;
			Для Каждого ТекущееОформление Из Элементы.ГруппаОформлениеТаблицы.ПодчиненныеЭлементы Цикл
				
				ТекущееОформление.Пометка = Ложь;
				Если ТекущееОформление.Имя = ИмяОформления Тогда
					ТекущееОформление.Пометка = Истина;
					Элементы.ГруппаОформлениеТаблицы.Заголовок = МакетОформления.Представление;
				КонецЕсли;
				
			КонецЦикла;
			
			ЭтоДвухмернаяДиаграмма = ОтчетыКлиентСервер.ДвухмернаяДиаграмма(ТекущаяСтрока.ТипДиаграммы);
			Элементы.Точки.Доступность                  = ЭтоДвухмернаяДиаграмма;
			Элементы.КоличествоЗаписейТочек.Доступность = ЭтоДвухмернаяДиаграмма;
			
		КонецЕсли;
		
		// Переместим структуру отчета на первую страницу
		ПереместитьТаблицуОтчета(ФормаОтчета, 1);
		
	КонецЕсли;
	
	//ВариантЗагрузкиПериода = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьВариантПериода(ФормаОтчета.КлючОтчета, ФормаОтчета.КлючТекущегоВарианта);
	//
	//Если ВариантЗагрузкиПериода = "" Тогда
	//	Попытка
	//		МенеджерОтчета = Отчеты[СтрЗаменить(ФормаОтчета.КлючОтчета, "Отчет.", "")];
	//		Результат = МенеджерОтчета.ПолучитьВариантПериода();
	//		Если ТипЗнч(Результат) = Тип("ВариантСтандартногоПериода") Тогда
	//			Если Результат = ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
	//				Результат = "ВариантСтандартногоПериода.ЭтотМесяц";
	//			Иначе
	//				Результат = ПолучитьПолноеИмяПредопределенногоЗначения(Результат);
	//			КонецЕсли;
	//		КонецЕсли;
	//		ВариантЗагрузкиПериода = Результат;
	//	Исключение
	//		ВариантЗагрузкиПериода = "ВариантСтандартногоПериода.ЭтотМесяц";
	//	КонецПопытки;
	//КонецЕсли;
	//
	//Если НЕ ВариантЗагрузкиПериода = "ВариантСтандартногоПериода.ПроизвольныйПериод" Тогда
	//	СтандартныйПериодОтчета = Новый СтандартныйПериод(ПредопределенноеЗначение(ВариантЗагрузкиПериода));
	//	ФормаОтчета.НачалоПериода = СтандартныйПериодОтчета.ДатаНачала;
	//	ФормаОтчета.КонецПериода  = СтандартныйПериодОтчета.ДатаОкончания;
	//КонецЕсли;
	//
	//ОтчетыКлиентСервер.ОбновитьВариантыПериодов(ФормаОтчета);
	//ОтчетыКлиентСервер.УстановитьПометкуНаПериод(ФормаОтчета, "Период"+ФормаОтчета.ВидПериода);
	
	ВидимостьПериода(ФормаОтчета);
	
КонецПроцедуры // ОбновитьНастройкиОтчета()

Процедура ВыбратьВидТаблицы(ФормаОтчета, ВидТаблицы, НомерТаблицы) Экспорт
	
	Элементы             = ФормаОтчета.Элементы;
	ИдентификаторТаблицы = ФормаОтчета.ИдентификаторТаблицы;
	
	Элементы[ИдентификаторТаблицы].Заголовок = ВидТаблицы + " №" + Строка(НомерТаблицы);
	Если ВидТаблицы = "Таблица" Тогда
		
		Элементы[ИдентификаторТаблицы].Картинка = БиблиотекаКартинок.Таблица;
		
		Элементы.ВидТаблица.Пометка   = Истина;
		Элементы.ВидДиаграмма.Пометка = Ложь;
		
		Элементы.ГруппаСтроки.Видимость                   = Истина;
		Элементы.ГруппаКолонки.Видимость                  = Истина;
		Элементы.ГруппаПоказатели.Видимость               = Истина;
		
		Элементы.ГруппаОформлениеТаблицы.Видимость        = Истина;
		Элементы.ТаблицаРеквизитыОтдельно.Видимость       = Истина;
		Элементы.ТаблицаОтображатьТаблицуДанных.Видимость = Ложь;
		
		Элементы.ГруппаДиаграмма.Видимость = Ложь;
		
	ИначеЕсли ВидТаблицы = "Диаграмма" Тогда
		
		Элементы[ИдентификаторТаблицы].Картинка = БиблиотекаКартинок.Диаграмма;
		
		Элементы.ВидТаблица.Пометка   = Ложь;
		Элементы.ВидДиаграмма.Пометка = Истина;
		
		Элементы.ГруппаСтроки.Видимость             = Ложь;
		Элементы.ГруппаКолонки.Видимость            = Ложь;
		Элементы.ГруппаПоказатели.Видимость         = Ложь;
		
		Элементы.ГруппаОформлениеТаблицы.Видимость  = Ложь;
		Элементы.ТаблицаРеквизитыОтдельно.Видимость = Ложь;
		Элементы.ТаблицаОтображатьТаблицуДанных.Видимость = Истина;
		
		Элементы.ГруппаДиаграмма.Видимость = Истина;
		
	КонецЕсли;
	
	ОграниченияНастроек = ФормаОтчета.ОграниченияНастроек;
	
	Если НЕ ОграниченияНастроек.НайтиПоЗначению("Группировки") = Неопределено Тогда
		Элементы.ГруппаСтроки.Видимость    = Ложь;
		Элементы.ГруппаКолонки.Видимость   = Ложь;
		Элементы.ПанельТаблицы.Видимость   = Ложь;
		Элементы.ГруппаДиаграмма.Видимость = Ложь;
		
		Элементы.ДобавитьТаблицу.Доступность = Ложь;
		
	КонецЕсли;
	
	Если НЕ ОграниченияНастроек.НайтиПоЗначению("Показатели") = Неопределено Тогда
		
		Элементы.ГруппаПоказатели.Видимость  = Ложь;
		Элементы.ВидПодписей.Видимость       = Ложь;
		Элементы.Показатель.Видимость        = Ложь;
		
		Элементы.ДобавитьТаблицу.Доступность = Ложь;
		
	КонецЕсли;
	
	Если НЕ ОграниченияНастроек.НайтиПоЗначению("РежимВыводаОтчета") = Неопределено Тогда
		Элементы.ГруппаВидТаблицы.Видимость = Ложь;
		Элементы.ДобавитьТаблицу.Доступность = Ложь;
	КонецЕсли;
	
	Если НЕ ОграниченияНастроек.НайтиПоЗначению("Колонки") = Неопределено Тогда
		Элементы.ГруппаКолонки.Видимость = Ложь;
		ФормаОтчета.ОтключеныКолонки     = Истина;
	КонецЕсли;
	
	Элементы.ГруппаВидТаблицы.Заголовок = ВидТаблицы;
	
КонецПроцедуры // ВыбратьВидТаблицы()

#КонецОбласти


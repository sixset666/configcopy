
#Область ПрограммныйИнтерфейс

Процедура СформироватьФайлУведомления(Форма, Документ, Действие) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Документ.Ссылка)
		ИЛИ Форма.Модифицированность Тогда
		Сообщить("Перед выполнением необходимо записать документ!");
		Возврат;
	КонецЕсли;
	
	Если Действие = "УведомлениеОбОстатках" Тогда
		СформироватьУведомлениеОбОстаткахПрослеживаемогоТовара(Документ);
	ИначеЕсли Действие = "УведомлениеОВвозе" Тогда
		СформироватьУведомлениеОВвозеПрослеживаемогоТовара(Документ);
	ИначеЕсли Действие = "УведомлениеОПеремещении" Тогда
		СформироватьУведомлениеОПеремещенииПрослеживаемогоТовара(Документ);
	ИначеЕсли Действие = "ОтчетОбОперациях" Тогда
		СформироватьОтчетОбОперациях(Документ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучениеКаталогаФайлов(ИмяФайла, Расширение = "xml", Фильтр = "(*.xml)|*.xml")
	
	Результат = Ложь;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбора.Заголовок = "Сохранить файл ...";
	ДиалогВыбора.ПолноеИмяФайла = ИмяФайла;
	ДиалогВыбора.Расширение = Расширение;
	ДиалогВыбора.Фильтр = Фильтр;
	
	Если ДиалогВыбора.Выбрать()
		И НЕ ПустаяСтрока(ДиалогВыбора.ПолноеИмяФайла) Тогда
		ИмяФайла =  ДиалогВыбора.ПолноеИмяФайла;
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СформироватьУведомлениеОбОстаткахПрослеживаемогоТовара(Документ)
	
	Если НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель
		И НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		Сообщить("Для данного вида организации нет возможности сделать выгрузку уведомления, т.к. не является юридическим лицом или индивидуальным предпринимателем!");
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора=Новый Структура("Организация,Объект", Документ.Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(РабочаяДата,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	
	Если НЕ ЗначениеЗаполнено(Руководитель) Тогда
		Сообщить("У организации не указан руководитель для подписания!");
		Возврат;
	КонецЕсли;
	
	Если Документ.Товары.Количество() = 0 Тогда
		Сообщить("В документе не указаны товары.");
		Возврат;
	КонецЕсли;
	
	ЭтоИП = Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель;
	
	// Проверим наличие ФИО
	Фамилия = "";
	Имя = "";
	Отчество = "";
	Если ЭтоИП Тогда
		Фамилия = СокрЛП(Документ.Организация.Фамилия);
		Имя = СокрЛП(Документ.Организация.Имя);
		Отчество = СокрЛП(Документ.Организация.Отчество);
		Если ПустаяСтрока(Фамилия) ИЛИ ПустаяСтрока(Имя) Тогда
			Сообщить("Не указана фамилия или имя физического лица организации.");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Сформируем идентификатор данные
	КодНалоговогоОргана = Документ.Организация.КодИМНС;
	
	ИННКПП = СокрЛП(Документ.Организация.ИНН) + ?(ЭтоИП, "", СокрЛП(Документ.Организация.КПП));
	
	ИдентификаторФайла = СтрШаблон("ON_UVOSTTOV_%1_%1_%2_%3_%4",
		КодНалоговогоОргана, ИННКПП, Формат(ТекущаяДата(), "ДФ=yyyyMMdd"), Документ.Ссылка.УникальныйИдентификатор());
	
	ИмяФайла = ИдентификаторФайла;
	
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// Данные о файле
	ЗаписьXML.ЗаписатьНачалоЭлемента("Файл");
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьАтрибут("ИдФайл", ИдентификаторФайла);
	ЗаписьXML.ЗаписатьАтрибут("ВерсПрог", "Альфа-Авто ПРОФ " + Метаданные.Версия);
	ЗаписьXML.ЗаписатьАтрибут("ВерсФорм", "5.02");
	
	// Данные о документе уведомлении
	ЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
	ЗаписьXML.ЗаписатьАтрибут("КНД", "1169011");
	ЗаписьXML.ЗаписатьАтрибут("НомУвед", дкПолучитьНомерДляПечати(Документ));
	ЗаписьXML.ЗаписатьАтрибут("ДатаУвед", Формат(Документ.Дата, "ДФ=dd.MM.yyyy"));
	ЗаписьXML.ЗаписатьАтрибут("НомКорр", "0");
	
	// Данные об организации
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвНП");
	
	Если ЭтоИП Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведИП");
		ЗаписьXML.ЗаписатьАтрибут("ИННФЛ", СокрЛП(Документ.Организация.ИНН));
		ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
		ЗаписьXML.ЗаписатьАтрибут("Фамилия", Фамилия);
		ЗаписьXML.ЗаписатьАтрибут("Имя", Имя);
		Если НЕ ПустаяСтрока(Отчество) Тогда
			ЗаписьXML.ЗаписатьАтрибут("Отчество", Отчество);
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведЮЛ");
		ЗаписьXML.ЗаписатьАтрибут("НаимОрг", обЗаменитьСпециальныеСимволыXML(СокрЛП(Документ.Организация.НаименованиеПолное)));
		ЗаписьXML.ЗаписатьАтрибут("ИННЮЛ", СокрЛП(Документ.Организация.ИНН));
		Если НЕ ПустаяСтрока(СокрЛП(Документ.Организация.КПП)) Тогда
			ЗаписьXML.ЗаписатьАтрибут("КПП", СокрЛП(Документ.Организация.КПП));
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Подписант данных
	ЗаписьXML.ЗаписатьНачалоЭлемента("Подписант");
	ЗаписьXML.ЗаписатьАтрибут("ПрПодп", "1");
	ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
	МассивФИО = СтрРазделить(Руководитель.Наименование, " ", Ложь);
	ЗаписьXML.ЗаписатьАтрибут("Фамилия", МассивФИО[0]);
	ЗаписьXML.ЗаписатьАтрибут("Имя", ?(МассивФИО.Количество() > 1, МассивФИО[1], ""));
	ЗаписьXML.ЗаписатьАтрибут("Отчество", ?(МассивФИО.Количество() > 2, МассивФИО[2], ""));
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Товары
	НаименованиеТоваров = "";
	Если Документ.Товары.Количество() = 1 Тогда
		НаименованиеТоваров = СокрЛП(Документ.Товары[0].Номенклатура.Наименование);
	Иначе
		НаименованиеТоваров = СокрЛП(Документ.КодТНВЭД.Наименование);
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("УведОстатТов");
	ЗаписьXML.ЗаписатьАтрибут("НаимПервичДок", СокрЛП(Документ.ПервичныйДокумент.ХозОперация.Наименование));
	ЗаписьXML.ЗаписатьАтрибут("НомПервичДок", дкПолучитьНомерДляПечати( Документ.ПервичныйДокумент));
	ЗаписьXML.ЗаписатьАтрибут("ДатаПервичДок", Формат(Документ.Дата, "ДФ=dd.MM.yyyy"));
	ЗаписьXML.ЗаписатьАтрибут("НаимТовДок", НаименованиеТоваров);
	ЗаписьXML.ЗаписатьАтрибут("КодТНВЭД", СокрЛП(Документ.КодТНВЭД.Код));
	ЗаписьXML.ЗаписатьАтрибут("КолТоварДок", Формат(Документ.Товары.Итог("Количество"), "ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧГ=0"));
	ЗаписьXML.ЗаписатьАтрибут("ЕдинИзмерДок", ?(ЗначениеЗаполнено(Документ.ЕдиницаИзмерения), СокрЛП(Документ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код), "796"));
	ЗаписьXML.ЗаписатьАтрибут("ЕдинИзмерКол", ?(ЗначениеЗаполнено(Документ.ЕдиницаИзмеренияПрослеживаемости), СокрЛП(Документ.ЕдиницаИзмеренияПрослеживаемости.Код), "796"));
	ЗаписьXML.ЗаписатьАтрибут("КоличТоварКол", Формат(Документ.Товары.Итог("КоличествоПрослеживаемости"), "ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧГ=0"));
	ЗаписьXML.ЗаписатьАтрибут("СтТоварБезНДС", Формат(Документ.Товары.Итог("Сумма"), "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧГ=0"));
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	Сообщить("Сформирован файл: " + ИмяФайла);
	
КонецПроцедуры

Процедура СформироватьУведомлениеОВвозеПрослеживаемогоТовара(Документ)
	
	Отказ = Ложь;
	
	Если НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель
		И НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		Сообщить("Для данного вида организации нет возможности сделать выгрузку уведомления, т.к. не является юридическим лицом или индивидуальным предпринимателем!");
		Отказ = Истина;
	КонецЕсли;
	
	// Получим данные организации
	СтруктураОтбора=Новый Структура("Организация,Объект", Документ.Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(РабочаяДата,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	
	Если НЕ ЗначениеЗаполнено(Руководитель) Тогда
		Сообщить("У организации не указан руководитель для подписания.");
		Отказ = Истина;
	КонецЕсли;
	
	ИНН = СокрЛП(Документ.Организация.ИНН);
	ЭтоИП = Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель;
	
	// Проверим наличие ФИО
	Фамилия = "";
	Имя = "";
	Отчество = "";
	Если ЭтоИП Тогда
		Фамилия = СокрЛП(Документ.Организация.Фамилия);
		Имя = СокрЛП(Документ.Организация.Имя);
		Отчество = СокрЛП(Документ.Организация.Отчество);
		Если ПустаяСтрока(Фамилия) ИЛИ ПустаяСтрока(Имя) Тогда
			Сообщить("Не указана фамилия или имя физического лица организации.");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ИНН) Тогда
		Сообщить("У организации не заполнен ИНН.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(ИНН) <> 10 И НЕ ЭтоИП Тогда
		Сообщить("У организации ИНН не состоит из 10 цифр.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(ИНН) <> 12 И ЭтоИП Тогда
		Сообщить("У организации ИНН не состоит из 12 цифр.");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЭтоИП Тогда
		КПП = СокрЛП(Документ.Организация.КПП);
		Если ПустаяСтрока(КПП) Тогда
			Сообщить("У организации не заполнен КПП.");
			Отказ = Истина;
		КонецЕсли;
		Если СтрДлина(КПП) <> 9 Тогда
			Сообщить("У организации КПП не состоит из девяти цифр.");
			Отказ = Истина;
		КонецЕсли;
	Иначе
		КПП = "";
	КонецЕсли;
	
	ИННКПП = ИНН + КПП;
	КодНалоговогоОргана = СокрЛП(Документ.Организация.КодИМНС);
	
	Если ПустаяСтрока(КодНалоговогоОргана) Тогда
		Сообщить("У организации не заполнен код налогового органа.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(КодНалоговогоОргана) <> 4 Тогда
		Сообщить("У организации код налогового органа не состоит из четырех цифр.");
		Отказ = Истина;
	КонецЕсли;
	
	ПредставлениеОрганизации = спПолучитьПредставление(Документ.Организация, Новый Структура("Наименование"));
	
	Если ПустаяСтрока(ПредставлениеОрганизации) Тогда
		Сообщить("Для организации не сформировано наименование.");
		Отказ = Истина;
	КонецЕсли;
	
	// Получим данные контрагента (продавца)
	КодСтраны = СокрЛП(Документ.Контрагент.СтранаРегистрации.Код);
	
	Если ПустаяСтрока(КодСтраны) Тогда
		Сообщить("Не задан код государства-члена ЕАЭС контрагента (ОКСМ).");
		Отказ = Истина;
	КонецЕсли;
	
	НалоговыйНомер = СокрЛП(Документ.Контрагент.НалоговыйНомер);
	
	Если ПустаяСтрока(НалоговыйНомер) Тогда
		Сообщить("Не задан налоговый номер контрагента.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(НалоговыйНомер) > 14 ИЛИ СтрДлина(НалоговыйНомер) < 8 Тогда
		Сообщить("Налоговый номер контрагента должен быть указан из 8-14 цифр.");
		Отказ = Истина;
	КонецЕсли;
	
	АдресКонтрагента = спПолучитьПредставление(Документ.Контрагент, Новый Структура("АдресЮридический"));
	НаименованиеКонтрагента = спПолучитьПредставление(Документ.Контрагент, Новый Структура("Наименование"));
	
	Если ПустаяСтрока(АдресКонтрагента) Тогда
		Сообщить("Не заполнен юридический адрес контрагента.");
		Отказ = Истина;
	КонецЕсли;
	
	// Остальные проверки
	Если ЗначениеЗаполнено(Документ.РНПТ) Тогда
		Сообщить("Регистрационный номер партии товаров (РНПТ) не должен быть заполнен.");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Документ.ДокументОснование) Тогда
		Сообщить("Не заполнен документ основание.");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Документ.ДокументОснование.ВхДокНомер) Тогда
		Сообщить("Не заполнен номер входящего документа в основании");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Документ.ДокументОснование.ВхДокДата) Тогда
		Сообщить("Не заполнена дата входящего документа в основании");
		Отказ = Истина;
	КонецЕсли;
	
	Если Документ.Товары.Количество() = 0 Тогда
		Сообщить("В документе не указаны товары.");
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторФайла = СтрШаблон("ON_UVVOZTOV_%1_%1_%2_%3_%4",
		КодНалоговогоОргана, ИННКПП, Формат(ТекущаяДата(), "ДФ=yyyyMMdd"), Документ.Ссылка.УникальныйИдентификатор());
	
	ИмяФайла = ИдентификаторФайла;
	
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// Данные о файле
	ЗаписьXML.ЗаписатьНачалоЭлемента("Файл");
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьАтрибут("ИдФайл", ИдентификаторФайла);
	ЗаписьXML.ЗаписатьАтрибут("ВерсПрог", "Альфа-Авто ПРОФ " + Метаданные.Версия);
	ЗаписьXML.ЗаписатьАтрибут("ВерсФорм", "5.02");
	
	// Данные о документе уведомлении
	ЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
	ЗаписьXML.ЗаписатьАтрибут("КНД", "1169008");
	ЗаписьXML.ЗаписатьАтрибут("НомУвед", дкПолучитьНомерДляПечати(Документ));
	ЗаписьXML.ЗаписатьАтрибут("ДатаУвед", Формат(Документ.Дата, "ДФ=dd.MM.yyyy"));
	ЗаписьXML.ЗаписатьАтрибут("НомКорр", "0");
	
	// Данные об организации
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвНП");
	
	Если ЭтоИП Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведИП");
		ЗаписьXML.ЗаписатьАтрибут("ИННФЛ", СокрЛП(Документ.Организация.ИНН));
		ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
		ЗаписьXML.ЗаписатьАтрибут("Фамилия", Фамилия);
		ЗаписьXML.ЗаписатьАтрибут("Имя", Имя);
		Если НЕ ПустаяСтрока(Отчество) Тогда
			ЗаписьXML.ЗаписатьАтрибут("Отчество", Отчество);
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведЮЛ");
		ЗаписьXML.ЗаписатьАтрибут("НаимОрг", обЗаменитьСпециальныеСимволыXML(СокрЛП(Документ.Организация.НаименованиеПолное)));
		ЗаписьXML.ЗаписатьАтрибут("ИННЮЛ", СокрЛП(Документ.Организация.ИНН));
		КППОрганизации = СокрЛП(Документ.Организация.КПП);
		Если НЕ ПустаяСтрока(КППОрганизации) Тогда
			ЗаписьXML.ЗаписатьАтрибут("КПП", КППОрганизации);
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Подписант данных
	ЗаписьXML.ЗаписатьНачалоЭлемента("Подписант");
	ЗаписьXML.ЗаписатьАтрибут("ПрПодп", "1");
	ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
	МассивФИО = СтрРазделить(СокрЛП(Руководитель.Наименование), " ", Ложь);
	ЗаписьXML.ЗаписатьАтрибут("Фамилия", МассивФИО[0]);
	ЗаписьXML.ЗаписатьАтрибут("Имя", ?(МассивФИО.Количество() > 1, МассивФИО[1], ""));
	ЗаписьXML.ЗаписатьАтрибут("Отчество", ?(МассивФИО.Количество() > 2, МассивФИО[2], ""));
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Товары
	НаименованиеТоваров = "";
	Если Документ.Товары.Количество() = 1 Тогда
		НаименованиеТоваров = СокрЛП(Документ.Товары[0].Номенклатура.Наименование);
	Иначе
		НаименованиеТоваров = СокрЛП(Документ.КодТНВЭД.Наименование);
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("УведПрмщТов");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвЛицПрд");
	ЗаписьXML.ЗаписатьАтрибут("ОКСМ", КодСтраны);
	ЗаписьXML.ЗаписатьАтрибут("ИдКод", НалоговыйНомер);
	ЗаписьXML.ЗаписатьАтрибут("Адрес", СокрЛП(АдресКонтрагента));
	Если Документ.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
		МассивФИО = СтрРазделить(Документ.Контрагент.Наименование, " ", Ложь);
		ЗаписьXML.ЗаписатьАтрибут("Фамилия", МассивФИО[0]);
		ЗаписьXML.ЗаписатьАтрибут("Имя", ?(МассивФИО.Количество() > 1, МассивФИО[1], ""));
		ЗаписьXML.ЗаписатьАтрибут("Отчество", ?(МассивФИО.Количество() > 2, МассивФИО[2], ""));
		ЗаписьXML.ЗаписатьКонецЭлемента();
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента("НаимОрг");
		ЗаписьXML.ЗаписатьТекст(СокрЛП(НаименованиеКонтрагента));
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвСопрДок");
	ЗаписьXML.ЗаписатьАтрибут("ВидСопрДок", "2");
	ЗаписьXML.ЗаписатьАтрибут("ДатаСопрДок", Формат(Документ.ДокументОснование.ВхДокДата, "ДФ=dd.MM.yyyy"));
	ЗаписьXML.ЗаписатьАтрибут("НомСопрДок", СокрЛП(Документ.ДокументОснование.ВхДокНомер));
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СведТовар");
	ЗаписьXML.ЗаписатьАтрибут("КодТовТНВЭД", СокрЛП(Документ.КодТНВЭД.Код));
	ЗаписьXML.ЗаписатьАтрибут("КолТовСопрДок", Формат(Документ.Товары.Итог("Количество"), "ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧГ=0"));
	ЗаписьXML.ЗаписатьАтрибут("ЕдинИзмСопрДок", ?(ЗначениеЗаполнено(Документ.ЕдиницаИзмерения), СокрЛП(Документ.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код), "796"));
	ЗаписьXML.ЗаписатьАтрибут("ЕдинИзмерКол", ?(ЗначениеЗаполнено(Документ.ЕдиницаИзмеренияПрослеживаемости), СокрЛП(Документ.ЕдиницаИзмеренияПрослеживаемости.Код), "796"));
	ЗаписьXML.ЗаписатьАтрибут("КоличТоварКол", Формат(Документ.Товары.Итог("КоличествоПрослеживаемости"), "ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧГ=0"));
	ЗаписьXML.ЗаписатьАтрибут("СтоимБезНДС", Формат(Документ.Товары.Итог("Сумма"), "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧГ=0"));
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	Сообщить("Сформирован файл: " + ИмяФайла);
	
КонецПроцедуры

Процедура СформироватьУведомлениеОПеремещенииПрослеживаемогоТовара(Документ)
	
	Отказ = Ложь;
	
	Если НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель
		И НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		Сообщить("Для данного вида организации нет возможности сделать выгрузку уведомления, т.к. не является юридическим лицом или индивидуальным предпринимателем!");
		Отказ = Истина;
	КонецЕсли;
	
	// Получим данные организации
	СтруктураОтбора=Новый Структура("Организация,Объект", Документ.Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(РабочаяДата,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	
	Если НЕ ЗначениеЗаполнено(Руководитель) Тогда
		Сообщить("У организации не указан руководитель для подписания.");
		Отказ = Истина;
	КонецЕсли;
	
	ИНН = СокрЛП(Документ.Организация.ИНН);
	ЭтоИП = Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель;
	
	// Проверим наличие ФИО
	Фамилия = "";
	Имя = "";
	Отчество = "";
	Если ЭтоИП Тогда
		Фамилия = СокрЛП(Документ.Организация.Фамилия);
		Имя = СокрЛП(Документ.Организация.Имя);
		Отчество = СокрЛП(Документ.Организация.Отчество);
		Если ПустаяСтрока(Фамилия) ИЛИ ПустаяСтрока(Имя) Тогда
			Сообщить("Не указана фамилия или имя физического лица организации.");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ИНН) Тогда
		Сообщить("У организации не заполнен ИНН.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(ИНН) <> 10 И НЕ ЭтоИП Тогда
		Сообщить("У организации ИНН не состоит из 10 цифр.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(ИНН) <> 12 И ЭтоИП Тогда
		Сообщить("У организации ИНН не состоит из 12 цифр.");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЭтоИП Тогда
		КПП = СокрЛП(Документ.Организация.КПП);
		Если ПустаяСтрока(КПП) Тогда
			Сообщить("У организации не заполнен КПП.");
			Отказ = Истина;
		КонецЕсли;
		Если СтрДлина(КПП) <> 9 Тогда
			Сообщить("У организации КПП не состоит из девяти цифр.");
			Отказ = Истина;
		КонецЕсли;
	Иначе
		КПП = "";
	КонецЕсли;
	
	ИННКПП = ИНН + КПП;
	КодНалоговогоОргана = СокрЛП(Документ.Организация.КодИМНС);
	
	Если ПустаяСтрока(КодНалоговогоОргана) Тогда
		Сообщить("У организации не заполнен код налогового органа.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(КодНалоговогоОргана) <> 4 Тогда
		Сообщить("У организации код налогового органа не состоит из четырех цифр.");
		Отказ = Истина;
	КонецЕсли;
	
	ПредставлениеОрганизации = спПолучитьПредставление(Документ.Организация, Новый Структура("Наименование"));
	
	Если ПустаяСтрока(ПредставлениеОрганизации) Тогда
		Сообщить("Для организации не сформировано наименование.");
		Отказ = Истина;
	КонецЕсли;
	
	ДанныеКонтрагента = Новый Соответствие;
	
	// Получим данные контрагента (покупателя)
	Если Документ.Контрагенты.Количество() = 0 Тогда
		Сообщить("Не указаны покупатели прослеживаемых товаров.");
		Отказ = Истина;
	КонецЕсли;
	
	Для Каждого ТекущийКонтрагент Из Документ.Контрагенты Цикл
		
		РеквизитыКонтрагента = Новый Структура("Контрагент,НаименованиеКонтрагента,КодСтраны,НалоговыйНомер,АдресКонтрагента");
		РеквизитыКонтрагента.Контрагент = ТекущийКонтрагент.Контрагент;
		РеквизитыКонтрагента.КодСтраны = СокрЛП(ТекущийКонтрагент.Контрагент.СтранаРегистрации.Код);
		РеквизитыКонтрагента.НаименованиеКонтрагента = спПолучитьПредставление(ТекущийКонтрагент.Контрагент, Новый Структура("Наименование"));
		
		Если ПустаяСтрока(РеквизитыКонтрагента.КодСтраны) Тогда
			Сообщить("Не задан код государства-члена ЕАЭС (ОКСМ) контрагента " + РеквизитыКонтрагента.НаименованиеКонтрагента + ".");
			Отказ = Истина;
		КонецЕсли;
		
		РеквизитыКонтрагента.НалоговыйНомер = СокрЛП(ТекущийКонтрагент.Контрагент.НалоговыйНомер);
		
		Если ПустаяСтрока(РеквизитыКонтрагента.НалоговыйНомер) Тогда
			Сообщить("Не задан налоговый номер контрагента " + РеквизитыКонтрагента.НаименованиеКонтрагента + ".");
			Отказ = Истина;
		ИначеЕсли СтрДлина(РеквизитыКонтрагента.НалоговыйНомер) > 14 
			ИЛИ СтрДлина(РеквизитыКонтрагента.НалоговыйНомер) < 8 Тогда
			Сообщить("Налоговый номер контрагента " + РеквизитыКонтрагента.НаименованиеКонтрагента + " должен быть указан из 8-14 цифр.");
			Отказ = Истина;
		КонецЕсли;
		
		РеквизитыКонтрагента.АдресКонтрагента = спПолучитьПредставление(ТекущийКонтрагент.Контрагент, Новый Структура("АдресЮридический"));
		
		Если ПустаяСтрока(РеквизитыКонтрагента.АдресКонтрагента) Тогда
			Сообщить("Не заполнен юридический адрес контрагента" + РеквизитыКонтрагента.НаименованиеКонтрагента + ".");
			Отказ = Истина;
		КонецЕсли;
		
		ДанныеКонтрагента.Вставить(ТекущийКонтрагент.ИдентификаторСтроки, РеквизитыКонтрагента);
		
	КонецЦикла;
	
	// Проверим ТЧ Товары
	Если Документ.Товары.Количество() = 0 Тогда
		Сообщить("В документе не указаны товары.");
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторФайла = СтрШаблон("ON_UVVIOZTOV_%1_%1_%2_%3_%4",
		КодНалоговогоОргана, ИННКПП, Формат(ТекущаяДата(), "ДФ=yyyyMMdd"), Документ.Ссылка.УникальныйИдентификатор());
	
	ИмяФайла = ИдентификаторФайла;
	
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// Данные о файле
	ЗаписьXML.ЗаписатьНачалоЭлемента("Файл");
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьАтрибут("ИдФайл", ИдентификаторФайла);
	ЗаписьXML.ЗаписатьАтрибут("ВерсПрог", "Альфа-Авто ПРОФ " + Метаданные.Версия);
	ЗаписьXML.ЗаписатьАтрибут("ВерсФорм", "5.02");
	
	// Данные о документе уведомлении
	ЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
	ЗаписьXML.ЗаписатьАтрибут("КНД", "1169009");
	ЗаписьXML.ЗаписатьАтрибут("НомУвед", дкПолучитьНомерДляПечати(Документ));
	ЗаписьXML.ЗаписатьАтрибут("ДатаУвед", Формат(Документ.Дата, "ДФ=dd.MM.yyyy"));
	ЗаписьXML.ЗаписатьАтрибут("НомКорр", "0");
	
	// Данные об организации
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвНП");
	
	Если ЭтоИП Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведИП");
		ЗаписьXML.ЗаписатьАтрибут("ИННФЛ", СокрЛП(Документ.Организация.ИНН));
		ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
		ЗаписьXML.ЗаписатьАтрибут("Фамилия", Фамилия);
		ЗаписьXML.ЗаписатьАтрибут("Имя", Имя);
		Если НЕ ПустаяСтрока(Отчество) Тогда
			ЗаписьXML.ЗаписатьАтрибут("Отчество", Отчество);
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведЮЛ");
		ЗаписьXML.ЗаписатьАтрибут("НаимОрг", обЗаменитьСпециальныеСимволыXML(СокрЛП(Документ.Организация.НаименованиеПолное)));
		ЗаписьXML.ЗаписатьАтрибут("ИННЮЛ", СокрЛП(Документ.Организация.ИНН));
		КППОрганизации = СокрЛП(Документ.Организация.КПП);
		Если НЕ ПустаяСтрока(КППОрганизации) Тогда
			ЗаписьXML.ЗаписатьАтрибут("КПП", КППОрганизации);
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Подписант данных
	ЗаписьXML.ЗаписатьНачалоЭлемента("Подписант");
	ЗаписьXML.ЗаписатьАтрибут("ПрПодп", "1");
	ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
	МассивФИО = СтрРазделить(СокрЛП(Руководитель.Наименование), " ", Ложь);
	ЗаписьXML.ЗаписатьАтрибут("Фамилия", МассивФИО[0]);
	ЗаписьXML.ЗаписатьАтрибут("Имя", ?(МассивФИО.Количество() > 1, МассивФИО[1], ""));
	ЗаписьXML.ЗаписатьАтрибут("Отчество", ?(МассивФИО.Количество() > 2, МассивФИО[2], ""));
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Для Каждого ТекущийКонтрагент Из ДанныеКонтрагента Цикл
		// Добавляем данные о контрагенте
		ЗаписьXML.ЗаписатьНачалоЭлемента("УведВывозТов");
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвЛицПрд");
		РеквизитыКонтрагента = ТекущийКонтрагент.Значение;
		ЗаписьXML.ЗаписатьАтрибут("ОКСМ", РеквизитыКонтрагента.КодСтраны);
		ЗаписьXML.ЗаписатьАтрибут("ИдКод", РеквизитыКонтрагента.НалоговыйНомер);
		ЗаписьXML.ЗаписатьАтрибут("Адрес", СокрЛП(РеквизитыКонтрагента.АдресКонтрагента));
		Если РеквизитыКонтрагента.Контрагент.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
			ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
			МассивФИО = СтрРазделить(СокрЛП(РеквизитыКонтрагента.НаименованиеКонтрагента), " ", Ложь);
			ЗаписьXML.ЗаписатьАтрибут("Фамилия", МассивФИО[0]);
			ЗаписьXML.ЗаписатьАтрибут("Имя", ?(МассивФИО.Количество() > 1, МассивФИО[1], ""));
			ЗаписьXML.ЗаписатьАтрибут("Отчество", ?(МассивФИО.Количество() > 2, МассивФИО[2], ""));
			ЗаписьXML.ЗаписатьКонецЭлемента();
		Иначе
			ЗаписьXML.ЗаписатьНачалоЭлемента("НаимОрг");
			ЗаписьXML.ЗаписатьТекст(СокрЛП(РеквизитыКонтрагента.НаименованиеКонтрагента));
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		// Инициализируем данные по документам и товарам
		СписокДокументов = Новый Массив;
		СписокТоваров = Новый Массив;
		НайденныеСтроки = Документ.Товары.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ТекущийКонтрагент.Ключ));
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			Если СписокДокументов.Найти(ТекущаяСтрока.СопроводительныйДокумент) = Неопределено Тогда
				СписокДокументов.Добавить(ТекущаяСтрока.СопроводительныйДокумент);
			КонецЕсли;
			СтрокаТоваров = Новый Структура();
			СтрокаТоваров.Вставить("НомТовДок", ТекущаяСтрока.ПорядковыйНомер);
			СтрокаТоваров.Вставить("НаимТовДок", спПолучитьНаименование(ТекущаяСтрока.Номенклатура));
			СтрокаТоваров.Вставить("КоличТоварДок", Формат(ТекущаяСтрока.Количество,  "ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧГ=0"));
			КодЕдиницыИзмерения = "";
			Если ТипЗнч(ТекущаяСтрока.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
				КодЕдиницыИзмерения = ТекущаяСтрока.ЕдиницаИзмерения.Код;
			ИначеЕсли ТипЗнч(ТекущаяСтрока.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
				КодЕдиницыИзмерения = ТекущаяСтрока.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код;
			Иначе
				КодЕдиницыИзмерения = Справочники.КлассификаторЕдиницИзмерения.шт.Код;
			КонецЕсли;
			СтрокаТоваров.Вставить("ЕдинИзмерДок", СокрЛП(КодЕдиницыИзмерения));
			СтрокаТоваров.Вставить("РегНомерТов", СокрЛП(ТекущаяСтрока.РНПТ.Наименование));
			СтрокаТоваров.Вставить("ЕдинИзмерПер", СокрЛП(ТекущаяСтрока.ЕдиницаИзмеренияПрослеживаемости.Код));
			СтрокаТоваров.Вставить("КоличТоварПер", Формат(ТекущаяСтрока.КоличествоПрослеживаемости,  "ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧГ=0"));
			СтрокаТоваров.Вставить("СтТоварБезНДС", Формат(ТекущаяСтрока.Сумма,  "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧГ=0"));
			СписокТоваров.Добавить(СтрокаТоваров);
		КонецЦикла;
		
		// Запись в файл документы
		Для Каждого ТекущийДокумент Из СписокДокументов Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента("СвСопрДок");
			ЗаписьXML.ЗаписатьАтрибут("ВидСопрДок", "2");
			ЗаписьXML.ЗаписатьАтрибут("ДатаСопрДок", Формат(ТекущийДокумент.Дата, "ДФ=dd.MM.yyyy"));
			ЗаписьXML.ЗаписатьАтрибут("НомСопрДок", дкПолучитьНомерДляПечати(ТекущийДокумент));
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
		
		// Запись в файл товаров
		Для Каждого СтрокаТоваров Из СписокТоваров Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента("УведВывозТовСтр");
			ЗаписьXML.ЗаписатьАтрибут("НомТовДок", Строка(СтрокаТоваров.НомТовДок));
			ЗаписьXML.ЗаписатьАтрибут("НаимТовДок", СтрокаТоваров.НаимТовДок);
			ЗаписьXML.ЗаписатьАтрибут("КоличТоварДок", СтрокаТоваров.КоличТоварДок);
			ЗаписьXML.ЗаписатьАтрибут("ЕдинИзмерДок", СтрокаТоваров.ЕдинИзмерДок);
			ЗаписьXML.ЗаписатьАтрибут("РегНомерТов", СтрокаТоваров.РегНомерТов);
			ЗаписьXML.ЗаписатьАтрибут("ЕдинИзмерПер", СтрокаТоваров.ЕдинИзмерПер);
			ЗаписьXML.ЗаписатьАтрибут("КоличТоварПер", СтрокаТоваров.КоличТоварПер);
			ЗаписьXML.ЗаписатьАтрибут("СтТоварБезНДС", СтрокаТоваров.СтТоварБезНДС);
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	Сообщить("Сформирован файл: " + ИмяФайла);
	
КонецПроцедуры

Процедура СформироватьОтчетОбОперациях(Документ)
	
	Отказ = Ложь;
	
	Если НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель
		И НЕ Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		Сообщить("Для данного вида организации нет возможности сделать выгрузку уведомления, т.к. не является юридическим лицом или индивидуальным предпринимателем!");
		Отказ = Истина;
	КонецЕсли;
	
	// Получим данные организации
	СтруктураОтбора=Новый Структура("Организация,Объект", Документ.Организация, Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(РабочаяДата,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	
	Если НЕ ЗначениеЗаполнено(Руководитель) Тогда
		Сообщить("У организации не указан руководитель для подписания.");
		Отказ = Истина;
	КонецЕсли;
	
	ИНН = СокрЛП(Документ.Организация.ИНН);
	ЭтоИП = Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель;
	
	// Проверим наличие ФИО
	Фамилия = "";
	Имя = "";
	Отчество = "";
	Если ЭтоИП Тогда
		Фамилия = СокрЛП(Документ.Организация.Фамилия);
		Имя = СокрЛП(Документ.Организация.Имя);
		Отчество = СокрЛП(Документ.Организация.Отчество);
		Если ПустаяСтрока(Фамилия) ИЛИ ПустаяСтрока(Имя) Тогда
			Сообщить("Не указана фамилия или имя физического лица организации.");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ИНН) Тогда
		Сообщить("У организации не заполнен ИНН.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(ИНН) <> 10 И НЕ ЭтоИП Тогда
		Сообщить("У организации ИНН не состоит из 10 цифр.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(ИНН) <> 12 И ЭтоИП Тогда
		Сообщить("У организации ИНН не состоит из 12 цифр.");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЭтоИП Тогда
		КПП = СокрЛП(Документ.Организация.КПП);
		Если ПустаяСтрока(КПП) Тогда
			Сообщить("У организации не заполнен КПП.");
			Отказ = Истина;
		КонецЕсли;
		Если СтрДлина(КПП) <> 9 Тогда
			Сообщить("У организации КПП не состоит из девяти цифр.");
			Отказ = Истина;
		КонецЕсли;
	Иначе
		КПП = "";
	КонецЕсли;
	
	ИННКПП = ИНН + КПП;
	КодНалоговогоОргана = СокрЛП(Документ.Организация.КодИМНС);
	
	Если ПустаяСтрока(КодНалоговогоОргана) Тогда
		Сообщить("У организации не заполнен код налогового органа.");
		Отказ = Истина;
	ИначеЕсли СтрДлина(КодНалоговогоОргана) <> 4 Тогда
		Сообщить("У организации код налогового органа не состоит из четырех цифр.");
		Отказ = Истина;
	КонецЕсли;
	
	ПредставлениеОрганизации = спПолучитьПредставление(Документ.Организация, Новый Структура("Наименование"));
	
	Если ПустаяСтрока(ПредставлениеОрганизации) Тогда
		Сообщить("Для организации не сформировано наименование.");
		Отказ = Истина;
	КонецЕсли;
	
	// Проверим ТЧ Операции
	Если Документ.Операции.Количество() = 0 Тогда
		Сообщить("В документе нет операций о прослеживаемости товаров.");
		Отказ = Истина;
	КонецЕсли;
	
	// Проверим данные контрагентов
	Для Каждого ТекущаяОперация Из Документ.Операции Цикл
		Если обЗначениеНеЗаполнено(ТекущаяОперация.ДатаОперации) Тогда
			Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" не заполнена дата операции.");
			Отказ = Истина;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ТекущаяОперация.КодОперации) Тогда
			Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" не заполнен код вида операции.");
			Отказ = Истина;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ТекущаяОперация.ВидДокумента) Тогда
			Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" не заполнен вид документа.");
			Отказ = Истина;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ТекущаяОперация.НомерДокумента) Тогда
			Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" не заполнен номер документа.");
			Отказ = Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ТекущаяОперация.ДатаДокумента) Тогда
			Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" не заполнен дата документа.");
			Отказ = Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекущаяОперация.НаименованиеКонтрагента) Тогда
			Если СтрДлина(ТекущаяОперация.ИННКонтрагента) <> 10
				И СтрДлина(ТекущаяОперация.ИННКонтрагента) <> 12 Тогда
				Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" ИНН контрагента должен состоять из 10 или 12 символов.");
				Отказ = Истина;
			КонецЕсли;
			Если СтрДлина(ТекущаяОперация.ИННКонтрагента) = 12 Тогда
				ФИО = ЧастиИмени(ТекущаяОперация.НаименованиеКонтрагента);
				Если НЕ ЗначениеЗаполнено(ФИО.Фамилия) Тогда
					Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" в наименовании контрагента отсутствует фамилия.");
					Отказ = Истина;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(ФИО.Имя) Тогда
					Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" в наименовании контрагента отсутствует имя.");
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
			Если СтрДлина(ТекущаяОперация.ИННКонтрагента) = 10
				И СтрДлина(ТекущаяОперация.КППКонтрагента) <> 9 Тогда
				Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" КПП контрагента должен состоять из 9 символов.");
				Отказ = Истина;
			ИначеЕсли СтрДлина(ТекущаяОперация.ИННКонтрагента) = 12
				И НЕ ПустаяСтрока(ТекущаяОперация.КППКонтрагента) Тогда
				Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" КПП контрагента не должен быть заполнен для контрагента с ИНН из 12 символов.");
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		// Проверим РНПТ и коды единиц
		НайденныеСтроки = Документ.СписокРНПТ.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ТекущаяОперация.ИдентификаторСтроки));
		Для Каждого ТекущийРНПТ Из НайденныеСтроки Цикл
			Если НЕ ЗначениеЗаполнено(ТекущийРНПТ.РНПТ) Тогда
				Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" не заполнен РНПТ.");
				Отказ = Истина;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущийРНПТ.КодЕдиницыИзмерения) Тогда
				Сообщить("В строке операции №"+ТекущаяОперация.НомерСтроки+" не заполнен код единицы измерения прослеживаемости.'");
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторФайла = СтрШаблон("ON_OTCHOPTOV_%1_%1_%2_%3_%4",
		КодНалоговогоОргана,
		ИННКПП,
		Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMdd"),
		Документ.Ссылка.УникальныйИдентификатор()
	);
	
	ИмяФайла = ИдентификаторФайла;
	
	Если НЕ ПолучениеКаталогаФайлов(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// Данные о файле
	ЗаписьXML.ЗаписатьНачалоЭлемента("Файл");
	ЗаписьXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьАтрибут("ИдФайл", ИдентификаторФайла);
	ЗаписьXML.ЗаписатьАтрибут("ВерсПрог", "Альфа-Авто ПРОФ " + Метаданные.Версия);
	ЗаписьXML.ЗаписатьАтрибут("ВерсФорм", "5.02");
	
	// Данные о документе уведомлении
	ЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
	ЗаписьXML.ЗаписатьАтрибут("КНД", "1169010");
	ЗаписьXML.ЗаписатьАтрибут("ОтчетГод", Формат(Документ.ПериодОтчета, "ДФ=гггг"));
	ЗаписьXML.ЗаписатьАтрибут("Период", "2"+Формат(Документ.ПериодОтчета, "ДФ=к"));
	ЗаписьXML.ЗаписатьАтрибут("НомКорр", Строка(Документ.НомерКорректировки));
	ЗаписьXML.ЗаписатьАтрибут("КодНО", СокрЛП(Документ.Организация.КодИМНС));
	
	Если Документ.Организация.ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
		ПоМесту = "116";
	Иначе
		ПоМесту = "214";
	КонецЕсли;
	ЗаписьXML.ЗаписатьАтрибут("ПоМесту", ПоМесту);
	
	// Данные об организации
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвНП");
	
	Если ЭтоИП Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведИП");
		ЗаписьXML.ЗаписатьАтрибут("ИННФЛ", СокрЛП(Документ.Организация.ИНН));
		ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
		ЗаписьXML.ЗаписатьАтрибут("Фамилия", Фамилия);
		ЗаписьXML.ЗаписатьАтрибут("Имя", Имя);
		Если НЕ ПустаяСтрока(Отчество) Тогда
			ЗаписьXML.ЗаписатьАтрибут("Отчество", Отчество);
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента("СведЮЛ");
		ЗаписьXML.ЗаписатьАтрибут("НаимОрг", обЗаменитьСпециальныеСимволыXML(СокрЛП(Документ.Организация.НаименованиеПолное)));
		ЗаписьXML.ЗаписатьАтрибут("ИННЮЛ", СокрЛП(Документ.Организация.ИНН));
		КППОрганизации = СокрЛП(Документ.Организация.КПП);
		Если НЕ ПустаяСтрока(КППОрганизации) Тогда
			ЗаписьXML.ЗаписатьАтрибут("КПП", КППОрганизации);
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Подписант данных
	ЗаписьXML.ЗаписатьНачалоЭлемента("Подписант");
	ЗаписьXML.ЗаписатьАтрибут("ПрПодп", "1");
	ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
	МассивФИО = СтрРазделить(СокрЛП(Руководитель.Наименование), " ", Ложь);
	ЗаписьXML.ЗаписатьАтрибут("Фамилия", МассивФИО[0]);
	ЗаписьXML.ЗаписатьАтрибут("Имя", ?(МассивФИО.Количество() > 1, МассивФИО[1], ""));
	Если МассивФИО.Количество() > 2 И ЗначениеЗаполнено(МассивФИО[2]) Тогда
		ЗаписьXML.ЗаписатьАтрибут("Отчество", СокрЛП(МассивФИО[2]));
	КонецЕсли;
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// Запишем операции
	ЗаписьXML.ЗаписатьНачалоЭлемента("ОтчетОперПрТов");
	Для Каждого ТекущаяОперация Из Документ.Операции Цикл
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("ОтчетОперПрТовСтр");
		ЗаписьXML.ЗаписатьАтрибут("НомерПор", Строка(ТекущаяОперация.НомерСтроки));
		ЗаписьXML.ЗаписатьАтрибут("ДатаОпер", Формат(ТекущаяОперация.ДатаОперации, "ДФ=dd.MM.yyyy"));
		ЗаписьXML.ЗаписатьАтрибут("КодВидОпер", ТекущаяОперация.КодОперации.Код);
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("РеквПервичДок");
		ЗаписьXML.ЗаписатьАтрибут("ВидДок", Строка(ТекущаяОперация.ВидДокумента));
		ЗаписьXML.ЗаписатьАтрибут("НомДок", СокрЛП(ТекущаяОперация.НомерДокумента));
		ЗаписьXML.ЗаписатьАтрибут("ДатаДок", Формат(ТекущаяОперация.ДатаДокумента, "ДФ=dd.MM.yyyy"));
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		Если ЗначениеЗаполнено(ТекущаяОперация.НаименованиеКонтрагента) Тогда
			ЭтоИП = (СтрДлина(ТекущаяОперация.ИННКонтрагента) = 12);
			
			// Данные о контрагенте
			ЗаписьXML.ЗаписатьНачалоЭлемента("Контрагент");
			
			Если ЭтоИП Тогда
				ФИО = ЧастиИмени(СокрЛП(ТекущаяОперация.НаименованиеКонтрагента));
				ЗаписьXML.ЗаписатьНачалоЭлемента("СведИП");
				ЗаписьXML.ЗаписатьАтрибут("ИННФЛ", СокрЛП(ТекущаяОперация.ИННКонтрагента));
				ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
				ЗаписьXML.ЗаписатьАтрибут("Фамилия", ФИО.Фамилия);
				ЗаписьXML.ЗаписатьАтрибут("Имя", ФИО.Имя);
				Если НЕ ПустаяСтрока(ФИО.Отчество) Тогда
					ЗаписьXML.ЗаписатьАтрибут("Отчество", ФИО.Отчество);
				КонецЕсли;
				ЗаписьXML.ЗаписатьКонецЭлемента();
				ЗаписьXML.ЗаписатьКонецЭлемента();
			Иначе
				ЗаписьXML.ЗаписатьНачалоЭлемента("СведЮЛ");
				ЗаписьXML.ЗаписатьАтрибут("НаимОрг", обЗаменитьСпециальныеСимволыXML(СокрЛП(ТекущаяОперация.НаименованиеКонтрагента)));
				ЗаписьXML.ЗаписатьАтрибут("ИННЮЛ", СокрЛП(ТекущаяОперация.ИННКонтрагента));
				КППКонтрагента = СокрЛП(ТекущаяОперация.КППКонтрагента);
				Если НЕ ПустаяСтрока(КППКонтрагента) Тогда
					ЗаписьXML.ЗаписатьАтрибут("КПП", КППКонтрагента);
				КонецЕсли;
				ЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЕсли;
			
			ЗаписьXML.ЗаписатьКонецЭлемента();
			
		КонецЕсли;
		
		// Проверим РНПТ и коды единиц
		НайденныеСтроки = Документ.СписокРНПТ.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ТекущаяОперация.ИдентификаторСтроки));
		Для Каждого ТекущийРНПТ Из НайденныеСтроки Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента("ТоварПросл");
			ЗаписьXML.ЗаписатьАтрибут("РегНомерТов", СокрЛП(ТекущийРНПТ.РНПТ));
			ЗаписьXML.ЗаписатьАтрибут("ЕдинИзмерПер", СокрЛП(ТекущийРНПТ.КодЕдиницыИзмерения));
			ЗаписьXML.ЗаписатьАтрибут("КоличТоварПер", Формат(ТекущийРНПТ.Количество, "ЧЦ=23; ЧДЦ=11; ЧРД=.; ЧН=0; ЧГ=0"));
			ЗаписьXML.ЗаписатьАтрибут("СтТоварБезНДС", Формат(ТекущийРНПТ.Сумма,  "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ=0"));
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	Сообщить("Сформирован файл: " + ИмяФайла);
	
КонецПроцедуры

Функция ЧастиИмени(ФамилияИмяОтчество)
	Результат = Новый Структура("Фамилия,Имя,Отчество");
	ЧастиИмени = СтрРазделить(ФамилияИмяОтчество, " ", Ложь);
	Если ЧастиИмени.Количество() >= 1 Тогда
		Результат.Фамилия = ЧастиИмени[0];
	КонецЕсли;
	Если ЧастиИмени.Количество() >= 2 Тогда
		Результат.Имя = ЧастиИмени[1];
	КонецЕсли;
	Если ЧастиИмени.Количество() >= 3 Тогда
		Результат.Отчество = ЧастиИмени[2];
	КонецЕсли;
	Возврат Результат;
КонецФункции

#КонецОбласти

// Модуль представляет собой библиотеку функций используемых в обработках АРМ (Автоматизированное рабочее место)


// Проставляет номера строк в колонке "НомерСтроки" таблицы значений.
// 
// Параметры: 
//  Таблица – ТаблицаЗначений – таблица, в которой проставляются номера.
//  В таблице должна присутствовать колонка "НомерСтроки"
// 
// Возвращаемое значение: 
//  Нет.
Процедура арПроставитьНомераСтрок(Таблица) Экспорт
	Возврат;
	Если Таблица.Колонки.Найти("НомерСтроки") = Неопределено Тогда
		Таблица.Колонки.Добавить("НомерСтроки");
	КонецЕсли;
	Для Сч = 1 По Таблица.Количество() Цикл
		Таблица[Сч-1].НомерСтроки = Формат(Сч, "ЧДЦ=0; ЧГ=");
	КонецЦикла;
КонецПроцедуры // арПроставитьНомераСтрок()

// Восстанавливает сохраненные отложенные сделки из регистра сведений КартинкиИФайлы
// или формирует пустую таблицу отложенных сделок, если они не сохранялись.
// 
// Параметры: 
//  ЭтотОбъект – ОбработкаОбъект – Обработка АРМ.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арИнициализироватьОтложенныеСделки(ЭтотОбъект) Экспорт
	ОтложенныеСделки = ЭтотОбъект.ОтложенныеСделки;
	
	СоответствиеОтложенныеСделки = Новый Соответствие;
	СоответствиеОтложенныеСделки.Вставить("КоличествоКолонок", 32);

	ИдентификаторОтложенныеСделки = "ОтложенныеСделкиПользователя";
	
	РегКартинкиИФайлы = РегистрыСведений.КартинкиИФайлы.СоздатьМенеджерЗаписи();
	РегКартинкиИФайлы.Объект = ПараметрыСеанса.Пользователь;
	РегКартинкиИФайлы.Идентификатор = ИдентификаторОтложенныеСделки;
	РегКартинкиИФайлы.Прочитать();
	ОтложенныеСделкиСохраненные = РегКартинкиИФайлы.Данные.Получить();
	Если НЕ ТипЗнч(ОтложенныеСделкиСохраненные) = Тип("ТаблицаЗначений") Тогда
		ОтложенныеСделкиСохраненные = Новый ТаблицаЗначений;
	КонецЕсли;	
	
	ЕстьНесоответствия = Ложь;
	КоличествоКолонок = СоответствиеОтложенныеСделки["КоличествоКолонок"];
	
	Если ОтложенныеСделкиСохраненные.Колонки.Количество() <> КоличествоКолонок Тогда
		ЕстьНесоответствия = Истина;
		
		ЭтотОбъект.ОтложенныеСделки.Колонки.Очистить();
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("Строка"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("Представление", ОписаниеТипов);
		
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("UUID", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("ТаблицаЗначений"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("Товары", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.Контрагенты"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("Контрагент", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.ДоговорыВзаиморасчетов"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("ДоговорВзаиморасчетов", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.Валюты"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("Валюта", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("СправочникСсылка.ХозОперации"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("ХозОперация", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("Дата"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("Дата", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("Булево"));
		
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флСоздатьЗаказ", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флСоздатьПоступление", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флСоздатьРКО", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флСоздатьПКО", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флСоздатьСчФакт", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флСоздатьСчет", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флСоздатьРеализация", ОписаниеТипов);
		
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флПечататьЗаказ", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флПечататьПоступление", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флПечататьРКО", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флПечататьПКО", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флПечататьСчФакт", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флПечататьСчет", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флПечататьРеализация", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("Число"));
		
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флЭкземпляровЗаказ", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флЭкземпляровПоступление", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флЭкземпляровРКО", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флЭкземпляровПКО", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флЭкземпляровСчФакт", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флЭкземпляровСчет", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("флЭкземпляровРеализация", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("ТаблицаЗначений"));
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("ТекущаяСделка", ОписаниеТипов);
		
		ОписаниеТипов=Новый ОписаниеТипов;
		ОписаниеТипов.Типы().Добавить(Тип("Строка"));
		
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("Комментарий", ОписаниеТипов);
		ЭтотОбъект.ОтложенныеСделки.Колонки.Добавить("ИнтерфейсПользователя", ОписаниеТипов);
	КонецЕсли;

	Если ЕстьНесоответствия Тогда
		арСохранитьОтложенныеСделки(ЭтотОбъект);
	Иначе
		ЭтотОбъект.ОтложенныеСделки = ОтложенныеСделкиСохраненные.Скопировать();
	КонецЕсли;
КонецПроцедуры // арИнициализироватьОтложенныеСделки()

// Сохраняет отложенные сделки в регистр сведений КартинкиИФайлы
// 
// Параметры: 
//  ЭтотОбъект – ОбработкаОбъект – Обработка АРМ.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арСохранитьОтложенныеСделки(ЭтотОбъект) Экспорт
	ОтложенныеСделки = ЭтотОбъект.ОтложенныеСделки;
	
	ИдентификаторОтложенныеСделки = "ОтложенныеСделкиПользователя";
	
	РегКартинкиИФайлы = РегистрыСведений.КартинкиИФайлы.СоздатьМенеджерЗаписи();
	РегКартинкиИФайлы.Объект = ПараметрыСеанса.Пользователь;
	РегКартинкиИФайлы.Идентификатор = ИдентификаторОтложенныеСделки;
	РегКартинкиИФайлы.Данные = Новый ХранилищеЗначения(ОтложенныеСделки);
	РегКартинкиИФайлы.Записать(Истина);
КонецПроцедуры // арСохранитьОтложенныеСделки()

// Отбирает значения отложенных сделок по указанному АРМ-у в реквизит "ОтложенныеСделкиТекущие"
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  ИмяАРМ - Строка - имя АРМ по которому производится фильтрация
// 
// Возвращаемое значение: 
//  Нет.
Процедура арОтфильтроватьОтложенныеСделки(ЭтаФорма, ИмяАРМ) Экспорт
	ЭтотОбъект = ЭтаФорма.ОбработкаОбъект;
	ВремОтложенныеСделкиТекущие = ЭтотОбъект.ОтложенныеСделки.Скопировать();
	Пока Истина Цикл
		НадоПрервать = Истина;
		Для Каждого СтрСделка Из ВремОтложенныеСделкиТекущие Цикл
			Если СтрСделка.ИнтерфейсПользователя <> ИмяАрм И НЕ ПустаяСтрока(СтрСделка.ИнтерфейсПользователя) Тогда
				ВремОтложенныеСделкиТекущие.Удалить(СтрСделка);
				НадоПрервать = Ложь;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		Если НадоПрервать Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ЭтотОбъект.ОтложенныеСделкиТекущие = ВремОтложенныеСделкиТекущие.Скопировать();
	Оповестить("АРМИзменениеТекущихОтложенныхСделок");
КонецПроцедуры // арОтфильтроватьОтложенныеСделки()

// Вызывается при изменении отбора в списке номенклатуры, управляет индикацией отбора на форме
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Элемент - Элемент формы - элемент формы инициировавший изменение отбора
// 
// Возвращаемое значение: 
//  Нет.
Процедура арСписокНоменклатураПриИзмененииОтбора(ЭтаФорма, Элемент = Неопределено) Экспорт
	ПредставлениеОтбора = обПолучитьПредставлениеОтбора(ЭтаФорма.СписокНоменклатура.Отбор);
	Попытка
		ЭтаФорма.ЭлементыФормы.тКомментарийНоменклатура.Заголовок = Лев(ПредставлениеОтбора, СтрДлина(ПредставлениеОтбора)-1);
		ЭтаФорма.ЭлементыФормы.тКомментарийНоменклатура.ЦветТекста = Новый Цвет(155,0,0);
		ЭтаФорма.ЭлементыФормы.тКомментарийНоменклатура.Подсказка="Текущий фильтр";
	Исключение
	КонецПопытки;
	Попытка
		Если НЕ ПустаяСтрока(ПредставлениеОтбора) Тогда
			ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатура.Кнопки.ПоказатьОтбор.Текст = "Показать отбор (!)";
			ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатура.Кнопки.ПоказатьОтбор.Подсказка = "Установленный отбор: " + ПредставлениеОтбора;
			ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатура.Кнопки.ПоказатьОтбор.Пояснение = "Показать/скрыть отбор";
		Иначе
			ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатура.Кнопки.ПоказатьОтбор.Текст = "Показать отбор";
			ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатура.Кнопки.ПоказатьОтбор.Подсказка = "Отбор не установлен";
			ЭтаФорма.ЭлементыФормы.КоманднаяПанельСписокНоменклатура.Кнопки.ПоказатьОтбор.Пояснение = "Показать/скрыть отбор";
		КонецЕсли;
	Исключение;
	КонецПопытки;
КонецПроцедуры // арСписокНоменклатураПриИзмененииОтбора()

// Вызывается при изменении отбора в списке контрагентов, управляет индикацией отбора на форме
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Элемент - Элемент формы - элемент формы инициировавший изменение отбора
// 
// Возвращаемое значение: 
//  Нет.
Процедура арСписокКонтрагентыПриИзмененииОтбора(ЭтаФорма, Элемент = Неопределено) Экспорт
	ПредставлениеОтбора = обПолучитьПредставлениеОтбора(ЭтаФорма.СписокКонтрагенты.Отбор);
	Попытка
		ЭтаФорма.ЭлементыФормы.тКомментарийКонтрагенты.Заголовок = Лев(ПредставлениеОтбора, СтрДлина(ПредставлениеОтбора)-1);
		ЭтаФорма.ЭлементыФормы.тКомментарийКонтрагенты.ЦветТекста = Новый Цвет(155,0,0);
		ЭтаФорма.ЭлементыФормы.тКомментарийКонтрагенты.Подсказка="Текущий фильтр";
	Исключение
	КонецПопытки;
КонецПроцедуры // арСписокКонтрагентыПриИзмененииОтбора()

// Заполняет начальные значения реквизитов и элементов формы АРМ по умолчанию
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Права - Соответствие - Соответствие со значениями прав текущего пользователя
// 
// Возвращаемое значение: 
//  Нет.
Процедура арЗаполнитьНачальныеЗначения(ЭтаФорма, Права) Экспорт
	ЭтотОбъект = ЭтаФорма.ОбработкаОбъект;

	Попытка
		ЭтотОбъект.ХозОперация = Неопределено;
	Исключение
	КонецПопытки;
	
	Попытка
		ЭтотОбъект.ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
		ЭтотОбъект.ОбработкаРеквизита("ВалютаДокумента");
	Исключение
	КонецПопытки;
	
	Попытка
		ЭтаФорма.МоментВремени=РабочаяДата;
	Исключение
	КонецПопытки;

	Попытка
		ЭтотОбъект.Дата=РабочаяДата;
	Исключение
	КонецПопытки;
	
	Попытка
		ЭтаФорма.СкладКомпании=обПраво("ОсновнойСкладКомпании",Права);
		ЭтотОбъект.ОбработкаРеквизита("СкладКомпании");
	Исключение
	КонецПопытки;
	
	Попытка ЭтаФорма.СкладКомпанииНоменклатура=обПраво("ОсновнойСкладКомпании",Права); Исключение КонецПопытки;
	
	Попытка
		ЭтаФорма.ПодразделениеКомпании=ПараметрыСеанса.ПодразделениеКомпании;
		ЭтотОбъект.ОбработкаРеквизита("ПодразделениеКомпании");
	Исключение
	КонецПопытки;
	
	Попытка
		Если ЭтаФорма.СкладКомпании.Розничный Тогда
			ЭтотОбъект.ТипЦен=ЭтаФорма.СкладКомпании.ТипЦенРозничнойТорговли;
		КонецЕсли;	
	Исключение
	КонецПопытки;
	
	Попытка
		ЭтаФорма.Комментарий = "";
	Исключение
	КонецПопытки;
	
	Попытка
		арСписокНоменклатураПриИзмененииОтбора(ЭтаФорма);
	Исключение
	КонецПопытки;
	
КонецПроцедуры // арЗаполнитьНачальныеЗначения()	

// Вызывается из обработчика ПриВыводеСтроки табличного поля документов для создания принятой для АРМ
//  цветовой индикации строк списка.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Элемент - ТабличноеПоле - табличное поле из обработчика которого вызывается процедура.
//  ОформлениеСтроки - ОформлениеСтроки - оформление строки переданное в обработчик.
//  Данные строки - данные строки, переданные в обработчик.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арСписокДокументовПриВыводеСтроки(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	ЭтотОбъект = ЭтаФорма.ОбработкаОбъект;
	Если ДанныеСтроки.Автор=ПараметрыСеанса.Пользователь Тогда
		ОформлениеСтроки.ЦветТекста=ЦветаСтиля.ЦветТекстаПоля;
	Иначе
		ОформлениеСтроки.ЦветТекста=ЦветаСтиля.ЦветРамки;
	КонецЕсли;
	
	Если ДанныеСтроки.Контрагент=ЭтотОбъект.Контрагент Тогда
		ОформлениеСтроки.ЦветФона=ЦветаСтиля.АльтернативныйЦветФонаПоля;
	Иначе
		ОформлениеСтроки.ЦветФона=ЦветаСтиля.ЦветФонаПоля;
	КонецЕсли;
КонецПроцедуры

// Вызывается из обработчика ПриАктивизацииСтроки табличного поля документов 
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Элемент - ТабличноеПоле - табличное поле из обработчика которого вызывается процедура.
//  глОповещениеАктивации - строка оповещения
//  СтруктураДополнительныхПолей - Структура,  структура дополнительных передаваемых полей
//  ПраваПользователя            – Соответствие - Кэш прав и настроек.
// ИмяСписка          - Строка - имя списка документов.
// ИмяКоманднойПанели - Строка - имя командной панели списка документов.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арСписокДокументовПриАктивизацииСтроки(ЭтаФорма,Элемент,глОповещениеАктивации,СтруктураДополнительныхПолей, ПраваПользователя, ИмяСписка = Неопределено, ИмяКоманднойПанели = Неопределено) Экспорт
	дкСписокПриАктивизацииСтроки(ЭтаФорма, Элемент, глОповещениеАктивации, ПраваПользователя, ИмяСписка, ИмяКоманднойПанели);
	ИмяТабличногоПоляСостава = Элемент.Имя+"Товары";
	ТабличноеПолеСостава = ЭтаФорма.ЭлементыФормы[ИмяТабличногоПоляСостава];
	Если ТабличноеПолеСостава.Свертка = РежимСверткиЭлементаУправления.Нет Тогда
		арПолучитьСоставДокумента(Элемент, ТабличноеПолеСостава, СтруктураДополнительныхПолей);
	КонецЕсли;
КонецПроцедуры //арСписокДокументовПриВыводеСтроки()

// Вызывает обработку ОбработкаДокументов в режиме печати для выделенных строк табличного поля документов.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  ИмяСписка - Строка - имя табличного поля для которого производится печать.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арГрупповаяПечатьДокументов(ЭтаФорма, ИмяСписка) Экспорт
	ЭлементСписок = ЭтаФорма.ЭлементыФормы[ИмяСписка];
	дкОсновныеДействияФормыРеестр(ЭтаФорма, ЭлементСписок);
КонецПроцедуры // арГрупповаяПечатьДокументов()

// Формирует печатную форму выбранного документа.
// 
// Параметры: 
//  ТекущиеДанные - текущие данные табличного поля списка документов
//  ЭтаФорма – Форма – форма АРМ.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арПечататьДокумент(ТекущиеДанные, ЭтаФорма = Неопределено) Экспорт
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные.Ссылка.ПолучитьОбъект().Печать();
	КонецЕсли;
КонецПроцедуры // арПечататьДокумент()

// Обработчик нажатия на печатную форму подменю выбора печати
// Параметры: 

//  ЭтаФорма – ЭУ формы - кнопка командной панели.
//  ЭтаФорма – Форма – форма АРМ.
//  ТекДанные - текущие данные табличного поля списка документов
//  ПодменюПечати - меню печати
//
// Возвращаемое значение: 
//  Нет.
Процедура арПечатьВыбор(Кнопка, ЭтаФорма, ТекДанные, ПодменюПечати) Экспорт
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка = ТекДанные.Ссылка; 
	Если Кнопка = Неопределено Тогда Возврат; КонецЕсли;
	
	// сохраним текущую печатную форму
	СохранитьЗначение(Ссылка.Метаданные().Имя + "ПечатнаяФорма", Кнопка.Текст);
	
	// напечатаем документ
	арПечататьДокумент(ТекДанные, ЭтаФорма);
	
	// установим пометку у выбранной формы
	Для Каждого КнопкаПодменю Из ПодменюПечати.Кнопки Цикл
		КнопкаПодменю.Пометка = (КнопкаПодменю = Кнопка);
	КонецЦикла;
КонецПроцедуры // арПечатьВыбор()

// Возвращает подменю расположенное на указанной командной панели.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  ИмяКоманднойПанели - Строка - имя командной панели в которой ищется подменю
//  ТекстПодменю - Строка - заголовок искомого подменю
// 
// Возвращаемое значение: 
//  КнопкаКоманднойПанели - найденное подменю.
Функция арПолучитьПодменю(ЭтаФорма, ИмяКоманднойПанели, ТекстПодменю) Экспорт
	Для Каждого Кнопка Из ЭтаФорма.ЭлементыФормы[ИмяКоманднойПанели].Кнопки Цикл
		Если (Кнопка.ТипКнопки = ТипКнопкиКоманднойПанели.Подменю) И (Кнопка.Текст = ТекстПодменю) Тогда
			ПодменюДействия = Кнопка;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат ПодменюДействия.Кнопки;
КонецФункции // арПолучитьПодменю()

// Добавляет стандартные кнопки в командные панели формы АРМ.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  СтруктураВсеПанели - Структура - структура содержащая имена и назначения командных панелей
//                       (например ключ="КоманднаяПанельСписокКонтрагенты", значение="СправочникСписок").
//
// Возвращаемое значение: 
//  Нет.
Процедура арЗаполнитьДополнительно(ЭтаФорма, СтруктураВсеПанели) Экспорт
	ЭтотОбъект = ЭтаФорма.ОбработкаОбъект;
	Для Каждого ЗначениеПанель Из СтруктураВсеПанели Цикл
		
		ПанельИмя = ЗначениеПанель.Ключ;
		ВидОбъекта = СтруктураВсеПанели[ПанельИмя];
		ПрефиксИмя = ВидОбъекта + "_1_" + ПанельИмя + "_2_";
		
		Если (ВидОбъекта = "ДокументСписок")
		 ИЛИ (ВидОбъекта = "СправочникСписок") Тогда
			// получаем кнопки подменю "Действия" для командной панели
			Попытка // попытка нужна если у нас нет кнопки-подменю "Действия", но другие кнопки сгенерировать нужно
				КнопкиПодменюДействия = арПолучитьПодменю(ЭтаФорма, ПанельИмя, "Действия");

				КнопкаИмя = "ДопВозможностиРазделитель";
				Кнопка = КнопкиПодменюДействия.Найти(КнопкаИмя);
				Если Кнопка = Неопределено Тогда
					КнопкиПодменюДействия.Вставить(0,КнопкаИмя,ТипКнопкиКоманднойПанели.Разделитель);
				КонецЕсли;	
				
				// добавляем кнопку выгрузки в табличный документ
				КнопкаИмя = ПрефиксИмя + "СоздатьНапоминание";
				Кнопка = КнопкиПодменюДействия.Найти(КнопкаИмя);
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0, КнопкаИмя,ТипКнопкиКоманднойПанели.Действие,"Создать напоминание",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.Оповещение;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Создать напоминание";
				КонецЕсли;
				
				// добавляем кнопку выгрузки в табличный документ
				КнопкаИмя = ПрефиксИмя + "ВыгрузкаВТабличныйДокумент";
				Кнопка = КнопкиПодменюДействия.Найти(КнопкаИмя);
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0, КнопкаИмя,ТипКнопкиКоманднойПанели.Действие,"Выгрузка в табличный документ",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.ТабличныйДокумент;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Выгрузка в табличный документ";
				КонецЕсли;
				
				// добавляем кнопку выведения истории объекта
				КнопкаИмя = ПрефиксИмя + "ИсторияОбъекта";
				Кнопка = КнопкиПодменюДействия.Найти(КнопкаИмя);
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0, КнопкаИмя,ТипКнопкиКоманднойПанели.Действие,"История объекта",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.ЖурналРегистрацииКонфигурации;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "История объекта";
				КонецЕсли;
				
				// добавляем кнопку поиска ссылок
				КнопкаИмя = ПрефиксИмя + "ПоискСсылок";
				Кнопка = КнопкиПодменюДействия.Найти(КнопкаИмя);
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0, КнопкаИмя,ТипКнопкиКоманднойПанели.Действие,"Поиск ссылок",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.ПоискСсылок;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Поиск ссылок на объекты использующие данный объект и используемых в данном объекте";
				КонецЕсли;
				
				// добавляем кнопку выгрузки в табличный документ
				КнопкаИмя = ПрефиксИмя + "ДобавитьВИзбранное";
				Кнопка = КнопкиПодменюДействия.Найти(КнопкаИмя);
				Если Кнопка = Неопределено Тогда
					Кнопка = КнопкиПодменюДействия.Вставить(0, КнопкаИмя,ТипКнопкиКоманднойПанели.Действие,"Добавить в Избранное",Новый Действие("ДополнительныеДействияФормы"));
					Кнопка.Картинка    = БиблиотекаКартинок.ДобавитьВИзбранное;
					Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
					Кнопка.Подсказка   = "Добавить в избранное";
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;
			Если ВидОбъекта = "ДокументСписок" Тогда
			// получаем кнопки подменю "Перейти" для командной панели
			Попытка // попытка нужна если у нас нет кнопки-подменю "Перейти", но другие кнопки сгенерировать нужно
				КнопкиПодменюПерейти = арПолучитьПодменю(ЭтаФорма, ПанельИмя, "Перейти");
				
				КнопкаИмя = "ДопВозможностиРазделитель";
				Кнопка = КнопкиПодменюПерейти.Найти(КнопкаИмя);
				Если Кнопка = Неопределено Тогда
					КнопкиПодменюПерейти.Вставить(0,КнопкаИмя,ТипКнопкиКоманднойПанели.Разделитель);
				КонецЕсли;
				
				// добавляем кнопку вывода движений документа
				Если ЗначениеПанель.Ключ <> "КоманднаяПанельСписокСчета" Тогда
					КнопкаИмя = ПрефиксИмя + "ДвиженияДокумента";
					Кнопка = КнопкиПодменюПерейти.Найти(КнопкаИмя);
					Если Кнопка = Неопределено Тогда
						Кнопка = КнопкиПодменюПерейти.Вставить(0, КнопкаИмя,ТипКнопкиКоманднойПанели.Действие,"Движения документа",Новый Действие("ДополнительныеДействияФормы"));
						Кнопка.Подсказка   = "Вывести движения документа";
					КонецЕсли;
				КонецЕсли;
			Исключение
			КонецПопытки;
			
			// разделитель ДеревоДокумента
			КнопкиПанели = ЭтаФорма.ЭлементыФормы[ПанельИмя].Кнопки;
			КнопкаИмя = "ДеревоДокументаРазделитель";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Разделитель);
			КонецЕсли;
			
			// кнопка ДеревоДокумента
			КнопкаИмя = ПрефиксИмя + "ДеревоДокумента";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Действие, "Показать дерево связей документа", Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка = БиблиотекаКартинок.Дерево;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
				Кнопка.Подсказка = "Показать дерево связей документа";
			КонецЕсли;
			
			// разделитель Свойства
			КнопкиПанели = ЭтаФорма.ЭлементыФормы[ПанельИмя].Кнопки;
			КнопкаИмя = "СвойстваРазделитель";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Разделитель);
			КонецЕсли;
			
			// кнопка Свойства
			КнопкаИмя = ПрефиксИмя + "Свойства";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Действие, "Открыть панель свойств", Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка = БиблиотекаКартинок.СвойстваОбъекта;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
				Кнопка.Подсказка = "Отрыть панель свойств документа";
			КонецЕсли;
			
			// разделитель Состав
			КнопкиПанели = ЭтаФорма.ЭлементыФормы[ПанельИмя].Кнопки;
			КнопкаИмя = "ОтображатьСоставРазделитель";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Разделитель);
			КонецЕсли;
			
			// кнопка Состав
			КнопкаИмя = ПрефиксИмя + "ОтображатьСостав";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Действие, "Открыть панель свойств", Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка = БиблиотекаКартинок.ПоказатьСкрытьПанельИнформации;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
				Кнопка.Подсказка = "Отображать / скрыть состав документа";
				Кнопка.Пометка = Истина;
			КонецЕсли;
			
			// разделитель Печать
			КнопкиПанели = ЭтаФорма.ЭлементыФормы[ПанельИмя].Кнопки;
			КнопкаИмя = "ПечатьРазделитель";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Разделитель);
			КонецЕсли;
			
			// кнопка "Печать"
			КнопкаИмя = ПрефиксИмя + "Печать";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Действие, "Печать", Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка = БиблиотекаКартинок.Печать;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка = "Печать выделенных документов";
			КонецЕсли;
			// подменю "Выбор печатной формы"
			КнопкаИмя = "ПечатьВыбор";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПанели.Добавить(КнопкаИмя, ТипКнопкиКоманднойПанели.Подменю, " ");
				Кнопка.Подсказка = "Выбор печатной формы";
			КонецЕсли;
			
			// разделитель ПечатьГрупповая
			КнопкиПанели = ЭтаФорма.ЭлементыФормы[ПанельИмя].Кнопки;
			КнопкаИмя = "ПечатьГрупповаяРазделитель";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Разделитель);
			КонецЕсли;
			
			// кнопка "ПечатьГрупповая"
			КнопкаИмя = ПрефиксИмя + "ПечатьГрупповая";
			Кнопка = КнопкиПанели.Найти(КнопкаИмя);
			Если Кнопка = Неопределено Тогда
				Кнопка = КнопкиПанели.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Действие, "Реестр", Новый Действие("ДополнительныеДействияФормы"));
				Кнопка.Картинка = БиблиотекаКартинок.ПечатьСписка;
				Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.НадписьКартинка;
				Кнопка.Подсказка = "Реестр документов";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры	// арЗаполнитьДополнительно()

// Добавляет кнопки с пометками для выбора хозяйственной операции в подменю командной панели
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  СписокХозОпераций - СписокЗначений - список хозяйственных операция
//  ИмяПанели - Строка - имя командной панели в подменю "Операция" которой добавляются кнопки
//
// Возвращаемое значение: 
//  Нет.
Процедура арДобавитьСписокХозОпераций(ЭтаФорма, СписокХозОпераций, ИмяПанели) Экспорт
	ЭтотОбъект = ЭтаФорма.ОбработкаОбъект;
	ПодменюОперация = арПолучитьПодменю(ЭтаФорма, ИмяПанели, "Операция");
	Первый = Истина;
	Для Каждого Операция Из СписокХозОпераций Цикл
		КнопкаИмя = Операция.Значение;
		Кнопка = ПодменюОперация.Найти(КнопкаИмя);
		Если Кнопка = Неопределено Тогда
			Кнопка = ПодменюОперация.Добавить(КнопкаИмя,ТипКнопкиКоманднойПанели.Действие,Операция.Представление,Новый Действие("ХозОперацияНажатие"));
		КонецЕсли;
		Кнопка.Пометка = Ложь;
		Если обЗначениеНеЗаполнено(ЭтотОбъект.ХозОперация) Тогда
			Если Первый Тогда
				Кнопка.Пометка = Истина;
				ЭтотОбъект.ХозОперация = Справочники.ХозОперации[Операция.Значение];
				Первый  = Ложь;
			КонецЕсли;
		Иначе
			Если ЭтотОбъект.ХозОперация = Справочники.ХозОперации[Операция.Значение] Тогда
				Кнопка.Пометка = Истина;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // арДобавитьСписокХозОпераций()

// Вызывается по нажатию кнопок подменю "Операция" командной панели формы АРМ.
//  Управляет пометками кнопок подменю "Операция" и производит стандартные действия при
//  смене текущей хозяйственной операции.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Кнопка - КнопкаКоманднойПанели - кнопка, действие которой обрабатывается.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арХозОперацияВыбор(ЭтаФорма, Кнопка) Экспорт
	ЭтотОбъект = ЭтаФорма.ОбработкаОбъект;
	ПодменюОперация = арПолучитьПодменю(ЭтаФорма, "КоманднаяПанельСделка", "Операция");
	ЭтотОбъект.ХозОперация = Справочники.ХозОперации[Кнопка.Имя];
	Для Каждого КнопкаПодменю Из ПодменюОперация Цикл
		КнопкаПодменю.Пометка = Кнопка.Имя = КнопкаПодменю.Имя;
	КонецЦикла;
	ЭтотОбъект.ОбработкаРеквизита("Контрагент", , ЭтаФорма);
КонецПроцедуры // арХозОперацияВыбор()

// Вызывается по нажатию стандартных кнопок командных панелей.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Кнопка - КнопкаКоманднойПанели - кнопка, действие которой обрабатывается.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арДополнительныеДействия(ЭтаФорма, Кнопка) Экспорт
	
	ЭтотОбъект = ЭтаФорма.ОбработкаОбъект;
	ИмяКнопки = Кнопка.Имя;
	ВидОбъекта = Лев(ИмяКнопки, Найти(ИмяКнопки, "_1_") - 1);
	ИмяПанели = Сред(ИмяКнопки, Найти(ИмяКнопки, "_1_") + 3, Найти(ИмяКнопки, "_2_") - Найти(ИмяКнопки, "_1_") - 3);
	ИмяСвязанныйЭлемент = Прав(ИмяПанели, СтрДлина(ИмяПанели) - СтрДлина("КоманднаяПанель"));
	ИмяДействие = Сред(ИмяКнопки, Найти(ИмяКнопки, "_2_") + 3, СтрДлина(ИмяКнопки) - Найти(ИмяКнопки, "_2_") - 2);
	
	Попытка
		Объект = ЭтаФорма.ЭлементыФормы[ИмяСвязанныйЭлемент].ТекущиеДанные.Ссылка;
	Исключение
		Объект = Неопределено;
	КонецПопытки;
	
	Если (ВидОбъекта = "ДокументСписок")
		ИЛИ (ВидОбъекта = "СправочникСписок") Тогда
		Если ИмяДействие = "ПоискСсылок" Тогда
			Если НЕ Объект = Неопределено Тогда
				ОбъектПоискСсылок=Обработки.ПоискСсылок.Создать();
				ОбъектПоискСсылок.ОбъектСсылка=Объект;
				ОбъектПоискСсылок.НайтиВходящие();
				ОбъектПоискСсылок.НайтиИсходящие();
			КонецЕсли;
		ИначеЕсли ИмяДействие = "ИсторияОбъекта" Тогда
			Если НЕ Объект = Неопределено Тогда
				ОбъектИсторияОбъекта = Обработки.ИсторияОбъекта.Создать();
				ОбъектИсторияОбъекта.ОбъектСсылка = Объект;
				ОбъектИсторияОбъекта.СформироватьЖурналРегистрации();
			КонецЕсли;	
		ИначеЕсли ИмяДействие = "ВыгрузкаВТабличныйДокумент" Тогда
			Если НЕ Объект = Неопределено Тогда
				ОбъектИсторияОбъекта = Обработки.ВыгрузкаВТабличныйДокумент.Создать();
				ОбъектИсторияОбъекта.ОбъектСсылка = Объект;
				ОбъектИсторияОбъекта.ВыгрузитьВТабличныйДокумент();
			КонецЕсли;	
		ИначеЕсли ИмяДействие = "ДвиженияДокумента" Тогда
			Если ВидОбъекта = "ДокументСписок" Тогда
				Если НЕ Объект = Неопределено Тогда
					дкДвиженияДокумента(Объект);
				КонецЕсли;
			КонецЕсли;	
		ИначеЕсли ИмяДействие = "ДеревоДокумента" Тогда
			Если ВидОбъекта = "ДокументСписок" Тогда
				Если НЕ Объект = Неопределено Тогда
					Дерево=Обработки.ДеревоДокументов.Создать();
					Дерево.ВыбДокумент=Объект.Ссылка;
					Попытка
						Дерево.ПолучитьДеревоДокументов(Дерево.ДеревоДокументов);
						Дерево.ВывестиДеревоДокументов(Дерево.ДеревоДокументов);
						Дерево.НастроитьПолеДеревоДокументов();
					Исключение
						Предупреждение("По документу данного вида построение дерева невозможно!",10);
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;	
		ИначеЕсли ИмяДействие = "Свойства" Тогда
			Если ВидОбъекта = "ДокументСписок" Тогда
				Если НЕ Объект = Неопределено Тогда
					дкДействияФормыСвойства(ЭтаФорма, Кнопка, ИмяСвязанныйЭлемент);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ИмяДействие = "ОтображатьСостав" Тогда
			ИмяСписокСостав = ИмяСвязанныйЭлемент + "Товары";
			ИмяРазделительСостав = "Разделитель" + ИмяСвязанныйЭлемент + "Товары";
			Кнопка.Пометка = НЕ Кнопка.Пометка;
			удУправлениеВидимостиЭлементов(Кнопка.Пометка, ЭтаФорма.ЭлементыФормы[ИмяСписокСостав], ЭтаФорма.ЭлементыФормы[ИмяРазделительСостав], ЭтаФорма.ЭлементыФормы.ПанельОсновная, "низ");   
			Если НЕ Кнопка.Пометка И
				ТипЗнч(ЭтаФорма.ЭлементыФормы[ИмяСписокСостав])=Тип("ТабличноеПоле") И
				ТипЗнч(ЭтаФорма.ЭлементыФормы[ИмяСписокСостав].Значение) = Тип("ТаблицаЗначений") Тогда 
				ЭтаФорма.ЭлементыФормы[ИмяСписокСостав].Значение.Очистить(); 
			КонецЕсли;
		ИначеЕсли ИмяДействие = "Печать" Тогда
			Если ВидОбъекта = "ДокументСписок" Тогда
				Для Каждого ВыделеннаяСтрока Из ЭтаФорма.ЭлементыФормы[ИмяСвязанныйЭлемент].ВыделенныеСтроки Цикл
					арПечататьДокумент(ВыделеннаяСтрока, Этаформа);
				КонецЦикла;
			КонецЕсли;
		ИначеЕсли ИмяДействие = "ПакетнаяПечать" Тогда
			ОбработкаПакетнойПечати=Обработки.ПакетнаяПечать.Создать();
			ФормаПакетнойПечати=ОбработкаПакетнойПечати.ПолучитьФорму();
			Для Каждого ВыделеннаяСтрока Из ЭтаФорма.ЭлементыФормы[ИмяСвязанныйЭлемент].ВыделенныеСтроки Цикл
				СтрокаДокумента=ФормаПакетнойПечати.ТаблицаДокументы.Добавить();
				СтрокаДокумента.Документ=ВыделеннаяСтрока.Ссылка;
				ФормаПакетнойПечати.ЭлементыФормы.ТаблицаДокументы.ТекущаяСтрока=СтрокаДокумента;
				Для Каждого СтрокаПечатныхФорм Из ФормаПакетнойПечати.ТаблицаМакеты Цикл
					СтрокаПечатныхФорм.Пометка=Истина;
					СтрокаРодитель=ФормаПакетнойПечати.мДеревоПомеченныеМакеты.Строки.Найти(ВыделеннаяСтрока.Ссылка,"Ветка");
					СтрокаДерева=СтрокаРодитель.Строки.Найти(СтрокаПечатныхФорм.ИмяМакета,"Ветка");
					СтрокаДерева.Пометка=Истина;
				КонецЦикла;
			КонецЦикла;
			ФормаПакетнойПечати.ЭлементыФормы.ТаблицаДокументы.ТекущаяСтрока=ФормаПакетнойПечати.ЭлементыФормы.ТаблицаДокументы.Значение[0];
			ФормаПакетнойПечати.Открыть();
		ИначеЕсли ИмяДействие = "ПечатьГрупповая" Тогда
			Если ВидОбъекта = "ДокументСписок" Тогда
				арГрупповаяПечатьДокументов(ЭтаФорма, ИмяСвязанныйЭлемент);
			КонецЕсли;
		ИначеЕсли ИмяДействие = "ДобавитьВИзбранное" Тогда
			Если НЕ Объект = Неопределено Тогда
				Избранное = Обработки.Избранное.Создать();
				Избранное.ДобавитьВИзбранное(Объект);
			КонецЕсли;
		ИначеЕсли ИмяДействие = "СоздатьНапоминание" Тогда
			Если НЕ Объект = Неопределено Тогда
				Напоминание = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
				Напоминание.Объект = Объект;
				Напоминание.ПолучитьФорму().Открыть();
			КонецЕсли;	
		ИначеЕсли ИмяДействие = "Подготовлен" ИЛИ ИмяДействие = "Согласован" ИЛИ 
			ИмяДействие = "Утвержден" ИЛИ ИмяДействие = "Отклонен" ИЛИ ИмяДействие = "История" Тогда
			//Обработка изменения статус документа при утверждении
			#Если Клиент Тогда
			Попытка
				ПраваПользователя=ЭтаФорма.Права;
			Исключение
				ПраваПользователя=Обработки.Защита.Создать().Права;
			КонецПопытки; 
			дкОсновныеДействияФормыУтверждение(ЭтаФорма, ИмяДействие, ИмяСвязанныйЭлемент);
			дкЗаполнитьСписокУтверждение(ЭтаФорма, , ПраваПользователя, Истина, ИмяСвязанныйЭлемент, ИмяПанели);
			#КонецЕсли
		КонецЕсли;
	Иначе
		
	КонецЕсли;
	
КонецПроцедуры // арДополнительныеДействия()

// Добавляет панель в дерево, описывающее структуру формы. Используется при формирования дерева формы,
//  необходимого обработке ПанельАРМ для настройки видимости и положения закладок формы АРМ.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Дерево - ДеревоЗначений - формируемое дерево формы.
//  ИмяПанели - Строка - имя панели, добавляемой в дерево формы.
//  ИмяПанелиРодитель - Строка - имя панели на которой расположена добавляемая панель.
//  ИмяСтраницыРодитель - Строка - имя страницы панели на которой расположена добавляемая панель.
// 
// Возвращаемое значение: 
//  Нет.
Процедура арДобавитьПанельВДерево(ЭтаФорма, Дерево, ИмяПанели, ИмяПанелиРодитель = Неопределено, ИмяСтраницыРодитель = Неопределено) Экспорт
	// Найдем панель- и страницу-родитель, к которым нужно добавить страницы новой панели
	Если ИмяПанелиРодитель <> Неопределено Тогда
		Отбор = Новый Структура("ИмяПанели,ИмяСтраницы", ИмяПанелиРодитель, ИмяСтраницыРодитель);
		МассивСтроки = Дерево.Строки.НайтиСтроки(Отбор, ИСТИНА);
		Если МассивСтроки.Количество() <> 0 Тогда
			Строки = МассивСтроки[0].Строки;
		КонецЕсли;
	КонецЕсли;
	Если Строки = Неопределено Тогда
		Строки = Дерево.Строки;
	КонецЕсли;

	// Приступим к заполнению найденного родителя списком страниц добавляемой панели
	ТекПанель = ЭтаФорма.ЭлементыФормы[ИмяПанели];
	Для Каждого Страница Из ТекПанель.Страницы Цикл
		// Проверим, может быть данная страница уже присутствует в Дереве
		Отбор = Новый Структура("ИмяПанели,ИмяСтраницы",ТекПанель.Имя,Страница.Имя);
		МассивСтроки = Дерево.Строки.НайтиСтроки(Отбор, ИСТИНА);
		Если МассивСтроки.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Добавляем страницу в Дерево
		НоваяСтрока = Строки.Добавить();
		НоваяСтрока.ИмяПанели			= ТекПанель.Имя;
		НоваяСтрока.ИмяСтраницы			= Страница.Имя;
		НоваяСтрока.Представление		= Страница.Заголовок;
		НоваяСтрока.Видимость			= Страница.Видимость;
		Попытка
		НоваяСтрока.Текущая				= (Страница = ТекПанель.ТекущаяСтраница);
		Исключение КонецПопытки;
		НоваяСтрока.Картинка			= Страница.КартинкаЗаголовка;
		НоваяСтрока.ОтображениеЗакладок	= ТекПанель.ОтображениеЗакладок;
	КонецЦикла;
КонецПроцедуры // арДобавитьПанельВДерево()

// Проверяет на корректность дерево формы, вызывается после восстановления сохраненного
//  значения дерева формы.
// 
// Параметры: 
//  ЭтаФорма – Форма – форма АРМ.
//  Дерево - ДеревоЗначений - дерево формы.
//  Результат - Булево - этот параметр для внутреннего использования функцией, его передавать не надо.
// 
// Возвращаемое значение: 
//  Истина если ДеревоФормы заполнено корректно, иначе Ложь.
Функция арПроверитьКорректностьДереваФормы(ЭтаФорма, Строки, Результат = Истина) Экспорт
	Для Каждого Стр Из Строки Цикл
		Результат = Результат И арПроверитьКорректностьДереваФормы(ЭтаФорма, Стр.Строки, Результат);
		Попытка
			ТестоваяПанель = ЭтаФорма.ЭлементыФормы[Стр.ИмяПанели];
			ТестоваяСтраница = ЭтаФорма.ЭлементыФормы[Стр.ИмяПанели].Страницы[Стр.ИмяСтраницы];
		Исключение
			Результат = Ложь;
		КонецПопытки;
	КонецЦикла;
	Возврат Результат;
КонецФункции // арПроверитьКорректностьДереваФормы()

//Функция для получения номенклатуры, входящей в группу при добавлении группы
//
// Параметры: 
//   СписокГруппРодителей  - добавляемая группа.
//
// Возвращаемое значение: 
//   Возвращаемое значение - массив номенклатуры, входящей в группу.
//
Функция арПолучитьВходящуюНоменклатуру(СписокГруппРодителей) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|ГДЕ
	|	(СправочникНоменклатура.ЭтоГруппа = ЛОЖЬ) И
	|	(СправочникНоменклатура.Родитель В ИЕРАРХИИ(&Родитель))";
	Запрос.УстановитьПараметр("Родитель", СписокГруппРодителей);
	МассивНоменклатуры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	Возврат МассивНоменклатуры;
КонецФункции // арПолучитьВходящуюНоменклатуру()

//Функция заполняет состав документа в списке документов АРМ
//
// Параметры: 
//   Элемент - табличное поле списка документов
//   Табличное поле - табличное поле состава документа
//   Структура дополнительных полей - структура, содержащая идентификаторы приватных полей документа
//
Процедура арПолучитьСоставДокумента(Элемент, ТабличноеПоле, СтруктураДополнительныхПолей = Неопределено) Экспорт
	Документ=Элемент.ТекущаяСтрока;
	
	ТабличноеПоле.Значение.Очистить();
	Если Документ=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВидДокумента=Документ.Метаданные().Имя;
	
	Запрос=Новый Запрос;	
	ТекстЗапроса = "	
	|ВЫБРАТЬ
	|	ДокументТовары.НомерСтроки,
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Количество,
	|	ДокументТовары.ЕдиницаИзмерения,
	|	ДокументТовары.Коэффициент";
	Если СтруктураДополнительныхПолей <> Неопределено И СтруктураДополнительныхПолей.Количество() > 0 Тогда
		Для Каждого Элемент Из СтруктураДополнительныхПолей Цикл
			ТекстЗапроса = ТекстЗапроса + ",
			|ДокументТовары."+Элемент.Значение;
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ИЗ
	|	Документ." + ВидДокумента + ".Товары КАК ДокументТовары
	|ГДЕ
	|	ДокументТовары.Ссылка = &Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Результат=Запрос.Выполнить();
	ТабличноеПоле.Значение=Результат.Выгрузить(ОбходРезультатаЗапроса.Прямой);
КонецПроцедуры // арПолучитьСоставДокумента()

Процедура арОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник) Экспорт
	Если ИмяСобытия="ЗакрытьВсеАРМ" Тогда
		Если ЭтаФорма.Открыта() Тогда
			ЭтаФорма.Закрыть();
		КонецЕсли;
		ИмяСобытия = "";
	ИначеЕсли ИмяСобытия = "ОбновилисьЗаметки" Тогда
		Попытка
			ТекСтрока = ЭтаФорма.ЭлементыФормы.Список.ТекущаяСтрока;
		Исключение
			ТекСтрока = Неопределено;
		КонецПопытки;
		
		удОбновитьКартинкуЗаметок(ТекСтрока, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры
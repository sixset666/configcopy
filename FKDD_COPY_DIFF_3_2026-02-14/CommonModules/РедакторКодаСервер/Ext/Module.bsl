#Область ПрограммныйИнтерфейс

#Область СозданиеЭлементовФормы

Процедура ФормаПриСозданииНаСервере(Форма, ВидРедактора = Неопределено) Экспорт
	Если ВидРедактора = Неопределено Тогда
		ВидРедактора = ТекущийВариантРедактораКода1С();
	КонецЕсли;
	ВариантыРедактора = РедакторКодаКлиентСервер.ВариантыРедактораКода();
	
	ПараметрыСеансаВХранилище = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
		ОбщегоНазначенияКлиентСервер.КлючОбъектаВХранилищеНастроек(),
		ОбщегоНазначенияКлиентСервер.КлючНастроекПараметровСеанса());
	Если Тип(ПараметрыСеансаВХранилище) = Тип("Структура") Тогда
		Если ПараметрыСеансаВХранилище.Свойство("ПолеHTMLПостроеноНаWebkit") Тогда
			Если Не ПараметрыСеансаВХранилище.ПолеHTMLПостроеноНаWebkit Тогда
				ВидРедактора = ВариантыРедактора.Текст;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ИмяРеквизитаВидРедактора=РедакторКодаКлиентСервер.ИмяРеквизитаРедактораКодаВидРедактора();
	ИмяРеквизитаАдресБиблиотеки=РедакторКодаКлиентСервер.ИмяРеквизитаРедактораКодаАдресБиблиотеки();
	ИмяРеквизитаРедактораКодаСписокРедакторовФормы = РедакторКодаКлиентСервер.ИмяРеквизитаРедактораКодаСписокРедакторовФормы();
	
	МассивРеквизитов=Новый Массив;
	МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизитаВидРедактора, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20,
		ДопустимаяДлина.Переменная)), "", "", Истина));
	МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизитаАдресБиблиотеки, Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0,
		ДопустимаяДлина.Переменная)), "", "", Истина));
	МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизитаРедактораКодаСписокРедакторовФормы, Новый ОписаниеТипов, "", "", Истина));
		
	Форма.ИзменитьРеквизиты(МассивРеквизитов);
	
	Форма[ИмяРеквизитаВидРедактора]=ВидРедактора;
	Форма[ИмяРеквизитаАдресБиблиотеки] = ПоместитьБиблиотекуВоВременноеХранилище(Форма.УникальныйИдентификатор, ВидРедактора);
	Форма[ИмяРеквизитаРедактораКодаСписокРедакторовФормы] = Новый Структура;
КонецПроцедуры

Процедура СоздатьЭлементыРедактораКода(Форма, ИдентификаторРедактора, ПолеРедактора, ЯзыкРедактора = "bsl") Экспорт
	ИмяРеквизитаВидРедактора=РедакторКодаКлиентСервер.ИмяРеквизитаРедактораКодаВидРедактора();
	
	ВидРедактора = Форма[ИмяРеквизитаВидРедактора];
	//ВариантыРедактора = РедакторКодаКлиентСервер.ВариантыРедактораКода();
	//
	//ИмяРеквизитаЭлемента=РедакторКодаКлиентСервер.ИмяРеквизитаРедактораКода(ИдентификаторРедактора);

	//// 1. Создаем реквизит
	//МассивРеквизитов=Новый Массив;
	//МассивРеквизитов.Добавить(Новый РеквизитФормы(ИмяРеквизитаЭлемента, Новый ОписаниеТипов("Строка", ,
	//	Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)), "", ИдентификаторРедактора, Истина));

	//Форма.ИзменитьРеквизиты(МассивРеквизитов);
	
	Если РедакторКодаКлиентСервер.РедакторКодаИспользуетПолеHTML(ВидРедактора) Тогда
		ПолеРедактора.Вид = ВидПоляФормы.ПолеHTMLДокумента;
		ПолеРедактора.УстановитьДействие("ДокументСформирован", "Подключаемый_ПолеРедактораДокументСформирован");
		//ПолеРедактора.УстановитьДействие("ПриНажатии", "Подключаемый_ПолеРедактораПриНажатии");
	Иначе
		ПолеРедактора.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
	КонецЕсли;

	ДанныеРедактора = Новый Структура;
	ДанныеРедактора.Вставить("Язык", ЯзыкРедактора);
	ДанныеРедактора.Вставить("ПолеРедактора", ПолеРедактора.Имя);
	ДанныеРедактора.Вставить("ИмяРеквизита", ПолеРедактора.ПутьКДанным);
	
	Форма[РедакторКодаКлиентСервер.ИмяРеквизитаРедактораКодаСписокРедакторовФормы()].Вставить(ИдентификаторРедактора,  ДанныеРедактора);	
КонецПроцедуры

#КонецОбласти

Функция ПоместитьБиблиотекуВоВременноеХранилище(ИдентификаторФормы, ВидРедактора=Неопределено) Экспорт
	Если ВидРедактора = Неопределено Тогда
		ВидРедактора = ТекущийВариантРедактораКода1С();
	КонецЕсли;
	ВариантыРедактора = РедакторКодаКлиентСервер.ВариантыРедактораКода();
	
	Если ВидРедактора = ВариантыРедактора.Monaco Тогда
		ДвоичныеДанныеБиблиотеки=ПолучитьОбщийМакет("MonacoEditor");
	ИначеЕсли ВидРедактора = ВариантыРедактора.Ace Тогда
		ДвоичныеДанныеБиблиотеки=ПолучитьОбщийМакет("Ace");
	Иначе
		Возврат Неопределено;
	КонецЕсли;

	КаталогНаСервере=ПолучитьИмяВременногоФайла();
	СоздатьКаталог(КаталогНаСервере);

	Поток=ДвоичныеДанныеБиблиотеки.ОткрытьПотокДляЧтения();

	ЧтениеZIP=Новый ЧтениеZipФайла(Поток);
	ЧтениеZIP.ИзвлечьВсе(КаталогНаСервере, РежимВосстановленияПутейФайловZIP.Восстанавливать);

	СтруктураБиблиотеки=Новый Соответствие;

	ФайлыАрхива=НайтиФайлы(КаталогНаСервере, "*", Истина);
	Для Каждого ФайлБиблиотеки Из ФайлыАрхива Цикл
		КлючФайла=СтрЗаменить(ФайлБиблиотеки.ПолноеИмя, КаталогНаСервере + ПолучитьРазделительПути(), "");
		Если ФайлБиблиотеки.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли;

		СтруктураБиблиотеки.Вставить(КлючФайла, Новый ДвоичныеДанные(ФайлБиблиотеки.ПолноеИмя));
	КонецЦикла;

	АдресБиблиотеки=ПоместитьВоВременноеХранилище(СтруктураБиблиотеки, ИдентификаторФормы);

	Попытка
		УдалитьФайлы(КаталогНаСервере);
	Исключение
		// TODO:
	КонецПопытки;

	Возврат АдресБиблиотеки;
КонецФункции

#Область НастройкиИнструментов

Функция ТекущийВариантРедактораКода1С() Экспорт
	РедакторКода = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
	ОбщегоНазначенияКлиентСервер.КлючДанныхНастроекВХранилищеНастроек(), "РедакторКода1С",
	РедакторКодаКлиентСервер.ВариантРедактораПоУмолчанию());
	
	ПараметрыСеанса1 = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
	ОбщегоНазначенияКлиентСервер.КлючОбъектаВХранилищеНастроек(),
	ОбщегоНазначенияКлиентСервер.КлючНастроекПараметровСеанса());
	
	Если Тип(ПараметрыСеанса1) = Тип("Структура") Тогда
		Если ПараметрыСеанса1.ПолеHTMLПостроеноНаWebkit<>Истина Тогда
			РедакторКода = РедакторКодаКлиентСервер.ВариантыРедактораКода().Текст;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РедакторКода;
КонецФункции

// Установить вариант редактора кода 1С.
// 
// Параметры:
//  НовыйВариант - Строка - Устанавливаемый вариант редактора
Процедура УстановитьВариантРедактораКода1С(НовыйВариант) Экспорт
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ОбщегоНазначенияКлиентСервер.КлючДанныхНастроекВХранилищеНастроек(), "РедакторКода1С", НовыйВариант);
КонецПроцедуры
#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// предназначен для модулей, которые являются частью некоторой функциональной подсистемы. В нем должны быть размещены экспортные процедуры и функции, которые допустимо вызывать только из других функциональных подсистем этой же библиотеки.
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
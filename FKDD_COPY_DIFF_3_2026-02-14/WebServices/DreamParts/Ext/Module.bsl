
Функция getStock() Экспорт
	//Возврат ПолучитьОстаткиНоменклатуры_DP(GetStockRequest);  //GetStockResponse 
	
	Пакет_DreamParts = ФабрикаXDTO.Пакеты.Получить("http://dreamparts.archive.systems/v1/");
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦеныСрезПоследних.Номенклатура,
	               |	МАКСИМУМ(ЦеныСрезПоследних.Период) КАК Период
	               |ПОМЕСТИТЬ ВТ_ПоследняяЗаписьЗакупки
	               |ИЗ
	               |	РегистрСведений.Цены.СрезПоследних(
	               |			,
	               |			ТипЦен = &ЦенаЗакупки
	               |				И Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)) КАК ЦеныСрезПоследних
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЦеныСрезПоследних.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЦеныСрезПоследних.Номенклатура,
	               |	МАКСИМУМ(ЦеныСрезПоследних.Период) КАК Период
	               |ПОМЕСТИТЬ ВТ_ПоследняяЗаписьПродажи
	               |ИЗ
	               |	РегистрСведений.Цены.СрезПоследних(
	               |			,
	               |			ТипЦен = &ЦенаПродажи
	               |				И Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)) КАК ЦеныСрезПоследних
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЦеныСрезПоследних.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПоследняяЗаписьЗакупки.Номенклатура,
	               |	МИНИМУМ(ЦеныСрезПоследних.Цена) КАК Цена,
	               |	ВТ_ПоследняяЗаписьЗакупки.Период
	               |ПОМЕСТИТЬ ВТ_ЦеныЗакупочные
	               |ИЗ
	               |	ВТ_ПоследняяЗаписьЗакупки КАК ВТ_ПоследняяЗаписьЗакупки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(, ТипЦен = &ЦенаЗакупки) КАК ЦеныСрезПоследних
	               |		ПО ВТ_ПоследняяЗаписьЗакупки.Номенклатура = ЦеныСрезПоследних.Номенклатура
	               |			И ВТ_ПоследняяЗаписьЗакупки.Период = ЦеныСрезПоследних.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ПоследняяЗаписьЗакупки.Номенклатура,
	               |	ВТ_ПоследняяЗаписьЗакупки.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПоследняяЗаписьПродажи.Номенклатура,
	               |	МИНИМУМ(ЦеныСрезПоследних.Цена) КАК Цена,
	               |	ВТ_ПоследняяЗаписьПродажи.Период
	               |ПОМЕСТИТЬ ВТ_ЦеныПродажные
	               |ИЗ
	               |	ВТ_ПоследняяЗаписьПродажи КАК ВТ_ПоследняяЗаписьПродажи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(, ТипЦен = &ЦенаПродажи) КАК ЦеныСрезПоследних
	               |		ПО ВТ_ПоследняяЗаписьПродажи.Номенклатура = ЦеныСрезПоследних.Номенклатура
	               |			И ВТ_ПоследняяЗаписьПродажи.Период = ЦеныСрезПоследних.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ПоследняяЗаписьПродажи.Номенклатура,
	               |	ВТ_ПоследняяЗаписьПродажи.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_ПоследняяЗаписьЗакупки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_ПоследняяЗаписьПродажи
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК quantity,
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.Наименование КАК title,
	               |	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Наименование, """") КАК unit,
	               |	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.Номенклатура.Артикул, """") КАК number1,
	               |	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.Номенклатура.Производитель.Наименование, """") КАК make,
	               |	0.1 КАК min_order,
	               |	ЛОЖЬ КАК is_vat_used,
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.Код КАК dms_id,
	               |	НЕ ОстаткиТоваровКомпанииОстатки.Номенклатура.ЗапретПродажи КАК is_available,
	               |	ЛОЖЬ КАК hide_quantity,
	               |	ЕСТЬNULL(ВТ_ЦеныПродажные.Цена, 0) КАК price,
	               |	ЕСТЬNULL(ВТ_ЦеныЗакупочные.Цена, 0) КАК cost
	               |ИЗ
	               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Дата, Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныПродажные КАК ВТ_ЦеныПродажные
	               |		ПО ОстаткиТоваровКомпанииОстатки.Номенклатура = ВТ_ЦеныПродажные.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныЗакупочные КАК ВТ_ЦеныЗакупочные
	               |		ПО ОстаткиТоваровКомпанииОстатки.Номенклатура = ВТ_ЦеныЗакупочные.Номенклатура";
	
	 Запрос.УстановитьПараметр("ГруппаНоменклатуры", Справочники.Номенклатура.НайтиПоКоду("ЦБ003259"));             //fiat
	 Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	 Запрос.УстановитьПараметр("ЦенаЗакупки", Справочники.ТипыЦен.ОсновнойТипЦенЗакупки);
	 Запрос.УстановитьПараметр("ЦенаПродажи", Справочники.ТипыЦен.ОсновнойТипЦенПродажи);
	 
	 Запчасти = Запрос.Выполнить().Выбрать(); 
	 
    GetStockResponseType = ФабрикаXDTO.Тип("http://dreamparts.archive.systems/v1/", "GetStockResponse");
    GetStockResponse = ФабрикаXDTO.Создать(GetStockResponseType);
     
      
    StockItemType = ФабрикаXDTO.Тип("http://dreamparts.archive.systems/v1/", "StockItem");  
     
    Пока Запчасти.Следующий() Цикл      
         
        Part = ФабрикаXDTO.Создать(StockItemType);
         
        Part.make = Запчасти.make;
        Part.number = Запчасти.number1;
        Part.title = Запчасти.title;
        Part.quantity  = Запчасти.quantity;
		Part.unit = Запчасти.unit;
        Part.min_order = Запчасти.min_order; //необязателен
		Part.cost  = Запчасти.cost;
		Part.price = Запчасти.price;
		Part.is_vat_used = Запчасти.is_vat_used; //НДС есть
		Part.dms_id = Запчасти.dms_id; //необязателен
		Part.is_available = Запчасти.is_available;
		Part.hide_quantity = Запчасти.hide_quantity; //необязателен
		
		
		GetStockResponse.Part.Добавить(Part);
         
    КонецЦикла;
     
    Возврат GetStockResponse;

КонецФункции

Функция getMovements(date_from,date_till) Экспорт
	
	Пакет_DreamParts = ФабрикаXDTO.Пакеты.Получить("http://dreamparts.archive.systems/v1/");
	
	GetMovementsResponseType = ФабрикаXDTO.Тип("http://dreamparts.archive.systems/v1/", "GetMovementsResponse");
	GetMovementsResponse = ФабрикаXDTO.Создать(GetMovementsResponseType);
		
	MovementType = ФабрикаXDTO.Тип("http://dreamparts.archive.systems/v1/", "Movement");  
		
	date_from1 = date_from;
	//	
	date_till1 = date_till;

	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ОстаткиТоваровКомпанииОбороты.Номенклатура.Производитель.Наименование, """") КАК make,
	               |	ЕСТЬNULL(ОстаткиТоваровКомпанииОбороты.Номенклатура.Артикул, """") КАК number1,
	               |	ОстаткиТоваровКомпанииОбороты.Номенклатура.Код КАК dms_id,
	               |	ЕСТЬNULL(ОстаткиТоваровКомпанииОбороты.КоличествоПриход, 0) КАК quantity_arrived,
	               |	ЕСТЬNULL(ОстаткиТоваровКомпанииОбороты.КоличествоРасход, 0) КАК quantity_left
	               |ИЗ
	               |	РегистрНакопления.ОстаткиТоваровКомпании.Обороты(&НачалоПериода, &КонецПериода, , Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)) КАК ОстаткиТоваровКомпанииОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода", date_from1);
	Запрос.УстановитьПараметр("КонецПериода", date_till1);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", Справочники.Номенклатура.НайтиПоКоду("ЦБ003259"));             //fiat
	
	Запчасти = Запрос.Выполнить().Выбрать(); 
     
    Пока Запчасти.Следующий() Цикл      
         
        movement = ФабрикаXDTO.Создать(MovementType);
         
        movement.make = Запчасти.make;
        movement.number = Запчасти.number1;
		movement.dms_id = Запчасти.dms_id; //необязателен
        movement.quantity_arrived  = Запчасти.quantity_arrived;
		movement.quantity_left = Запчасти.quantity_left;
	
		GetMovementsResponse.movement.Добавить(movement);
         
    КонецЦикла;
     
    Возврат GetMovementsResponse;

КонецФункции

Функция SearchParts (number) Экспорт
	//Возврат ПолучитьЭлементНоменклатуры_DP(SearchPartsRequest); //SearchPartsResponse 
	Пакет_DreamParts = ФабрикаXDTO.Пакеты.Получить("http://dreamparts.archive.systems/v1/");
	SearchPartsResponseType = ФабрикаXDTO.Тип("http://dreamparts.archive.systems/v1/", "SearchPartsResponse");
    SearchPartsResponse = ФабрикаXDTO.Создать(SearchPartsResponseType);

	number1 = number;
	
	Если number1 = "" тогда
		Возврат SearchPartsResponse;
	КонецЕсли;

	Запрос = Новый Запрос();
	Менеджер = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Номенклатура,
	               |	Номенклатура.Наименование КАК НоменклатураНаименование,
	               |	Номенклатура.ЗапретПродажи,
	               |	Номенклатура.Код,
	               |	Номенклатура.Артикул,
	               |	ЕСТЬNULL(Номенклатура.ОсновнаяЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмерения,
	               |	ЕСТЬNULL(Номенклатура.Производитель.Наименование, """") КАК Производитель
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Артикул = &Артикул";
	
	Запрос.УстановитьПараметр("Артикул", number1);//"0008904");
	
	ТаблицаНоменклатуры = Запрос.Выполнить().Выгрузить(); 

	Если ТаблицаНоменклатуры.Количество() = 0 тогда
		Возврат SearchPartsResponse;
	КонецЕсли;
	                        
	МассивСсылокНоменклатуры = ТаблицаНоменклатуры.Скопировать(,"Номенклатура");
	
	Запрос2 = Новый Запрос();
	Запрос2.МенеджерВременныхТаблиц = Менеджер;
	Запрос2.Текст = 
	"ВЫБРАТЬ
	|	ВТ_Номенклатура.Номенклатура,
	|	ВТ_Номенклатура.НоменклатураНаименование,
	|	ВТ_Номенклатура.ЗапретПродажи,
	|	ВТ_Номенклатура.Код,
	|	ВТ_Номенклатура.Артикул,
	|	ВТ_Номенклатура.ЕдиницаИзмерения,
	|	ВТ_Номенклатура.Производитель
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	&ВТ_Номенклатура КАК ВТ_Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныСрезПоследних.Номенклатура,
	|	МАКСИМУМ(ЦеныСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_ПоследняяЗаписьЗакупки
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			,
	|			ТипЦен = &ЦенаЗакупки
	|				И Номенклатура В (&СписокНоменклатуры)) КАК ЦеныСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныСрезПоследних.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныСрезПоследних.Номенклатура,
	|	МАКСИМУМ(ЦеныСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_ПоследняяЗаписьПродажи
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			,
	|			ТипЦен = &ЦенаПродажи
	|				И Номенклатура В (&СписокНоменклатуры)) КАК ЦеныСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныСрезПоследних.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоследняяЗаписьЗакупки.Номенклатура,
	|	МИНИМУМ(ЦеныСрезПоследних.Цена) КАК Цена,
	|	ВТ_ПоследняяЗаписьЗакупки.Период
	|ПОМЕСТИТЬ ВТ_ЦеныЗакупочные
	|ИЗ
	|	ВТ_ПоследняяЗаписьЗакупки КАК ВТ_ПоследняяЗаписьЗакупки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(, ТипЦен = &ЦенаЗакупки) КАК ЦеныСрезПоследних
	|		ПО ВТ_ПоследняяЗаписьЗакупки.Номенклатура = ЦеныСрезПоследних.Номенклатура
	|			И ВТ_ПоследняяЗаписьЗакупки.Период = ЦеныСрезПоследних.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПоследняяЗаписьЗакупки.Номенклатура,
	|	ВТ_ПоследняяЗаписьЗакупки.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоследняяЗаписьПродажи.Номенклатура,
	|	МИНИМУМ(ЦеныСрезПоследних.Цена) КАК Цена,
	|	ВТ_ПоследняяЗаписьПродажи.Период
	|ПОМЕСТИТЬ ВТ_ЦеныПродажные
	|ИЗ
	|	ВТ_ПоследняяЗаписьПродажи КАК ВТ_ПоследняяЗаписьПродажи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(, ТипЦен = &ЦенаПродажи) КАК ЦеныСрезПоследних
	|		ПО ВТ_ПоследняяЗаписьПродажи.Номенклатура = ЦеныСрезПоследних.Номенклатура
	|			И ВТ_ПоследняяЗаписьПродажи.Период = ЦеныСрезПоследних.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПоследняяЗаписьПродажи.Номенклатура,
	|	ВТ_ПоследняяЗаписьПродажи.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПоследняяЗаписьЗакупки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПоследняяЗаписьПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, Номенклатура В (&СписокНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Номенклатура.Номенклатура,
	|	ВТ_Номенклатура.НоменклатураНаименование КАК title,
	|	НЕ ВТ_Номенклатура.ЗапретПродажи КАК is_available,
	|	ВТ_Номенклатура.Код КАК dms_id,
	|	ВТ_Номенклатура.Артикул КАК number1,
	|	ВТ_Номенклатура.ЕдиницаИзмерения КАК unit,
	|	ВТ_Номенклатура.Производитель КАК make,
	|	ЕСТЬNULL(ВТ_ЦеныЗакупочные.Цена, 0) КАК cost,
	|	ЕСТЬNULL(ВТ_ЦеныПродажные.Цена, 0) КАК price,
	|	ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК quantity,
	|	0.1 КАК min_order,
	|	ЛОЖЬ КАК is_vat_used,
	|	ЛОЖЬ КАК hide_quantity
	|ИЗ
	|	ВТ_Номенклатура КАК ВТ_Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
	|		ПО ВТ_Номенклатура.Номенклатура = ВТ_Остатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныЗакупочные КАК ВТ_ЦеныЗакупочные
	|		ПО ВТ_Номенклатура.Номенклатура = ВТ_ЦеныЗакупочные.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныПродажные КАК ВТ_ЦеныПродажные
	|		ПО ВТ_Номенклатура.Номенклатура = ВТ_ЦеныПродажные.Номенклатура";
	
	 Запрос2.УстановитьПараметр("ВТ_Номенклатура", ТаблицаНоменклатуры);
	 Запрос2.УстановитьПараметр("СписокНоменклатуры", МассивСсылокНоменклатуры);
	 Запрос2.УстановитьПараметр("ЦенаЗакупки", Справочники.ТипыЦен.ОсновнойТипЦенЗакупки);
	 Запрос2.УстановитьПараметр("ЦенаПродажи", Справочники.ТипыЦен.ОсновнойТипЦенПродажи);

	 НоменклатураЗапрос = Запрос2.Выполнить().Выбрать();
	 
	 StockItemType = ФабрикаXDTO.Тип("http://dreamparts.archive.systems/v1/", "StockItem");  
	 
	 Пока НоменклатураЗапрос.Следующий() Цикл      
         
	 	Part = ФабрикаXDTO.Создать(StockItemType);
        Part.make = НоменклатураЗапрос.make;
        Part.number = НоменклатураЗапрос.number1;
        Part.title = НоменклатураЗапрос.title;
        Part.quantity  = НоменклатураЗапрос.quantity;
		Part.unit = НоменклатураЗапрос.unit;
        Part.min_order = НоменклатураЗапрос.min_order; //необязателен
		Part.cost  = НоменклатураЗапрос.cost;
		Part.price = НоменклатураЗапрос.price;
		Part.is_vat_used = НоменклатураЗапрос.is_vat_used; //НДС есть
		Part.dms_id = НоменклатураЗапрос.dms_id; //необязателен
		Part.is_available = НоменклатураЗапрос.is_available;
		Part.hide_quantity = НоменклатураЗапрос.hide_quantity; //необязателен
		
		SearchPartsResponse.Part.Добавить(Part);
         
    КонецЦикла;
     
    Возврат SearchPartsResponse;

КонецФункции
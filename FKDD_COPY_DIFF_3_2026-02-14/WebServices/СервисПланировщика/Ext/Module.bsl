//***************************************************
//Общие Процедуры и Функции
//***************************************************

// Функция производит инициализацию среды,
// заполняя параметры сеанса: пользователь, компьютер, организация и т.д. 
Функция Инициализация() Экспорт
	
	// Авторизуемся и получим права текущего пользователя
	Пользователь = обОпределитьТекущегоПользователя();
	ПараметрыСеанса.Пользователь = Пользователь;
	
	// Найдем соответствующий элемент справочника Компьютеры (или создадим, если новый)
	Имя = ИмяКомпьютера();
	Комп = Справочники.Компьютеры.НайтиПоНаименованию(Имя,Истина);
	Если Комп.Пустая() Тогда
		Комп = Справочники.Компьютеры.СоздатьЭлемент();
		Комп.УстановитьНовыйКод("");
		Комп.Наименование = Имя;
		Комп.Записать();
	КонецЕсли;
	
	// Установим флаги режима работы с оборудованием
	ПараметрыСеанса.Компьютер = Комп.Ссылка;
	ПараметрыСеанса.РежимРаботы = "02"; // Web-сервисы с оборудованием не работают
	
	// Получение прав и настроек текущего пользователя
	глПрава = обПолучитьПраваИНастройкиПользователя(Пользователь);
	Подразделение = Пользователь.Подразделение;
	Организация = Пользователь.Организация;
	
	// Установим значения основных параметров Сеанса
	ПараметрыСеанса.Организация = Организация;
	ПараметрыСеанса.ПодразделениеКомпании = Подразделение;
	
	Возврат Истина;
	
КонецФункции

// функция возвращает пространство имен по-умолчанию
Функция URIПространствоИмен()	
	
	Возврат "http://www.activewebservices.ru/TimePlanner";	
	
КонецФункции	

//Функция возвращает Истина, если значение null или Неопределено
Функция ЗначениеНеОпределено(Значение)
	
	Возврат  (Значение = null) или (Значение = Неопределено) 
	
КонецФункции	

// Функция приводит значение произвольного типа к строковому
Функция ПривестиКСтроке(Значение)	
	
	Если 	ЗначениеНеОпределено(Значение) Тогда		
		Возврат "";		
	Иначе		
		Возврат СокрЛП(Значение);			
	КонецЕсли;
	
КонецФункции	

// Функция приводит значение произвольного типа к числовому,
// в случае неудачи возвращается "0"
Функция ПривестиКЧислу(Значение)	
	
	Результат =0;
	
	Если	ЗначениеНеОпределено(Значение) Тогда
		Возврат Результат;	
	Иначе
		Попытка
			Результат = Число(Значение);			
		Исключение
			Результат = 0;
		КонецПопытки;
		
		Возврат Результат;		
		
	КонецЕсли;           		
	
КонецФункции

// Функция приводит значение произвольного типа к типу дата,
// в случае неудачи возвращается "01/01/0001"
Функция ПривестиКДате(Значение)
	
	Если ЗначениеНеОпределено(Значение) Тогда
		Возврат Дата('00010101');			
	Иначе
		Возврат Значение;			
	КонецЕсли;	
	
КонецФункции

// Создает новый XDTO объект по имени ТипОбъекта из пространства имен URIПространствоИмен()
Функция СоздатьОбъектXDTOПоТипу(ТипОбъекта)
	
	ЭлементТип = ФабрикаXDTO.Тип(URIПространствоИмен(), ТипОбъекта);					
	Возврат ФабрикаXDTO.Создать(ЭлементТип);
	
КонецФункции	

// Определяет необходимость ограничить выборку запроса по количеству
Функция ЗапросОграниченКоличественно(КонтекстЗапроса)
	
	Если ЗначениеНеОпределено(КонтекстЗапроса) Тогда
		Возврат Ложь;	
	КонецЕсли;	
	
	Если КонтекстЗапроса.Количество = Неопределено Тогда
		Возврат Ложь;	
	КонецЕсли;	
	
	Возврат КонтекстЗапроса.Количество>0;	
	
КонецФункции	

// Определяет необходимость ограничить выборку запроса по элементам
Функция ЗапросОграниченЭлементами(КонтекстЗапроса)
	
	Если (ЗначениеНеОпределено(КонтекстЗапроса) ИЛИ КонтекстЗапроса.Идентификаторы=Неопределено)Тогда
		Возврат Ложь;	
	КонецЕсли;	
	
	Если (КонтекстЗапроса.Идентификаторы.Элементы.Количество()>0)Тогда
		Возврат Истина;	
	КонецЕсли;		
	
	Возврат Ложь;
	
КонецФункции	

// Определяет необходимость ограничить выборку запроса по параметру Расширенный
Функция ЗапросОграниченДопПараметрами(КонтекстЗапроса)
	
	Если ЗначениеНеОпределено(КонтекстЗапроса)Тогда
		Возврат Ложь;	
	КонецЕсли;	
	
	Если ЗначениеНеОпределено(КонтекстЗапроса.Расширенный) Тогда
		Возврат Ложь;	
	КонецЕсли;	
	
	Возврат КонтекстЗапроса.Расширенный;
	
КонецФункции

// функция возвращает первый не пустой элемент массива
Функция ПолучитьПервыйНеПустойЭлементМассива(МассивЭлементов)
	
	Для Каждого Элемент из МассивЭлементов Цикл 
		Если (ЗначениеЗаполнено(Элемент)) Тогда 
			Возврат Элемент;	
		КонецЕсли;		
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

// Создает сообщение об ошибке по коду "КодОшибки" и описанию ошибки "ОписаниеОшибки"
Функция ВернутьОписаниеОшибки(КодОшибки, ОписаниеОшибки)
	
	Если 		(КодОшибки = 1)	Тогда
		Возврат "Ошибка["+КодОшибки+"](элемент не найден): " + ОписаниеОшибки;
	ИначеЕсли 	(КодОшибки = 2)	Тогда
		Возврат "Ошибка["+КодОшибки+"](тип не определен): " + ОписаниеОшибки;
	ИначеЕсли 	(КодОшибки = 3)	Тогда
		Возврат "Ошибка["+КодОшибки+"](редактирование запрещено): " + ОписаниеОшибки;
	КонецЕсли;
	
	Возврат "Ошибка["+КодОшибки+"](неизвестная ошибка): "+ОписаниеОшибки;
	
КонецФункции

// Функция формирует результат выполнения метода
//
// Параметры:
//	-	Успешно 		- "Истина", если метод выполнился без ошибок;
//	-	Параметры		- выходные параметры, обычно используется для возврата кода созданного/измененного объекта;
// При возникновении ошибки:
//	-	ИмяМодуля 		- содержит информациою о модуле, где произошла ошибка;
//	-	ОписаниеОшибки 	- содержит описание ошибки;
//	-	НомерСтроки 	- номер строки в модуле "ИмяМодуля";
//	-	ИсходнаяСтрока 	- исходная строка в модуле "ИмяМодуля"; 
Функция СформироватьРезультатВыполнения(ТипРезультата = Неопределено,	Успешно = Ложь,	Объект = Неопределено,	Параметры = Неопределено, 	КодОшибки = 0,	Ошибка = Неопределено, 	СообщениеСервера = Неопределено, ПредупрежденияСервера = Неопределено)	
	
	Если (ТипРезультата = Неопределено) Тогда
		ВызватьИсключение "Не задан ТипРезультата";	
	КонецЕсли;	
	
	РезультатВыполнения = СоздатьОбъектXDTOПоТипу(ТипРезультата);
	
	РезультатВыполнения.Успешно = Успешно;
	
	Если (Объект <> Неопределено) Тогда
		РезультатВыполнения.Объект = Объект;
	КонецЕсли;	
	
	Если (Параметры <> Неопределено) Тогда
		РезультатВыполнения.Параметры = Параметры;
	КонецЕсли;	
	
	РезультатВыполнения.КодОшибки = КодОшибки;
	
	Если (Ошибка <> Неопределено) Тогда
		РезультатВыполнения.ИмяМодуля = Ошибка.ИмяМодуля;
		РезультатВыполнения.ОписаниеОшибки = Ошибка.Описание;
		РезультатВыполнения.НомерСтроки = Ошибка.НомерСтроки;
		РезультатВыполнения.ИсходнаяСтрока = Ошибка.ИсходнаяСтрока;	
	КонецЕсли;		
	
	Если (СообщениеСервера <> Неопределено) Тогда
		РезультатВыполнения.СообщениеСервера = СообщениеСервера;
	КонецЕсли;		
	
	Если (ПредупрежденияСервера <> Неопределено) Тогда
		РезультатВыполнения.ПредупрежденияСервера = ПредупрежденияСервера;
	КонецЕсли;		
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Функция выбирает первую строку из ТаблицыЗначений "ТЗ", имеющую пару "ОтборКлюч" - "ОтборЗначение"
Функция НайтиСтрокиОтбором(ТЗ,ОтборКлюч,ОтборЗначение)
	
	Отбор = Новый Структура();
	Отбор.Вставить(ОтборКлюч,ОтборЗначение);	
	
	НайденныеСтроки = ТЗ.НайтиСтроки(Отбор);
	Если (НайденныеСтроки.Количество()>0) Тогда
		Возврат НайденныеСтроки[0];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	

// Функция создает XDTO объект "КонтекстЗапроса" с параметрами "Код", "Расширенный"
Функция СформироватьКонтекстЗапросаПоКоду(Код,Расширенный = Ложь)
	
	КонтекстЗапроса = СоздатьОбъектXDTOПоТипу("КонтекстЗапроса");
	КонтекстЗапроса.Количество = 1;
	КонтекстЗапроса.Расширенный = Расширенный;
	
	МассивСтрок = СоздатьОбъектXDTOПоТипу("МассивСтрок");				
	МассивСтрок.Элементы.Добавить(Код);
	КонтекстЗапроса.Идентификаторы =  МассивСтрок;
	
	Возврат КонтекстЗапроса;
	
КонецФункции

// Функция собирает наименование автомобиля исходя из ввдененных параметров
Функция СформироватьНаименованияАвтомобиля(ВариантКомплектации, Модель, Цвет, VIN, ГосНомер)
	
	НовоеНаименование="";
	
	Если ЗначениеЗаполнено(ВариантКомплектации) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда НовоеНаименование=НовоеНаименование+" "; КонецЕсли; 
		НовоеНаименование=НовоеНаименование+СокрЛП(ВариантКомплектации);
	Иначе
		Если ЗначениеЗаполнено(Модель) Тогда
			Если НЕ ПустаяСтрока(НовоеНаименование) Тогда НовоеНаименование=НовоеНаименование+" "; КонецЕсли; 
			НовоеНаименование=НовоеНаименование+СокрЛП(Модель);
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Цвет) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда НовоеНаименование=НовоеНаименование+" "; КонецЕсли; 
		НовоеНаименование=НовоеНаименование+СокрЛП(Цвет);
	КонецЕсли; 
	Если ЗначениеЗаполнено(ГосНомер) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда НовоеНаименование=НовоеНаименование+" "; КонецЕсли; 
		НовоеНаименование=НовоеНаименование+"№ "+СокрЛП(ГосНомер);
	КонецЕсли;
	Если ЗначениеЗаполнено(VIN) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда НовоеНаименование=НовоеНаименование+" "; КонецЕсли; 
		НовоеНаименование=НовоеНаименование+"VIN "+СокрЛП(VIN);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(НовоеНаименование) Тогда 
		Возврат НовоеНаименование;
	КонецЕсли; 
	
КонецФункции

// Процедура заполняет реквизиты документа Заказ-Наряд значениями по-умолчанию
Процедура ЗаполнитьДокументДаннымиПоУмолчанию(ЭтотОбъект,Основание=Неопределено,Копирование=Ложь)
	
	Пользователь = ПараметрыСеанса.Пользователь;
	
	// Получим ссылку на набор прав и переменных окружения
	Попытка 	ПраваПользователя = ЭтотОбъект.Права;
	Исключение	ПраваПользователя = Неопределено;
	КонецПопытки; 
	
	// установим хоз. операцию по умолчанию
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ХозОперация) Тогда
		СписокХозОпераций=ЭтотОбъект.ПолучитьСписокХозОпераций();
		Для Каждого ХО Из СписокХозОпераций Цикл
			Если ХО.Пометка Тогда
				Попытка ЭтотОбъект.ХозОперация=Справочники.ХозОперации[ХО.Значение]; Исключение КонецПопытки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// попытаемся заполнить реквизиты по умолчанию
	Попытка ЭтотОбъект.Автор=?(НЕ ЗначениеЗаполнено(ЭтотОбъект.Автор),Пользователь,ЭтотОбъект.Автор); Исключение КонецПопытки;
	Попытка ЭтотОбъект.Организация=?(НЕ ЗначениеЗаполнено(ЭтотОбъект.Организация),ПараметрыСеанса.Организация,ЭтотОбъект.Организация); Исключение КонецПопытки;
	Попытка ЭтотОбъект.ПодразделениеКомпании=?(НЕ ЗначениеЗаполнено(ЭтотОбъект.ПодразделениеКомпании),ПараметрыСеанса.ПодразделениеКомпании,ЭтотОбъект.ПодразделениеКомпании); Исключение КонецПопытки;
	Попытка ЭтотОбъект.СкладКомпании=?(НЕ ЗначениеЗаполнено(ЭтотОбъект.СкладКомпании),обПраво("ОсновнойСкладКомпании",ПраваПользователя),ЭтотОбъект.СкладКомпании); Исключение КонецПопытки;
	Попытка ЭтотОбъект.КассаКомпании=?(НЕ ЗначениеЗаполнено(ЭтотОбъект.КассаКомпании),обПраво("ОсновнаяКассаКомпании",ПраваПользователя),ЭтотОбъект.КассаКомпании); Исключение КонецПопытки;
	Попытка ЭтотОбъект.ВалютаДокумента=?(НЕ ЗначениеЗаполнено(ЭтотОбъект.ВалютаДокумента),Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(),ЭтотОбъект.ВалютаДокумента); Исключение КонецПопытки;
	Попытка ЭтотОбъект.ВерсияОбъекта = Метаданные.Версия; Исключение КонецПопытки;
	
	Попытка 
		Если обПраво("ОтношениеКРегламентированномуУчету",ПраваПользователя) = Перечисления.ОтношениеКРегламентированномуУчету.ПросмотрТолькоРегламентированныхДокументов Тогда
			ЭтотОбъект.РегламентированныйУчет = Истина; 
		ИначеЕсли обПраво("ОтношениеКРегламентированномуУчету",ПраваПользователя) = Перечисления.ОтношениеКРегламентированномуУчету.ПросмотрТолькоНеРегламентированныхДокументов Тогда
			ЭтотОбъект.РегламентированныйУчет = Ложь; 
		ИначеЕсли обПраво("ОтношениеКРегламентированномуУчету",ПраваПользователя) = Перечисления.ОтношениеКРегламентированномуУчету.ПросмотрВсехДокументовИстина Тогда
			ЭтотОбъект.РегламентированныйУчет = Истина; 
		ИначеЕсли обПраво("ОтношениеКРегламентированномуУчету",ПраваПользователя) = Перечисления.ОтношениеКРегламентированномуУчету.ПросмотрВсехДокументовЛожь   Тогда
			ЭтотОбъект.РегламентированныйУчет = Ложь; 
		ИначеЕсли обПраво("ОтношениеКРегламентированномуУчету",ПраваПользователя) = Перечисления.ОтношениеКРегламентированномуУчету.РедактированиеВсехДокументовИстина  Тогда
			ЭтотОбъект.РегламентированныйУчет = Истина; 
		ИначеЕсли обПраво("ОтношениеКРегламентированномуУчету",ПраваПользователя) = Перечисления.ОтношениеКРегламентированномуУчету.РедактированиеВсехДокументовЛожь  Тогда
			ЭтотОбъект.РегламентированныйУчет = Ложь; 	
		КонецЕсли;	
	Исключение 
	КонецПопытки;
	
	Попытка 
		Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Дата) Тогда
			#Если Клиент Тогда
			НаДату=РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
			#Иначе
			НаДату=ТекущаяДата();
			#КонецЕсли
			ЭтотОбъект.Дата = НаДату;
		Иначе
			НаДату=ЭтотОбъект.Дата;
		КонецЕсли; 
	Исключение
		НаДату = ТекущаяДата();
		ЭтотОбъект.Дата = НаДату;
	КонецПопытки;
	Попытка
		СтруктураКурса = обКурсДляВалюты(ЭтотОбъект.ВалютаДокумента,НаДату);
		ЭтотОбъект.КурсДокумента = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Исключение
	КонецПопытки;
	Попытка
		СтруктураКурса = обКурсДляВалюты(Константы.ВалютаУправленческогоУчетаКомпании.Получить(),НаДату);
		ЭтотОбъект.КурсВалютыУпр = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);;
	Исключение
	КонецПопытки;
	
	Попытка
		Если ЭтотОбъект.СкладКомпании.Розничный Тогда 
			ЭтотОбъект.ТипЦен=ЭтотОбъект.СкладКомпании.ТипЦенРозничнойТорговли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	// тип цен
	Если ЭтотОбъект.Метаданные().Реквизиты.Найти("ТипЦен")<>Неопределено И НЕ ЗначениеЗаполнено(ЭтотОбъект.ТипЦен) Тогда
		Если ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ОтгрузкаТМЦ Тогда
			//Для документов продажи
			ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенПродажи",ПраваПользователя);
		ИначеЕсли ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ОтгрузкаАвтомобилей Тогда
			//Для документов продажи
			ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенПродажиАвтомобилей",ПраваПользователя);
		ИначеЕсли ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ПоступлениеТМЦ Тогда
			//Для документов покупки
			ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенЗакупки",ПраваПользователя);
		ИначеЕсли ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ПоставкаАвтомобилей Тогда
			//Для документов покупки
			ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенЗакупкиАвтомобилей",ПраваПользователя);
		Иначе
			//Для прочих документов тип цен покупка
			ЭтотОбъект.ТипЦен=обПраво("ОсновнойТипЦенЗакупки",ПраваПользователя);
		КонецЕсли;
	КонецЕсли;
	
	//Установим вид оплаты в соответствии с типом цен
	Попытка
		ЭтотОбъект.ВидОплаты=?(НЕ ЗначениеЗаполнено(ЭтотОбъект.ВидОплаты),ЭтотОбъект.ТипЦен.ВидОплаты,ЭтотОбъект.ВидОплаты);
	Исключение КонецПопытки; 
	
	Если (ЭтотОбъект.Метаданные().Реквизиты.Найти("Контрагент")<>Неопределено) И НЕ ЗначениеЗаполнено(ЭтотОбъект.Контрагент) Тогда
		Если Основание<>Неопределено Тогда
			Попытка
				ЭтотОбъект.Контрагент=Основание.Контрагент;
			Исключение
				ЭтотОбъект.Контрагент=Неопределено;
			КонецПопытки; 
		Иначе
			ЭтотОбъект.Контрагент=Неопределено;
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(ЭтотОбъект.Контрагент) Тогда
			Если ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ОтгрузкаТМЦ ИЛИ 
				ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ОтгрузкаАвтомобилей Тогда
				//Для документов продажи
				Покупатель=обПраво("ОсновнойПокупатель",ПраваПользователя);
				Если ЗначениеЗаполнено(Покупатель) Тогда
					ЭтотОбъект.Контрагент=Покупатель;
				КонецЕсли;
			ИначеЕсли ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ПоступлениеТМЦ ИЛИ 
				ЭтотОбъект.ХозОперация.Родитель=Справочники.ХозОперации.ПоставкаАвтомобилей Тогда
				//Для документов покупки
				Поставщик=обПраво("ОсновнойПоставщик",ПраваПользователя);
				Если ЗначениеЗаполнено(Поставщик) Тогда
					ЭтотОбъект.Контрагент=Поставщик;
				КонецЕсли;
			Иначе
				Покупатель=обПраво("ОсновнойПокупатель",ПраваПользователя);
				Если ЗначениеЗаполнено(Покупатель) Тогда
					ЭтотОбъект.Контрагент=Покупатель;
				КонецЕсли;
			КонецЕсли;
			//Если есть договор взаиморасчетов, подставим его из основного договора контрагента
			Если ЗначениеЗаполнено(ЭтотОбъект.Контрагент) Тогда
				ЭтотОбъект.ОбработкаРеквизита("Контрагент",);
			КонецЕсли;
		КонецЕсли;
		
		Если (ЭтотОбъект.Метаданные().Реквизиты.Найти("ДоговорВзаиморасчетов")<>Неопределено) И НЕ ЗначениеЗаполнено(ЭтотОбъект.ДоговорВзаиморасчетов) Тогда
			Если Основание<>Неопределено Тогда
				Попытка
					ЭтотОбъект.ДоговорВзаиморасчетов=Основание.ДоговорВзаиморасчетов;
				Исключение
					ЭтотОбъект.ДоговорВзаиморасчетов=Неопределено;
				КонецПопытки; 
			Иначе
				ЭтотОбъект.ДоговорВзаиморасчетов=Неопределено;
			КонецЕсли; 
			
			Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ДоговорВзаиморасчетов) Тогда
				
				ВидДоговора = Неопределено;
				Если (ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.ОтгрузкаТМЦ) ИЛИ
					(ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.ОтгрузкаАвтомобилей) ИЛИ
					(ЭтотОбъект.ХозОперация = Справочники.ХозОперации.УстановкаЦенАвтоработКонтрагента) ИЛИ
					(ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.ЗаказыНаОтгрузку) ИЛИ
					(ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.ПрочиеАктивы) Тогда
					
					ВидДоговора = Перечисления.ВидыДоговоров.Продажа;
					
				ИначеЕсли (ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.ПоступлениеТМЦ) ИЛИ
						(ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.ПоставкаАвтомобилей) ИЛИ
						(ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.Ценообразование) ИЛИ
						(ЭтотОбъект.ХозОперация.Родитель = Справочники.ХозОперации.ЗаказыНаПоставку) Тогда
						
					ВидДоговора = Перечисления.ВидыДоговоров.Покупка;
					
				Иначе
					
					ВидДоговора = Перечисления.ВидыДоговоров.Продажа;
					
				КонецЕсли;
				
				ЭтотОбъект.ДоговорВзаиморасчетов = Справочники.Контрагенты.ПолучитьДоговорВзаиморасчетов(ЭтотОбъект.Контрагент, ВидДоговора, ЭтотОбъект,, Истина, Истина);
			КонецЕсли;
				
			ЭтотОбъект.ОбработкаРеквизита("ДоговорВзаиморасчетов");
			
		КонецЕсли;
	КонецЕсли;
	
	//Заполнение ставки НДС
	Попытка
		ЭтотОбъект.СтавкаНДС=Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
	Исключение КонецПопытки; 
	
	// а теперь когда все уже заполнено установим номер документу
	Если ((Основание = Неопределено) и (НЕ ЗначениеЗаполнено(ЭтотОбъект.Номер)= Истина)) Тогда
		// не было вызова обработки заполнения и номер нужно установить
		ЭтотОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	
	Попытка ЭтотОбъект.Менеджер=ЭтотОбъект.Автор.Сотрудник; Исключение КонецПопытки;
	
КонецПроцедуры

// Процедура накапливает сообщения об ошибках
Процедура ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, Сообщение, Важность = "")
	
	ПрефиксСообщения = Важность;
	
	Если (НЕ ЗначениеЗаполнено(ПрефиксСообщения))  Тогда
		ПрефиксСообщения = "";	
	Иначе	
		ПрефиксСообщения = ПрефиксСообщения + " ";
	КонецЕсли;			
	
	Если (НЕ ЗначениеЗаполнено(ПредупрежденияСервера)) Тогда
		ПредупрежденияСервера =  ПрефиксСообщения + Сообщение;
	Иначе 
		ПредупрежденияСервера = ПредупрежденияСервера  + ПрефиксСообщения +Сообщение;		
	КонецЕсли	
	
КонецПРоцедуры	

// Функция создает каталог врменных файлов и возвращает файл обмена
Функция ПолучитьКаталогВременныхФайлов(ФайлОбмена)
	
	КаталогОбмена  	=	КаталогВременныхФайлов()+"\TimePlanner";
	КаталогОбмена	=	СтрЗаменить(КаталогОбмена,"\\","\");	
	СоздатьКаталог(КаталогОбмена);	
	
	ФайлОбмена		=	КаталогОбмена + "\" +ФайлОбмена;
	
	Возврат КаталогОбмена;
	
КонецФункции

// Процедура удаляет файлы из каталога временных файлов, исключая ФайлОбмена
Процедура УдалитьФайлыОбмена(КаталогОбмена,ФайлОбмена);
	
	ФайлыОбмена = НайтиФайлы(КаталогОбмена,"*.xml");
	
	Для Каждого ТекущийФайл Из ФайлыОбмена Цикл
		
		// Оставим текущий Файл
		Если ТекущийФайл.ПолноеИмя = ФайлОбмена Тогда
			Продолжить;
		КонецЕсли;
		
		// Пытаемся удалить файл со старым сообщением
		Попытка
			УдалитьФайлы(ТекущийФайл.ПолноеИмя);
		Исключение
			// удалить не возможно
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

//***************************************************
//Процедуры и Функции для работы с данными
//***************************************************

// Функция ищет элемент из справочника "СправочникСсылка" по коду "ЭлементКод"
Функция НайтиЭлементПоКоду(ЭлементКод, ПредупрежденияСервера, СправочникСсылка, ТипЭлемента = "Элемент")
	
	ЭлементСсылка = СправочникСсылка.НайтиПоКоду(ЭлементКод);
	
	Если (ЭлементСсылка = СправочникСсылка.ПустаяСсылка()) Тогда		
		ДобавитьСообщениеОбОшибке(ПредупрежденияСервера,ТипЭлемента + " с кодом " + ЭлементКод + " не найден(а)" + Символы.ПС);
	КонецЕсли;
	
	Возврат  ЭлементСсылка;
	
КонецФункции	

// Функция ищет элемент из справочника "СправочникСсылка" по наименованию "ЭлементНаименование"
Функция НайтиЭлементПоНаименованию(ЭлементНаименование, ПредупрежденияСервера, СправочникСсылка, ТипЭлемента = "Элемент")
	
	ЭлементСсылка = СправочникСсылка.НайтиПоНаименованию(ЭлементНаименование);
	
	Если (ЭлементСсылка = СправочникСсылка.ПустаяСсылка()) Тогда		
		ДобавитьСообщениеОбОшибке(ПредупрежденияСервера,ТипЭлемента + " с наименованием " + ЭлементНаименование + " не найден" + Символы.ПС);
	КонецЕсли;
	
	Возврат  ЭлементСсылка;
	
КонецФункции	

//Функция ищет Автора документа
Функция ПолучитьАвтораИзменений(Автор,ПредупрежденияСервера)
	
	Если (ЗначениеЗаполнено(Автор)) Тогда
		
		Запрос = Новый Запрос;
		
		ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	Пользователи.Ссылка,
		|	Пользователи.Наименование
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Наименование = &Наименование
		|	И Пользователи.Ссылка ЕСТЬ НЕ NULL 
		|	И Пользователи.Ссылка <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Наименование", Автор);
		
		Результат = Запрос.Выполнить().Выбрать();
		
		Если (Результат.Следующий()) Тогда
			Возврат Результат.Ссылка;	
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	

// Если для данного документа достаточно  деталей для выполнения всех работ
// функция возвращает "True", в противном случае - "False"
Функция ПолучитьДоступностьТоваров(ЗаказНарядНомер, ЗаказНарядДата, Остатки, Резервы)
	
	Остатки = СоздатьОбъектXDTOПоТипу("СловарьСтрокаЧисло");	
	Резервы = СоздатьОбъектXDTOПоТипу("СловарьСтрокаЧисло");	
	
	// создаем объект-менеджер документами Заказ-Наряд
	ЗаказНарядМенеджер = Документы.ЗаказНаряд;
	
	// задаем интервал поиска документа
	ПоискВИнтервале = ЗаказНарядДата;			
	
	// ищем все документы с заданным номером за последнии 10 лет
	Для индекс = 1 по 10 Цикл
		ЗаказНарядСсылка = ЗаказНарядМенеджер.НайтиПоНомеру(ЗаказНарядНомер,ПоискВИнтервале);	
		
		// если документ найден
		Если НЕ (ЗаказНарядСсылка.Пустая()) Тогда
			
			//получаем объект документа заказ-наряд
			ЗаказНарядОбъект = ЗаказНарядСсылка.ПолучитьОбъект();
			
			//если документ проведен - то всегда ложь
			//проведенные документы не должны изменятсья
			Если (ЗаказНарядОбъект.Проведен) Тогда
				Возврат "False";
			КонецЕсли;	
			
			//генерируем список номенклатуры для запроса
			СписокНоменклатуры = Новый СписокЗначений;
			Для Каждого Товар Из ЗаказНарядОбъект.Товары Цикл
				СписокНоменклатуры.Добавить(Товар.Номенклатура);	
			КонецЦикла;
			
			// получаем данные об остатках номенклатуры
			ТаблицаОстатков 	= ПолучитьКоличествоВОстатках(ЗаказНарядСсылка,СписокНоменклатуры);
			
			
			// получаем данные о номенклатуре, зарезервированной под текущего клиента
			ТаблицаРезервов 	= ПолучитьКоличествоВРезерве(ЗаказНарядСсылка,СписокНоменклатуры);
			
			Результат = "True";
			
			Для Каждого Товар Из ЗаказНарядОбъект.Товары Цикл
				
				ОстаткиНоменклатуры = ТаблицаОстатков.НайтиСтроки(Новый Структура("СкладКомпании, Номенклатура", Товар.СкладКомпании, Товар.Номенклатура));
				
				Если (ОстаткиНоменклатуры.Количество()> 0) Тогда
					Элемент = СоздатьОбъектXDTOПоТипу("ПараСтрокаЧисло");
					Элемент.Ключ 		= ОстаткиНоменклатуры[0].Номенклатура.Код;
					Элемент.Значение	= ОстаткиНоменклатуры[0].Остаток;
					
					Остатки.Элементы.Добавить(Элемент);
				КонецЕсли;	
				
				РезервыНоменклатуры = ТаблицаРезервов.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, СкладКомпании", Товар.Номенклатура, Товар.ХарактеристикаНоменклатуры, Товар.СкладКомпании));
				
				Если (РезервыНоменклатуры.Количество()> 0) Тогда
					Элемент = СоздатьОбъектXDTOПоТипу("ПараСтрокаЧисло");
					Элемент.Ключ 		= РезервыНоменклатуры[0].Номенклатура.Код;
					Элемент.Значение	= РезервыНоменклатуры[0].РезервОстаток;
					
					Резервы.Элементы.Добавить(Элемент);
				КонецЕсли;	
				
				//если нет свободного количества на складе
				Если (ОстаткиНоменклатуры.Количество()= 0) Тогда				
					
					//если деталь зарезервирована под клиента
					Если РезервыНоменклатуры.Количество() > 0 Тогда
						Продолжить;
					КонецЕсли;
					
					//нехватка деталей для произведения работ
					Результат = "False";
					
				КонецЕсли;
				
			КонецЦикла;					
			
			//все хорошо, можно работать
			Возврат Результат;
		КонецЕсли;	
		
		// раширяем дату поиска
		ПоискВИнтервале = ДобавитьМесяц(ПоискВИнтервале,-1 * 12);
	КонецЦикла;			
	
	Возврат "False";
	
КонецФункции

// Функция возвращает таблицу остатков на складах для всех товаров документа Заказ-Наряд
Функция	ПолучитьКоличествоВОстатках(ЗаказНаряд, Номенклатура = Неопределено) 
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.РезервОстаток, 0)) КАК Остаток
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, Номенклатура В (&Номенклатура)) КАК ОстаткиТоваровКомпанииОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиТоваровКомпанииОстатки.СкладКомпании,
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура";
	
	Запрос.УстановитьПараметр("ЗаказНаряд", ЗаказНаряд);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Функция возвращает таблицу зарезервированных деталей для всех товаров документа Заказ-Наряд
Функция ПолучитьКоличествоВРезерве(ЗаказНаряд, Номенклатура = Неопределено)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказыПокупателейОстатки.Номенклатура,
	|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	|	ЗаказыПокупателейОстатки.СкладКомпании,
	|	СУММА(ЗаказыПокупателейОстатки.РезервОстаток) КАК РезервОстаток
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(, Заказ.ДокументОснование = &Заказ " + ?(Номенклатура <> Неопределено," И Номенклатура В (&Номенклатура) ","") + ") КАК ЗаказыПокупателейОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыПокупателейОстатки.Номенклатура,
	|	ЗаказыПокупателейОстатки.ХарактеристикаНоменклатуры,
	|	ЗаказыПокупателейОстатки.СкладКомпании";
	
	Запрос.УстановитьПараметр("Заказ", ЗаказНаряд);	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Функция возвращает объект Заказ-Наряд
// если документ не найден возвращается Неопределено
Функция ПолучитьЗаказНарядПоНомеруИДатеВнутренний(ЗаказНарядНомер, ЗаказНарядДата)
	
	ЗаказНаряд =  НайтиДокументЗаказНарядВнутренний(ЗаказНарядНомер, ЗаказНарядДата);	
	
	Если ЗаказНаряд <> Неопределено Тогда
		
		Элемент = СоздатьОбъектXDTOПоТипу("ЗаказНаряд");
		
		// заполняем основные свойсва 
		Элемент.Ссылка				=	ПолучитьУникальныйИдентификаторСтрокой(ЗаказНаряд.Ссылка);
		
		Элемент.Номер 				=	ПривестиКСтроке(ЗаказНаряд.Номер);
		Элемент.Дата 				=	ПривестиКДате(ЗаказНаряд.Дата);
		
		Элемент.КонтрагентКод		=	ПривестиКСтроке(ЗаказНаряд.Контрагент.Код);
		Элемент.АвтомобильКод		=	ПривестиКСтроке(ЗаказНаряд.Автомобиль.Код);
		Элемент.ВидРемонтаКод		=	ПривестиКСтроке(ЗаказНаряд.ВидРемонта.Код);
		
		Элемент.Состояние			=	ПривестиКСтроке(ЗаказНаряд.Состояние);
		Элемент.Комментарий			=	ПривестиКСтроке(ЗаказНаряд.Комментарий);
		
		Элемент.Автор				=	ПривестиКСтроке(ЗаказНаряд.Автор.Наименование);
		
		// заполняем контрегента
		Элемент.Контрагент			=	ПолучитьКонтрагентаПоКодуВнутренний(ЗаказНаряд.Контрагент.Код,Истина);		
		
		// заполняем автомобиль
		Элемент.Автомобиль			=	ПолучитьАвтомобильПоКодуВнутренний(ЗаказНаряд.Автомобиль.Код,Истина);
		
		// заполняем таблицу товаров
		Для Каждого ЭлементТоваров из ЗаказНаряд.Товары Цикл
			
			Товар = СоздатьОбъектXDTOПоТипу("ЗаказНарядТовар");
			
			Товар.Код				=	ПривестиКСтроке(ЭлементТоваров.Номенклатура.Код);
			Товар.Количество		=	ПривестиКЧислу(ЭлементТоваров.Количество);			
			Товар.Цена				=	ПривестиКЧислу(ЭлементТоваров.Цена);			
			
			Товар.Коэффициент		=	ПривестиКЧислу(ЭлементТоваров.Коэффициент);			
			Товар.Сумма				=	ПривестиКЧислу(ЭлементТоваров.Сумма);			
			Товар.СуммаВсего		=	ПривестиКЧислу(ЭлементТоваров.СуммаВсего);			
			Товар.СуммаНДС			=	ПривестиКЧислу(ЭлементТоваров.СуммаНДС);			
			
			Товар.Наименование		=	ПривестиКСтроке(ЭлементТоваров.Номенклатура.Наименование);			
			
			Элемент.ЭлементыТовары.Добавить(Товар);
			
		КонецЦикла;	
		
		// заполняем таблицу работ
		Для Каждого ЭлементРабот из ЗаказНаряд.Работы Цикл
			
			Работа = СоздатьОбъектXDTOПоТипу("ЗаказНарядРабота");
			
			Работа.Код				=	ПривестиКСтроке(ЭлементРабот.Работа.Код);
			Работа.Количество		=	ПривестиКЧислу(ЭлементРабот.Количество);			
			Работа.Нормочас			=	ПривестиКЧислу(ЭлементРабот.Нормочас.Цена);			
			Работа.Цена				=	ПривестиКЧислу(ЭлементРабот.Цена);			
			
			Работа.Коэффициент		=	ПривестиКЧислу(ЭлементРабот.Коэффициент);			
			Работа.Сумма			=	ПривестиКЧислу(ЭлементРабот.Сумма);			
			Работа.СуммаВсего		=	ПривестиКЧислу(ЭлементРабот.СуммаВсего);			
			Работа.СуммаНДС			=	ПривестиКЧислу(ЭлементРабот.СуммаНДС);			
			
			Работа.Наименование			=	ПривестиКСтроке(ЭлементРабот.Работа.Наименование);			
			Работа.ИдентификаторСтроки	=	ПривестиКСтроке(ЭлементРабот.ИдентификаторРаботы);			
			
			Элемент.ЭлементыРаботы.Добавить(Работа);
			
		КонецЦикла;	
		
		Возврат Элемент;
		
	КонецЕсли;
	
	Возврат Неопределено;	 	
	
КонецФункции

// Функция конвертирует цену в валюту "по-умолчанию"
Функция ПолучитьВВалютеПоУмолчанию(Цена,Валюта)
	
	Если (НЕ ЗначениеЗаполнено(Цена)) Тогда
		Цена	=	1;
	КонецЕсли;		
	
	ВалютаСсылка = Справочники.Валюты.НайтиПоНаименованию(Валюта,Истина);
	
	СтруктураКурса = обКурсДляВалюты(ВалютаСсылка,ТекущаяДата());
	Курс = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Возврат Цена*Курс;
	
КонецФункции	

// Функция возвращает объект "Контрегент" по коду "Код"
// параметр "Расширенный" определяет необходимость включения дополнительной информации по объекту
// если объект не найден, возвращается "Неопределено"
Функция ПолучитьКонтрагентаПоКодуВнутренний(Код,Расширенный = Ложь)
	
	КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(Код, Расширенный);
	Объекты				=	ВыбратьКонтрагентов(КонтекстЗапроса);
	Если (Объекты<> Неопределено И Объекты.Элементы.Количество()>0) Тогда
		Возврат Объекты.Элементы[0];
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции	

// Функция возвращает объект "Комплектация" по коду "Код"
// параметр "Расширенный" определяет необходимость включения дополнительной информации по объекту
// если объект не найден, возвращается "Неопределено"
Функция ПолучитьКомплектациюПоКодуВнутренний(Код,Расширенный = Ложь)
	
	КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(Код, Расширенный);
	Объекты				=	ВыбратьВариантыКомплектации(КонтекстЗапроса);
	Если (Объекты <> Неопределено И Объекты.Элементы.Количество()>0) Тогда
		Возврат Объекты.Элементы[0];
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции	

// Функция возвращает объект "Автомобиль" по коду "Код"
// параметр "Расширенный" определяет необходимость включения дополнительной информации по объекту
// если объект не найден, возвращается "Неопределено"
Функция ПолучитьАвтомобильПоКодуВнутренний(Код,Расширенный = Ложь)
	
	КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(Код, Расширенный);
	Объекты				=	ВыбратьАвтомобили(КонтекстЗапроса);
	Если (Объекты <> Неопределено И Объекты.Элементы.Количество()>0) Тогда
		Возврат Объекты.Элементы[0];
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

// Функция возвращает объект "ТипКузова" по коду "Код"
// параметр "Расширенный" определяет необходимость включения дополнительной информации по объекту
// если объект не найден, возвращается "Неопределено"
Функция ПолучитьТипКузоваПоКодуВнутренний(Код,Расширенный = Ложь)
	
	КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(Код, Расширенный);
	Объекты				=	ВыбратьТипыКузова(КонтекстЗапроса);
	Если (Объекты <> Неопределено И Объекты.Элементы.Количество()>0) Тогда
		Возврат Объекты.Элементы[0];
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

// Функция возвращает объект "ТипДвигателя" по коду "Код"
// параметр "Расширенный" определяет необходимость включения дополнительной информации по объекту
// если объект не найден, возвращается "Неопределено"
Функция ПолучитьТипДвигателяПоКодуВнутренний(Код,Расширенный = Ложь)
	
	КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(Код, Расширенный);
	Объекты				=	ВыбратьТипыДвигателей(КонтекстЗапроса);
	Если (Объекты <> Неопределено И Объекты.Элементы.Количество()>0) Тогда
		Возврат Объекты.Элементы[0];
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

// Функция возвращает объект "ТипКПП" по коду "Код"
// параметр "Расширенный" определяет необходимость включения дополнительной информации по объекту
// если объект не найден, возвращается "Неопределено"
Функция ПолучитьТипКПППоКодуВнутренний(Код,Расширенный = Ложь)
	
	КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(Код, Расширенный);
	Объекты				=	ВыбратьТипыКПП(КонтекстЗапроса);
	Если (Объекты <> Неопределено И Объекты.Элементы.Количество()>0) Тогда
		Возврат Объекты.Элементы[0];
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

// Функция возвращает объект "Модель" по коду "Код"
// параметр "Расширенный" определяет необходимость включения дополнительной информации по объекту
// если объект не найден, возвращается "Неопределено"
Функция ПолучитьМодельПоКодуВнутренний(Код,Расширенный = Ложь)
	
	КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(Код, Расширенный);
	Объекты				=	ВыбратьМоделиАвтомобилей(КонтекстЗапроса);
	Если (Объекты <> Неопределено И Объекты.Элементы.Количество()>0) Тогда
		Возврат Объекты.Элементы[0];
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

// Функция ищет документ Заказ-наряд с номером "ЗаказНарядНомер" в интервале "ЗаказНарядДата"
// Возвращает объект документа, если документ не найден возвращается Неопределено
Функция НайтиДокументЗаказНарядВнутренний(ЗаказНарядНомер, ЗаказНарядДата)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказНаряд.Ссылка,
	|	ЗаказНаряд.Номер,
	|	ЗаказНаряд.Дата КАК Дата
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Номер = &Номер
	|	И ЗаказНаряд.Дата < КОНЕЦПЕРИОДА(&Дата, ГОД)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Номер", ЗаказНарядНомер);
	Запрос.УстановитьПараметр("Дата", ЗаказНарядДата);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();	
	
	Если (Выборка.Следующий()) Тогда
		Возврат Выборка.Ссылка.ПолучитьОбъект();	
	КонецЕсли;	
	
	// возвращаем пустое значение
	Возврат Неопределено;
	
КонецФункции	

// Функция создает/изменяет значение характеристик автомобиля в регистре сведений "Автомобили" 
Процедура ИзменитьДанныеХарактеристикиАвтомобиля(АвтомобильСсылка,ВидАИ,Значение)
	
	УдалитьЗапись = Ложь;
	Если НЕ ЗначениеЗаполнено(Значение)Тогда
		УдалитьЗапись = Истина;
	КонецЕсли;	
	
	НаборИнформацииОбАвто = РегистрыСведений.Автомобили.СоздатьНаборЗаписей();
	НаборИнформацииОбАвто.Отбор.Автомобиль.Установить(АвтомобильСсылка);
	НаборИнформацииОбАвто.Отбор.ВидЗначения.Установить(ВидАИ);
	НаборИнформацииОбАвто.Отбор.Период.Установить(НачалоДня(ТекущаяДата()));
	
	НаборИнформацииОбАвто.Прочитать();	
	
	Если НаборИнформацииОбАвто.Количество() = 0 Тогда
		ЗаписьАИ = НаборИнформацииОбАвто.Добавить();
	Иначе	
		ЗаписьАИ =  НаборИнформацииОбАвто[0];
	КонецЕсли;
	
	// удаляем запись
	Если УдалитьЗапись Тогда
		НаборИнформацииОбАвто.Удалить(0);
	Иначе	
		// создаем/изменяем данные
		ЗаписьАИ.Автомобиль = АвтомобильСсылка;
		ЗаписьАИ.ВидЗначения = ВидАИ;
		ЗаписьАИ.Значение = Значение;
		ЗаписьАИ.Период = ТекущаяДата();
	КонецЕсли;		
	
	НаборИнформацииОбАвто.Записать();	
	
КонецПроцедуры

// Функция создает/изменяет значение контактоной информации клиента в регистре сведений "КонтактнаяИнформация" 
Процедура ИзменитьДанныеКонтактнойИнформации(КонтрагентСсылка,ТипКИ,ВидКИ,Значение)
	
	УдалитьЗапись = Ложь;
	Если НЕ ЗначениеЗаполнено(Значение)Тогда
		УдалитьЗапись = Истина;
	КонецЕсли;	
	
	
	НаборКонтактнойИнформации = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	НаборКонтактнойИнформации.Отбор.Объект.Установить(КонтрагентСсылка);
	НаборКонтактнойИнформации.Отбор.Тип.Установить(ТипКИ);
	НаборКонтактнойИнформации.Отбор.Вид.Установить(ВидКИ);
	
	НаборКонтактнойИнформации.Прочитать();
	
	Если НаборКонтактнойИнформации.Количество() = 0 Тогда
		ЗаписьКИ = НаборКонтактнойИнформации.Добавить();
	Иначе	
		ЗаписьКИ =  НаборКонтактнойИнформации[0];	
	КонецЕсли;
	
	// удаляем запись
	Если УдалитьЗапись Тогда
		НаборКонтактнойИнформации.Удалить(0);
	Иначе	
		// создаем/изменяем данные
		ЗаписьКИ.Объект = КонтрагентСсылка;
		ЗаписьКИ.Тип = ТипКИ;
		ЗаписьКИ.Вид = ВидКИ;			
		ЗаписьКИ.Представление = Значение;			
	КонецЕсли;		
	
	
	НаборКонтактнойИнформации.Записать();
	
КонецПроцедуры


//***************************************************
//Процедуры и Функции для работы с Перечислениями
//***************************************************

// Возвращает элемент справочника видос состояний заказ-наряда по наименованию
Функция ПолучитьСостояниеПоНаименованию(Состояние)
	
	ЗначениеСостояния=Справочники.ВидыСостоянийЗаказНарядов.НайтиПоНаименованию(Состояние);
	Если ЗначениеСостояния<>Неопределено Тогда
		Возврат ЗначениеСостояния;
	Иначе
		Возврат Справочники.ВидыСостоянийЗаказНарядов.Заявка;
	КонецЕсли; 
	
КонецФункции	

// Возвращает элемент типа Перечисление.ВидыКонтрагентов по наименованию
Функция ПолучитьВидКонтрагентаПоНаименованию(ВидКонтрагента)
	
	Для Каждого ВидКонтрагентаПеречисление из Перечисления.ВидыКонтрагентов Цикл
		
		Если (ВидКонтрагента = ВРЕГ(ВидКонтрагентаПеречисление)) Тогда
			Возврат ВидКонтрагентаПеречисление;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат Перечисления.ВидыКонтрагентов.Покупатель;  	
	
КонецФункции	

// Возвращает элемент типа Перечисление.ФормыСобственности по наименованию
Функция ПолучитьФормуСобственностиПоНаименованию(ФормаСобственности)
	
	Для Каждого ФормаСобственностиПеречисление из Перечисления.ФормыСобственности Цикл
		
		Если (ФормаСобственности = ВРЕГ(ФормаСобственностиПеречисление)) Тогда
			Возврат ФормаСобственностиПеречисление;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат Перечисления.ФормыСобственности.ЧастноеЛицо;  
	
КонецФункции	

// Возвращает элемент типа Перечисление.ТипыКонтактнойИнформации по наименованию
Функция ПолучитьТипКонтактнойИНформацииПоНаименованию(ТипКИ)
	
	Для Каждого ТипКИПеречисление из Перечисления.ТипыКонтактнойИнформации Цикл
		
		Если (ТипКИ = ВРЕГ(ТипКИПеречисление)) Тогда
			Возврат ТипКИПеречисление;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат Перечисления.ТипыКонтактнойИнформации.Другое;  
	
КонецФункции	

// Возвращает элемент типа Справочники.ВидыКонтактнойИнформации по наименованию
Функция ПолучитьВидКонтактнойИНформацииПоНаименованию(ВидКИ)
	
	СпрВидыКИ = Справочники.ВидыКонтактнойИнформации;
	ВидКИСсылка = СпрВидыКИ.НайтиПоНаименованию(ВидКИ,Ложь);
	
	Если (ВидКИСсылка = СпрВидыКИ.ПустаяСсылка()) Тогда		
		Возврат СпрВидыКИ.ПрочаяИнформация;
	Иначе
		Возврат ВидКИСсылка;
	КонецЕсли;		
	
КонецФункции

//***************************************************
//Процедуры и Функции для заполнения данных по выборке
//***************************************************

// возвращает значение уникального идентификатора ссылки и возвращает его строковое представление
Функция ПолучитьУникальныйИдентификаторСтрокой (Ссылка) 
	
	Возврат XMLСтрока(Ссылка); 
	
КонецФункции	

// Копирует свойства из объекта Выборка в объект Элемент для объекта "Базовый"
Процедура ЗаполнитьЗначенияСвойствПоБазовому(Элемент, Выборка)
	
	// Процедура ЗаполнитьЗначенияСвойств не используется, 
	// т.к. работает более медленно, чем обычное присвоение
	
	Элемент.Код					=	Выборка.Код;
	Элемент.Наименование		=	Выборка.Наименование;
	Элемент.НаименованиеПолное	=	Выборка.НаименованиеПолное;
	Элемент.ПометкаУдаления		=	Выборка.ПометкаУдаления;
	Элемент.Комментарий			=	Выборка.Комментарий;
	Элемент.КодРодителя			=	Выборка.КодРодителя;
	Элемент.Группа				=	Выборка.Группа;
	
КонецПроцедуры	

// Копирует свойства из объекта Выборка в объект Элемент для объекта "БазовыйИерархический"
Процедура ЗаполнитьБазовыйИерархический(Элемент, Выборка, КонтекстЗапроса)
	
	ЗаполнитьЗначенияСвойствПоБазовому(Элемент, Выборка);
	Элемент.Ссылка				=	ПолучитьУникальныйИдентификаторСтрокой(Выборка.Ссылка);
	
КонецПроцедуры

// Копирует свойства из объекта Выборка в объект Элемент для объекта "АвтоРабота"
Процедура ЗаполнитьАвтоРаботу(Элемент, Выборка, КонтекстЗапроса)
	
	ЗаполнитьЗначенияСвойствПоБазовому(Элемент, Выборка);
	Элемент.Артикул				=	Выборка.Артикул;
	
КонецПроцедуры	

// Копирует свойства из объекта Выборка в объект Элемент для объекта "СвязаннаяРабота"
Процедура ЗаполнитьСвязаннуюРаботу(Элемент, Выборка, КонтекстЗапроса)
	
	Элемент.АвтоработаКод		=	Выборка.АвтоработаКод;
	Элемент.СвязаннаяРаботаКод	=	Выборка.СвязаннаяРаботаКод;
	Элемент.МодельКод 			=	Выборка.МодельКод;
	Элемент.ТипДвигателяКод		=	Выборка.ТипДвигателяКод;
	Элемент.ТипКППКод			=	Выборка.ТипКППКод;
	
	Элемент.Количество			=	ПривестиКЧислу(Выборка.Количество);
	Элемент.СвязьПоНоменклатуре	=	Выборка.СвязьПоНоменклатуре;		
	
КонецПроцедуры	

// Копирует свойства из объекта Выборка в объект Элемент для объекта "Номенклатура"
Процедура ЗаполнитьНоменклатуру(Элемент, Выборка, КонтекстЗапроса)
	
	ЗаполнитьЗначенияСвойствПоБазовому(Элемент,Выборка);		
	Элемент.Ссылка 				=	ПолучитьУникальныйИдентификаторСтрокой(Выборка.Ссылка);			
	Элемент.Артикул				=	Выборка.Артикул;
	
КонецПроцедуры

// Копирует свойства из объекта Выборка в объект Элемент для объекта "ВидРемонта"
Процедура ЗаполнитьВидРемонта(Элемент, Выборка, КонтекстЗапроса)
	
	Элемент.Код 				=	Выборка.Код;
	Элемент.Наименование		=	Выборка.Наименование;
	Элемент.ТипРемонта			=	ПривестиКСтроке(Выборка.ТипРемонта);
	Элемент.ПометкаУдаления		=	Выборка.ПометкаУдаления;
	
	Элемент.Ссылка				=	ПолучитьУникальныйИдентификаторСтрокой(Выборка.Ссылка);
	
КонецПроцедуры	

// Копирует свойства из объекта Выборка в объект Элемент для объекта "Контрагент"
Процедура ЗаполнитьКонтрагента(Элемент, Выборка, КонтекстЗапроса)
	
	ЗаполнитьЗначенияСвойствПоБазовому(Элемент, Выборка);
	
	Элемент.Ссылка  			=   ПолучитьУникальныйИдентификаторСтрокой(Выборка.Ссылка);                               
	Элемент.ВидКонтрагента		=	ПривестиКСтроке(Выборка.ВидКонтрагента);
	Элемент.ФормаСобственности	=	ПривестиКСтроке(Выборка.ФормаСобственности);
	Элемент.Автор				=	ПривестиКСтроке(Выборка.Автор);
	
	КодКонтрагенте = Элемент.Код; 
	
	
	МассивТелефонов 	= Новый Массив(5);			
	МассивАдресов 		= Новый Массив(5);
	МассивEmails 		= Новый Массив(5);			
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КонтактнаяИнформация.Вид КАК Вид,
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|
	|УПОРЯДОЧИТЬ ПО
	|	Вид";
	
	Запрос.УстановитьПараметр("Объект", Выборка.Ссылка);
	
	Результат = Запрос.Выполнить();
	КИ = Результат.Выбрать();
	
	Пока КИ.Следующий() Цикл
		
		// телефоны
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный) Тогда 
			МассивТелефонов[1] = КИ.Представление;
		КонецЕсли;	
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонСотовый) Тогда 
			МассивТелефонов[2] = КИ.Представление;
		КонецЕсли;	
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонДомашний) Тогда 
			МассивТелефонов[3] = КИ.Представление;
		КонецЕсли;	
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонРабочий) Тогда 
			МассивТелефонов[4] = КИ.Представление;
		КонецЕсли;	
		
		// адреса
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресФактический) Тогда 
			МассивАдресов[1] = КИ.Представление;
		КонецЕсли;	
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресЮридический) Тогда 
			МассивАдресов[2] = КИ.Представление;
		КонецЕсли;	
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресПочтовый) Тогда 
			МассивАдресов[3] = КИ.Представление;
		КонецЕсли;	
		
		// e-mails
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний) Тогда 
			МассивEmails[1] = КИ.Представление;
		КонецЕсли;	
		Если (КИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий) Тогда 
			МассивEmails[2] = КИ.Представление;
		КонецЕсли;			
		
	КонецЦикла;
	
	Элемент.Адрес 		 =  ПолучитьПервыйНеПустойЭлементМассива(МассивАдресов);
	Элемент.Телефон 	 =  ПолучитьПервыйНеПустойЭлементМассива(МассивТелефонов);
	Элемент.Email  		 =  ПолучитьПервыйНеПустойЭлементМассива(МассивEmails);
	
	//  включать Расширенную информацию по контрагенту
	Если (ЗапросОграниченДопПараметрами(КонтекстЗапроса)) Тогда
		
		// заполняем контактную информацию 
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	КонтактнаяИнформация.Тип КАК Тип,
		|	КонтактнаяИнформация.Вид КАК Вид,
		|	КонтактнаяИнформация.Представление КАК Представление,
		|	КонтактнаяИнформация.Объект.Код КАК КодКонтрагента
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект.Код = &Код";	
		
		Запрос.УстановитьПараметр("Код",КодКонтрагенте);				   
		
		ВыборкаКИ = Запрос.Выполнить().Выбрать();
		Пока ВыборкаКИ.Следующий() Цикл
			
			КонтактнаяИнформация = СоздатьОбъектXDTOПоТипу("КонтактнаяИнформация");					
			
			КонтактнаяИнформация.КодКонтрагента	=	ПривестиКСтроке(ВыборкаКИ.КодКонтрагента);
			КонтактнаяИнформация.Тип 			=	ПривестиКСтроке(ВыборкаКИ.Тип);
			КонтактнаяИнформация.Вид 			=	ПривестиКСтроке(ВыборкаКИ.Вид);
			КонтактнаяИнформация.Представление	=	ПривестиКСтроке(ВыборкаКИ.Представление);
			
			Элемент.ЭлементыКонтактнойИнформации.Добавить(КонтактнаяИнформация);
		КонецЦикла;
		
		
		// заполняем автомобили клиента
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	АвтомобилиСрезПоследних.Период КАК Период,
		|	АвтомобилиСрезПоследних.Автомобиль.Код КАК КодАвтомобиля,
		|	АвтомобилиСрезПоследних.ВидЗначения КАК ДополнительнаяИнформация,
		|	АвтомобилиСрезПоследних.Значение.Код КАК КодКонтрагента
		|ИЗ
		|	РегистрСведений.Автомобили.СрезПоследних КАК АвтомобилиСрезПоследних
		|ГДЕ
		|	АвтомобилиСрезПоследних.Значение.Код = &Код";	
		
		Запрос.УстановитьПараметр("Код",КодКонтрагенте);				   
		
		ВыборкаАК = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаАК.Следующий() Цикл
			
			АвтомобилиКонтрагента = СоздатьОбъектXDTOПоТипу("АвтомобилиКонтрагента");
			
			АвтомобилиКонтрагента.КодКонтрагента			=	ПривестиКСтроке(ВыборкаАК.КодКонтрагента);
			АвтомобилиКонтрагента.Период 					=	ВыборкаАК.Период;
			АвтомобилиКонтрагента.КодАвтомобиля 			=	ПривестиКСтроке(ВыборкаАК.КодАвтомобиля);
			АвтомобилиКонтрагента.ДополнительнаяИнформация	=	ПривестиКСтроке(ВыборкаАК.ДополнительнаяИнформация);
			
			Элемент.ЭлементыАвтомобилейКонтрагента.Добавить(АвтомобилиКонтрагента);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры  

// Копирует свойства из объекта Выборка в объект Элемент для объекта "Автомобиль"
Процедура ЗаполнитьАвтомобиль(Элемент, Выборка, КонтекстЗапроса)
	
	ЗаполнитьЗначенияСвойствПоБазовому(Элемент, Выборка);
	Элемент.Ссылка				=	ПолучитьУникальныйИдентификаторСтрокой(Выборка.Ссылка);
	
	Элемент.VIN					=	Выборка.VIN;
	Элемент.ГодВыпуска			=	Выборка.ГодВыпуска;
	Элемент.МодельКод			=	Выборка.КодМодели;
	Элемент.Цвет				=	Выборка.Цвет;
	
	Элемент.ГосНомер			=	Выборка.ГосНомер;
	Элемент.Пробег				=	Выборка.Пробег;
	Элемент.СтраховаяКомпанияКод=	Выборка.СтраховаяКомпанияКод;
	
	Элемент.НомерДвигателя		=	Выборка.НомерДвигателя;
	Элемент.КПП					=	Выборка.КПП;
	Элемент.ПродавецКод			=	Выборка.ПродавецКод;
	Элемент.ДатаНачалаГарантии	=	Выборка.ДатаНачалаГарантии;
	
	Элемент.ХозяинКод			=	Выборка.Хозяин;
	Элемент.КомплектацияКод		=	Выборка.КомплектацияКод;
	
	Если (ЗапросОграниченДопПараметрами(КонтекстЗапроса)) Тогда
		Элемент.Комплектация	=	ПолучитьКомплектациюПоКодуВнутренний(Выборка.КомплектацияКод, Истина);
		Элемент.Модель			=	ПолучитьМодельПоКодуВнутренний(Выборка.КодМодели,Истина)
	КонецЕсли;
	
КонецПроцедуры

// Копирует свойства из объекта Выборка в объект Элемент для объекта "Нормочас"
Процедура ЗаполнитьНормочас(Элемент, Выборка, КонтекстЗапроса)
	
	Элемент.АвтоработаКод       = Выборка.АвтоработаКод;
	Элемент.Модель              = Выборка.Модель;
	Элемент.ВариантКомплектации = Выборка.ВариантКомплектации;
	Элемент.Время               = Выборка.Время;
	
	Если Элемент.Время = 0 Тогда
		Элемент.Время = 1;
	КонецЕсли;
	
КонецПроцедуры

// Копирует свойства из объекта Выборка в объект Элемент для объекта "Комплектация"
Процедура ЗаполнитьВариантКомплектации(Элемент, Выборка, КонтекстЗапроса)
	
	ЗаполнитьЗначенияСвойствПоБазовому(Элемент, Выборка);
	Элемент.Ссылка 				=	ПолучитьУникальныйИдентификаторСтрокой(Выборка.Ссылка);		
	
	Элемент.ТипКузоваКод		=	Выборка.ТипКузоваКод;
	Элемент.ТипДвигателяКод		=	Выборка.ТипДвигателяКод;
	Элемент.ТипКППКод			=	Выборка.ТипКППКод;
	
	Элемент.МодельКод			=	Выборка.МодельКод;
	
	Если (ЗапросОграниченДопПараметрами(КонтекстЗапроса)) Тогда
		Элемент.ТипКузова		=	ПолучитьТипКузоваПоКодуВнутренний(Выборка.ТипКузоваКод, Истина);
		Элемент.ТипДвигателя	=	ПолучитьТипДвигателяПоКодуВнутренний(Выборка.ТипДвигателяКод, Истина);
		Элемент.ТипКПП			=	ПолучитьТипКПППоКодуВнутренний(Выборка.ТипКППКод, Истина);
		Элемент.Модель			=	ПолучитьМодельПоКодуВнутренний(Выборка.МодельКод, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Функция создает объект по типу "ТипСписка"
// обходит выборку и заполняет соответствующие значения элементов
Функция МенеджерЗаполненияПоВыборке(Выборка, ТипСписка, ТипЭлемента, КонтекстЗапроса)
	
	СписокЭлементов = СоздатьОбъектXDTOПоТипу(ТипСписка);
	
	Пока Выборка.Следующий() Цикл
		
		Элемент = СоздатьОбъектXDTOПоТипу(ТипЭлемента);
		
		Если 		(ТипЭлемента = "АвтоРабота") Тогда
			ЗаполнитьАвтоРаботу(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "МодельАвтомобиля" ИЛИ
			ТипЭлемента = "ТипКПП" ИЛИ
			ТипЭлемента = "ТипДвигателя" ИЛИ
			ТипЭлемента = "ТипКузова") Тогда
			ЗаполнитьБазовыйИерархический(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "СвязаннаяРабота") Тогда
			ЗаполнитьСвязаннуюРаботу(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "Номенклатура") Тогда
			ЗаполнитьНоменклатуру(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "ВидРемонта") Тогда
			ЗаполнитьВидРемонта(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "Контрагент") Тогда
			ЗаполнитьКонтрагента(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "Автомобиль") Тогда
			ЗаполнитьАвтомобиль(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "Нормочас") Тогда
			ЗаполнитьНормочас(Элемент, Выборка, КонтекстЗапроса);
			
		ИначеЕсли 	(ТипЭлемента = "Комплектация") Тогда
			ЗаполнитьВариантКомплектации(Элемент, Выборка, КонтекстЗапроса);
			
		Иначе
			ВызватьИсключение ВернутьОписаниеОшибки(2,"Менеджер заполнения не определил тип: "+ ТипЭлемента);
			
		КонецЕсли;	
		
		СписокЭлементов.Элементы.Добавить(Элемент);
		
	КонецЦикла;
	
	Возврат СписокЭлементов;  	
	
КонецФункции

//***************************************************
//Методы веб-сервиса
//***************************************************

// Вовзращает ТекущуюДату на 1С клиенте
Функция ПолучитьТекущееВремя()	
	
	Возврат ТекущаяДата();
	
КонецФункции

// Возвращает коэффициент и цену нормочаса по автомобилю и авто работе
Функция ПолучитьКоэффициентИЦенуНормочаса(КодАвтомобиля, КодАвтоРаботы, КоэффициентНормочаса)
	
	ЦенаНормочаса = 0;
	КоэффициентНормочаса = 1;
	
	Если (НЕ ЗначениеЗаполнено(КодАвтоРаботы)) Тогда
		Возврат ЦенаНормочаса;
	КонецЕсли;
	
	Если (НЕ ЗначениеЗаполнено(КодАвтомобиля)) Тогда
		Модель                  = Справочники.Модели.ПустаяСсылка();
	Иначе
		Автомобиль = Справочники.Автомобили.НайтиПоКоду(КодАвтомобиля);
		Модель     = Автомобиль.Модель;
		Если ЗначениеЗаполнено(Автомобиль.ВариантКомплектации) Тогда
			ВариантКомплектации = Автомобиль.ВариантКомплектации;
		Иначе
			Модель              = Автомобиль.Модель;
		КонецЕсли;
	КонецЕсли;
	
	Работа = Справочники.Автоработы.НайтиПоКоду(КодАвтоРаботы);
	Если (НЕ ЗначениеЗаполнено(Работа)) Тогда
		Возврат ЦенаНормочаса;
	КонецЕсли;
	
	Договор    = Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	Цех        = Справочники.Цеха.ПустаяСсылка();
	ВидРемонта = Справочники.ВидыРемонта.ПустаяСсылка();
	РеглВалюта = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	СтруктураКурса = обКурсДляВалюты(РеглВалюта, ТекущаяДата());
	Курс = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	Курс = ?(Курс = 0, 1, Курс);
	
	ТипЦенРабот = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
	СтруктураЦены = орПолучитьЦенуАвтоработы(ТипЦенРабот, Работа, Модель, Договор, Цех, ВидРемонта, ТекущаяДата(), Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить(), Курс);
	
	Если НЕ СтруктураЦены.Нормочас = Справочники.Нормочасы.Рубль Тогда
		ЦенаНормочаса        = СтруктураЦены.Цена;
		КоэффициентНормочаса = СтруктураЦены.НормаВремени;
	КонецЕсли;
	
	Возврат ЦенаНормочаса;
	
КонецФункции

// Функция возвращает скидку контрагента на работы из последнего Заказ-Наряда
Функция ПолучитьМаксимальнуюСкидкуПоКонтрагенту(КодКонтрагента)
	
	МаксимальнаяСкидка = 20;
	
	СкидкаПоЗаказНарядам = 0;
	
	Запрос = Новый Запрос;	
	
	ТекстЗапроса =  "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказНаряд.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ЗаказНаряд.СкидкаНаценкаРаботы.ЗначениеСкидки, 0) КАК Скидка
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Контрагент.Код = &КодКонтрагента
	|	И ЗаказНаряд.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказНаряд.Дата УБЫВ";  	
	
	Запрос.УстановитьПараметр("КодКонтрагента",КодКонтрагента);				   
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();	
	
	Если   Выборка.Следующий() Тогда	
		СкидкаПоЗаказНарядам =  Выборка.Скидка;
	КонецЕсли;	
	
	Возврат Мин(СкидкаПоЗаказНарядам,МаксимальнаяСкидка);
	
КонецФункции

// Функция возвращает список измененных объектов в формате "ТипОбъекта, КодОбъекта"
// удаленые объекты игнорируются
Функция ПолучитьИзмененныеОбъекты()
	
	ТипСписка = "СловарьСтрокаКоллекция";
	ТипЭлемента = "ПараСтрокаКоллекция";
	
	УзелОбмена = ПланыОбмена.ПланОбменаСПланировщиком.НайтиПоКоду("TP");	
	
	Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
		
		Запрос = Новый Запрос;	
		
		ТекстЗапроса =  "ВЫБРАТЬ
		|	ПланОбменаСПланировщиком.Ссылка КАК TimePlannerУзел
		|ИЗ
		|	ПланОбмена.ПланОбменаСПланировщиком КАК ПланОбменаСПланировщиком
		|ГДЕ
		|	ПланОбменаСПланировщиком.Ссылка <> &ТекущийУзел
		|	И (НЕ ПланОбменаСПланировщиком.ПометкаУдаления)";  	
		
		Запрос.УстановитьПараметр("ТекущийУзел",ПланыОбмена.ПланОбменаСПланировщиком.ЭтотУзел());				   
		
		Запрос.Текст = ТекстЗапроса;
		Выборка = Запрос.Выполнить().Выбрать();			
		
		Если (Выборка.Количество()>0) Тогда
			Выборка.Следующий();
			УзелОбмена = Выборка.TimePlannerУзел;
		Иначе
			ВызватьИсключение ВернутьОписаниеОшибки (1,"Узел обмена не найден. Заведите узел обмена с кодом 'TP'");
		КонецЕсли;	
		
	КонецЕсли;
	
	// устанавливаем настройки сеанса обмена
	
	ФайлОбмена	  = Строка(Новый УникальныйИдентификатор) + ".xml";
	КаталогОбмена = ПолучитьКаталогВременныхФайлов(ФайлОбмена);
	
	НастройкиСеансаОбмена = Новый Структура();
	НастройкиСеансаОбмена.Вставить("УзелОбмена",УзелОбмена);
	НастройкиСеансаОбмена.Вставить("КаталогОбмена",КаталогОбмена);
	НастройкиСеансаОбмена.Вставить("ФайлОбмена",ФайлОбмена);
	
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(НастройкиСеансаОбмена.ФайлОбмена);
	
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, НастройкиСеансаОбмена.УзелОбмена);
	Выборка = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель, ЗаписьСообщения.НомерСообщения);
	
	СписокЭлементов = СоздатьОбъектXDTOПоТипу(ТипСписка);
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл				
		
		// если выборка не задана, выходим из цикла
		Если (Выборка = Неопределено) Тогда
			Прервать;
		КонецЕсли;	
		
		// не обрабатываем удаление объектов			
		Если (ТипЗнч(Выборка) = Тип("УдалениеОбъекта")) Тогда
			Продолжить;
		КонецЕсли;	
		
		Объект = Выборка.Получить();
		
		Если (НЕ ЗначениеНеОпределено(Объект)) Тогда
			
			ТипОбъекта = ВРЕГ(Строка(ТипЗнч(Объект)));
			
			// если объект удален: ничего не делаем
			Если		(Найти(ТипОбъекта,"УДАЛЕНИЕ ОБЪЕКТА") > 0) ИЛИ (Найти(ВРЕГ(ТипОбъекта),"OBJECT DELETION") > 0) Тогда
				Продолжить;	
			КонецЕсли;	
			
			Элемент = СоздатьОбъектXDTOПоТипу(ТипЭлемента);
			
			Элемент.Ключ		= 	ТипОбъекта;			
			
			Если		(Найти(ТипОбъекта,"СПРАВОЧНИК") > 0) ИЛИ (Найти(ВРЕГ(ТипОбъекта),"CATALOG") > 0) Тогда
				// сохраняем Код справочника
				ЭлементКоллекции = СоздатьОбъектXDTOПоТипу("ПараСтрокаСтрока");
				ЭлементКоллекции.Ключ 		= "Код";
				ЭлементКоллекции.Значение 	= Объект.Код;
				
				Элемент.Элементы.Добавить(ЭлементКоллекции);
				
			ИначеЕсли	(Найти(ТипОбъекта,"ДОКУМЕНТ") > 0) ИЛИ (Найти(ТипОбъекта,"DOCUMENT") > 0) Тогда
				// сохраняем Номер документа
				ЭлементКоллекции = СоздатьОбъектXDTOПоТипу("ПараСтрокаСтрока");
				ЭлементКоллекции.Ключ 		= "Номер";
				ЭлементКоллекции.Значение 	= Объект.Номер;
				
				Элемент.Элементы.Добавить(ЭлементКоллекции);
				
				// сохраняем Дату документа
				ЭлементКоллекции = СоздатьОбъектXDTOПоТипу("ПараСтрокаСтрока");
				ЭлементКоллекции.Ключ 		= "Дата";
				ЭлементКоллекции.Значение 	= Строка(Объект.Дата);
				
				Элемент.Элементы.Добавить(ЭлементКоллекции);
				
			Иначе
				ВызватьИсключение ТипОбъекта; 
				
			КонецЕсли;	
			
			СписокЭлементов.Элементы.Добавить(Элемент);
			
		КонецЕсли		
		
	КонецЦикла;
	
	// фиксируем изменения
	ПланыОбмена.УдалитьРегистрациюИзменений(ЗаписьСообщения.Получатель, ЗаписьСообщения.НомерСообщения);
	ЗаписьСообщения.ЗакончитьЗапись();
	
	// закрываем xml-файл
	Попытка 
		ЗаписьXML.Закрыть();
	Исключение 
		
		Ошибка = ИнформацияОбОшибке();
		
		// неизвестная ошибка, вернем описание
		Возврат СформироватьРезультатВыполнения("РезультатВыполнения",	Ложь,	Неопределено,	Неопределено,	9,	Ошибка,	Неопределено,	Неопределено);		
		
	КонецПопытки;
	
	
	УдалитьФайлыОбмена(КаталогОбмена, ФайлОбмена);
	
	Возврат СписокЭлементов;
	
КонецФункции	

//***************************************************
//Методы веб-сервиса: Полная синхронизация
//***************************************************

// Возвращает список всех Авторабот
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьАвтоРаботы(КонтекстЗапроса)
	
	ТипСписка = "СписокАвтоРабот";
	ТипЭлемента = "АвтоРабота";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ЕСТЬNULL(Автоработы.Код, """") КАК Код,
	|	ЕСТЬNULL(Автоработы.Артикул, """") КАК Артикул,
	|	ЕСТЬNULL(Автоработы.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(Автоработы.НаименованиеПолное, """") КАК НаименованиеПолное,
	|	ЕСТЬNULL(Автоработы.Комментарий, """") КАК Комментарий,
	|	ЕСТЬNULL(Автоработы.Родитель.Код, """") КАК КодРодителя,
	|	Автоработы.ЭтоГруппа КАК Группа,
	|	Автоработы.ПометкаУдаления
	|ИЗ
	|	Справочник.Автоработы КАК Автоработы";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	Автоработы.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	Автоработы.Ссылка ИЕРАРХИЯ";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список связанных Авторабот/Номенклатур
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьСвязанныеАвтоРаботы(КонтекстЗапроса)
	
	ТипСписка = "СписокСвязанныхРабот";
	ТипЭлемента = "СвязаннаяРабота";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "   
	|	ЕСТЬNULL(СвязанныеРаботы.Авторабота.Код, """") КАК АвтоработаКод,
	|	ЕСТЬNULL(СвязанныеРаботы.СвязаннаяРабота.Код, """") КАК СвязаннаяРаботаКод,
	|	"""" КАК МодельКод,
	|	"""" КАК ТипДвигателяКод,
	|	"""" КАК ТипКППКод,
	|	ЕСТЬNULL(СвязанныеРаботы.Количество, 1) КАК Количество,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(СвязанныеРаботы.СвязаннаяРабота.Ссылка КАК Справочник.Автоработы) ЕСТЬ НЕ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СвязьПоНоменклатуре
	|ИЗ
	|	РегистрСведений.СвязанныеРаботы КАК СвязанныеРаботы
	|ГДЕ
	|	ВЫРАЗИТЬ(СвязанныеРаботы.Авторабота.Ссылка КАК Справочник.Автоработы) ЕСТЬ НЕ NULL ";		
	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|	И СвязанныеРаботы.Авторабота.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всей Номенклатуры
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьНоменклатуру(КонтекстЗапроса)
	
	ТипСписка = "СписокНоменклатуры";
	ТипЭлемента = "Номенклатура";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "	
	|	Номенклатура.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(Номенклатура.Код, """") КАК Код,
	|	ЕСТЬNULL(Номенклатура.Артикул, """") КАК Артикул,
	|	ЕСТЬNULL(Номенклатура.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(Номенклатура.НаименованиеПолное, """") КАК НаименованиеПолное,
	|	ЕСТЬNULL(Номенклатура.Комментарий, """") КАК Комментарий,
	|	ЕСТЬNULL(Номенклатура.Родитель.Код, """") КАК КодРодителя,
	|	Номенклатура.ЭтоГруппа КАК Группа,
	|	Номенклатура.ПометкаУдаления
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	Номенклатура.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура.Ссылка ИЕРАРХИЯ";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список видов Ремонта
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьВидыРемонта(КонтекстЗапроса)
	
	ТипСписка = "СписокВидовРемонта";
	ТипЭлемента = "ВидРемонта";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "	
	|	ВидыРемонта.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ВидыРемонта.Код, """") КАК Код,
	|	ЕСТЬNULL(ВидыРемонта.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(ВидыРемонта.ТипРемонта, """") КАК ТипРемонта,
	|	ВидыРемонта.ПометкаУдаления
	|ИЗ
	|	Справочник.ВидыРемонта КАК ВидыРемонта";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	ВидыРемонта.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	ВидыРемонта.Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всех Контрагентов
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
// Если значение свойства "КонтекстЗапроса.Расширенный" Истина, в объект Контрагент
// включаются данные о КонтактнойИнформации и об Автомобилях клиента
Функция ВыбратьКонтрагентов(КонтекстЗапроса)
	
	ТипСписка = "СписокКонтрагентов";
	ТипЭлемента = "Контрагент";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "	
	|	Контрагенты.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(Контрагенты.Код, """") КАК Код,
	|	ЕСТЬNULL(Контрагенты.Ссылка.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(ПОДСТРОКА(Контрагенты.Ссылка.НаименованиеПолное, 1, 256), """") КАК НаименованиеПолное,
	|	ЕСТЬNULL(ПОДСТРОКА(Контрагенты.Ссылка.Комментарий, 1, 256), """") КАК Комментарий,
	|	ЕСТЬNULL(Контрагенты.Ссылка.Родитель.Код, """") КАК КодРодителя,
	|	Контрагенты.Ссылка.ЭтоГруппа КАК Группа,
	|	Контрагенты.ПометкаУдаления КАК ПометкаУдаления,
	|	Контрагенты.ВидКонтрагента КАК ВидКонтрагента,
	|	Контрагенты.ФормаСобственности КАК ФормаСобственности,
	|	ЕСТЬNULL(Контрагенты.Автор.Наименование, """") КАК Автор
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	Контрагенты.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	Контрагенты.Ссылка ИЕРАРХИЯ";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всех МоделейАвтомобилей
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьМоделиАвтомобилей(КонтекстЗапроса)
	
	ТипСписка = "СписокМоделейАвтомобилей";
	ТипЭлемента = "МодельАвтомобиля";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	Модели.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(Модели.Код, """") КАК Код,
	|	ЕСТЬNULL(Модели.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(Модели.НаименованиеПолное, """") КАК НаименованиеПолное,
	|	"""" КАК Комментарий,
	|	ЕСТЬNULL(Модели.Родитель.Код, """") КАК КодРодителя,
	|	Модели.ЭтоГруппа КАК Группа,
	|	Модели.ПометкаУдаления
	|ИЗ
	|	Справочник.Модели КАК Модели";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	Модели.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	Модели.Ссылка ИЕРАРХИЯ";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всех Автомобилей
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьАвтомобили(КонтекстЗапроса)
	
	ТипСписка = "СписокАвтомобилей";
	ТипЭлемента = "Автомобиль";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	Автомобили.Ссылка КАК Ссылка,
	|
	|	ЕСТЬNULL(Автомобили.Код, """") КАК Код,
	|	ЕСТЬNULL(Автомобили.Наименование, """") КАК Наименование,
	|	"""" КАК НаименованиеПолное,
	|	ЕСТЬNULL(Автомобили.Комментарий, """") КАК Комментарий,
	|	ЕСТЬNULL(Автомобили.Родитель.Код, """") КАК КодРодителя,
	|	Автомобили.ЭтоГруппа КАК Группа,
	|	Автомобили.ПометкаУдаления,
	|
	|	ЕСТЬNULL(Автомобили.VIN, """") КАК VIN,
	|	ЕСТЬNULL(Автомобили.ГодВыпуска, ДАТАВРЕМЯ(1,1,1)) КАК ГодВыпуска,
	|	ЕСТЬNULL(Автомобили.Модель.Код, """") КАК КодМодели,
	|	ЕСТЬNULL(Автомобили.Цвет.Наименование, """") КАК Цвет,
	|	ЕСТЬNULL(Автомобили.НомерДвигателя, """") КАК НомерДвигателя,
	|	ЕСТЬNULL(Автомобили.ТипКПП.Наименование, """") КАК КПП,
	|	ЕСТЬNULL(Автомобили.ДатаНачалаГарантии, ДАТАВРЕМЯ(1,1,1)) КАК ДатаНачалаГарантии,
	|	ЕСТЬNULL(Автомобили.ВариантКомплектации.Код, """") КАК КомплектацияКод,
	|	ЕСТЬNULL(Автомобили.Продавец.Код, """") КАК ПродавецКод,
	|	"""" КАК СтраховаяКомпанияКод,
	|
	|	ЕСТЬNULL(ЗапросПробег.Значение, 0) КАК Пробег,
	|	ЕСТЬNULL(ЗапросГосНомер.Значение, """") КАК ГосНомер,
	|	ЕСТЬNULL(ЗапросХозяин.Значение.Код, """") КАК Хозяин
	|
	|ИЗ
	|	Справочник.Автомобили КАК Автомобили
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Пробег)) КАК ЗапросПробег
	|		ПО (ЗапросПробег.Автомобиль.Ссылка = Автомобили.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ГосНомер)) КАК ЗапросГосНомер
	|		ПО (ЗапросГосНомер.Автомобиль.Ссылка = Автомобили.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)) КАК ЗапросХозяин
	|		ПО (ЗапросХозяин.Автомобиль.Ссылка = Автомобили.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.ТехПаспорт)) КАК ЗапросТехПаспорт
	|		ПО (ЗапросТехПаспорт.Автомобиль.Ссылка = Автомобили.Ссылка)";  		
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	Автомобили.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	Автомобили.Ссылка ИЕРАРХИЯ";	
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всех ТиповКПП
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьТипыКПП(КонтекстЗапроса)
	
	ТипСписка = "СписокТиповКПП";
	ТипЭлемента = "ТипКПП";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ТипыКПП.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ТипыКПП.Код, """") КАК Код,
	|	ЕСТЬNULL(ТипыКПП.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(ТипыКПП.Представление, """") КАК НаименованиеПолное,
	|	ЕСТЬNULL(ТипыКПП.КоличествоПередач, """") КАК Комментарий,
	|	ЕСТЬNULL(ТипыКПП.Родитель.Код, """") КАК КодРодителя,
	|	ТипыКПП.ЭтоГруппа КАК Группа,
	|	ТипыКПП.ПометкаУдаления
	|ИЗ
	|	Справочник.ТипыКПП КАК ТипыКПП";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	ТипыКПП.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	ТипыКПП.Ссылка ИЕРАРХИЯ";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всех ТиповДвигателей
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьТипыДвигателей(КонтекстЗапроса)
	
	ТипСписка = "СписокТиповДвигателей";
	ТипЭлемента = "ТипДвигателя";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ТипыДвигателей.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ТипыДвигателей.Код, """") КАК Код,
	|	ЕСТЬNULL(ТипыДвигателей.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(ТипыДвигателей.Представление, """") КАК НаименованиеПолное,
	|	ЕСТЬNULL(ТипыДвигателей.ОбъемДвигателя, """") КАК Комментарий,
	|	ЕСТЬNULL(ТипыДвигателей.Родитель.Код, """") КАК КодРодителя,
	|	ТипыДвигателей.ЭтоГруппа КАК Группа,
	|	ТипыДвигателей.ПометкаУдаления
	|ИЗ
	|	Справочник.ТипыДвигателей КАК ТипыДвигателей";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	ТипыДвигателей.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	ТипыДвигателей.Ссылка ИЕРАРХИЯ";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всех ТиповКузова
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьТипыКузова(КонтекстЗапроса)
	
	ТипСписка = "СписокТиповКузова";
	ТипЭлемента = "ТипКузова";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ТипыКузовов.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ТипыКузовов.Код, """") КАК Код,
	|	ЕСТЬNULL(ТипыКузовов.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(ТипыКузовов.Представление, """") КАК НаименованиеПолное,
	|	"""" КАК Комментарий,
	|	ЕСТЬNULL(ТипыКузовов.Родитель.Код, """") КАК КодРодителя,
	|	ТипыКузовов.ЭтоГруппа КАК Группа,
	|	ТипыКузовов.ПометкаУдаления
	|ИЗ
	|	Справочник.ТипыКузовов КАК ТипыКузовов";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	ТипыКузовов.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   
	
	ТекстЗапроса = ТекстЗапроса + "
	|УПОРЯДОЧИТЬ ПО
	|	ТипыКузовов.Ссылка ИЕРАРХИЯ";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

// Возвращает список всех Нормочасов
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьНормочасы(КонтекстЗапроса)
	
	ТипСписка = "СписокНормочасов";
	ТипЭлемента = "Нормочас";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстОтбора = "";
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда
		ТекстОтбора = "
		|	ГДЕ
		|		АвтоработыНормыВремени.Ссылка.Код В (&Элементы)";
		Запрос.УстановитьПараметр("Элементы", КонтекстЗапроса.Идентификаторы.Элементы);
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + " 
	|	ОбъединенныйЗапрос.АвтоработаКод КАК АвтоработаКод,
	|	ОбъединенныйЗапрос.Модель КАК Модель,
	|	ОбъединенныйЗапрос.ВариантКомплектации КАК ВариантКомплектации,
	|	ОбъединенныйЗапрос.Время КАК Время
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(АвтоработыНормыВремени.Ссылка.Код, """") КАК АвтоработаКод,
	|		ЕСТЬNULL(АвтоработыНормыВремени.Модель.Наименование, ""Общие работы"") КАК Модель,
	|		ЕСТЬNULL(АвтоработыНормыВремени.ВариантКомплектации.Наименование, """") КАК ВариантКомплектации,
	|		ЕСТЬNULL(АвтоработыНормыВремени.ВремяВыполнения, 1) КАК Время
	|	ИЗ
	|		Справочник.Автоработы.НормыВремени КАК АвтоработыНормыВремени" + ТекстОтбора + "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(АвтоработыНормыВремени.Ссылка.Код, """") КАК АвтоработаКод,
	|		""Общие работы"" КАК Модель,
	|		"""" КАК ВариантКомплектации,
	|		ЕСТЬNULL(АвтоработыНормыВремени.ВремяВыполнения, 1) КАК Время
	|	ИЗ
	|		Справочник.Автоработы КАК АвтоработыНормыВремени" + ТекстОтбора + ") КАК ОбъединенныйЗапрос";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса)
	
КонецФункции

// Возвращает список всех Вариантов Комплектации
// Параметр "КонтекстЗапроса" задает дополнительные ограничения на выборку
Функция ВыбратьВариантыКомплектации(КонтекстЗапроса)
	
	ТипСписка = "СписокВариантовКомплектации";
	ТипЭлемента = "Комплектация";
	
	Запрос = Новый Запрос;
	
	Если (ЗапросОграниченКоличественно(КонтекстЗапроса)) Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ " + КонтекстЗапроса.Количество;
	Иначе	
		ТекстЗапроса = "
		|ВЫБРАТЬ ";
	Конецесли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ВариантыКомплектации.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ВариантыКомплектации.Код, """") КАК Код,
	|	ЕСТЬNULL(ВариантыКомплектации.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(ВариантыКомплектации.НаименованиеПолное, """") КАК НаименованиеПолное,
	|	"""" КАК КодРодителя,
	|	"""" КАК Комментарий,
	|	Ложь КАК Группа,
	|	ВариантыКомплектации.ПометкаУдаления КАК ПометкаУдаления,
	|	ЕСТЬNULL(ВариантыКомплектации.ТипКузова.Код, """") КАК ТипКузоваКод,
	|	ЕСТЬNULL(ВариантыКомплектации.ТипДвигателя.Код, """") КАК ТипДвигателяКод,
	|	ЕСТЬNULL(ВариантыКомплектации.ТипКПП.Код, """") КАК ТипКППКод,
	|	ЕСТЬNULL(ВариантыКомплектации.Владелец.Код, """") КАК МодельКод
	|ИЗ
	|	Справочник.ВариантыКомплектации КАК ВариантыКомплектации";  	
	
	Если (ЗапросОграниченЭлементами(КонтекстЗапроса)) Тогда		
		ТекстЗапроса= ТекстЗапроса + "
		|ГДЕ
		|	ВариантыКомплектации.Код В(&Элементы)";
		
		Запрос.УстановитьПараметр("Элементы",КонтекстЗапроса.Идентификаторы.Элементы);				   
	КонецЕсли;				   	
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат МенеджерЗаполненияПоВыборке(Выборка,ТипСписка,ТипЭлемента,КонтекстЗапроса);	
	
КонецФункции

//***************************************************
//Методы веб-сервиса: Редактирование данных
//***************************************************

// Создает/изменяет информацию об автомобиле
Функция ИзменитьАвтомобиль(Автомобиль)
	
	//очищаем значения выходных параметров
	ПредупрежденияСервера = "";
	КодАвтомобиля = "";
	
	Попытка
		
		#ЕСЛИ НЕ КЛИЕНТ ТОГДА
			//инициализируем псевдо-переменные окружения, 
			//такие как: ПараметрыСеанса, Пользватель, Организация		
			Инициализация();
		#КОНЕЦЕСЛИ
		
		// ищем автомобиль по куоду
		СпрАвтомобили = Справочники.Автомобили;	
		АвтомобильСсылка = СпрАвтомобили.НайтиПоКоду(Автомобиль.Код);
		
		НовыйОбъект = Истина;
		
		// если автомобиль найден в базе данных, возвращаем его
		// иначе создаем новый объект Автомобиль
		Если (АвтомобильСсылка = СпрАвтомобили.ПустаяСсылка()) Тогда		
			АвтомобильОбъект = СпрАвтомобили.СоздатьЭлемент();      					
		Иначе
			АвтомобильОбъект = АвтомобильСсылка.ПолучитьОбъект();
			НовыйОбъект = Ложь;
		КонецЕсли;		
		
		// заполняем основные поля объекта "Автомобиль"
		АвтомобильОбъект.Код =  Автомобиль.Код;
		АвтомобильОбъект.Комментарий = Автомобиль.Комментарий;
		АвтомобильОбъект.VIN = Автомобиль.VIN;
		АвтомобильОбъект.ГодВыпуска = Автомобиль.ГодВыпуска;
		АвтомобильОбъект.Модель = НайтиЭлементПоКоду(Автомобиль.МодельКод, ПредупрежденияСервера, Справочники.Модели, "Модель");
		АвтомобильОбъект.Цвет   = НайтиЭлементПоНаименованию(Автомобиль.Цвет, ПредупрежденияСервера, Справочники.Цвета, "Цвет");		
		АвтомобильОбъект.НомерДвигателя = Автомобиль.НомерДвигателя;
		АвтомобильОбъект.ТипКПП = НайтиЭлементПоНаименованию(Автомобиль.КПП, ПредупрежденияСервера, Справочники.ТипыКПП, "КПП");
		АвтомобильОбъект.Продавец = НайтиЭлементПоКоду(Автомобиль.ПродавецКод, ПредупрежденияСервера, Справочники.Контрагенты, "Продавец");
		АвтомобильОбъект.ДатаНачалаГарантии = Автомобиль.ДатаНачалаГарантии;		
		АвтомобильОбъект.ВариантКомплектации = НайтиЭлементПоКоду(Автомобиль.КомплектацияКод, ПредупрежденияСервера, Справочники.ВариантыКомплектации, "Комплектация");		
		АвтомобильОбъект.Наименование = СформироватьНаименованияАвтомобиля("", АвтомобильОбъект.Модель, АвтомобильОбъект.Цвет, АвтомобильОбъект.VIN, "");				
		АвтомобильОбъект.ВалютаУчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();		
		
		//следующие поля игнорируются
		Если (ЗначениеЗаполнено(Автомобиль.СтраховаяКомпанияКод)) Тогда
			ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, "Свойство Страховая компания отсутствует в текущей реализации конфигурации. Обратитесь к разработчику.");
		КонецЕсли;
		
		Если (ЗначениеЗаполнено(Автомобиль.КодРодителя)) Тогда
			ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, "Установка родителя отличного от корневого не реализована в текущей версии. Обратитесь к разработчику.");
		КонецЕсли;
		
		Если (ЗначениеЗаполнено(Автомобиль.Группа)) Тогда
			ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, "Создание/изменение групп не реализована в текущей версии. Обратитесь к разработчику.");
		КонецЕсли;
		
		Если (Автомобиль.ПометкаУдаления = Истина) Тогда
			ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, "Установка отметки об удалении не реализована в текущей версии. Обратитесь к разработчику.");
		КонецЕсли;		
		
		// автора нельзя установить для новых элементов,
		// поэтому делаем двойное сохранение		
		Если (НовыйОбъект) Тогда
			
			// устанавливаем новый код
			АвтомобильОбъект.УстановитьНовыйКод();
			
			// проверяем корректность введенных данных
			ТекстСообщенияОбОшибке = "";
			Если НЕ АвтомобильОбъект.ПроверитьКорректность(ТекстСообщенияОбОшибке) Тогда
				
				// вернем бизнес-ошибку: не корректный объект
				Возврат СформироватьРезультатВыполнения("РезультатВыполненияАвтомобиль",	Ложь,	Неопределено,	Неопределено,	4,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);
				
			КонецЕсли;	
			
			// пытаемся сохранить созданный объект в базу данных		
			АвтомобильОбъект.Записать();	
		КонецЕсли;		
		
		// устанавливаем автора изменений
		Автор = ПолучитьАвтораИзменений(Автомобиль.Автор, ПредупрежденияСервера);
		Если (ЗначениеЗаполнено(Автор)) Тогда			
			АвтомобильОбъект.Автор = Автор;		
		КонецЕсли;	     				
		
		// проверяем корректность введенных данных
		ТекстСообщенияОбОшибке = "";
		Если НЕ АвтомобильОбъект.ПроверитьКорректность(ТекстСообщенияОбОшибке) Тогда
			
			// вернем бизнес-ошибку: не корректный объект
			Возврат СформироватьРезультатВыполнения("РезультатВыполненияАвтомобиль",	Ложь,	Неопределено,	Неопределено,	4,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);
			
		КонецЕсли;	
		
		// пытаемся сохранить созданный объект в базу данных		
		АвтомобильОбъект.Записать();
		
		// сохраняем информацию об автомобиле
		Авто = НайтиЭлементПоКоду(АвтомобильОбъект.Код, ПредупрежденияСервера, Справочники.Автомобили, "Автомобиль");		
		
		// Пробег
		Если (ЗначениеЗаполнено(Автомобиль.Пробег)) Тогда			
			ИзменитьДанныеХарактеристикиАвтомобиля(
			Авто,
			Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег,
			Автомобиль.Пробег);			
		КонецЕсли;
		
		// ГосНомер
		Если (ЗначениеЗаполнено(Автомобиль.ГосНомер)) Тогда			
			ИзменитьДанныеХарактеристикиАвтомобиля(
			Авто,
			Перечисления.ДополнительнаяИнформацияАвтомобилей.ГосНомер,
			Автомобиль.ГосНомер);						
		КонецЕсли;
		
		// Хозяин
		Если (ЗначениеЗаполнено(Автомобиль.ХозяинКод)) Тогда			
			ИзменитьДанныеХарактеристикиАвтомобиля(
			Авто,
			Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин,
			НайтиЭлементПоКоду(Автомобиль.ХозяинКод, ПредупрежденияСервера, Справочники.Контрагенты, "Хозяин"));
		КонецЕсли;
		
		КодАвтомобиля	 = АвтомобильОбъект.Код;
		
		// получим измененный объект		
		КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(КодАвтомобиля, Истина);
		Объекты				=	ВыбратьАвтомобили(КонтекстЗапроса);
		
		// изменение прошло успешно, вернем объект
		Возврат СформироватьРезультатВыполнения("РезультатВыполненияАвтомобиль",	Истина,	Объекты,	КодАвтомобиля,	0,	Неопределено,	Неопределено,	ПредупрежденияСервера);
		
	Исключение
		
		Ошибка = ИнформацияОбОшибке();
		
		// неизвестная ошибка, вернем описание
		Возврат СформироватьРезультатВыполнения("РезультатВыполненияАвтомобиль",	Ложь,	Неопределено,	Неопределено,	9,	Ошибка,	Неопределено,	ПредупрежденияСервера);
		
	КонецПопытки;       	
	
	
КонецФункции

// Создает/изменяет информацию о текущем Заказ-Наряде
Функция ИзменитьЗаказНаряд(ЗаказНаряд)
	
	//очищаем значения выходных параметров
	ЗаказНарядДата = ТекущаяДата();
	ЗаказНарядНомер = "";
	ПредупрежденияСервера = "";
	
	Попытка
		
		#ЕСЛИ НЕ КЛИЕНТ ТОГДА			
			//инициализируем псевдо-переменные окружения, 
			//такие как: ПараметрыСеанса, Пользватель, Организация		
			Инициализация();		
		#КОНЕЦЕСЛИ
		
		//инициализируем локальные переменные
		Отказ = Ложь;		
		Права = Неопределено;
		ДопПараметры = Неопределено;
		
		//эмулируем объект ЭтаФорма путем создавания структуры с идентичными свойствами
		ЭтаФормаСтруктура = Новый Структура;
		ЭтаФормаСтруктура.Вставить("ТекущаяРабота",Неопределено);
		ЭтаФормаСтруктура.Вставить("Нормочасы",Неопределено);
		ЭтаФормаСтруктура.Вставить("Права",Новый Соответствие);
		
		//ищем документ по переданному номеру, если необходимо обновить документ
		//либо создаем новый документ
		ДокЗаказНаряд = Документы.ЗаказНаряд;	
		ЗаказНарядСсылка = ДокЗаказНаряд.НайтиПоНомеру(ЗаказНаряд.Номер, ЗаказНаряд.Дата);
		
		Если (ЗаказНарядСсылка = ДокЗаказНаряд.ПустаяСсылка()) Тогда		
			ЗаказНарядОбъект = ДокЗаказНаряд.СоздатьДокумент();
			ЗаказНарядОбъект.Дата = ЗаказНарядДата;
			ЗаказНарядОбъект.ДатаСоздания = ТекущаяДата();
		Иначе
			ЗаказНарядОбъект = ЗаказНарядСсылка.ПолучитьОбъект();
			
			Если (ЗаказНарядОбъект.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ
				ЗаказНарядОбъект.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт) 
				Тогда
				
				ТекстСообщенияОбОшибке = "Документ Заказ-Наряд " + ЗаказНарядОбъект.Номер +" от " + ЗаказНарядОбъект.Дата+
				" находится в состоянии " + ЗаказНарядОбъект.Состояние + ". Редактирование документа запрещено." +
				"Для редактирования переведите его в состояние <<В Работе>> в системе 1С";
				
				// вернем бизнес-ошибку: не корректный объект
				Возврат СформироватьРезультатВыполнения("РезультатВыполненияЗаказНаряд",	Ложь,	Неопределено,	Неопределено,	3,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);
				
			КонецЕсли;	
			
		КонецЕсли;
		
		//заполняем основные реквизиты документа
		ЗаказНарядОбъект.ТипЦен=Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
		
		//заполяем общие реквизиты значениями по-умолчанию
		ЗаполнитьДокументДаннымиПоУмолчанию(ЗаказНарядОбъект);
		
		//заполняем основные реквизиты документа
		Заказчик = НайтиЭлементПоКоду(ЗаказНаряд.КонтрагентКод, ПредупрежденияСервера, Справочники.Контрагенты, "Контрагент");		
		Если (НЕ ЗначениеЗаполнено(Заказчик) ИЛИ Заказчик.ЭтоГруппа) Тогда
			
			ТекстСообщенияОбОшибке = "Контрегент не найден или является группой";
			
			// вернем бизнес-ошибку: не корректный объект
			Возврат СформироватьРезультатВыполнения("РезультатВыполненияЗаказНаряд",	Ложь,	Неопределено,	Неопределено,	1,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);			
			
		КонецЕсли;	
		
		ЗаказНарядОбъект.Заказчик = Заказчик;
		ЗаказНарядОбъект.ОбработкаРеквизита("Заказчик");
		
		Автомобиль = НайтиЭлементПоКоду(ЗаказНаряд.АвтомобильКод, ПредупрежденияСервера, Справочники.Автомобили, "Автомобиль");
		Если (НЕ ЗначениеЗаполнено(Автомобиль) ИЛИ Автомобиль.ЭтоГруппа) Тогда
			
			ТекстСообщенияОбОшибке = "Автомобиль не найден или является группой";
			
			// вернем бизнес-ошибку: не корректный объект
			Возврат СформироватьРезультатВыполнения("РезультатВыполненияЗаказНаряд",	Ложь,	Неопределено,	Неопределено,	1,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);			
			
		КонецЕсли;	
		
		ЗаказНарядОбъект.Автомобиль = Автомобиль;
		ЭтаФормаСтруктура.Вставить("Автомобиль", Автомобиль);
		ЗаказНарядОбъект.ОбработкаРеквизита("Автомобиль"); 
		
		ВидРемонта = НайтиЭлементПоКоду(ЗаказНаряд.ВидРемонтаКод,ПредупрежденияСервера, Справочники.ВидыРемонта, "Вид ремонта");
		ЗаказНарядОбъект.ВидРемонта = ВидРемонта;
		
		ЗаказНарядОбъект.Карточка = Справочники.Карточки.ПустаяСсылка();	
		ЗаказНарядОбъект.ОбработкаРеквизита("Карточка");	
		
		// Поищем дисконтные карты
		Запрос = Новый Запрос;
		ТекстЗапроса = "ВЫБРАТЬ
		|	Карточки.Ссылка КАК Карточка
		|ИЗ
		|	Справочник.Карточки КАК Карточки
		|ГДЕ
		|	Карточки.Объект = &Контрагент
		|	И Карточки.Объект <> &ПустойКонтрагент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Карточки.ДатаСоздания";
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Контрагент", ЗаказНарядОбъект.Заказчик);
		Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			СписокДисконтныеКарточки = Результат.Выгрузить();
			
			ЗаказНарядОбъект.Карточка = СписокДисконтныеКарточки[0].Карточка;	
			ЗаказНарядОбъект.ОбработкаРеквизита("Карточка");	
		КонецЕсли;                          		
		
		ЗаказНарядОбъект.Цех = Справочники.Цеха.ОсновнойЦех;
		Состояние	= ПолучитьСостояниеПоНаименованию(ВРЕГ(ЗаказНаряд.Состояние));
		Если (Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен ИЛИ
			Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт) Тогда
			Состояние =Справочники.ВидыСостоянийЗаказНарядов.ВРаботе;
		КонецЕсли;	
		
		ЗаказНарядОбъект.Состояние = Состояние;
		
		ЗаказНарядОбъект.Комментарий = ЗаказНаряд.Комментарий;		
		
		// устанавливаем автора документа
		Автор = ПолучитьАвтораИзменений(ЗаказНаряд.Автор, ПредупрежденияСервера);
		Если (ЗначениеЗаполнено(Автор)) Тогда
			
			ЗаказНарядОбъект.Автор = Автор;	
			
			// устанавливаем Диспетчера, если Автор не пустой
			Если (ЗначениеЗаполнено(Автор.Сотрудник)) Тогда
				ЗаказНарядОбъект.Диспетчер = Автор.Сотрудник; 	
			КонецЕсли;
			
			// устанавливаем Организацию
			Если (ЗначениеЗаполнено(Автор.Организация)) Тогда
				ЗаказНарядОбъект.Организация = Автор.Организация;	
			КонецЕсли;	
			
			// устанавливаем Подразделение
			Если (ЗначениеЗаполнено(Автор.Подразделение)) Тогда
				ЗаказНарядОбъект.ПодразделениеКомпании = Автор.Подразделение;	
			КонецЕсли;
			
		КонецЕсли;	     		
		
		//заполняем табличную часть Товары
		ЗаказНарядОбъект.Товары.Очистить();
		Для каждого Товар из ЗаказНаряд.ЭлементыТовары Цикл
			
			ТекущаяНоменклатура = НайтиЭлементПоКоду(Товар.Код, ПредупрежденияСервера, Справочники.Номенклатура, "Номенклатура");
			
			// не обрабатываем нмоенклатуру, если:
			//	-	номенклатура не найдена;
			// 	-	номенклатура является группой;
			//	-	номенклатура является услугой;
			//	-	номенклатура является прочими активами;
			//	-	по номенклатуре ведется учет по характеристикам;
			Если (
				НЕ ЗначениеЗаполнено(ТекущаяНоменклатура) ИЛИ 
				ТекущаяНоменклатура.ЭтоГруппа ИЛИ 
				ТекущаяНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга ИЛИ
				ТекущаяНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПрочиеАктивы ) 
				Тогда
				Продолжить;	
			КонецЕсли;
			
			
			Если (ЗначениеЗаполнено(ТекущаяНоменклатура.ТипНоменклатуры) И 
				ТекущаяНоменклатура.ТипНоменклатуры.ИспользованиеХарактеристик <> 3) 
				Тогда
				Продолжить;					
			КонецЕсли;	
			
			Строка = ЗаказНарядОбъект.Товары.Добавить();
			
			Строка.Номенклатура = ТекущаяНоменклатура;
			ЗаказНарядОбъект.ОбработкаРеквизита("Товары.Номенклатура",Строка);		
			
			Строка.Количество = Товар.Количество;			
			ЗаказНарядОбъект.ОбработкаРеквизита("Товары.Количество",Строка);
			
			Строка.Цена = Товар.Цена;			
			ЗаказНарядОбъект.ОбработкаРеквизита("Товары.Цена",Строка);						
			
			ЗаказНарядОбъект.ОбработкаРеквизита("Товары.СтавкаНДС", Строка);
			
		КонецЦикла;	
		
		//заполняем табличную часть Работы
		ЗаказНарядОбъект.Работы.Очистить();
		Для каждого Работа из ЗаказНаряд.ЭлементыРаботы Цикл
			
			ТекущаяАвторабота = НайтиЭлементПоКоду(Работа.Код, ПредупрежденияСервера, Справочники.Автоработы, "Авторабота");
			
			Если (НЕ ЗначениеЗаполнено(ТекущаяАвторабота) ИЛИ ТекущаяАвторабота.ЭтоГруппа) Тогда
				Продолжить;	
			КонецЕсли;
			
			Строка = ЗаказНарядОбъект.Работы.Добавить();
			
			Строка.Работа =  ТекущаяАвторабота;			
			ЗаказНарядОбъект.ОбработкаРеквизита("Работы.Работа",Строка);
			
			Строка.Количество = Работа.Количество;			
			ЗаказНарядОбъект.ОбработкаРеквизита("Работы.Количество",Строка);
			
			Строка.Цена = Работа.Цена;			
			ЗаказНарядОбъект.ОбработкаРеквизита("Работы.Цена",Строка);						
			
			// назначаем цену в зависимости от Нормочаса
			СтараяЦенаРаботы=Строка.Цена;
			ЗаказНарядОбъект.ОбработкаРеквизита("РасчетЦеныРаботы",Строка);
			Если СтараяЦенаРаботы<>Строка.Цена Тогда
				ЗаказНарядОбъект.ОбработкаРеквизита("Работы.Цена",Строка);
			КонецЕсли;             			
			
			Если (НЕ ЗначениеЗаполнено(Работа.Коэффициент)) Тогда
				Строка.Коэффициент = 1;
			Иначе
				Строка.Коэффициент = Работа.Коэффициент;			
			КонецЕсли;
			
			ЗаказНарядОбъект.ОбработкаРеквизита("Работы.Коэффициент",Строка);									
			
			Если НЕ ЗначениеЗаполнено(Работа.ИдентификаторСтроки) Тогда
				Строка.ИдентификаторРаботы = Строка(Новый УникальныйИдентификатор);	
			Иначе
				Строка.ИдентификаторРаботы = Работа.ИдентификаторСтроки;	
			КонецЕсли;
			
		КонецЦикла;	
		
		//теоретически следующая строчка не нужна, т.к.
		//новый уникальный номер присваивается функцией ЗаполнитьДокументДаннымиПоУмолчанию
		//но как-то она не всегда верно номер возвращает в режиме WS-соединения
		//понадеемся, что автоинкремент 1С сработает лучше
		Если (НЕ ЗначениеЗаполнено(ЗаказНарядОбъект.Номер)) Тогда
			ЗаказНарядОбъект.Номер = "";		
		КонецЕсли;
		
		ТекстСообщенияОбОшибке = "";
		Если НЕ ЗаказНарядОбъект.ПроверитьКорректность(ТекстСообщенияОбОшибке) Тогда
			
			// вернем бизнес-ошибку: не корректный объект
			Возврат СформироватьРезультатВыполнения("РезультатВыполненияЗаказНаряд",	Ложь,	Неопределено,	Неопределено,	4,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);			
			
		КонецЕсли;	
		
		ЗаказНарядОбъект.Записать(РежимЗаписиДокумента.Запись);			
		
		ЗаказНарядНомер = ЗаказНарядОбъект.Номер; 
		ЗаказНарядДата  = ЗаказНарядОбъект.Дата; 
		
		// получим измененный объект		
		
		СловарьНомерДата = СоздатьОбъектXDTOПоТипу("СловарьСтрокаДата");
		
		ЭлементНомерДата = СоздатьОбъектXDTOПоТипу("ПараСтрокаДата");	
		ЭлементНомерДата.Ключ = ЗаказНарядНомер;
		ЭлементНомерДата.Значение = ЗаказНарядДата;	
		СловарьНомерДата.Элементы.Добавить(ЭлементНомерДата);
		
		Объекты	=	ПолучитьСписокЗаказНарядовПоНомерам(СловарьНомерДата);
		
		// изменение прошло успешно, вернем объект
		Возврат СформироватьРезультатВыполнения("РезультатВыполненияЗаказНаряд",	Истина,	Объекты,	ЗаказНарядНомер,	0,	Неопределено,	Неопределено,	ПредупрежденияСервера);
		
	Исключение
		
		Ошибка = ИнформацияОбОшибке();
		
		// неизвестная ошибка, вернем описание
		Возврат СформироватьРезультатВыполнения("РезультатВыполненияЗаказНаряд",	Ложь,	Неопределено,	Неопределено,	9,	Ошибка,	Неопределено,	ПредупрежденияСервера);	
		
	КонецПопытки;        	
	
КонецФункции

// Создает/изменяет информацию о текущем контрагенте
Функция ИзменитьКонтрагента(Контрагент)
	
	//очищаем значения выходных параметров
	КодКонтрагента = "";
	ПредупрежденияСервера = "";
	
	Попытка
		
		#ЕСЛИ НЕ КЛИЕНТ ТОГДА
			//инициализируем псевдо-переменные окружения, 
			//такие как: ПараметрыСеанса, Пользватель, Организация		
			Инициализация();
		#КОНЕЦЕСЛИ
		
		НовыйОбъект = Истина;
		
		//ищем контрагента по коду
		СпрКонтрагенты = Справочники.Контрагенты;
		КонтрагентСсылка = СпрКонтрагенты.НайтиПоКоду(Контрагент.Код);
		
		//если контрагент найден в базе данных, возвращаем его
		///иначе создаем нового контрагента
		Если (КонтрагентСсылка = СпрКонтрагенты.ПустаяСсылка()) Тогда		
			КонтрагентОбъект = СпрКонтрагенты.СоздатьЭлемент();      		
		Иначе
			КонтрагентОбъект = КонтрагентСсылка.ПолучитьОбъект();
			НовыйОбъект = Ложь;
		КонецЕсли;		
		
		//заполняем основные поля
		КонтрагентОбъект.Наименование = Контрагент.Наименование;
		КонтрагентОбъект.НаименованиеПолное = Контрагент.НаименованиеПолное;
		КонтрагентОбъект.Комментарий = Контрагент.Комментарий;
		
		Если (Контрагент.ПометкаУдаления = Истина) Тогда
			ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, "Установка отметки об удалении не реализована в текущей версии. Обратитесь к разработчику.");
		КонецЕсли;
		
		Если (Контрагент.КодРодителя <> "") Тогда
			ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, "Установка родителя отличного от корневого не реализована в текущей версии. Обратитесь к разработчику.");
		КонецЕсли;
		
		Если (Контрагент.Группа = Истина) Тогда
			ДобавитьСообщениеОбОшибке(ПредупрежденияСервера, "Создание/изменение групп не реализована в текущей версии. Обратитесь к разработчику.");
		КонецЕсли;
		
		КонтрагентОбъект.ВидКонтрагента = ПолучитьВидКонтрагентаПоНаименованию(ВРЕГ(Контрагент.ВидКонтрагента));
		КонтрагентОбъект.ФормаСобственности = ПолучитьФормуСобственностиПоНаименованию(ВРЕГ(Контрагент.ФормаСобственности));	
		
		// автора нельзя установить для новых элементов,
		// поэтому делаем двойное сохранение		
		Если (НовыйОбъект) Тогда
			// устанавливаем новый код
			КонтрагентОбъект.УстановитьНовыйКод();
			
			// проверяем корректность введенных данных
			ТекстСообщенияОбОшибке = "";
			Если НЕ КонтрагентОбъект.ПроверитьКорректность(ТекстСообщенияОбОшибке) Тогда
				
				// вернем бизнес-ошибку: не корректный объект
				Возврат СформироватьРезультатВыполнения("РезультатВыполненияКонтрагент",	Ложь,	Неопределено,	Неопределено,	4,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);				
				
			КонецЕсли;	
			
			// пытаемся сохранить созданный объект в базу данных		
			КонтрагентОбъект.Записать();	
		КонецЕсли;    		
		
		// устанавливаем автора изменений
		Автор = ПолучитьАвтораИзменений(Контрагент.Автор, ПредупрежденияСервера);
		Если (ЗначениеЗаполнено(Автор)) Тогда			
			КонтрагентОбъект.Автор = Автор;		
		КонецЕсли;	     		
		
		// проверяем корректность введенных данных
		ТекстСообщенияОбОшибке = "";
		Если НЕ КонтрагентОбъект.ПроверитьКорректность(ТекстСообщенияОбОшибке) Тогда
			
			// вернем бизнес-ошибку: не корректный объект
			Возврат СформироватьРезультатВыполнения("РезультатВыполненияКонтрагент",	Ложь,	Неопределено,	Неопределено,	4,	Неопределено,	ТекстСообщенияОбОшибке,	ПредупрежденияСервера);						
			
		КонецЕсли;	
		
		// пытаемся сохранить созданный объект в базу данных		
		КонтрагентОбъект.Записать();			
		
		
		// заполняем данные контактной информации контрагента
		// и соханяем в регистр сведений "КонтактнаяИНформация"
		Для Каждого ЭлементКИ из Контрагент.ЭлементыКонтактнойИнформации Цикл
			
			ТипКИ = ПолучитьТипКонтактнойИНформацииПоНаименованию(ВРЕГ(ЭлементКИ.Тип));
			ВидКИ = ПолучитьВидКонтактнойИНформацииПоНаименованию(ВРЕГ(ЭлементКИ.Вид));
			
			// телефоны               
			ИзменитьДанныеКонтактнойИнформации(
			КонтрагентОбъект.Ссылка,
			ТипКИ,
			ВидКИ,
			ЭлементКИ.Представление);		
			
		КонецЦикла;				
		
		// заполняем данные об автомобилях контрагента
		// и сохраняем в регистр сведегний "Автомобили"
		Для Каждого ЭлементАИ из Контрагент.ЭлементыАвтомобилейКонтрагента Цикл
			
			АвтоАИ = НайтиЭлементПоКоду(ЭлементАИ.КодАвтомобиля, ПредупрежденияСервера, Справочники.Автомобили, "Автомобиль");
			ВидАИ = Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин; 
			
			ИзменитьДанныеХарактеристикиАвтомобиля(
			АвтоАИ,
			ВидАИ,
			КонтрагентОбъект.Ссылка);
			
		КонецЦикла;		
		
		КодКонтрагента = КонтрагентОбъект.Код;
		
		// получим измененный объект		
		КонтекстЗапроса		=	СформироватьКонтекстЗапросаПоКоду(КодКонтрагента, Истина);
		Объекты				=	ВыбратьКонтрагентов(КонтекстЗапроса);
		
		// изменение прошло успешно, вернем объект
		Возврат СформироватьРезультатВыполнения("РезультатВыполненияКонтрагент",	Истина,	Объекты,	КодКонтрагента,	0,	Неопределено,	Неопределено,	ПредупрежденияСервера);		
		
	Исключение
		
		Ошибка = ИнформацияОбОшибке();
		
		// неизвестная ошибка, вернем описание
		Возврат СформироватьРезультатВыполнения("РезультатВыполненияКонтрагент",	Ложь,	Неопределено,	Неопределено,	9,	Ошибка,	Неопределено,	ПредупрежденияСервера);	
		
	КонецПопытки;       	
	
КонецФункции	

//***************************************************
//Методы веб-сервиса: Данные по Заказ-Нарядам
//***************************************************

// возврощает список документов типа "Заказ-Наряд"
Функция ПолучитьСписокЗаказНарядовПоНомерам(СловарьНомерДата)
	
	// создаем объекты для возврата
	СписокЭлементов = СоздатьОбъектXDTOПоТипу("СписокЗаказНарядов");
	
	Если (НЕ ЗначениеНеОпределено(СловарьНомерДата)) Тогда
		
		// перебираем элменты  словаря Заказ-Нарядов  и получаем данные документа
		Для Каждого элементСписка из СловарьНомерДата.Элементы Цикл
			Элемент = СоздатьОбъектXDTOПоТипу("ЗаказНаряд");
			
			Элемент	=	ПолучитьЗаказНарядПоНомеруИДатеВнутренний(элементСписка.Ключ, элементСписка.Значение);
			
			Если (Элемент <> Неопределено) Тогда
				СписокЭлементов.Элементы.Добавить(Элемент);						
			КонецЕсли;
			
		КонецЦикла;		
		
	КонецЕсли;	
	
	Возврат СписокЭлементов;
	
КонецФункции

// возврощает список документов типа "Заказ-Наряд"
Функция ПолучитьСписокЗаказНарядовЗаПериод(ДатаНачала,ДатаКонца)		
	
	Запрос = Новый Запрос;	
	
	ТекстЗапроса =  "ВЫБРАТЬ
	|	ЗаказНаряд.Номер КАК Номер,
	|	ЗаказНаряд.Дата КАК Дата
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ГДЕ
	|	ЗаказНаряд.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";  	
	
	Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);				   
	Запрос.УстановитьПараметр("ДатаКонца",ДатаКонца);				   
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();	
	
	// создаем объекты для возврата
	СписокЭлементов = СоздатьОбъектXDTOПоТипу("СписокЗаказНарядов");
	
	Пока   Выборка.Следующий() Цикл
		
		Элемент = СоздатьОбъектXDTOПоТипу("ЗаказНаряд");
		
		Элемент	=	ПолучитьЗаказНарядПоНомеруИДатеВнутренний(Выборка.Номер, Выборка.Дата);
		
		Если (Элемент <> Неопределено) Тогда
			СписокЭлементов.Элементы.Добавить(Элемент);						
		КонецЕсли;
		
	КонецЦикла;		
	
	Возврат СписокЭлементов;
	
КонецФункции

// возвращает список документов, доступных для проведения работ
Функция ПолучитьДоступностьТоваровДляЗаказНарядов(СловарьНомерДата)
	
	// создаем объекты для возврата
	СписокЭлементов = СоздатьОбъектXDTOПоТипу("СписокЗаказНарядовОстаткиРезервы");
	
	Если (НЕ ЗначениеНеОпределено(СловарьНомерДата)) Тогда
		
		// перебираем элменты массива номеров Заказ-Наряда и получаем текущий статус документа по номеру
		Для Каждого элементСписка из СловарьНомерДата.Элементы Цикл
			Элемент = СоздатьОбъектXDTOПоТипу("ЗаказНарядОстаткиРезервы");
			
			Остатки	=	Неопределено;
			Резервы	=	Неопределено;
			
			Элемент.ЗаказНарядНомер 			=	ПривестиКСтроке(элементСписка.Ключ);
			Элемент.ЗаказНарядДата				=	ПривестиКДате(элементСписка.Значение);
			
			Элемент.Состояние					=	ПривестиКСтроке(ПолучитьДоступностьТоваров(элементСписка.Ключ,элементСписка.Значение, Остатки, Резервы));
			
			Элемент.Остатки						=	Остатки;
			Элемент.Резервы						=	Резервы;
			
			СписокЭлементов.Элементы.Добавить(Элемент);			
		КонецЦикла;			
		
	КонецЕсли;
	
	Возврат СписокЭлементов;
	
КонецФункции

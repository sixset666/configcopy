ВЫБРАТЬ РАЗРЕШЕННЫЕ
	СУММА(ВнутреннийЗапрос.СуммаРегПланОборот) КАК СуммаРегПлановое,
	СУММА(ВнутреннийЗапрос.СуммаУпрПланОборот) КАК СуммаУпрПлановое,
	СУММА(ВнутреннийЗапрос.СуммаНДСПланОборот) КАК СуммаНДСПлановое,
	СУММА(ВнутреннийЗапрос.СуммаРегФактОборот) КАК СуммаРегФактическое,
	СУММА(ВнутреннийЗапрос.СуммаУпрФактОборот) КАК СуммаУпрФактическое,
	СУММА(ВнутреннийЗапрос.СуммаНДСФактОборот) КАК СуммаНДСФактическое,
	СУММА(ВнутреннийЗапрос.СуммаРегФактОборот) - СУММА(ВнутреннийЗапрос.СуммаРегПланОборот) КАК СуммаРегОтклонение,
	СУММА(ВнутреннийЗапрос.СуммаУпрФактОборот) - СУММА(ВнутреннийЗапрос.СуммаУпрПланОборот) КАК СуммаУпрОтклонение,
	СУММА(ВнутреннийЗапрос.СуммаНДСФактОборот) - СУММА(ВнутреннийЗапрос.СуммаНДСПланОборот) КАК СуммаНДСОтклонение,
	0 КАК СуммаРегПроцентОтклонения,
	0 КАК СуммаУпрПроцентОтклонения,
	0 КАК СуммаНДСПроцентОтклонения
	//СВОЙСТВА

{ВЫБРАТЬ 
#Область ИзмеренияПостроителяДляСвойств
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ТипНоменклатуры.* КАК ТипНоменклатуры,
#КонецОбласти
	Период КАК Период
	//СВОЙСТВА}
    
ИЗ 
	(
	 ВЫБРАТЬ
	 	ЕСТЬNULL(БюджетЗакупокОбороты.СуммаРегОборот , 0) КАК СуммаРегПланОборот,
	 	ЕСТЬNULL(БюджетЗакупокОбороты.СуммаУпрОборот , 0) КАК СуммаУпрПланОборот,
	 	ЕСТЬNULL(БюджетЗакупокОбороты.СуммаНДСОборот , 0) КАК СуммаНДСПланОборот,
	 		 	
	  	ЕСТЬNULL(ПартииОбороты.СуммаРегПриход        , 0) КАК СуммаРегФактОборот,
	 	ЕСТЬNULL(ПартииОбороты.СуммаУпрПриход        , 0) КАК СуммаУпрФактОборот,
	 	ЕСТЬNULL(ПартииОбороты.СуммаНДСПриход        , 0) КАК СуммаНДСФактОборот,
			
	 	ЕСТЬNULL(БюджетЗакупокОбороты.ПодразделениеКомпании, ПартииОбороты.ПодразделениеКомпании) КАК ПодразделениеКомпании,
	 	ЕСТЬNULL(БюджетЗакупокОбороты.Номенклатура,          ПартииОбороты.ТипНоменклатуры) КАК ТипНоменклатуры,
	 	ЕСТЬNULL(БюджетЗакупокОбороты.Период,                НачалоПериода(ПартииОбороты.Период, Месяц)) КАК Период
	
	 ИЗ
	 РегистрНакопления.БюджетЗакупок.Обороты(&ДатаНач, &ДатаКон, Месяц, Номенклатура ССЫЛКА Справочник.ТипыНоменклатуры и СценарийПланирования = &СценарийПланирования {
		ПодразделениеКомпании.Организация.* КАК Организация, 
		ПодразделениеКомпании.* КАК ПодразделениеКомпании, 
		Номенклатура.* КАК ТипНоменклатуры
		}) КАК БюджетЗакупокОбороты
	   
	 ПОЛНОЕ СОЕДИНЕНИЕ
	 	 
	( ВЫБРАТЬ
	  	ПартииОборотыНеСвернутые.СкладКомпании.Подразделение  КАК ПодразделениеКомпании,
	  	ПартииОборотыНеСвернутые.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	  	ПартииОборотыНеСвернутые.Период КАК Период,
	  	СУММА(ПартииОборотыНеСвернутые.СуммаПриход)    КАК СуммаРегПриход,
	  	СУММА(ПартииОборотыНеСвернутые.СуммаУпрПриход) КАК СуммаУпрПриход,
	  	СУММА(ПартииОборотыНеСвернутые.СуммаНДСПриход) КАК СуммаНДСПриход
	 ИЗ
	  	РегистрНакопления.ПартииТоваровКомпании.Обороты(&ДатаНач, &ДатаКон, Месяц, {
		СкладКомпании.Подразделение.Организация.* КАК Организация, 
		СкладКомпании.Подразделение.*             КАК ПодразделениеКомпании, 
		Номенклатура.ТипНоменклатуры.*            КАК ТипНоменклатуры
		}) КАК ПартииОборотыНеСвернутые     
	  
	  СГРУППИРОВАТЬ ПО
	  	ПартииОборотыНеСвернутые.СкладКомпании.Подразделение,
	  	ПартииОборотыНеСвернутые.Номенклатура.ТипНоменклатуры,
	  	ПартииОборотыНеСвернутые.Период	
	) КАК ПартииОбороты 
	     
	 ПО БюджетЗакупокОбороты.ПодразделениеКомпании = ПартииОбороты.ПодразделениеКомпании И БюджетЗакупокОбороты.Номенклатура = ПартииОбороты.ТипНоменклатуры И БюджетЗакупокОбороты.Период = НачалоПериода(ПартииОбороты.Период, Месяц)
	
	) КАК ВнутреннийЗапрос
	//СОЕДИНЕНИЯ
	     	    
СГРУППИРОВАТЬ ПО
	ПодразделениеКомпании, 
	ТипНоменклатуры
	//СГРУППИРОВАТЬПО
	
{ГДЕ 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ТипНоменклатуры.* КАК ТипНоменклатуры
	 //СВОЙСТВА}
    
{УПОРЯДОЧИТЬ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ТипНоменклатуры.* КАК ТипНоменклатуры,
	Период КАК Период
	 //СВОЙСТВА}
    
{ИТОГИ ПО 
	ПодразделениеКомпании.Организация.* КАК Организация,
	ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	ТипНоменклатуры.* КАК ТипНоменклатуры,
	Период КАК Период
	//СВОЙСТВА}
    
ИТОГИ  
	СУММА(СуммаРегПлановое),
	СУММА(СуммаУпрПлановое),
	СУММА(СуммаНДСПлановое),
	СУММА(СуммаРегФактическое),
	СУММА(СуммаУпрФактическое),
	СУММА(СуммаНДСФактическое),
	СУММА(СуммаРегОтклонение),
	СУММА(СуммаУпрОтклонение),
	СУММА(СуммаНДСОтклонение),
	ВЫБОР КОГДА СУММА(СуммаРегПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаРегОтклонение) / СУММА(СуммаРегПлановое) * 100 КОНЕЦ КАК СуммаРегПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаУпрПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаУпрОтклонение) / СУММА(СуммаУпрПлановое) * 100 КОНЕЦ КАК СуммаУпрПроцентОтклонения,
	ВЫБОР КОГДА СУММА(СуммаНДСПлановое)=0 ТОГДА 100 ИНАЧЕ СУММА(СуммаНДСОтклонение) / СУММА(СуммаНДСПлановое) * 100 КОНЕЦ КАК СуммаНДСПроцентОтклонения
	
	//ИТОГИСВОЙСТВА
ПО ОБЩИЕ

АВТОУПОРЯДОЧИВАНИЕ
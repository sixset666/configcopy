#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структура связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем СтруктураКИ Экспорт;                             // хранит структуру контактной информации владельца

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// формирует текст запроса
Функция ПолучитьТекстЗапроса() Экспорт
	
	ТекстВыборкиИнтервалов = "";
	ТекстВыборкиИнтервалов3 = "";
	ТекстВыборкиИнтервалов2 = "";
	ТекстВыборкиИнтервалов4 = "";
	ТекстЗапросаРезультат = "";
	ТекстСоединенияИнтервалов = "";
	ТекстСоединенияИнтервалов2 = "";
	
	ТекстЗапросаРезультат = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказНаряды.Ссылка КАК ЗаказНаряд,
	|	КорректировкаРеализации.Ссылка КАК Корректировка,
	|	КорректировкаРеализации.ДокументОснование,
	|	(КорректировкаРеализации.СуммаДокумента * КорректировкаРеализации.КурсДокумента / КорректировкаРеализации.КурсВалютыУпр) - (ЗаказНаряды.СуммаДокумента * ЗаказНаряды.КурсДокумента/ЗаказНаряды.КурсВалютыУпр) КАК СуммаДокумента
	|ПОМЕСТИТЬ
	|	ТаблицаКорректировок
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Документ.ЗаказНаряд КАК ЗаказНаряды
	|ПО
	|	КорректировкаРеализации.Сделка = ЗаказНаряды.Ссылка
	|ГДЕ
	|	КорректировкаРеализации.Проведен И 
	|	КорректировкаРеализации.Сделка ССЫЛКА Документ.ЗаказНаряд И 
	|	ЗаказНаряды.ДатаСоздания < &ДатаКон
	|	И ЗаказНаряды.ПометкаУдаления = ЛОЖЬ
	|{ГДЕ
	|	ЗаказНаряды.Заказчик.* КАК Заказчик,
	|	ЗаказНаряды.ВидРемонта.* КАК ВидРемонта,
	|	ЗаказНаряды.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	|	ЗаказНаряды.Менеджер.* КАК Менеджер,
	|	ЗаказНаряды.Автомобиль.* КАК Автомобиль,
	|	ЗаказНаряды.Цех.* КАК Цех,
	|	ЗаказНаряды.Мастер.* КАК Мастер,
	|	МИНИМУМ(ЗаказНаряды.ДатаСоздания) КАК ДатаПервогоРемонта,
	|	МАКСИМУМ(ЗаказНаряды.ДатаСоздания) КАК ДатаПоследнегоРемонта
	|}
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаКорректировок.ЗаказНаряд КАК Сделка,
	|	СУММА(ТаблицаКорректировок.СуммаДокумента) КАК СуммаДокумента
	|ПОМЕСТИТЬ
	|	ДокументКорректировкаРеализации
	|ИЗ
	|	ТаблицаКорректировок КАК ТаблицаКорректировок
	|ГДЕ
	|	(НЕ ТаблицаКорректировок.Корректировка В (ВЫБРАТЬ ДокументОснование ИЗ ТаблицаКорректировок))
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаКорректировок.ЗаказНаряд
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаКорректировок
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказНаряды.Ссылка КАК Сделка,
	|	СУММА((АктыРазногласий.СуммаДокумента * АктыРазногласий.КурсДокумента / АктыРазногласий.КурсВалютыУпр) - (ЗаказНаряды.СуммаДокумента * ЗаказНаряды.КурсДокумента / ЗаказНаряды.КурсВалютыУпр)) КАК СуммаДокумента
	|ПОМЕСТИТЬ
	|	ДокументАктРазногласий
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряды
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Документ.АктРазногласий КАК АктыРазногласий
	|ПО
	|	ЗаказНаряды.Ссылка = АктыРазногласий.ДокументОснование
	|ГДЕ
	|	АктыРазногласий.Проведен И 
	|	ЗаказНаряды.ДатаСоздания < &ДатаКон
	|	И ЗаказНаряды.ПометкаУдаления = ЛОЖЬ
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаряды.Ссылка
	|{ГДЕ
	|	ЗаказНаряды.Заказчик.* КАК Заказчик,
	|	ЗаказНаряды.ВидРемонта.* КАК ВидРемонта,
	|	ЗаказНаряды.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	|	ЗаказНаряды.Менеджер.* КАК Менеджер,
	|	ЗаказНаряды.Автомобиль.* КАК Автомобиль,
	|	ЗаказНаряды.Цех.* КАК Цех,
	|	ЗаказНаряды.Мастер.* КАК Мастер,
	|	МИНИМУМ(ЗаказНаряды.ДатаСоздания) КАК ДатаПервогоРемонта,
	|	МАКСИМУМ(ЗаказНаряды.ДатаСоздания) КАК ДатаПоследнегоРемонта
	|}
	|;
	|";
	
	ЕстьИнтервалы = Ложь;
	//Определим таблицы интервалов сумм задолженностей
	Если ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалСуммыРабот")).Количество()>0 ИЛИ ИзмеренияКолонки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалСуммыРабот")).Количество()>0 Тогда
		Если СуммовыеГруппы.Количество()=0 Тогда
			НовыйПериод = СуммовыеГруппы.Добавить();
			НовыйПериод.СуммаРемонта	= 9999999999999;
			НовыйПериод.Представление	= "Все";
		КонецЕсли;
		СуммовыеГруппы.Сортировать("СуммаРемонта ВОЗР");
		ПревСумма = 0;
		ТекстИнтервалов="";
		Порядок = 0;
		Для Каждого ТекИнтервал Из СуммовыеГруппы Цикл
			ТекстИнтервалов = ТекстИнтервалов + ?(ТекстИнтервалов="","", "ОБЪЕДИНИТЬ ВСЕ ")+"
			|
			|ВЫБРАТЬ
			|	"+Формат(ПревСумма, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МинПредел,
			|	"+Формат(ТекИнтервал.СуммаРемонта, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МаксПредел,
			|	"""+ТекИнтервал.Представление+""" КАК Представление,
			|	"+Формат(Порядок, "ЧН=0; ЧГ=")+" КАК Порядок
			|";
			ПревСумма = ТекИнтервал.СуммаРемонта;
			Порядок = Порядок+1;
		КонецЦикла;
		
		ТекстЗапросаРезультат = ТекстЗапросаРезультат+"
		|ВЫБРАТЬ
		|	ОбъединеннаяТаблица.МинПредел КАК МинПредел,
		|	ОбъединеннаяТаблица.МаксПредел КАК МаксПредел,
		|	ОбъединеннаяТаблица.Представление КАК Представление,
		|	ОбъединеннаяТаблица.Порядок КАК Порядок
		|ПОМЕСТИТЬ
		|	ИнтервалыСумм
		|ИЗ("+ТекстИнтервалов+") КАК ОбъединеннаяТаблица
		|;
		|";
		
		ТекстВыборкиИнтервалов = ",
		|	ИтоговаяТаблицаОборотов.ИнтервалСуммыРабот КАК ИнтервалСуммыРабот,
		|	ИтоговаяТаблицаОборотов.ПорядокИнтервалаСуммыРабот КАК ПорядокИнтервалаСуммыРабот";
		
		ТекстВыборкиИнтервалов3 = ",
		|	ИтоговаяТаблицаОборотов.ИнтервалСуммыРабот КАК ИнтервалСуммыРабот,
		|	ИтоговаяТаблицаОборотов.ПорядокИнтервалаСуммыРабот КАК ПорядокИнтервалаСуммыРабот";
		
		ТекстВыборкиИнтервалов4 = ",
		|	ЕСТЬNULL(ИтоговаяТаблицаОборотов.ИнтервалСуммыРабот, ИнтервалыСумм.Представление) КАК ИнтервалСуммыРабот,
		|	ЕСТЬNULL(ИтоговаяТаблицаОборотов.ПорядокИнтервалаСуммыРабот, ИнтервалыСумм.Порядок) КАК ПорядокИнтервалаСуммыРабот";
		
		ТекстВыборкиИнтервалов2 = ",
		|	ИнтервалыСумм.Представление КАК ИнтервалСуммыРабот,
		|	ИнтервалыСумм.Порядок КАК ПорядокИнтервалаСуммыРабот";
		
		ТекстСоединенияИнтервалов = "
		|ЛЕВОЕ СОЕДИНЕНИЕ ИнтервалыСумм КАК ИнтервалыСумм
		|ПО
		|	ВЫБОР
		|		КОГДА ТаблицаКлиентовПоТипам.СуммаЗН ЕСТЬ NULL ИЛИ ТаблицаКлиентовПоТипам.СуммаЗН = 0 ТОГДА
		|			ИнтервалыСумм.МинПредел=0
		|		ИНАЧЕ
		|			ТаблицаКлиентовПоТипам.СуммаЗН > ИнтервалыСумм.МинПредел И
		|			ТаблицаКлиентовПоТипам.СуммаЗН <= ИнтервалыСумм.МаксПредел
		|	КОНЕЦ";
		
		ТекстСоединенияИнтервалов2 = "
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнтервалыСумм КАК ИнтервалыСумм
		|ПО
		|	ВЫБОР
		|		КОГДА ИтоговаяТаблицаОборотов.СуммаЗН = 0 ТОГДА
		|			ИнтервалыСумм.МинПредел=0
		|		ИНАЧЕ
		|			ИтоговаяТаблицаОборотов.СуммаЗН > ИнтервалыСумм.МинПредел И
		|			ИтоговаяТаблицаОборотов.СуммаЗН <= ИнтервалыСумм.МаксПредел
		|	КОНЕЦ";
		
		ЕстьИнтервалы = Истина;
	Иначе
		ТекстВыборкиИнтервалов = ",
		|	0 КАК ИнтервалСуммыРабот,
		|	0 КАК ПорядокИнтервалаСуммыРабот";
	КонецЕсли;
	
	//Определим таблицы интервалов просроченных дней
	Если ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалОбращений")).Количество()>0 ИЛИ ИзмеренияКолонки.НайтиСтроки(Новый Структура("Использование, ПутьКДанным", Истина, "ИнтервалОбращений")).Количество()>0 Тогда
		Если КоличественныеГруппы.Количество()=0 Тогда
			НовыйПериод = КоличественныеГруппы.Добавить();
			НовыйПериод.КоличествоРемонтов = 50000;
			НовыйПериод.Представление	   = "Все";
		КонецЕсли;
		КоличественныеГруппы.Сортировать("КоличествоРемонтов ВОЗР");
		ПревКоличествоРемонтов = 0;
		ТекстИнтервалов = "";
		Порядок = 0;
		Для Каждого ТекИнтервал Из КоличественныеГруппы Цикл
			ТекстИнтервалов = ТекстИнтервалов + ?(ТекстИнтервалов="","", "ОБЪЕДИНИТЬ ВСЕ ")+"
			|
			|ВЫБРАТЬ
			|	"+Формат(ПревКоличествоРемонтов, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МинПредел,
			|	"+Формат(ТекИнтервал.КоличествоРемонтов, "ЧРД=.; ЧН=0; ЧГ=")+" КАК МаксПредел,
			|	"""+ТекИнтервал.Представление+""" КАК Представление,
			|	"+Формат(Порядок, "ЧН=0; ЧГ=")+" КАК Порядок
			|";
			ПревКоличествоРемонтов = ТекИнтервал.КоличествоРемонтов;
			Порядок = Порядок+1;
		КонецЦикла;
		ТекстЗапросаРезультат = ТекстЗапросаРезультат+"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОбъединеннаяТаблица.МинПредел КАК МинПредел,
		|	ОбъединеннаяТаблица.МаксПредел КАК МаксПредел,
		|	ОбъединеннаяТаблица.Представление КАК Представление,
		|	ОбъединеннаяТаблица.Порядок КАК Порядок
		|ПОМЕСТИТЬ
		|	ИнтервалыОбращений
		|ИЗ("+ТекстИнтервалов+") КАК ОбъединеннаяТаблица
		|;
		|";
		
		ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов+",
		|	ИтоговаяТаблицаОборотов.ИнтервалОбращений КАК ИнтервалОбращений,
		|	ИтоговаяТаблицаОборотов.ПорядокИнтервалаОбращений КАК ПорядокИнтервалаОбращений";
		
		ТекстВыборкиИнтервалов3 = ТекстВыборкиИнтервалов3+",
		|	ИтоговаяТаблицаОборотов.ИнтервалОбращений КАК ИнтервалОбращений,
		|	ИтоговаяТаблицаОборотов.ПорядокИнтервалаОбращений КАК ПорядокИнтервалаОбращений";
		
		ТекстВыборкиИнтервалов4 = ",
		|	ЕСТЬNULL(ИтоговаяТаблицаОборотов.ИнтервалОбращений, ИнтервалыОбращений.Представление) КАК ИнтервалОбращений,
		|	ЕСТЬNULL(ИтоговаяТаблицаОборотов.ПорядокИнтервалаОбращений, ИнтервалыОбращений.Порядок) КАК ПорядокИнтервалаОбращений";
		
		ТекстВыборкиИнтервалов2 = ТекстВыборкиИнтервалов2+",
		|	ИнтервалыОбращений.Представление КАК ИнтервалОбращений,
		|	ИнтервалыОбращений.Порядок КАК ПорядокИнтервалаОбращений";

		ТекстСоединенияИнтервалов = ТекстСоединенияИнтервалов + "
		|ЛЕВОЕ СОЕДИНЕНИЕ ИнтервалыОбращений КАК ИнтервалыОбращений
		|ПО
		|	ВЫБОР
		|		КОГДА ТаблицаКлиентовПоТипам.КоличествоЗН ЕСТЬ NULL ИЛИ ТаблицаКлиентовПоТипам.КоличествоЗН = 0 ТОГДА
		|			ИнтервалыОбращений.МинПредел=0
		|		ИНАЧЕ
		|			ТаблицаКлиентовПоТипам.КоличествоЗН > ИнтервалыОбращений.МинПредел И
		|			ТаблицаКлиентовПоТипам.КоличествоЗН <= ИнтервалыОбращений.МаксПредел
		|	КОНЕЦ";
		
		ТекстСоединенияИнтервалов2 = ТекстСоединенияИнтервалов2 + "
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнтервалыОбращений КАК ИнтервалыОбращений
		|ПО
		|	ВЫБОР
		|		КОГДА ИтоговаяТаблицаОборотов.КоличествоЗН = 0 ТОГДА
		|			ИнтервалыОбращений.МинПредел=0
		|		ИНАЧЕ
		|			ИтоговаяТаблицаОборотов.КоличествоЗН > ИнтервалыОбращений.МинПредел И
		|			ИтоговаяТаблицаОборотов.КоличествоЗН <= ИнтервалыОбращений.МаксПредел
		|	КОНЕЦ";
		
		ЕстьИнтервалы = Истина;
	Иначе
		ТекстВыборкиИнтервалов = ТекстВыборкиИнтервалов+",
		|	0 КАК ИнтервалОбращений,
		|	0 КАК ПорядокИнтервалаОбращений";
	КонецЕсли;
	
	//Определим перечень свойств
	СтрЗапросаСвойств		= "";
	СтрЗапросаИтогиСвойств	= "";
	СтрСоединенийСвойств	= "";
	Для Каждого ТекСтруктура Из СтруктураСвязиСвойств Цикл
		
		ОтборСтрок = Новый Структура("ПутьКДанным, Использование", ТекСтруктура.Значение.ИмяИзмерения, Истина);
		Если ИзмеренияСтроки.НайтиСтроки(ОтборСтрок).Количество() = 0 И ИзмеренияКолонки.НайтиСтроки(ОтборСтрок).Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяСвойства = СтрЗаменить(ТекСтруктура.Ключ, "Значение", "");
		Свойство = ТекСтруктура.Значение.Свойство;
		ПутьКДаннымИзмерения = "ИтоговаяТаблицаОборотов."+ТекСтруктура.Значение.ИмяИзмерения;
		ИмяПараметра = ТекСтруктура.Значение.ИмяПараметра;
		ЗапросСвойстваПоля = ", ЕСТЬNULL(" + ИмяСвойства + ".Значение, """ + Свойство.Наименование + ": <Пусто>"") КАК " + ИмяСвойства + "Значение	//СВОЙСТВО" + Символы.ПС;
		ЗапросСвойстваИтоги = ",МАКСИМУМ(" + ИмяСвойства + "Значение)	//СВОЙСТВО" + Символы.ПС;
		ЗапросСвойстваСоединение = "{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + " ПО " + ИмяСвойства + ".Объект = " + ПутьКДаннымИзмерения + " И " + ИмяСвойства + ".Свойство = &" + ИмяПараметра + "}	//СВОЙСТВО" + Символы.ПС;
		
		СтрЗапросаСвойств      = СтрЗапросаСвойств      + ЗапросСвойстваПоля;
		СтрЗапросаИтогиСвойств = СтрЗапросаИтогиСвойств + ЗапросСвойстваИтоги;
		СтрСоединенийСвойств   = СтрСоединенийСвойств   + ЗапросСвойстваСоединение;
	КонецЦикла;
	ТекстОтбораСвойств = "";
	Если Не ПустаяСтрока(СтрЗапросаСвойств) Тогда
		ТекстОтбораСвойств = "{ГДЕ
		|	ИтоговаяТаблицаОборотов.Заказчик.* КАК Заказчик
		|	"+СтрЗапросаСвойств+Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА}";
	КонецЕсли;
	СтрЗапросаСвойств      = СтрЗапросаСвойств+Символы.ПС+"//ВЫБРАННЫЕСВОЙСТВА";
	СтрЗапросаИтогиСвойств = СтрЗапросаИтогиСвойств+Символы.ПС+"//ВЫБРАННЫЕИТОГИСВОЙСТВА";
	СтрСоединенийСвойств   = СтрСоединенийСвойств+Символы.ПС+"//ВЫБРАННЫЕСОЕДИНЕНИЯ";
	
	ТекстСоединенийКИ  = "";
	ТекстВыборкиКИ     = "";
	ТекстПсевданимовКИ = "";
	ТекстИтоговКИ      = "";
	ИзмерениеЗаказчик = ИзмеренияСтроки.НайтиСтроки(Новый Структура("ПутьКДанным, Использование", "Заказчик", Истина));
	Если ИзмерениеЗаказчик.Количество() > 0 Тогда
		Если ПараметрыИзмеренийСтрок.Свойство("Заказчик") Тогда
			Если ПараметрыИзмеренийСтрок.Заказчик.Свойство("Поля") Тогда
				ДопПоля = ПараметрыИзмеренийСтрок.Заказчик.Поля;
				Если ТипЗнч(ДопПоля) = Тип("ТаблицаЗначений") Тогда
					Для Каждого ТекПоле Из ДопПоля Цикл
						ИмяПоля = СтрЗаменить(ТекПоле.ПутьКДанным, ".", "");
						Если СтруктураКИ.Свойство(ИмяПоля) Тогда
							
							ДанныеКИ = СтруктураКИ[ИмяПоля];
							
							ИмяПараметраВид = "ПараметрВид" + ИмяПоля;
							ИмяПараметраТип = "ПараметрТип" + ИмяПоля;
							ИмяТаблицы      = "ТаблицаКИ"   + ИмяПоля;
							
							ТекстВыборкиКИ    = ТекстВыборкиКИ + ",
							|	ЕСТЬNULL(" + ИмяТаблицы + ".Представление, """") КАК " + ИмяПоля;
							
							ТекстПсевданимовКИ = ТекстПсевданимовКИ + ",
							|	" + ИмяПоля + " КАК " + ИмяПоля;
							
							ТекстИтоговКИ = ТекстИтоговКИ + ",
							|	МАКСИМУМ(" + ИмяПоля + ")";
							
							ТекстСоединенийКИ  = ТекстСоединенийКИ + "
							|{ЛЕВОЕ СОЕДИНЕНИЕ
							|	РегистрСведений.КонтактнаяИнформация КАК " + ИмяТаблицы + "
							|ПО
							|	ИтоговаяТаблицаОборотов.Заказчик = " + ИмяТаблицы + ".Объект И 
							|	" + ИмяТаблицы + ".Вид = &" + ИмяПараметраВид + " И 
							|	" + ИмяТаблицы + ".Тип = &" + ИмяПараметраТип + "
							|}";
							
							ПостроительОтчета.Параметры.Вставить(ИмяПараметраВид, ДанныеКИ.Вид);
							ПостроительОтчета.Параметры.Вставить(ИмяПараметраТип, ДанныеКИ.Тип);
							
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Поля временной таблицы
	ТекстВыборки = "";
	ТекстСгруппировать = "";
	ТекстСгруппировать2 = "";
	ТекстПолейИндексов = "";
	ТекстПолейПериодов = "";
	ТекстПолейПериодов2 = "";
	// Поля основной таблицы
	ТекстВыборки2 = "";
	ТекстВыборки3 = "";
	ТекстВыборки4 = "";
	ТекстСоединения = "";
	ТекстПсевдонимов = "";
	
	Исключения    = Новый Структура("ТипКлиента, ТипДанных, ИнтервалСуммыРабот, ИнтервалОбращений");
	Индексировать = Новый Структура("Автомобиль, Заказчик");
	Периоды       = Новый Структура();
	Периоды.Вставить("ПериодМесяц", "Месяц");
	Периоды.Вставить("ПериодКвартал", "Квартал");
	Периоды.Вставить("ПериодГод", "Год");
	
	ЕстьПериоды    = Ложь;
	ЕстьТипДанных  = Ложь;
	ЕстьТипКлиента = Ложь;
	Для Каждого ТекГруппировка Из ИзмеренияСтроки Цикл
		Имя = СтрЗаменить(ТекГруппировка.ПутьКДанным, ".", "");
		Если НЕ ТекГруппировка.Использование Тогда Продолжить; КонецЕсли;
		
		Если Имя = "ТипДанных" Тогда
			ЕстьТипДанных = Истина;
		КонецЕсли;
		
		Если Имя = "ТипКлиента" Тогда
			ЕстьТипКлиента = Истина;
		КонецЕсли;
		
		Если Исключения.Свойство(Имя) Тогда Продолжить; КонецЕсли;
		
		Если Периоды.Свойство(Имя) Тогда
			// Поля временной таблицы
			ТекстПолейПериодов  = ТекстПолейПериодов + ",
			|	НачалоПериода(ЗаказНаряд.ДатаСоздания, "+Периоды[Имя] + ") КАК " + Имя;
			ТекстПолейПериодов2 = ТекстПолейПериодов2 + ",
			|	НачалоПериода(&ДатаНач, "+Периоды[Имя] + ") КАК " + Имя;
			
			ЕстьПериоды = Истина;
			ТекстВыборки2 = ТекстВыборки2 + ",
			|	ТаблицаОборотов."+Имя+" КАК "+Имя;
			
			ТекстВыборки4 = ТекстВыборки4 + ",
			|	ДатаВремя(1, 1, 1) КАК "+Имя;
		
			ТекстПсевдонимов = ТекстПсевдонимов + ",
			|	"+Имя+".* КАК "+Имя;
		
			Продолжить;
		КонецЕсли;
		
		// Поля временной таблицы
		ТекстВыборки = ТекстВыборки + ",
		|	ЗаказНаряд."+ТекГруппировка.ПутьКДанным+" КАК "+Имя;
		
		ТекстСгруппировать = ТекстСгруппировать + ?(ТекстСгруппировать="", "", ",") + "
		|	ЗаказНаряд."+ТекГруппировка.ПутьКДанным;
		
		ТекстСгруппировать2 = ТекстСгруппировать2 + ?(ТекстСгруппировать2="", "", ",") + "
		|	ТаблицаОборотов."+Имя;
		
		ТекстВыборки2 = ТекстВыборки2 + ",
		|	ТаблицаОборотов."+Имя+" КАК "+Имя;
		
		ТекстВыборки4 = ТекстВыборки4 + ",
		|	ТаблицаКлиентовПоТипам."+Имя+" КАК "+Имя;
		
		ТекстВыборки3 = ТекстВыборки3 + ",
		|	ТаблицаОборотов."+Имя+" КАК "+Имя;
		
		ТекстПсевдонимов = ТекстПсевдонимов + ",
		|	"+Имя+".* КАК "+Имя;
		
		ТекстСоединения = ТекстСоединения + ?(ТекстСоединения="", "", " И ") + "
		|	ИтоговаяТаблицаОборотов."+Имя+" = ТаблицаОборотов."+Имя;
		// Поля основной таблицы
		
		Если Индексировать.Свойство(Имя) Тогда
			ТекстПолейИндексов = ТекстПолейИндексов + ?(ТекстПолейИндексов="", "", ",") + "
			|	" + Имя;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ТекГруппировка Из ИзмеренияКолонки Цикл
		Имя = СтрЗаменить(ТекГруппировка.ПутьКДанным, ".", "");
		Если НЕ ТекГруппировка.Использование Тогда Продолжить; КонецЕсли;
		
		Если Имя = "ТипДанных" Тогда
			ЕстьТипДанных = Истина;
		КонецЕсли;
		
		Если Имя = "ТипКлиента" Тогда
			ЕстьТипКлиента = Истина;
		КонецЕсли;
		
		Если Исключения.Свойство(Имя) Тогда Продолжить; КонецЕсли;
		
		Если Периоды.Свойство(Имя) Тогда
			// Поля временной таблицы
			ТекстПолейПериодов  = ТекстПолейПериодов + ",
			|	НачалоПериода(ЗаказНаряд.ДатаСоздания, "+Периоды[Имя] + ") КАК " + Имя;
			ТекстПолейПериодов2 = ТекстПолейПериодов2 + ",
			|	НачалоПериода(&ДатаНач, "+Периоды[Имя] + ") КАК " + Имя;
			
			ЕстьПериоды = Истина;
			ТекстВыборки2 = ТекстВыборки2 + ",
			|	ТаблицаОборотов."+Имя+" КАК "+Имя;
			
			ТекстВыборки4 = ТекстВыборки4 + ",
			|	ДатаВремя(1, 1, 1) КАК "+Имя;
		
			ТекстПсевдонимов = ТекстПсевдонимов + ",
			|	"+Имя+".* КАК "+Имя;
		
			Продолжить;
		КонецЕсли;
		
		// Поля временной таблицы
		ТекстВыборки = ТекстВыборки + ",
		|	ЗаказНаряд."+ТекГруппировка.ПутьКДанным+" КАК "+Имя;
		ТекстСгруппировать = ТекстСгруппировать + ?(ТекстСгруппировать="", "", ",") + "
		|	ЗаказНаряд."+ТекГруппировка.ПутьКДанным;
		
		ТекстСгруппировать2 = ТекстСгруппировать2 + ?(ТекстСгруппировать2="", "", ",") + "
		|	ТаблицаОборотов."+Имя;
		
		ТекстВыборки2 = ТекстВыборки2 + ",
		|	ТаблицаОборотов."+Имя+" КАК "+Имя;
		
		ТекстВыборки4 = ТекстВыборки4 + ",
		|	ТаблицаКлиентовПоТипам."+Имя+" КАК "+Имя;
		
		ТекстВыборки3 = ТекстВыборки3 + ",
		|	ТаблицаОборотов."+Имя+" КАК "+Имя;
		
		ТекстПсевдонимов = ТекстПсевдонимов + ",
		|	"+Имя+".* КАК "+Имя;
		
		ТекстСоединения = ТекстСоединения + ?(ТекстСоединения="", "", " И ") + "
		|	ИтоговаяТаблицаОборотов."+Имя+" = ТаблицаОборотов."+Имя;
		
		// Поля основной таблицы
		Если Индексировать.Свойство(Имя) Тогда
			ТекстПолейИндексов = ТекстПолейИндексов + ?(ТекстПолейИндексов="", "", ",") + "
			|	" + Имя;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстСоединения) Тогда
		ТекстСоединения = "ИСТИНА";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстПолейИндексов) Тогда
		ТекстПолейИндексов = "ИНДЕКСИРОВАТЬ ПО" + ТекстПолейИндексов;
	КонецЕсли;
	
	//ПрошлыеДанные
	//ТекущиеДанные
	
	Если ЕстьПериоды Тогда
		КопияТекстСгруппировать = ТекстСгруппировать + ?(ТекстСгруппировать="", "", ",") + "
		|	ЗаказНаряд.ДатаСоздания";
	Иначе
		КопияТекстСгруппировать = ТекстСгруппировать;
	КонецЕсли;
	
	ТекстЗапросаРезультат = ТекстЗапросаРезультат + "
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ЗаказНаряд.ДатаСоздания) КАК ДатаПервогоРемонта,
	|	МАКСИМУМ(ЗаказНаряд.ДатаСоздания) КАК ДатаПоследнегоРемонта,
	|	СУММА(ВЫБОР
	|			КОГДА ЗаказНаряд.КурсВалютыУпр = 0 ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЗаказНаряд.СуммаДокумента*ЗаказНаряд.КурсДокумента/ЗаказНаряд.КурсВалютыУпр + ЕСТЬNULL(ДокументКорректировкаРеализации.СуммаДокумента, 0) + ЕСТЬNULL(ДокументАктРазногласий.СуммаДокумента, 0)
	|	КОНЕЦ) КАК СуммаЗН,
	|	КОЛИЧЕСТВО(ЗаказНаряд.Ссылка) КАК КоличествоЗН,
	|	СУММА(ВЫБОР
	|			КОГДА ЗаказНаряд.ВидРемонта.ТехОбслуживание И НЕ ЗаказНаряд.КурсВалютыУпр = 0 ТОГДА
	|				ЗаказНаряд.СуммаДокумента*ЗаказНаряд.КурсДокумента/ЗаказНаряд.КурсВалютыУпр + ЕСТЬNULL(ДокументКорректировкаРеализации.СуммаДокумента, 0) + ЕСТЬNULL(ДокументАктРазногласий.СуммаДокумента, 0)
	|			ИНАЧЕ
	|				0
	|	КОНЕЦ) КАК СуммаТО,
	|	СУММА(ВЫБОР
	|			КОГДА ЗаказНаряд.ВидРемонта.ТехОбслуживание ТОГДА
	|				1
	|			ИНАЧЕ
	|				0
	|	КОНЕЦ) КАК КоличествоТО" + ТекстВыборки + ТекстПолейПериодов + "
	|ПОМЕСТИТЬ
	|	ТаблицаОборотов
	|ИЗ
	|	Документ.ЗаказНаряд КАК ЗаказНаряд
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ДокументКорректировкаРеализации КАК ДокументКорректировкаРеализации
	|ПО
	|	ЗаказНаряд.Ссылка = ДокументКорректировкаРеализации.Сделка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ДокументАктРазногласий КАК ДокументАктРазногласий
	|ПО
	|	ЗаказНаряд.Ссылка = ДокументАктРазногласий.Сделка
	|ГДЕ
	|	ЗаказНаряд.ДатаСоздания МЕЖДУ &ДатаНач И &ДатаКон
	|	И ЗаказНаряд.ПометкаУдаления = ЛОЖЬ
	|{ГДЕ
	|	ЗаказНаряд.Заказчик.* КАК Заказчик,
	|	ЗаказНаряд.ВидРемонта.* КАК ВидРемонта,
	|	ЗаказНаряд.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
	|	ЗаказНаряд.Менеджер.* КАК Менеджер,
	|	ЗаказНаряд.Автомобиль.* КАК Автомобиль,
	|	ЗаказНаряд.Цех.* КАК Цех,
	|	ЗаказНаряд.Мастер.* КАК Мастер,
	|	МИНИМУМ(ЗаказНаряд.ДатаСоздания) КАК ДатаПервогоРемонта,
	|	МАКСИМУМ(ЗаказНаряд.ДатаСоздания) КАК ДатаПоследнегоРемонта
	|}
	|"+?(КопияТекстСгруппировать = "", "", "СГРУППИРОВАТЬ ПО
	|	" + КопияТекстСгруппировать) + "
	|"+ТекстПолейИндексов+"
	|;
	|";
	
	ТекстСоединенияИнтервалов3 = "";
	Если ЕстьИнтервалы Тогда
		ТекстЗапросаРезультат = ТекстЗапросаРезультат + "
		|ВЫБРАТЬ
		|	ИтоговаяТаблицаОборотов.КоличествоЗН"+ТекстВыборкиИнтервалов2+СтрЗаменить(ТекстВыборки3, "ТаблицаОборотов.", "ИтоговаяТаблицаОборотов.") + "
		|ПОМЕСТИТЬ
		|	ИтоговаяТаблицаОборотов
		|ИЗ(
		|	ВЫБРАТЬ
		|		СУММА(ТаблицаОборотов.СуммаЗН) КАК СуммаЗН,
		|		СУММА(ТаблицаОборотов.КоличествоЗН) КАК КоличествоЗН" + ТекстВыборки3 + "
		|	ИЗ
		|		ТаблицаОборотов КАК ТаблицаОборотов
		|	" + ?(ТекстСгруппировать2 = "", "" , "СГРУППИРОВАТЬ ПО
		|		" + ТекстСгруппировать2) + ") КАК ИтоговаяТаблицаОборотов
		|" + ТекстСоединенияИнтервалов2 + "
		|"+ТекстПолейИндексов+"
		|;
		|";
		
		ТекстСоединенияИнтервалов3 = "
		|ЛЕВОЕ СОЕДИНЕНИЕ ИтоговаяТаблицаОборотов КАК ИтоговаяТаблицаОборотов
		|ПО
		|	"+ТекстСоединения;
		
	КонецЕсли;
	
	Если ЕстьТипДанных ИЛИ ЕстьТипКлиента Тогда
		ТекстЗапросаРезультат = ТекстЗапросаРезультат + "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ВЫБОР
		|		КОГДА ЗаказНаряд.ДатаСоздания < &ДатаНач ТОГДА
		|			1
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК КоличествоЗН,
		|	СУММА(ВЫБОР
		|		КОГДА ЗаказНаряд.ДатаСоздания < &ДатаНач И НЕ ЗаказНаряд.КурсВалютыУпр = 0 ТОГДА
		|			ЗаказНаряд.СуммаДокумента * ЗаказНаряд.КурсДокумента/ЗаказНаряд.КурсВалютыУпр + ЕСТЬNULL(ДокументКорректировкаРеализации.СуммаДокумента, 0) + ЕСТЬNULL(ДокументАктРазногласий.СуммаДокумента, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК СуммаЗН,
		|	СУММА(ВЫБОР
		|		КОГДА ЗаказНаряд.ДатаСоздания < &ДатаНач И ЗаказНаряд.ВидРемонта.ТехОбслуживание И НЕ ЗаказНаряд.КурсВалютыУпр = 0 ТОГДА
		|			ЗаказНаряд.СуммаДокумента * ЗаказНаряд.КурсДокумента/ЗаказНаряд.КурсВалютыУпр + ЕСТЬNULL(ДокументКорректировкаРеализации.СуммаДокумента, 0) + ЕСТЬNULL(ДокументАктРазногласий.СуммаДокумента, 0)
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК СуммаТО,
		|	СУММА(ВЫБОР
		|		КОГДА  ЗаказНаряд.ДатаСоздания < &ДатаНач И ЗаказНаряд.ВидРемонта.ТехОбслуживание ТОГДА
		|			1
		|		ИНАЧЕ
		|			0
		|	КОНЕЦ) КАК КоличествоТО,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЗаказНаряд.ДатаСоздания) >= &ДатаНач ТОГДА
		|			""Постоянные клиенты""
		|		ИНАЧЕ
		|			""Потерянные клиенты""
		|	КОНЕЦ КАК ТипКлиента,
		|	МИНИМУМ(ЗаказНаряд.ДатаСоздания) КАК ДатаПервогоРемонта,
		|	МАКСИМУМ(ВЫБОР
		|		КОГДА ЗаказНаряд.ДатаСоздания < &ДатаНач ТОГДА
		|			ЗаказНаряд.ДатаСоздания
		|		ИНАЧЕ
		|			ДатаВремя(1, 1, 1)
		|	КОНЕЦ) КАК ДатаПоследнегоРемонта" + ТекстВыборки + ТекстПолейПериодов2 + "
		|ПОМЕСТИТЬ
		|	ТаблицаКлиентовПоТипам
		|ИЗ
		|	Документ.ЗаказНаряд КАК ЗаказНаряд
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ДокументКорректировкаРеализации КАК ДокументКорректировкаРеализации
		|ПО
		|	ЗаказНаряд.Ссылка = ДокументКорректировкаРеализации.Сделка
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ДокументАктРазногласий КАК ДокументАктРазногласий
		|ПО
		|	ЗаказНаряд.Ссылка = ДокументАктРазногласий.Сделка
		|ГДЕ
		|	ЗаказНаряд.ДатаСоздания < &ДатаКон
		|	И ЗаказНаряд.ПометкаУдаления = ЛОЖЬ
		|{ГДЕ
		|	ЗаказНаряд.ВидРемонта.* КАК ВидРемонта,
		|	ЗаказНаряд.Заказчик.* КАК Заказчик,
		|	ЗаказНаряд.ПодразделениеКомпании.* КАК ПодразделениеКомпании,
		|	ЗаказНаряд.Менеджер.* КАК Менеджер,
		|	ЗаказНаряд.Автомобиль.* КАК Автомобиль,
		|	ЗаказНаряд.Цех.* КАК Цех,
		|	ЗаказНаряд.Мастер.* КАК Мастер,
		|	МИНИМУМ(ЗаказНаряд.ДатаСоздания) КАК ДатаПервогоРемонта,
		|	МАКСИМУМ(ЗаказНаряд.ДатаСоздания) КАК ДатаПоследнегоРемонта}
		|"+?(ТекстСгруппировать = "", "", "СГРУППИРОВАТЬ ПО
		|	"+ТекстСгруппировать)+"
		|
		|"+?(ЕстьТипДанных И ЕстьТипКлиента, "", ?(ЕстьТипДанных, "ИМЕЮЩИЕ
		|	МАКСИМУМ(ЗаказНаряд.ДатаСоздания) >= &ДатаНач", "ИМЕЮЩИЕ
		|	МАКСИМУМ(ЗаказНаряд.ДатаСоздания) < &ДатаНач"))+"
		|
		|"+ТекстПолейИндексов+"
		|;
		|";
	КонецЕсли;
	
	ТекстЗапросаРезультат = ТекстЗапросаРезультат + "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИтоговаяТаблицаОборотов.ТипКлиента КАК ТипКлиента,
	|	ИтоговаяТаблицаОборотов.ТипДанных КАК ТипДанных" + СтрЗаменить(ТекстВыборки2, "ТаблицаОборотов.", "ИтоговаяТаблицаОборотов.") + ",
	|	ИтоговаяТаблицаОборотов.ДатаПервогоРемонта КАК ДатаПервогоРемонта,
	|	ИтоговаяТаблицаОборотов.ДатаПоследнегоРемонта КАК ДатаПоследнегоРемонта,
	|	ИтоговаяТаблицаОборотов.СуммаЗН КАК СуммаЗН,
	|	ИтоговаяТаблицаОборотов.КоличествоЗН КАК КоличествоЗН,
	|	ИтоговаяТаблицаОборотов.СуммаТО КАК СуммаТО,
	|	ИтоговаяТаблицаОборотов.КоличествоТО КАК КоличествоТО"+ТекстВыборкиИнтервалов+"
	|	"+СтрЗапросаСвойств+"
	|	"+ТекстВыборкиКИ+"
	|{
	|ВЫБРАТЬ
	|	ТипКлиента КАК ТипКлиента,
	|	ТипДанных КАК ТипДанных" + ТекстПсевдонимов + ",
	|	СуммаЗН КАК СуммаЗН,
	|	СуммаТО КАК СуммаТО,
	|	КоличествоЗН КАК КоличествоЗН,
	|	КоличествоТО КАК КоличествоТО,
	|	ДатаПоследнегоРемонта КАК ДатаПоследнегоРемонта
	|	"+СтрЗапросаСвойств+"
	|	"+ТекстПсевданимовКИ+"}
	|ИЗ
	|	(ВЫБРАТЬ
	|		""Постоянные клиенты"" КАК ТипКлиента,
	|		""Текущие данные"" КАК ТипДанных" + ТекстВыборки2 + ",
	|		ТаблицаОборотов.ДатаПервогоРемонта КАК ДатаПервогоРемонта,
	|		ТаблицаОборотов.ДатаПоследнегоРемонта КАК ДатаПоследнегоРемонта,
	|		ТаблицаОборотов.СуммаЗН КАК СуммаЗН,
	|		ТаблицаОборотов.КоличествоЗН КАК КоличествоЗН,
	|		ТаблицаОборотов.СуммаТО КАК СуммаТО,
	|		ТаблицаОборотов.КоличествоТО КАК КоличествоТО"+ТекстВыборкиИнтервалов3+"
	|	{ВЫБРАТЬ
	|		ТипКлиента КАК ТипКлиента,
	|		ТипДанных КАК ТипДанных" + ТекстПсевдонимов + ",
	|		СуммаЗН КАК СуммаЗН,
	|		СуммаТО КАК СуммаТО,
	|		КоличествоЗН КАК КоличествоЗН,
	|		КоличествоТО КАК КоличествоТО,
	|		ДатаПоследнегоРемонта КАК ДатаПоследнегоРемонта}
	|	ИЗ
	|	ТаблицаОборотов КАК ТаблицаОборотов
	|	"+ТекстСоединенияИнтервалов3+?(ЕстьТипКлиента ИЛИ ЕстьТипДанных, "
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаКлиентовПоТипам.ТипКлиента КАК ТипКлиента,
	|		""Прошлые данные"" КАК ТипДанных" + ТекстВыборки4 + ",
	|		ТаблицаКлиентовПоТипам.ДатаПервогоРемонта КАК ДатаПервогоРемонта,
	|		ТаблицаКлиентовПоТипам.ДатаПоследнегоРемонта КАК ДатаПоследнегоРемонта,
	|		ТаблицаКлиентовПоТипам.СуммаЗН КАК СуммаЗН,
	|		ТаблицаКлиентовПоТипам.КоличествоЗН КАК КоличествоЗН,
	|		ТаблицаКлиентовПоТипам.СуммаТО КАК СуммаТО,
	|		ТаблицаКлиентовПоТипам.КоличествоТО КАК КоличествоТО"+ТекстВыборкиИнтервалов4+"
	|	{ВЫБРАТЬ
	|		ТипКлиента КАК ТипКлиента,
	|		ТипДанных КАК ТипДанных" + ТекстПсевдонимов + ",
	|		СуммаЗН КАК СуммаЗН,
	|		СуммаТО КАК СуммаТО,
	|		КоличествоЗН КАК КоличествоЗН,
	|		КоличествоТО КАК КоличествоТО,
	|		ДатаПоследнегоРемонта КАК ДатаПоследнегоРемонта
	|		}
	|ИЗ
	|	ТаблицаКлиентовПоТипам КАК ТаблицаКлиентовПоТипам
	|	"+ТекстСоединенияИнтервалов+"
	|	"+СтрЗаменить(ТекстСоединенияИнтервалов3, " ТаблицаОборотов.", " ТаблицаКлиентовПоТипам."), "")+") КАК ИтоговаяТаблицаОборотов
	|
//	|	" + ТекстСоединения + "}
	|	"+СтрСоединенийСвойств+"
	|	" + ТекстСоединенийКИ + "
	|	"+ТекстОтбораСвойств+"
	|	
	|{УПОРЯДОЧИТЬ ПО
	|	ТипКлиента КАК ТипКлиента,
	|	ТипДанных КАК ТипДанных" + ТекстПсевдонимов + "
	|	"+СтрЗапросаСвойств+"
	|	"+ТекстПсевданимовКИ+"}
	|	
	|{ИТОГИ ПО
	|	ТипКлиента КАК ТипКлиента,
	|	ТипДанных КАК ТипДанных" + ТекстПсевдонимов + "
	|	"+СтрЗапросаСвойств+"
	|	"+ТекстПсевданимовКИ+"}
	|	
	|ИТОГИ
	|	СУММА(СуммаЗН),
	|	СУММА(СуммаТО),
	|	СУММА(КоличествоЗН),
	|	СУММА(КоличествоТО),
	|	МИНИМУМ(ДатаПервогоРемонта),
	|	МАКСИМУМ(ДатаПоследнегоРемонта),
	|	ВЫБОР
	|		КОГДА ИнтервалСуммыРабот ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(ПорядокИнтервалаСуммыРабот)
	|	КОНЕЦ КАК ПорядокИнтервалаСуммыРабот,
	|	ВЫБОР
	|		КОГДА ИнтервалОбращений ЕСТЬ NULL ТОГДА 0
	|		ИНАЧЕ МАКСИМУМ(ПорядокИнтервалаОбращений)
	|	КОНЕЦ КАК ПорядокИнтервалаОбращений
	|	"+СтрЗапросаИтогиСвойств+"
	|	"+ТекстИтоговКИ+"
	|ПО
	|	ОБЩИЕ,
	|	ИнтервалСуммыРабот,
	|	ИнтервалОбращений,
	|	ТипКлиента,
	|	ТипДанных
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат ТекстЗапросаРезультат;
	
КонецФункции

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	Если КоличественныеГруппы.Количество()=0 Тогда
		НовыйПериод = КоличественныеГруппы.Добавить();
		НовыйПериод.КоличествоРемонтов = 3;
		НовыйПериод.Представление      = "До 3 раз.";
		НовыйПериод = КоличественныеГруппы.Добавить();
		НовыйПериод.КоличествоРемонтов = 7;
		НовыйПериод.Представление      = "От 4 до 7 раз.";
		НовыйПериод = КоличественныеГруппы.Добавить();
		НовыйПериод.КоличествоРемонтов = 10;
		НовыйПериод.Представление      = "От 8 до 10 раз.";
		НовыйПериод = КоличественныеГруппы.Добавить();
		НовыйПериод.КоличествоРемонтов = 20;
		НовыйПериод.Представление      = "От 11 до 20 раз.";
		НовыйПериод = КоличественныеГруппы.Добавить();
		НовыйПериод.КоличествоРемонтов = 30;
		НовыйПериод.Представление      = "От 21 до 30 раз.";
		НовыйПериод = КоличественныеГруппы.Добавить();
		НовыйПериод.КоличествоРемонтов = 50000;
		НовыйПериод.Представление      = "Более 30 раз.";
	КонецЕсли;
	
	Если СуммовыеГруппы.Количество()=0 Тогда
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаРемонта  = 100;
		НовыйПериод.Представление = "До 100";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаРемонта  = 500;
		НовыйПериод.Представление = "От 100 до 500";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаРемонта  = 1000;
		НовыйПериод.Представление = "От 500 до 1000";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаРемонта  = 5000;
		НовыйПериод.Представление = "От 1000 до 5000";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаРемонта  = 5000;
		НовыйПериод.Представление = "От 5000 до 10000";
		НовыйПериод = СуммовыеГруппы.Добавить();
		НовыйПериод.СуммаРемонта  = 9999999999999;
		НовыйПериод.Представление = "Более 10000";
	КонецЕсли;
	
	ВетвьВладельца = ДеревоДоступныхПолей.Строки.Найти("Заказчик", "ПутьКДанным");
	Если НЕ ВетвьВладельца = Неопределено Тогда
		
		// Добавим в доступные поля
		ВетвьКИ = ВетвьВладельца.Строки.Найти("Заказчик.КИ", "ПутьКДанным");
		Если ВетвьКИ = Неопределено Тогда
			
			ВетвьКИ = ВетвьВладельца.Строки.Добавить();
			ВетвьКИ.Имя           = "КИ";
			ВетвьКИ.ПутьКДанным   = "Заказчик.КИ";
			ВетвьКИ.Представление = "Контактная информация";
			ВетвьКИ.Отбор         = Ложь;
			ВетвьКИ.Порядок       = Ложь;
			ВетвьКИ.Измерение     = Ложь;
			
		КонецЕсли;
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ВидыКонтактнойИнформации.Ссылка,
		|	ВидыКонтактнойИнформации.Наименование,
		|	ВидыКонтактнойИнформации.Тип
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|ГДЕ
		|	ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ");
		
		ТипКИ = Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации");
		
		РодительПоля = ВетвьКИ.Родитель.Родитель;
		ПредставлениеРодителя = ВетвьКИ.Родитель.Представление;
		Пока РодительПоля <> Неопределено Цикл
			ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
			РодительПоля = РодительПоля.Родитель;
		КонецЦикла;
		
		ВыборкаКИ = Запрос.Выполнить().Выбрать();
		Пока ВыборкаКИ.Следующий() Цикл
			
			ИмяПоля = "ЗаказчикКИ" + СтрЗаменить(ВыборкаКИ.Ссылка.УникальныйИдентификатор(),"-","_");
			СтруктураКИ.Вставить(ИмяПоля, Новый Структура("Вид, Тип", ВыборкаКИ.Ссылка, ВыборкаКИ.Тип));
			отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Ложь, "Заказчик.КИ", ИмяПоля, ВыборкаКИ.Наименование, ТипКИ);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента – передает ссылку на табличный документ 
//						или поле табличного документа
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент 
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт

	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// формирует новый макет отчета
// Параметры:
//	ТекПостроитель - построитель отчета
//	МакетОтчета - начальный макет отчета
//	ПараметрыОформления - параметры оформления нового макета
// Возвращает:
//	ЛОЖЬ
Функция ИзменитьМакетПостроителя(ТекПостроитель, МакетОтчета, ПараметрыОформления) Экспорт
	
	// Сортировка интервалов
	ТекИзмерение = ТекПостроитель.Порядок.Найти("ИнтервалСуммыРабот");
	Если Не ТекИзмерение = Неопределено Тогда
		Инд = ТекПостроитель.Порядок.Индекс(ТекИзмерение);
		ТекПостроитель.Порядок.Удалить(Инд);
		ТекПостроитель.Порядок.Добавить("ПорядокИнтервалаСуммыРабот", "ПорядокИнтервалаСуммыРабот",,НаправлениеСортировки.Возр);
		Смещение = Инд-(ТекПостроитель.Порядок.Количество()-1);
		ТекПостроитель.Порядок.Сдвинуть(ТекПостроитель.Порядок.Количество()-1, Смещение);
		ТекПостроитель.ВыбранныеПоля.Добавить("ПорядокИнтервалаСуммыРабот", "ПорядокИнтервалаСуммыРабот");
	КонецЕсли;
	
	ТекИзмерение = ТекПостроитель.Порядок.Найти("ИнтервалОбращений");
	Если Не ТекИзмерение = Неопределено Тогда
		Инд = ТекПостроитель.Порядок.Индекс(ТекИзмерение);
		ТекПостроитель.Порядок.Удалить(Инд);
		ТекПостроитель.Порядок.Добавить("ПорядокИнтервалаОбращений", "ПорядокИнтервалаОбращений",,НаправлениеСортировки.Возр);
		Смещение = Инд-(ТекПостроитель.Порядок.Количество()-1);
		ТекПостроитель.Порядок.Сдвинуть(ТекПостроитель.Порядок.Количество()-1, Смещение);
		ТекПостроитель.ВыбранныеПоля.Добавить("ПорядокИнтервалаОбращений", "ПорядокИнтервалаОбращений");
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураКИ = Новый Структура();
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Функция ПолучитьОсновныеПараметры() Экспорт 
	
	СтруктураОсновныхПараметров = Новый Структура;
	СтруктураОсновныхПараметров.Вставить("ВалютаОтчета");
	
	Возврат СтруктураОсновныхПараметров;
	
КонецФункции

Функция ПолучитьСвойства() Экспорт
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("Заказчик");
	СтруктураСвойств.Вставить("Автомобиль");
	СтруктураСвойств.Вставить("ЗаказНаряд");
	СтруктураСвойств.Вставить("Хозяин");
	
	Возврат СтруктураСвойств;
	
КонецФункции

// Устанавливает основные параметры
Процедура УстановитьПараметры()
	
	ВалютаОтчета  = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ВалютаОтчета", КомпоновщикНастроек.Настройки);
	ДатаОкончания = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", КомпоновщикНастроек.Настройки);
	
	КурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаОкончания, Новый Структура("Валюта",ВалютаОтчета));
	КурсВалютыОтчета = КурсВалюты.Курс / ?(КурсВалюты.Кратность = 0,1,КурсВалюты.Кратность);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КурсВалютыОтчета", КурсВалютыОтчета);
	
КонецПроцедуры

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
	ФорматПредставленияГодаВыпускаАвтомобиля = орПолучитьФорматПредставленияГодаВыпускаАвтомобиля();
	Для Каждого ТекНабор Из СхемаКомпоновкиДанных.НаборыДанных Цикл
		Для Каждого ТекПоле Из ТекНабор.Поля Цикл
			Если НЕ ТипЗнч(ТекПоле) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			ИмяПоля = СокрЛП(ТекПоле.Поле);
			Если ИмяПоля = "ГодВыпуска" Тогда
				ТекПоле.ПараметрыРедактирования.УстановитьЗначениеПараметра("ФорматРедактирования", ФорматПредставленияГодаВыпускаАвтомобиля);
				ТекПоле.Оформление.УстановитьЗначениеПараметра("Формат", ФорматПредставленияГодаВыпускаАвтомобиля);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// Стандартная процедура настройки компоновщика данных
//
Процедура ПриИзмененииВарианта(ФормаОтчета) Экспорт
	
	ВалютаОтчета = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВалютаОтчета"));
	Если НЕ ЗначениеЗаполнено(ВалютаОтчета.Значение) Тогда
		ВалютаОтчета.Значение = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииВарианта()

Процедура ПолучитьОтборСКД(ПолеКомпоновки, Отбор, НайденныеОтборы)
	
	Для Каждого ТекОтбор Из Отбор Цикл
		Если ТекОтбор.Использование Тогда
			Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				ПолучитьОтборСКД(ПолеКомпоновки, ТекОтбор.Элементы, НайденныеОтборы);
			ИначеЕсли ТипЗнч(ТекОтбор) = Тип("ЭлементОтбораКомпоновкиДанных") И ТекОтбор.ЛевоеЗначение = ПолеКомпоновки Тогда
				НайденныеОтборы.Добавить(ТекОтбор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	УстановитьПараметры();
	
	ФорматПредставления = обПраво("ФорматПредставленияГодаВыпускаАвтомобиля");
	
	Если ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.MMyyyy ИЛИ 
		ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.yyyy Тогда
		
		ПолеПоиска      = Новый ПолеКомпоновкиДанных("ГодВыпуска");
		НайденныеОтборы = Новый Массив;
		ПолучитьОтборСКД(ПолеПоиска, КомпоновщикНастроек.Настройки.Отбор.Элементы, НайденныеОтборы);
		Для Каждого ТекОтбор Из НайденныеОтборы Цикл
			Если ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.MMyyyy Тогда
				ТекОтбор.ПравоеЗначение = НачалоМесяца(ТекОтбор.ПравоеЗначение);
			ИначеЕсли ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.yyyy Тогда
				ТекОтбор.ПравоеЗначение = НачалоГода(ТекОтбор.ПравоеЗначение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

// проверяет настройки отчета на корректность заполнения
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ВалютаОтчета = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВалютаОтчета")).Значение;
	Если НЕ ЗначениеЗаполнено(ВалютаОтчета) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не задана валюта отчета!";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Если Клиент Тогда
Перем ТекстЗапросаУпр; // текст запроса управленческий
Перем ТекстЗапросаРег; // текст запроса регламентированный
Перем ИмяРегистра Экспорт; // регистр источник данных
// Модуль отчета Активы и Пассивы

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем КоличествоГруппировок, МакетОтчета, ОформлениеСтроки; // переменные


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Формирует текст запроса
//
//Без параметров
//
// Возвращаемое значение:
//   ТекстЗапроса – текст
//
Функция ПолучитьТекстЗапроса()
	ТекстЗапроса = "";
	
	
	Возврат ТекстЗапроса;
КонецФункции // ПолучитьТекстЗапроса()
 
// Устанавливает интервал
//
// Параметры
//	Изменение - число
//	ДатаНачалаПериодаФорыОтчета - Дата
//	ДатаКонцаФормыПериодаОтчета - Дата
//
Процедура УстановитьИнтервал(Изменение=0, ДатаНачалаПериодаФорыОтчета="", ДатаКонцаФормыПериодаОтчета="") Экспорт
	
	Если ДатаНачалаПериодаФорыОтчета<>"" Тогда
		ДатаНачала = ДатаНачалаПериодаФорыОтчета;
	КонецЕсли;
	
	Если ДатаНачалаПериодаФорыОтчета<>"" Тогда
		ДатаКонца = ДатаКонцаФормыПериодаОтчета;
	КонецЕсли; 	
	
	Если СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Месяц Тогда
		ДатаНачала = НачалоМесяца(ДобавитьМесяц(ДатаНачала, Изменение));
		ДатаКонца  = КонецМесяца(ДобавитьМесяц(ДатаКонца,Изменение));
		Если ДатаКонца<ДатаНачала Тогда
			ДатаКонца = КонецМесяца(ДатаНачала);
		КонецЕсли;
		
	ИначеЕсли СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Квартал Тогда
		ДатаНачала = НачалоКвартала(ДобавитьМесяц(ДатаНачала, Изменение*3));
		ДатаКонца  = КонецКвартала(ДобавитьМесяц(ДатаКонца,Изменение*3));
        Если ДатаКонца<ДатаНачала Тогда
			ДатаКонца = КонецКвартала(ДатаНачала);
		КонецЕсли;
		
	ИначеЕсли СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Год Тогда
		ДатаНачала = НачалоГода(ДобавитьМесяц(ДатаНачала, Изменение*12));
		ДатаКонца  = КонецГода(ДобавитьМесяц(ДатаКонца,Изменение*12));
		Если ДатаКонца<ДатаНачала Тогда
			ДатаКонца = КонецГода(ДатаНачала);
		КонецЕсли;
		
	КонецЕсли;
	
	ДатаНачалаПериодаФорыОтчета = ДатаНачала;
	ДатаКонцаФормыПериодаОтчета = ДатаКонца;
КонецПроцедуры // УстановитьИнтервал()


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент (ДокРезультат=Неопределено) Экспорт
	
	Если ТипЗнч(ДокРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокРезультат) <> Тип("ТабличныйДокумент") Тогда
		ДокРезультат = Новый ТабличныйДокумент();
	Иначе
		ДокРезультат.Очистить();
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(СценарийПланирования) Тогда
		Предупреждение("Необходимо выбрать сценарий планирования!");
		Возврат ДокРезультат;
	КонецЕсли;
	
	Если ПолучитьРазвернутыеПериод Тогда
		ПолучитьРазвернутыйОтчет(ДокРезультат);
	Иначе
		ПолучитьСвернутыйОтчет(ДокРезультат);
	КонецЕсли;
		
	Возврат ДокРезультат;
КонецФункции // СформироватьТабличныйДокумент()

// Получает развернутый отчет
//
// Параметры
//	ДокРезультат - ТабличныйДокумент
//
Процедура ПолучитьРазвернутыйОтчет(ДокРезультат)
	
	// Выполним запрос
	Запрос       = Новый Запрос;
	Запрос.Текст = ПолучитьМакет("ТекстЗапросаРазвернутыйПоУпрВалюте").ПолучитьТекст();
	
	// Установим нужную периодичность
	Если СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Квартал Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Месяц", "Квартал");		
	ИначеЕсли СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Год Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Месяц", "Год");		
	КонецЕсли;
	
	// Теперь отбор по подразделению
	Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
		Если обЗначениеНеЗаполнено(Организация) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "МЕТКА_Подразделение", "");	
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "МЕТКА_Подразделение", "И ПодразделениеКомпании.Организация = &Организация");
		КонецЕсли;
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "МЕТКА_Подразделение", "И ПодразделениеКомпании = &ПодразделениеКомпании");	
	КонецЕсли;
	
	//Определимся с валютой
	Если ВалютаОтчета Тогда  // Управленческая валюта
		// В валюте рег.учета
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Упр", "Рег");	
	КонецЕсли;

	
	Запрос.УстановитьПараметр("СценарийПланирования",          СценарийПланирования);
	Запрос.УстановитьПараметр("ДатаНачала",                    ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",                     ДатаКонца);
	Запрос.УстановитьПараметр("Организация",                   Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",         ПодразделениеКомпании);
	Запрос.УстановитьПараметр("КоэффициентПересчетаИзУпрВРег", плПолучитьКоэффициентПересчетаВалют(ДатаКонца));
	
	Запрос.УстановитьПараметр("ВидСтатьиВыручка",       Перечисления.ВидыСтатейДоходовИРасходов.ВыручкаОтОсновногоВидаДеятельности);
	Запрос.УстановитьПараметр("ВидСтатьиСебестоимость", Перечисления.ВидыСтатейДоходовИРасходов.Себестоимость);
	Запрос.УстановитьПараметр("ВидСтатьиДоход",         Перечисления.ВидыСтатейДоходовИРасходов.НакладныеДоходы);
	Запрос.УстановитьПараметр("ВидСтатьиРасход",        Перечисления.ВидыСтатейДоходовИРасходов.НакладныеРасходы);

 	РезультатЗапроса = Запрос.Выполнить();
	
	Макет = ПолучитьМакет("Макет"); 	
	
	//Формируем заголовок отчета
	ОблЗаголовок                                 = Макет.ПолучитьОбласть("Заголовок");
	ОблЗаголовок.Параметры.ТекстШапкиОтчета      = "Прогнозный отчет о прибылях и убытках за период: " + ПредставлениеПериода(ДатаНачала, ДатаКонца, "ДФ=dd.MM.yyyy");
	
	Если обЗначениеНеЗаполнено(Организация) Тогда
		Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
			ОблЗаголовок.Параметры.Компания = "По всем организациям";
		Иначе	
			ОблЗаголовок.Параметры.Компания = "" + ПодразделениеКомпании.Организация;
		КонецЕсли;
	Иначе
		ОблЗаголовок.Параметры.Компания = "" + Организация;		
	КонецЕсли;    	
		
	ОблЗаголовок.Параметры.ПодразделениеКомпании = ?(обЗначениеНеЗаполнено(ПодразделениеКомпании), "По всем подразделениям", "" + ПодразделениеКомпании);
	ОблЗаголовок.Параметры.СценарийПланирования  = "" + СценарийПланирования;
		
	Если ВалютаОтчета Тогда
		ТекстВалюты = "В валюте регламентированного учета (" + ?(ЕдиницаИзмерения, "тыс. ", "") + НРег(Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить().Наименование) +  ")";		
	Иначе
		ТекстВалюты = "В валюте управленческого учета ("     + ?(ЕдиницаИзмерения, "тыс. ", "") + НРег(Константы.ВалютаУправленческогоУчетаКомпании.Получить().Наименование) +  ")";		
	КонецЕсли;
	ОблЗаголовок.Параметры.ВалютаОтчета  = "" + ТекстВалюты;
	
	ДокРезультат.Вывести(ОблЗаголовок);
	
	// Формируем шапочку 	
	мсвПредставлениеПериодов = Новый Массив;
	Параметры = Новый Структура;
	Параметры.Вставить("Периодичность", СценарийПланирования.Периодичность);
	
	ВыборкаПериодов = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
	Пока ВыборкаПериодов.Следующий() Цикл
		Параметры.Вставить("ДатаИзПериода", ВыборкаПериодов.Период);
		Представление = плПолучитьДатыПланируемогоПериода(Параметры);
		
		мсвПредставлениеПериодов.Добавить(Новый Структура("Период", Представление) );
	КонецЦикла;
                     	
	Параметры = Новый Структура;
	Параметры.Вставить("Список", мсвПредставлениеПериодов);
	ОблШапка = СформироватьОбласть(Макет, Параметры, "Шапка"); 	
	ДокРезультат.Вывести(ОблШапка);
	
	// Формируем подвал
	Подвал = СформироватьОбласть(Макет, Параметры, "Подвал"); 
        	
	// Основная часть поехала
	Параметры = Новый Структура;
	
	// Сформируем данные по периодам
	Делитель = ?(ЕдиницаИзмерения, 1000, 1);
	
	ВыборкаПериодов.Сбросить();
	мсвЗначения = Новый Массив;
	Пока ВыборкаПериодов.Следующий() Цикл 
		
		ВремСтруктура = Новый Структура;
		ВремСтруктура.Вставить("ВыручкаВсего",                Формат( ?(ВыборкаПериодов.СуммаВыручкаВсего=NULL, 0, ВыборкаПериодов.СуммаВыручкаВсего / Делитель),                   "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		ВремСтруктура.Вставить("ВыручкаОсновнойДеятельности", Формат( ?(ВыборкаПериодов.СуммаВыручка=NULL, 0, ВыборкаПериодов.СуммаВыручка / Делитель),                             "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		ВремСтруктура.Вставить("ВыручкаДополнительная",       Формат( ?(ВыборкаПериодов.СуммаДополнительнаяВыручка=NULL, 0, ВыборкаПериодов.СуммаДополнительнаяВыручка / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		ВремСтруктура.Вставить("СебестоимостьПродаж",         Формат( ?(ВыборкаПериодов.СуммаСебестоимость=NULL, 0, ВыборкаПериодов.СуммаСебестоимость / Делитель),                 "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		ВремСтруктура.Вставить("ВаловаяПрибыль",              Формат( ?(ВыборкаПериодов.ВаловаяПрибыль=NULL, 0, ВыборкаПериодов.ВаловаяПрибыль / Делитель),                         "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		Если ВыборкаПериодов.ПроцентПрибыли=NULL ИЛИ ВыборкаПериодов.ПроцентПрибыли=0 Тогда
			СтрПроцент = "" ;
		Иначе
			СтрПроцент = Формат( ВыборкаПериодов.ПроцентПрибыли,      "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '")  + "%";
		КонецЕсли; 		
		ВремСтруктура.Вставить("ПроцентПрибыли",      СтрПроцент);				
		ВремСтруктура.Вставить("ОперационныеЗатраты", Формат( ?(ВыборкаПериодов.ОперационныеЗатраты=NULL, 0, ВыборкаПериодов.ОперационныеЗатраты / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
        		
		мсвЗначения.Добавить( ВремСтруктура );	     	
	КонецЦикла; 	
	Параметры.Вставить("Список", мсвЗначения);	

	// Добавим итоговую запись (Итоги справа)
	ВыборкаОбщегоИтога = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОБЩИЕ");
	Если ВыборкаОбщегоИтога.Следующий() Тогда
		Параметры.Вставить("ВыручкаВсего",                Формат( ?(ВыборкаОбщегоИтога.СуммаВыручкаВсего=NULL, 0, ВыборкаОбщегоИтога.СуммаВыручкаВсего / Делитель),  "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		Параметры.Вставить("ВыручкаОсновнойДеятельности", Формат( ?(ВыборкаОбщегоИтога.СуммаВыручка=NULL, 0, ВыборкаОбщегоИтога.СуммаВыручка / Делитель) ,       "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		Параметры.Вставить("ВыручкаДополнительная",       Формат( ?(ВыборкаОбщегоИтога.СуммаДополнительнаяВыручка=NULL, 0, ВыборкаОбщегоИтога.СуммаДополнительнаяВыручка / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		Параметры.Вставить("СебестоимостьПродаж",         Формат( ?(ВыборкаОбщегоИтога.СуммаСебестоимость=NULL, 0, ВыборкаОбщегоИтога.СуммаСебестоимость / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		Параметры.Вставить("ВаловаяПрибыль",              Формат( ?(ВыборкаОбщегоИтога.ВаловаяПрибыль=NULL, 0, ВыборкаОбщегоИтога.ВаловаяПрибыль / Делитель),     "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
		Параметры.Вставить("ПроцентПрибыли",              Формат( ВыборкаОбщегоИтога.ПроцентПрибыли      / Делитель,     "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '")  + "%");
		Параметры.Вставить("ОперационныеЗатраты",         Формат( ?(ВыборкаОбщегоИтога.ОперационныеЗатраты=NULL, 0, ВыборкаОбщегоИтога.ОперационныеЗатраты / Делитель),   "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") );
	КонецЕсли;
	ОсновнаяЧасть = СформироватьОбласть(Макет, Параметры, "ОсновнаяЧасть"); 
	ДокРезультат.Вывести(ОсновнаяЧасть);
	
	// Выводим накладные расходы постатейно.
	Если ВыводитьРасходыПоСтатейно Тогда
		// Выполним еще один запрос
		ТекстВрем = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	БюджетДоходовИРасходовОбороты.СтатьяДоходовИРасходов КАК СтатьяРасхода,
		|	БюджетДоходовИРасходовОбороты.Период КАК Период,
		|  	ВЫБОР КОГДА &ИспользоватьРегВалюту ТОГДА
		|		БюджетДоходовИРасходовОбороты.СуммаРасходРегОборот 
		|	ИНАЧЕ 
		|	    БюджетДоходовИРасходовОбороты.СуммаРасходУпрОборот 
		|	КОНЕЦ КАК РасходПоСтатье
		|ИЗ
		|	РегистрНакопления.БюджетДоходовИРасходов.Обороты(
		|		&ДатаНачала,
		|		&ДатаКонца,
		|		Метка_Периодичность,
		|		СценарийПланирования = &СценарийПланирования
		|			И СтатьяДоходовИРасходов.ВидСтатьи = &СтатьяРасход МЕТКА_Подразделение) КАК БюджетДоходовИРасходовОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтатьяРасхода,
		|	Период
		|ИТОГИ 
		|	СУММА(РасходПоСтатье)
		|ПО
		|	СтатьяРасхода,
		|	Период ПЕРИОДАМИ(Метка_Периодичность, &ДатаНачала, &ДатаКонца)";
		
		// Установим нужную периодичность                                                                           		
		ТекстВрем = СтрЗаменить(ТекстВрем, "Метка_Периодичность", Строка(СценарийПланирования.Периодичность));	
		       		
		// Теперь отбор по подразделению
		Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
			Если обЗначениеНеЗаполнено(Организация) Тогда
				ТекстВрем = СтрЗаменить(ТекстВрем, "МЕТКА_Подразделение", "");	
			Иначе
				ТекстВрем = СтрЗаменить(ТекстВрем, "МЕТКА_Подразделение", "И ПодразделениеКомпании.Организация = &Организация");
			КонецЕсли;
		Иначе
			ТекстВрем = СтрЗаменить(ТекстВрем, "МЕТКА_Подразделение", "И ПодразделениеКомпании = &ПодразделениеКомпании");	
		КонецЕсли;  	
		
		ЗапросСтатей = Новый Запрос;
		ЗапросСтатей.Текст = ТекстВрем; 
		ЗапросСтатей.УстановитьПараметр("СценарийПланирования",   СценарийПланирования);
		ЗапросСтатей.УстановитьПараметр("ДатаНачала",             ДатаНачала);
		ЗапросСтатей.УстановитьПараметр("ДатаКонца",              ДатаКонца);
		ЗапросСтатей.УстановитьПараметр("ПодразделениеКомпании",  ПодразделениеКомпании);
		ЗапросСтатей.УстановитьПараметр("ИспользоватьРегВалюту",   ВалютаОтчета);
        ЗапросСтатей.УстановитьПараметр("СтатьяРасход",           Перечисления.ВидыСтатейДоходовИРасходов.НакладныеРасходы);
        ЗапросСтатей.УстановитьПараметр("Организация",            Организация);
				
		ВыборкаСтатей = ЗапросСтатей.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяРасхода");
		Параметры = Новый Структура;
		Пока ВыборкаСтатей.Следующий() Цикл
			Параметры.Вставить("СтатьяРасхода",  ВыборкаСтатей.СтатьяРасхода);
			Параметры.Вставить("РасходПоСтатье", Формат( ?(ВыборкаСтатей.РасходПоСтатье=NULL, 0, ВыборкаСтатей.РасходПоСтатье / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '"));
			
			мсвЗначений = Новый Массив;
			ВыборкаПериодов = ВыборкаСтатей.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
			Пока ВыборкаПериодов.Следующий() Цикл
				мсвЗначений.Добавить( Новый Структура("РасходПоСтатье", Формат( ?(ВыборкаПериодов.РасходПоСтатье=NULL, 0, ВыборкаПериодов.РасходПоСтатье / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '") ));		
			КонецЦикла;
			
			Параметры.Вставить("Список", мсвЗначений);
			
			ОблСтатьиРасходов = СформироватьОбласть(Макет, Параметры, "СтатьяРасхода"); 
			ДокРезультат.Вывести(ОблСтатьиРасходов);			
		КонецЦикла;   		
	КонецЕсли;
		
	// Выводим подвал
	ДокРезультат.Вывести(Подвал);
	ДокРезультат.ФиксацияСлева = 4;	
	
КонецПроцедуры // ПолучитьРазвернутыйОтчет()

// Получает свернутый отчет
//
// Параметры
//	ДокРезультат - ТабличныйДокумент
//
Процедура ПолучитьСвернутыйОтчет(ДокРезультат)
	
	// Выполним запрос
	Запрос       = Новый Запрос;
	Запрос.Текст = ПолучитьМакет("ТекстЗапросаСвернутыйПоУпрВалюте").ПолучитьТекст();
	
	// Теперь отбор по подразделению
	Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
		Если обЗначениеНеЗаполнено(Организация) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "МЕТКА_Подразделение", "");	
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "МЕТКА_Подразделение", "И ПодразделениеКомпании.Организация = &Организация");
		КонецЕсли;
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "МЕТКА_Подразделение", "И ПодразделениеКомпании = &ПодразделениеКомпании");	
	КонецЕсли;   	
	
	//Определимся с валютой
	Если ВалютаОтчета Тогда  // Управленческая валюта
		// В валюте рег.учета
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Упр", "Рег");	
	КонецЕсли;

	
	Запрос.УстановитьПараметр("СценарийПланирования",  СценарийПланирования);
	Запрос.УстановитьПараметр("ДатаНачала",            ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",             ДатаКонца);
	Запрос.УстановитьПараметр("Организация",           Организация);  
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	Запрос.УстановитьПараметр("ВидСтатьиВыручка",       Перечисления.ВидыСтатейДоходовИРасходов.ВыручкаОтОсновногоВидаДеятельности);
	Запрос.УстановитьПараметр("ВидСтатьиСебестоимость", Перечисления.ВидыСтатейДоходовИРасходов.Себестоимость);
	Запрос.УстановитьПараметр("ВидСтатьиДоход",         Перечисления.ВидыСтатейДоходовИРасходов.НакладныеДоходы);
	Запрос.УстановитьПараметр("ВидСтатьиРасход",        Перечисления.ВидыСтатейДоходовИРасходов.НакладныеРасходы);
	
	
 	РезультатЗапросаТекущий = Запрос.Выполнить();
	
	// Прошлый период
	Параметры = Новый Структура;
	Параметры.Вставить("ДатаНачала", ДатаНачала);
	Параметры.Вставить("ДатаКонца",  ДатаКонца);
	Параметры.Вставить("Периодичность",  СценарийПланирования.Периодичность);
	ПолучитьДатыПредыдущегоПериода(Параметры);
		
	Запрос.УстановитьПараметр("ДатаНачала",  Параметры.ДатаНачалаПредыдущая);
	Запрос.УстановитьПараметр("ДатаКонца",   Параметры.ДатаКонцаПредыдущая);

	РезультатЗапросаПредыдущий = Запрос.Выполнить();
	
	Макет = ПолучитьМакет("МакетСвернутыйПоПериодам"); 	
	
	//Формируем заголовок отчета
	ОблЗаголовок                                 = Макет.ПолучитьОбласть("Заголовок");
	ОблЗаголовок.Параметры.ТекстШапкиОтчета      = "Прогнозный отчет о прибылях и убытках за период: " + ПредставлениеПериода(ДатаНачала, ДатаКонца, "ДФ=dd.MM.yyyy");
	
	Если обЗначениеНеЗаполнено(Организация) Тогда
		Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
			ОблЗаголовок.Параметры.Компания = "По всем организациям";
		Иначе	
			ОблЗаголовок.Параметры.Компания = "" + ПодразделениеКомпании.Организация;
		КонецЕсли;
	Иначе
		ОблЗаголовок.Параметры.Компания = "" + Организация;		
	КонецЕсли;
	
	ОблЗаголовок.Параметры.ПодразделениеКомпании = ?(обЗначениеНеЗаполнено(ПодразделениеКомпании), "По всем подразделениям", "" + ПодразделениеКомпании);
	ОблЗаголовок.Параметры.СценарийПланирования  = "" + СценарийПланирования;
	
	Если ВалютаОтчета Тогда
		ТекстВалюты = "В валюте регламентированного учета (" + ?(ЕдиницаИзмерения, "тыс. ", "") + НРег(Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить().Наименование) +  ")";		
	Иначе
		ТекстВалюты = "В валюте управленческого учета ("     + ?(ЕдиницаИзмерения, "тыс. ", "") + НРег(Константы.ВалютаУправленческогоУчетаКомпании.Получить().Наименование) +  ")";		
	КонецЕсли;
	ОблЗаголовок.Параметры.ВалютаОтчета  = "" + ТекстВалюты;
	
	ДокРезультат.Вывести(ОблЗаголовок);
	
	// Формируем шапочку 	
	ДокРезультат.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	// Основная часть поехала  	
	ОсновнаяЧасть = Макет.ПолучитьОбласть("ОсновнаяЧасть");	
	Делитель = ?(ЕдиницаИзмерения, 1000, 1);

	Если НЕ РезультатЗапросаТекущий.Пустой() Тогда
		ВыборкаТекущая = РезультатЗапросаТекущий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОБЩИЕ");
		Если ВыборкаТекущая.Следующий() Тогда  
			ОсновнаяЧасть.Параметры.ВыручкаВсего                = Формат( ?(ВыборкаТекущая.СуммаВыручкаВсего=NULL, 0, ВыборкаТекущая.СуммаВыручкаВсего / Делитель),  "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.ВыручкаОсновнойДеятельности = Формат( ?(ВыборкаТекущая.СуммаВыручка=NULL, 0, ВыборкаТекущая.СуммаВыручка / Делитель) ,       "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.ВыручкаДополнительная       = Формат( ?(ВыборкаТекущая.СуммаДополнительнаяВыручка=NULL, 0, ВыборкаТекущая.СуммаДополнительнаяВыручка / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.СебестоимостьПродаж         = Формат( ?(ВыборкаТекущая.СуммаСебестоимость=NULL, 0, ВыборкаТекущая.СуммаСебестоимость / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.ВаловаяПрибыль              = Формат( ?(ВыборкаТекущая.ВаловаяПрибыль=NULL, 0, ВыборкаТекущая.ВаловаяПрибыль / Делитель),     "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			
			Если ВыборкаТекущая.ПроцентПрибыли=NULL ИЛИ ВыборкаТекущая.ПроцентПрибыли=0 Тогда
				СтрПроцент = "" ;
			Иначе
				СтрПроцент = Формат( ВыборкаТекущая.ПроцентПрибыли, "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '")  + "%";				
			КонецЕсли; 
			
			ОсновнаяЧасть.Параметры.ПроцентПрибыли      = СтрПроцент;			
			ОсновнаяЧасть.Параметры.ОперационныеЗатраты = Формат( ?(ВыборкаТекущая.ОперационныеЗатраты=NULL, 0, ВыборкаТекущая.ОперационныеЗатраты / Делитель),   "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
		КонецЕсли;            		
	КонецЕсли;
	
	Если Не РезультатЗапросаПредыдущий.Пустой() Тогда
		ВыборкаПредыдущая  = РезультатЗапросаПредыдущий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОБЩИЕ");
		Если ВыборкаПредыдущая.Следующий() Тогда
			ОсновнаяЧасть.Параметры.ВыручкаВсегоПрошлая                = Формат( ?(ВыборкаПредыдущая.СуммаВыручкаВсего=NULL, 0, ВыборкаПредыдущая.СуммаВыручкаВсего / Делитель),  "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.ВыручкаОсновнойДеятельностиПрошлая = Формат( ?(ВыборкаПредыдущая.СуммаВыручка=NULL, 0, ВыборкаПредыдущая.СуммаВыручка / Делитель) ,       "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.ВыручкаДополнительнаяПрошлая       = Формат( ?(ВыборкаПредыдущая.СуммаДополнительнаяВыручка=NULL, 0, ВыборкаПредыдущая.СуммаДополнительнаяВыручка / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.СебестоимостьПродажПрошлая         = Формат( ?(ВыборкаПредыдущая.СуммаСебестоимость=NULL, 0, ВыборкаПредыдущая.СуммаСебестоимость / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОсновнаяЧасть.Параметры.ВаловаяПрибыльПрошлая              = Формат( ?(ВыборкаПредыдущая.ВаловаяПрибыль=NULL, 0, ВыборкаПредыдущая.ВаловаяПрибыль/ Делитель),     "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			
			Если ВыборкаПредыдущая.ПроцентПрибыли=NULL ИЛИ ВыборкаПредыдущая.ПроцентПрибыли=0 Тогда
				СтрПроцент = "" ;
			Иначе
				СтрПроцент = Формат( ВыборкаПредыдущая.ПроцентПрибыли, "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '")  + "%";				
			КонецЕсли; 
			
			ОсновнаяЧасть.Параметры.ПроцентПрибылиПрошлая      = СтрПроцент;	
			ОсновнаяЧасть.Параметры.ОперационныеЗатратыПрошлая = Формат( ?(ВыборкаПредыдущая.ОперационныеЗатраты=NULL, 0, ВыборкаПредыдущая.ОперационныеЗатраты / Делитель),   "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
		КонецЕсли;
	КонецЕсли;
	
	ДокРезультат.Вывести(ОсновнаяЧасть);
	                               	
	// Выводим накладные расходы постатейно.
	Если ВыводитьРасходыПоСтатейно Тогда
		// Выполним еще один запрос
		ТекстВрем = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(БюджетДоходовИРасходовОборотыТекПериод.СтатьяДоходовИРасходов, БюджетДоходовИРасходовОборотыПредПериод.СтатьяДоходовИРасходов) КАК СтатьяРасхода,
		|  	ВЫБОР КОГДА &ИспользоватьРегВалюту ТОГДА
		|		ЕСТЬNULL(БюджетДоходовИРасходовОборотыТекПериод.СуммаРасходРегОборот, 0) 
		|	ИНАЧЕ 
		|	    ЕСТЬNULL(БюджетДоходовИРасходовОборотыТекПериод.СуммаРасходУпрОборот, 0)
		|	КОНЕЦ КАК РасходПоСтатье, 
		
		|	ВЫБОР КОГДА &ИспользоватьРегВалюту ТОГДА
		|		ЕСТЬNULL(БюджетДоходовИРасходовОборотыПредПериод.СуммаРасходРегОборот, 0) 
		|	ИНАЧЕ 
		|	    ЕСТЬNULL(БюджетДоходовИРасходовОборотыПредПериод.СуммаРасходУпрОборот, 0) 
		|	КОНЕЦ КАК РасходПоСтатьеПрошлая
		
		|ИЗ
		|	РегистрНакопления.БюджетДоходовИРасходов.Обороты(
		|		&ДатаНачала,
		|		&ДатаКонца,
		|		Метка_Периодичность,
		|		СценарийПланирования = &СценарийПланирования
		|			И СтатьяДоходовИРасходов.ВидСтатьи = &СтатьяРасход МЕТКА_Подразделение) КАК БюджетДоходовИРасходовОборотыТекПериод
		|
		|ПОЛНОЕ СОЕДИНЕНИЕ 
		|
		|РегистрНакопления.БюджетДоходовИРасходов.Обороты(
		|		&ДатаНачала2,
		|		&ДатаКонца2,
		|		Метка_Периодичность,
		|		СценарийПланирования = &СценарийПланирования
		|			И СтатьяДоходовИРасходов.ВидСтатьи = &СтатьяРасход МЕТКА_Подразделение) КАК БюджетДоходовИРасходовОборотыПредПериод
		|	ПО БюджетДоходовИРасходовОборотыТекПериод.СтатьяДоходовИРасходов = БюджетДоходовИРасходовОборотыПредПериод.СтатьяДоходовИРасходов
		
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтатьяРасхода
		|ИТОГИ 
		|	СУММА(РасходПоСтатье),
		|	СУММА(РасходПоСтатьеПрошлая)
		|ПО
		|	СтатьяРасхода";
		
		// Установим нужную периодичность                                                                           		
		ТекстВрем = СтрЗаменить(ТекстВрем, "Метка_Периодичность", Строка(СценарийПланирования.Периодичность));	
			   		
		// Теперь отбор по подразделению
		Если обЗначениеНеЗаполнено(ПодразделениеКомпании) Тогда
			Если обЗначениеНеЗаполнено(Организация) Тогда
				ТекстВрем = СтрЗаменить(ТекстВрем, "МЕТКА_Подразделение", "");	
			Иначе
				ТекстВрем = СтрЗаменить(ТекстВрем, "МЕТКА_Подразделение", "И ПодразделениеКомпании.Организация = &Организация");
			КонецЕсли;
		Иначе
			ТекстВрем = СтрЗаменить(ТекстВрем, "МЕТКА_Подразделение", "И ПодразделениеКомпании = &ПодразделениеКомпании");	
		КонецЕсли;
		
		ЗапросСтатей = Новый Запрос;
		ЗапросСтатей.Текст = ТекстВрем; 
		ЗапросСтатей.УстановитьПараметр("СценарийПланирования", СценарийПланирования);
		ЗапросСтатей.УстановитьПараметр("ДатаНачала",           ДатаНачала);
		ЗапросСтатей.УстановитьПараметр("ДатаКонца",            ДатаКонца);
		ЗапросСтатей.УстановитьПараметр("СтатьяРасход",         Перечисления.ВидыСтатейДоходовИРасходов.НакладныеРасходы);
		
		Параметры = Новый Структура;
		Параметры.Вставить("ДатаНачала", ДатаНачала);
		Параметры.Вставить("ДатаКонца",  ДатаКонца);
		Параметры.Вставить("Периодичность",  СценарийПланирования.Периодичность);
		ПолучитьДатыПредыдущегоПериода(Параметры);

		ЗапросСтатей.УстановитьПараметр("ДатаНачала2", Параметры.ДатаНачалаПредыдущая);
		ЗапросСтатей.УстановитьПараметр("ДатаКонца2",  Параметры.ДатаКонцаПредыдущая);
		
		ЗапросСтатей.УстановитьПараметр("ПодразделениеКомпании",  ПодразделениеКомпании);
		ЗапросСтатей.УстановитьПараметр("ИспользоватьРегВалюту",   ВалютаОтчета);
        ЗапросСтатей.УстановитьПараметр("Организация",			  Организация);
				
		ВыборкаСтатей = ЗапросСтатей.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяРасхода");
		Параметры = Новый Структура;
		Пока ВыборкаСтатей.Следующий() Цикл
			ОблСтатьиРасходов = Макет.ПолучитьОбласть("СтатьяРасхода");
			ОблСтатьиРасходов.Параметры.СтатьяРасхода  = ВыборкаСтатей.СтатьяРасхода;
			ОблСтатьиРасходов.Параметры.РасходПоСтатье        = Формат( ?(ВыборкаСтатей.РасходПоСтатье=NULL, 0, ВыборкаСтатей.РасходПоСтатье / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			ОблСтатьиРасходов.Параметры.РасходПоСтатьеПрошлая = Формат( ?(ВыборкаСтатей.РасходПоСтатьеПрошлая=NULL, 0, ВыборкаСтатей.РасходПоСтатьеПрошлая / Делитель), "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧРГ=' '");
			
			ДокРезультат.Вывести(ОблСтатьиРасходов);			
		КонецЦикла;   		
	КонецЕсли;
		
	// Выводим подвал
	
	ДокРезультат.Вывести(Макет.ПолучитьОбласть("Подвал"));
	
КонецПроцедуры // ПолучитьСвернутыйОтчет()

// Получает даты предыдущего периода
//
// Параметры
//	Параметры - Структура
//
Процедура ПолучитьДатыПредыдущегоПериода(Параметры)
	
	ТекПериодНачало = НачалоМесяца(Параметры.ДатаНачала);
	ТекПериодКонец  = КонецМесяца(Параметры.ДатаКонца);
	Периодичность   = Параметры.Периодичность;
	
	Запрос = Новый Запрос;
	
	Если Периодичность=Перечисления.ПериодичностьПланирования.Месяц Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДОБАВИТЬКДАТЕ(&ДатаНачала, МЕСЯЦ, -(РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, МЕСЯЦ)+1)) КАК ДатаНачалаПредыдущая,
		|	ДОБАВИТЬКДАТЕ(&ДатаКонца, МЕСЯЦ,  -(РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, МЕСЯЦ)+1)) КАК ДатаКонцаПредыдущая";
		
	ИначеЕсли Периодичность=Перечисления.ПериодичностьПланирования.Квартал Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДОБАВИТЬКДАТЕ(&ДатаНачала, КВАРТАЛ, -(РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, МЕСЯЦ)+1)) КАК ДатаНачалаПредыдущая,
		|	ДОБАВИТЬКДАТЕ(&ДатаКонца, КВАРТАЛ, -(РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, МЕСЯЦ)+1)) КАК ДатаКонцаПредыдущая";
		
	ИначеЕсли Периодичность=Перечисления.ПериодичностьПланирования.Год Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДОБАВИТЬКДАТЕ(&ДатаНачала, ГОД, -(РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, МЕСЯЦ)+1)) КАК ДатаНачалаПредыдущая,
		|	ДОБАВИТЬКДАТЕ(&ДатаКонца, ГОД,  -(РАЗНОСТЬДАТ(&ДатаНачала, &ДатаКонца, МЕСЯЦ)+1)) КАК ДатаКонцаПредыдущая";

	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаНачала", ТекПериодНачало);
	Запрос.УстановитьПараметр("ДатаКонца",  ТекПериодКонец);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Параметры.Вставить("ДатаНачалаПредыдущая", Выборка.ДатаНачалаПредыдущая);
		Параметры.Вставить("ДатаКонцаПредыдущая",  Выборка.ДатаКонцаПредыдущая);
		
	Иначе
		Параметры.Вставить("ДатаНачалаПредыдущая", ТекПериодНачало);
		Параметры.Вставить("ДатаКонцаПредыдущая",  ТекПериодКонец);
		
	КонецЕсли;
	
КонецПроцедуры // ПолучитьДатыПредыдущегоПериода()

// Формирует область
//
// Параметры
//	Макет - ТабличныйДокумент или ТекстовыйДокумент
//	Параметры - Структура
//	ИмяОбласти - Строка
Функция СформироватьОбласть(Знач Макет, Параметры, ИмяОбласти)
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	ТекОбл            = Макет.ПолучитьОбласть(ИмяОбласти + "|Начало");
	
	// Заполним параметры
	ЗаполнитьЗначенияСвойств(ТекОбл.Параметры, Параметры);
	
	ТабличныйДокумент.Вывести(ТекОбл);
	
	// Теперь повторяющиеся пошли
	Если Параметры.Свойство("Список") Тогда
		Для каждого ТекЭлем Из Параметры.Список Цикл
			ТекОбл = Макет.ПолучитьОбласть(ИмяОбласти + "|Период");
			ЗаполнитьЗначенияСвойств(ТекОбл.Параметры, ТекЭлем);
			ТабличныйДокумент.Присоединить(ТекОбл);      
		КонецЦикла;		
	КонецЕсли;
	
	// Итоговую колонку добавим
	ТекОбл = Макет.ПолучитьОбласть(ИмяОбласти + "|ИтогоКолонка");
	ЗаполнитьЗначенияСвойств(ТекОбл.Параметры, Параметры);
	ТабличныйДокумент.Присоединить(ТекОбл);      
	
	// Границу жирной сделаем
	ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть(ИмяОбласти + "|Конец"));
	
	Возврат ТабличныйДокумент;
КонецФункции // СформироватьОбласть()


//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

#КонецЕсли
ИмяРегистра = Неопределено;

Функция ПолучитьСвойства() Экспорт
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("Модель");
	СтруктураСвойств.Вставить("Сотрудник");
	СтруктураСвойств.Вставить("Реклама");
	
	Возврат СтруктураСвойств;
	
КонецФункции

// Стандартная процедура настройки схемы компоновщика данных
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

Функция ПолучитьМинимальныйПериод(СтруктураОтчета, МассивПериодов, Индекс)
	
	Результат = Неопределено;
	Для н=0 По Индекс Цикл
		ТекЭлемент = МассивПериодов[н];
		Если ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре("Периодичность."+ТекЭлемент, СтруктураОтчета) Тогда
			Результат = СтрЗаменить(ТекЭлемент, "Период", "");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Результат = "Неделя" Тогда
		Результат = "День"
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	НастройкиОтчета = КомпоновщикНастроек.Настройки;
	
	ДатаНачала      = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаНачала",    НастройкиОтчета);
	ДатаКонца       = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", НастройкиОтчета);
	
	СтруктураОтчета = НастройкиОтчета.Структура;
	
	МинимальныйПериод = 0;
	
	МассивПериодов = Новый Массив;
	МассивПериодов.Добавить("ПериодЧас");
	МассивПериодов.Добавить("ПериодДень");
	МассивПериодов.Добавить("ПериодНеделя");
	МассивПериодов.Добавить("ПериодМесяц");
	
	Интервал = 0;
	ИмяПериода = "";
	Если ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре("НомераПериодов.ПоЧасам", СтруктураОтчета) Тогда
		ИмяПериода = "Час";
	ИначеЕсли ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре("НомераПериодов.ПоДнямНедели", СтруктураОтчета) Тогда
		ИмяПериода = ПолучитьМинимальныйПериод(СтруктураОтчета, МассивПериодов, 0);
		Если ИмяПериода = Неопределено Тогда
			ИмяПериода = "День";
		КонецЕсли;
	ИначеЕсли ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре("НомераПериодов.ПоДням", СтруктураОтчета) Тогда
		ИмяПериода = ПолучитьМинимальныйПериод(СтруктураОтчета, МассивПериодов, 0);
		Если ИмяПериода = Неопределено Тогда
			ИмяПериода = "День";
		КонецЕсли;
	ИначеЕсли ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре("НомераПериодов.ПоМесяцам", СтруктураОтчета) Тогда
		ИмяПериода = ПолучитьМинимальныйПериод(СтруктураОтчета, МассивПериодов, 2);
		Если ИмяПериода = Неопределено Тогда
			ИмяПериода = "Месяц";
		КонецЕсли;
	ИначеЕсли ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре("НомераПериодов.ПоКварталам", СтруктураОтчета) Тогда
		ИмяПериода = ПолучитьМинимальныйПериод(СтруктураОтчета, МассивПериодов, 3);
		Если ИмяПериода = Неопределено Тогда
			ИмяПериода = "Квартал";
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяПериода = "Час" Тогда
		Интервал = Окр(((КонецДня(ДатаКонца) - НачалоДня(ДатаНачала))/3600)+0.49, 0, 1);
	ИначеЕсли ИмяПериода = "День" Тогда
		Интервал = Окр(((КонецДня(ДатаКонца) - НачалоДня(ДатаНачала))/86400)+0.49, 0, 1);
	ИначеЕсли ИмяПериода = "Месяц" Тогда
		Интервал = Цел(12*(Год(КонецДня(ДатаКонца)) - Год(НачалоДня(ДатаНачала))) + (Месяц(КонецДня(ДатаКонца)) - Месяц(НачалоДня(ДатаНачала))))+1;
	ИначеЕсли ИмяПериода = "Квартал" Тогда
		Интервал = Цел(4*(Год(КонецДня(ДатаКонца)) - Год(НачалоДня(ДатаНачала))) + Окр(Месяц(КонецДня(ДатаКонца)) - Месяц(НачалоДня(ДатаНачала))+0.49, 0, 1))+1;
	КонецЕсли;
	
	Если Интервал=0 Тогда
		ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
		Возврат;
	КонецЕсли;
		
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, СхемаКомпоновкиДанных, "dataCompositionScheme", "http://v8.1c.ru/8.1/data-composition-system/scheme");
	ТекстЗапроса = ЗаписьXML.Закрыть();
	
	ДлинаПорядка = СтрДлина(Формат(СокрЛП(Интервал), "ЧН=0; ЧГ="));
	
	ТекстПериода = "";
	ТекстТаблиц  = "";
	ЗначениеПорядка = 10;
	Пока ДлинаПорядка>1 Цикл
		ТекстПорядка = Формат(ЗначениеПорядка, "ЧН=0; ЧГ=");
		ТекстПериода = ТекстПериода + " (ТаблицаДесятков"+ТекстПорядка+".Номер*"+ТекстПорядка+") + ";
		ТекстТаблиц  = ТекстТаблиц  + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ТаблицаНомеров КАК ТаблицаДесятков"+ТекстПорядка+"
		|ПО
		|	ИСТИНА";
		ЗначениеПорядка = ЗначениеПорядка * 10;
		ДлинаПорядка    = ДлинаПорядка - 1;
	КонецЦикла;
	
	ТекстЗамены = "
	|ВЫБРАТЬ
	|	0 КАК Номер
	|ПОМЕСТИТЬ
	|	ТаблицаНомеров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9
	|;
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаНачала, "+ИмяПериода+", "+ТекстПериода+" ТаблицаНомеров.Номер), "+ИмяПериода+") КАК Период
	|ПОМЕСТИТЬ
	|	ТаблицаПериодов
	|ИЗ
	|	ТаблицаНомеров КАК ТаблицаНомеров
	|"+ТекстТаблиц+"
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаНачала, "+ИмяПериода+", "+ТекстПериода+" ТаблицаНомеров.Номер), "+ИмяПериода+") <= &ДатаОкончания
	|;
	|";
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстЗапроса);
	КопияСхемы = СериализаторXDTO.ПрочитатьXML(ЧтениеXML, Тип("СхемаКомпоновкиДанных"));
	
	КопияСхемы.НаборыДанных.ТекстЗапроса.Запрос = ТекстЗамены + "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Итоговая.Автор КАК Автор,
	|	Итоговая.Сотрудник КАК Сотрудник,
	|	Итоговая.Клиент КАК Клиент,
	|	Итоговая.Организация КАК Организация,
	|	Итоговая.Подразделение КАК Подразделение,
	|	Итоговая.Модель КАК Модель,
	|	Итоговая.ВидКонтакта КАК ВидКонтакта,
	|	Итоговая.Реклама КАК Реклама,
	|	Итоговая.ВторичныйКонтакт КАК ВторичныйКонтакт,
	|	Итоговая.ЮридическоеЛицо КАК ЮридическоеЛицо,
	|	Итоговая.Примечание КАК Примечание,
	|	Итоговая.ПредметОбращения КАК ПредметОбращения,
	|	Итоговая.ПредставлениеТелефона КАК ПредставлениеТелефона,
	|	Итоговая.Участник КАК Участник,
	|	Итоговая.Период КАК ПериодЧас,
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, ДЕНЬ) КАК ПериодДень,
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, НЕДЕЛЯ) КАК ПериодНеделя,
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, МЕСЯЦ) КАК ПериодМесяц,
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, КВАРТАЛ) КАК ПериодКвартал,
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, ГОД) КАК ПериодГод,
	|	ЧАС(Итоговая.Период) КАК ПоЧасам,
	|	ДЕНЬНЕДЕЛИ(Итоговая.Период) КАК ПоДнямНедели,
	|	ДЕНЬГОДА(Итоговая.Период) КАК ПоДням,
	|	ДОБАВИТЬКДАТЕ(ДатаВремя(2000, 1, 1), МЕСЯЦ, МЕСЯЦ(Итоговая.Период)-1) КАК ПоМесяцам,
	|	КВАРТАЛ(Итоговая.Период) КАК ПоКварталам,
	|	СУММА(Итоговая.Количество) КАК Количество,
	|	СРЕДНЕЕ(Итоговая.СреднееКоличество) КАК СреднееКоличество
	|{ВЫБРАТЬ
	|	Автор.* КАК Автор,
	|	Модель.* КАК Модель,
	|	Сотрудник.* КАК Сотрудник,
	|	ВидКонтакта.* КАК ВидКонтакта,
	|	Реклама.* КАК Реклама,
	|	Примечание,
	|	ВторичныйКонтакт,
	|	ЮридическоеЛицо КАК ЮридическоеЛицо,
	|	Количество КАК Количество,
	|	Организация.*,
	|	Подразделение.*,
	|	ПредметОбращения.*,
	|	Клиент.*,
	|	ПредставлениеТелефона,
	|	Участник.*,
	|	ПериодЧас,
	|	ПериодДень,
	|	ПериодНеделя,
	|	ПериодМесяц,
	|	ПериодКвартал,
	|	ПериодГод,
	|	ПоЧасам,
	|	ПоДням,
	|	ПоМесяцам,
	|	ПоКварталам,
	|	ПоДнямНедели,
	|	СреднееКоличество
	|	}
	|ИЗ (
	|ВЫБРАТЬ
	|	Вложенная.Автор,
	|	Вложенная.Сотрудник КАК Сотрудник,
	|	Вложенная.Клиент КАК Клиент,
	|	Вложенная.Организация КАК Организация,
	|	Вложенная.Подразделение КАК Подразделение,
	|	Вложенная.Модель КАК Модель,
	|	Вложенная.ВидКонтакта КАК ВидКонтакта,
	|	Вложенная.Реклама КАК Реклама,
	|	Вложенная.ВторичныйКонтакт КАК ВторичныйКонтакт,
	|	Вложенная.ЮридическоеЛицо КАК ЮридическоеЛицо,
	|	Вложенная.Примечание КАК Примечание,
	|	Вложенная.ПредметОбращения КАК ПредметОбращения,
	|	Вложенная.ПредставлениеТелефона КАК ПредставлениеТелефона,
	|	Вложенная.Участник КАК Участник,
	|	Вложенная.Период КАК Период,
	|	МАКСИМУМ(Вложенная.Количество) КАК Количество,
	|	МАКСИМУМ(Вложенная.СреднееКоличество) КАК СреднееКоличество
	|{ВЫБРАТЬ
	|	Автор.* КАК Автор,
	|	Модель.* КАК Модель,
	|	Сотрудник.* КАК Сотрудник,
	|	ВидКонтакта.* КАК ВидКонтакта,
	|	Реклама.* КАК Реклама,
	|	Примечание,
	|	ВторичныйКонтакт,
	|	ЮридическоеЛицо КАК ЮридическоеЛицо,
	|	Количество КАК Количество,
	|	Организация.*,
	|	Подразделение.*,
	|	ПредметОбращения.*,
	|	Клиент.*,
	|	ПредставлениеТелефона,
	|	Участник.*,
	|	СреднееКоличество
	|	}
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияЗвонковИКонтактов.Автор,
	|		РегистрацияЗвонковИКонтактов.Сотрудник КАК Сотрудник,
	|		РегистрацияЗвонковИКонтактов.Клиент КАК Клиент,
	|		РегистрацияЗвонковИКонтактов.Организация КАК Организация,
	|		РегистрацияЗвонковИКонтактов.Подразделение КАК Подразделение,
	|		РегистрацияЗвонковИКонтактов.Модель КАК Модель,
	|		РегистрацияЗвонковИКонтактов.ВидКонтакта КАК ВидКонтакта,
	|		РегистрацияЗвонковИКонтактов.Реклама КАК Реклама,
	|		РегистрацияЗвонковИКонтактов.ВторичныйКонтакт КАК ВторичныйКонтакт,
	|		РегистрацияЗвонковИКонтактов.ЮридическоеЛицо КАК ЮридическоеЛицо,
	|		РегистрацияЗвонковИКонтактов.Примечание КАК Примечание,
	|		РегистрацияЗвонковИКонтактов.ПредметОбращения КАК ПредметОбращения,
	|		РегистрацияЗвонковИКонтактов.ПредставлениеТелефона КАК ПредставлениеТелефона,
	|		РегистрацияЗвонковИКонтактов.Участник КАК Участник," + ?(ИмяПериода = "Час", "
	|		ДОБАВИТЬКДАТЕ(РегистрацияЗвонковИКонтактов.ДатаДень, ЧАС, ЧАС(РегистрацияЗвонковИКонтактов.ДатаВремя))", "
	|		НАЧАЛОПЕРИОДА(РегистрацияЗвонковИКонтактов.ДатаДень, "+ИмяПериода+")") + " КАК Период,
	|		СУММА(1) КАК Количество,
	|		СУММА(1) КАК СреднееКоличество
	|	{ВЫБРАТЬ
	|		Автор.* КАК Автор,
	|		Модель.* КАК Модель,
	|		Сотрудник.* КАК Сотрудник,
	|		ВидКонтакта.* КАК ВидКонтакта,
	|		Реклама.* КАК Реклама,
	|		Примечание,
	|		ВторичныйКонтакт,
	|		ЮридическоеЛицо КАК ЮридическоеЛицо,
	|		Количество КАК Количество,
	|		Организация.*,
	|		Подразделение.*,
	|		ПредметОбращения.*,
	|		Клиент.*,
	|		ПредставлениеТелефона,
	|		Участник.*,
	|		СреднееКоличество
	|		}
	|	ИЗ
	|		РегистрСведений.РегистрацияЗвонковИКонтактов КАК РегистрацияЗвонковИКонтактов
	|	ГДЕ
	|		РегистрацияЗвонковИКонтактов.ДатаДень МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|	СГРУППИРОВАТЬ ПО
	|		РегистрацияЗвонковИКонтактов.Автор,
	|		РегистрацияЗвонковИКонтактов.Сотрудник,
	|		РегистрацияЗвонковИКонтактов.Клиент,
	|		РегистрацияЗвонковИКонтактов.Организация,
	|		РегистрацияЗвонковИКонтактов.Подразделение,
	|		РегистрацияЗвонковИКонтактов.Модель,
	|		РегистрацияЗвонковИКонтактов.ВидКонтакта,
	|		РегистрацияЗвонковИКонтактов.Реклама,
	|		РегистрацияЗвонковИКонтактов.ВторичныйКонтакт,
	|		РегистрацияЗвонковИКонтактов.ЮридическоеЛицо,
	|		РегистрацияЗвонковИКонтактов.Примечание,
	|		РегистрацияЗвонковИКонтактов.ПредметОбращения,
	|		РегистрацияЗвонковИКонтактов.ПредставлениеТелефона,
	|		РегистрацияЗвонковИКонтактов.Участник," + ?(ИмяПериода = "Час", "
	|		ДОБАВИТЬКДАТЕ(РегистрацияЗвонковИКонтактов.ДатаДень, ЧАС, ЧАС(РегистрацияЗвонковИКонтактов.ДатаВремя))", "
	|		НАЧАЛОПЕРИОДА(РегистрацияЗвонковИКонтактов.ДатаДень, "+ИмяПериода+")") + "
	|	{ГДЕ
	|		РегистрацияЗвонковИКонтактов.Автор.*,
	|		РегистрацияЗвонковИКонтактов.Сотрудник.*,
	|		РегистрацияЗвонковИКонтактов.Клиент.*,
	|		РегистрацияЗвонковИКонтактов.Организация.*,
	|		РегистрацияЗвонковИКонтактов.Подразделение.*,
	|		РегистрацияЗвонковИКонтактов.Модель.*,
	|		РегистрацияЗвонковИКонтактов.ВидКонтакта.*,
	|		РегистрацияЗвонковИКонтактов.Реклама.*,
	|		РегистрацияЗвонковИКонтактов.ВторичныйКонтакт,
	|		РегистрацияЗвонковИКонтактов.ЮридическоеЛицо,
	|		РегистрацияЗвонковИКонтактов.Примечание,
	|		РегистрацияЗвонковИКонтактов.ПредметОбращения.*,
	|		РегистрацияЗвонковИКонтактов.ПредставлениеТелефона,
	|		РегистрацияЗвонковИКонтактов.Участник.*}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РегистрацияЗвонковИКонтактов.Автор,
	|		РегистрацияЗвонковИКонтактов.Сотрудник КАК Сотрудник,
	|		РегистрацияЗвонковИКонтактов.Клиент КАК Клиент,
	|		РегистрацияЗвонковИКонтактов.Организация КАК Организация,
	|		РегистрацияЗвонковИКонтактов.Подразделение КАК Подразделение,
	|		РегистрацияЗвонковИКонтактов.Модель КАК Модель,
	|		РегистрацияЗвонковИКонтактов.ВидКонтакта КАК ВидКонтакта,
	|		РегистрацияЗвонковИКонтактов.Реклама КАК Реклама,
	|		РегистрацияЗвонковИКонтактов.ВторичныйКонтакт КАК ВторичныйКонтакт,
	|		РегистрацияЗвонковИКонтактов.ЮридическоеЛицо КАК ЮридическоеЛицо,
	|		РегистрацияЗвонковИКонтактов.Примечание КАК Примечание,
	|		РегистрацияЗвонковИКонтактов.ПредметОбращения КАК ПредметОбращения,
	|		РегистрацияЗвонковИКонтактов.ПредставлениеТелефона КАК ПредставлениеТелефона,
	|		РегистрацияЗвонковИКонтактов.Участник КАК Участник," + ?(ИмяПериода = "Час", "
	|		ДОБАВИТЬКДАТЕ(ТаблицаПериодов.Период, ЧАС, ЧАС(РегистрацияЗвонковИКонтактов.ДатаВремя))", "
	|		ТаблицаПериодов.Период") + ",
	|		0 КАК Количество,
	|		0 КАК СреднееКоличество
	|	ИЗ
	|		РегистрСведений.РегистрацияЗвонковИКонтактов КАК РегистрацияЗвонковИКонтактов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаПериодов КАК ТаблицаПериодов
	|	ПО
	|		ИСТИНА
	|	ГДЕ
	|		РегистрацияЗвонковИКонтактов.ДатаДень МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	{ГДЕ
	|		РегистрацияЗвонковИКонтактов.Автор.*,
	|		РегистрацияЗвонковИКонтактов.Сотрудник.*,
	|		РегистрацияЗвонковИКонтактов.Клиент.*,
	|		РегистрацияЗвонковИКонтактов.Организация.*,
	|		РегистрацияЗвонковИКонтактов.Подразделение.*,
	|		РегистрацияЗвонковИКонтактов.Модель.*,
	|		РегистрацияЗвонковИКонтактов.ВидКонтакта.*,
	|		РегистрацияЗвонковИКонтактов.Реклама.*,
	|		РегистрацияЗвонковИКонтактов.ВторичныйКонтакт,
	|		РегистрацияЗвонковИКонтактов.ЮридическоеЛицо,
	|		РегистрацияЗвонковИКонтактов.Примечание,
	|		РегистрацияЗвонковИКонтактов.ПредметОбращения.*,
	|		РегистрацияЗвонковИКонтактов.ПредставлениеТелефона,
	|		РегистрацияЗвонковИКонтактов.Участник.*}) КАК Вложенная
	|СГРУППИРОВАТЬ ПО
	|	Вложенная.Автор,
	|	Вложенная.Сотрудник,
	|	Вложенная.Клиент,
	|	Вложенная.Организация,
	|	Вложенная.Подразделение,
	|	Вложенная.Модель,
	|	Вложенная.ВидКонтакта,
	|	Вложенная.Реклама,
	|	Вложенная.ВторичныйКонтакт,
	|	Вложенная.ЮридическоеЛицо,
	|	Вложенная.Примечание,
	|	Вложенная.ПредметОбращения,
	|	Вложенная.ПредставлениеТелефона,
	|	Вложенная.Участник,
	|	Вложенная.Период) КАК Итоговая
	|СГРУППИРОВАТЬ ПО
	|	Итоговая.Автор,
	|	Итоговая.Сотрудник,
	|	Итоговая.Клиент,
	|	Итоговая.Организация,
	|	Итоговая.Подразделение,
	|	Итоговая.Модель,
	|	Итоговая.ВидКонтакта,
	|	Итоговая.Реклама,
	|	Итоговая.ВторичныйКонтакт,
	|	Итоговая.ЮридическоеЛицо,
	|	Итоговая.Примечание,
	|	Итоговая.ПредметОбращения,
	|	Итоговая.ПредставлениеТелефона,
	|	Итоговая.Участник,
	|	Итоговая.Период,
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, ДЕНЬ),
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, НЕДЕЛЯ),
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, КВАРТАЛ),
	|	НАЧАЛОПЕРИОДА(Итоговая.Период, ГОД),
	|	ЧАС(Итоговая.Период),
	|	ДЕНЬНЕДЕЛИ(Итоговая.Период),
	|	ДЕНЬГОДА(Итоговая.Период),
	|	ДОБАВИТЬКДАТЕ(ДатаВремя(2000, 1, 1), МЕСЯЦ, МЕСЯЦ(Итоговая.Период)-1),
	|	КВАРТАЛ(Итоговая.Период)";
	
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, КопияСхемы, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

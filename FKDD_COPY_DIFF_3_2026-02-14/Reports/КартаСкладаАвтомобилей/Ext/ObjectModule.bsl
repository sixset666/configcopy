#Если Клиент Тогда
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем СписокЦветов Экспорт; // список цветов
Перем ОбъектСклад Экспорт; // в переменную передается интерактивно открытый объект справочника Места хранения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Сохранение макета карты склада в реквизит типа ХранилищеЗначений
// 
// Параметры
//   ГеографическаяСхема - ГеографическаяСхема
//
Процедура СохранитьМакет(ГеографическаяСхема)Экспорт 
	
	ИмяТемпФайла = ПолучитьИмяВременногоФайла();	
	ГеографическаяСхема.Записать(ИмяТемпФайла);	
	ДвоичныеДанные = Новый  ДвоичныеДанные(ИмяТемпФайла);
	Если ОбъектСклад = Неопределено Тогда
		НовыйОбъектСклад =  Склад.ПолучитьОбъект();
		НовыйОбъектСклад.КартаСклада = Новый ХранилищеЗначения(ДвоичныеДанные,Новый СжатиеДанных(5));
	Иначе
		ОбъектСклад.КартаСклада = Новый ХранилищеЗначения(ДвоичныеДанные,Новый СжатиеДанных(5));
	КонецЕсли;
	УдалитьФайлы(ИмяТемпФайла);
	
	Если ОбъектСклад = Неопределено  Тогда
		Попытка
			НовыйОбъектСклад.Записать();
		Исключение
			Сообщить("Невозможно сохранить макет карты склада!" + "Необходимо записать "+ Строка(ОбъектСклад)+" и повторить действие.");
		КонецПопытки;
	Иначе
		Попытка
			ОбъектСклад.Записать();
		Исключение
			Сообщить("Невозможно сохранить макет карты склада!" + "Необходимо записать "+ Строка(ОбъектСклад)+" и повторить действие.");
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры // СохранитьМакет()

// Чтение макета склада из реквизита типа  ХранилищеЗначений
// 
// Параметры
//   ГеографическаяСхема - ГеографическаяСхема
//
Процедура ПрочитатьМакет(ГеографическаяСхема) Экспорт
	
	ИмяТемпФайла = ПолучитьИмяВременногоФайла();
	Склад.КартаСклада.Получить().Записать(ИмяТемпФайла);
	ГеографическаяСхема.Прочитать(ИмяТемпФайла);
	УдалитьФайлы(ИмяТемпФайла);

КонецПроцедуры // ПрочитатьМакет()

//Формируется структура объектов склада
//
// Параметры
//   ГеографическаяСхема - ГеографическаяСхема
//
Процедура НарисоватьСклад(ГеографическаяСхема) Экспорт 

	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЯчейкиХранения.КоординатаX КАК КоординатаX,
	               |	ЯчейкиХранения.КоординатаY КАК КоординатаY,
	               |	ЯчейкиХранения.Уровень КАК Уровень,
	               |	ЯчейкиХранения.ДлинаЯчейки,
	               |	ЯчейкиХранения.ШиринаЯчейки,
	               |	ЯчейкиХранения.Код КАК Код,
	               |	ЯчейкиХранения.Родитель,
	               |	ЯчейкиХранения.ПометкаУдаления,
	               |	ЯчейкиХранения.НеДоступна
	               |ИЗ
	               |	Справочник.ЯчейкиХранения КАК ЯчейкиХранения
	               |ГДЕ
	               |	ЯчейкиХранения.Владелец = &Склад
	               |	И (НЕ ЯчейкиХранения.ЭтоГруппа)
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Код)
	               |ПО
	               |	Уровень";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Склад",Склад);
	
	ЕдИзм = Строка(Склад.ЕдиницаРазмерности);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// инициализация переменных
	СчетчикПрогресса  = 0;
	ОтступРасшифровок = 10;

	// читаем количество ячеек 
	РезультатЗапроса.Следующий();
	КоличествоЯчеек = РезультатЗапроса.Количество()* РезультатЗапроса.Код;
	РезультатЗапроса.Сбросить();
	
	// очистим слои
	ГеографическаяСхема.Слои.Очистить();
	ПредыдущееЗначениеПрогресса = 0;
	// обход по уровням
	Пока РезультатЗапроса.Следующий() = Истина Цикл
		
		//добавляем новый слой для уровня
		Уровень      = РезультатЗапроса.Уровень;
		ИмяСлояЯчеек = "Уровень" + Строка(Уровень);
		СлойУровня   = ГеографическаяСхема.Слои.Добавить(ИмяСлояЯчеек, Тип ("ПолигональныйОбъектГеографическойСхемы"));
		
		ИмяСлояРасшифровок = "Расшифровки" + Строка(Уровень);
		СлойРасшифровок    = ГеографическаяСхема.Слои.Добавить(ИмяСлояРасшифровок, Тип ("ТочечныйОбъектГеографическойСхемы"));
		
		//запретим выделения объекта на схеме
		СлойРасшифровок.РазрешитьВыбор = Ложь;
		
		// по тексту слой можно найти методом Найти()
		СлойУровня.Текст      = ИмяСлояЯчеек;
		СлойРасшифровок.Текст = ИмяСлояРасшифровок;
		
		// серия расшифровки, значения - имена ячеек;
		СерияКодовЯчеек = СлойРасшифровок.Серии.Добавить("КодыЯчеек");
		СерияКодовЯчеек.Текст          = "Ячейка";
		СерияКодовЯчеек.ТипОтображения = ТипОтображенияСерииСлояГеографическойСхемы.Текст;
		СерияКодовЯчеек.ШрифтТекста    = Шрифт;
		
		// получение коллекции объектов
		ЯчейкиУровня     = СлойУровня.Объекты;
		РасшифровкиЯчеек = СлойРасшифровок.Объекты;
		
		//отступы для отрисовки слоев
		ОтШир = Уровень*Смещение;
		ОтДол = Уровень*Смещение;
		
		// обход ячеек уровня
		ВыборкаЯчейки = РезультатЗапроса.Выбрать();
		Пока ВыборкаЯчейки.Следующий() Цикл
			
			УникальноеИмяЯчейки = СокрЛП(ВыборкаЯчейки.Код);
			
			НоваяРасшифровка = РасшифровкиЯчеек.Добавить();
			НоваяРасшифровка.Значение = УникальноеИмяЯчейки;
			
			НоваяЯчейка = ЯчейкиУровня.Добавить();
			НоваяЯчейка.Значение = УникальноеИмяЯчейки;
			//НоваяЯчейка.ОтображатьДанные = Ложь;
			
			Если ВыборкаЯчейки.ПометкаУдаления ИЛИ ВыборкаЯчейки.НеДоступна Тогда
				
				НоваяЯчейка.Цвет = WebЦвета.Серый;
				
			Иначе
				
				Родитель = ВыборкаЯчейки.Родитель;
				Если Родитель.Пустая() Тогда
					Если Уровень < 20 Тогда
						//	ЦветУровня = Новый Цвет(0 + Уровень*15,191,225);	
						НоваяЯчейка.Цвет =  СписокЦветов.Получить(Уровень).Значение;
					Иначе
						НоваяЯчейка.Цвет =  СписокЦветов.Получить(Уровень - 20*Цел(Уровень/20)).Значение;
					КонецЕсли;
				Иначе
					ЦветГруппы = Родитель.ЦветГруппы.Получить();
					Если  НЕ ЦветГруппы = Неопределено   Тогда
						НоваяЯчейка.Цвет = ЦветГруппы;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			//отрисовка ячейки и расшифровки
			Контуры = НоваяЯчейка.Контуры;
			Контур  = Контуры.Добавить();
			ДлинаЯчейки  = ВыборкаЯчейки.ДлинаЯчейки;
			ШиринаЯчейки = ВыборкаЯчейки.ШиринаЯчейки;
			
			КоординатаY = ВыборкаЯчейки.КоординатаY;
			КоординатаX = ВыборкаЯчейки.КоординатаX;
			
			Координата = Новый КоординатыГеографическойСхемы(Долгота(КоординатаX+ОтДол), Широта(КоординатаY + ОтШир));
			Контур.Добавить(Координата);
			
			Координата = Новый КоординатыГеографическойСхемы(Долгота(КоординатаX+ОтДол), Широта(КоординатаY+ШиринаЯчейки+ОтШир));
			Контур.Добавить(Координата);
			
			Координата = Новый КоординатыГеографическойСхемы(Долгота(КоординатаX+ДлинаЯчейки+ОтДол),Широта(КоординатаY +ШиринаЯчейки+ОтШир));
			Контур.Добавить(Координата);
			
			Координата = Новый КоординатыГеографическойСхемы(Долгота(КоординатаX+ДлинаЯчейки+ОтДол), Широта(КоординатаY+ОтШир ));
			Контур.Добавить(Координата);
			
			КоординатаYРасшифровки = КоординатаY + ШиринаЯчейки*0.35;
			КоординатаXРасшифровки = КоординатаX + ДлинаЯчейки*0.6;
			
			Координата = Новый КоординатыГеографическойСхемы(Долгота(КоординатаXРасшифровки+ОтДол),Широта(КоординатаYРасшифровки+ОтШир));
			НоваяРасшифровка.Координаты = Координата;
			
			// настройка расшифровки
			НоваяРасшифровка.ТипОтрисовки = ТипОтображенияТочечногоОбъектаГеографическойСхемы.Символ;
			НоваяРасшифровка.Картинка = БиблиотекаКартинок.Автомобили;
			НоваяРасшифровка.Символ = "";
			
			//для объектов расшифровки заполним серию  с именами ячеек
			СлойРасшифровок.УстановитьЗначение(НоваяРасшифровка, СерияКодовЯчеек, СокрЛП(УникальноеИмяЯчейки), "размеры : ширина "+ШиринаЯчейки+ЕдИзм+" ,длина "+ДлинаЯчейки+ЕдИзм+".");
			
			СчетчикПрогресса = СчетчикПрогресса + 1;
			Прогресс = Цел(СчетчикПрогресса * 100 / КоличествоЯчеек);
			Если Прогресс<= 100 И ПредыдущееЗначениеПрогресса < Прогресс Тогда
				Состояние("Выполнено: " + Прогресс + "%");
				ПредыдущееЗначениеПрогресса = Прогресс;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // НарисоватьСклад()

// Возвращает географическую широту координаты
//
// Параметры
//  Долгота - число
//
// Возвращаемое значение:
//   – число
//
Функция  Широта(Расстояние)Экспорт
	
	 Возврат  Расстояние*360/40000000;

	
КонецФункции // Широта()

// Возвращает географическую долготу координаты
//
// Параметры
//  Долгота - число
//
// Возвращаемое значение:
//   – число
//
Функция Долгота(Расстояние)Экспорт
	
	Возврат  Расстояние*360/40000000;
	
КонецФункции // Долгота()

// Возвращает конвертирует  долготу в координату
//
// Параметры
//  Долгота - число
//
// Возвращаемое значение:
//   – число
//
Функция ДолготаВКоординату(Долгота)Экспорт
	
	Возврат  Долгота*40000000/360;
	
КонецФункции // ДолготаВКоординату()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

СписокЦветов = Новый СписокЗначений;
СписокЦветов.Добавить(WebЦвета.Голубой);
СписокЦветов.Добавить(WebЦвета.ЗеленаяЛужайка);
СписокЦветов.Добавить(WebЦвета.Золотистый);
СписокЦветов.Добавить(WebЦвета.Золотой);
СписокЦветов.Добавить(WebЦвета.СветлоЖелтый);
СписокЦветов.Добавить(WebЦвета.ГрифельноСерый);
СписокЦветов.Добавить(WebЦвета.Лимонный);
СписокЦветов.Добавить(WebЦвета.ЛососьТемный);
СписокЦветов.Добавить(WebЦвета.Персиковый);
СписокЦветов.Добавить(WebЦвета.Хаки);
СписокЦветов.Добавить(WebЦвета.Коричневый);
СписокЦветов.Добавить(WebЦвета.Оранжевый);
СписокЦветов.Добавить(WebЦвета.ОрхидеяТемный);
СписокЦветов.Добавить(WebЦвета.Бежевый);
СписокЦветов.Добавить(WebЦвета.КрасноФиолетовый);
СписокЦветов.Добавить(WebЦвета.Циан);
СписокЦветов.Добавить(WebЦвета.Шоколадный);
СписокЦветов.Добавить(WebЦвета.НейтральноПурпурный);
СписокЦветов.Добавить(WebЦвета.Киноварь );
СписокЦветов.Добавить(WebЦвета.РыжеватоКоричневый);
#КонецЕсли

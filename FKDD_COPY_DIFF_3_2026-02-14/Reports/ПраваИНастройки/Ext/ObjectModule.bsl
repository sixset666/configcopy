#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем НазначениеОбъекта; // назначение объекта
Перем СтруктураОписаний; // структура описаний

//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Заполняет реквизит "СтруктураОписаний" из макета "НастройкиПоУмолчанию"
// В дальнейшем эта структура может использоваться для вывода описаний в форме
//
// Без параметров
//
Процедура ПолучитьСтруктуруОписаний()
	Макет=ПланыВидовХарактеристик.ПраваИНастройки.ПолучитьМакет("НастройкиПоУмолчанию");	
	Для Номер = 1 По 10000 Цикл	// Заведомо больше, чем есть в макете
		Код=СокрЛП(Макет.ПолучитьОбласть("R"+Строка(Номер)+"C3").ТекущаяОбласть.Текст);
		Описание=СокрЛП(Макет.ПолучитьОбласть("R"+Строка(Номер)+"C8").ТекущаяОбласть.Текст);
		Если Код="" Тогда Прервать;
		ИначеЕсли Описание="" Тогда Продолжить;
		КонецЕсли;
		СтруктураОписаний.Вставить("_"+Код,Описание);
	КонецЦикла;
КонецПроцедуры // ПолучитьСтруктуруОписаний()

// Возвращает представление установленного фильтра строкой
//
// Без параметров
//
// Возвращаемое значение
//	Стр - Строка
//
Функция ПолучитьПредставлениеФильтра()
	Стр = "Отбор: ";
	Стр = Стр + "Формировать: ";
	Если ОтображатьПрава Тогда
		Стр = Стр + "Права; ";
	КонецЕсли;
	Если ОтображатьНастройки Тогда
		Стр = Стр + "Настройки; ";
	КонецЕсли;
	Если ОтображатьТолькоПоВыбранномуОбъекту Тогда
		Стр = Стр + "Выбранный тип: ";
		Если Назначение = Перечисления.НазначениеПравИНастроек.Компания Тогда
			Стр = Стр + "Компания; ";
		ИначеЕсли Назначение = Перечисления.НазначениеПравИНастроек.Организация Тогда
			Стр = Стр + "Организация; ";
		ИначеЕсли Назначение = Перечисления.НазначениеПравИНастроек.Подразделение Тогда
			Стр = Стр + "Подразделение; ";
		ИначеЕсли Назначение = Перечисления.НазначениеПравИНастроек.Пользователь Тогда
			Стр = Стр + "Пользователь; ";
		Иначе
		КонецЕсли;
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(Подразделение) Тогда
		Стр = Стр + "Подразделение: " + Подразделение.Наименование + "; ";
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(Организация) Тогда
		Стр = Стр + "Организация: " + Организация.Наименование + "; ";
	КонецЕсли;
	Если Пользователи.Количество() > 0 Тогда
		Стр = Стр + "Пользователи: ";
		Для Каждого Объект Из Пользователи Цикл
			Стр = Стр + Объект.Пользователь.Наименование + "; ";
		КонецЦикла;
	КонецЕсли;
	Возврат Стр;
КонецФункции // ПолучитьПредставлениеФильтра()

// Возвращает значение права или настройки
//
// Параметры
//	Выборка - Выборка
//
Функция ПолучитьЗначениеИзВыборки(Выборка)
	Значение = Неопределено;
	Если Выборка.Назначение = Перечисления.НазначениеПравИНастроек.Компания Тогда
		Значение = Выборка.ЗначениеКомпания;
	ИначеЕсли Выборка.Назначение = Перечисления.НазначениеПравИНастроек.Организация Тогда
		Значение = Выборка.ЗначениеОрганизация;
	ИначеЕсли Выборка.Назначение = Перечисления.НазначениеПравИНастроек.Подразделение Тогда
		Значение = Выборка.ЗначениеПодразделение;
	ИначеЕсли Выборка.Назначение = Перечисления.НазначениеПравИНастроек.Пользователь Тогда
		Значение = Выборка.ЗначениеПользователь;
	Иначе
		Если НЕ обЗначениеНеЗаполнено(Выборка.ЗначениеПользователь) Тогда
			Значение = Выборка.ЗначениеПользователь;
		ИначеЕсли НЕ обЗначениеНеЗаполнено(Выборка.ЗначениеПодразделение) Тогда
			Значение = Выборка.ЗначениеПодразделение;
		ИначеЕсли НЕ обЗначениеНеЗаполнено(Выборка.ЗначениеОрганизация) Тогда
			Значение = Выборка.ЗначениеОрганизация;
		КонецЕсли; 
	КонецЕсли;
	Если обЗначениеНеЗаполнено(Значение) Тогда
		Если Значение = NULL Тогда
			Значение = Выборка.ЗначениеПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(Значение) Тогда
		Значение = Выборка.ТипЗначения.ПривестиЗначение(Неопределено);
	КонецЕсли;
    Возврат Значение;
КонецФункции // ПолучитьЗначениеИзВыборки()

// Возвращает выборку прав и настроек с учетов установленных настроек.
//
// Параметры
//	Объект - СправочникСсылка
//
// Тип возвращаемого значения - РезультатЗапроса                            
//
Функция ПолучитьВыборкуПравИНастроек(Объект)
	
	Если ТипЗнч (Объект) = Тип("СправочникСсылка.Пользователи") Тогда
		Если обЗначениеНеЗаполнено(Объект.ШаблонПрав) Тогда ПараметрПользователь = Объект;
		Иначе ПараметрПользователь = Объект.ШаблонПрав;
		КонецЕсли;
		ПараметрПодразделение = Объект.Подразделение;
		ПараметрОрганизация = Объект.Организация;
	ИначеЕсли ТипЗнч (Объект) = Тип("СправочникСсылка.ПодразделенияКомпании") Тогда
		ПараметрПользователь = Неопределено;
		ПараметрПодразделение = Объект;
		ПараметрОрганизация = Объект.Организация;
	ИначеЕсли ТипЗнч (Объект) = Тип("СправочникСсылка.Организации") Тогда
		ПараметрПользователь = Неопределено;
		ПараметрПодразделение = Неопределено;
		ПараметрОрганизация = Объект;
	Иначе
		ПараметрПользователь = Неопределено;
		ПараметрПодразделение = Неопределено;
		ПараметрОрганизация = Неопределено;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Назначение) Тогда
		НазначениеОбъекта = Перечисления.НазначениеПравИНастроек.Пользователь;
	Иначе
		НазначениеОбъекта = Назначение;
	КонецЕсли;
	
	УсловияНаНастройку = НЕ ОтображатьПрава = ОтображатьНастройки;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	0 КАК ПустойИтог,
	|	ПланВидовХарактеристикПраваИНастройки.Код,
	|	ПланВидовХарактеристикПраваИНастройки.Наименование,
	|	ПланВидовХарактеристикПраваИНастройки.Ссылка,
	|	ПланВидовХарактеристикПраваИНастройки.Родитель,
	|   ПланВидовХарактеристикПраваИНастройки.ЭтоГруппа,
	|	ПланВидовХарактеристикПраваИНастройки.ТипЗначения,
	|	ПланВидовХарактеристикПраваИНастройки.Назначение,
	|	ПланВидовХарактеристикПраваИНастройки.ЭтоНастройка,
	|	ПланВидовХарактеристикПраваИНастройки.ЗначениеПоУмолчанию,
	|	РегистрСведенийПраваИНастройки1.Значение КАК ЗначениеПользователь,
	|	РегистрСведенийПраваИНастройки2.Значение КАК ЗначениеОрганизация,
	|	РегистрСведенийПраваИНастройки3.Значение КАК ЗначениеПодразделение,
	|	РегистрСведенийПраваИНастройки4.Значение КАК ЗначениеКомпания
	|ИЗ
	|	ПланВидовХарактеристик.ПраваИНастройки КАК ПланВидовХарактеристикПраваИНастройки
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ПраваИНастройки КАК РегистрСведенийПраваИНастройки1
	|		ПО
	|			ПланВидовХарактеристикПраваИНастройки.Ссылка = РегистрСведенийПраваИНастройки1.ПравоНастройка
	|			И РегистрСведенийПраваИНастройки1.Объект = &Пользователь
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ПраваИНастройки КАК РегистрСведенийПраваИНастройки2
	|		ПО
	|			ПланВидовХарактеристикПраваИНастройки.Ссылка = РегистрСведенийПраваИНастройки2.ПравоНастройка
	|			И РегистрСведенийПраваИНастройки2.Объект = &Организация
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ПраваИНастройки КАК РегистрСведенийПраваИНастройки3
	|		ПО
	|			ПланВидовХарактеристикПраваИНастройки.Ссылка = РегистрСведенийПраваИНастройки3.ПравоНастройка
	|			И РегистрСведенийПраваИНастройки3.Объект = &Подразделение
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ПраваИНастройки КАК РегистрСведенийПраваИНастройки4
	|		ПО
	|			ПланВидовХарактеристикПраваИНастройки.Ссылка = РегистрСведенийПраваИНастройки4.ПравоНастройка
	|			И РегистрСведенийПраваИНастройки4.Объект = Неопределено
	|ГДЕ ПланВидовХарактеристикПраваИНастройки.ЭтоГруппа = ЛОЖЬ" + 
	?(ОтображатьТолькоПоВыбранномуОбъекту, " И ПланВидовХарактеристикПраваИНастройки.Назначение = &Назначение", "") +
	?(УсловияНаНастройку, " И ПланВидовХарактеристикПраваИНастройки.ЭтоНастройка = &ЭтоНастройка", "") + "
	|УПОРЯДОЧИТЬ ПО 
	|	ПланВидовХарактеристикПраваИНастройки.Код
	|ИТОГИ Среднее(ПустойИтог) ПО ПланВидовХарактеристикПраваИНастройки.Ссылка ТОЛЬКО ИЕРАРХИЯ
	|";
			
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Пользователь",ПараметрПользователь);
	Запрос.УстановитьПараметр("Подразделение",ПараметрПодразделение);
	Запрос.УстановитьПараметр("Организация",ПараметрОрганизация);
	Запрос.УстановитьПараметр("Назначение",НазначениеОбъекта);
	Запрос.УстановитьПараметр("ЭтоНастройка",ОтображатьНастройки);
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьВыборкуПравИНастроек()

// Функция формирования отчета
// 
// Параметры: 
//  Документ – ТабличныйДокумент – Табличный документ, в который формируется отчет.
// 
// Возвращаемое значение: 
// 	Документ - ТабличныйДокумент
//
Функция СформироватьОтчет(Документ = Неопределено)
	Если Документ = Неопределено Тогда
		Документ = Новый ТабличныйДокумент;
	КонецЕсли;

	Документ.ФиксацияСверху = 0;
	Документ.ФиксацияСлева = 0;
	Документ.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	// считаем оформление с макета оформления
	МакетОтчетаПараметров = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента);
	ОформлениеСтроки = Новый Массив;
	ОбластьОформленияИзмерений = МакетОтчетаПараметров.ПолучитьОбласть("ОформлениеИзмерений");
	Для Сч = 1 По ОбластьОформленияИзмерений.ВысотаТаблицы Цикл
		ОформлениеСтроки.Добавить(ОбластьОформленияИзмерений.Область(Сч, 1));
	КонецЦикла;
	
	ОформлениеШапки = МакетОтчетаПараметров.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеИерархии = МакетОтчетаПараметров.ПолучитьОбласть("ОформлениеИерархии").Область(1, 1);
	ОформлениеСтрок = ОформлениеСтроки[6];
	ОформлениеСтрокНеПоУмолчанию = ОформлениеСтроки[5];
	
	Если ИмяМакетаОформленияТабличногоДокумента = "МакетОформлениеОтчета" Тогда
		// если выбран основой макет оформления - оставляем оформление как и было в нашем макете
		ШрифтШапка = Неопределено;
		ЦветТекстаШапка = Неопределено;
		ЦветФонаШапка = Неопределено;
		
		ШрифтИерархия = Неопределено;
		ЦветТекстаИерархия = Неопределено;
		ЦветФонаИерархия = Неопределено;
		
		ШрифтСтрока = Неопределено;
		ЦветТекстаСтрока = Неопределено;
		ЦветФонаСтрока = Неопределено;
		
		ШрифтСтрокаНеПоУмолчанию = Неопределено;
		ЦветТекстаСтрокаНеПоУмолчанию = Неопределено;
		ЦветФонаСтрокаНеПоУмолчанию = Неопределено;
	Иначе
		ШрифтШапка = ОформлениеШапки.Шрифт;
		ЦветТекстаШапка = ОформлениеШапки.ЦветТекста;
		ЦветФонаШапка = ОформлениеШапки.ЦветФона;
		
		ШрифтИерархия = ОформлениеИерархии.Шрифт;
		ЦветТекстаИерархия = ОформлениеИерархии.ЦветТекста;
		ЦветФонаИерархия = ОформлениеИерархии.ЦветФона;
		
		ШрифтСтрока = ОформлениеСтрок.Шрифт;
		ЦветТекстаСтрока = ОформлениеСтрок.ЦветТекста;
		ЦветФонаСтрока = ОформлениеСтрок.ЦветФона;
		
		ШрифтСтрокаНеПоУмолчанию = ОформлениеСтрокНеПоУмолчанию.Шрифт;
		ЦветТекстаСтрокаНеПоУмолчанию = ОформлениеСтрокНеПоУмолчанию.ЦветТекста;
		ЦветФонаСтрокаНеПоУмолчанию = ОформлениеСтрокНеПоУмолчанию.ЦветФона;
	КонецЕсли;
	
	Если (ОтображатьТолькоПоВыбранномуОбъекту) И (НЕ Назначение = Перечисления.НазначениеПравИНастроек.Пользователь) Тогда
		ТаблицаВыборка = Новый ТаблицаЗначений;
		ТаблицаВыборка.Колонки.Добавить("Объект");
		Если Назначение = Перечисления.НазначениеПравИНастроек.Организация Тогда
			Если обЗначениеНеЗаполнено(Организация) Тогда
				Запрос = Новый Запрос("ВЫБРАТЬ Ссылка КАК Объект ИЗ Справочник.Организации АВТОУПОРЯДОЧИВАНИЕ");
				ТаблицаВыборка = Запрос.Выполнить().Выгрузить();
			Иначе
				НоваяСтрока = ТаблицаВыборка.Добавить();
				НоваяСтрока.Объект = Организация;
			КонецЕсли;
		ИначеЕсли Назначение = Перечисления.НазначениеПравИНастроек.Подразделение Тогда
			Если обЗначениеНеЗаполнено(Подразделение) Тогда
				Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ Ссылка КАК Объект ИЗ Справочник.ПодразделенияКомпании УПОРЯДОЧИТЬ ПО Ссылка.Наименование ИЕРАРХИЯ");
				ТаблицаВыборка = Запрос.Выполнить().Выгрузить();
			Иначе
				НоваяСтрока = ТаблицаВыборка.Добавить();
				НоваяСтрока.Объект = Подразделение;
			КонецЕсли;
		ИначеЕсли Назначение = Перечисления.НазначениеПравИНастроек.Компания Тогда
			ТаблицаВыборка.Добавить();
		Иначе
		КонецЕсли;
	Иначе
		ТаблицаВыборка = ПолучитьТаблицуПользователей();
	КонецЕсли;
	
	Макет = ПолучитьМакет("Макет");
	Область = Макет.ПолучитьОбласть("Заголовок|Общая");
	Документ.Вывести(Область);
	Область = Макет.ПолучитьОбласть("Фильтр|Общая");
	Область.Параметры.Отбор = ПолучитьПредставлениеФильтра();
	Документ.Вывести(Область);
	
	ВсегоВВыборке = ТаблицаВыборка.Количество();
	Если ВсегоВВыборке = 0 Тогда
		Предупреждение("По установленному отбору нет данных для отчета", обПраво("ТаймаутДиалогов",глПрава));
		Возврат Документ
	КонецЕсли;
	
	ИндексСтолбцы = 4;
	
	Область = Макет.ПолучитьОбласть("Шапка|ПравоНастройка");
	Документ.Вывести(Область);
	
	Если ВыводитьОписаниеПрава Тогда
		Область = Макет.ПолучитьОбласть("Шапка|Описание");
		Документ.Присоединить(Область);
		ИндексСтолбцы = ИндексСтолбцы + 1;
	КонецЕсли;
	
	Если ПоказыватьЗначениеПоУмолчанию Тогда
		Область = Макет.ПолучитьОбласть("Шапка|ЗначениеПоУмолчанию");
		Документ.Присоединить(Область);
		ИндексСтолбцы = ИндексСтолбцы + 1;
	КонецЕсли;
	
	СчетчикПоВыборке = 0;
	СоответствиеПравНастроек = Новый Соответствие;
	ВыборкаШаблон = Неопределено;
	
	// Формируем соответствие: ключ - объект (пользователь, подразделение, организация, компания),
	// значение - таблица прав и настроек
	Для Каждого ВыборкаСтр Из ТаблицаВыборка Цикл
		Объект = ВыборкаСтр.Объект;
		Если обЗначениеНеЗаполнено(Объект) Тогда
			ОбъектСтрокой = "Компания";
			Объект = "Компания";
		Иначе
			ОбъектСтрокой = Строка(Объект);
		КонецЕсли;
		РезультатПравНастроек = ПолучитьВыборкуПравИНастроек(Объект);
		Если ВыборкаШаблон = Неопределено Тогда
			ВыборкаШаблон = РезультатПравНастроек.Выбрать();
		КонецЕсли;
		ТаблицаПравНастроек = РезультатПравНастроек.Выгрузить();
		СоответствиеПравНастроек.Вставить(Объект, ТаблицаПравНастроек);
		
		Область = Макет.ПолучитьОбласть("Шапка|Значение");
		Область.Параметры.Значение = ОбъектСтрокой;
		Документ.Присоединить(Область);
		
		ИндексСтолбцы = ИндексСтолбцы + 1;
		
		СчетчикПоВыборке = СчетчикПоВыборке + 1;
		Состояние("Выполнено: " + Цел(СчетчикПоВыборке / ВсегоВВыборке * 50) + "%");
	КонецЦикла;
	
	// применяем оформление для шапки
	Область = Документ.Область(5,2,5,ИндексСтолбцы);
	Если ШрифтШапка <> Неопределено Тогда
		Область.Шрифт = ШрифтШапка;
	КонецЕсли;
	Если ЦветТекстаШапка <> Неопределено Тогда
		Область.ЦветТекста = ЦветТекстаШапка;
	КонецЕсли;
	Если ЦветФонаШапка <> Неопределено Тогда
		Область.ЦветФона = ЦветФонаШапка;
	КонецЕсли;
	
	ВсегоВВыборке = ВыборкаШаблон.Количество();
	СчетчикПоВыборке = 0;
	ТаблицаГрупп = Новый ТаблицаЗначений;
	ТаблицаГрупп.Колонки.Добавить("Группа");
	ТаблицаГрупп.Колонки.Добавить("Уровень");
	Документ.НачатьАвтогруппировкуСтрок();
	Пока ВыборкаШаблон.Следующий() Цикл
		Если ВыборкаШаблон.ЭтоГруппа = NULL Тогда
			Продолжить;
		КонецЕсли;
			
		Если ВыборкаШаблон.ЭтоГруппа И ТаблицаГрупп.Найти(ВыборкаШаблон.Ссылка, "Группа") <> Неопределено Тогда
			Продолжить;
		Иначе
			НоваяСтрока = ТаблицаГрупп.Добавить();
			НоваяСтрока.Группа = ВыборкаШаблон.Ссылка;
			НоваяСтрока.Уровень = ВыборкаШаблон.Уровень() + 1;
		КонецЕсли;
			
		Если ВыборкаШаблон.ЭтоГруппа Тогда
			ИмяСекции = "СтрокаГруппа";
		ИначеЕсли ВыборкаШаблон.ЭтоНастройка Тогда
			ИмяСекции = "СтрокаНастройка";
		Иначе
			ИмяСекции = "СтрокаПраво";
		КонецЕсли;
			
		СтрУровень = ТаблицаГрупп.Найти(ВыборкаШаблон.Родитель, "Группа");
		Если СтрУровень = Неопределено Тогда
			ТекУровень = 0;
		Иначе
			ТекУровень = СтрУровень.Уровень;
		КонецЕсли;
			
		Область = Макет.ПолучитьОбласть(ИмяСекции + "|ПравоНастройка");
		Область.Параметры.Код = ВыборкаШаблон.Код;
		Область.Параметры.Наименование = ВыборкаШаблон.Наименование;
		Документ.Вывести(Область, ТекУровень);
			
		ИндексПоГоризонтали = 4;
			
		СтрокаОписание = "";
		СтруктураОписаний.Свойство("_" + СокрЛП(ВыборкаШаблон.Код), СтрокаОписание);
		Если ВыводитьОписаниеПрава Тогда
			Область = Макет.ПолучитьОбласть(ИмяСекции + "|Описание");
			Область.Параметры.Описание = СтрокаОписание;
			Документ.Присоединить(Область);
			ИндексПоГоризонтали = ИндексПоГоризонтали + 1;
		Иначе
			Ячейка = Документ.Область(Документ.ВысотаТаблицы, 3);
			Ячейка.Примечание.Текст = СтрокаОписание;
		КонецЕсли;
			
		Если ПоказыватьЗначениеПоУмолчанию Тогда
			Если ВыборкаШаблон.ЭтоГруппа Тогда
				Область = Макет.ПолучитьОбласть(ИмяСекции + "|ЗначениеПоУмолчанию");
				Документ.Присоединить(Область);
			Иначе
				Объект = ВыборкаСтр.Объект;
				Если обЗначениеНеЗаполнено(Объект) Тогда
					Объект = "Компания";
				КонецЕсли;
							
				ЗначениеВОтчет = ВыборкаШаблон.ЗначениеПоУмолчанию;
				МассивТипов = ВыборкаШаблон.ТипЗначения.Типы();
				Если МассивТипов.Количество() = 1 Тогда
					Если МассивТипов[0] = Тип("Булево") Тогда
						Если ЗначениеВОтчет = Истина Тогда
							ИмяСекцияБулево = "|ЗначениеБулевоИстина";
						Иначе
							ИмяСекцияБулево = "|ЗначениеБулевоЛожь";
						КонецЕсли;
						Область = Макет.ПолучитьОбласть(ИмяСекции + ИмяСекцияБулево);
					Иначе
						Область = Макет.ПолучитьОбласть(ИмяСекции + "|ЗначениеПоУмолчанию");
						Область.Параметры.Значение = ЗначениеВОтчет;
					КонецЕсли;
				Иначе
					Область = Макет.ПолучитьОбласть(ИмяСекции + "|ЗначениеПоУмолчанию");
					Область.Параметры.Значение = "<Ошибка в заданных типах>";
				КонецЕсли;
				Документ.Присоединить(Область);
			КонецЕсли;
			ИндексПоГоризонтали = ИндексПоГоризонтали + 1;
		КонецЕсли;

		Область = Документ.Область(Документ.ВысотаТаблицы,2,Документ.ВысотаТаблицы,ИндексПоГоризонтали);
		Если ВыборкаШаблон.ЭтоГруппа Тогда
			Если ШрифтИерархия <> Неопределено Тогда
				Область.Шрифт = ШрифтИерархия;
			КонецЕсли;
			Если ЦветТекстаИерархия <> Неопределено Тогда
				Область.ЦветТекста = ЦветТекстаИерархия;
			КонецЕсли;
			Если ЦветФонаИерархия <> Неопределено Тогда
				Область.ЦветФона = ЦветФонаИерархия;
			КонецЕсли;
		Иначе
			Если ЦветТекстаСтрока <> Неопределено Тогда
				Область.ЦветТекста = ЦветТекстаСтрока;
			КонецЕсли;
			Если ШрифтСтрока <> Неопределено Тогда
				Область.Шрифт = ШрифтСтрока;
			КонецЕсли;
			Если ЦветФонаСтрока <> Неопределено Тогда
				Область.ЦветФона = ЦветФонаСтрока;
			КонецЕсли;
		КонецЕсли;
			
		ИндексПоГоризонтали = ИндексПоГоризонтали + 1;
			
		Для Каждого ВыборкаСтр Из ТаблицаВыборка Цикл
			Если ВыборкаШаблон.ЭтоГруппа Тогда
				Область = Макет.ПолучитьОбласть(ИмяСекции + "|Значение");
				Документ.Присоединить(Область);
				Ячейка = Документ.Область(Документ.ВысотаТаблицы, ИндексПоГоризонтали);
				Если ШрифтИерархия <> Неопределено Тогда
					Ячейка.Шрифт = ШрифтИерархия;
				КонецЕсли;
				Если ЦветТекстаИерархия <> Неопределено Тогда
					Ячейка.ЦветТекста = ЦветТекстаИерархия;
				КонецЕсли;
				Если ЦветФонаИерархия <> Неопределено Тогда
					Ячейка.ЦветФона = ЦветФонаИерархия;
				КонецЕсли;
			Иначе
				Объект = ВыборкаСтр.Объект;
				Если обЗначениеНеЗаполнено(Объект) Тогда
					Объект = "Компания";
				КонецЕсли;
						
				ТабЗначения = СоответствиеПравНастроек[Объект];
				СтрЗначение = ТабЗначения.Найти(ВыборкаШаблон.Ссылка, "Ссылка");
				ЗначениеВОтчет = ПолучитьЗначениеИзВыборки(СтрЗначение);
				МассивТипов = СтрЗначение.ТипЗначения.Типы();
				Если МассивТипов.Количество() = 1 Тогда
					Если МассивТипов[0] = Тип("Булево") Тогда
						Если ЗначениеВОтчет = Истина Тогда
							ИмяСекцияБулево = "|ЗначениеБулевоИстина";
						Иначе
							ИмяСекцияБулево = "|ЗначениеБулевоЛожь";
						КонецЕсли;
						Область = Макет.ПолучитьОбласть(ИмяСекции + ИмяСекцияБулево);
					Иначе
						Область = Макет.ПолучитьОбласть(ИмяСекции + "|Значение");
						Область.Параметры.Значение = ЗначениеВОтчет;
					КонецЕсли;
				Иначе
					Область = Макет.ПолучитьОбласть(ИмяСекции + "|Значение");
					Область.Параметры.Значение = "<Ошибка в заданных типах>";
				КонецЕсли;				
				Документ.Присоединить(Область);
					
				Ячейка = Документ.Область(Документ.ВысотаТаблицы, ИндексПоГоризонтали);
				Если ЗначениеВОтчет = СтрЗначение.ЗначениеПоУмолчанию Тогда
					Если ШрифтСтрока <> Неопределено Тогда
						Ячейка.Шрифт = ШрифтСтрока;
					КонецЕсли;
					Если ЦветТекстаСтрока <> Неопределено Тогда
						Ячейка.ЦветТекста = ЦветТекстаСтрока;
					Иначе
						Ячейка.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
					КонецЕсли;
					Если ЦветФонаСтрока <> Неопределено Тогда
						Ячейка.ЦветФона = ЦветФонаСтрока;
					КонецЕсли;
				Иначе
					Если ШрифтСтрокаНеПоУмолчанию <> Неопределено Тогда
						Ячейка.Шрифт = ШрифтСтрокаНеПоУмолчанию;
					КонецЕсли;
					Если ЦветТекстаСтрокаНеПоУмолчанию <> Неопределено Тогда
						Ячейка.ЦветТекста = ЦветТекстаСтрокаНеПоУмолчанию;
					КонецЕсли;
					Если ЦветФонаСтрокаНеПоУмолчанию <> Неопределено Тогда
						Ячейка.ЦветФона = ЦветФонаСтрока;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			ИндексПоГоризонтали = ИндексПоГоризонтали + 1;
				
		КонецЦикла;
			
		СчетчикПоВыборке = СчетчикПоВыборке + 1;
		Состояние("Выполнено: " + Цел((СчетчикПоВыборке / ВсегоВВыборке * 50) + 50) + "%");
	КонецЦикла;
	Документ.ЗакончитьАвтогруппировкуСтрок();
	
	Документ.ОтображатьСетку = Ложь;
	Документ.ОтображатьЗаголовки = Ложь;
	Документ.ФиксацияСверху = 5;
	Возврат Документ;
КонецФункции // СформироватьОтчет()

// возвращает таблицу пользователей с учетом фильтров
//
// Без параметров
//
// Тип возвращаемого значения: ТаблицаЗначений
//
Функция ПолучитьТаблицуПользователей()
	Запрос = Новый Запрос;
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Пользователи.Ссылка КАК Объект
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ" + ?(обЗначениеНеЗаполнено(Подразделение), "", " Пользователи.Подразделение = &Подразделение И") +
		?(обЗначениеНеЗаполнено(Организация), "", " Пользователи.Организация = &Организация И") +
		?(Пользователи.Количество() > 0, " Пользователи.Ссылка В(&МассивПользователи)", "");
	Если Прав(ТекстЗапроса, 1) = "И" Тогда
		ТекстЗапроса = Лев(ТекстЗапроса, СтрДлина(ТекстЗапроса) - 1);
	КонецЕсли;
	Если Прав(ТекстЗапроса, 3) = "ГДЕ" Тогда
		ТекстЗапроса = Лев(ТекстЗапроса, СтрДлина(ТекстЗапроса) - 3);
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + " АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МассивПользователи", Пользователи.ВыгрузитьКолонку("Пользователь"));
	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции // ПолучитьТаблицуПользователей()


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Формирует табличный документ
//
// Без параметров
//
Процедура Сформировать() Экспорт
	Документ = СформироватьТабличныйДокумент();
	Документ.Показать("Права и настройки");
КонецПроцедуры // Сформировать()

// Формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Если ТипЗнч(ДокументРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокументРезультат) <> Тип("ТабличныйДокумент") Тогда
		  ДокументРезультат = Новый ТабличныйДокумент();
	Иначе ДокументРезультат.Очистить();
	КонецЕсли;
	
	Результат = СформироватьОтчет(ДокументРезультат);
	Возврат Результат;
	
КонецФункции // СформироватьТабличныйДокумент()


//////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ОтображатьНастройки = Истина;
ОтображатьПрава = Истина;
ОтображатьТолькоПоВыбранномуОбъекту = Ложь;

СтруктураОписаний = Новый Структура;
ПолучитьСтруктуруОписаний();

#КонецЕсли
//Перем АлгоритмПолученияЦены;                           // хранит алгоритм получения цен
//Перем ПодразделениеПоУмолчанию Экспорт;                // хранит подразделение по умолчанию
//Перем ТипЦенПоУмолчанию Экспорт;                       // хранит тип цен по умолчанию

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА



Процедура КомпоновщикПолучитьПоляОтбора(ОтборКомпоновки, УникальныеПоля = Неопределено)
	
	Если УникальныеПоля = Неопределено Тогда
		УникальныеПоля = Новый Структура;
	КонецЕсли;
	
	Для Каждого ТекОтбор Из ОтборКомпоновки.Элементы Цикл
		
		Если НЕ ТекОтбор.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			КомпоновщикПолучитьПоляОтбора(ТекОтбор, УникальныеПоля);
		Иначе
			ИмяОтбора = СокрЛП(ТекОтбор.ЛевоеЗначение);
			Если ИмяОтбора = "" Тогда
				Продолжить;
			КонецЕсли;
			
			ПозицияТочки = Найти(ИмяОтбора, ".");
			Если ПозицияТочки > 0 Тогда
				//ИмяОтбора = Лев(ИмяОтбора, ПозицияТочки);
				ИмяОтбора = Лев(ИмяОтбора, ПозицияТочки-1);
			КонецЕсли;
			
			УникальныеПоля.Вставить(ИмяОтбора);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // КомпоновщикПолучитьПоляСтруктуры()


// формирует тест запроса по себестоимости
Функция ПолучитьТекстЗапросаЦенСебестоимости(Запрос)
	
	СоответствиеЗамен = Новый Соответствие;
	СоответствиеЗамен.Вставить("Поставщик", "Партия.Контрагент");
	СоответствиеЗамен.Вставить("Организация", "СкладКомпании.Организация");
	СоответствиеЗамен.Вставить("Подразделение", "СкладКомпании.Подразделение");
	
	ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор;
	
	ТекстОтбора = ОтчетыСервер.КомпоновщикПолучитьТекстОтбора(ОтборКомпоновки.Элементы, ОтборКомпоновки.ДоступныеПоляОтбора, "", Запрос.Параметры, СоответствиеЗамен);
	
	Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЦенПоСебестоимости.СкладКомпании,
	|	ТаблицаЦенПоСебестоимости.Номенклатура,
	|	ТаблицаЦенПоСебестоимости.Партия,
	|	ТаблицаЦенПоСебестоимости.Партия.Контрагент КАК Поставщик,
	|	ТаблицаЦенПоСебестоимости.СтатусПартии КАК СтатусПартии,
	|	МИНИМУМ(ТаблицаЦенПоСебестоимости.Цена) КАК ЦенаМин,
	|	МАКСИМУМ(ТаблицаЦенПоСебестоимости.Цена) КАК ЦенаМакс,
	|	СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.Цена) КАК ЦенаСред
	|ПОМЕСТИТЬ ТаблицаЦенПоСебестоимости
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПартииТоваровКомпанииОбороты.СкладКомпании КАК СкладКомпании,
	|		ПартииТоваровКомпанииОбороты.Номенклатура КАК Номенклатура,
	|		ПартииТоваровКомпанииОбороты.Партия КАК Партия,
	|		ПартииТоваровКомпанииОбороты.СтатусПартии КАК СтатусПартии,
	|		ВЫБОР
	|			КОГДА ПартииТоваровКомпанииОбороты.КоличествоОборот = 0
	|				ТОГДА 0
	|			ИНАЧЕ ПартииТоваровКомпанииОбороты.СуммаОборот / ПартииТоваровКомпанииОбороты.КоличествоОборот
	|		КОНЕЦ КАК Цена
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровКомпании.Обороты(&ДатаНач, &ДатаКон, , "+ТекстОтбора+") КАК ПартииТоваровКомпанииОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ 
	|		ПартииТоваровКомпанииОстатки.СкладКомпании,
	|		ПартииТоваровКомпанииОстатки.Номенклатура,
	|		ПартииТоваровКомпанииОстатки.Партия,
	|		ПартииТоваровКомпанииОстатки.СтатусПартии,
	|		ВЫБОР
	|			КОГДА ПартииТоваровКомпанииОстатки.КоличествоОстаток = 0
	|				ТОГДА 0
	|			ИНАЧЕ ПартииТоваровКомпанииОстатки.СуммаОстаток / ПартииТоваровКомпанииОстатки.КоличествоОстаток
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровКомпании.Остатки(&ДатаКонГраница, "+ТекстОтбора+") КАК ПартииТоваровКомпанииОстатки) КАК ТаблицаЦенПоСебестоимости
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦенПоСебестоимости.СкладКомпании,
	|	ТаблицаЦенПоСебестоимости.Номенклатура,
	|	ТаблицаЦенПоСебестоимости.Партия,
	|	ТаблицаЦенПоСебестоимости.СтатусПартии
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|";
	
	Возврат Текст;
	
КонецФункции

Функция ПолучитьТаблицуПодразделений(ПодразделениеЦены)
	
	ТаблицаПодразделений = Новый ТаблицаЗначений;
	ТаблицаПодразделений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ТаблицаПодразделений.Колонки.Добавить("Уровень",       Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	
	Уровень     = 0;
	ТекРодитель = ПодразделениеЦены;
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		НоваяСтрока = ТаблицаПодразделений.Добавить();
		НоваяСтрока.Уровень       = Уровень;
		НоваяСтрока.Подразделение = ТекРодитель;
		
		Уровень = Уровень + 1;
		ТекРодитель = ТекРодитель.Родитель;
	КонецЦикла;
	
	НоваяСтрока = ТаблицаПодразделений.Добавить();
	НоваяСтрока.Уровень       = Уровень;
	НоваяСтрока.Подразделение = Справочники.ПодразделенияКомпании.ПустаяСсылка();
	
	Возврат ТаблицаПодразделений;
	
КонецФункции

// формирует текст запроса по ценам
Функция ПолучитьТекстЗапросаТаблицыЦены(Запрос, ТипЦен, АлгоритмПолученияЦены)
	
	РабочийТипЦен = ТипЦен;
	ШаблонЦена = "ТаблицаЦен.Цена";
	
	// Если расчетная цена получим базовую цену
	Счетчик = 1;
	ТекстСоединенийПроцентыСкидкиНаценки = "";
	Пока РабочийТипЦен.Рассчитывается Цикл
		
		Если РабочийТипЦен.ПроцентыСкидкиНаценки.Количество()>0 Тогда
			
			ИмяТаблицы = "ПроцентыСкидкиНаценки" + Строка(Счетчик);
			ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК " + ИмяТаблицы + "
			|ПО
			|	" + ИмяТаблицы + ".Ссылка = &ТипЦены" + Строка(Счетчик) + " И 
			|	ТаблицаЦен.Номенклатура.ТипНоменклатуры = " + ИмяТаблицы + ".ТипНоменклатуры";
			
			ШаблонЦена = "("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
			
			Запрос.УстановитьПараметр("ТипЦены" + Строка(Счетчик), РабочийТипЦен);
			
			Счетчик = Счетчик + 1;
			
		ИначеЕсли РабочийТипЦен.ПроцентСкидкиНаценки <> 0 Тогда
			
			ШаблонЦена = "("+ШаблонЦена+"+"+ШаблонЦена+"*"+Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100, "ЧРД=.")+")";
			
		КонецЕсли;
		
		РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
		
	КонецЦикла;
	
	АлгоритмПолученияЦены = РабочийТипЦен.АлгоритмПолученияЦены;

	Если Не РабочийТипЦен.ВВалютеУчета Тогда
		Запрос.УстановитьПараметр("ВалютаЦены", РабочийТипЦен.ВалютаЦены);
	КонецЕсли;
	Запрос.УстановитьПараметр("ТипЦен", РабочийТипЦен);
	
	ТекстРезультат = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПодразделений.Подразделение КАК Подразделение,
	|	ТаблицаПодразделений.Уровень КАК Уровень
	|ПОМЕСТИТЬ
	|	ТаблицаПодразделений
	| ИЗ
	|	&ТаблицаПодразделений КАК ТаблицаПодразделений
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаЦен.Период КАК Период,
	|	ТаблицаПодразделений.Уровень КАК Уровень,
	|	ТаблицаЦен.Цена КАК Цена
	|ПОМЕСТИТЬ
	|	ВремТаблицаЦен
	|ИЗ
	|	ТаблицаПодразделений КАК ТаблицаПодразделений
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	РегистрСведений.Цены КАК ТаблицаЦен
	|ПО
	|	ТаблицаПодразделений.Подразделение = ТаблицаЦен.ПодразделениеКомпании
	|ГДЕ 
	|	ТаблицаЦен.Период<=&ДатаКон И 
	|	ТипЦен = &ТипЦен И 
	|	ТаблицаЦен.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) И 
	|	ТаблицаЦен.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) И 
	|	((ТаблицаЦен.ЕдиницаИзмерения.ЕдиницаПоКлассификатору = ТаблицаЦен.Номенклатура.БазоваяЕдиницаИзмерения И ТаблицаЦен.ЕдиницаИзмерения.Коэффициент=1) ИЛИ
	|	(ТаблицаЦен.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) И 
	|	НЕ ТаблицаЦен.Номенклатура.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров))
	|{ГДЕ
	|	(Номенклатура).* КАК Номенклатура
	|	}
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаПодразделений
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МАКСИМУМ(ТаблицаЦен.Период) КАК Период
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ВремТаблицаЦен КАК ТаблицаЦен
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦен.Номенклатура,
	|	ТаблицаЦен.ЕдиницаИзмерения
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|ВЫБРАТЬ
	|	ОптимальнаяТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ОптимальнаяТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаЦен.Уровень КАК Уровень,
	|	МАКСИМУМ(ТаблицаЦен.Цена) КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦенПодразделений
	|ИЗ
	|	СрезПоследних КАК ОптимальнаяТаблицаЦен
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ВремТаблицаЦен КАК ТаблицаЦен
	|ПО
	|	ОптимальнаяТаблицаЦен.Номенклатура     = ТаблицаЦен.Номенклатура И 
	|	ОптимальнаяТаблицаЦен.ЕдиницаИзмерения = ТаблицаЦен.ЕдиницаИзмерения И 
	|	ОптимальнаяТаблицаЦен.Период           = ТаблицаЦен.Период
	|ГДЕ
	|	НЕ ТаблицаЦен.Цена = 0
	|СГРУППИРОВАТЬ ПО
	|	ОптимальнаяТаблицаЦен.Номенклатура,
	|	ОптимальнаяТаблицаЦен.ЕдиницаИзмерения,
	|	ТаблицаЦен.Уровень
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|УНИЧТОЖИТЬ
	|	ВремТаблицаЦен
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследних
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаЦенПодразделений.Номенклатура КАК Номенклатура,
	|	ТаблицаЦенПодразделений.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МИНИМУМ(ТаблицаЦенПодразделений.Уровень) КАК Уровень
	|ПОМЕСТИТЬ
	|	СрезПоследних
	|ИЗ
	|	ТаблицаЦенПодразделений КАК ТаблицаЦенПодразделений
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦенПодразделений.Номенклатура,
	|	ТаблицаЦенПодразделений.ЕдиницаИзмерения
	|;
	|
	|ВЫБРАТЬ
	|	ОптимальнаяТаблицаЦен.Номенклатура КАК Номенклатура,
	|	ОптимальнаяТаблицаЦен.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	" + ШаблонЦена + "*КурсыВалютСрезПоследних.Курс/ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) КАК Цена
	|ПОМЕСТИТЬ
	|	ТаблицаЦен
	|ИЗ
	|	СрезПоследних КАК ОптимальнаяТаблицаЦен
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаЦенПодразделений КАК ТаблицаЦен
	|ПО
	|	ОптимальнаяТаблицаЦен.Номенклатура     = ТаблицаЦен.Номенклатура И 
	|	ОптимальнаяТаблицаЦен.ЕдиницаИзмерения = ТаблицаЦен.ЕдиницаИзмерения И 
	|	ОптимальнаяТаблицаЦен.Уровень          = ТаблицаЦен.Уровень
	|"+ ?(РабочийТипЦен.ВВалютеУчета, "ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКон, ) КАК КурсыВалютСрезПоследних
	|ПО ОптимальнаяТаблицаЦен.Номенклатура.ВалютаУчета = КурсыВалютСрезПоследних.Валюта","ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКон, Валюта=&ВалютаЦены) КАК КурсыВалютСрезПоследних
	|ПО ИСТИНА")+"
	|" + ТекстСоединенийПроцентыСкидкиНаценки + "
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|УНИЧТОЖИТЬ
	|	ТаблицаЦенПодразделений
	|;
	|
	|УНИЧТОЖИТЬ
	|	СрезПоследних
	|;
	|";
	
	Возврат ТекстРезультат;
	
КонецФункции // ПолучитьТекстЗапросаТаблицыЦены()

// формирует текст запроса отчета
Функция ПолучитьОсновнойЗапрос()
	
	ТипЦен            = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ТипЦен",               КомпоновщикНастроек.Настройки);
	ПодразделениеЦены = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ПодразделениеЦены",    КомпоновщикНастроек.Настройки);
	ДатаНачала        = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаНачала",           КомпоновщикНастроек.Настройки);
	ДатаКонца         = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания",        КомпоновщикНастроек.Настройки);
	ДатаКонцаГраница  = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
	
	Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("ТипЦенПоУмолчанию", ТипЦенПоУмолчанию);
	//Запрос.УстановитьПараметр("КурсВалютыОтчета",  КурсВалюты.Курс/?(КурсВалюты.Кратность=0,1,КурсВалюты.Кратность));
	Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКонцаГраница);
	Запрос.УстановитьПараметр("ДатаНач",        НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКон",        КонецДня(ДатаКонца));
	
	// Текст запроса по себестоимости
	ТекстЗапросаРезультат = ПолучитьТекстЗапросаЦенСебестоимости(Запрос);
	// Текст запроса по ценам
	АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре;
	ТекстЗапросаРезультат = ТекстЗапросаРезультат + Символы.ПС + ПолучитьТекстЗапросаТаблицыЦены(Запрос, ТипЦен, АлгоритмПолученияЦены);
	// Таблица родителей подразделения
	ТаблицаПодразделений  = ПолучитьТаблицуПодразделений(ПодразделениеЦены);
	
	Запрос.УстановитьПараметр("ТаблицаПодразделений", ТаблицаПодразделений);
	
	ТекстСоединенияОбщейТаблицыЦен = "";
	ТекстВыборкиЦены = "";
	
	ТекстВыборкиПроцентов = ",
	|	МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) - СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред) КАК СредНаценка,
	|	МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) - МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) КАК МинНаценка,
	|	МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) - МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс) КАК МаксНаценка,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) = СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред) ТОГДА 0
	|		КОГДА СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред) = 0 ТОГДА 100
	|	ИНАЧЕ (МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) - СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред))/СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред)*100
	|	КОНЕЦ КАК ПроцентСредНаценки,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) = МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) ТОГДА 0
	|		КОГДА МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) = 0 ТОГДА 100
	|	ИНАЧЕ (МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) - МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин))/МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин)*100
	|	КОНЕЦ КАК ПроцентМинНаценки,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) = МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс) ТОГДА 0
	|		КОГДА МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс) = 0 ТОГДА 100
	|	ИНАЧЕ (МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) - МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс))/МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс)*100
	|	КОНЕЦ КАК ПроцентМаксНаценки";
	
	Если АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоХарактеристике Тогда
		
		ТекстВыборкиЦены = ",
		|	МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) КАК Цена";
		
		ТекстСоединенияОбщейТаблицыЦен = ТекстСоединенияОбщейТаблицыЦен + "
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен
		|ПО
		|	ТаблицаЦенПоСебестоимости.Номенклатура = ТаблицаЦен.Номенклатура И 
		|	ТаблицаЦен.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)";
		
	ИначеЕсли АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения Тогда
		ТекстВыборкиЦены = ",
		|	МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) КАК Цена";
		
		ТекстВыборкиПроцентов = ",
		|	МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) - СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред) КАК СредНаценка,
		|	МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) - МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) КАК МинНаценка,
		|	МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) - МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс) КАК МаксНаценка,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) = СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред) ТОГДА 0
		|		КОГДА МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) = 0 ТОГДА 100
		|		ИНАЧЕ (МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) - МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин))/МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин)*100
		|	КОНЕЦ КАК ПроцентСредНаценки,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) = МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) ТОГДА 0
		|		КОГДА МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) = 0 ТОГДА 100
		|		ИНАЧЕ (МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) - МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин))/МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин)*100
		|	КОНЕЦ КАК ПроцентМинНаценки,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) = МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс) ТОГДА 0
		|		КОГДА МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс) = 0 ТОГДА 100
		|		ИНАЧЕ (МАКСИМУМ(ЕСТЬNULL(ЕСТЬNULL(ТаблицаЦен.Цена, ТаблицаЦен2.Цена), 0)) - МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс))/МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс)*100
		|	КОНЕЦ КАК ПроцентМаксНаценки";
		
		ТекстСоединенияОбщейТаблицыЦен = "
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен
		|ПО 
		|	ТаблицаЦенПоСебестоимости.Номенклатура = ТаблицаЦен.Номенклатура И 
		|	ТаблицаЦен.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен2
		|ПО 
		|	ТаблицаЦенПоСебестоимости.Номенклатура = ТаблицаЦен2.Номенклатура И 
		|	ТаблицаЦен.Номенклатура ЕСТЬ NULL И
		|	ТаблицаЦен2.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)";
	Иначе
		ТекстВыборкиЦены = ",
		|	МАКСИМУМ(ЕСТЬNULL(ТаблицаЦен.Цена, 0)) КАК Цена";
		ТекстСоединенияОбщейТаблицыЦен = "
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен
		|ПО 
		|	ТаблицаЦенПоСебестоимости.Номенклатура = ТаблицаЦен.Номенклатура И 
		|	ТаблицаЦен.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)";
	КонецЕсли;
	
	УникальныеПоля = Новый Структура;
	ОтчетыСервер.КомпоновщикПолучитьПоляСтруктуры(КомпоновщикНастроек.Настройки.Структура, УникальныеПоля);
	КомпоновщикПолучитьПоляОтбора(КомпоновщикНастроек.Настройки.Отбор, УникальныеПоля);
	
	Замены = Новый Структура;
	Замены.Вставить("Организация", "СкладКомпании.Организация");
	Замены.Вставить("Подразделение", "СкладКомпании.Подразделение");
	
	ТекстВыборки      = "";
	ТекстГруппировки  = "";
	ИмяТаблицыЗапроса = "ТаблицаЦенПоСебестоимости";
	Для Каждого ТекЭлемент Из УникальныеПоля Цикл
		ВыражениеПоля = ?(Замены.Свойство(ТекЭлемент.Ключ), Замены[ТекЭлемент.Ключ], ТекЭлемент.Ключ);
		ТекстВыборки     = ТекстВыборки     + ?(ТекстВыборки     = "", "", ", ") + Символы.ПС + ИмяТаблицыЗапроса + "." + ВыражениеПоля + " КАК " + ТекЭлемент.Ключ;
		ТекстГруппировки = ТекстГруппировки + ?(ТекстГруппировки = "", "", ", ") + Символы.ПС + ИмяТаблицыЗапроса + "." + ВыражениеПоля;
	КонецЦикла;
	
	ТекстГруппировки = ?(ТекстГруппировки = "", "", "СГРУППИРОВАТЬ ПО") + ТекстГруппировки;
	
	Запрос.Текст = ТекстЗапросаРезультат+"
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМин) КАК ЦенаМин,
	|	МАКСИМУМ(ТаблицаЦенПоСебестоимости.ЦенаМакс) КАК ЦенаМакс,
	|	СРЕДНЕЕ(ТаблицаЦенПоСебестоимости.ЦенаСред) КАК ЦенаСред," + ТекстВыборки + ТекстВыборкиЦены + ТекстВыборкиПроцентов + "
	|ИЗ
	|	ТаблицаЦенПоСебестоимости КАК ТаблицаЦенПоСебестоимости " + ТекстСоединенияОбщейТаблицыЦен + "
	|" + ТекстГруппировки;
	
	Возврат Запрос;
	
КонецФункции

// устанавливает оформление табличного документа
Процедура УстановитьУсловноеОформление(Построитель)
	
	//Построитель.УсловноеОформление.Очистить();
	//
	//МассивПоказателей = Новый Массив;
	//МассивПоказателей.Добавить("СредНаценка");
	//МассивПоказателей.Добавить("МинНаценка");
	//МассивПоказателей.Добавить("МаксНаценка");
	//МассивПоказателей.Добавить("ПроцентСредНаценки");
	//МассивПоказателей.Добавить("ПроцентМинНаценки");
	//МассивПоказателей.Добавить("ПроцентМаксНаценки");
	//
	//Для Каждого ТекПоказатель Из МассивПоказателей Цикл
	//	НайденныйПоказатель = Показатели.Найти(ТекПоказатель);
	//	Если НЕ НайденныйПоказатель = Неопределено И НайденныйПоказатель.Использование Тогда
	//		ЭлементОформления = Построитель.УсловноеОформление.Добавить(НайденныйПоказатель.Имя);
	//		
	//		ОбластьОформления = ЭлементОформления.Область.Добавить(НайденныйПоказатель.Имя);
	//		ОбластьОформления.ТипОбласти = ТипОбластиОформления.Поле;
	//		
	//		ОтборОформления = ЭлементОформления.Отбор.Добавить(НайденныйПоказатель.Имя);
	//		ОтборОформления.ВидСравнения = ВидСравнения.Меньше;
	//		ОтборОформления.Значение = 0;
	//		ОтборОформления.Использование = Истина;
	//		
	//		ЭлементОформления.Оформление.ЦветТекста.Значение = WebЦвета.Красный;
	//		ЭлементОформления.Оформление.ЦветТекста.Использование = Истина;	
	//		ЭлементОформления.Использование = Истина;  
	//	КонецЕсли;
	//КонецЦикла;
	
КонецПроцедуры

// Стандартная процедура настройки схемы компоновщика данных
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// Стандартная процедура настройки компоновщика данных
//
Процедура ПриИзмененииВарианта(ФормаОтчета) Экспорт
	
	ПодразделениеЦены = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПодразделениеЦены"));
	Если НЕ ЗначениеЗаполнено(ПодразделениеЦены.Значение) Тогда
		ПодразделениеЦены.Значение = Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
	КонецЕсли;
	
	ТипЦен = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТипЦен"));
	Если НЕ ЗначениеЗаполнено(ТипЦен.Значение) Тогда
		ТипЦенПоУмолчанию = обПраво("ОсновнойТипЦенПродажи");
		Если обЗначениеНеЗаполнено(ТипЦенПоУмолчанию) Тогда
			ТипЦенПоУмолчанию = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
		КонецЕсли;
		ТипЦен.Значение = ТипЦенПоУмолчанию;
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииВарианта()

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	Запрос = ПолучитьОсновнойЗапрос();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаНаценки", Запрос.Выполнить());
	
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, ВнешниеНаборыДанных);
	
КонецПроцедуры

// проверяет настройки отчета на корректность заполнения, возвращает "истина", если корректно, "ложь" - если нет
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЕстьНоменклатура = ОтчетыСервер.КомпоновщикЕстьПолеВСтруктуре("Номенклатура", КомпоновщикНастроек.Настройки.Структура);
	
	Если НЕ ЕстьНоменклатура Тогда
		ПолеПоиска = ОтчетыСервер.КомпоновщикПолучитьПолеКомпоновки("Номенклатура");
		Счетчик = ОтчетыСервер.КомпоновщикСчетчикУсловийПоля(ПолеПоиска, КомпоновщикНастроек.Настройки.Отбор);
		Если НЕ Счетчик = 1 Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "В группировках должна присутствовать номенклатура!";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ТипЦен = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТипЦен")).Значение;
	Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не задан тип цен!";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
	ПодразделениеЦены = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПодразделениеЦены")).Значение;
	Если НЕ ЗначениеЗаполнено(ПодразделениеЦены) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не задано подразделение цены!";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры


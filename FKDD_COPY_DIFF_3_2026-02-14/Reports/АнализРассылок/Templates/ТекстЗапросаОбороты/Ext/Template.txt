ВЫБРАТЬ
	РассылкаПолучатели.Ссылка.Реклама КАК ИсточникИнформации,
	РассылкаПолучатели.Получатель КАК Клиент
ПОМЕСТИТЬ ТаблицаКлиентов
ИЗ
	Документ.Рассылка.Получатели КАК РассылкаПолучатели
ГДЕ
	РассылкаПолучатели.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
	
СГРУППИРОВАТЬ ПО
	РассылкаПолучатели.Ссылка.Реклама,
	РассылкаПолучатели.Получатель
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	#Область ИзмеренияПостроителяДляСвойств
	Рассылки.ИсточникИнформации							КАК ИсточникИнформации,	
	Рассылки.Клиент										КАК Клиент,
	#КонецОбласти
	ТаблицаРассылок.КоличествоРассылок					КАК КоличествоРассылок,
	ТаблицаРассылокКлиентов.КоличествоРассылокКлиентов	КАК КоличествоРассылокКлиентов,
	РеализацияТоваров.СуммаРеализацийТоваров			КАК СуммаРеализацийТоваров,
	ЗаказНаряды.СуммаЗаказНарядов						КАК СуммаЗаказНарядов,
	ЗаказыНаАвтомобили.СуммаЗаказаНаАвтомобиль			КАК СуммаЗаказаНаАвтомобиль,
	События.Событие										КАК Событие,
	0													КАК КоличествоСобытий
	//СВОЙСТВА
	
{ВЫБРАТЬ
	ИсточникИнформации.*	КАК ИсточникИнформации,
	Клиент.*				КАК Клиент,
	Событие.*					КАК Событие
}

ИЗ
	ТаблицаКлиентов КАК Рассылки
		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			РассылкаПолучатели.Реклама КАК Реклама,
            СУММА(1) КАК КоличествоРассылок
		ИЗ
			Документ.Рассылка КАК РассылкаПолучатели
		ГДЕ
			РассылкаПолучатели.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
		СГРУППИРОВАТЬ ПО
			РассылкаПолучатели.Реклама) КАК ТаблицаРассылок
		ПО Рассылки.ИсточникИнформации = ТаблицаРассылок.Реклама
		
		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			РассылкаПолучатели.Получатель КАК Клиент,
            КОЛИЧЕСТВО(РассылкаПолучатели.Ссылка) КАК КоличествоРассылокКлиентов
		ИЗ
			Документ.Рассылка.Получатели КАК РассылкаПолучатели
		ГДЕ
			РассылкаПолучатели.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
		СГРУППИРОВАТЬ ПО
			РассылкаПолучатели.Получатель) КАК ТаблицаРассылокКлиентов
		ПО Рассылки.Клиент = ТаблицаРассылокКлиентов.Клиент	
		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			РеализацияТоваров.Контрагент КАК Контрагент,
			СУММА(РеализацияТоваров.СуммаДокумента) КАК СуммаРеализацийТоваров
		ИЗ
			Документ.РеализацияТоваров КАК РеализацияТоваров
		ГДЕ
			РеализацияТоваров.Дата МЕЖДУ &ДатаНачалаОбращения И &ДатаКонцаОбращения
		
		СГРУППИРОВАТЬ ПО
			РеализацияТоваров.Контрагент) КАК РеализацияТоваров
		ПО (Рассылки.Клиент = РеализацияТоваров.Контрагент)
		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			ЗаказНаряд.Контрагент КАК Контрагент,
			СУММА(ЗаказНаряд.СуммаДокумента) КАК СуммаЗаказНарядов
		ИЗ
			Документ.ЗаказНаряд КАК ЗаказНаряд
		ГДЕ
			ЗаказНаряд.Дата МЕЖДУ &ДатаНачалаОбращения И &ДатаКонцаОбращения
		
		СГРУППИРОВАТЬ ПО
			ЗаказНаряд.Контрагент) КАК ЗаказНаряды
		ПО (Рассылки.Клиент = ЗаказНаряды.Контрагент)
		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			ЗаказНаАвтомобиль.Контрагент КАК Контрагент,
			СУММА(ЗаказНаАвтомобиль.СуммаДокумента) КАК СуммаЗаказаНаАвтомобиль
		ИЗ
			Документ.ЗаказНаАвтомобиль КАК ЗаказНаАвтомобиль
		ГДЕ
			ЗаказНаАвтомобиль.Дата МЕЖДУ &ДатаНачалаОбращения И &ДатаКонцаОбращения
		
		СГРУППИРОВАТЬ ПО
			ЗаказНаАвтомобиль.Контрагент) КАК ЗаказыНаАвтомобили
		ПО (Рассылки.Клиент = ЗаказыНаАвтомобили.Контрагент)
		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			СторонниеЛица.Ссылка КАК Событие,
			СторонниеЛица.Ссылка.ИсточникИнформации КАК ИсточникИнформации,
			СторонниеЛица.Ссылка.Контрагент КАК Контрагент,
			СУММА(1) КАК КоличествоСобытий
		ИЗ
			Документ.Событие.СторонниеЛица КАК СторонниеЛица
		ГДЕ
			СторонниеЛица.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
			И (НЕ СторонниеЛица.Ссылка.ДокументОснование ССЫЛКА Документ.Рассылка)
		
		СГРУППИРОВАТЬ ПО
			СторонниеЛица.Ссылка,
			СторонниеЛица.Ссылка.Контрагент) КАК События
		ПО (Рассылки.Клиент = События.Контрагент) И
		(Рассылки.ИсточникИнформации = События.ИсточникИнформации)
	//СОЕДИНЕНИЯ
	
{ГДЕ
	Рассылки.ИсточникИнформации КАК ИсточникИнформации,
	Рассылки.Клиент КАК Клиент
	//СВОЙСТВА
}

{УПОРЯДОЧИТЬ ПО
	ИсточникИнформации.*		КАК ИсточникИнформации,
	Клиент.*					КАК Клиент,
	Событие.*					КАК Событие
	//СВОЙСТВА
}

{ИТОГИ ПО
	ИсточникИнформации.*		КАК ИсточникИнформации,
	Клиент.*					КАК Клиент,
	Событие.*					КАК Событие
	//СВОЙСТВА
}

ИТОГИ
	ВЫБОР КОГДА (НЕ ИсточникИнформации ЕСТЬ NULL) И (Клиент ЕСТЬ NULL) ТОГДА МАКСИМУМ(КоличествоРассылок) ИНАЧЕ МАКСИМУМ(КоличествоРассылокКлиентов) КОНЕЦ КАК КоличествоРассылок,
	ВЫБОР КОГДА (НЕ ИсточникИнформации ЕСТЬ NULL) И (НЕ Клиент ЕСТЬ NULL) ТОГДА МАКСИМУМ(СуммаРеализацийТоваров) ИНАЧЕ 0 КОНЕЦ	КАК СуммаРеализацийТоваров,
	ВЫБОР КОГДА (НЕ ИсточникИнформации ЕСТЬ NULL) И (НЕ Клиент ЕСТЬ NULL) ТОГДА МАКСИМУМ(СуммаЗаказНарядов) ИНАЧЕ 0 КОНЕЦ			КАК СуммаЗаказНарядов,
	ВЫБОР КОГДА (НЕ ИсточникИнформации ЕСТЬ NULL) И (НЕ Клиент ЕСТЬ NULL) ТОГДА МАКСИМУМ(СуммаЗаказаНаАвтомобиль) ИНАЧЕ 0 КОНЕЦ	КАК СуммаЗаказаНаАвтомобиль,
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Событие)		КАК КоличествоСобытий
	//ИТОГИСВОЙСТВА
ПО
	ИсточникИнформации,
	Клиент
	
АВТОУПОРЯДОЧИВАНИЕ
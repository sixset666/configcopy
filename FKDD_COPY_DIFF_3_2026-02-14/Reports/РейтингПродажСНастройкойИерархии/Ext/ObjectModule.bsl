#Если Клиент Тогда
// Модуль отчета
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;   // хранит структуру итоговых выражений показателей
Перем ОтборПоИерархии;                                 // Отбор по иерархии
Перем ОтборПервых;                                     // Отбор первых
Перем РесурсыОтчета;                                   // Ресурсы отчета
Перем ЕстьПроцентПродажВОбщемОбороте;                  // флаг наличия процента продаж
Перем ЕстьПроцентНаценкиВОбщемОбороте;                 // флаг наличия процента наценки
Перем ЕстьПроцентСкидокВОбщемОбороте;                  // флаг наличия процента скидки
Перем ТипПоискаПоДереву;                               // тип поиска по дереву
Перем СтруктураПолейСтрок;                             // структура полей строк
	
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверяет корректность сортировки
//
// без параметров
// 
Процедура КорректностьСортировки()
		
	Сортировка = "";
	Сортировка2 = "";
		
	Если РежимСортировки = 1 Тогда	
		Сортировка = "Количество";
		Сортировка2 = "КоличествоОсн";
	ИначеЕсли РежимСортировки = 2 Тогда	
		Сортировка = "СуммаРегл";
		Сортировка2 = "СуммаУпр";
	ИначеЕсли РежимСортировки = 3 Тогда	
		Сортировка = "СебестоимостьРегл";
		Сортировка2 = "СебестоимостьУпр";
	ИначеЕсли РежимСортировки = 4 Тогда	
		Сортировка = "СуммаНаценкиРегл";
		Сортировка2 = "СуммаНаценкиУпр";
	ИначеЕсли РежимСортировки = 5 Тогда	
		Сортировка = "СуммаСкидки";
		Сортировка2 = "СуммаСкидки";
	ИначеЕсли РежимСортировки = 6 Тогда	
		Сортировка = "ПроцентСкидки";
		Сортировка2 = "ПроцентСкидки";
	Иначе
		Возврат;
	КонецЕсли;
		
	Показатель = Показатели.Найти(Сортировка, "Имя");
	Показатель2 = Показатели.Найти(Сортировка2, "Имя");
		
	Если НЕ Показатель.Использование И НЕ Показатель2.Использование Тогда
		Показатель.Использование = Истина;
	Конецесли;
		
КонецПроцедуры // КорректностьСортировки()
	
// Возвращает итог по ресурсам
//
// Параметры
//	Строка - Массив
//	Колонки - Массив
//	
// Возвращаемое значение:
//	Итог - Число
//
Функция ПолучитьИтогПоРесурсам(Строка, Колонки)
	Итог = 0;
	Для Каждого ТекКолонка Из Колонки Цикл
		Итог = Итог+Строка[ТекКолонка];
	КонецЦикла;
	Возврат Итог;
КонецФункции // ПолучитьИтогПоРесурсам()
	
// Получить настройку групп
//
// Параметры
//  ИмяИзмерения - Строка
//
Функция ПолучитьНастройкуГрупп(ИмяИзмерения) Экспорт
		
	ТекСтрока = ИзмеренияСтроки.Найти(ИмяИзмерения, "ПутьКДанным");
		
	Возврат Новый Структура("ВыбКоличество, флВыводитьПрочие", ?(ТекСтрока.ВыводитьПервые=0,1000,ТекСтрока.ВыводитьПервые), ТекСтрока.Прочие);
		
КонецФункции // ПолучитьНастройкуГрупп()
	
// Заполнить настройку групп
//
// Параметры
//	ИмяИзмерения - Строка
//	ДеревоГр - Произвольный
//	ВыбКоличество - Произвольный
//	флВыводитьПрочие - Произвольный
//	ВыбПоказатель - Строка
//
Процедура ЗаполнитьНастройкуГрупп(ИмяИзмерения=Неопределено, ДеревоГр=Неопределено, ВыбКоличество=Неопределено, флВыводитьПрочие=Неопределено, ВыбПоказатель=Неопределено) Экспорт
		
	Если ИмяИзмерения<>Неопределено Тогда
		ТекСтрока = ИзмеренияСтроки.Найти(ИмяИзмерения, "ПутьКДанным");
		ТипИзмерения = ?(ТекСтрока = Неопределено, "Элементы", Строка(ТекСтрока.ТипИзмерения));
		Если ДеревоГр<>Неопределено И ТипИзмерения = "Только иерархия" Тогда
			ИдентификаторПути = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "_");
			Если ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) И ПараметрыИзмеренийСтрок[ИдентификаторПути].Свойство("Группы") Тогда 
				ПараметрыИзмеренийСтрок[ИдентификаторПути].Группы = ДеревоГр.Скопировать();
			КонецЕсли;
		КонецЕсли;
			
		Если ВыбКоличество<>Неопределено Тогда
			ТекСтрока.ВыводитьПервые=?(ВыбКоличество=1000,0,ВыбКоличество);
		КонецЕсли;
			
		Если флВыводитьПрочие<>Неопределено Тогда
			ТекСтрока.Прочие=флВыводитьПрочие;
		КонецЕсли;
	КонецЕсли;
		
	Если ВыбПоказатель<>Неопределено Тогда
		Если ВыбПоказатель = "Количество" Тогда
			РежимСортировки = 1;
		ИначеЕсли ВыбПоказатель = "СуммаРегл" ИЛИ ВыбПоказатель = "СуммаУпр" Тогда
			РежимСортировки = 2;
		ИначеЕсли ВыбПоказатель = "СебестоимостьРегл" ИЛИ ВыбПоказатель = "СебестоимостьУпр" Тогда
			РежимСортировки = 3;
		ИначеЕсли ВыбПоказатель = "СуммаНаценкиРегл" ИЛИ ВыбПоказатель = "СуммаНаценкиУпр" Тогда
			РежимСортировки = 4;
		ИначеЕсли ВыбПоказатель = "СуммаСкидки" ИЛИ ВыбПоказатель = "СуммаСкидкиУпр" Тогда
			РежимСортировки = 5;
		ИначеЕсли ВыбПоказатель = "ПроцентСкидки" Тогда
			РежимСортировки = 6;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры // ЗаполнитьНастройкуГрупп()

// Упорядочивает дерево результатов
//
// Параметры
//	ДеревоРезультата - ДеревоЗначений
//
Процедура УпорядочитьДеревоРезультата(ДеревоРезультата)
		
	СтруктураСортировкиПоля = СтруктураСортировки();
	Если ТипЗнч(СтруктураСортировкиПоля) = Тип("Структура") Тогда
		Если СтруктураСортировкиПоля.НаправлениеСортировки = НаправлениеСортировки.Убыв Тогда
			ДеревоРезультата.Сортировать(СтруктураСортировкиПоля.Описание+" Убыв", Истина);
		Иначе
			ДеревоРезультата.Сортировать(СтруктураСортировкиПоля.Описание+" Возр", Истина);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры // УпорядочитьДеревоРезультата()

// Рассчитать прочие
//
// Параметры
//  ДеревоВыборки - КоллекцияСтрокДереваЗначений.
//  КоличествоИзмерений - Число
//  Индекс - Число
//  СтруктураИтогов - Массив
//
Процедура РассчитатьПрочие(ДеревоВыборки, КоличествоИзмерений, Знач Индекс, СтруктураИтогов)
		
	Если Индекс<КоличествоИзмерений Тогда
		Для Каждого ТекСтрока Из ДеревоВыборки.Строки Цикл
			РассчитатьПрочие(ТекСтрока, КоличествоИзмерений, Индекс+1, СтруктураИтогов);
		КонецЦикла;
	Иначе
		Для Каждого ТекРесурс Из РесурсыОтчета Цикл
			СтруктураИтогов[ТекРесурс] = СтруктураИтогов[ТекРесурс]+ДеревоВыборки[ТекРесурс];
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры // РассчитатьПрочие()
	
// Вставить строку дерева значений
//
// Параметры
//	Строка1 - Строка
//	Строка2 - Строка
//
Процедура ВставитьСтрокуДереваЗначений(Строка1, Строка2)
		
	Для Каждого ТекСтрока Из Строка2.Строки Цикл
		НоваяСтрока = Строка1.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		ВставитьСтрокуДереваЗначений(НоваяСтрока, ТекСтрока);
	КонецЦикла;
		
КонецПроцедуры // ВставитьСтрокуДереваЗначений()

// Сворачивает дерево значений
//
// Параметры
//	ДеревоЗначений - ДеревоЗначений
//	КолонкиСумм - Массив
Процедура СвернутьДеревоЗначений(ДеревоЗначений, КолонкиСумм)
		
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Группировка");
		
	КоличествоСтрок = ДеревоЗначений.Строки.Количество();
	Номер=0;
	Пока Номер < КоличествоСтрок Цикл
		ТекСтрока = ДеревоЗначений.Строки[Номер];
			
		СтруктураОтбора.Вставить(ТекСтрока.Группировка, ТекСтрока[ТекСтрока.Группировка]);
		СтруктураОтбора.Вставить("Группировка", ТекСтрока.Группировка);
			
		ТекСтроки = ДеревоЗначений.Строки.НайтиСтроки(СтруктураОтбора, Ложь);
			
		КоличествоСовпадений = ТекСтроки.Количество();
		Ном=0;
		Пока Ном < КоличествоСовпадений Цикл
			СверяемаяСтрока = ТекСтроки[Ном];
			Если ДеревоЗначений.Строки.Индекс(ТекСтрока)=ДеревоЗначений.Строки.Индекс(СверяемаяСтрока) Тогда Ном = Ном+1; Продолжить; КонецЕсли;
				
			Для Каждого ТекКолонки Из КолонкиСумм Цикл
				ТекСтрока[ТекКолонки] = ТекСтрока[ТекКолонки] + СверяемаяСтрока[ТекКолонки];
			КонецЦикла;
				
			ВставитьСтрокуДереваЗначений(ТекСтрока, СверяемаяСтрока);
			ДеревоЗначений.Строки.Удалить(СверяемаяСтрока);
			Ном = Ном+1;
			КоличествоСовпадений = КоличествоСовпадений-1;
			КоличествоСтрок = КоличествоСтрок-1;
		КонецЦикла;
		СвернутьДеревоЗначений(ТекСтрока, КолонкиСумм);
		Номер = Номер+1;
	КонецЦикла;
		
КонецПроцедуры // СвернутьДеревоЗначений()
	
// Если элемент иерархический, возвращает "истина", иначе "ложь"
//
// Параметры
//	ПутьКДанным - Строка
//
Функция ЭлементИерархический(ПутьКДанным) Экспорт
		
	Измерение = ДеревоДоступныхПолей.Строки.Найти(ПутьКДанным, "ПутьКДанным", Истина);
	Если Измерение <> Неопределено Тогда
		ТипИзмерения = Измерение.ТипЗначения;
		Если НЕ Справочники.ТипВсеСсылки().СодержитТип(ТипИзмерения.Типы()[0]) И
			НЕ ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипИзмерения.Типы()[0]) Тогда
			Возврат Ложь;
		Иначе
			ТекОбъект = Новый (ТипИзмерения.Типы()[0]);
			Если НЕ ТекОбъект.Метаданные().Иерархический Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Истина;
КонецФункции // ЭлементИерархический()
	
// Получает представление группы
//
// Параметры
// 	ТаблицаГрупп - ДеревоЗначений 
//
// Возвращаемое значение:
//	СтрПредставлений - Строка
//
Функция ПолучитьПредставлениеГруппы(ТаблицаГрупп) Экспорт
	Если ТаблицаГрупп = Неопределено Тогда Возврат ""; КонецЕсли;
	Если ТаблицаГрупп.Строки.Найти(Ложь, "Пометка", Истина) = Неопределено Тогда
		СтрПредставлений = "";
	Иначе
		СтрПредставлений = "!";
	КонецЕсли;
	Возврат СтрПредставлений
КонецФункции // ПолучитьПредставлениеГруппы()
	
// Получает отбор по иерархии
//
// Параметры
//   ТекПостроитель - ПостроительОтчета
//
Процедура ПолучитьОтборПоИерархии(ТекПостроитель)
		
	ОтборПоИерархии = Новый Структура();
	СтруктураОтбора = Новый Структура("Пометка", Истина);
	Для Каждого ТекИзмерения Из ТекПостроитель.ИзмеренияСтроки Цикл
		ИдентификаторПути = СтрЗаменить(ТекИзмерения.ПутьКДанным, ".", "_");
		Если НЕ ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) Тогда Продолжить; КонецЕсли;
		Если НЕ ПараметрыИзмеренийСтрок[ИдентификаторПути].Свойство("Группы") Тогда Продолжить; КонецЕсли;
		Если ПараметрыИзмеренийСтрок[ИдентификаторПути].Группы = Неопределено Тогда Продолжить; КонецЕсли;
		СтрокаПараметровИзмеренийСтроки=ПараметрыИзмеренийСтрок[ИдентификаторПути];
		ОтборПоИерархии.Вставить(ТекИзмерения.Имя, Новый СписокЗначений);
		СтрокиГрупп = СтрокаПараметровИзмеренийСтроки.Группы.Строки.НайтиСтроки(СтруктураОтбора,Истина);
		Инд=0;
		Пока Инд<СтрокиГрупп.Количество() Цикл
			ОтборПоИерархии[ТекИзмерения.Имя].Добавить(СтрокиГрупп.Получить(Инд).Ссылка);
			Инд = Инд+1;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // ПолучитьОтборПоИерархии()

// Создает дерево результата
//
// Параметры
//  Выборка - Выборка
//  ИзмеренияПостроителя - Массив
//  КоличествоИзмерений - Число
//  Индекс - Число
//  ДеревоРезультата - ДеревоЗначений
//	ЗначениеПоиска - Строка
//
Процедура ПолучитьДеревоРезультата(Выборка, ИзмеренияПостроителя, КоличествоИзмерений, Знач Индекс, ДеревоРезультата, ЗначениеПоиска);
		
	ИзмерениеСтроки = ИзмеренияПостроителя[Индекс];
	Если ОтборПоИерархии.Свойство(ИзмерениеСтроки) Тогда
		ТолькоГруппировки = Истина;
		СписокГрупп = ОтборПоИерархии[ИзмерениеСтроки];
	Иначе
		ТолькоГруппировки = Ложь;
	КонецЕсли;
		
	Пока Выборка.Следующий() Цикл
			
		ПересчетИтога = Истина;	
		ТипЗаписиВыборки = Выборка.ТипЗаписи();
			
		Инд = ?(ТипЗаписиВыборки = ТипЗаписиЗапроса.ИтогПоИерархии, Индекс, Индекс+1);	
		ТекГруппировка = Выборка.Группировка();
			
		ИдтиВГлубь = Инд < КоличествоИзмерений;
		Если ИдтиВГлубь Тогда
			СледВыборка = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, ИзмеренияПостроителя[Инд]);
			ИдтиВГлубь = СледВыборка.Количество()>0;
		КонецЕсли;
			
		Если ТолькоГруппировки Тогда
			Если СписокГрупп.НайтиПоЗначению(Выборка[ИзмерениеСтроки]) = Неопределено Тогда
				Если ТипЗаписиВыборки <> ТипЗаписиЗапроса.ИтогПоИерархии И НЕ ИдтиВГлубь Тогда Продолжить; КонецЕсли;
				ТекЗначениеПоиска = ?(ЗначениеПоиска = ТипПоискаПоДереву.НайденаГруппа И ИдтиВГлубь, ЗначениеПоиска, ТипПоискаПоДереву.НеНайденаГруппа);
				ПересчетИтога = Ложь ИЛИ НЕ ИдтиВГлубь;
			Иначе
				ТекЗначениеПоиска = ТипПоискаПоДереву.НайденаГруппа;
				Если ТипЗнч(ДеревоРезультата) = Тип("СтрокаДереваЗначений") Тогда
					ПересчетИтога = НЕ(Выборка[ТекГруппировка] = ДеревоРезультата[ТекГруппировка]);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ТекЗначениеПоиска = ЗначениеПоиска;
		КонецЕсли;
			
		Если (ТекЗначениеПоиска = ТипПоискаПоДереву.НеНайденаГруппа И ТипЗаписиВыборки <> ТипЗаписиЗапроса.ИтогПоИерархии) Тогда Продолжить; КонецЕсли;
			
		Если ПересчетИтога Тогда
			НоваяСтрока = ДеревоРезультата.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Группировка = ТекГруппировка;
		Иначе
			НоваяСтрока = ДеревоРезультата;	
		КонецЕсли;
			
		Если ИдтиВГлубь Тогда
			ПолучитьДеревоРезультата(СледВыборка, ИзмеренияПостроителя, КоличествоИзмерений, Инд, НоваяСтрока, ?(Инд = Индекс, ТекЗначениеПоиска, ТипПоискаПоДереву.НетГрупп));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ПолучитьДеревоРезультата()
	
// Создает дерево вывода
//
// Параметры
//  ДеревоВыборки - КоллекцияСтрокДереваЗначений.
//  ИзмеренияПостроителя - Массив
//  КоличествоИзмерений - Число
//  Индекс - Число
//  ТаблицаРезультата - ТаблицаЗначений
//	СтрокаРезультата - Структура
//	СуммируемыеПоля - Строка
//
Процедура ПолучитьДеревоВывода(ДеревоВыборки, ИзмеренияПостроителя, КоличествоИзмерений, Знач Индекс, ТаблицаРезультата, СтрокаРезультата, СуммируемыеПоля)
		
	Инд = Индекс+1;
	ИзмерениеСтроки = ИзмеренияПостроителя[Индекс];
		
	Если ОтборПервых.Свойство(ИзмерениеСтроки) Тогда
		ВыводитьПрочие = ОтборПервых[ИзмерениеСтроки].Прочие;
		ЧислоПозиций   = ОтборПервых[ИзмерениеСтроки].ВыводитьПервые;
	Иначе
		ЧислоПозиций = 0;
	КонецЕсли;
		
	ЭтоПрочие = Ложь;
	Для НомерСтроки = 0 По ДеревоВыборки.Количество()-1 Цикл
		Выборка = ДеревоВыборки[НомерСтроки];
		Если (НомерСтроки >= ЧислоПозиций) И (ЧислоПозиций<>0) Тогда
			Если ВыводитьПрочие Тогда
				ЭтоПрочие = Истина;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если ЭтоПрочие Тогда
			СтрокаРезультата.Вставить(ИзмерениеСтроки, "Прочие...");
			Если СтруктураПолейСтрок.Свойство(ИзмерениеСтроки) Тогда
				Для Каждого ТекДопПоле Из СтруктураПолейСтрок[ИзмерениеСтроки] Цикл
					СтрокаРезультата.Вставить(ТекДопПоле.Ключ, "Прочие...");
				КонецЦикла;
			КонецЕсли;
		Иначе
			СтрокаРезультата.Вставить(ИзмерениеСтроки, Выборка[ИзмерениеСтроки]);
			Если СтруктураПолейСтрок.Свойство(ИзмерениеСтроки) Тогда
				Для Каждого ТекДопПоле Из СтруктураПолейСтрок[ИзмерениеСтроки] Цикл
					СтрокаРезультата.Вставить(ТекДопПоле.Ключ, Выборка[ТекДопПоле.Ключ]);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
			
		Если Инд < КоличествоИзмерений Тогда
			ПолучитьДеревоВывода(Выборка.Строки, ИзмеренияПостроителя, КоличествоИзмерений, Инд, ТаблицаРезультата,СтрокаРезультата, СуммируемыеПоля);
		Иначе
			НоваяСтрока = ТаблицаРезультата.Добавить();
				
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, СуммируемыеПоля);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРезультата);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры // ПолучитьДеревоВывода()

// Возвращает таблицу источника данных
//
// Параметры
//   ТекПостроитель - ПостроительОтчета
//	 ТаблицаВывода - ТаблицаЗначений
//   ПоляНастройкиОтчета - структура
//
// Возвращаемое значение:
//   НоваяТаблицаВывода - ТаблицаЗначений
//
Функция ПолучитьТаблицуИсточникаДанных(ТекПостроитель, ТаблицаВывода, ПоляНастройкиОтчета) Экспорт
		
	ПолучитьОтборПоИерархии(ТекПостроитель);
		
	ИзмеренияПостроителя = Новый Массив();
	ОтборПервых = Новый Структура;
	СтруктураПолейСтрок = Новый Структура;
	
	Для Каждого ТекИзмерение Из ИзмеренияСтроки Цикл
		
		Если Не ТекИзмерение.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяИзмерения = СтрЗаменить(ТекИзмерение.ПутьКДанным,".","");
		ИзмеренияПостроителя.Добавить(ИмяИзмерения);
			
		ОтборПервых.Вставить(ИмяИзмерения, Новый Структура("ВыводитьПервые, Прочие", ТекИзмерение.ВыводитьПервые, ТекИзмерение.Прочие));
			
		Если НЕ СтруктураПолейСтрок.Свойство(ИмяИзмерения) Тогда
			СтруктураПолейСтрок.Вставить(ИмяИзмерения, Новый Структура());
		КонецЕсли;
			
		МассивПолей = ПоляНастройкиОтчета.Поля.НайтиСтроки(Новый Структура("ИмяРодителя", ИмяИзмерения));
		Для Каждого ТекПоле Из МассивПолей Цикл
			СтруктураПолейСтрок[ИмяИзмерения].Вставить(ТекПоле.Имя);
		КонецЦикла;
			
		МассивСвойств = ПоляНастройкиОтчета.Свойства.НайтиСтроки(Новый Структура("ИмяРодителя", ИмяИзмерения));
		Для Каждого ТекСвойство Из МассивСвойств Цикл
			Если ТекСвойство.ПутьКДанным = ТекИзмерение.ПутьКДанным+"."+ТекСвойство.Имя Тогда
				СтруктураПолейСтрок[ИмяИзмерения].Вставить(ТекСвойство.Имя);
			КонецЕсли;
		КонецЦикла
	КонецЦикла;	                                
	
	ДеревоРезультата = Новый ДеревоЗначений;
	ДеревоРезультата.Колонки.Добавить("Группировка");
	НоваяТаблицаВывода = Новый ТаблицаЗначений;
		
	Для Каждого ТекКолонка Из ТаблицаВывода.Колонки Цикл
		Если (ОтборПервых.Свойство(ТекКолонка.Имя)) И (ОтборПервых[ТекКолонка.Имя].Прочие) Тогда
			ТекТипЗначения = Новый ОписаниеТипов(ТекКолонка.ТипЗначения, "Строка",,, Новый КвалификаторыСтроки(9));
		Иначе
			ТекТипЗначения = ТекКолонка.ТипЗначения;
		КонецЕсли;
		
		НоваяТаблицаВывода.Колонки.Добавить(ТекКолонка.Имя, ТекТипЗначения);
		ДеревоРезультата.Колонки.Добавить(ТекКолонка.Имя, ТекТипЗначения);
	КонецЦикла;
		
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(15,4));
	СтруктураВычисляемыхПоказателей = Новый Структура ("ПроцентПродажВОбщемОбороте, ПроцентНаценкиВОбщемОбороте, ПроцентСкидокВОбщемОбороте");
		
	РесурсыОтчета = Новый Массив();
	Для Каждого ТекПоказатель Из Показатели Цикл
		Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
		ИмяРесурса = ТекПоказатель.Имя;
		Если НЕ СтруктураВычисляемыхПоказателей.Свойство(ИмяРесурса) Тогда
			РесурсыОтчета.Добавить(ИмяРесурса);
		КонецЕсли;
	КонецЦикла;
		
	// Проверим а выбраны ли обязательные показатели
	СтруктураНеобходимыхПоказателей = Новый Структура ("СуммаСкидки, СуммаРегл, СебестоимостьУпр, СуммаНаценкиУпр");
	Для Каждого ТекНужныйПоказатель Из СтруктураНеобходимыхПоказателей Цикл
		Если ТаблицаВывода.Колонки.Найти(ТекНужныйПоказатель.Ключ) = Неопределено Тогда
			РесурсыОтчета.Добавить(ТекНужныйПоказатель.Ключ);
			ДеревоРезультата.Колонки.Добавить(ТекНужныйПоказатель.Ключ, ОписаниеТиповЧисло);
			НоваяТаблицаВывода.Колонки.Добавить(ТекНужныйПоказатель.Ключ, ОписаниеТиповЧисло);
		КонецЕсли;
	КонецЦикла;
	
	ЕстьПроцентПродажВОбщемОбороте = ДеревоРезультата.Колонки.Найти("ПроцентПродажВОбщемОбороте")<>Неопределено;
	ЕстьПроцентНаценкиВОбщемОбороте = ДеревоРезультата.Колонки.Найти("ПроцентНаценкиВОбщемОбороте")<>Неопределено;
	ЕстьПроцентСкидокВОбщемОбороте = ДеревоРезультата.Колонки.Найти("ПроцентСкидокВОбщемОбороте")<>Неопределено;
		
	КоличествоИзмерений = ТекПостроитель.ИзмеренияСтроки.Количество();
		
	ПолучитьДеревоРезультата(ТекПостроитель.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, ТекПостроитель.ИзмеренияСтроки[0].Имя),ИзмеренияПостроителя ,КоличествоИзмерений , 0, ДеревоРезультата, ТипПоискаПоДереву.НетГрупп);
		
	СвернутьДеревоЗначений(ДеревоРезультата, РесурсыОтчета);
		
	УпорядочитьДеревоРезультата(ДеревоРезультата.Строки);
		
	СтрокаРезультата = Новый Структура();
		
	ПоляСвертки = "";
	СуммируемыеПоля = "";
		
	Для Каждого ТекКолонка Из НоваяТаблицаВывода.Колонки Цикл
		Если ТекКолонка.ТипЗначения.Типы()[0] = Тип("Число") Тогда
			СуммируемыеПоля = СуммируемыеПоля + ?(СуммируемыеПоля="", "",",")+ТекКолонка.Имя;
		Иначе
			ПоляСвертки = ПоляСвертки + ?(ПоляСвертки="", "",",")+ТекКолонка.Имя;
			СтрокаРезультата.Вставить(ТекКолонка.Имя);
		КонецЕсли;
	КонецЦикла;
		
	ПолучитьДеревоВывода(ДеревоРезультата.Строки, ИзмеренияПостроителя, КоличествоИзмерений, 0, НоваяТаблицаВывода, СтрокаРезультата, СуммируемыеПоля);
		
	НоваяТаблицаВывода.Свернуть(ПоляСвертки, СуммируемыеПоля);
		
	Если ЕстьПроцентПродажВОбщемОбороте Тогда
		НоваяТаблицаВывода.Колонки.Добавить("ИтогСуммаПродажСоСкидкой", ОписаниеТиповЧисло);
		НоваяТаблицаВывода.ЗаполнитьЗначения(НоваяТаблицаВывода.Итог("СуммаРегл"), "ИтогСуммаПродажСоСкидкой");
	КонецЕсли;
		
	Если ЕстьПроцентНаценкиВОбщемОбороте Тогда
		НоваяТаблицаВывода.Колонки.Добавить("ИтогСуммаТорговойНаценкиУпр", ОписаниеТиповЧисло);
		НоваяТаблицаВывода.ЗаполнитьЗначения(НоваяТаблицаВывода.Итог("СуммаНаценкиУпр"), "ИтогСуммаТорговойНаценкиУпр");
	КонецЕсли;
		
	Если ЕстьПроцентСкидокВОбщемОбороте Тогда
		НоваяТаблицаВывода.Колонки.Добавить("ИтогСуммаСкидки", ОписаниеТиповЧисло);
		НоваяТаблицаВывода.ЗаполнитьЗначения(НоваяТаблицаВывода.Итог("СуммаСкидки"), "ИтогСуммаСкидки");
	КонецЕсли;
		
	Возврат НоваяТаблицаВывода;
		
КонецФункции // ПолучитьТаблицуИсточникаДанных()
	
// Возвращает дерево групп для иерархического справочника (если тип измерения - иерархия)
//для остальных случаев возвращает Неопределено
//
// Параметры
//   ТекПоле - ИзмерениеПостроителяОтчета 
//
Функция ДеревоГрупп(ТекПоле) Экспорт
		
	ТипИзмерения = ?(ТекПоле = Неопределено, "Элементы", Строка(ТекПоле.ТипИзмерения));
	Если ТипИзмерения <> "Только иерархия" Тогда Возврат Неопределено; КонецЕсли;
		
	флИерархияГрупп = Ложь;                            
		
	Попытка
		ТипПоля = ЭтотОбъект.ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным,"ПутьКДанным", Истина).ТипЗначения.Типы()[0];
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипПоля);
		ВидИерархииОбъекта = ОбъектМетаданных.ВидИерархии;
		флИерархия = ОбъектМетаданных.Иерархический;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
		
	Если НЕ флИерархия Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Запрос = Новый Запрос();
	Если ВидИерархииОбъекта = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
		Запрос.Текст = "ВЫБРАТЬ
		|	ВыбСправочник.Ссылка КАК Ссылка,
		|	ВыбСправочник.Ссылка.Представление КАК Представление,
		|	ВЫБОР
		|   КОГДА ВыбСправочник.Родитель = &ПустаяСсылка ТОГДА ИСТИНА
		|	ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Пометка
		|	
		|ИЗ
		|	Справочник." + ОбъектМетаданных.Имя + " КАК ВыбСправочник
		|
		|ГДЕ
		|	ВыбСправочник.Ссылка.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка.Наименование ИЕРАРХИЯ";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		|	ВыбСправочник.Ссылка КАК Ссылка,
		|	ВыбСправочник.Ссылка.Представление КАК Представление,
		|	ИСТИНА КАК Пометка
		|	
		|ИЗ
		|	Справочник." + ОбъектМетаданных.Имя + " КАК ВыбСправочник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка.Наименование ИЕРАРХИЯ";
	КонецЕсли;
	Запрос.УстановитьПараметр("ПустаяСсылка", Справочники[ОбъектМетаданных.Имя].ПустаяСсылка());
		
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
КонецФункции // ДеревоГрупп()
	
// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
		
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
		
	Если ТипЗнч(ПараметрыИзмеренийСтрок) <> Тип("Структура") Тогда
		ПараметрыИзмеренийСтрок = Новый Структура();
	КонецЕсли;
		
	Для Каждого ТекИзмерение Из ИзмеренияСтроки Цикл
		ТаблицаГрупп = ДеревоГрупп(ТекИзмерение);
		ИдентификаторПути = СтрЗаменить(ТекИзмерение.ПутьКДанным, ".", "_");
		Если НЕ ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПути) Тогда
			ПараметрыИзмеренийСтрок.Вставить(ИдентификаторПути, Новый Структура("Группы, ГруппыПредставление", ТаблицаГрупп, ПолучитьПредставлениеГруппы(ТаблицаГрупп)));
		Иначе
			ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("Группы", ТаблицаГрупп);
			ПараметрыИзмеренийСтрок[ИдентификаторПути].Вставить("ГруппыПредставление", ПолучитьПредставлениеГруппы(ТаблицаГрупп));
		КонецЕсли;
	КонецЦикла;
		
	ТипПоискаПоДереву = Новый Структура;
	ТипПоискаПоДереву.Вставить("НетГрупп", 1);
	ТипПоискаПоДереву.Вставить("НайденаГруппа", 2);
	ТипПоискаПоДереву.Вставить("НеНайденаГруппа", 3);
		
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()
	
// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
		
	КорректностьСортировки();
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,1, глПрава);
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()
	
// формирует диаграмму
//
// Параметры
//  ПолеДиаграммы – Диаграмма
//  СтруктураПараметров - структура
//
// Возвращаемое значение:
//  ПолеДиаграммы - Диаграмма
//     
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
		
	КорректностьСортировки();
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров);
		
	Возврат ПолеДиаграммы;
		
КонецФункции // СформироватьДиаграмму()
	
// Функция возвращает структуру форматирования полей
Функция СтруктураФорматированияПолей() Экспорт
		
	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);
		
	// Если необходимо, добавим другие или переустановим строки форматирования полей
		
	Возврат СтруктураФорматаПолей;
		
КонецФункции	//	СтруктураФорматированияПолей()
	
// Формирует структуру сортировки
//
// Без параметров
//
// Возвращаемое значение:
//   СтруктураСортировкиПоля – Структура
//
Функция СтруктураСортировки() Экспорт
		
	СтруктураСортировкиПоля = Новый Структура("Описание, НаправлениеСортировки");
	Если НаправлениеСорт = 0 Тогда
		СтруктураСортировкиПоля.НаправлениеСортировки = НаправлениеСортировки.Убыв;
	Иначе
		СтруктураСортировкиПоля.НаправлениеСортировки = НаправлениеСортировки.Возр;
	КонецЕсли;
		
	Сортировка = "";
	Сортировка2 = "";
	Если РежимСортировки = 1 Тогда	
		Сортировка = "Количество";	
		Сортировка2 = "КоличествоОсн";
	ИначеЕсли РежимСортировки = 2 Тогда	
		Сортировка = "СуммаРегл";
		Сортировка2 = "СуммаУпр";
	ИначеЕсли РежимСортировки = 3 Тогда	
		Сортировка = "СебестоимостьРегл";
		Сортировка2 = "СебестоимостьУпр";
	ИначеЕсли РежимСортировки = 4 Тогда	
		Сортировка = "СуммаНаценкиРегл";
		Сортировка2 = "СуммаНаценкиУпр";
	ИначеЕсли РежимСортировки = 5 Тогда	
		Сортировка = "СуммаСкидки";
	ИначеЕсли РежимСортировки = 6 Тогда	
		Сортировка = "ПроцентСкидки";
	КонецЕсли;
		
	Если НЕ ПустаяСтрока(Сортировка) Тогда
		Для Каждого ТекПоказатель Из Показатели Цикл
			Если Сортировка = ТекПоказатель.Имя И (не ТекПоказатель.Использование) Тогда
				Сортировка = "";
			КонецЕсли;
			Если Сортировка2 = ТекПоказатель.Имя И (не ТекПоказатель.Использование) Тогда
				Сортировка2 = "";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Если НЕ ПустаяСтрока(Сортировка) Тогда
		СтруктураСортировкиПоля.Описание = Сортировка;
	ИначеЕсли НЕ ПустаяСтрока(Сортировка2) Тогда
		СтруктураСортировкиПоля.Описание = Сортировка2;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
	Возврат СтруктураСортировкиПоля;
КонецФункции // СтруктураФорматированияПолей()
	
//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	
ИмяРегистра = "Продажи";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
ТипПоискаПоДереву = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();
#КонецЕсли
#Если Клиент Тогда
// Модуль отчета
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем МакетОтчета, ОформлениеСтроки;     // переменные объекта
Перем ТаблицаПриход,ТаблицаРасход;       // переменные объекта
Перем СоответствиеХозОперацийПереоценки; // переменные объекта

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Заполняет начальные настройки
//
//Без параметров
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	ПереключательВидСуммы="Сумма";
	// фильтры
	ДоступныеПоляОтбора.Очистить();
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("СкладКомпании.Организация","Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("СкладКомпании.Подразделение","Подразделение",Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("СкладКомпании","Склад компании",Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании, СправочникСсылка.Цеха"));
	ПолеНастройки.Отбор=Истина;
	ПолеНастройки=ДоступныеПоляОтбора.Добавить("Номенклатура","Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура, СправочникСсылка.Автомобили"));
	ПолеНастройки.Отбор=Истина;
	ФильтрыОтчета.УстановитьДоступныеПоля(ДоступныеПоляОтбора);
	ФильтрыОтчета.Добавить("СкладКомпании.Организация","Организация","Организация");
	ФильтрыОтчета.Добавить("СкладКомпании.Подразделение","Подразделение","Подразделение");
	ФильтрыОтчета.Добавить("СкладКомпании", "СкладКомпании", "СкладКомпании/Цех");
	ФильтрыОтчета.Добавить("Номенклатура", "Номенклатура", "Номенклатура/Автомобиль");
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

//Формирует текст запроса
//
// Параметры
//   Реж - строка - режим запроса (Движения, Остатки)
//
// Возвращаемое значение:
//   ТекстЗапроса – текст
//
Функция ПолучитьТекстЗапроса(Реж="Движения")
	// формируем строку фильтров виртуальной таблицы
	
	СтрокаУсловия="";
	Для Каждого ЭлементОтбора Из ФильтрыОтчета Цикл
		Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
		СтрокаУсловия=СтрокаУсловия+?(СтрокаУсловия="",""," И ")+СтрЗаменить(СтрЗаменить(отСформироватьСтрокуВидаСравнения(ЭлементОтбора.ВидСравнения),"#","#ИмяТаблицы#"+ЭлементОтбора.ПутьКДанным),"&","&"+ЭлементОтбора.Имя);
	КонецЦикла;
	
	Если Реж="Движения" Тогда
		СтрокаУсловия = ?(СтрокаУсловия="","", "	И "+СтрокаУсловия);
		Если ПереключательВидСуммы = "СуммаПродажи" Тогда
			СтрокаУсловия = СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "ОстаткиТоваров.");
			ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|ОстаткиТоваров.Регистратор КАК Регистратор,
			|ОстаткиТоваров.Регистратор.Дата КАК ДатаДок,
			|ОстаткиТоваров.ХозОперация КАК ХозОперация,
			|СУММА(ВЫБОР КОГДА ОстаткиТоваров.Номенклатура.ВидНоменклатуры<>&Тара ТОГДА ОстаткиТоваров.СуммаРозн ИНАЧЕ 0 КОНЕЦ) КАК ТоварСуммаРозн,
			|СУММА(ВЫБОР КОГДА ОстаткиТоваров.Номенклатура.ВидНоменклатуры=&Тара ТОГДА ОстаткиТоваров.СуммаРозн ИНАЧЕ 0 КОНЕЦ) КАК ТараСуммаРозн
			|ИЗ
			|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваров
			|ГДЕ
			|	ОстаткиТоваров.Период МЕЖДУ &ДатаНачала И &ДатаКонца
			|   И ОстаткиТоваров.ВидДвижения=&ВидДвижения
			|	"+СтрокаУсловия+"
			|СГРУППИРОВАТЬ ПО 
			|	ОстаткиТоваров.Регистратор,
			|	ОстаткиТоваров.ХозОперация
			|ИМЕЮЩИЕ 
			|  СУММА(ЕСТЬNULL(ОстаткиТоваров.СуммаРозн,0))<>0
			|
			|УПОРЯДОЧИТЬ ПО ДатаДок, Регистратор, ХозОперация Возр";
		Иначе
			//Отбор партий
			СтрокаУсловияПартийТоваров	= СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "ПартииТоваровКомпании.");
			//Отбор по производству
			СтрокаУсловияВПроизводстве	= СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "ТоварыВПроизводстве.");
			СтрокаУсловияВПроизводстве	= СтрЗаменить(СтрокаУсловияВПроизводстве, "СкладКомпании", "Цех");
			СтрокаУсловияВПроизводстве	= СтрЗаменить(СтрокаУсловияВПроизводстве, "&Цех", "&СкладКомпании");
			//Отбор по Автомобилям
			СтрокаУсловияАвтомобилей	= СтрЗаменить(СтрокаУсловия, "Номенклатура","Автомобиль");
			СтрокаУсловияАвтомобилей	= СтрЗаменить(СтрокаУсловияАвтомобилей,"&Автомобиль","&Номенклатура");
			СтрокаУсловияКомплектации	= СтрЗаменить(СтрокаУсловияАвтомобилей, "#ИмяТаблицы#", "КомплектацияАвтомобилей.");
			СтрокаУсловияАвтомобилей	= СтрЗаменить(СтрокаУсловияАвтомобилей, "#ИмяТаблицы#", "ОстаткиАвтомобилей.");
			
			ТекстЗапроса = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КомплектацияАвтомобилей.Регистратор КАК Регистратор,
			|	// ТОВАРЫ
			|	СУММА(ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры<>&Тара ИЛИ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции ТОГДА КомплектацияАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-КомплектацияАвтомобилей.СуммаНДС","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТоварСумма,
			|	// ТАРА
			|	СУММА(ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры=&Тара И (НЕ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции) ТОГДА КомплектацияАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-КомплектацияАвтомобилей.СуммаНДС","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТараСумма
			|ПОМЕСТИТЬ
			|	КомплектацияАвтомобилейПлюс
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			|ГДЕ
			|	КомплектацияАвтомобилей.Период МЕЖДУ &ДатаНачала И &ДатаКонца
			|	И КомплектацияАвтомобилей.ВидДвижения=&ВидДвижения И КомплектацияАвтомобилей.Сумма > 0
			|	"+СтрокаУсловияКомплектации+"
			|СГРУППИРОВАТЬ ПО 
			|	КомплектацияАвтомобилей.Регистратор
			|;
			|
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КомплектацияАвтомобилей.Регистратор КАК Регистратор,
			|	// ТОВАРЫ
			|	-СУММА(ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры<>&Тара ИЛИ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции ТОГДА КомплектацияАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-КомплектацияАвтомобилей.СуммаНДС","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТоварСумма,
			|	// ТАРА
			|	-СУММА(ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры=&Тара И (НЕ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции) ТОГДА КомплектацияАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-КомплектацияАвтомобилей.СуммаНДС","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТараСумма
			|ПОМЕСТИТЬ
			|	КомплектацияАвтомобилейМинус
			|ИЗ
			|	РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
			|ГДЕ
			|	КомплектацияАвтомобилей.Период МЕЖДУ &ДатаНачала И &ДатаКонца
			|	И (НЕ КомплектацияАвтомобилей.ВидДвижения=&ВидДвижения) И КомплектацияАвтомобилей.Сумма < 0
			|	"+СтрокаУсловияКомплектации+"
			|СГРУППИРОВАТЬ ПО 
			|	КомплектацияАвтомобилей.Регистратор
			|;
			|
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ *
			|ИЗ
			|(
			|ВЫБРАТЬ
			|	ПартииТоваровКомпании.Регистратор КАК Регистратор,
			|	ПартииТоваровКомпании.Регистратор.Дата КАК ДатаДок,
			|	ПартииТоваровКомпании.ХозОперация КАК ХозОперация,
			|// ТОВАРЫ
			|	СУММА(ВЫБОР КОГДА ПартииТоваровКомпании.Номенклатура.ВидНоменклатуры<>&Тара ТОГДА ЕСТЬNULL(ПартииТоваровКомпании.Сумма, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ПартииТоваровКомпании.СуммаНДС, 0)","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТоварСумма,
			|// ТАРА
			|	СУММА(ВЫБОР КОГДА ПартииТоваровКомпании.Номенклатура.ВидНоменклатуры=&Тара ТОГДА ЕСТЬNULL(ПартииТоваровКомпании.Сумма, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ПартииТоваровКомпании.СуммаНДС, 0)","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТараСумма
			|ИЗ
			|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
			|
			|ГДЕ
			|	ПартииТоваровКомпании.Период МЕЖДУ &ДатаНачала И &ДатаКонца
			|	И ПартииТоваровКомпании.ВидДвижения=&ВидДвижения
			|	"+СтрокаУсловияПартийТоваров+"
			|
			|СГРУППИРОВАТЬ ПО
			|	ПартииТоваровКомпании.Регистратор,
			|	ПартииТоваровКомпании.ХозОперация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТоварыВПроизводстве.Регистратор,
			|	ТоварыВПроизводстве.Регистратор.Дата,
			|	ТоварыВПроизводстве.ХозОперация,
			|// ТОВАРЫ
			|	СУММА(ВЫБОР КОГДА ТоварыВПроизводстве.Номенклатура.ВидНоменклатуры<>&Тара ТОГДА ЕСТЬNULL(ТоварыВПроизводстве.Сумма, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ТоварыВПроизводстве.СуммаНДС, 0)","")+" ИНАЧЕ 0 КОНЕЦ),
			|// ТАРА
			|	СУММА(ВЫБОР КОГДА ТоварыВПроизводстве.Номенклатура.ВидНоменклатуры=&Тара ТОГДА ЕСТЬNULL(ТоварыВПроизводстве.Сумма, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ТоварыВПроизводстве.СуммаНДС, 0)","")+" ИНАЧЕ 0 КОНЕЦ)
			|ИЗ
			|	РегистрНакопления.ТоварыВПроизводстве КАК ТоварыВПроизводстве
			|ГДЕ
			|	ТоварыВПроизводстве.Период МЕЖДУ &ДатаНачала И &ДатаКонца
			|	И ТоварыВПроизводстве.ВидДвижения=&ВидДвижения
			|	"+СтрокаУсловияВПроизводстве+"
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыВПроизводстве.Регистратор,
			|	ТоварыВПроизводстве.ХозОперация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОстаткиАвтомобилей.Регистратор,
			|	ОстаткиАвтомобилей.Регистратор.Дата,
			|	ОстаткиАвтомобилей.ХозОперация,
			|// ТОВАРЫ
			|	СУММА(ОстаткиАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-ОстаткиАвтомобилей.СуммаНДС","")+") +
			|	МАКСИМУМ(ЕСТЬNULL(КомплектацияАвтомобилей.ТоварСумма, 0)) КАК ТоварСумма,
			|// ТАРА
			|	МАКСИМУМ(ЕСТЬNULL(КомплектацияАвтомобилей.ТараСумма, 0)) КАК ТараСумма
			|ИЗ
			|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	КомплектацияАвтомобилейПлюс КАК КомплектацияАвтомобилей
			|ПО
			|	ОстаткиАвтомобилей.Регистратор = КомплектацияАвтомобилей.Регистратор
			|ГДЕ
			|	ОстаткиАвтомобилей.Период МЕЖДУ &ДатаНачала И &ДатаКонца И 
			|	ОстаткиАвтомобилей.ВидДвижения=&ВидДвижения И ОстаткиАвтомобилей.Сумма > 0
			|	"+СтрокаУсловияАвтомобилей+"
			|СГРУППИРОВАТЬ ПО
			|	ОстаткиАвтомобилей.Регистратор,
			|	ОстаткиАвтомобилей.ХозОперация
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОстаткиАвтомобилей.Регистратор,
			|	ОстаткиАвтомобилей.Регистратор.Дата,
			|	ОстаткиАвтомобилей.ХозОперация,
			|// ТОВАРЫ
			|	-СУММА(ОстаткиАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-ОстаткиАвтомобилей.СуммаНДС","")+") +
			|	-МАКСИМУМ(ЕСТЬNULL(КомплектацияАвтомобилей.ТоварСумма, 0)) КАК ТоварСумма,
			|// ТАРА
			|	-МАКСИМУМ(ЕСТЬNULL(КомплектацияАвтомобилей.ТараСумма, 0)) КАК ТараСумма
			|ИЗ
			|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	КомплектацияАвтомобилейМинус КАК КомплектацияАвтомобилей
			|ПО
			|	ОстаткиАвтомобилей.Регистратор = КомплектацияАвтомобилей.Регистратор
			|ГДЕ
			|	ОстаткиАвтомобилей.Период МЕЖДУ &ДатаНачала И &ДатаКонца И
			|	(НЕ ОстаткиАвтомобилей.ВидДвижения=&ВидДвижения) И ОстаткиАвтомобилей.Сумма < 0
			|	"+СтрокаУсловияАвтомобилей+"
			|СГРУППИРОВАТЬ ПО
			|	ОстаткиАвтомобилей.Регистратор,
			|	ОстаткиАвтомобилей.ХозОперация
			|
			|) КАК ОбъединенныйЗапрос
			|
			|УПОРЯДОЧИТЬ ПО ОбъединенныйЗапрос.ДатаДок, ОбъединенныйЗапрос.Регистратор, ХозОперация";
		КонецЕсли;
		
	ИначеЕсли Реж="Остатки" Тогда
		// формируем текст запроса
		СтрокаУсловия	= СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "");
		Если ПереключательВидСуммы = "СуммаПродажи" Тогда
			ТекстЗапроса="ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СУММА(ВЫБОР КОГДА ОстаткиТоваров.Номенклатура.ВидНоменклатуры <> &Тара ТОГДА ОстаткиТоваров.СуммаРознОстаток ИНАЧЕ 0 КОНЕЦ) КАК ТоварОстРозн,
			|	СУММА(ВЫБОР КОГДА ОстаткиТоваров.Номенклатура.ВидНоменклатуры = &Тара ТОГДА ОстаткиТоваров.СуммаРознОстаток ИНАЧЕ 0 КОНЕЦ) КАК ТараОстРозн
			|ИЗ
			|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&НаДату, "+СтрокаУсловия+") КАК ОстаткиТоваров";
		Иначе
			ТекстЗапроса="ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СУММА(ВЫБОР КОГДА ПартииТоваровКомпании.Номенклатура.ВидНоменклатуры <> &Тара ТОГДА ЕСТЬNULL(ПартииТоваровКомпании.СуммаОстаток, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ПартииТоваровКомпании.СуммаНДСОстаток, 0)","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТоварОст,
			|	СУММА(ВЫБОР КОГДА ПартииТоваровКомпании.Номенклатура.ВидНоменклатуры = &Тара ТОГДА ЕСТЬNULL(ПартииТоваровКомпании.СуммаОстаток, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ПартииТоваровКомпании.СуммаНДСОстаток, 0)","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТараОст
			|ИЗ
			|	РегистрНакопления.ПартииТоваровКомпании.Остатки(&НаДату, "+СтрокаУсловия+") КАК ПартииТоваровКомпании";
		КонецЕсли;
	ИначеЕсли Реж="ОстаткиДеталей" Тогда
		СтрокаУсловия	= СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "");
		СтрокаУсловия	= СтрЗаменить(СтрокаУсловия, "СкладКомпании", "Цех");
		СтрокаУсловия	= СтрЗаменить(СтрокаУсловия, "&Цех", "&СкладКомпании");
		
		// формируем текст запроса
		ТекстЗапроса="
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ВЫБОР КОГДА ТоварыВПроизводстве.Номенклатура.ВидНоменклатуры <> &Тара ТОГДА ЕСТЬNULL(ТоварыВПроизводстве.СуммаОстаток, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ТоварыВПроизводстве.СуммаНДСОстаток, 0)","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТоварОст,
		|	СУММА(ВЫБОР КОГДА ТоварыВПроизводстве.Номенклатура.ВидНоменклатуры = &Тара ТОГДА ЕСТЬNULL(ТоварыВПроизводстве.СуммаОстаток, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ТоварыВПроизводстве.СуммаНДСОстаток, 0)","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТараОст
		|ИЗ
		|	РегистрНакопления.ТоварыВПроизводстве.Остатки(&НаДату,"+СтрокаУсловия+") КАК ТоварыВПроизводстве";
		
	ИначеЕсли Реж="ОстаткиАвтомобилей" Тогда
		СтрокаУсловия	= СтрЗаменить(СтрокаУсловия, "#ИмяТаблицы#", "");
		СтрокаУсловия	= СтрЗаменить(СтрокаУсловия, "Номенклатура","Автомобиль");
		СтрокаУсловия	= СтрЗаменить(СтрокаУсловия,"&Автомобиль","&Номенклатура");
		// формируем текст запроса
		ТекстЗапроса="
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ТоварОст) КАК ТоварОст,
		|	СУММА(ТараОст) КАК ТараОст
		|ИЗ
		|(
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ОстаткиАвтомобилей.СуммаОстаток, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(ОстаткиАвтомобилей.СуммаНДСОстаток, 0)","")+" КАК ТоварОст,
		|	0 КАК ТараОст
		|ИЗ
		|	РегистрНакопления.ОстаткиАвтомобилей.Остатки(&НаДату,"+СтрокаУсловия+") КАК ОстаткиАвтомобилей
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры <> &Тара ИЛИ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции ТОГДА ЕСТЬNULL(КомплектацияАвтомобилей.СуммаОстаток, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(КомплектацияАвтомобилей.СуммаНДСОстаток, 0)","")+" ИНАЧЕ 0 КОНЕЦ,
		|	ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры = &Тара И (НЕ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции) ТОГДА ЕСТЬNULL(КомплектацияАвтомобилей.СуммаОстаток, 0)"+?(ПереключательВидСуммы="БезНДС","-ЕСТЬNULL(КомплектацияАвтомобилей.СуммаНДСОстаток, 0)","")+" ИНАЧЕ 0 КОНЕЦ
		|ИЗ 
		|	РегистрНакопления.КомплектацияАвтомобилей.Остатки(&НаДату,"+СтрокаУсловия+") КАК КомплектацияАвтомобилей
		|
		|) КАК ОбъединенныйЗапрос
		|";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапроса()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент (ДокРезультат=Неопределено) Экспорт
		
	Если ТипЗнч(ДокРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокРезультат) <> Тип("ТабличныйДокумент") Тогда
		ДокРезультат = Новый ТабличныйДокумент();
	Иначе ДокРезультат.Очистить();
	КонецЕсли;
		
	// проверим корректность заполнения
	Если ДатаНачала > ДатаКонца И ДатаКонца <> '00010101000000' Тогда
		Предупреждение("Дата начала периода не может быть больше даты конца периода", 60);
		Возврат ДокРезультат;
	КонецЕсли;
	
		
	Если обПраво("ПредупреждатьОНарушенииПоследовательностей", глПрава) Тогда
		
		ОбработкаВосстановлениеПоследовательностей = Обработки.ВосстановлениеПоследовательностей.Создать();
		
		ПоследовательностиОтчета = Новый Структура;
		ПоследовательностиОтчета.Вставить("ГраницыПартий");
		ПоследовательностиОтчета.Вставить("ГраницыПроизводство");
		ПоследовательностиОтчета.Вставить("ГраницыАвтомобили");
		ПоследовательностиОтчета.Вставить("ГраницыКомплектация");
		ТаблицаПоследовательностей = ОбработкаВосстановлениеПоследовательностей.ТаблицаПоследовательностей;
		КоличествоПоследовательностей = ТаблицаПоследовательностей.Количество()-1;
		
		Пока КоличествоПоследовательностей>=0 Цикл
			ТекПоследовательность = ТаблицаПоследовательностей[КоличествоПоследовательностей];
			Если Не ПоследовательностиОтчета.Свойство(ТекПоследовательность.ИмяГраницы) Тогда
				ТаблицаПоследовательностей.Удалить(ТекПоследовательность);
			КонецЕсли;
			КоличествоПоследовательностей = КоличествоПоследовательностей-1;
		КонецЦикла;
		
		Если ТаблицаПоследовательностей.Количество()>0 Тогда
			ТаблицаГраниц = ОбработкаВосстановлениеПоследовательностей.ПолучитьТаблицуНеактуальныхДокументов(ДатаКонца);
			ТекстПредупреждения = "";
			Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
				Граница = ТаблицаГраниц.Найти(ТекПоследовательность.ИмяГраницы,"ИмяПоследовательности");
				Если Граница <> Неопределено Тогда
					ТекстАктуальна = "";
					Если Граница.Граница=Неопределено Тогда
						ТекстАктуальна	= Формат(Граница.МоментВремени,"ДФ=dd.MM.yyyy");
					Иначе
						ТекстАктуальна	= Формат(Граница.Граница.Дата,"ДФ=dd.MM.yyyy")+" - "+Граница.Граница.Метаданные().Синоним+" № "+СокрЛП(Граница.Граница.Номер);
					КонецЕсли;
					ТекстПредупреждения = ТекстПредупреждения+"
					| Последовательность: <" + ТекПоследовательность.Представление + "> актуальна " + ТекстАктуальна;
				КонецЕсли;
			КонецЦикла;
			Если Не ПустаяСтрока(ТекстПредупреждения) Тогда
				Предупреждение("Последовательности разрушены и возможны некорректные результаты отчетов!"+ТекстПредупреждения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	ВидПоказателя=?(ПереключательВидСуммы="СуммаПродажи", "Розн", "");
		
	// получим макеты для результата и оформления
	МакетОтчета = ПолучитьМакет("Макет");
		
	// ПОСТРОИМ ЗАГОЛОВОК
	ДопСекция="|Начало";
	ОбластьЗаголовка = МакетОтчета.ПолучитьОбласть("Шапка"+ДопСекция);
	// организация
	Если ФильтрыОтчета.Организация.Использование И ФильтрыОтчета.Организация.ВидСравнения=ВидСравнения.Равно И НЕ обЗначениеНеЗаполнено(ФильтрыОтчета.Организация.Значение) Тогда
		Организация=ФильтрыОтчета.Организация.Значение;
		Телефон = киПолучитьПредставлениеКИ(Организация, Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
		ОбластьЗаголовка.Параметры.Заголовок=СокрЛП(Организация.НаименованиеПолное)+" ИНН "+СокрЛП(Организация.ИНН)+", "+Телефон;
	Иначе
		ОбластьЗаголовка.Параметры.Заголовок="<по нескольким организациям>";
	КонецЕсли;
	// склад
	Если ФильтрыОтчета.СкладКомпании.Использование И ФильтрыОтчета.СкладКомпании.ВидСравнения=ВидСравнения.Равно И НЕ обЗначениеНеЗаполнено(ФильтрыОтчета.СкладКомпании.Значение) Тогда
		СкладКомпании=ФильтрыОтчета.СкладКомпании.Значение;
		ОбластьЗаголовка.Параметры.ПечСклад=СкладКомпании.Наименование;
		Если ТипЗнч(СкладКомпании)=Тип("СправочникСсылка.Цеха") Тогда
			ОбластьЗаголовка.Параметры.ПечМОЛ=СкладКомпании.Мастер;
		Иначе
			ОбластьЗаголовка.Параметры.ПечМОЛ=СкладКомпании.МОЛ;
		КонецЕсли; 
	Иначе
		ОбластьЗаголовка.Параметры.ПечСклад="<по нескольким складам>";
		ОбластьЗаголовка.Параметры.ПечМОЛ="";
	КонецЕсли;
		
	ОбластьЗаголовка.Параметры.НомерОтчета = НомерОтчета;
	ОбластьЗаголовка.Параметры.ДатаСоставления = ТекущаяДата();
	ОбластьЗаголовка.Параметры.ДатаНачала = ДатаНачала;
	ОбластьЗаголовка.Параметры.ДатаКонца = ДатаКонца;
	ДокРезультат.Вывести(ОбластьЗаголовка);
		
	ОбластьСтрока = МакетОтчета.ПолучитьОбласть("Строка"+ДопСекция);
	ОбластьПереоценка = МакетОтчета.ПолучитьОбласть("Переоценка"+ДопСекция);
	// СОЗДАДИМ ЗАПРОС
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Тара",Перечисления.ВидыНоменклатуры.Тара);
	Для Каждого ЭлементОтбора Из ФильтрыОтчета Цикл
		// не используем элемент отбора
		Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
		Запрос.УстановитьПараметр(ЭлементОтбора.Имя,ЭлементОтбора.Значение);
	КонецЦикла;
		
	// НАЧАЛЬНЫЙ ОСТАТОК
	ОбластьНачальныйОстаток = МакетОтчета.ПолучитьОбласть("НачальныйОстаток"+ДопСекция);
	Запрос.УстановитьПараметр("НаДату",НачалоДня(ДатаНачала));
	
	//НАЧАЛЬНЫЙ ОСТАТОК ПО ТОВАРАМ
	Запрос.Текст = ПолучитьТекстЗапроса("Остатки");
	Выборка=Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ТоварНачОст = ?(Выборка["ТоварОст"+ВидПоказателя]=NULL,0,Выборка["ТоварОст"+ВидПоказателя]);
	ТараНачОст = ?(Выборка["ТараОст"+ВидПоказателя]=NULL,0,Выборка["ТараОст"+ВидПоказателя]);
	//НАЧАЛЬНЫЙ ОСТАТОК ПО ТОВАРАМ В ПРОИЗВОДСТВЕ
	Если ПереключательВидСуммы="СуммаПродажи" Тогда
		ТоварВПроизводствеНачОст = 0;
		ТараВПроизводствеНачОст = 0;
	Иначе
		Запрос.Текст = ПолучитьТекстЗапроса("ОстаткиДеталей");
		Выборка=Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ТоварВПроизводствеНачОст = ?(Выборка["ТоварОст"]=NULL,0,Выборка["ТоварОст"]);
		ТараВПроизводствеНачОст = ?(Выборка["ТараОст"]=NULL,0,Выборка["ТараОст"]);
	КонецЕсли;
	//НАЧАЛЬНЫЙ ОСТАТОК ПО АВТОМОБИЛЯМ
	Если ПереключательВидСуммы="СуммаПродажи" Тогда
		АвтомобильНачОст = 0;
		ТараКомплектацииНачОст = 0;
	Иначе
		Запрос.Текст = ПолучитьТекстЗапроса("ОстаткиАвтомобилей");
		Выборка=Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		АвтомобильНачОст = ?(Выборка["ТоварОст"]=NULL,0,Выборка["ТоварОст"]);
		ТараКомплектацииНачОст = ?(Выборка["ТараОст"]=NULL,0,Выборка["ТараОст"]);
	КонецЕсли; 
	//Вывод начальных остатков
	ОбластьНачальныйОстаток.Параметры.Товар = ТоварНачОст+ТоварВПроизводствеНачОст+АвтомобильНачОст;
	ОбластьНачальныйОстаток.Параметры.Тара  = ТараНачОст+ТараВПроизводствеНачОст+ТараКомплектацииНачОст;
	ОбластьНачальныйОстаток.Параметры.СтрОстатокНаНачало="Остаток на "+Формат(ДатаНачала,"ДФ=dd.MM.yyyy");
	ДокРезультат.Вывести(ОбластьНачальныйОстаток);
	
	// ПРИХОД
	ОбластьЗаголовокПриход = МакетОтчета.ПолучитьОбласть("ЗаголовокПриход"+ДопСекция);
	ДокРезультат.Вывести(ОбластьЗаголовокПриход);
	
	Запрос.Текст = ПолучитьТекстЗапроса("Движения");
	Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКонца",КонецДня(ДатаКонца));
	Запрос.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Приход);

	// идем по выборке и заполняем временные таблицы
	ТоварПриход=0; ТараПриход=0;
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		ТекОбласть=?(СоответствиеХозОперацийПереоценки[Выборка.ХозОперация]=Неопределено,ОбластьСтрока, ОбластьПереоценка);
		ТекОбласть.Параметры.ДатаДок	= Выборка.Регистратор.Дата;
		ТекОбласть.Параметры.НомерДок	= Выборка.Регистратор.Номер;
		Попытка 
			НаименованиеКонтрагента = ?(обЗначениеНеЗаполнено(Выборка.Регистратор.Контрагент),"",Выборка.Регистратор.Контрагент);
		Исключение
			НаименованиеКонтрагента="";
		КонецПопытки;
		ПредстДок=СокрЛП(Выборка.Регистратор.ХозОперация)+": "+НаименованиеКонтрагента;
		ТекОбласть.Параметры.Представление = ПредстДок;
		ТоварСумма = ?(Выборка["ТоварСумма"+ВидПоказателя]=NULL,0,Выборка["ТоварСумма"+ВидПоказателя]);
		ТекОбласть.Параметры.Товар		= ТоварСумма;
		ТараСумма = ?(Выборка["ТараСумма"+ВидПоказателя]=NULL,0,Выборка["ТараСумма"+ВидПоказателя]);
		ТекОбласть.Параметры.Тара		= ТараСумма;
		ТекОбласть.Область("R1C1").Расшифровка=Выборка.Регистратор;
		ДокРезультат.Вывести(ТекОбласть);
		ТоварПриход = ТоварПриход+ТоварСумма;
		ТараПриход = ТараПриход+ТараСумма;
	КонецЦикла;
		
	// для подсчета количества документов 
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	ТаблицаДокументов.Свернуть("Регистратор");
		
	ОбластьИтогоПриход = МакетОтчета.ПолучитьОбласть("ИтогПриход"+ДопСекция);
	ОбластьИтогоПриход.Параметры.Товар = ТоварПриход;
	ОбластьИтогоПриход.Параметры.Тара  = ТараПриход;
	ДокРезультат.Вывести(ОбластьИтогоПриход);
		
	ОбластьИтогоПриходОстаток = МакетОтчета.ПолучитьОбласть("ПриходСОстатком"+ДопСекция);
	ОбластьИтогоПриходОстаток.Параметры.Товар = ТоварПриход+ТоварНачОст+ТоварВПроизводствеНачОст+АвтомобильНачОст;
	ОбластьИтогоПриходОстаток.Параметры.Тара = ТараПриход+ТараНачОст+ТараВПроизводствеНачОст+ТараКомплектацииНачОст;
	ДокРезультат.Вывести(ОбластьИтогоПриходОстаток);
		
	// РАСХОД
	ОбластьЗаголовокРасход = МакетОтчета.ПолучитьОбласть("ЗаголовокРасход"+ДопСекция);
	ДокРезультат.Вывести(ОбластьЗаголовокРасход);
	
	Запрос.Текст = ПолучитьТекстЗапроса("Движения");
	Запрос.УстановитьПараметр("ВидДвижения",ВидДвиженияНакопления.Расход);
	
	// идем по выборке и заполняем временные таблицы
	ТоварРасход=0; ТараРасход=0;
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		ТекОбласть=?(СоответствиеХозОперацийПереоценки[Выборка.ХозОперация]=Неопределено, ОбластьСтрока, ОбластьПереоценка);
		ТекОбласть.Параметры.ДатаДок	= Выборка.Регистратор.Дата;
		ТекОбласть.Параметры.НомерДок	= Выборка.Регистратор.Номер;
		Попытка 
			НаименованиеКонтрагента = ?(обЗначениеНеЗаполнено(Выборка.Регистратор.Контрагент),"",Выборка.Регистратор.Контрагент);
		Исключение
			НаименованиеКонтрагента="";
		КонецПопытки;
		ПредстДок=СокрЛП(Выборка.Регистратор.ХозОперация)+": "+НаименованиеКонтрагента;
		ТекОбласть.Параметры.Представление = ПредстДок;
		ТоварСумма = ?(Выборка["ТоварСумма"+ВидПоказателя]=NULL,0,Выборка["ТоварСумма"+ВидПоказателя]);
		ТекОбласть.Параметры.Товар		= ТоварСумма;
		ТараСумма = ?(Выборка["ТараСумма"+ВидПоказателя]=NULL,0,Выборка["ТараСумма"+ВидПоказателя]);
		ТекОбласть.Параметры.Тара		= ТараСумма;
		ТекОбласть.Область("R1C1").Расшифровка=Выборка.Регистратор;
		ДокРезультат.Вывести(ТекОбласть);
		ТоварРасход = ТоварРасход+ТоварСумма;
		ТараРасход = ТараРасход+ТараСумма;
	КонецЦикла;
		
	// для подсчета количества документов 
	ТаблицаДокументовРасход = Запрос.Выполнить().Выгрузить();
	ТаблицаДокументовРасход.Свернуть("Регистратор");
		
	Для Каждого Строка Из ТаблицаДокументовРасход Цикл
		СтрокаТаблицыДокументов = ТаблицаДокументов.Добавить();
		СтрокаТаблицыДокументов.Регистратор = Строка.Регистратор;
	КонецЦикла;	
	ТаблицаДокументов.Свернуть("Регистратор");
		
	ОбластьИтогоРасход = МакетОтчета.ПолучитьОбласть("ИтогРасход"+ДопСекция);
	ОбластьИтогоРасход.Параметры.Товар = ТоварРасход;
	ОбластьИтогоРасход.Параметры.Тара  = ТараРасход;
	ДокРезультат.Вывести(ОбластьИтогоРасход);
		
	// КОНЕЧНЫЙ ОСТАТОК
	ДатаКонДляОстатка = Новый Граница(КонецДня(ДатаКонца), ВидГраницы.Включая);
	Запрос.УстановитьПараметр("НаДату", ДатаКонДляОстатка);
	
	//КОНЕЧНЫЙ ОСТАТОК ПО ТОВАРАМ
	Запрос.Текст = ПолучитьТекстЗапроса("Остатки");
	Выборка=Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ОбластьКонечныйОстаток = МакетОтчета.ПолучитьОбласть("КонечныйОстаток"+ДопСекция);
	ТоварКонОст = ?(Выборка["ТоварОст"+ВидПоказателя]=NULL,0,Выборка["ТоварОст"+ВидПоказателя]);
	ТараКонОст = ?(Выборка["ТараОст"+ВидПоказателя]=NULL,0,Выборка["ТараОст"+ВидПоказателя]);
	
	//КОНЕЧНЫЙ ОСТАТОК ПО ТОВАРАМ В ПРОИЗВОДСТВЕ
	Если ПереключательВидСуммы="СуммаПродажи" Тогда
		ТоварВПроизводствеКонОст = 0;
		ТараВПроизводствеКонОст = 0;
	Иначе
		Запрос.Текст = ПолучитьТекстЗапроса("ОстаткиДеталей");
		Выборка=Запрос.Выполнить().Выбрать();
    	Выборка.Следующий();
		ТоварВПроизводствеКонОст = ?(Выборка["ТоварОст"]=NULL,0,Выборка["ТоварОст"]);
		ТараВПроизводствеКонОст = ?(Выборка["ТараОст"]=NULL,0,Выборка["ТараОст"]);
	КонецЕсли; 
	
	//КОНЕЧНЫЙ ОСТАТОК ПО АВТОМОБИЛЯМ
	Если ПереключательВидСуммы="СуммаПродажи" Тогда
		АвтомобильКонОст = 0;
		ТараКомплектацииКонОст = 0;
	Иначе
		Запрос.Текст = ПолучитьТекстЗапроса("ОстаткиАвтомобилей");
		Выборка=Запрос.Выполнить().Выбрать();
    	Выборка.Следующий();
		АвтомобильКонОст = ?(Выборка["ТоварОст"]=NULL,0,Выборка["ТоварОст"]);
		ТараКомплектацииКонОст = ?(Выборка["ТараОст"]=NULL,0,Выборка["ТараОст"]);
	КонецЕсли; 
	
	//Вывод конечных остатков
	ОбластьКонечныйОстаток.Параметры.Товар = ТоварКонОст + ТоварВПроизводствеКонОст+АвтомобильКонОст;
	ОбластьКонечныйОстаток.Параметры.Тара  = ТараКонОст + ТараВПроизводствеКонОст+ТараКомплектацииКонОст;
	ОбластьКонечныйОстаток.Параметры.СтрОстатокНаКонец="Остаток на "+Формат(ДатаКонца,"ДФ=dd.MM.yyyy");
	ДокРезультат.Вывести(ОбластьКонечныйОстаток);
		
	// ПОДВАЛ
	ОбластьПодвал = МакетОтчета.ПолучитьОбласть("Подвал"+ДопСекция);
	ОбластьПодвал.Параметры.ИтогоДок=ЧислоПрописью(ТаблицаДокументов.Количество(),,"документ, документа, документов, м, , , , м, 0");
	ДокРезультат.Вывести(ОбластьПодвал);
		
	ДокРезультат.АвтоМасштаб=Истина;
	ДокРезультат.ТолькоПросмотр=Истина;
		
	Возврат ДокРезультат;
		
КонецФункции // СформироватьТабличныйДокумент()

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
	
// создадим временные таблицы
МассивТипов=Новый Массив(1); ТаблицаПриход=Новый ТаблицаЗначений();
ТаблицаПриход.Колонки.Добавить("Документ",Документы.ТипВсеСсылки());
МассивТипов[0]=Тип("Дата"); 
ТаблицаПриход.Колонки.Добавить("ДатаДок",Новый ОписаниеТипов(МассивТипов));
МассивТипов[0]=Тип("Строка"); 
ТаблицаПриход.Колонки.Добавить("НомерДок",Новый ОписаниеТипов(МассивТипов));
ТаблицаПриход.Колонки.Добавить("Представление",Новый ОписаниеТипов(МассивТипов));
МассивТипов[0]=Тип("Число"); 
ТаблицаПриход.Колонки.Добавить("СуммаТоваров",Новый ОписаниеТипов(МассивТипов));
ТаблицаПриход.Колонки.Добавить("СуммаТары",Новый ОписаниеТипов(МассивТипов));
ТаблицаРасход=ТаблицаПриход.Скопировать();

СоответствиеХозОперацийПереоценки=Новый Соответствие();
СоответствиеХозОперацийПереоценки.Вставить(Справочники.ХозОперации.ПереоценкаРозницаПоПриходу,Истина);
СоответствиеХозОперацийПереоценки.Вставить(Справочники.ХозОперации.ПереоценкаРозницаПоРасходу,Истина);
СоответствиеХозОперацийПереоценки.Вставить(Справочники.ХозОперации.ПереоценкаТоваровВРознице,Истина);
#КонецЕсли

//|ВЫБРАТЬ
//			|	ОстаткиАвтомобилей.Регистратор,
//			|	ОстаткиАвтомобилей.Регистратор.Дата,
//			|	ОстаткиАвтомобилей.ХозОперация,
//			|// ТОВАРЫ
//			|	СУММА(ОстаткиАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-ОстаткиАвтомобилей.СуммаНДС","")+") +
//			|	МАКСИМУМ(ЕСТЬNULL(КомплектацияАвтомобилей.ТоварСумма, 0)) КАК ТоварСумма,
//			|// ТАРА
//			|	МАКСИМУМ(ЕСТЬNULL(КомплектацияАвтомобилей.ТараСумма, 0)) КАК ТараСумма
//			|ИЗ
//			|	РегистрНакопления.ОстаткиАвтомобилей КАК ОстаткиАвтомобилей
//			|		ЛЕВОЕ СОЕДИНЕНИЕ(
//			|			ВЫБРАТЬ
//			|				КомплектацияАвтомобилей.Регистратор КАК Регистратор,
//			|				// ТОВАРЫ
//			|				СУММА(ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры<>&Тара ИЛИ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции ТОГДА КомплектацияАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-КомплектацияАвтомобилей.СуммаНДС","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТоварСумма,
//			|				// ТАРА
//			|				СУММА(ВЫБОР КОГДА КомплектацияАвтомобилей.Номенклатура.ВидНоменклатуры=&Тара И (НЕ КомплектацияАвтомобилей.Номенклатура ССЫЛКА Справочник.Опции) ТОГДА КомплектацияАвтомобилей.Сумма"+?(ПереключательВидСуммы="БезНДС","-КомплектацияАвтомобилей.СуммаНДС","")+" ИНАЧЕ 0 КОНЕЦ) КАК ТараСумма
//			|			ИЗ РегистрНакопления.КомплектацияАвтомобилей КАК КомплектацияАвтомобилей
//			|			ГДЕ
//			|				КомплектацияАвтомобилей.Период МЕЖДУ &ДатаНачала И &ДатаКонца
//			|   			И КомплектацияАвтомобилей.ВидДвижения=&ВидДвижения
//			|   			"+СтрокаУсловияКомплектации+"
//			|			СГРУППИРОВАТЬ ПО 
//			|				КомплектацияАвтомобилей.Регистратор
//			|		) КАК КомплектацияАвтомобилей
//			|		ПО ОстаткиАвтомобилей.Регистратор=КомплектацияАвтомобилей.Регистратор
//			|ГДЕ
//			|	ОстаткиАвтомобилей.Период МЕЖДУ &ДатаНачала И &ДатаКонца
//			|   И ОстаткиАвтомобилей.ВидДвижения=&ВидДвижения
//			|   "+СтрокаУсловияАвтомобилей+"
//			|СГРУППИРОВАТЬ ПО 
//			|	ОстаткиАвтомобилей.Регистратор, 
//			|	ОстаткиАвтомобилей.ХозОперация
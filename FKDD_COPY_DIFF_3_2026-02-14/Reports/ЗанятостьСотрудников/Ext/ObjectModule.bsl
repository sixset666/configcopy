Перем ВидОтчета Экспорт;
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчет
Перем ПараметрыИзмеренийСтрок Экспорт;

Функция ПолучитьВыборкуДанных() Экспорт
	НастройкаПостроитель = ПостроительОтчета.ПолучитьНастройки(Истина,Ложь,Ложь,Ложь,Ложь);
	ПостроительОтчета.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПоСотрудникам.Сотрудник КАК Сотрудник,
	|	ТаблицаПоСотрудникам.ВидОтметки,
	|	ТаблицаПоСотрудникам.ЗаказНаряд,
	|	ТаблицаПоСотрудникам.РабочееМесто,
	|	ВЫБОР
	|		КОГДА ТаблицаПоСотрудникам.ДатаНач < &НачИнтервала
	|			ТОГДА &НачИнтервала
	|		ИНАЧЕ ТаблицаПоСотрудникам.ДатаНач
	|	КОНЕЦ КАК ДатаНач,
	|	ВЫБОР
	|		КОГДА ТаблицаПоСотрудникам.ДатаКон > &КонИнтервала
	|			ТОГДА &КонИнтервала
	|		ИНАЧЕ ТаблицаПоСотрудникам.ДатаКон
	|	КОНЕЦ КАК ДатаКон,
	|	ТаблицаПоСотрудникам.Продолжительность
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияВремениРабот.Сотрудник КАК Сотрудник,
	|		РегистрацияВремениРабот.ВидОтметки КАК ВидОтметки,
	|		РегистрацияВремениРабот.ЗаказНаряд КАК ЗаказНаряд,
	|		РегистрацияВремениРабот.РабочееМесто КАК РабочееМесто,
	|		РегистрацияВремениРабот.Период КАК ДатаНач,
	|		ВЫБОР
	|			КОГДА РегистрацияВремениРабот.КонецРаботы = &ПустаяДата И РегистрацияВремениРабот.ВидОтметки <> Значение(Справочник.ВидыОтметокВремени.НерабочееВремя)
	|			ТОГДА &ТекМомент
	|			ИНАЧЕ РегистрацияВремениРабот.КонецРаботы
	|		КОНЕЦ КАК ДатаКон,
	|		РегистрацияВремениРабот.Продолжительность КАК Продолжительность
	|	ИЗ
	|		РегистрСведений.РегистрацияВремениРабот КАК РегистрацияВремениРабот) КАК ТаблицаПоСотрудникам
	|ГДЕ
	|	(ТаблицаПоСотрудникам.ДатаНач МЕЖДУ &НачИнтервала И &КонИнтервала
	|			ИЛИ ТаблицаПоСотрудникам.ДатаКон МЕЖДУ &НачИнтервала И &КонИнтервала
	|			ИЛИ &НачИнтервала МЕЖДУ ТаблицаПоСотрудникам.ДатаНач И ТаблицаПоСотрудникам.ДатаКон)
	|//	И ТаблицаПоСотрудникам.ВидОтметки <> Значение(Справочник.ВидыОтметокВремени.НерабочееВремя)
	|{
	|ГДЕ
	|	ТаблицаПоСотрудникам.Сотрудник.* КАК Сотрудник,
	|	ТаблицаПоСотрудникам.ВидОтметки.* КАК ВидИнтервала
	|}
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНач
	|ИТОГИ ПО
	|	Сотрудник";
	ПостроительОтчета.Параметры.Вставить("КонИнтервала",КонецДня(ДатаОтчета));
	ПостроительОтчета.Параметры.Вставить("ПустаяДата",Дата("00010101"));
	ПостроительОтчета.Параметры.Вставить("ТекМомент",ТекущаяДата());
	ПостроительОтчета.Параметры.Вставить("НачИнтервала",НачалоДня(ДатаОтчета));
	ПостроительОтчета.УстановитьНастройки(НастройкаПостроитель,Истина,Ложь,Ложь,Ложь,Ложь);
	ПостроительОтчета.Выполнить();
	Выборка = ПостроительОтчета.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Возврат Выборка;
КонецФункции

Процедура ИнициализацияПостроителя() Экспорт
	ПостроительОтчета.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПоСотрудникам.Сотрудник   КАК Сотрудник,
	|	ТаблицаПоСотрудникам.ВидОтметки  КАК ВидИнтервала,
	|	ВЫБОР 
	|		КОГДА ТаблицаПоСотрудникам.Период < &ДатаНач ТОГДА
	|			&ДатаНач
	|		ИНАЧЕ
	|			ТаблицаПоСотрудникам.Период
	|	КОНЕЦ КАК ПериодНачало,
	|	ВЫБОР 
	|		КОГДА ТаблицаПоСотрудникам.КонецРаботы > &ДатаКон ТОГДА
	|			&ДатаКон
	|		ИНАЧЕ
	|			ТаблицаПоСотрудникам.КонецРаботы
	|	КОНЕЦ КАК ПериодКонец,
	|	ТаблицаПоСотрудникам.Продолжительность
	|ИЗ
	|	РегистрСведений.РегистрацияВремениРабот КАК ТаблицаПоСотрудникам
	|ГДЕ
	|	ТаблицаПоСотрудникам.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	ИЛИ ТаблицаПоСотрудникам.КонецРаботы МЕЖДУ &ДатаНач И &ДатаКон
	|{ГДЕ
	|	ТаблицаПоСотрудникам.Сотрудник.* КАК Сотрудник,
	|	ТаблицаПоСотрудникам.ВидОтметки.* КАК ВидИнтервала
	|}
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Сотрудник,
	|	ВидОтметки";
КонецПроцедуры

Функция ПолучитьИнтервалыОтчета() Экспорт
	НачалоРабочегоДня = Неопределено;
	КонецРабочегоДня = Неопределено;
	
	// сформирум список всех графиков
	// получим отборы	
	ПостроительПоСотрудникам = Новый ПостроительОтчета;
	ПостроительПоСотрудникам.Текст = 
	"ВЫБРАТЬ
	|	РегистрацияВремениРабот.Сотрудник,
	|	РегистрацияВремениРабот.Период КАК ДатаНач,
	|	ВЫБОР
	|		КОГДА РегистрацияВремениРабот.КонецРаботы = &ПустаяДата
	|			ТОГДА &ТекДата
	|		ИНАЧЕ РегистрацияВремениРабот.КонецРаботы
	|	КОНЕЦ КАК ДатаКон
	|ПОМЕСТИТЬ ТаблицаПоСотрудникам
	|ИЗ
	|	РегистрСведений.РегистрацияВремениРабот КАК РегистрацияВремениРабот
	|ГДЕ
	|	РегистрацияВремениРабот.ВидОтметки <> &НерабочееВремя
	|	И ВЫБОР
	|	  КОГДА РегистрацияВремениРабот.КонецРаботы = &ПустаяДата
	|	  ТОГДА ИСТИНА
	|	  ИНАЧЕ (РегистрацияВремениРабот.Период МЕЖДУ &НачИнтервала И &КонИнтервала) ИЛИ
	|			(РегистрацияВремениРабот.КонецРаботы МЕЖДУ &НачИнтервала И &КонИнтервала) ИЛИ
	|			(&НачИнтервала МЕЖДУ РегистрацияВремениРабот.Период И РегистрацияВремениРабот.КонецРаботы)
	|	  КОНЕЦ
	|;
	|ВЫБРАТЬ
	|	ТаблицаПоСотрудникам.Сотрудник,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ТаблицаПоСотрудникам.ДатаНач < &НачИнтервала
	|				ТОГДА &НачИнтервала
	|			ИНАЧЕ ТаблицаПоСотрудникам.ДатаНач
	|		КОНЕЦ) КАК ДатаНач,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаПоСотрудникам.ДатаКон > &КонИнтервала
	|				ТОГДА &КонИнтервала
	|			ИНАЧЕ ТаблицаПоСотрудникам.ДатаКон
	|		КОНЕЦ) КАК ДатаКон
	|ИЗ
	|	ТаблицаПоСотрудникам КАК ТаблицаПоСотрудникам
	|ГДЕ
	|	ТаблицаПоСотрудникам.ДатаНач МЕЖДУ &НачИнтервала И &КонИнтервала
	|	ИЛИ ТаблицаПоСотрудникам.ДатаКон МЕЖДУ &НачИнтервала И &КонИнтервала
	|	ИЛИ &НачИнтервала МЕЖДУ ТаблицаПоСотрудникам.ДатаНач И ТаблицаПоСотрудникам.ДатаКон
	|{
	|ГДЕ
	|	ТаблицаПоСотрудникам.Сотрудник.* КАК Сотрудник
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПоСотрудникам.Сотрудник";
	ПостроительПоСотрудникам.Параметры.Вставить("КонИнтервала",КонецДня(ДатаОтчета));
	ПостроительПоСотрудникам.Параметры.Вставить("ПустаяДата",Дата("00010101"));
	ПостроительПоСотрудникам.Параметры.Вставить("ТекДата",ТекущаяДата());
	ПостроительПоСотрудникам.Параметры.Вставить("НерабочееВремя",Справочники.ВидыОтметокВремени.НерабочееВремя);
	ПостроительПоСотрудникам.Параметры.Вставить("НачИнтервала",НачалоДня(ДатаОтчета));
	ПостроительПоСотрудникам.УстановитьНастройки(ПостроительОтчета.ПолучитьНастройки(Истина,Ложь,Ложь,Ложь,Ложь),Истина,Ложь,Ложь,Ложь,Ложь);
	
	ПостроительПоСотрудникам.Выполнить(); 
	ВидыДней = Новый Массив();
	ВидыДней.Добавить(Перечисления.ВидДня.Рабочий);
	ТаблицаСотрудников = ПостроительПоСотрудникам.Результат.Выгрузить();
	Для Каждого стрСотрудник Из ТаблицаСотрудников Цикл
		НачалоРабочегоДняПоГрафику = Неопределено;
		КонецРабочегоДняПоГрафику = Неопределено;
		
		Если обЗначениеНеЗаполнено(стрСотрудник.Сотрудник.ГрафикРаботы) Тогда
			ГрафикРаботы = стрСотрудник.Сотрудник.Подразделение.ГрафикРаботы; 
		Иначе
			ГрафикРаботы = стрСотрудник.Сотрудник.ГрафикРаботы; 
		КонецЕсли;
		ТаблГрафика = кпПолучитьГрафик(ГрафикРаботы,НачалоДня(ДатаОтчета),КонецДня(ДатаОтчета),, ВидыДней);
		Если ТаблГрафика.Количество() = 0 Тогда
			Если (НачалоРабочегоДня = Неопределено) ИЛИ (НачалоРабочегоДня > стрСотрудник.ДатаНач) Тогда
				НачалоРабочегоДня =стрСотрудник.ДатаНач;
			КонецЕсли;
			
			Если (КонецРабочегоДня = Неопределено) ИЛИ (КонецРабочегоДня < стрСотрудник.ДатаКон) Тогда
				КонецРабочегоДня = стрСотрудник.ДатаКон;
			КонецЕсли;
		Иначе
			ТаблГрафика.Сортировать("НачалоРабочегоВремени ВОЗР");
			НачалоРабочегоДняПоГрафику = НачалоДня(ДатаОтчета) + ((Час(ТаблГрафика[0].НачалоРабочегоВремени)*3600)+(Минута(ТаблГрафика[0].НачалоРабочегоВремени)*60)+Секунда(ТаблГрафика[0].НачалоРабочегоВремени));
			
			ТаблГрафика.Сортировать("НачалоРабочегоВремени УБЫВ");
			КонецРабочегоДняПоГрафику = НачалоДня(ДатаОтчета) + ((Час(ТаблГрафика[0].КонецРабочегоВремени)*3600)+(Минута(ТаблГрафика[0].КонецРабочегоВремени)*60)+Секунда(ТаблГрафика[0].КонецРабочегоВремени));
			Если (НачалоРабочегоДня = Неопределено) ИЛИ (НачалоРабочегоДня > НачалоРабочегоДняПоГрафику) ИЛИ (НачалоРабочегоДня > стрСотрудник.ДатаНач) Тогда
				НачалоРабочегоДня = Мин(НачалоРабочегоДняПоГрафику,стрСотрудник.ДатаНач);
			КонецЕсли;
			
			Если (КонецРабочегоДня = Неопределено) ИЛИ (КонецРабочегоДня < КонецРабочегоДняПоГрафику) ИЛИ (КонецРабочегоДня < стрСотрудник.ДатаКон) Тогда
				КонецРабочегоДня = Макс(КонецРабочегоДняПоГрафику,стрСотрудник.ДатаКон);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	стрИнтервалов = Новый Структура;
	стрИнтервалов.Вставить("НачалоРабочегоДня",?(НачалоРабочегоДня = Неопределено,НачалоДня(ТекущаяДата()),НачалоРабочегоДня));
	стрИнтервалов.Вставить("КонецРабочегоДня",?(КонецРабочегоДня = Неопределено,КонецДня(ТекущаяДата()),КонецРабочегоДня));
	стрИнтервалов.Вставить("НетРабочихИнтервалов", ТаблицаСотрудников.Количество() = 0);
	Возврат стрИнтервалов;
КонецФункции

Функция ПолучитьНеявивщихсяСотрудников(ОтметивщиесяСотрудники) Экспорт
	
	Если ТипЗнч(ОтметивщиесяСотрудники) <> Тип("Массив") Тогда
		ОтметивщиесяСотрудники = Новый Массив;
	КонецЕсли;
	
	ВыборкаСотрудников = Справочники.Сотрудники.Выбрать();
	
	НеявивщиесяСотрудники = Новый Массив;
	ВидыДней = Новый Массив();
	ВидыДней.Добавить(Перечисления.ВидДня.Рабочий);
	
	Пока ВыборкаСотрудников.Следующий() Цикл
		
		Если ВыборкаСотрудников.ЭтоГруппа Тогда Продолжить; КонецЕсли;
		
		Если ОтметивщиесяСотрудники.Найти(ВыборкаСотрудников) <> Неопределено Тогда Продолжить; КонецЕсли;
		
		Если обЗначениеНеЗаполнено(ВыборкаСотрудников.ГрафикРаботы) Тогда
			ГрафикРаботы = ВыборкаСотрудников.Подразделение.ГрафикРаботы; 
		Иначе
			ГрафикРаботы = ВыборкаСотрудников.ГрафикРаботы; 
		КонецЕсли;
		ТаблГрафика = кпПолучитьГрафик(ГрафикРаботы,НачалоДня(ДатаОтчета),КонецДня(ДатаОтчета),, ВидыДней);
		Если ТаблГрафика.Количество() > 0 Тогда
			НеявивщиесяСотрудники.Добавить(ВыборкаСотрудников.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат НеявивщиесяСотрудники;
КонецФункции

ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ВложеннаяТаблица.Номенклатура                 КАК Номенклатура,
	ВложеннаяТаблица.СкладКомпании                        КАК СкладКомпании,
	ВложеннаяТаблица.Контрагент                   КАК Контрагент,
	НАЧАЛОПЕРИОДА(ВложеннаяТаблица.Период, МЕСЯЦ) КАК Период,
	ВложеннаяТаблица.Количество                   КАК Количество,
	ВложеннаяТаблица.СуммаУпр                     КАК СуммаУпр
	
ПОМЕСТИТЬ
	ТаблицаРегистра

ИЗ
(ВЫБРАТЬ
	ПартииТоваровКомпании.Номенклатура                 КАК Номенклатура,
	ПартииТоваровКомпании.СкладКомпании                КАК СкладКомпании,
	ПартииТоваровКомпании.Партия.Контрагент            КАК Контрагент,
	НАЧАЛОПЕРИОДА(ПартииТоваровКомпании.Период, МЕСЯЦ) КАК Период,
	СУММА(ПартииТоваровКомпании.Количество)            КАК Количество,
	СУММА(ПартииТоваровКомпании.СуммаУпр)              КАК СуммаУпр
	
{ВЫБРАТЬ
	Номенклатура  КАК Номенклатура,
	СкладКомпании КАК СкладКомпании,
	Контрагент    КАК Контрагент
}

Из
	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании

ГДЕ
	ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И 
	(НЕ ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ПеремещениеТоваров) И 
	ПартииТоваровКомпании.Период <= &ДатаКон
	
{ГДЕ
	ПартииТоваровКомпании.Номенклатура.*      КАК Номенклатура,
	ПартииТоваровКомпании.СкладКомпании.*     КАК СкладКомпании,
	ПартииТоваровКомпании.Партия.Контрагент.* КАК Контрагент,
	ПартииТоваровКомпании.Партия.*            КАК Партия
}

СГРУППИРОВАТЬ ПО
	ПартииТоваровКомпании.Номенклатура,
	ПартииТоваровКомпании.СкладКомпании,
	ПартииТоваровКомпании.Партия.Контрагент,
	НАЧАЛОПЕРИОДА(ПартииТоваровКомпании.Период, МЕСЯЦ)) КАК ВложеннаяТаблица

ГДЕ
	ВложеннаяТаблица.Количество <> 0 ИЛИ
	ВложеннаяТаблица.СуммаУпр <> 0
;

ВЫБРАТЬ
	ПартииТоваровКомпании.Номенклатура     КАК Номенклатура,
	ПартииТоваровКомпании.СкладКомпании            КАК СкладКомпании,
	ПартииТоваровКомпании.Контрагент       КАК Контрагент,
	МАКСИМУМ(ПартииТоваровКомпании.Период) КАК ПериодМакс,
	РАЗНОСТЬДАТ(МАКСИМУМ(ПартииТоваровКомпании.Период), &ДатаКон, МЕСЯЦ) КАК КоличествоМесяцевБезПродаж

{ВЫБРАТЬ
	Номенклатура     КАК Номенклатура,
	СкладКомпании            КАК СкладКомпании,
	Контрагент       КАК Контрагент
}

ПОМЕСТИТЬ
	ТаблицаРасходов
Из
	ТаблицаРегистра КАК ПартииТоваровКомпании
СГРУППИРОВАТЬ ПО
	ПартииТоваровКомпании.Номенклатура,
	ПартииТоваровКомпании.СкладКомпании,
	ПартииТоваровКомпании.Контрагент
ИМЕЮЩИЕ
	РАЗНОСТЬДАТ(МАКСИМУМ(ПартииТоваровКомпании.Период), &ДатаКон, МЕСЯЦ) >= &ЗапасВторойПредел
;

ВЫБРАТЬ
	ТаблицаРасходов.Номенклатура                        КАК Номенклатура,
	ТаблицаРасходов.СкладКомпании                               КАК СкладКомпании,
	ТаблицаРасходов.Контрагент                          КАК Контрагент,
	ТаблицаРасходов.ПериодМакс                          КАК ПериодМакс,
	ТаблицаРасходов.КоличествоМесяцевБезПродаж          КАК КоличествоМесяцевБезПродаж,
	СУММА(ЕСТЬNULL(ПартииТоваровКомпании.Количество,0)) КАК КоличествоПродажЗаТриМесяца

{ВЫБРАТЬ
	Номенклатура     КАК Номенклатура,
	СкладКомпании            КАК СкладКомпании,
	Контрагент       КАК Контрагент
}

ПОМЕСТИТЬ
	ТабНелеквидовИКондидатов
	
ИЗ
	ТаблицаРасходов КАК ТаблицаРасходов
ЛЕВОЕ СОЕДИНЕНИЕ
	ТаблицаРегистра КАК ПартииТоваровКомпании
ПО
	ПартииТоваровКомпании.Номенклатура = ТаблицаРасходов.Номенклатура И
	ПартииТоваровКомпании.СкладКомпании = ТаблицаРасходов.СкладКомпании И
	ПартииТоваровКомпании.Контрагент = ТаблицаРасходов.Контрагент И
	ПартииТоваровКомпании.Период <= ТаблицаРасходов.ПериодМакс И
	РАЗНОСТЬДАТ(ПартииТоваровКомпании.Период, ПериодМакс, МЕСЯЦ) < 3

СГРУППИРОВАТЬ ПО
	ТаблицаРасходов.Номенклатура,
	ТаблицаРасходов.СкладКомпании,
	ТаблицаРасходов.Контрагент,
	ТаблицаРасходов.ПериодМакс,
	ТаблицаРасходов.КоличествоМесяцевБезПродаж
;
ВЫБРАТЬ
	СкладКомпании                              КАК СкладКомпании,
	СУММА(Остатки.СуммаУпрОстаток)             КАК ОбщСумма,
	КОЛИЧЕСТВО(Остатки.Номенклатура) КАК ОбщКоличПоКатолгу
ПОМЕСТИТЬ
	ТабРасчетаДолей

ИЗ
	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		&ДатаКон,) КАК Остатки
СГРУППИРОВАТЬ ПО
	СкладКомпании
;

УНИЧТОЖИТЬ ТаблицаРасходов
;

ВЫБРАТЬ
	Валюты.Валюта,
	Валюты.Курс,
	Валюты.Кратность
ПОМЕСТИТЬ
	ТабВалют
ИЗ
	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКон,
		ВЫБОР
			КОГДА &ВалютаТипаЦен = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
				Валюта В (ВЫБРАТЬ РАЗЛИЧНЫЕ
							ТабНелеквидовИКондидатов.Номенклатура.ВалютаУчета КАК Валюта
							ИЗ
							ТабНелеквидовИКондидатов КАК ТабНелеквидовИКондидатов)
			ИНАЧЕ
				Валюта = &ВалютаТипаЦен
		КОНЕЦ) КАК Валюты
ГДЕ
	Валюты.Курс > 0
;

ВЫБРАТЬ
	ТабНелеквидовИКондидатов.Номенклатура                        КАК Номенклатура,
	ТабНелеквидовИКондидатов.СкладКомпании                       КАК СкладКомпании,
	ТабНелеквидовИКондидатов.Контрагент                          КАК Контрагент,
	ТабНелеквидовИКондидатов.КоличествоМесяцевБезПродаж          КАК КоличествоМесяцевБезПродаж,
	ТабРасчетаДолей.ОбщКоличПоКатолгу                            КАК ОбщКоличПоКатолгу,
	ЕСТЬNULL(Остатки.СуммаУпрОстаток, 0)                         КАК Себестоимость,
	ЕСТЬNULL(Остатки.КоличествоОстаток, 0)                       КАК Количество,
	ВЫБОР
		КОГДА Остатки.КоличествоОстаток ЕСТЬ NULL ТОГДА
			0
		ИНАЧЕ
			Остатки.КоличествоОстаток*3/ТабНелеквидовИКондидатов.КоличествоПродажЗаТриМесяца
	КОНЕЦ                                               КАК ЗапасНаПродажу,
	ВЫБОР
		КОГДА ТабРасчетаДолей.ОбщСумма ЕСТЬ NULL ТОГДА
			0
		ИНАЧЕ
			(ЕСТЬNULL(Остатки.СуммаУпрОстаток, 0)/ТабРасчетаДолей.ОбщСумма)*100
	КОНЕЦ КАК ДоляСебестоимости,
	(ЕСТЬNULL(Остатки.СуммаУпрОстаток, 0)*&КурсВалютыУпрУчета)/ЕСТЬNULL(ТабВалют.Курс, 1) КАК СебестоимостьПоТипуЦен
{ВЫБРАТЬ
	Номенклатура     КАК Номенклатура,
	СкладКомпании            КАК СкладКомпании,
	Контрагент       КАК Контрагент
}
ПОМЕСТИТЬ
	ТабИтоговая
ИЗ
	ТабНелеквидовИКондидатов КАК ТабНелеквидовИКондидатов
ЛЕВОЕ СОЕДИНЕНИЕ
	РегистрНакопления.ПартииТоваровКомпании.Остатки
	(&ДатаКон,(Номенклатура,СкладКомпании)
	В (ВЫБРАТЬ
		ТабНелеквидовИКондидатов.Номенклатура КАК Номенклатура,
		ТабНелеквидовИКондидатов.СкладКомпании        КАК СкладКомпании
	ИЗ
		ТабНелеквидовИКондидатов КАК ТабНелеквидовИКондидатов)) КАК Остатки
ПО
	Остатки.Номенклатура = ТабНелеквидовИКондидатов.Номенклатура
	И Остатки.СкладКомпании = ТабНелеквидовИКондидатов.СкладКомпании
	И Остатки.Партия.Контрагент = ТабНелеквидовИКондидатов.Контрагент
ЛЕВОЕ СОЕДИНЕНИЕ
	ТабРасчетаДолей КАК ТабРасчетаДолей
ПО
	ТабРасчетаДолей.СкладКомпании = ТабНелеквидовИКондидатов.СкладКомпании
ЛЕВОЕ СОЕДИНЕНИЕ
	ТабВалют КАК ТабВалют
ПО
	ВЫБОР
		КОГДА &ВалютаТипаЦен = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
			ТабНелеквидовИКондидатов.Номенклатура.ВалютаУчета = ТабВалют.Валюта
		ИНАЧЕ
			ИСТИНА
	КОНЕЦ
ГДЕ
	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) > 0
	И ЕСТЬNULL(Остатки.СуммаУпрОстаток, 0) > 0
	И ЕСТЬNULL(ТабРасчетаДолей.ОбщСумма, 0) > 0
	И ЕСТЬNULL(ТабРасчетаДолей.ОбщКоличПоКатолгу, 0) > 0
;

УНИЧТОЖИТЬ
	ТабВалют
;
УНИЧТОЖИТЬ
	ТабНелеквидовИКондидатов
;
УНИЧТОЖИТЬ
	ТабРасчетаДолей
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
#Область ИзмеренияПостроителяДляСвойств
	ТабИтоговая.Номенклатура               КАК Номенклатура,
	ТабИтоговая.СкладКомпании                 КАК СкладКомпании,
	ТабИтоговая.Контрагент                 КАК Контрагент,
	ТабИтоговая.СкладКомпании.Подразделение КАК Подразделение,
	ТабИтоговая.СкладКомпании.Организация КАК Организация,
#КонецОбласти
	ТабИтоговая.Количество                 КАК Количество,
	ТабИтоговая.Себестоимость              КАК Себестоимость,
	ТабИтоговая.ЗапасНаПродажу             КАК ЗапасНаПродажу,
	ТабИтоговая.КоличествоМесяцевБезПродаж КАК КоличествоМесяцевБезПродаж,
	ТабИтоговая.СебестоимостьПоТипуЦен     КАК СебестоимостьПоТипуЦен,
	1/ТабИтоговая.ОбщКоличПоКатолгу*100    КАК ДоляВАсортименте,
	ВЫБОР
		КОГДА ТабИтоговая.КоличествоМесяцевБезПродаж >= &ЗапасПервыйПредел ТОГДА
			"Нелеквидные товары"
		ИНАЧЕ
			"Притенденты в нелеквидные товары"
	КОНЕЦ КАК ГруппаТоваров,
	ТабИтоговая.ДоляСебестоимости КАК ДоляСебестоимости
//СВОЙСТВА

{ВЫБРАТЬ
	Номенклатура.*             КАК Номенклатура,
	СкладКомпании.*            КАК СкладКомпании,
	Контрагент.*               КАК Контрагент,
	Количество                 КАК Количество,
	Себестоимость              КАК Себестоимость,
	ЗапасНаПродажу             КАК ЗапасНаПродажу,
	КоличествоМесяцевБезПродаж КАК КоличествоМесяцевБезПродаж,
	СебестоимостьПоТипуЦен     КАК СебестоимостьПоТипуЦен,
	ДоляВАсортименте           КАК ДоляВАсортименте,
	ДоляСебестоимости          КАК ДоляСебестоимости,
	Организация.*              КАК Организация,
	Подразделение.* КАК Подразделение
	//СВОЙСТВА
}
ИЗ
	ТабИтоговая КАК ТабИтоговая
//СОЕДИНЕНИЯ

ГДЕ
	ТабИтоговая.КоличествоМесяцевБезПродаж >= &ЗапасПервыйПредел
	ИЛИ (ТабИтоговая.КоличествоМесяцевБезПродаж >= &ЗапасВторойПредел
		И ТабИтоговая.КоличествоМесяцевБезПродаж < &ЗапасПервыйПредел
		И ЗапасНаПродажу >= &КритичныйЗапас)

{ГДЕ
	ТабИтоговая.Номенклатура.*      КАК Номенклатура,
	ТабИтоговая.СкладКомпании.*             КАК СкладКомпании,
	ТабИтоговая.Контрагент.*        КАК Контрагент,
	ТабИтоговая.СкладКомпании.Подразделение.* КАК Подразделение,
	ТабИтоговая.СкладКомпании.Организация.* КАК Организация
	//СВОЙСТВА
}

{УПОРЯДОЧИТЬ ПО
	Номенклатура.* КАК Номенклатура,
	СкладКомпании.*        КАК СкладКомпании,
	Контрагент.*   КАК Контрагент,
	ЗапасНаПродажу КАК ЗапасНаПродажу,
	КоличествоМесяцевБезПродаж КАК КоличествоМесяцевБезПродаж
	//СВОЙСТВА
}

ИТОГИ
	СУММА(Количество),
	СУММА(Себестоимость),
	СРЕДНЕЕ(ЗапасНаПродажу),
	СРЕДНЕЕ(КоличествоМесяцевБезПродаж),
	ВЫБОР
		КОГДА СкладКомпании ЕСТЬ NULL ТОГДА
			СУММА(ДоляВАсортименте)/КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СкладКомпании)
		ИНАЧЕ 
			СУММА(ДоляВАсортименте)
	КОНЕЦ КАК ДоляВАсортименте,
	ВЫБОР
		КОГДА &ВалютаТипаЦен <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
			СУММА(СебестоимостьПоТипуЦен)
		ИНАЧЕ
			ВЫБОР
				КОГДА Номенклатура ЕСТЬ NULL ТОГДА
					0
				ИНАЧЕ
					СУММА(СебестоимостьПоТипуЦен)
			КОНЕЦ
	КОНЕЦ КАК СебестоимостьПоТипуЦен,
	ВЫБОР
		КОГДА СкладКомпании ЕСТЬ NULL ТОГДА
			СУММА(ДоляСебестоимости)/КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СкладКомпании)
		ИНАЧЕ
			СУММА(ДоляСебестоимости)
	КОНЕЦ КАК ДоляСебестоимости
	//ИТОГИСВОЙСТВА

{ИТОГИ ПО
	ГруппаТоваров                   КАК ГруппаТоваров,
	СкладКомпании.*             КАК СкладКомпании,
	Номенклатура.*      КАК Номенклатура,
	Контрагент.*        КАК Контрагент,
	Подразделение.* КАК Подразделение,
	Организация.* КАК Организация
	//СВОЙСТВА
}
ПО
	ОБЩИЕ,
	СкладКомпании,
	Номенклатура
АВТОУПОРЯДОЧИВАНИЕ
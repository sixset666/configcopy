
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Функция ПолучитьСвойства() Экспорт
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("ПодразделениеКомпании");
	СтруктураСвойств.Вставить("Организация");
	СтруктураСвойств.Вставить("ЗаказНаряд");
	СтруктураСвойств.Вставить("Цех");
	СтруктураСвойств.Вставить("Автомобиль");
	СтруктураСвойств.Вставить("Модель");
	СтруктураСвойств.Вставить("Контрагент");
	СтруктураСвойств.Вставить("ДокументОплаты");
	СтруктураСвойств.Вставить("ИсточникИнформации");
	
	Возврат СтруктураСвойств;
	
КонецФункции

// Стандартная процедура настройки схемы компоновщика данных
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
	ФорматПредставленияГодаВыпускаАвтомобиля = орПолучитьФорматПредставленияГодаВыпускаАвтомобиля();
	Для Каждого ТекНабор Из СхемаКомпоновкиДанных.НаборыДанных Цикл
		Для Каждого ТекПоле Из ТекНабор.Поля Цикл
			Если НЕ ТипЗнч(ТекПоле) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			ИмяПоля = СокрЛП(ТекПоле.Поле);
			Если ИмяПоля = "ГодВыпуска" Тогда
				ТекПоле.ПараметрыРедактирования.УстановитьЗначениеПараметра("ФорматРедактирования", ФорматПредставленияГодаВыпускаАвтомобиля);
				ТекПоле.Оформление.УстановитьЗначениеПараметра("Формат", ФорматПредставленияГодаВыпускаАвтомобиля);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// Устанавливает основные параметры
//
//Процедура УстановитьПараметры()
	
	//ВалютаОтчета  = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	//ДатаОкончания = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончания", КомпоновщикНастроек.Настройки);
	//
	//КурсВалюты    = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаОкончания, Новый Структура("Валюта",ВалютаОтчета));
	//КурсВалютыУпр = КурсВалюты.Курс / ?(КурсВалюты.Кратность = 0,1,КурсВалюты.Кратность);
	//КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КурсВалютыУпр", КурсВалютыУпр);
	
//КонецПроцедуры

Процедура ПолучитьОтборСКД(ПолеКомпоновки, Отбор, НайденныеОтборы)
	
	Для Каждого ТекОтбор Из Отбор Цикл
		Если ТекОтбор.Использование Тогда
			Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				ПолучитьОтборСКД(ПолеКомпоновки, ТекОтбор.Элементы, НайденныеОтборы);
			ИначеЕсли ТипЗнч(ТекОтбор) = Тип("ЭлементОтбораКомпоновкиДанных") И ТекОтбор.ЛевоеЗначение = ПолеКомпоновки Тогда
				НайденныеОтборы.Добавить(ТекОтбор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	//УстановитьПараметры();
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

// Процедура помогает настроить отчет непосредственно перед выводом
//
Процедура ПередФормированиемМакетаКомпоновки(ПараметрСхемаКомпоновкиДанных, ПараметрКомпоновщикНастроек) Экспорт
	
	Поля = Новый Массив;
	Поля.Добавить("ДокументОплаты");
	ОтчетыСервер.КомпоновщикУстановитьОтборПустыхГруппировок(ПараметрКомпоновщикНастроек.Настройки.Структура, Поля);
	
	ФорматПредставления = обПраво("ФорматПредставленияГодаВыпускаАвтомобиля");
	
	Если ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.MMyyyy ИЛИ 
		ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.yyyy Тогда
		
		ПолеПоиска      = Новый ПолеКомпоновкиДанных("ГодВыпуска");
		НайденныеОтборы = Новый Массив;
		ПолучитьОтборСКД(ПолеПоиска, ПараметрКомпоновщикНастроек.Настройки.Отбор.Элементы, НайденныеОтборы);
		Для Каждого ТекОтбор Из НайденныеОтборы Цикл
			Если ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.MMyyyy Тогда
				ТекОтбор.ПравоеЗначение = НачалоМесяца(ТекОтбор.ПравоеЗначение);
			ИначеЕсли ФорматПредставления = Перечисления.ФорматПредставленияГодаВыпускаАвтомобиля.yyyy Тогда
				ТекОтбор.ПравоеЗначение = НачалоГода(ТекОтбор.ПравоеЗначение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПередФормированиемМакетаКомпоновки()

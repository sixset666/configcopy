#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                               //хранит текст запроса
Перем ПараметрыИспользованияПолейИПоказателей Экспорт;    //хранит параметры использование полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                       //хранит дерево доступных полей
Перем СтруктураСвязиСвойств Экспорт;                      //хранит структура связи свойств
Перем ПриемникПеретаскивания Экспорт;                     //хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;            //хранит структуру не связанных полей
Перем ДополнительныеПоляОтчета Экспорт;                   //хранит дополнительные поля отчета
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;      //хранит структуру итоговых выражений показателей
Перем ПромежуточныйТекстЗапроса;                          //хранит промежуточный текст запроса
Перем мСортировка Экспорт;                                //хранит возможные сортировки
Перем мПериодичность Экспорт;                             //хранит варианты периодичности
Перем мДетализация Экспорт;                               //хранит варианты детализации
Перем ТаблицаДляВывода;                                   //хранит таблица для вывода

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ПараметрыИспользованияПолейИПоказателей["XYZГруппа"].Использование.Ресурсы["ПроцентНаценки"]		= Ложь;;
	ПараметрыИспользованияПолейИПоказателей["XYZГруппа"].Использование.Ресурсы["КоэффициентВариации"] = Ложь;

	ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Использование.Ресурсы.ПроцентНаценки 		= Ложь;
	ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Использование.Ресурсы.КоэффициентВариации = Ложь;
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// получить таблицу источника данных
//
// Параметры
//  ТекПостроитель  – ПостроительОтчета - текущий построитель
//  ТаблицаВывода - таблица  
//  СтруктураКолонок - структура 
//
// Возвращаемое значение:
//  ТаблицаВывода
// 
Функция ПолучитьТаблицуИсточникаДанных(ТекПостроитель, ТаблицаВывода, СтруктураКолонок) Экспорт
	
	Если ТекПостроитель.ИзмеренияСтроки.Найти("XYZГруппа") = Неопределено Тогда
		СтрокаДерева = ДеревоДоступныхПолей.Строки.Найти("XYZГруппа", "ПутьКДанным");
		НоваяСтрока = ТекПостроитель.ИзмеренияСтроки.Вставить(СтрокаДерева.ПутьКДанным, СтрокаДерева.Имя, ТипИзмеренияПостроителяОтчета.Элементы, , , 0);
		НоваяСтрока.Представление = СтрокаДерева.Представление;
	КонецЕсли;
	
	Если ТаблицаВывода.Колонки.Найти("XYZГруппа") = Неопределено Тогда
		ТаблицаВывода.Колонки.Добавить("XYZГруппа", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	
	ТекПостроитель.ИзмеренияСтроки.Добавить("Период"+Периодичность);
	
	Выборка = ТекПостроитель.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, ТекПостроитель.ИзмеренияСтроки[1].ПутьКДанным, "Все");
	
	//КоличествоИзмерений = 3;
	//НачальнаяПозиция = 1;
	
	НакопленнаяСумма = 0;
	ПорогХ = ГруппаX;
	ПорогУ = ГруппаY;
	
	СтруктураВычисляемыхПоказателей = Новый Структура ("КоэффициентВариации, Доля");
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(15,2));
	
	СтруктураНеобходимыхПоказателей = Новый Структура ("КоэффициентВариации, СебестоимостьУпр, СуммаНаценкиУпр, СуммаРегл, СуммаСкидки");
	Для Каждого ТекНужныйПоказатель Из СтруктураНеобходимыхПоказателей Цикл
		Если ТаблицаВывода.Колонки.Найти(ТекНужныйПоказатель.Ключ) = Неопределено Тогда
			ТаблицаВывода.Колонки.Добавить(ТекНужныйПоказатель.Ключ, ОписаниеТиповЧисло);
		КонецЕсли;
	КонецЦикла;
	
	ДетализацияВыборкиПериода = ?(НеУчитыватьДниБезПродаж, "", "Все");
	Пока Выборка.Следующий() Цикл
		
		ТекСуммаПараметра = Выборка[Сортировка];
		
		//Если ВычислятьXYZ Тогда
		Отклонение = 0;
		ВыборкаПоПериоду = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период"+Периодичность, ДетализацияВыборкиПериода);
		КоличествоВВыборке = ВыборкаПоПериоду.Количество();
		
		Среднее = ТекСуммаПараметра/КоличествоВВыборке;
		Пока ВыборкаПоПериоду.Следующий() Цикл
			Отклонение = Отклонение + Pow(?(ВыборкаПоПериоду[Сортировка]=Null, -Среднее, (ВыборкаПоПериоду[Сортировка] - Среднее)), 2);
		КонецЦикла;
		
		Если Среднее = 0 Тогда
			КоэффициентВариации = 0;
		Иначе
			КоэффициентВариации = ?(КоличествоВВыборке = 0,0,(SQRT(Отклонение/КоличествоВВыборке)/Среднее)*100);
		КонецЕсли;
		
		////Если КоэффициентВариации < 0 И НеУчитыватьОтрицательнуюВариацию Тогда Продолжить; КонецЕсли;
		
		КоэффициентВариацииПоМодулю = обМод(КоэффициентВариации);
		
		Если КоэффициентВариацииПоМодулю <= ПорогХ Тогда
			ТекXYZГруппа = "Группа X";
		ИначеЕсли КоэффициентВариацииПоМодулю <= ПорогУ Тогда
			ТекXYZГруппа = "Группа Y";
		Иначе
			ТекXYZГруппа = "Группа Z";
		КонецЕсли;
		//КонецЕсли;
		
		ВыборкаДетали = Выборка.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока ВыборкаДетали.Следующий() Цикл
			Если НЕ ВыборкаДетали.Группировка() = "" Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаВывода.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетали);
			НоваяСтрока.XYZГруппа           = ТекXYZГруппа;
			НоваяСтрока.КоэффициентВариации = КоэффициентВариации;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаВывода;
	
КонецФункции // ПолучитьТаблицуИсточникаДанных()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента – передает ссылку
//                      на табличный документ или поле табличного документа
//
// Возвращаемое значение:
//  отчет в виде табличного документа 
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Если НЕ отКорректностьЗаполнения(ЭтотОбъект) Тогда	
		Возврат ДокументРезультат;	
	КонецЕсли;
	
	Если (ГруппаX > ГруппаY) ИЛИ  (ГруппаY > ГруппаZ) Тогда
		Предупреждение("Параметры XYZ должны быть упорядочены по возрастанию!");
		Возврат ДокументРезультат;
	КонецЕсли;
	
	Если СокрЛП(Сортировка) = "" Тогда
		Предупреждение("Не задана сортировка данных отчета!");
		Возврат ДокументРезультат;
	КонецЕсли;

	ОсновныеДействияПередФормированием();
	
	Если Детализация = "ПоТипуНоменклатуры" Тогда
		ТекстПостроителя = СтрЗаменить(ТекстЗапроса, "Номенклатура КАК Номенклатура","Номенклатура.ТипНоменклатуры КАК Номенклатура");
	КонецЕсли;

	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат, 1);
	Результат.ТолькоПросмотр = обПраво("ЗащитаПечатныхФорм", глПрава);
	
	ОсновныеДействияПослеФормирования();

	Возврат Результат;
КонецФункции // СформироватьТабличныйДокумент()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// возвращает список сортировки
// 
// Без параметров
//                 
// Возвращаемое значение:
//  СписокСортировки - список
// 
Функция СтруктураСортировки() Экспорт
		
	Если СокрЛП(Сортировка) = "" Тогда
		СписокСортировки = Неопределено;
	Иначе
		СписокСортировки = Новый СписокЗначений();
		СписокСортировки.Добавить(НаправлениеСортировки.Возр, "XYZГруппа");
		СписокСортировки.Добавить(НаправлениеСортировки.Возр, "КоэффициентВариации");
	КонецЕсли;
	Возврат СписокСортировки;
	
КонецФункции // СтруктураФорматированияПолей()

// Производит основные действия перед формированием
//
// Без параметров
//
Процедура ОсновныеДействияПередФормированием()
	
	ПромежуточныйТекстЗапроса = ТекстЗапроса;
	
	Показатель = Показатели.Найти(Сортировка);
	Показатель.Использование = Истина;

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПЕРИОДИЧНОСТЬ", ", Период"+Периодичность+" ПЕРИОДАМИ ("+Периодичность+", &ДатаНач, &ДатаКон)");

КонецПроцедуры // ОсновныеДействияПередФормированием()

// Производит основные действия после формирования
//
// Без параметров
//
Процедура ОсновныеДействияПослеФормирования()
		
	ТекстЗапроса = ПромежуточныйТекстЗапроса;
		
КонецПроцедуры // ОсновныеДействияПослеФормирования()


//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСортировка = Новый СписокЗначений;
мСортировка.Добавить("СуммаРегл", "Сумма продаж (Регл.)");
мСортировка.Добавить("СуммаУпр", "Сумма продаж (Упр.)");
мСортировка.Добавить("СуммаНаценкиРегл", "Сумма наценки (Регл.)");
мСортировка.Добавить("СуммаНаценкиУпр", "Сумма наценки (Упр.)");
мСортировка.Добавить("ПроцентНаценки", "Процент наценки");
мСортировка.Добавить("СебестоимостьРегл", "Себестоимость (Регл.)");
мСортировка.Добавить("СебестоимостьУпр", "Себестоимость (Упр.)");
мСортировка.Добавить("Количество", "Количество (в базовых ед.)");
мСортировка.Добавить("СуммаСкидки", "Сумма скидки");
мСортировка.Добавить("ПроцентСкидки", "Процент скидки");

мПериодичность = Новый СписокЗначений;
мПериодичность.Добавить("День", "День");
мПериодичность.Добавить("Неделя", "Неделя");
мПериодичность.Добавить("Месяц", "Месяц");

мДетализация = Новый СписокЗначений;
мДетализация.Добавить("ПоНоменклатуре", "Номенклатура");
мДетализация.Добавить("ПоТипуНоменклатуры", "Тип номенклатуры");

ГруппаX = 10;
ГруппаY = 25;
ГруппаZ = 65;

//установим сортировку
Сортировка    = мСортировка[0].Значение;
Периодичность = мПериодичность[0].Значение;
Детализация   = мДетализация[0].Значение;

ИмяРегистра = "Продажи";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();
#КонецЕсли
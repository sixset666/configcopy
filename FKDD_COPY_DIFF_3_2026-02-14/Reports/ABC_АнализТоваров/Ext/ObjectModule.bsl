#Если Клиент Тогда
// Модуль отчета
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт; 	//хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; //хранит Параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт; //хранит дерево доступных полей
Перем СтруктураСвязиСвойств Экспорт; //хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт; //хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт; //хранит структуру не связанных полей
Перем ДополнительныеПоляОтчета Экспорт; //хранит дополнительные поля отчета
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт; 	//хранит структуру итоговых выражений показателей
Перем мСортировка Экспорт; // Возможные сортировки
Перем мДетализация Экспорт; // Варианты детализации
Перем ВыражениеРасчетаДоли Экспорт; // Варианты детализации

	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА
	
// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
// 
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
		
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ТекСтруктураПараметров = ПараметрыИспользованияПолейИПоказателей["ABCГруппа"];
	ТекСтруктураПараметров.Использование.Ресурсы["ПроцентСкидки"]		= Ложь;
	ТекСтруктураПараметров.Использование.Ресурсы["ПроцентНаценки"]		= Ложь;
	
	ПостроительОтчета.Параметры.Вставить("ГруппаА");
	ПостроительОтчета.Параметры.Вставить("ГруппаВ");
	ПостроительОтчета.Параметры.Вставить("ГруппаС");
	
	
	Если ТипЗнч(СтруктураИтоговыхВыраженийПоказателей) = Тип("Структура") И СтруктураИтоговыхВыраженийПоказателей.Свойство("Доля") Тогда
		
		ВыражениеРасчетаДоли = СтруктураИтоговыхВыраженийПоказателей.Доля;
		
	КонецЕсли;
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// получить таблицу источника данных
//
// Параметры
//  ТекПостроитель  – ПостроительОтчета - текущий построитель
//  ТаблицаВывода - таблица  
//  СтруктураКолонок - структура 
//
// Возвращаемое значение:
//  ТаблицаВывода
// 
Функция ПолучитьТаблицуИсточникаДанных(ТекПостроитель, ТаблицаВывода, СтруктураКолонок) Экспорт

	//КоличествоИзмерений = 2;
	ТекПостроитель.ИзмеренияСтроки.Добавить("Номенклатура");
	Выборка = ТекПостроитель.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,ТекПостроитель.ИзмеренияСтроки[1].ПутьКДанным, "Все");
	
	ВсегоСумма = 0;
	Пока Выборка.Следующий() Цикл
		ВсегоСумма = ВсегоСумма+Выборка[Сортировка];
	КонецЦикла;
	
	Выборка.Сбросить();
	Если ВсегоСумма<0 Тогда
		ВсегоСумма=-ВсегоСумма;
	КонецЕсли;
	ПорогА = Окр((ВсегоСумма*ГруппаА/100),2);
	ПорогВ = Окр((ВсегоСумма*ГруппаВ/100),2);
	ГраницаПорогаВ = ПорогА + ПорогВ;
	
	НакопленнаяСумма = 0;
	СтруктураНеобходимыхПоказателей = Новый Структура ("СебестоимостьУпр, СуммаУпр, СуммаСкидки, СуммаРегл");
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(15,2));
	Для Каждого ТекНужныйПоказатель Из СтруктураНеобходимыхПоказателей Цикл
		Если ТаблицаВывода.Колонки.Найти(ТекНужныйПоказатель.Ключ) = Неопределено Тогда
			ТаблицаВывода.Колонки.Добавить(ТекНужныйПоказатель.Ключ, ОписаниеТиповЧисло);
		КонецЕсли;
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл
		ТекСуммаПараметра = Выборка[Сортировка];
		НакопленнаяСумма = НакопленнаяСумма + ТекСуммаПараметра;
		Если НакопленнаяСумма <= ПорогА ИЛИ ПорогА < ТекСуммаПараметра Тогда
			ТекABCГруппа = "Группа А";
		ИначеЕсли НакопленнаяСумма <= ГраницаПорогаВ ИЛИ ПорогВ < ТекСуммаПараметра Тогда
			ТекABCГруппа = "Группа В";
		Иначе
			ТекABCГруппа = "Группа С";
		КонецЕсли;
		
		ВыборкаДетали = Выборка.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока ВыборкаДетали.Следующий() Цикл
			Если НЕ ВыборкаДетали.Группировка() = "" Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаВывода.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетали);
			НоваяСтрока.ABCГруппа = ТекABCГруппа;
		КонецЦикла;
	КонецЦикла;
	
	ЕстьДоля = Показатели.Найти("Доля", "Имя").Использование;
	Если ЕстьДоля Тогда
		ТаблицаВывода.Колонки.Добавить("ИтоговаяСуммаСортировки", Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(15,3)));
		ТаблицаВывода.ЗаполнитьЗначения(ВсегоСумма, "ИтоговаяСуммаСортировки");
	КонецЕсли;
	
	Возврат ТаблицаВывода;
	
КонецФункции

// формирует отчет в виде табличного документа и возвращает его в качестве результата
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  отчет в виде табличного документа 
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Показатель = Показатели.Найти(Сортировка, "Имя");
	Показатель.Использование = Истина;
	СтруктураИтоговыхВыраженийПоказателей.Вставить("Доля", СтрЗаменить(ВыражениеРасчетаДоли, "ВыбСуммаСортировки", Сортировка));
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,1);
	Результат.ТолькоПросмотр = обПраво("ЗащитаПечатныхФорм", глПрава);
	
	Возврат Результат;
КонецФункции // СформироватьТабличныйДокумент()
	
// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт
		
	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);
		
	// Если необходимо, добавим другие или переустановим строки форматирования полей
		
	Возврат СтруктураФорматаПолей;
		
КонецФункции	//	СтруктураФорматированияПолей()

// возвращает список сортировки
// 
// Без параметров
//                 
// Возвращаемое значение:
//  СписокСортировки - список
// 
Функция СтруктураСортировки() Экспорт
	
	Если СокрЛП(Сортировка) = "" Тогда
		СписокСортировки = Неопределено;
	Иначе
		СписокСортировки = Новый СписокЗначений();
		СписокСортировки.Добавить(НаправлениеСортировки.Возр, "ABCГруппа");
		
		СписокСортировки.Добавить(НаправлениеСортировки.Убыв, Сортировка);
	КонецЕсли;
		
	Возврат СписокСортировки;
 	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	
мСортировка = Новый СписокЗначений;
мСортировка.Добавить("СуммаРегл", "Сумма продаж (Регл.)");
мСортировка.Добавить("СуммаУпр", "Сумма продаж (Упр.)");
мСортировка.Добавить("СуммаНаценкиРегл", "Сумма наценки (Регл.)");
мСортировка.Добавить("СуммаНаценкиУпр", "Сумма наценки (Упр.)");
мСортировка.Добавить("ПроцентНаценки", "Процент наценки");
мСортировка.Добавить("СебестоимостьРегл", "Себестоимость (Регл.)");
мСортировка.Добавить("СебестоимостьУпр", "Себестоимость (Упр.)");
мСортировка.Добавить("Количество", "Количество (в базовых ед.)");
мСортировка.Добавить("СуммаСкидки", "Сумма скидки");
мСортировка.Добавить("ПроцентСкидки", "Процент скидки");

мДетализация = Новый СписокЗначений;
мДетализация.Добавить("ПоНоменклатуре", "Номенклатура");
мДетализация.Добавить("ПоТипуНоменклатуры", "Тип номенклатуры");

ГруппаА = 75;
ГруппаВ = 20;
ГруппаС = 5;
	
Сортировка  = мСортировка[0].Значение;
Детализация = мДетализация[0].Значение;
	
ИмяРегистра = "Продажи";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();
ВыражениеРасчетаДоли = "ВЫРАЗИТЬ(ВЫБОР КОГДА МАКСИМУМ(ИтоговаяСуммаСортировки)=0 ТОГДА 0 ИНАЧЕ (СУММА(ВыбСуммаСортировки)/МАКСИМУМ(ИтоговаяСуммаСортировки))*100 КОНЕЦ КАК Число(15,2))";

#КонецЕсли
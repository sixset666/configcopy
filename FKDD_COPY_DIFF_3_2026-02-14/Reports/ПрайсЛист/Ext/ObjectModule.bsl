#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем ТаблицаПараметровТипаЦен;                        // Таблица параметров типа цена
  
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// возвращает текст запроса отчета
Функция ПолучитьТекстЗапроса() Экспорт
	
	ТекстЗапросаРезультат = "//ТАБЛИЦА ПРАЙС-ЛИСТОВ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	ПрайсЛисты.Ссылка КАК ПрайсЛист,
	|	ПрайсЛисты.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаПрайсЛистов
	| ИЗ
	|	Справочник.ПрайсЛист КАК ПрайсЛисты
	|{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПрименяемость КАК НоменклатураПрименяемость
	|ПО ПрайсЛисты.Номенклатура = НоменклатураПрименяемость.Номенклатура}
	|ГДЕ
	|	ПрайсЛисты.Владелец = &ВидПрайсЛиста И
	|	ПрайсЛисты.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|{ГДЕ
	|	ПрайсЛисты.Ссылка.* КАК Товар,
	|	ПрайсЛисты.Номенклатура.* КАК Номенклатура,
	|	НоменклатураПрименяемость.Модель.* КАК НоменклатураПрименяемостьМодель
	|}
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;";
	
	Если ВНаличиеНаСкладе Тогда
		
		ТекстЗапросаРезультат=ТекстЗапросаРезультат+"//ТАБЛИЦА ОСТАТКОВ
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрайсЛисты.ПрайсЛист КАК ПрайсЛист,
		|	ПрайсЛисты.Номенклатура КАК Номенклатура,
		|	ПрайсЛисты.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
		|	(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток) / ПрайсЛисты.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Количество
		|ПОМЕСТИТЬ ТаблицаОстатков
		|ИЗ
		|	ТаблицаПрайсЛистов КАК ПрайсЛисты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаКон, Номенклатура В (ВЫБРАТЬ РАЗЛИЧНЫЕ ТаблицаПрайсЛистов.Номенклатура ИЗ ТаблицаПрайсЛистов КАК ТаблицаПрайсЛистов)
		|   		{Номенклатура.* КАК Номенклатура,
		|   		СкладКомпании.* КАК СкладКомпании,
		|   		СкладКомпании.Организация.* КАК Организация}) КАК ОстаткиТоваровКомпанииОстатки
		| ПО ПрайсЛисты.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура И 
		|   (ОстаткиТоваровКомпанииОстатки.КоличествоОстаток-ОстаткиТоваровКомпанииОстатки.РезервОстаток)>0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура" + ?(ВыводитьЦеныЕдиницНоменклатуры, ",
		|	ЕдиницаИзмерения", "") + ?(ВыводитьЦеныХарактеристикНоменклатуры, ",
		|	ХарактеристикаНоменклатуры", "") + "
		|;
		|УНИЧТОЖИТЬ ТаблицаПрайсЛистов
		|;";
	ИначеЕсли НЕ (ВыводитьЦеныЕдиницНоменклатуры ИЛИ ВыводитьЦеныХарактеристикНоменклатуры) Тогда
		ТекстЗапросаРезультат=ТекстЗапросаРезультат+"//ТАБЛИЦА ОСТАТКОВ
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрайсЛисты.ПрайсЛист КАК ПрайсЛист,
		|	ПрайсЛисты.Номенклатура КАК Номенклатура,
		|	ВЫБОР 
		|		КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура ЕСТЬ NULL ТОГДА 0
		|		ИНАЧЕ (ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток)/ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ ТаблицаОстатков
		|ИЗ
		|	ТаблицаПрайсЛистов КАК ПрайсЛисты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаКон, Номенклатура В (ВЫБРАТЬ РАЗЛИЧНЫЕ ТаблицаПрайсЛистов.Номенклатура ИЗ ТаблицаПрайсЛистов КАК ТаблицаПрайсЛистов)
		|   		{Номенклатура.* КАК Номенклатура,
		|   		СкладКомпании.* КАК СкладКомпании,
		|   		СкладКомпании.Организация.* КАК Организация}) КАК ОстаткиТоваровКомпанииОстатки
		| ПО ПрайсЛисты.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|УНИЧТОЖИТЬ ТаблицаПрайсЛистов
		|;";
	Иначе
		ТекстЗапросаРезультат=ТекстЗапросаРезультат+"//ТАБЛИЦА ПРАЙС-ЛИСТОВ С ДОПОЛНЕНИЯМИ
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрайсЛисты.ПрайсЛист КАК ПрайсЛист,
		|	ПрайсЛисты.Номенклатура КАК Номенклатура"+?(ВыводитьЦеныХарактеристикНоменклатуры,",
		|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаНоменклатуры","")+?(ВыводитьЦеныЕдиницНоменклатуры,",
		|	ЕСТЬNULL(ЕдиницыИзмерения.Ссылка, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))  КАК ЕдиницаИзмерения","")+"
		|ПОМЕСТИТЬ ТаблицаПрайсЛистовСДополнениями
		|ИЗ
		|	ТаблицаПрайсЛистов КАК ПрайсЛисты
		|		"+?(ВыводитьЦеныХарактеристикНоменклатуры,"ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|		ПО (ПрайсЛисты.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 1
		|					И ПрайсЛисты.Номенклатура.ТипНоменклатуры = ХарактеристикиНоменклатуры.Владелец
		|				ИЛИ ПрайсЛисты.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик = 2
		|					И ПрайсЛисты.Номенклатура = ХарактеристикиНоменклатуры.Владелец)","")+"
		|		"+?(ВыводитьЦеныЕдиницНоменклатуры,"ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
		|		ПО (ПрайсЛисты.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1
		|					И ПрайсЛисты.Номенклатура.ТипНоменклатуры = ЕдиницыИзмерения.Владелец
		|				ИЛИ ПрайсЛисты.Номенклатура.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2
		|					И ПрайсЛисты.Номенклатура = ЕдиницыИзмерения.Владелец)","")+"
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|	"+?(ВыводитьЦеныХарактеристикНоменклатуры,",ХарактеристикаНоменклатуры","")+"
		|	"+?(ВыводитьЦеныЕдиницНоменклатуры,",ЕдиницаИзмерения","")+"
		|;
		|УНИЧТОЖИТЬ ТаблицаПрайсЛистов
		|;";
		
		ТекстЗапросаРезультат=ТекстЗапросаРезультат+"//ТАБЛИЦА ОСТАТКОВ
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрайсЛисты.ПрайсЛист КАК ПрайсЛист,
		|	ПрайсЛисты.Номенклатура КАК Номенклатура,
		|	"+?(ВыводитьЦеныЕдиницНоменклатуры,"ПрайсЛисты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,","")+"
		|	"+?(ВыводитьЦеныХарактеристикНоменклатуры,"ПрайсЛисты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,","")+"
		|ВЫБОР 
		|	КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура ЕСТЬ NULL ТОГДА 0
		|	ИНАЧЕ (ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ОстаткиТоваровКомпанииОстатки.РезервОстаток)/ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ ТаблицаОстатков
		|ИЗ
		|	ТаблицаПрайсЛистовСДополнениями КАК ПрайсЛисты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаКон, (Номенклатура"+?(ВыводитьЦеныХарактеристикНоменклатуры,", ХарактеристикаНоменклатуры","")+?(ВыводитьЦеныЕдиницНоменклатуры,", Номенклатура.ОсновнаяЕдиницаИзмерения","")+") В 
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ ТаблицаПрайсЛистов.Номенклатура"+?(ВыводитьЦеныХарактеристикНоменклатуры,", ТаблицаПрайсЛистов.ХарактеристикаНоменклатуры","")+?(ВыводитьЦеныЕдиницНоменклатуры,", ТаблицаПрайсЛистов.ЕдиницаИзмерения","")+" ИЗ ТаблицаПрайсЛистовСДополнениями КАК ТаблицаПрайсЛистов)
		|   		{Номенклатура.* КАК Номенклатура,
		|   		СкладКомпании.* КАК СкладКомпании,
		|   		СкладКомпании.Организация.* КАК Организация}) КАК ОстаткиТоваровКомпанииОстатки
		| ПО ПрайсЛисты.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура"+?(ВыводитьЦеныХарактеристикНоменклатуры," И 
		|	 ПрайсЛисты.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры","")+?(ВыводитьЦеныЕдиницНоменклатуры," И 
		| 	 ПрайсЛисты.ЕдиницаИзмерения = ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения","")+"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура"+?(ВыводитьЦеныЕдиницНоменклатуры,",
		|	ЕдиницаИзмерения","")+?(ВыводитьЦеныХарактеристикНоменклатуры,",
		|	ХарактеристикаНоменклатуры","")+"
		|;
		|УНИЧТОЖИТЬ ТаблицаПрайсЛистовСДополнениями
		|;";
	КонецЕсли;
	
	ТекРодитель = Подразделение;
	ТекстОбъединенияУровней = "";
	Уровень=0;
	
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл
		ПостроительОтчета.Параметры.Вставить("Подразделение"+Уровень, ТекРодитель);
		ТекстОбъединенияУровней = ТекстОбъединенияУровней+"
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	&Подразделение"+Уровень+",
		|	"+Уровень;
		ТекРодитель = ТекРодитель.Родитель;
		Уровень=Уровень+1;
	КонецЦикла;
	
	ТекстОбъединенияУровней = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПодразделений.Родитель КАК Родитель,
	|	ТаблицаПодразделений.Уровень КАК Уровень
	|	ПОМЕСТИТЬ ТаблицаПодразделений
	| ИЗ (
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) КАК Родитель,
	|	"+Уровень+" КАК Уровень
	|"+ТекстОбъединенияУровней+") КАК ТаблицаПодразделений
	|ИНДЕКСИРОВАТЬ ПО Родитель
	|;";
	ТекстЗапросаРезультат = ТекстЗапросаРезультат+Символы.ПС+ТекстОбъединенияУровней;
	
	//ТекстОбъединенияУровней
	ИспользуемыеБазовыеТипы = Новый Структура;
	ТекстТаблицЦенБазовыхТиповЦен = "";
	ТекстСоединения = "";
	ТекстВыборки = "";
	ТекстИтогов = "";
	Для Каждого ТекПоказатель Из Показатели Цикл
		Если Не ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
		ЦеновойПоказатель = ТаблицаПараметровТипаЦен.Найти(ТекПоказатель.Имя, "Имя");
		Если ЦеновойПоказатель <> Неопределено Тогда
			БазовыйТипЦен = ЦеновойПоказатель.ТипЦен;
			ИмяПоля = ЦеновойПоказатель.Имя;
			ШаблонВыборкиЦены = ЦеновойПоказатель.ШаблонРасчетаЦены;
			ИмяБазовогоТипаЦен = ЦеновойПоказатель.ИмяБазовогоТипаЦен;
			Если НЕ ИспользуемыеБазовыеТипы.Свойство(ИмяБазовогоТипаЦен) Тогда
				ИспользуемыеБазовыеТипы.Вставить(ИмяБазовогоТипаЦен);
				ПолеДополнения="";
				ИмяДополнения="";
				Если БазовыйТипЦен.АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоХарактеристике И ВыводитьЦеныХарактеристикНоменклатуры Тогда
					УсловиеОтбора = "ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
					|				И (ХарактеристикаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
					|				ИЛИ (НЕ Номенклатура.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров ИЛИ Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик=3))";
					ПолеДополнения=",ТаблицаЦен.ХарактеристикаНоменклатуры";
					ИмяДополнения = ",ХарактеристикаНоменклатуры";
					ТекстСоединения = ТекстСоединения + "
					|ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаЦенПодразделения"+ИмяБазовогоТипаЦен+" КАК "+ИмяБазовогоТипаЦен+"
					|	ПО ТаблицаОстатков.Номенклатура = "+ИмяБазовогоТипаЦен+".Номенклатура И
					|      ТаблицаОстатков.ХарактеристикаНоменклатуры = "+ИмяБазовогоТипаЦен+".ХарактеристикаНоменклатуры
					|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦенПодразделения"+ИмяБазовогоТипаЦен+" КАК "+ИмяБазовогоТипаЦен+"Дополнения
					|	ПО ТаблицаОстатков.Номенклатура = "+ИмяБазовогоТипаЦен+"Дополнения.Номенклатура И 
					|	"+ИмяБазовогоТипаЦен+"Дополнения.ХарактеристикаНоменклатуры=ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
					
				ИначеЕсли БазовыйТипЦен.АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения И ВыводитьЦеныЕдиницНоменклатуры Тогда
					УсловиеОтбора = "ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
					|				И (ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
					|				ИЛИ НЕ Номенклатура.ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров)";
					ПолеДополнения=",ТаблицаЦен.ЕдиницаИзмерения";
					ИмяДополнения = ",ЕдиницаИзмерения";
					ТекстСоединения = ТекстСоединения + "
					|ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаЦенПодразделения"+ИмяБазовогоТипаЦен+" КАК "+ИмяБазовогоТипаЦен+"
					|	ПО ТаблицаОстатков.Номенклатура = "+ИмяБазовогоТипаЦен+".Номенклатура И
					|      ТаблицаОстатков.ЕдиницаИзмерения = "+ИмяБазовогоТипаЦен+".ЕдиницаИзмерения
					|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦенПодразделения"+ИмяБазовогоТипаЦен+" КАК "+ИмяБазовогоТипаЦен+"Дополнения
					|	ПО ТаблицаОстатков.Номенклатура = "+ИмяБазовогоТипаЦен+"Дополнения.Номенклатура И
					|	"+ИмяБазовогоТипаЦен+"Дополнения.ЕдиницаИзмерения=ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)";
				Иначе
					УсловиеОтбора = "ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) И 
					|		ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)";
					ТекстСоединения = ТекстСоединения + "
					|ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаЦенПодразделения"+ИмяБазовогоТипаЦен+" КАК "+ИмяБазовогоТипаЦен+"
					|	ПО ТаблицаОстатков.Номенклатура = "+ИмяБазовогоТипаЦен+".Номенклатура";
				КонецЕсли;
				
				ТекстТаблицЦенБазовыхТиповЦен = ТекстТаблицЦенБазовыхТиповЦен+"
				|//ТАБЛИЦА ЦЕН "+ИмяБазовогоТипаЦен+"
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ТаблицаПодразделений.Уровень КАК Уровень,
				|	ТаблицаЦен.Номенклатура,
				|	ТаблицаЦен.ТипЦен,
				|	(ВЫБОР КОГДА ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = 1 ТОГДА
				|		ВЫРАЗИТЬ((ТаблицаЦен.Цена + 0.05) * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета КАК ЧИСЛО(15, 1))
				|	КОГДА ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = 0 ТОГДА
				|		ВЫРАЗИТЬ((ТаблицаЦен.Цена + 0.0) * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета КАК ЧИСЛО(15, 0))
				|	КОГДА ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = -1 ТОГДА
				|		(ВЫРАЗИТЬ((ТаблицаЦен.Цена + 5) * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета / 10 КАК ЧИСЛО(15, 0))) * 10
				|	КОГДА ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = -2 ТОГДА
				|		(ВЫРАЗИТЬ((ТаблицаЦен.Цена + 50) * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета / 100 КАК ЧИСЛО(15, 0))) * 100
				|	КОГДА ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = -3 ТОГДА
				|		(ВЫРАЗИТЬ((ТаблицаЦен.Цена + 500) * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета / 1000 КАК ЧИСЛО(15, 0))) * 1000
				|	КОГДА ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = 2 ТОГДА
				|		ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета
				|	КОГДА НЕ ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = 1 ТОГДА
				|		ВЫРАЗИТЬ(ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета КАК ЧИСЛО(15, 1))
				|	КОГДА НЕ ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = 0 ТОГДА
				|		ВЫРАЗИТЬ(ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета КАК ЧИСЛО(15, 0))
				|	КОГДА НЕ ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = -1 ТОГДА
				|		(ВЫРАЗИТЬ(ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета / 10 КАК ЧИСЛО(15, 0))) * 10
				|	КОГДА НЕ ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = -2 ТОГДА
				|		(ВЫРАЗИТЬ(ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета / 100 КАК ЧИСЛО(15, 0))) * 100
				|	КОГДА НЕ ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = -3 ТОГДА
				|		(ВЫРАЗИТЬ(ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета / 1000 КАК ЧИСЛО(15, 0))) * 1000
				|	КОГДА НЕ ТаблицаЦен.ТипЦен.ОкруглятьВБольшуюСторону И ТаблицаЦен.ТипЦен.Точность = 2 ТОГДА
				|		ТаблицаЦен.Цена * КурсыВалютСрезПоследних.Курс / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / &КурсВалютыОтчета
				|	ИНАЧЕ 0
				|	КОНЕЦ) КАК Цена
				|	"+ПолеДополнения+"
				|ПОМЕСТИТЬ ТаблицаЦен"+ИмяБазовогоТипаЦен+"
				|  ИЗ
				|  ТаблицаПодразделений КАК ТаблицаПодразделений
				|  ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&ДатаКон, ТипЦен=&"+ИмяБазовогоТипаЦен+" И Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
				|															И ПодразделениеКомпании В (ВЫБРАТЬ ТаблицаПодразделений.Родитель ИЗ ТаблицаПодразделений КАК ТаблицаПодразделений)
				|															И "+УсловиеОтбора+") КАК ТаблицаЦен
				|		"+?(БазовыйТипЦен.ВВалютеУчета,"	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКон, ) КАК КурсыВалютСрезПоследних
				|				ПО ТаблицаЦен.Номенклатура.ВалютаУчета = КурсыВалютСрезПоследних.Валюта","ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКон, Валюта=&Валюта"+ИмяБазовогоТипаЦен+") КАК КурсыВалютСрезПоследних
				|				ПО ИСТИНА")+"
				|ПО ТаблицаПодразделений.Родитель = ТаблицаЦен.ПодразделениеКомпании
				|ГДЕ
				|	ТаблицаЦен.Цена > 0
				|;
				|
				|// ТАБЛИЦА ОТБОРА ЦЕН ВЫБРАННОГО ПОДРАЗДЕЛЕНИЯ "+ИмяБазовогоТипаЦен+"
				|ВЫБРАТЬ
				|	МИНИМУМ(ТаблицаЦен.Уровень) КАК Уровень,
				|	ТаблицаЦен.Номенклатура КАК Номенклатура
				|	"+ПолеДополнения+"
				|ПОМЕСТИТЬ 
				|	ТаблицаОтбораЦен"+ИмяБазовогоТипаЦен+"
				|	
				|ИЗ 
				|	ТаблицаЦен"+ИмяБазовогоТипаЦен+" КАК ТаблицаЦен
				|СГРУППИРОВАТЬ ПО
				|	ТаблицаЦен.Номенклатура
				|	"+ПолеДополнения+"
				|ИНДЕКСИРОВАТЬ ПО
				|	Номенклатура
				|	"+ИмяДополнения+"
				|;
				|
				|// ТАБЛИЦА ЦЕН ВЫБРАННОГО ПОДРАЗДЕЛЕНИЯ "+ИмяБазовогоТипаЦен+"
				|ВЫБРАТЬ
				|	ТаблицаЦен.Номенклатура,
				|	ТаблицаЦен.Цена,
				|	ТаблицаЦен.ТипЦен
				|	"+ПолеДополнения+"
				|ПОМЕСТИТЬ ТаблицаЦенПодразделения"+ИмяБазовогоТипаЦен+"
				|  ИЗ ТаблицаЦен"+ИмяБазовогоТипаЦен+" КАК ТаблицаЦен
				|  
				|  ГДЕ (Уровень, ТаблицаЦен.Номенклатура"+ПолеДополнения+") В (ВЫБРАТЬ 
				|	ТаблицаЦен.Уровень,
				|	ТаблицаЦен.Номенклатура
				|	"+ПолеДополнения+"
				|	
				|  ИЗ ТаблицаОтбораЦен"+ИмяБазовогоТипаЦен+" КАК ТаблицаЦен)
				|ИНДЕКСИРОВАТЬ ПО
				|	Номенклатура
				|	"+ИмяДополнения+"
				|;
				|УНИЧТОЖИТЬ ТаблицаЦен"+ИмяБазовогоТипаЦен+"
				|;";
			КонецЕсли;	
			
			
			Если (БазовыйТипЦен.АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения И ВыводитьЦеныЕдиницНоменклатуры) Тогда
				ТекстВыборки = ТекстВыборки+",
				|"+СтрЗаменить(ШаблонВыборкиЦены, "*ВыражениеЦены*", "ЕСТЬNULL("+ИмяБазовогоТипаЦен+".Цена, ЕСТЬNULL("+ИмяБазовогоТипаЦен+"Дополнения.Цена*ТаблицаОстатков.ЕдиницаИзмерения.Коэффициент, 0))")+" КАК "+ИмяПоля+",
				|"+СтрЗаменить(ШаблонВыборкиЦены, "*ВыражениеЦены*", "ЕСТЬNULL("+ИмяБазовогоТипаЦен+"Дополнения.Цена, 0)")+" КАК "+ИмяПоля+"Дополнения";
				ТекстИтогов = ТекстИтогов+",
				|	ВЫБОР КОГДА (НЕ Номенклатура ЕСТЬ NULL) ТОГДА МАКСИМУМ("+ИмяПоля+"Дополнения) ИНАЧЕ 0 КОНЕЦ КАК "+ИмяПоля;
				ТекстВыборки = СтрЗаменить(ТекстВыборки, "*ИмяТаблицы*", ИмяБазовогоТипаЦен);
			ИначеЕсли (БазовыйТипЦен.АлгоритмПолученияЦены=Перечисления.АлгоритмПолученияЦены.ПоХарактеристике И ВыводитьЦеныХарактеристикНоменклатуры) Тогда
				ТекстВыборки = ТекстВыборки+",
				|"+СтрЗаменить(ШаблонВыборкиЦены, "*ВыражениеЦены*", "ЕСТЬNULL("+ИмяБазовогоТипаЦен+".Цена, ЕСТЬNULL("+ИмяБазовогоТипаЦен+"Дополнения.Цена, 0))")+?(ВыводитьЦеныЕдиницНоменклатуры, "*ТаблицаОстатков.ЕдиницаИзмерения.Коэффициент","")+" КАК "+ИмяПоля+",
				|"+СтрЗаменить(ШаблонВыборкиЦены, "*ВыражениеЦены*", "ЕСТЬNULL("+ИмяБазовогоТипаЦен+"Дополнения.Цена, 0)")+" КАК "+ИмяПоля+"Дополнения";
				ТекстИтогов = ТекстИтогов+",
				|	ВЫБОР КОГДА (НЕ Номенклатура ЕСТЬ NULL) ТОГДА МАКСИМУМ("+ИмяПоля+"Дополнения) ИНАЧЕ 0 КОНЕЦ КАК "+ИмяПоля;
				ТекстВыборки = СтрЗаменить(ТекстВыборки, "*ИмяТаблицы*", ИмяБазовогоТипаЦен);
			Иначе
				ТекстВыборки = ТекстВыборки+",
				|"+СтрЗаменить(ШаблонВыборкиЦены, "*ВыражениеЦены*", "ЕСТЬNULL("+ИмяБазовогоТипаЦен+".Цена, 0)")+?(ВыводитьЦеныЕдиницНоменклатуры, "*ТаблицаОстатков.ЕдиницаИзмерения.Коэффициент","")+" КАК "+ИмяПоля;
				ТекстИтогов = ТекстИтогов+",
				|	ВЫБОР КОГДА (НЕ Номенклатура ЕСТЬ NULL) ТОГДА МАКСИМУМ("+ИмяПоля+") ИНАЧЕ 0 КОНЕЦ КАК "+ИмяПоля;
				ТекстВыборки = СтрЗаменить(ТекстВыборки, "*ИмяТаблицы*", ИмяБазовогоТипаЦен);
			КонецЕсли;
			ТекстСоединения = "
			|	"+ТекстСоединения + ЦеновойПоказатель.ТекстСоединений;
		КонецЕсли;
	КонецЦикла;
	
	СтрЗапросаСвойств     	= "";
	СтрПсевдонимСвойств     = "";
	СтрЗапросаИтогиСвойств	= "";
	СтрОтборСвойств			= "";
	СтрСоединенийСвойств   	= "";
		
	Для Каждого ТекСвойство Из СтруктураСвязиСвойств Цикл
		ИмяСвойства = СтрЗаменить(ТекСвойство.Ключ, "Значение", "");
		НаименованиеСвойства = ТекСвойство.Значение.Представление;
		ИмяПараметра = ТекСвойство.Значение.ИмяПараметра;
		ПутьКДаннымПоля = "ТаблицаОстатков." + ТекСвойство.Значение.ИмяИзмерения;
		ПостроительОтчета.Параметры.Вставить(ИмяПараметра, ТекСвойство.Значение.Свойство);
		
		ТипСвойства = отПолучитьСтрокуФорматаВыраженияПоТипу(ТекСвойство.Значение.Свойство.ТипЗначения);
		
		//ЗапросСвойстваПоля       = ", ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КАК " + ИмяСвойства + "Значение";
		ЗапросСвойстваПоля    	 = ","+Символы.ПС+
			отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) + " КАК " + ИмяСвойства + "Значение";
		ЗапросСвойстваПсевдоним  = ","+Символы.ПС+ИмяСвойства+"Значение КАК "+ИмяСвойства+"Значение";
		ЗапросСвойстваИтоги      = ","+Символы.ПС+"МАКСИМУМ(" + ИмяСвойства + "Значение)";
		//ЗапросСвойстваОтборы     = ", ВЫРАЗИТЬ(" + ИмяСвойства + ".Значение КАК " + ТипСвойства + ") КАК " + ИмяСвойства + "Значение";
		ЗапросСвойстваОтборы	 = ","+Символы.ПС+
			отПолчитьЗначениеСвойства(ИмяСвойства, ТипСвойства) + " КАК " + ИмяСвойства + "Значение";
		ЗапросСвойстваСоединение = "{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + " ПО " + ИмяСвойства + ".Объект = " + ПутьКДаннымПоля + " И " + ИмяСвойства + ".Свойство = &" + ИмяПараметра + "}" + Символы.ПС;
		
		СтрЗапросаСвойств       = СтрЗапросаСвойств      + ЗапросСвойстваПоля;
		СтрПсевдонимСвойств     = СтрПсевдонимСвойств    + ЗапросСвойстваПсевдоним;
		СтрЗапросаИтогиСвойств  = СтрЗапросаИтогиСвойств + ЗапросСвойстваИтоги;
		СтрОтборСвойств         = СтрОтборСвойств		 + ЗапросСвойстваОтборы;
		СтрСоединенийСвойств    = СтрСоединенийСвойств   + ЗапросСвойстваСоединение;
	КонецЦикла;
	
	ПутьКЕдинице=?(ВыводитьЦеныЕдиницНоменклатуры,"ТаблицаОстатков.ЕдиницаИзмерения","ТаблицаОстатков.Номенклатура.ОсновнаяЕдиницаИзмерения");
	ПутьКХарактеристике=?(ВыводитьЦеныХарактеристикНоменклатуры,"ТаблицаОстатков.ХарактеристикаНоменклатуры","ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)");
	
	ТекстЗапросаРезультат = ТекстЗапросаРезультат+Символы.ПС+ТекстТаблицЦенБазовыхТиповЦен+"
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОстатков.ПрайсЛист КАК Товар,
	|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
	|	"+ПутьКЕдинице+" КАК ЕдиницаИзмерения,
	|	"+ПутьКХарактеристике+" КАК ХарактеристикаНоменклатуры,
	|	ШтрихКоды.ШтрихКод КАК ШтрихКод,
	|	ТаблицаОстатков.Количество КАК Количество"+ТекстВыборки+СтрЗапросаСвойств+"
	|
	|	{ВЫБРАТЬ
	|	Товар.* КАК Товар,
	|	Номенклатура.* КАК Номенклатура,
	|	ЕдиницаИзмерения.* КАК ЕдиницаИзмерения,
	|	ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	|	ШтрихКод КАК ШтрихКод"+СтрПсевдонимСвойств+"
	|	}
	|
	|ИЗ ТаблицаОстатков
	|	"+ТекстСоединения+"
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихКоды КАК ШтрихКоды
	|		ПО ТаблицаОстатков.Номенклатура = ШтрихКоды.Объект
	|			И (НЕ ШтрихКоды.Запрет)
	|			И (ВЫБОР
	|					КОГДА ПрайсЛист.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов = 1 ТОГДА
	|						(ШтрихКоды.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка) И
	|						 ШтрихКоды.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|					КОГДА ПрайсЛист.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов = 2 ТОГДА 
	|						ШтрихКоды.ЕдиницаИзмерения = "+ПутьКЕдинице+"
	|					КОГДА ПрайсЛист.Номенклатура.ТипНоменклатуры.ИспользованиеШтрихКодов = 3 ТОГДА 
	|						ШтрихКоды.ХарактеристикаНоменклатуры = "+ПутьКХарактеристике+"
	|					ИНАЧЕ
	|						(ШтрихКоды.ЕдиницаИзмерения = "+ПутьКЕдинице+" И
	|						 ШтрихКоды.ХарактеристикаНоменклатуры = "+ПутьКХарактеристике+")
	|				КОНЕЦ)
	|				"+СтрСоединенийСвойств+"
	|{ГДЕ
	|	ПрайсЛист.Ссылка.* КАК Товар,
	|	ПрайсЛист.Номенклатура.* КАК Номенклатура"+СтрОтборСвойств+"
	|}
	|{УПОРЯДОЧИТЬ ПО
	|	Товар.* КАК Товар,
	|	Номенклатура.* КАК Номенклатура,
	|	ЕдиницаИзмерения.* КАК ЕдиницаИзмерения,
	|	ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	|	ШтрихКод КАК ШтрихКод"+СтрПсевдонимСвойств+"
	|}
	|{ИТОГИ ПО
	|	Товар.* КАК Товар,
	|	Номенклатура.* КАК Номенклатура,
	|	ЕдиницаИзмерения.* КАК ЕдиницаИзмерения,
	|	ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры"+СтрПсевдонимСвойств+"
	|}
	|ИТОГИ
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(ШтрихКод)"+ТекстИтогов+СтрЗапросаИтогиСвойств+"
	|ПО
	|	Товар,
	|	Номенклатура";
	
	Возврат ТекстЗапросаРезультат;
	
КонецФункции // ПолучитьТекстЗапроса()

// проверяет на корректность заполнения, возвращает "истина", если корректно, "ложь" - если нет
//
// Без параметров
// 
Функция КорректностьЗаполнения(ПродолжитьФормирование) Экспорт
	
	Если обЗначениеНеЗаполнено(ВидПрайсЛиста) Тогда
		Предупреждение("Не указан вид прайс-листа!", 10);
		ПродолжитьФормирование = Ложь;
		Возврат Ложь;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ВалютаОтчета) Тогда
		Предупреждение("Не указана валюта отчета!", 10);
		ПродолжитьФормирование = Ложь;
		Возврат Ложь;
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(Подразделение) Тогда
		Предупреждение("Не выбрано подразделение!", 10);
		ПродолжитьФормирование = Ложь;
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции // КорректностьЗаполнения()

// Устанавливает основные параметры
//
//Без параметров
//
Процедура УстановитьОсновныеПараметры()
	//Заполним параметры
	ПостроительОтчета.Параметры.Вставить("ВидПрайсЛиста",ВидПрайсЛиста);
	
	КурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКонца, Новый Структура("Валюта", ВалютаОтчета));
	КурсВалютыОтчета = КурсВалюты.Курс/?(КурсВалюты.Кратность=0,1,КурсВалюты.Кратность);
	ПостроительОтчета.Параметры.Вставить("КурсВалютыОтчета", ?(КурсВалютыОтчета=0,1,КурсВалютыОтчета));
	
КонецПроцедуры // УстановитьОсновныеПараметры()

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ВыводитьЦеныЕдиницНоменклатуры		  = Истина;
	ВыводитьЦеныХарактеристикНоменклатуры = Истина;
	
	// Вставим вспомогательные параметры
	ПостроительОтчета.Параметры.Вставить("Контрагент",Справочники.Контрагенты.ПустаяСсылка());
	ПостроительОтчета.Параметры.Вставить("НоменклатураПустая",Справочники.Номенклатура.ПустаяСсылка());
	
	СтруктураНеСвязанныхПоказателей.Вставить("ХарактеристикаНоменклатуры",Новый Структура("Показатели", Новый Структура("ХарактеристикаНоменклатуры, ЕдиницаИзмерения")));
	
	ВалютаОтчета	= Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	ВидПрайсЛиста	= Справочники.ВидыПрайсЛистов.ОсновнойВидПрайсЛиста;
	
	Подразделение = Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
		
	// Дополним текст запроса
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ТипыЦен.Ссылка КАК ТипЦен,
	|	ТипыЦен.Наименование КАК Наименование,
	|	ТипыЦен.ВключатьВПрайсЛист
	|ИЗ
	|	Справочник.ТипыЦен КАК ТипыЦен
	|ГДЕ
	|	ТипыЦен.ДляТоваров = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипыЦен.Наименование УБЫВ");
	
	
	ТаблицаПараметровТипаЦен = Новый ТаблицаЗначений;
	ТаблицаПараметровТипаЦен.Колонки.Добавить("Имя");
	ТаблицаПараметровТипаЦен.Колонки.Добавить("ШаблонРасчетаЦены");
	ТаблицаПараметровТипаЦен.Колонки.Добавить("ИмяБазовогоТипаЦен");
	ТаблицаПараметровТипаЦен.Колонки.Добавить("ТипЦен");
	ТаблицаПараметровТипаЦен.Колонки.Добавить("ТекстСоединений");

	Выборка = Запрос.Выполнить().Выбрать();
	Счетчик = 1;
	Пока Выборка.Следующий() Цикл
		РабочийТипЦен = Выборка.ТипЦен;
		ШаблонЦена = "*ВыражениеЦены*";
		// Если расчетная цена получим базовую цену
		ТекстСоединенийПроцентыСкидкиНаценки = "";
		Пока РабочийТипЦен.Рассчитывается Цикл
			
			Если РабочийТипЦен.ПроцентыСкидкиНаценки.Количество()>0 Тогда
				
				ИмяТаблицы = "ПроцентыСкидкиНаценки" + Строка(Счетчик);
				ТекстСоединенийПроцентыСкидкиНаценки = ТекстСоединенийПроцентыСкидкиНаценки + "
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	Справочник.ТипыЦен.ПроцентыСкидкиНаценки КАК " + ИмяТаблицы + "
				|ПО
				|	" + ИмяТаблицы + ".Ссылка = &ТипЦены" + Строка(Счетчик) + " И 
				|	ТаблицаОстатков.Номенклатура.ТипНоменклатуры = " + ИмяТаблицы + ".ТипНоменклатуры";
				
				Если РабочийТипЦен.ОкруглятьВБольшуюСторону Тогда
					Если РабочийТипЦен.Точность = 1 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 0.04999 КОНЕЦ) КАК ЧИСЛО(15, 1)))";
					ИначеЕсли РабочийТипЦен.Точность = 0 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 0.4999 КОНЕЦ) КАК ЧИСЛО(15, 0)))";
					ИначеЕсли РабочийТипЦен.Точность = -1 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 4.999 КОНЕЦ) / 10 КАК ЧИСЛО(15, 0))) * 10";
					ИначеЕсли РабочийТипЦен.Точность = -2 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 49.99 КОНЕЦ) / 100 КАК ЧИСЛО(15, 0))) * 100";
					ИначеЕсли РабочийТипЦен.Точность = -3 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 499.9 КОНЕЦ) / 1000 КАК ЧИСЛО(15, 0))) * 1000";
					Иначе
						ШаблонЦена = "("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
					КонецЕсли;
				Иначе
					Если РабочийТипЦен.Точность = 1 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") КАК ЧИСЛО(15, 1)))";
					ИначеЕсли РабочийТипЦен.Точность = 0 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") КАК ЧИСЛО(15, 0)))";
					ИначеЕсли РабочийТипЦен.Точность = -1 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ("+ШаблонЦена + "/10+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") / 10 КАК ЧИСЛО(15, 0))) * 10";
					ИначеЕсли РабочийТипЦен.Точность = -2 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ("+ШаблонЦена + "/100+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") / 100 КАК ЧИСЛО(15, 0))) * 100";
					ИначеЕсли РабочийТипЦен.Точность = -3 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ("+ШаблонЦена + "/1000+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+") / 1000 КАК ЧИСЛО(15, 0))) * 1000";
					Иначе
						ШаблонЦена = "("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
					КонецЕсли;
				КонецЕсли;
				
				//ШаблонЦена = "("+ШаблонЦена + "+" + ШаблонЦена + "*ЕСТЬNULL(" + ИмяТаблицы + ".ПроцентСкидкиНаценки/100, " + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.; ЧН=0")+"))";
				
				ПостроительОтчета.Параметры.Вставить("ТипЦены" + Строка(Счетчик), РабочийТипЦен);
				
				Счетчик = Счетчик + 1;
				
			ИначеЕсли РабочийТипЦен.ПроцентСкидкиНаценки <> 0 Тогда
				
				Если РабочийТипЦен.ОкруглятьВБольшуюСторону Тогда
					Если РабочийТипЦен.Точность = 1 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+" + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 0.04999 КОНЕЦ) КАК ЧИСЛО(15, 1)))";
					ИначеЕсли РабочийТипЦен.Точность = 0 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+" + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 0.4999 КОНЕЦ) КАК ЧИСЛО(15, 0)))";
					ИначеЕсли РабочийТипЦен.Точность = -1 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+" + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 4.999 КОНЕЦ) / 10 КАК ЧИСЛО(15, 0))) * 10";
					ИначеЕсли РабочийТипЦен.Точность = -2 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+" + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 49.99 КОНЕЦ) / 100 КАК ЧИСЛО(15, 0))) * 100";
					ИначеЕсли РабочийТипЦен.Точность = -3 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+" + ВЫБОР КОГДА " + ШаблонЦена + " = 0 ТОГДА 0 ИНАЧЕ 499.9 КОНЕЦ) / 1000 КАК ЧИСЛО(15, 0))) * 1000";
					Иначе
						ШаблонЦена = "("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
					КонецЕсли;
				Иначе
					Если РабочийТипЦен.Точность = 1 Тогда
						ШаблонЦена = "ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+") КАК ЧИСЛО(15, 1))";
					ИначеЕсли РабочийТипЦен.Точность = 0 Тогда
						ШаблонЦена = "ВЫРАЗИТЬ(("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+") КАК ЧИСЛО(15, 0))";
					ИначеЕсли РабочийТипЦен.Точность = -1 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "/10+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+"/10) КАК ЧИСЛО(15, 0))) * 10";
					ИначеЕсли РабочийТипЦен.Точность = -2 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "/100+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+"/100) КАК ЧИСЛО(15, 0))) * 100";
					ИначеЕсли РабочийТипЦен.Точность = -3 Тогда
						ШаблонЦена = "(ВЫРАЗИТЬ(("+ШаблонЦена + "/1000+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+"/1000) КАК ЧИСЛО(15, 0))) * 1000";
					Иначе
						ШаблонЦена = "("+ШаблонЦена + "+" + ШаблонЦена + "*" + Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100,"ЧРД=.")+")";
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
			
		КонецЦикла;
		
		
		
		ИмяПоля				= "Цена"+СтрЗаменить(Выборка.ТипЦен.УникальныйИдентификатор(),"-","_");
		ПредставлениеПоля	= Выборка.Наименование;
		
		отДобавитьФункциюПоказатель(Показатели, ИмяПоля, ПредставлениеПоля, Выборка.ВключатьВПрайсЛист, "ЧЦ=15; ЧДЦ=2", 20);

		ПараметрыТипа = ТаблицаПараметровТипаЦен.Найти(РабочийТипЦен,"ТипЦен");
		Если ПараметрыТипа = Неопределено Тогда
			ИмяБазовогоТипаЦен = "ТипЦен" + СтрЗаменить(РабочийТипЦен.УникальныйИдентификатор(),"-","_");
			ПостроительОтчета.Параметры.Вставить(ИмяБазовогоТипаЦен, РабочийТипЦен);
			Если НЕ РабочийТипЦен.ВВалютеУчета Тогда
				ПостроительОтчета.Параметры.Вставить("Валюта"+ИмяБазовогоТипаЦен, РабочийТипЦен.ВалютаЦены);
			КонецЕсли;
		Иначе
			ИмяБазовогоТипаЦен = ПараметрыТипа.ИмяБазовогоТипаЦен;
		КонецЕсли;
		
		НовыйПараметр = ТаблицаПараметровТипаЦен.Добавить();
		НовыйПараметр.Имя = ИмяПоля;
		НовыйПараметр.ШаблонРасчетаЦены  = ШаблонЦена;
		НовыйПараметр.ИмяБазовогоТипаЦен = ИмяБазовогоТипаЦен;
		НовыйПараметр.ТипЦен             = РабочийТипЦен;
		НовыйПараметр.ТекстСоединений    = ТекстСоединенийПроцентыСкидкиНаценки;
	КонецЦикла;
	
	
КонецПроцедуры	//ЗаполнитьНачальныеНастройки()

// Процедура убирает вывод итогов для ресурсов 
//
Процедура УбратьИтогиДляЦен()
	
	//Если НЕ Показатели.Найти("ЦенаРозн", "Имя").Использование Тогда Возврат; КонецЕсли;
	ПозицияНоменклатуры = 0;
	Для Каждого ТекСтрока Из ИзмеренияСтроки Цикл
		Если Найти(ТекСтрока.ПутьКДанным, "Номенклатура")>0 Тогда
			ПозицияНоменклатуры = ИзмеренияСтроки.Индекс(ТекСтрока);
			Прервать;
		КонецЕсли;
		ПозицияНоменклатуры = ПозицияНоменклатуры+1;
	КонецЦикла;
	
	Для Индекс=0 По ИзмеренияСтроки.Количество()-1 Цикл
		
		ТекСтрока = ИзмеренияСтроки[Индекс];
		
		ПараметрыПолейИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ПараметрыИспользованияПолейИПоказателей);
		Если ПараметрыПолейИзмерения=Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Использование = Индекс >= ПозицияНоменклатуры;
		Если ПараметрыПолейИзмерения.Свойство("Использование") Тогда
			РесурсыПоля = ПараметрыПолейИзмерения.Использование.Ресурсы;
			Для Каждого ТекРесурс Из Показатели Цикл
				РесурсыПоля.Вставить(ТекРесурс.Имя, Использование);
			КонецЦикла;
		КонецЕсли;
		
		Если ТекСтрока.ТипИзмерения = "Иерархия" Тогда
			ПараметрыПолейИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным+".Родитель", ПараметрыИспользованияПолейИПоказателей);
			Если ПараметрыПолейИзмерения = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			РесурсыПоля = ПараметрыПолейИзмерения.Использование.Ресурсы;
			Для Каждого ТекРесурс Из Показатели Цикл
				РесурсыПоля.Вставить(ТекРесурс.Имя, Ложь);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры //УбратьИтогиДляЦен()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	УстановитьОсновныеПараметры();
	
	УбратьИтогиДляЦен();
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);

	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// возвращает макет построителя отчета
Функция ПолучитьМакетПостроителя(ТекПостроительОтчета, МакетОтчета, ПараметрыОформления) Экспорт
	
	Если ВыводитьЦеныХарактеристикНоменклатуры Тогда
		ТекПостроительОтчета.ИзмеренияСтроки.Добавить("ХарактеристикаНоменклатуры","ХарактеристикаНоменклатуры",ТипИзмеренияПостроителяОтчета.Элементы);
		ТекПостроительОтчета.Порядок.Добавить("ХарактеристикаНоменклатуры");
		Если ВыводитьЦеныЕдиницНоменклатуры Тогда
			ТекПостроительОтчета.Порядок.Добавить("ЕдиницаИзмерения");
		КонецЕсли;
		ТекПостроительОтчета.ВыводитьДетальныеЗаписи = Истина;
	ИначеЕсли ВыводитьЦеныЕдиницНоменклатуры Тогда
		ТекПостроительОтчета.ИзмеренияСтроки.Добавить("ЕдиницаИзмерения","ЕдиницаИзмерения",ТипИзмеренияПостроителяОтчета.Элементы);
		ТекПостроительОтчета.Порядок.Добавить("ЕдиницаИзмерения");
		ТекПостроительОтчета.ВыводитьДетальныеЗаписи = Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// устанавливает флаг использование у измерений
Функция УстановитьИспользованиеИзмерений(Построитель) Экспорт 
	
	//Если ВыводитьЦеныХарактеристикНоменклатуры Тогда
	//	ТекВыбранноеПоле = ДополнительныеПоляОтчета.Добавить();
	//	ТекВыбранноеПоле.Имя = "ХарактеристикаНоменклатуры";
	//	ТекВыбранноеПоле.ПутьКДанным = "ХарактеристикаНоменклатуры";
	//	ТекВыбранноеПоле.ПолноеПредставление = "Характеристика номенклатуры";
	//	ТекВыбранноеПоле.ШиринаКолонки = "20";
	//	ТекВыбранноеПоле.Формат = "";
	//	ТекВыбранноеПоле.НеСвязанные = Истина;
	//	ТекВыбранноеПоле.ВыводитьИтогПоИерархии = Ложь;
	//	ТекВыбранноеПоле.ВыводитьИтогДляВсехУровней = Ложь;
	//	ТекВыбранноеПоле.ВыводитьОбщийИтог = Ложь;
	//	Построитель.ВыбранныеПоля.Добавить("ХарактеристикаНоменклатуры");
	//КонецЕсли;
	
	Если ВыводитьЦеныХарактеристикНоменклатуры И ВыводитьЦеныЕдиницНоменклатуры Тогда
		ТекВыбранноеПоле = ДополнительныеПоляОтчета.Добавить();
		ТекВыбранноеПоле.Имя = "ЕдиницаИзмерения";
		ТекВыбранноеПоле.ПутьКДанным = "ЕдиницаИзмерения";
		ТекВыбранноеПоле.ПолноеПредставление = "Единица измерения";
		ТекВыбранноеПоле.ШиринаКолонки = "14";
		ТекВыбранноеПоле.Формат = "";
		ТекВыбранноеПоле.НеСвязанные = Истина;
		ТекВыбранноеПоле.ВыводитьИтогПоИерархии = Ложь;
		ТекВыбранноеПоле.ВыводитьИтогДляВсехУровней = Ложь;
		ТекВыбранноеПоле.ВыводитьОбщийИтог = Ложь;
		Построитель.ВыбранныеПоля.Добавить("ЕдиницаИзмерения");
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции
//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

//ИмяРегистра = "ОстаткиТоваровКомпании";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
#КонецЕсли
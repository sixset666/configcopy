ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	СвязанныеРегистраторы.ДокументОснование,
	СвязанныеРегистраторы.ПодчиненныйДокумент
ПОМЕСТИТЬ
	СвязанныеРегистраторы
ИЗ
(
ВЫБРАТЬ
	ВЫБОР КОГДА Инвентаризация.ДокументОснование ССЫЛКА Документ.ИнвентаризацияОрдерногоСклада ТОГДА
		ВЫРАЗИТЬ(Инвентаризация.ДокументОснование КАК Документ.ИнвентаризацияОрдерногоСклада)
	ИНАЧЕ
		ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
	КОНЕЦ КАК ДокументОснование,
	Инвентаризация.Ссылка КАК ПодчиненныйДокумент
ИЗ
	Документ.Инвентаризация КАК Инвентаризация
ГДЕ
	Инвентаризация.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	(ВЫБОР КОГДА Инвентаризация.ДокументОснование ССЫЛКА Документ.ИнвентаризацияОрдерногоСклада ТОГДА
		ВЫРАЗИТЬ(Инвентаризация.ДокументОснование КАК Документ.ИнвентаризацияОрдерногоСклада)
	ИНАЧЕ
		ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
	КОНЕЦ).* КАК ДокументОснование,
	Инвентаризация.Ссылка.* КАК ПодчиненныйДокумент
}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ 
	ВЫБОР 
		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер И (НЕ ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)=ЗНАЧЕНИЕ(Документ.ПриходныйСкладскойОрдер.ПустаяСсылка)) ТОГДА
			ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)
		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер  И (НЕ ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)=ЗНАЧЕНИЕ(Документ.РасходныйСкладскойОрдер.ПустаяСсылка)) ТОГДА
			ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ,
	ПеремещениеТоваров.Ссылка
ИЗ
	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
ГДЕ
	ПеремещениеТоваров.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	(ВЫБОР 
		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер ТОГДА
			ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)
		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер ТОГДА
			ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ).* КАК ДокументОснование,
	ПеремещениеТоваров.Ссылка.* КАК ПодчиненныйДокумент
}
    
ОБЪЕДИНИТЬ ВСЕ
	
ВЫБРАТЬ 
	ВЫБОР
		КОГДА ВводОстатковТоваров.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер И (НЕ ВЫРАЗИТЬ(ВводОстатковТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)=ЗНАЧЕНИЕ(Документ.ПриходныйСкладскойОрдер.ПустаяСсылка)) ТОГДА
			ВЫРАЗИТЬ(ВводОстатковТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ,
	ВводОстатковТоваров.Ссылка
ИЗ
	Документ.ВводОстатковТоваров КАК ВводОстатковТоваров
ГДЕ
	ВводОстатковТоваров.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	(ВЫБОР
		КОГДА ВводОстатковТоваров.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер ТОГДА
			ВЫРАЗИТЬ(ВводОстатковТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ).* КАК ДокументОснование,
	ВводОстатковТоваров.Ссылка.* КАК ПодчиненныйДокумент
}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ 
	ВЫБОР
		КОГДА ПоступлениеТоваров.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер И (НЕ ВЫРАЗИТЬ(ПоступлениеТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)=ЗНАЧЕНИЕ(Документ.ПриходныйСкладскойОрдер.ПустаяСсылка)) ТОГДА
			ВЫРАЗИТЬ(ПоступлениеТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ,
	ПоступлениеТоваров.Ссылка
ИЗ
	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
ГДЕ
	ПоступлениеТоваров.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	(ВЫБОР
		КОГДА ПоступлениеТоваров.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер ТОГДА
			ВЫРАЗИТЬ(ПоступлениеТоваров.ДокументОснование КАК Документ.ПриходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ).* КАК ДокументОснование,
	ПоступлениеТоваров.Ссылка.* КАК ПодчиненныйДокумент
}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВЫБОР
		КОГДА СписаниеТоваров.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер И (НЕ ВЫРАЗИТЬ(СписаниеТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)=ЗНАЧЕНИЕ(Документ.РасходныйСкладскойОрдер.ПустаяСсылка)) ТОГДА
			ВЫРАЗИТЬ(СписаниеТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ,
	СписаниеТоваров.Ссылка
ИЗ
	Документ.СписаниеТоваров КАК СписаниеТоваров
ГДЕ
	СписаниеТоваров.Дата МЕЖДУ &ДатаНач И &ДатаКон
		
{ГДЕ
	(ВЫБОР
		КОГДА СписаниеТоваров.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер ТОГДА
			ВЫРАЗИТЬ(СписаниеТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ).* КАК ДокументОснование,
	СписаниеТоваров.Ссылка.* КАК ПодчиненныйДокумент
}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВЫБОР
		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер И (НЕ ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)=ЗНАЧЕНИЕ(Документ.РасходныйСкладскойОрдер.ПустаяСсылка)) ТОГДА
			ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ,
	РеализацияТоваров.Ссылка
ИЗ
	Документ.РеализацияТоваров КАК РеализацияТоваров
ГДЕ
	РеализацияТоваров.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	(ВЫБОР
		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер ТОГДА
			ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.РасходныйСкладскойОрдер)
		ИНАЧЕ
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		КОНЕЦ).* КАК ДокументОснование,
	РеализацияТоваров.Ссылка.* КАК ПодчиненныйДокумент
}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ИнвентаризацияОрдерногоСклада.ДокументОснование,
	ИнвентаризацияОрдерногоСклада.Ссылка
ИЗ
	Документ.ИнвентаризацияОрдерногоСклада КАК ИнвентаризацияОрдерногоСклада
ГДЕ
	ИнвентаризацияОрдерногоСклада.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	ИнвентаризацияОрдерногоСклада.Ссылка.* КАК ПодчиненныйДокумент,
	ИнвентаризацияОрдерногоСклада.ДокументОснование.* КАК ДокументОснование
}
ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВЫБОР
		КОГДА ПриходныйСкладскойОрдер.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер ТОГДА
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		ИНАЧЕ
			ПриходныйСкладскойОрдер.ДокументОснование
		КОНЕЦ,
	ПриходныйСкладскойОрдер.Ссылка
ИЗ
	Документ.ПриходныйСкладскойОрдер КАК ПриходныйСкладскойОрдер
ГДЕ
	ПриходныйСкладскойОрдер.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	ПриходныйСкладскойОрдер.Ссылка.* КАК ПодчиненныйДокумент,
	(ВЫБОР
		КОГДА ПриходныйСкладскойОрдер.ДокументОснование ССЫЛКА Документ.РасходныйСкладскойОрдер ТОГДА
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		ИНАЧЕ
			ПриходныйСкладскойОрдер.ДокументОснование
		КОНЕЦ).* КАК ДокументОснование
}
	
ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВЫБОР
		КОГДА РасходныйСкладскойОрдер.ДокументОснование ССЫЛКА Документ.ПереразмещениеТоваров ИЛИ 
				РасходныйСкладскойОрдер.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер ТОГДА
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		ИНАЧЕ
			РасходныйСкладскойОрдер.ДокументОснование
		КОНЕЦ,
	РасходныйСкладскойОрдер.Ссылка
ИЗ
	Документ.РасходныйСкладскойОрдер КАК РасходныйСкладскойОрдер
ГДЕ
	РасходныйСкладскойОрдер.Дата МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	РасходныйСкладскойОрдер.Ссылка.* КАК ПодчиненныйДокумент,
	(ВЫБОР
		КОГДА РасходныйСкладскойОрдер.ДокументОснование ССЫЛКА Документ.ПереразмещениеТоваров ИЛИ 
				РасходныйСкладскойОрдер.ДокументОснование ССЫЛКА Документ.ПриходныйСкладскойОрдер ТОГДА
			ЗНАЧЕНИЕ(Документ.ИнвентаризацияОрдерногоСклада.ПустаяСсылка)
		ИНАЧЕ
			РасходныйСкладскойОрдер.ДокументОснование
		КОНЕЦ).* КАК ДокументОснование
}) КАК СвязанныеРегистраторы

;
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ОбъединеннаяТаблица.ДокументОснование КАК ДокументОснование,
	ОбъединеннаяТаблица.ПодчиненныйДокумент КАК ПодчиненныйДокумент,
	ОбъединеннаяТаблица.Номенклатура КАК Номенклатура,
	ОбъединеннаяТаблица.СкладКомпании КАК СкладКомпании,
	ОбъединеннаяТаблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	ОбъединеннаяТаблица.КоличествоОрдерный КАК КоличествоОрдерный,
	ОбъединеннаяТаблица.КоличествоСкладской КАК КоличествоСкладской
ПОМЕСТИТЬ
	ДвиженияПодчинений
ИЗ
(
	
ВЫБРАТЬ
	СвязанныеРегистраторы.ДокументОснование КАК ДокументОснование,
	СвязанныеРегистраторы.ПодчиненныйДокумент КАК ПодчиненныйДокумент,
	ОстаткиТоваровОрдерныйСклад.Номенклатура КАК Номенклатура,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании КАК СкладКомпании,
	ОстаткиТоваровОрдерныйСклад.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	СУММА(ВЫБОР
		КОГДА ОстаткиТоваровОрдерныйСклад.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			ОстаткиТоваровОрдерныйСклад.Количество
		ИНАЧЕ
			-ОстаткиТоваровОрдерныйСклад.Количество
	КОНЕЦ) КАК КоличествоОрдерный,
	0 КАК КоличествоСкладской

	{ВЫБРАТЬ
	Номенклатура,
	ХарактеристикаНоменклатуры}
ИЗ
	СвязанныеРегистраторы КАК СвязанныеРегистраторы
ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровОрдерныйСклад КАК ОстаткиТоваровОрдерныйСклад
	ПО
		СвязанныеРегистраторы.ПодчиненныйДокумент = ОстаткиТоваровОрдерныйСклад.Регистратор
		// ОтборОстаткиТоваровОрдерныйСклад
{ГДЕ
	ОстаткиТоваровОрдерныйСклад.Номенклатура.* КАК Номенклатура,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании.* КАК СкладКомпании,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании.Организация.* КАК Организация,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании.Подразделение.* КАК Подразделение,
	ОстаткиТоваровОрдерныйСклад.ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры
}
СГРУППИРОВАТЬ ПО
	СвязанныеРегистраторы.ДокументОснование,
	СвязанныеРегистраторы.ПодчиненныйДокумент,
	ОстаткиТоваровОрдерныйСклад.Номенклатура,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании,
	ОстаткиТоваровОрдерныйСклад.ХарактеристикаНоменклатуры
	
ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	СвязанныеРегистраторы.ДокументОснование,
	СвязанныеРегистраторы.ПодчиненныйДокумент,
	ОстаткиТоваровКомпании.Номенклатура,
	ОстаткиТоваровКомпании.СкладКомпании,
	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры,
	0,
	СУММА(ВЫБОР
		КОГДА ОстаткиТоваровКомпании.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			ОстаткиТоваровКомпании.Количество
		ИНАЧЕ
			-ОстаткиТоваровКомпании.Количество
	КОНЕЦ)

	{ВЫБРАТЬ
	Номенклатура,
	ХарактеристикаНоменклатуры}
ИЗ
	СвязанныеРегистраторы КАК СвязанныеРегистраторы
ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
	ПО
		СвязанныеРегистраторы.ПодчиненныйДокумент = ОстаткиТоваровКомпании.Регистратор
		// ОтборОстаткиТоваровКомпании
{ГДЕ
	ОстаткиТоваровКомпании.Номенклатура.* КАК Номенклатура,
	ОстаткиТоваровКомпании.СкладКомпании.* КАК СкладКомпании,
	ОстаткиТоваровКомпании.СкладКомпании.Организация.* КАК Организация,
	ОстаткиТоваровКомпании.СкладКомпании.Подразделение.* КАК Подразделение,
	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры
}
СГРУППИРОВАТЬ ПО
	СвязанныеРегистраторы.ДокументОснование,
	СвязанныеРегистраторы.ПодчиненныйДокумент,
	ОстаткиТоваровКомпании.Номенклатура,
	ОстаткиТоваровКомпании.СкладКомпании,
	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры) КАК ОбъединеннаяТаблица
ИНДЕКСИРОВАТЬ ПО
	Номенклатура,
	СкладКомпании,
	ХарактеристикаНоменклатуры
;
УНИЧТОЖИТЬ СвязанныеРегистраторы
;

ВЫБРАТЬ
	ДвиженияПодчинений.ДокументОснование КАК ДокументОснование,
	ДвиженияПодчинений.Номенклатура КАК Номенклатура,
	ДвиженияПодчинений.СкладКомпании КАК СкладКомпании,
	ДвиженияПодчинений.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	КОЛИЧЕСТВО(ДвиженияПодчинений.ПодчиненныйДокумент) КАК Счетчик,
	СУММА(ДвиженияПодчинений.КоличествоОрдерный) КАК КоличествоОрдерный,
	СУММА(ДвиженияПодчинений.КоличествоСкладской) КАК КоличествоСкладской
	
ПОМЕСТИТЬ СчетчикиПодчиненныхДокументов
ИЗ 
	ДвиженияПодчинений КАК ДвиженияПодчинений
СГРУППИРОВАТЬ ПО
	ДвиженияПодчинений.ДокументОснование,
	ДвиженияПодчинений.Номенклатура,
	ДвиженияПодчинений.СкладКомпании,
	ДвиженияПодчинений.ХарактеристикаНоменклатуры
ИНДЕКСИРОВАТЬ ПО
	Номенклатура,
	СкладКомпании,
	ХарактеристикаНоменклатуры
;

ВЫБРАТЬ РАЗЛИЧНЫЕ
	СчетчикиПодчиненныхДокументов.ДокументОснование КАК ДокументОснование
ПОМЕСТИТЬ ТаблицаОснований
ИЗ
	СчетчикиПодчиненныхДокументов КАК СчетчикиПодчиненныхДокументов
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ОбъединеннаяТаблица.ДокументОснование КАК ДокументОснование,
	ОбъединеннаяТаблица.Номенклатура КАК Номенклатура,
	ОбъединеннаяТаблица.СкладКомпании КАК СкладКомпании,
	ОбъединеннаяТаблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	ОбъединеннаяТаблица.КоличествоОрдерный КАК КоличествоОрдерный,
	ОбъединеннаяТаблица.КоличествоСкладской КАК КоличествоСкладской
ПОМЕСТИТЬ
	ДвиженияОснований
ИЗ
(ВЫБРАТЬ
	ТаблицаОснований.ДокументОснование КАК ДокументОснование,
	ОстаткиТоваровОрдерныйСклад.Номенклатура КАК Номенклатура,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании КАК СкладКомпании,
	ОстаткиТоваровОрдерныйСклад.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	СУММА(ВЫБОР
		КОГДА ОстаткиТоваровОрдерныйСклад.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			ОстаткиТоваровОрдерныйСклад.Количество
		ИНАЧЕ
			-ОстаткиТоваровОрдерныйСклад.Количество
	КОНЕЦ) КАК КоличествоОрдерный,
	0 КАК КоличествоСкладской

	{ВЫБРАТЬ
	Номенклатура,
	ХарактеристикаНоменклатуры}
ИЗ ТаблицаОснований КАК ТаблицаОснований
ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровОрдерныйСклад КАК ОстаткиТоваровОрдерныйСклад
	ПО
		ТаблицаОснований.ДокументОснование = ОстаткиТоваровОрдерныйСклад.Регистратор
		// ОтборОстаткиТоваровОрдерныйСклад
{ГДЕ
	ОстаткиТоваровОрдерныйСклад.Номенклатура.* КАК Номенклатура,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании.* КАК СкладКомпании,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании.Организация.* КАК Организация,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании.Подразделение.* КАК Подразделение,
	ОстаткиТоваровОрдерныйСклад.ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры
}
СГРУППИРОВАТЬ ПО
	ТаблицаОснований.ДокументОснование,
	ОстаткиТоваровОрдерныйСклад.Номенклатура,
	ОстаткиТоваровОрдерныйСклад.СкладКомпании,
	ОстаткиТоваровОрдерныйСклад.ХарактеристикаНоменклатуры
	
ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ТаблицаОснований.ДокументОснование,
	ОстаткиТоваровКомпании.Номенклатура,
	ОстаткиТоваровКомпании.СкладКомпании,
	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры,
	0,
	СУММА(ВЫБОР
		КОГДА ОстаткиТоваровКомпании.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			ОстаткиТоваровКомпании.Количество
		ИНАЧЕ
			-ОстаткиТоваровКомпании.Количество
	КОНЕЦ)

	{ВЫБРАТЬ
	Номенклатура,
	ХарактеристикаНоменклатуры}
ИЗ
	ТаблицаОснований КАК ТаблицаОснований
ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
	ПО
		ТаблицаОснований.ДокументОснование = ОстаткиТоваровКомпании.Регистратор
		// ОтборОстаткиТоваровКомпании
{ГДЕ
	ОстаткиТоваровКомпании.Номенклатура.* КАК Номенклатура,
	ОстаткиТоваровКомпании.СкладКомпании.* КАК СкладКомпании,
	ОстаткиТоваровКомпании.СкладКомпании.Организация.* КАК Организация,
	ОстаткиТоваровКомпании.СкладКомпании.Подразделение.* КАК Подразделение,
	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры
}
СГРУППИРОВАТЬ ПО
	ТаблицаОснований.ДокументОснование,
	ОстаткиТоваровКомпании.Номенклатура,
	ОстаткиТоваровКомпании.СкладКомпании,
	ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры) КАК ОбъединеннаяТаблица
ИНДЕКСИРОВАТЬ ПО
	Номенклатура,
	СкладКомпании,
	ХарактеристикаНоменклатуры
;	
	
УНИЧТОЖИТЬ ТаблицаОснований	
	
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ЕСТЬNULL(ДвиженияПодчинений.ДокументОснование, ДвиженияОснований.ДокументОснование) КАК ДокументОснование,
	ДвиженияПодчинений.ПодчиненныйДокумент КАК ПодчиненныйДокумент,
	ЕСТЬNULL(ДвиженияПодчинений.Номенклатура, ДвиженияОснований.Номенклатура) КАК Номенклатура,
	ЕСТЬNULL(ДвиженияПодчинений.СкладКомпании, ДвиженияОснований.СкладКомпании) КАК СкладКомпании,
	(ЕСТЬNULL(ДвиженияПодчинений.СкладКомпании, ДвиженияОснований.СкладКомпании)).Организация КАК Организация,
	(ЕСТЬNULL(ДвиженияПодчинений.СкладКомпании, ДвиженияОснований.СкладКомпании)).Подразделение КАК Подразделение,
	ЕСТЬNULL(ДвиженияПодчинений.ХарактеристикаНоменклатуры, ДвиженияОснований.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатуры,
	ЕСТЬNULL(ДвиженияПодчинений.КоличествоОрдерный, 0) КАК КоличествоОрдерный,
	ЕСТЬNULL(ДвиженияПодчинений.КоличествоСкладской, 0) КАК КоличествоСкладской,
	ЕСТЬNULL(ДвиженияОснований.КоличествоОрдерный, 0)/ЕСТЬNULL(СчетчикиПодчиненныхДокументов.Счетчик, 1) КАК КоличествоОрдерныйОснование,
	ЕСТЬNULL(ДвиженияОснований.КоличествоСкладской, 0)/ЕСТЬNULL(СчетчикиПодчиненныхДокументов.Счетчик, 1) КАК КоличествоСкладскойОснование,
	0 КАК КоличествоРазница
		
{ВЫБРАТЬ
	ДокументОснование.* КАК ДокументОснование,
	ПодчиненныйДокумент.* КАК ПодчиненныйДокумент,
	Номенклатура.* КАК Номенклатура,
	СкладКомпании.* КАК СкладКомпании,
	Организация.* КАК Организация,
	Подразделение.* КАК Подразделение,
	ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	КоличествоОрдерный КАК КоличествоОрдерный,
	КоличествоСкладской КАК КоличествоСкладской
}
	
ИЗ
	ДвиженияПодчинений КАК ДвиженияПодчинений
ПОЛНОЕ СОЕДИНЕНИЕ ДвиженияОснований КАК ДвиженияОснований
  ПО
	ДвиженияПодчинений.ДокументОснование = ДвиженияОснований.ДокументОснование И
	ДвиженияПодчинений.Номенклатура = ДвиженияОснований.Номенклатура И
	ДвиженияПодчинений.СкладКомпании = ДвиженияОснований.СкладКомпании И
	ДвиженияПодчинений.ХарактеристикаНоменклатуры = ДвиженияОснований.ХарактеристикаНоменклатуры
ЛЕВОЕ СОЕДИНЕНИЕ СчетчикиПодчиненныхДокументов КАК СчетчикиПодчиненныхДокументов
  ПО
	ДвиженияОснований.ДокументОснование = СчетчикиПодчиненныхДокументов.ДокументОснование И
	ДвиженияОснований.Номенклатура = СчетчикиПодчиненныхДокументов.Номенклатура И
	ДвиженияОснований.СкладКомпании = СчетчикиПодчиненныхДокументов.СкладКомпании И
	ДвиженияОснований.ХарактеристикаНоменклатуры = СчетчикиПодчиненныхДокументов.ХарактеристикаНоменклатуры
ГДЕ
	&ВыводитьВсе ИЛИ
	(ВЫБОР КОГДА СчетчикиПодчиненныхДокументов.КоличествоОрдерный ЕСТЬ NULL ИЛИ ДвиженияОснований.КоличествоОрдерный  ЕСТЬ NULL ТОГДА
		ИСТИНА
	ИНАЧЕ
		(СчетчикиПодчиненныхДокументов.КоличествоОрдерный-СчетчикиПодчиненныхДокументов.КоличествоСкладской)<>(ДвиженияОснований.КоличествоСкладской-ДвиженияОснований.КоличествоОрдерный)
	КОНЕЦ)
{УПОРЯДОЧИТЬ ПО
	ДокументОснование.* КАК ДокументОснование,
	ПодчиненныйДокумент.* КАК ПодчиненныйДокумент,
	Номенклатура.* КАК Номенклатура,
	СкладКомпании.* КАК СкладКомпании,
	Организация.* КАК Организация,
	Подразделение.* КАК Подразделение,
	ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры
}

{ИТОГИ ПО
	ДокументОснование.* КАК ДокументОснование,
	ПодчиненныйДокумент.* КАК ПодчиненныйДокумент,
	Номенклатура.* КАК Номенклатура,
	СкладКомпании.* КАК СкладКомпании,
	Организация.* КАК Организация,
	Подразделение.* КАК Подразделение,
	ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры
}

ИТОГИ 
	ВЫБОР КОГДА ПодчиненныйДокумент ЕСТЬ NULL ИЛИ ДокументОснование ЕСТЬ NULL ТОГДА
		 СУММА(КоличествоОрдерный)+СУММА(КоличествоОрдерныйОснование)
	ИНАЧЕ
		СУММА(КоличествоОрдерный)
	КОНЕЦ КАК КоличествоОрдерный,
	
	ВЫБОР КОГДА ПодчиненныйДокумент ЕСТЬ NULL ИЛИ ДокументОснование ЕСТЬ NULL ТОГДА
		 СУММА(КоличествоСкладской)+СУММА(КоличествоСкладскойОснование)
	ИНАЧЕ
		СУММА(КоличествоСкладской)
	КОНЕЦ КАК КоличествоСкладской,
	
	ВЫБОР КОГДА ПодчиненныйДокумент ЕСТЬ NULL ИЛИ ДокументОснование ЕСТЬ NULL ТОГДА
		 (СУММА(КоличествоОрдерный)+СУММА(КоличествоОрдерныйОснование))-(СУММА(КоличествоСкладской)+СУММА(КоличествоСкладскойОснование))
	ИНАЧЕ
		0
	КОНЕЦ КАК КоличествоРазница
ПО
	ДокументОснование,
	ПодчиненныйДокумент,
	Номенклатура,
	СкладКомпании,
	ХарактеристикаНоменклатуры

#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем ПоследовательностиОтчета Экспорт;                // хранит последовательности отчета
Перем ШаблонИсточникаДанных Экспорт;                   // хранит шаблон источника данных
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;   // хранит структуру итоговых выражений показателей

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


// формирует таблицу заголовка отчета
//
// Параметры:
// 	ИсходныйШрифт	  - Шрифт			  - Шрифт
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица заголовка
//
Функция ПолучитьТаблицуЗаголовкаОтчета(ИзмеренияОтчета, ИсходныйШрифт)
	
	ТаблицаЗаголовка = Новый ТаблицаЗначений;
	ТаблицаЗаголовка.Колонки.Добавить("ИмяСтроки");
	ТаблицаЗаголовка.Колонки.Добавить("Текст");
	ТаблицаЗаголовка.Колонки.Добавить("ЦветТекста");
	ТаблицаЗаголовка.Колонки.Добавить("Шрифт");
	ТаблицаЗаголовка.Колонки.Добавить("Формат");
	
	СтруктураЗаголовка = ПолучитьСтруктуруЗаголовкаОтчета(ИзмеренияОтчета);
	
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "ИмяОтчета";
	НоваяСтрока.Текст = СтруктураЗаголовка.Наименование + Символы.ПС + СтруктураЗаголовка.Период;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 12, Истина);
		
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "Отбор";
	НоваяСтрока.Текст = "Отбор: " + СтруктураЗаголовка.Отбор;
	НоваяСтрока.ЦветТекста = ЦветаСтиля.ЦветФонаВыделенияПоля;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10, Истина);
		
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "Показатели";
	НоваяСтрока.Текст = "Показатели: " + СтруктураЗаголовка.Показатели;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10);
	
	НоваяСтрока = ТаблицаЗаголовка.Добавить();
	НоваяСтрока.ИмяСтроки = "Итоги";
	НоваяСтрока.Текст = "Итоги по: " + СтруктураЗаголовка.Измерения;
	НоваяСтрока.Шрифт = Новый Шрифт(ИсходныйШрифт,, 10);
	
	Возврат ТаблицаЗаголовка;
		
КонецФункции // ПолучитьТаблицуЗаголовкаОтчета()

// Функция возвращает структуру параметров заголовка отчета
//
// Параметры:
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//
// Возвращаемое значение:
//	Структура - Структура заголовка отчета
//
Функция ПолучитьСтруктуруЗаголовкаОтчета(ИзмеренияОтчета)
	
	// Получим наименование отчета из формы настройки
	Попытка	Наименование =	ПолучитьФорму(ИмяФормыНастроек).НаименованиеОтчета;
	Исключение Наименование = ""; КонецПопытки;
	
	// Описание периода
	Если ДатаКонца = '00010101000000' Тогда
		Период = "на " + Формат(ТекущаяДата(), "ДФ = ""дд.ММ.гггг ЧЧ:мм:сс""; ДП = ""...""");
	Иначе Период = "на конец дня " + Формат(ДатаКонца, "ДФ = ""дд.ММ.гггг""; ДП = ""...""");
	КонецЕсли;
	
	// Текст измерений строк
	Измерения = "";
	Для Каждого ТекИзмерение Из ИзмеренияОтчета Цикл
		Измерения = Измерения + ?(Измерения = "", "", ", ") + ТекИзмерение;
	КонецЦикла; 
	
	// Текст отборов
	Если ПостроительОтчета.Отбор.Количество() = 0 Тогда
		Отбор = "Без отбора";
	Иначе
		Отбор = "";
		Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
			Если ТекОтбор.Использование И НЕ ТекОтбор.Имя = "Периодичность" Тогда
				Если Найти(ТекОтбор.ВидСравнения,"Интервал")>0 Тогда
					Отбор = Отбор + ?(Отбор = "", "", ", ") + ТекОтбор.Представление + " " + ТекОтбор.ВидСравнения + " " + ТекОтбор.ЗначениеС+" - "+ТекОтбор.ЗначениеПо;
				Иначе
					Отбор = Отбор + ?(Отбор = "", "", ", ") + ТекОтбор.Представление + " " + ТекОтбор.ВидСравнения + " " + ТекОтбор.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если Отбор = "" Тогда Отбор = "Все"; КонецЕсли; 
	КонецЕсли; 
	
	ТекстПоказатели = "";
	Для Каждого ТекПоказатель Из Показатели Цикл
		Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
		ТекстПоказатели = ТекстПоказатели + ?(ТекстПоказатели = "", "", ", ") + ТекПоказатель.Представление;
	КонецЦикла;
	
	Возврат Новый Структура("Наименование,Период,Отбор,Измерения,Показатели",
	Наименование,Период,Отбор,Измерения,ТекстПоказатели);
	
КонецФункции	//	ПолучитьСтруктуруЗаголовкаОтчета()

// выгрузить дерево документов
Процедура ВыгрузитьДеревоДокументов(Выборка, ДеревоРегистраторов, Уровень=1)
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипЗаписи()=ТипЗаписиЗапроса.ДетальнаяЗапись Тогда Возврат; КонецЕсли;
		ИмяПоля = "ДокументДвиженияУровень"+Уровень;
		ТекДок = Выборка[ИмяПоля];
		Если ТекДок=Null Тогда Возврат; КонецЕсли;
		НоваяСтрока = ДеревоРегистраторов.Строки.Добавить();
		НоваяСтрока.Документ = ТекДок;
		НоваяСтрока.ДокументПредставление = Выборка[ИмяПоля+"Представление"];
		ВыгрузитьДеревоДокументов(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам), НоваяСтрока, Уровень+1)
	КонецЦикла;
	
КонецПроцедуры

// вывести иерархию документов
Процедура ВывестиИерархиюДокументов(ЦепочкаДокументов, СтруктураМакетов, ДокументРезультат, Уровень=1)
	
	Если ЦепочкаДокументов.Количество()=0 Тогда Возврат; КонецЕсли;
	ИмяМакета="ДокументДвиженияУровень"+Уровень;
	ТекМакет = СтруктураМакетов[ИмяМакета];
	Для Каждого ТекДокумент Из ЦепочкаДокументов Цикл
		ТекМакет.Параметры[ИмяМакета] = ТекДокумент.Документ;
		ТекМакет.Параметры[ИмяМакета+"Представление"] = ТекДокумент.ДокументПредставление;
		ДокументРезультат.Вывести(ТекМакет, Уровень+2, "Документ уровень"+(Уровень+1));
		ВывестиИерархиюДокументов(ТекДокумент.Строки, СтруктураМакетов, ДокументРезультат, Уровень+1);
	КонецЦикла;
	
КонецПроцедуры

// Процедура формирует строку измерений макета построителя отчета
//
// Параметры:
//	МакетОтчета        - ТабличныйДокумент          - Макет отчета
//	ТекИзмерение       - ИзмерениеПостроителяОтчета - Текущее измерение
//	СчетчикУровня      - Число						- Счетчик уровня
//	ПараметрыИзмерения - Структура					- Параметры измерения
//	ГраницаМакета      - Структура					- Границы макета
//
Процедура СформироватьМакетИзмеренияСтрокиВторойТаблицы(МакетОтчета, ИмяТекИзмерения, СчетчикУровня, ПараметрыИзмерения, ГраницаМакета, НомерСтроки)
	
	РесурсыОтчета				= ПараметрыИзмерения.РесурсыОтчета;
	ОбластьОформлениеИзмерений	= ПараметрыИзмерения.ОбластьОформлениеИзмерений;
	НачальноеОформление			= ПараметрыИзмерения.НачальноеОформление;
	НачальноеОформление2		= ПараметрыИзмерения.НачальноеОформление2;
	
	СтруктураФорматаПолей = СтруктураФорматированияПолей();
	НачальныйНомерВыводаКолонок = ?(ИспользоватьАвтоотступ, 3, 7);
	
	ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ИмяТекИзмерения, ПараметрыИспользованияПолейИПоказателей);
	Если НЕ ИспользоватьАвтоотступ Тогда
		Для к = 1 По СчетчикУровня-1 Цикл
			ИндексОформления = ?(к=3, НачальноеОформление2, НачальноеОформление+к);
			ОформлениеУровня = ОбластьОформлениеИзмерений.Область(ИндексОформления, 1);
			
			ОбластьПредУровня = МакетОтчета.Область(НомерСтроки, к+1);
			ОбластьПредУровня.Шрифт			= ОформлениеУровня.Шрифт;
			ОбластьПредУровня.ЦветТекста	= ОформлениеУровня.ЦветТекста;
			ОбластьПредУровня.ЦветФона		= ОформлениеУровня.ЦветФона;
			ОбластьПредУровня.ГраницаСлева	= ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
			ОбластьПредУровня.ШиринаКолонки = 2;
		КонецЦикла;
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
	Иначе
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
	КонецЕсли;
	
	ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	ИмяПараметра = ИмяТекИзмерения+"Представление";
	
	ДопПоляОтчета = ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные, Уровень", Ложь, 2));
	Если ДопПоляОтчета.Количество() > 0 И НЕ ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
		ТекстШаблона = "";
		Для ИндПоля=0 По ДопПоляОтчета.Количество()-1 Цикл
			ТекПоле = ДопПоляОтчета[ИндПоля];
			Если ИмяТекИзмерения=ТекПоле.Родитель Тогда
				ТекстШаблона = ТекстШаблона + ", [" + ТекПоле.Имя + "]";
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ обЗначениеНеЗаполнено(ТекстШаблона) Тогда
			ТипЗаполненияОбластиИзмерения =	ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
			ИмяПараметра = "[" + ИмяПараметра + "]"+ТекстШаблона;
		КонецЕсли;
	КонецЕсли;
	
	
	ОформлениеУровня = ОбластьОформлениеИзмерений.Область((НачальноеОформление2 + СчетчикУровня-3), 1);
	
	ОбластьКолонки.Объединить();
	ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьКолонки.Заполнение    = ТипЗаполненияОбластиИзмерения;
	ОбластьКолонки.Параметр      = ИмяПараметра;
	ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
	ОбластьКолонки.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
	ОбластьКолонки.Шрифт         = ОформлениеУровня.Шрифт;
	ОбластьКолонки.ЦветТекста    = ОформлениеУровня.ЦветТекста;
	ОбластьКолонки.ЦветФона      = ОформлениеУровня.ЦветФона;
	ОбластьКолонки.ГраницаСверху = ГраницаМакета.ИзмерениеСтрока.ГраницаСверху;
	ОбластьКолонки.ГраницаСлева  = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
	Если ИспользоватьАвтоотступ Тогда
		ОбластьКолонки.Автоотступ = 1;
	КонецЕсли;
	ОбластьКолонки.ПараметрРасшифровки = ИмяТекИзмерения;
	
	Если ДопПоляОтчета.Количество() > 0 И ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
		// Теперь добавим выбранные дополнительные поля
		Для ИндПоля=0 По ДопПоляОтчета.Количество()-1 Цикл
			ТекПоле = ДопПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			Попытка
				ЗаполнятьПараметр = ИзмеренияСтроки.Индекс(ИзмеренияСтроки.Найти(ИмяТекИзмерения, "ПутьКДанным"))>=ТекПоле.НомерСтроки;
			Исключение
				ЗаполнятьПараметр = Ложь;
			КонецПопытки;
			Если ЗаполнятьПараметр Тогда
				ОбластьДопПоле.Заполнение          = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьДопПоле.Параметр            = ТекПоле.Имя;
				ОбластьДопПоле.ПараметрРасшифровки = ТекПоле.Имя;
				ОбластьДопПоле.Формат              = ТекПоле.Формат;
			Иначе
				ОбластьДопПоле.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьДопПоле.Текст      = "";
			КонецЕсли;
			ОбластьДопПоле.ГраницаСверху = ГраницаМакета.ДопПоляСтрока.ГраницаСверху;
			ОбластьДопПоле.ГраницаСлева  = ГраницаМакета.ДопПоляСтрока.ГраницаСлева;
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		
		Если НЕ ИспользоватьАвтоотступ Тогда
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		Иначе ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		КонецЕсли;
		ОбластьДопПоле.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.Шрифт         = ОформлениеУровня.Шрифт;
		ОбластьДопПоле.ЦветТекста    = ОформлениеУровня.ЦветТекста;
		ОбластьДопПоле.ЦветФона      = ОформлениеУровня.ЦветФона;
	КонецЕсли;
	
	НомерКолонки = НачальныйНомерВыводаКолонок;
	ОбластьПостроителя = МакетОтчета.Область("R" + НомерСтроки);
	ОбластьПостроителя.Имя = ИмяТекИзмерения;
	СчетчикФункций=0;
	ОбластьКолонки = Неопределено;
	НомерПервойКолонки = НомерКолонки;
	Для Каждого ТекРесурс Из РесурсыОтчета Цикл
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
		ОбластьКолонки.РазмещениеТекста		 	= ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьКолонки.Заполнение				= ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ОбластьКолонки.Параметр					= ТекРесурс;
		ОбластьКолонки.Формат					= СтруктураФорматаПолей[ТекРесурс];
		ОбластьКолонки.ГоризонтальноеПоложение	= ГоризонтальноеПоложение.Право;
		ОбластьКолонки.ВертикальноеПоложение	= ВертикальноеПоложение.Центр;
		ОбластьКолонки.ГраницаСлева  = ?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСлева,ГраницаМакета.ФункцииСтрока.ГраницаСлева);
		ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиСтрока.ГраницаСверху;
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ФункцииСтрока.ГраницаСправа;
		НомерКолонки = НомерКолонки + 1;
		СчетчикФункций=СчетчикФункций+1;
	КонецЦикла;
	Если ОбластьКолонки<>Неопределено Тогда
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСправа;
	КонецЕсли;
	
	ОбластьСтроки = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок+РесурсыОтчета.Количество()-1);
	ОбластьСтроки.Шрифт         = ОформлениеУровня.Шрифт;
	ОбластьСтроки.ЦветТекста    = ОформлениеУровня.ЦветТекста;
	ОбластьСтроки.ЦветФона      = ОформлениеУровня.ЦветФона;
	
КонецПроцедуры	//	СформироватьМакетИзмеренияСтрокиВторойТаблицы()

// Процедура формирует строку измерений макета построителя отчета
//
// Параметры:
//	МакетОтчета        - ТабличныйДокумент          - Макет отчета
//	ТекИзмерение       - ИзмерениеПостроителяОтчета - Текущее измерение
//	СчетчикУровня      - Число						- Счетчик уровня
//	ПараметрыИзмерения - Структура					- Параметры измерения
//	ГраницаМакета      - Структура					- Границы макета
//
Процедура СформироватьМакетИзмеренияСтрокиПервойТаблицы(МакетОтчета, ИмяТекИзмерения, СчетчикУровня, ПараметрыИзмерения, ГраницаМакета)
	
	КоличествоУровней          = ПараметрыИзмерения.КоличествоУровней;
	ОбластьОформлениеИзмерений = ПараметрыИзмерения.ОбластьОформлениеИзмерений;
	НачальноеОформление        = ПараметрыИзмерения.НачальноеОформление;
	СтруктураФорматаПолей = СтруктураФорматированияПолей();
	НомерСтроки = МакетОтчета.ВысотаТаблицы + 1;
	НачальныйНомерВыводаКолонок = ?(ИспользоватьАвтоотступ, 3, КоличествоУровней + 2);
	
	ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ИмяТекИзмерения, ПараметрыИспользованияПолейИПоказателей);
	Если НЕ ИспользоватьАвтоотступ Тогда
		Для к = 1 По СчетчикУровня - 1 Цикл
			ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление + к, 1);
			ОбластьПредУровня = МакетОтчета.Область(НомерСтроки, к + 1);
			ОбластьПредУровня.Шрифт        = ОформлениеУровня.Шрифт;
			ОбластьПредУровня.ЦветТекста   = ОформлениеУровня.ЦветТекста;
			ОбластьПредУровня.ЦветФона     = ОформлениеУровня.ЦветФона;
			ОбластьПредУровня.ГраницаСлева = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
			ОбластьПредУровня.ШиринаКолонки = 2;
		КонецЦикла;
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
	Иначе
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
	КонецЕсли;	
	
	ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление + СчетчикУровня, 1);
	
	ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	ИмяПараметра = ИмяТекИзмерения+"Представление";
	
	ДопПоляОтчета = ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные, Уровень", Ложь, 1));
	Если ДопПоляОтчета.Количество() > 0 И НЕ ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
		ТекстШаблона = "";
		Для ИндПоля=0 По ДопПоляОтчета.Количество()-1 Цикл
			ТекПоле = ДопПоляОтчета[ИндПоля];
			Если ИмяТекИзмерения=ТекПоле.Родитель Тогда
				ТекстШаблона = ТекстШаблона + ", [" + ТекПоле.Имя + "]";
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ обЗначениеНеЗаполнено(ТекстШаблона) Тогда
			ТипЗаполненияОбластиИзмерения =	ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
			ИмяПараметра = "[" + ИмяПараметра + "]"+ТекстШаблона;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьКолонки.Объединить();
	ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьКолонки.Заполнение    = ТипЗаполненияОбластиИзмерения;
	ОбластьКолонки.Параметр      = ИмяПараметра;
	ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
	ОбластьКолонки.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
	ОбластьКолонки.Шрифт         = ОформлениеУровня.Шрифт;
	ОбластьКолонки.ЦветТекста    = ОформлениеУровня.ЦветТекста;
	ОбластьКолонки.ЦветФона      = ОформлениеУровня.ЦветФона;
	ОбластьКолонки.ГраницаСверху = ГраницаМакета.ИзмерениеСтрока.ГраницаСверху;
	ОбластьКолонки.ГраницаСлева  = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
	ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
	Если ИспользоватьАвтоотступ Тогда
		ОбластьКолонки.Автоотступ = 1;
	КонецЕсли;
	ОбластьКолонки.ПараметрРасшифровки = ИмяТекИзмерения;
	
	Если ДопПоляОтчета.Количество() > 0 И ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
		// Теперь добавим выбранные дополнительные поля
		Для ИндПоля=0 По ДопПоляОтчета.Количество()-1 Цикл
			ТекПоле = ДопПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			Попытка
				ЗаполнятьПараметр = ИзмеренияСтроки.Индекс(ИзмеренияСтроки.Найти(ИмяТекИзмерения, "ПутьКДанным"))>=ТекПоле.НомерСтроки;
			Исключение
				ЗаполнятьПараметр = Ложь;
			КонецПопытки;
			Если ЗаполнятьПараметр Тогда
				ОбластьДопПоле.Заполнение          = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьДопПоле.Параметр            = ТекПоле.Имя;
				ОбластьДопПоле.ПараметрРасшифровки = ТекПоле.Имя;
				ОбластьДопПоле.Формат              = ТекПоле.Формат;
			Иначе
				ОбластьДопПоле.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьДопПоле.Текст      = "";
			КонецЕсли;
			ОбластьДопПоле.ГраницаСверху = ГраницаМакета.ДопПоляСтрока.ГраницаСверху;
			ОбластьДопПоле.ГраницаСлева  = ГраницаМакета.ДопПоляСтрока.ГраницаСлева;
			ОбластьДопПоле.ГраницаСправа = ГраницаМакета.ДопПоляСтрока.ГраницаСлева;
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		
		Если НЕ ИспользоватьАвтоотступ Тогда
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		Иначе ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		КонецЕсли;
		ОбластьДопПоле.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.Шрифт         = ОформлениеУровня.Шрифт;
		ОбластьДопПоле.ЦветТекста    = ОформлениеУровня.ЦветТекста;
		ОбластьДопПоле.ЦветФона      = ОформлениеУровня.ЦветФона;
	КонецЕсли;
	
	ОбластьПостроителя = МакетОтчета.Область("R" + НомерСтроки);
	ОбластьПостроителя.Имя = ИмяТекИзмерения;
	
КонецПроцедуры	//	СформироватьМакетИзмеренияСтрокиПервойТаблицы()

// Функция возвращает оформленный табличный документ для использования в качестве макета построителя отчета
//
// Параметры:
//
// Возвращаемое значение:
//	ТабличныйДокумент - Макет отчета
//
Функция ПолучитьМакетПостроителя(Знач КоличествоУровней, ШиринаТаблицы)
	
	МакетОтчета = Новый ТабличныйДокумент();
	
	Попытка	   МакетОформления = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента);
	Исключение МакетОформления = ПолучитьОбщийМакет("МакетОформлениеОтчета");
	КонецПопытки;
	СтруктураФорматаПолей = СтруктураФорматированияПолей();
	ОбластьОформлениеГраниц = Неопределено;
	
	ОбластьОформлениеИзмерений = МакетОформления.ПолучитьОбласть("ОформлениеИзмерений");
	ОформлениеШапки            = МакетОформления.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеИтоги            = МакетОформления.ПолучитьОбласть("ОформлениеИтоги").Область(1, 1);
	Попытка 
		ОбластьОформлениеГраниц    = МакетОформления.ПолучитьОбласть("ОформлениеГраниц");
	Исключение
	КонецПопытки;
	
	ГраницаМакета = Новый Структура();
	
	Если ОбластьОформлениеГраниц<>Неопределено Тогда
		ГраницаМакета.Вставить("Измерения",ОбластьОформлениеГраниц.Области.Измерения);
		ГраницаМакета.Вставить("ИзмерениеСтрока",ОбластьОформлениеГраниц.Области.ИзмерениеСтрока);
		ГраницаМакета.Вставить("ИзмеренияИтог",ОбластьОформлениеГраниц.Области.ИзмеренияИтог);
		
		ГраницаМакета.Вставить("ДопПоля",ОбластьОформлениеГраниц.Области.ДопПоля);
		ГраницаМакета.Вставить("ДопПоляСтрока",ОбластьОформлениеГраниц.Области.ДопПоляСтрока);
		
		ГраницаМакета.Вставить("ИзмеренияПоГоризонтали",ОбластьОформлениеГраниц.Области.ИзмеренияПоГоризонтали);
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиСтрока",ОбластьОформлениеГраниц.Области.ИзмеренияПоГоризонталиСтрока);
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиИтог",ОбластьОформлениеГраниц.Области.ИзмеренияПоГоризонталиИтог);
		
		ГраницаМакета.Вставить("Функции",ОбластьОформлениеГраниц.Области.Функции);
		ГраницаМакета.Вставить("ФункцииСтрока",ОбластьОформлениеГраниц.Области.ФункцииСтрока);
		ГраницаМакета.Вставить("ФункцииИтог",ОбластьОформлениеГраниц.Области.ФункцииИтог);
		
		ГраницаМакета.Вставить("Показатели",ОбластьОформлениеГраниц.Области.Показатели);
		ГраницаМакета.Вставить("ПоказателиСтрока",ОбластьОформлениеГраниц.Области.ПоказателиСтрока);
		ГраницаМакета.Вставить("ПоказателиИтог",ОбластьОформлениеГраниц.Области.ПоказателиИтог);
		
		ЗначениеГраницОтчета  = ОбластьОформлениеГраниц.Области.ЗначениеГраницОтчета;
		ВключитьГраницуСправа = ЗначениеГраницОтчета.АвтоОтступ=1;
	Иначе
		ЛинияОбычная       = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,      1);
		
		ГраницаМакета.Вставить("Измерения",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмерениеСтрока",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмеренияИтог",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа, ГраницаСнизу", ЛинияОбычная, ЛинияОбычная, ЛинияОбычная, ЛинияОбычная));
		
		ГраницаМакета.Вставить("ДопПоля",Новый Структура("ГраницаСлева, ГраницаСверху", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ДопПоляСтрока",Новый Структура("ГраницаСлева, ГраницаСверху", ЛинияОбычная,ЛинияОбычная));
		
		ГраницаМакета.Вставить("ИзмеренияПоГоризонтали",Новый Структура("ГраницаСлева, ГраницаСверху", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиСтрока",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ИзмеренияПоГоризонталиИтог",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		
		ГраницаМакета.Вставить("Функции",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ФункцииСтрока",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ФункцииИтог",Новый Структура("ГраницаСлева, ГраницаСправа", ЛинияОбычная,ЛинияОбычная));
		
		ГраницаМакета.Вставить("Показатели",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ПоказателиСтрока",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		ГраницаМакета.Вставить("ПоказателиИтог",Новый Структура("ГраницаСлева, ГраницаСверху, ГраницаСправа, ГраницаСнизу", ЛинияОбычная,ЛинияОбычная,ЛинияОбычная,ЛинияОбычная));
		
		ЗначениеГраницОтчета  = Новый Структура("ГраницаСправа" ,ЛинияОбычная);
		ВключитьГраницуСправа = Истина;
	КонецЕсли;
	
	ПараметрыОформления = Новый Структура("ОбластьОформлениеИзмерений, ОформлениеШапки, ОформлениеИтоги, ГраницаМакета", ОбластьОформлениеИзмерений, ОформлениеШапки, ОформлениеИтоги, ГраницаМакета);
	
	ИзмеренияОтчета = Новый Массив;
	ИзмеренияОтчета.Добавить("Склад компании");
	ИзмеренияОтчета.Добавить("Документ движения");
	Если НЕ ИспользоватьАвтоотступ Тогда
		МинимальнаяШиринаПервогоУровня = 37.25-(КоличествоУровней*2);
	Иначе
		МинимальнаяШиринаПервогоУровня = 40;
	КонецЕсли;
	ЗаголовокШапки    = "";
	Для Каждого ТекИзмерение Из ИзмеренияОтчета Цикл
		ЗаголовокШапки = ЗаголовокШапки + ?(ЗаголовокШапки = "", "", " / ") + ТекИзмерение;
		КоличествоУровней = КоличествоУровней + 1;
	КонецЦикла;
	
	ПеремГраницаСправа = ?(ВключитьГраницуСправа, ЗначениеГраницОтчета.ГраницаСправа, ГраницаМакета.Функции.ГраницаСлева);
	
	// Приступим к формированию макета
	ТаблицаЗаголовка = ПолучитьТаблицуЗаголовкаОтчета(ИзмеренияОтчета, МакетОтчета.Область(1, 2).Шрифт);
	
	Для Инд=0 По ТаблицаЗаголовка.Количество()-1 Цикл
		ТекСтрокаЗаголовка = ТаблицаЗаголовка[Инд];
		ОбластьЗаголовок = МакетОтчета.Область(Инд+1, 2);
		Для Каждого ТекКолонка Из ТаблицаЗаголовка.Колонки Цикл
			Если ТекКолонка.Имя="ИмяСтроки" Тогда Продолжить; КонецЕсли;
			ТекПараметрЗаголовка = ТекСтрокаЗаголовка[ТекКолонка.Имя];
			Если обЗначениеНеЗаполнено(ТекПараметрЗаголовка) Тогда Продолжить; КонецЕсли;
			ОбластьЗаголовок[ТекКолонка.Имя] = ТекПараметрЗаголовка;
		КонецЦикла;
	КонецЦикла;
	
	ВысотаЗаголовка = МакетОтчета.ВысотаТаблицы;
	МакетОтчета.Область(1, 1).ШиринаКолонки = 1;
	МакетОтчета.Область("R1:R" + ВысотаЗаголовка).Имя = "ЗаголовокШаблон";
	
	РесурсыОтчета = Новый Массив();
	ДеревоРесурсов = Новый ДеревоЗначений;
	ДеревоРесурсов.Колонки.Добавить("ИмяРесурса");
	ДеревоРесурсов.Колонки.Добавить("Представление");
	ДеревоРесурсов.Колонки.Добавить("ФорматнаяСтрока");
	ДеревоРесурсов.Колонки.Добавить("ШиринаКолонки");
	ДеревоРесурсов.Колонки.Добавить("Одиночный");
	
	ШиринаПервогоУровня = МинимальнаяШиринаПервогоУровня;
	Для Каждого ТекФункция Из Функции Цикл
		Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
		НоваяГруппа = ДеревоРесурсов.Строки.Добавить();
		НоваяГруппа.ИмяРесурса = "";
		НоваяГруппа.Представление = ТекФункция.Представление;
		НоваяГруппа.ФорматнаяСтрока = "";
		НоваяГруппа.ШиринаКолонки = "";
		НоваяГруппа.Одиночный = Ложь;
		Для Каждого ТекПоказатель Из Показатели Цикл
			//Если ТекПоказатель.Имя="КоличествоРазница" Тогда Продолжить; КонецЕсли;
			Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
			ИмяРесурса = ТекПоказатель.Имя + ТекФункция.Имя;
			РесурсыОтчета.Добавить(ИмяРесурса);
			НоваяСтрока = НоваяГруппа.Строки.Добавить();
			НоваяСтрока.ИмяРесурса = ИмяРесурса;
			НоваяСтрока.Представление = ТекПоказатель.Представление;
			НоваяСтрока.ФорматнаяСтрока = ТекПоказатель.ФорматнаяСтрока;
			НоваяСтрока.ШиринаКолонки = ТекПоказатель.ШиринаКолонки;
			НоваяСтрока.Одиночный = Ложь;
			ШиринаПервогоУровня = ШиринаПервогоУровня+ТекПоказатель.ШиринаКолонки;
		КонецЦикла;
	КонецЦикла;
	
	Если ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
		Для Каждого ТекКолонка Из ДополнительныеПоляОтчета Цикл
			Если ТекКолонка.Уровень=1 Тогда
				ШиринаПервогоУровня = ШиринаПервогоУровня-ТекКолонка.ШиринаКолонки;
			Иначе
				ШиринаПервогоУровня = ШиринаПервогоУровня+ТекКолонка.ШиринаКолонки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ШиринаПервогоУровня = ШиринаПервогоУровня+0.25;
	ДопШиринаВторогоУровня = Макс(МинимальнаяШиринаПервогоУровня-ШиринаПервогоУровня, 0);
	ШиринаПервогоУровня = Макс(МинимальнаяШиринаПервогоУровня, ШиринаПервогоУровня);
	
	// Создадим отступы уровней измерений строк
	Если НЕ ИспользоватьАвтоотступ Тогда
		НачальныйНомерВыводаКолонок = 2;
		Для к = 1 По КоличествоУровней Цикл
			МакетОтчета.Область(ВысотаЗаголовка + 1, НачальныйНомерВыводаКолонок).ШиринаКолонки = ?(к = КоличествоУровней, ШиринаПервогоУровня, 2);
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
	Иначе
		НачальныйНомерВыводаКолонок = 3;
		МакетОтчета.Область(ВысотаЗаголовка + 1, 2).ШиринаКолонки = ШиринаПервогоУровня;
	КонецЕсли;
	
	ОбластьЗаголовокСтрок = МакетОтчета.Область(ВысотаЗаголовка + 1, 2, ВысотаЗаголовка + 2, НачальныйНомерВыводаКолонок - 1);
	ОбластьЗаголовокСтрок.Объединить();
	ОбластьЗаголовокСтрок.Текст                   = ЗаголовокШапки;
	ОбластьЗаголовокСтрок.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьЗаголовокСтрок.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ГраницаСлева            = ГраницаМакета.Измерения.ГраницаСлева;
	ОбластьЗаголовокСтрок.ГраницаСправа           = ГраницаМакета.Измерения.ГраницаСлева;
	ОбластьЗаголовокСтрок.ГраницаСверху           = ГраницаМакета.Измерения.ГраницаСверху;
	ОбластьЗаголовокСтрок.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
	
	Если ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
		Для Каждого ТекПоле Из ДополнительныеПоляОтчета Цикл
			Если ТекПоле.Уровень=2 Тогда Продолжить; КонецЕсли;
			ОбластьДопПоле = МакетОтчета.Область(ВысотаЗаголовка + 1, НачальныйНомерВыводаКолонок, ВысотаЗаголовка + 2, НачальныйНомерВыводаКолонок);
			ОбластьДопПоле.Объединить();
			ОбластьДопПоле.Текст                   = ТекПоле.ПолноеПредставление;
			ОбластьДопПоле.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьДопПоле.ШиринаКолонки           = ТекПоле.ШиринаКолонки;
			ОбластьДопПоле.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьДопПоле.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьДопПоле.ГраницаСлева            = ГраницаМакета.ДопПоля.ГраницаСлева;
			ОбластьДопПоле.ГраницаСверху           = ГраницаМакета.ДопПоля.ГраницаСверху;
			ОбластьДопПоле.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
			ОбластьДопПоле.ГраницаСправа		   = ГраницаМакета.Измерения.ГраницаСлева;
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
	КонецЕсли;
	
	ОбластьШапкаСтрок = МакетОтчета.Область("R" + (ВысотаЗаголовка + 1) + ":R" + (ВысотаЗаголовка + 2));
	ОбластьШапкаСтрок.Имя = "ШапкаТаблицы";
	
	ОбластьШапкиТаблицы = МакетОтчета.Область(ВысотаЗаголовка + 1, 2, ВысотаЗаголовка + 2, НачальныйНомерВыводаКолонок - 1);
	ОбластьШапкиТаблицы.Шрифт         = ОформлениеШапки.Шрифт;
	ОбластьШапкиТаблицы.ЦветТекста    = ОформлениеШапки.ЦветТекста;
	ОбластьШапкиТаблицы.ЦветФона      = ОформлениеШапки.ЦветФона;
	
	//ШиринаТаблицы = МакетОтчета.ШиринаТаблицы;
	
	НачальноеОформление = Макс(0, 8 - КоличествоУровней);
	ПараметрыИзмерения  = Новый Структура("КоличествоУровней,ОбластьОформлениеИзмерений,НачальноеОформление",
											КоличествоУровней,ОбластьОформлениеИзмерений,НачальноеОформление);
	ИзмеренияОтчета.Очистить();
	ИзмеренияОтчета.Добавить("СкладКомпании");
	ИзмеренияОтчета.Добавить("ДокументДвижения");
	Для Уровень=1 По КоличествоУровней-2 Цикл
		ИзмеренияОтчета.Добавить("ДокументДвиженияУровень"+Уровень);
	КонецЦикла;
	СчетчикУровня = 1;
	Для Каждого ТекИзмерение Из ИзмеренияОтчета Цикл
		СформироватьМакетИзмеренияСтрокиПервойТаблицы(МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, ГраницаМакета);
		СчетчикУровня = СчетчикУровня + 1;
	КонецЦикла;
 	ШиринаТаблицы = МакетОтчета.ШиринаТаблицы;
	// МАКЕТ ТАБЛИЦЫ ТОВАРОВ
	ВысотаПервойЧасти = МакетОтчета.ВысотаТаблицы;
	МакетОтчета.Область("R" + (ВысотаПервойЧасти + 1) + ":R" + (ВысотаПервойЧасти + 4)).СоздатьФорматСтрок();
	Если НЕ ИспользоватьАвтоотступ Тогда
		Для к = 1 По 2 Цикл
			ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление + к, 1);
			ОбластьПредУровня = МакетОтчета.Область(ВысотаПервойЧасти + 1, к + 1,ВысотаПервойЧасти + 2, к + 1);
			ОбластьПредУровня.Шрифт        = ОформлениеУровня.Шрифт;
			ОбластьПредУровня.ЦветТекста   = ОформлениеУровня.ЦветТекста;
			ОбластьПредУровня.ЦветФона     = ОформлениеУровня.ЦветФона;
			ОбластьПредУровня.ГраницаСлева = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
			ОбластьПредУровня.ШиринаКолонки = 2;
		КонецЦикла;
		НачальныйНомерВыводаКолонок = 5;
		Для к = 4 По 5 Цикл
			МакетОтчета.Область(ВысотаПервойЧасти + 1, НачальныйНомерВыводаКолонок).ШиринаКолонки = ?(к = 5, 31.5+ДопШиринаВторогоУровня, 2);
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		ОбластьЗаголовокСтрок = МакетОтчета.Область(ВысотаПервойЧасти + 1, 4, ВысотаПервойЧасти + 2, НачальныйНомерВыводаКолонок - 1);
	Иначе
		НачальныйНомерВыводаКолонок = 3;
		МакетОтчета.Область(ВысотаПервойЧасти + 1, 2).ШиринаКолонки = 40.25+ДопШиринаВторогоУровня;
		ОбластьЗаголовокСтрок = МакетОтчета.Область(ВысотаПервойЧасти + 1, 2, ВысотаПервойЧасти + 2, НачальныйНомерВыводаКолонок - 1);
	КонецЕсли;
	
	ОбластьЗаголовокСтрок.Объединить();
	ОбластьЗаголовокСтрок.Текст					  = "Номенклатура / Характеристика";
	ОбластьЗаголовокСтрок.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьЗаголовокСтрок.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ГраницаСлева            = ГраницаМакета.Измерения.ГраницаСлева;
	ОбластьЗаголовокСтрок.ГраницаСверху           = ГраницаМакета.Измерения.ГраницаСверху;
	//ОбластьЗаголовокСтрок.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
	
	Если ВыводитьДополнительныеПоляВОтдельнойКолонке Тогда
		Для Каждого ТекПоле Из ДополнительныеПоляОтчета Цикл
			
			Если ТекПоле.Уровень=1 Тогда Продолжить; КонецЕсли;
			ОбластьДопПоле = МакетОтчета.Область(ВысотаПервойЧасти + 1, НачальныйНомерВыводаКолонок, ВысотаПервойЧасти + 2, НачальныйНомерВыводаКолонок);
			ОбластьДопПоле.Объединить();
			ОбластьДопПоле.Текст                   = ТекПоле.ПолноеПредставление;
			ОбластьДопПоле.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьДопПоле.ШиринаКолонки           = ТекПоле.ШиринаКолонки;
			ОбластьДопПоле.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьДопПоле.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьДопПоле.ГраницаСлева            = ГраницаМакета.ДопПоля.ГраницаСлева;
			ОбластьДопПоле.ГраницаСверху           = ГраницаМакета.ДопПоля.ГраницаСверху;
			ОбластьДопПоле.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
			
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
	КонецЕсли;
	
	ОбластьШапкаСтрок = МакетОтчета.Область("R" + (ВысотаПервойЧасти + 1) + ":R" + (ВысотаПервойЧасти + 2));
	ОбластьШапкаСтрок.Имя = "ШапкаТаблицыТоваров";
	
	НомерКолонки = НачальныйНомерВыводаКолонок;
	НомерСтроки = ВысотаПервойЧасти + 1;
	НомерПервойКолонки = НачальныйНомерВыводаКолонок;
	НомерПервойКолонкиИзмерения = 0;
	ОбластьКолонкиПоказатель = Неопределено;
	
	Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
		НомерПервойКолонкиФункции = НомерКолонки;
		Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
			ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки+1, НомерКолонки, НомерСтроки+1, НомерКолонки);
			ОбластьКолонкиПоказатель.Текст                   = ТекПоказатель.Представление;
			ОбластьКолонкиПоказатель.ШиринаКолонки	  	     = ТекПоказатель.ШиринаКолонки;
			ОбластьКолонкиПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьКолонкиПоказатель.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьКолонкиПоказатель.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьКолонкиПоказатель.ГраницаСлева            = ?(НомерПервойКолонкиФункции = НомерКолонки, ?(НомерПервойКолонкиИзмерения = 0,ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева,ГраницаМакета.Функции.ГраницаСправа), ГраницаМакета.Показатели.ГраницаСлева);
			ОбластьКолонкиПоказатель.ГраницаСверху           = ГраницаМакета.Показатели.ГраницаСверху;
			ОбластьКолонкиПоказатель.ГраницаСнизу            = ГраницаМакета.Показатели.ГраницаСнизу;
			НомерКолонки = НомерКолонки + 1;
		КонецЦикла;
		
		Если ТекФункция.Одиночный Тогда
			ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки, НомерПервойКолонкиФункции, НомерСтроки+1, НомерПервойКолонкиФункции);
			ОбластьКолонкиФункция.ШиринаКолонки = ?(ЗначениеЗаполнено(ТекФункция.ШиринаКолонки), ТекФункция.ШиринаКолонки, 14);
			НомерКолонки = НомерКолонки+1;
		Иначе	
			ОбластьКолонкиПоказатель.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
			ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки, НомерПервойКолонкиФункции, НомерСтроки, НомерКолонки - 1);
		КонецЕсли;
		
		ОбластьКолонкиФункция.Объединить();
		ОбластьКолонкиФункция.Текст                   = ТекФункция.Представление;
		ОбластьКолонкиФункция.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонкиФункция.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьКолонкиФункция.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьКолонкиФункция.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
		ОбластьКолонкиФункция.ГраницаСверху           = ГраницаМакета.Функции.ГраницаСверху;
		ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
		НомерПервойКолонкиИзмерения = НомерПервойКолонкиИзмерения+1;
	КонецЦикла;
	
	ОбластьКолонкиФункция.ГраницаСправа           	  = ПеремГраницаСправа;
	Если ОбластьКолонкиПоказатель<> Неопределено Тогда
		ОбластьКолонкиПоказатель.ГраницаСправа        = ПеремГраницаСправа;
	КонецЕсли;
	
	НачальноеОформление2 = 7;
	ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление2, 1);
	Если НЕ ИспользоватьАвтоотступ Тогда
		ОбластьШапкиТаблицы = МакетОтчета.Область(ВысотаПервойЧасти + 1, 4, ВысотаПервойЧасти + 2, НомерКолонки - 1);
	Иначе
		ОбластьШапкиТаблицы = МакетОтчета.Область(ВысотаПервойЧасти + 1, 2, ВысотаПервойЧасти + 2, НомерКолонки - 1);
	КонецЕсли;
	ОбластьШапкиТаблицы.Шрифт         = ОформлениеУровня.Шрифт;
	ОбластьШапкиТаблицы.ЦветТекста    = ОформлениеУровня.ЦветТекста;
	ОбластьШапкиТаблицы.ЦветФона      = ОформлениеУровня.ЦветФона;
	
	ПараметрыИзмерения  = Новый Структура("РесурсыОтчета,ДеревоРесурсов,КоличествоУровней,ОбластьОформлениеИзмерений,НачальноеОформление, НачальноеОформление2",
	РесурсыОтчета, ДеревоРесурсов, КоличествоУровней, ОбластьОформлениеИзмерений, НачальноеОформление, НачальноеОформление2);
	
	ИзмеренияОтчета.Очистить();
	ИзмеренияОтчета.Добавить("Номенклатура");
	ИзмеренияОтчета.Добавить("ХарактеристикаНоменклатуры");
	СчетчикУровня = 4;
	НомерСтроки = НомерСтроки + 1;
	Для Каждого ТекИзмерение Из ИзмеренияОтчета Цикл
		НомерСтроки = НомерСтроки + 1;
		СформироватьМакетИзмеренияСтрокиВторойТаблицы(МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, ГраницаМакета, НомерСтроки);
		СчетчикУровня = СчетчикУровня + 1;
	КонецЦикла;
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "ПодвалТаблицы";
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "Подвал";
	
	ОбластьПостроителя = МакетОтчета.Область("R1:R" + (ВысотаЗаголовка + 2));
	ОбластьПостроителя.Имя = "ФиксированныеСтроки";    
	
	Для к = 1 По ВысотаЗаголовка Цикл
		ТекОбласть = МакетОтчета.Область(к, 2, к, ШиринаТаблицы);
		ТекОбласть.ПоВыделеннымКолонкам = Истина;
		ТекОбласть.РазмещениеТекста     = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	КонецЦикла;
	
	//МакетОтчета.Показать();
	Возврат МакетОтчета;
	
КонецФункции //	ПолучитьМакетПостроителя()

// возвращает конечных регистраторов
Функция ПолучитьКонечныеРегистраторов(Запрос)
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТипыОснования = Новый Массив;
	МассивОснований = Новый Массив;
	Пока Выборка.Следующий() Цикл
		ТипЗнач=ТипЗнч(Выборка.Регистратор);
		Если ТипыОснования.Найти(ТипЗнач)=Неопределено Тогда
			ТипыОснования.Добавить(ТипЗнач);
		КонецЕсли;
		МассивОснований.Добавить(Выборка.Регистратор);
	КонецЦикла;
	
	МассивРегистраторов = Новый Массив;
	ТипыРегистраторов = Новый Массив;
	Пока МассивОснований.Количество()>0 Цикл
		ТаблицаОснований = Новый ТаблицаЗначений;
		ТаблицаОснований.Колонки.Добавить("Регистратор", Новый ОписаниеТипов(ТипыОснования));
		Для Каждого ТекПоле Из МассивОснований Цикл
			НоваяСтрока = ТаблицаОснований.Добавить();
			НоваяСтрока.Регистратор = ТекПоле;
		КонецЦикла;
		// У документа "Пересортица товаров" нет реквизита "ДокументОснование"
		Попытка
			Запрос.Текст = "
				|ВЫБРАТЬ
				|	ТаблицаОснований.Регистратор КАК Регистратор
				|ПОМЕСТИТЬ ТаблицаОснований
				|ИЗ
				|	&ТаблицаОснований КАК ТаблицаОснований
				|;
				|
				|ВЫБРАТЬ
				|	ТаблицаОснований.Регистратор КАК Регистратор,
				|	ТаблицаОснований.Регистратор.ДокументОснование КАК Основание
				|ИЗ
				|	ТаблицаОснований КАК ТаблицаОснований
				|;
				|
				|УНИЧТОЖИТЬ ТаблицаОснований";
			Запрос.УстановитьПараметр("ТаблицаОснований", ТаблицаОснований);
			Выборка = Запрос.Выполнить().Выбрать();
		Исключение
			Запрос.Текст = "
				|ВЫБРАТЬ
				|	ТаблицаОснований.Регистратор КАК Регистратор
				|ПОМЕСТИТЬ ТаблицаОснований
				|ИЗ
				|	&ТаблицаОснований КАК ТаблицаОснований
				|;
				|
				|ВЫБРАТЬ
				|	ТаблицаОснований.Регистратор КАК Регистратор,
				|	NULL КАК Основание
				|ИЗ
				|	ТаблицаОснований КАК ТаблицаОснований
				|;
				|
				|УНИЧТОЖИТЬ ТаблицаОснований";
			Запрос.УстановитьПараметр("ТаблицаОснований", ТаблицаОснований);
			Выборка = Запрос.Выполнить().Выбрать();
		КонецПопытки;
		ТипыОснования.Очистить();
		МассивОснований.Очистить();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Основание) Тогда
				ТипЗнач=ТипЗнч(Выборка.Основание);
				Если ТипыОснования.Найти(ТипЗнач)=Неопределено Тогда
					ТипыОснования.Добавить(ТипЗнач);
				КонецЕсли;
				Если МассивОснований.Найти(Выборка.Основание)=Неопределено Тогда
					МассивОснований.Добавить(Выборка.Основание);
				КонецЕсли;
			Иначе
				ТипЗнач=ТипЗнч(Выборка.Регистратор);
				Если ТипыРегистраторов.Найти(ТипЗнач)=Неопределено Тогда
					ТипыРегистраторов.Добавить(ТипЗнач);
				КонецЕсли;
				МассивРегистраторов.Добавить(Выборка.Регистратор);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаВерхнегоУровня = Новый ТаблицаЗначений;
	ТаблицаВерхнегоУровня.Колонки.Добавить("Регистратор", Новый ОписаниеТипов(ТипыРегистраторов));
	Для Каждого ТекПоле Из МассивРегистраторов Цикл
		НоваяСтрока = ТаблицаВерхнегоУровня.Добавить();
		НоваяСтрока.Регистратор = ТекПоле;
	КонецЦикла;
	
	Возврат ТаблицаВерхнегоУровня;
	
КонецФункции

// возвращает таблицу регистраторов
Функция ПолучитьТаблицыРегистраторов(ТаблицаВерхнегоУровня, ОбщаяТаблица, Запрос)
	
	КоличествоУровней=0;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТипыОснования = Новый Массив;
	МассивОснований = Новый Массив;
	Пока Выборка.Следующий() Цикл
		ТипЗнач=ТипЗнч(Выборка.Регистратор);
		Если ТипыОснования.Найти(ТипЗнач)=Неопределено Тогда
			ТипыОснования.Добавить(ТипЗнач);
		КонецЕсли;
		МассивОснований.Добавить(Выборка.Регистратор);
	КонецЦикла;
	ПолучатьТаблицуВерхнегоУровня = (ТаблицаВерхнегоУровня=Неопределено);
	ТипДокументов = Документы.ТипВсеСсылки();
	Если ПолучатьТаблицуВерхнегоУровня Тогда
		ТаблицаВерхнегоУровня = Новый ТаблицаЗначений;
		ТаблицаВерхнегоУровня.Колонки.Добавить("Регистратор", ТипДокументов);
	КонецЕсли;
	
	ОбщаяТаблица = Новый ТаблицаЗначений;
	ОбщаяТаблица.Колонки.Добавить("Регистратор", ТипДокументов);
	ОбщаяТаблица.Колонки.Добавить("Основание", ТипДокументов);
	Пока МассивОснований.Количество()>0 Цикл
		ТаблицаОснований = Новый ТаблицаЗначений;
		ТаблицаОснований.Колонки.Добавить("Регистратор", Новый ОписаниеТипов(ТипыОснования));
		Для Каждого ТекПоле Из МассивОснований Цикл
			НоваяСтрока = ТаблицаОснований.Добавить();
			НоваяСтрока.Регистратор = ТекПоле;
		КонецЦикла;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТаблицаОснований.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ТаблицаОснований
		|ИЗ
		|	&ТаблицаОснований КАК ТаблицаОснований
		|;
		|
		|ВЫБРАТЬ
		|	ТаблицаОснований.Регистратор КАК Регистратор,
		|	ТаблицаОснований.Регистратор.ДокументОснование КАК Основание
		|ИЗ
		|	ТаблицаОснований КАК ТаблицаОснований
		|;
		|
		|УНИЧТОЖИТЬ ТаблицаОснований";
		Запрос.УстановитьПараметр("ТаблицаОснований", ТаблицаОснований);
		Выборка = Запрос.Выполнить().Выбрать();
		ТипыОснования.Очистить();
		МассивОснований.Очистить();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Основание) Тогда
				ТипЗнач=ТипЗнч(Выборка.Основание);
				Если ТипыОснования.Найти(ТипЗнач)=Неопределено Тогда
					ТипыОснования.Добавить(ТипЗнач);
				КонецЕсли;
				Если МассивОснований.Найти(Выборка.Основание)=Неопределено Тогда
					МассивОснований.Добавить(Выборка.Основание)
				КонецЕсли;
				НоваяСтрока=ОбщаяТаблица.Добавить();
				НоваяСтрока.Регистратор = Выборка.Регистратор;
				НоваяСтрока.Основание = Выборка.Основание;
			ИначеЕсли ПолучатьТаблицуВерхнегоУровня Тогда
				НоваяСтрока=ТаблицаВерхнегоУровня.Добавить();
				НоваяСтрока.Регистратор = Выборка.Регистратор;
			КонецЕсли;
		КонецЦикла;
		КоличествоУровней=КоличествоУровней+1;
	КонецЦикла;
	ТаблицаВерхнегоУровня.Свернуть("Регистратор");
	ОбщаяТаблица.Свернуть("Регистратор, Основание");
	
	Возврат КоличествоУровней-1;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройкиОтчетаСИсточникомДанных(СтруктураПараметрыЗаполнения, глПрава);
	
	ДополнительныеПоляОтчета.Колонки.Добавить("Уровень");
	ДополнительныеПоляОтчета.Колонки.Добавить("Родитель");
	ДополнительныеПоляОтчета.Колонки.Добавить("НомерСтроки");
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ТекстОтбора		 = "";
	ТекстОтбораОрдер = "";
	ТекстОтбораПоДокументу = "";
	Для Каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
		Если НЕ ТекОтбор.Использование Тогда Продолжить; КонецЕсли;
		СтрокаУсловия = СтрЗаменить(отСформироватьСтрокуВидаСравнения(ТекОтбор.ВидСравнения),"&","&Выб"+ТекОтбор.Имя);
		ПутьОтбора = ТекОтбор.ПутьКДанным;
		Если Найти(ТекОтбор.ПутьКДанным, "Подразделение") = 1 ИЛИ Найти(ТекОтбор.ПутьКДанным, "Организация") = 1 Тогда
			ПутьОтбора = "СкладКомпании."+ПутьОтбора;
		КонецЕсли;
		Если Найти(ПутьОтбора, "ДокументДвижения")=1 Тогда
			ПутьОтбора = "ОстаткиТоваровКомпании."+СтрЗаменить(ПутьОтбора,"ДокументДвижения","Регистратор");
			ТекстОтбораПоДокументу = ТекстОтбораПоДокументу+" И 
			|	"+СтрЗаменить(СтрокаУсловия, "#",ПутьОтбора);
		Иначе
			ТекстОтбора = ТекстОтбора+" И "+Символы.ПС+СтрЗаменить(СтрокаУсловия, "#","ОстаткиТоваровКомпании."+ПутьОтбора);
			ТекстОтбораОрдер = ТекстОтбораОрдер+" И "+Символы.ПС+СтрЗаменить(СтрокаУсловия, "#","ОстаткиТоваровОрдерныйСклад."+ПутьОтбора);
		КонецЕсли;
		Запрос.УстановитьПараметр("Выб"+ТекОтбор.Имя, ТекОтбор.Значение);
	КонецЦикла;
	
	ДополнительныеПоляОтчета.Очистить();
	
	ТекстПолей = "";
	Для Каждого ТекСтрока Из ИзмеренияСтроки Цикл
		Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
		ИндексСтроки = ИзмеренияСтроки.Индекс(ТекСтрока);
		ИдентификаторПоля = СтрЗаменить(ТекСтрока.ПутьКДанным, ".", "_");
		Если НЕ ПараметрыИзмеренийСтрок.Свойство(ИдентификаторПоля) Тогда Продолжить; КонецЕсли;
		Если ПараметрыИзмеренийСтрок[ИдентификаторПоля].Свойство("Поля") Тогда
			ДополнительныеПоля = ПараметрыИзмеренийСтрок[ИдентификаторПоля].Поля;
			Если ТипЗнч(ДополнительныеПоля) = Тип("ТаблицаЗначений") Тогда
				Для Каждого ТекПоле Из ДополнительныеПоля Цикл
					ИмяТекПоля=СтрЗаменить(ТекПоле.ПутьКДанным, ".","");
					Если ПустаяСтрока(ИмяТекПоля) Тогда
						Продолжить;
					КонецЕсли;
					ТекстПолей = ТекстПолей+",
					|	ОборотыТоваров."+ТекПоле.ПутьКДанным;
					ТекВыбранноеПоле = ДополнительныеПоляОтчета.Добавить();
					ТекВыбранноеПоле.Имя = ИмяТекПоля;
					ТекВыбранноеПоле.ПутьКДанным = ТекПоле.ПутьКДанным;
					ТекВыбранноеПоле.Уровень = ?(ИдентификаторПоля="Номенклатура" ИЛИ ИдентификаторПоля="ХарактеристикаНоменклатуры",2,1);
					ТекВыбранноеПоле.Родитель = ИдентификаторПоля;
					ТекВыбранноеПоле.НомерСтроки = ИндексСтроки;
					НайденноеПоле = ДеревоДоступныхПолей.Строки.Найти(ТекПоле.ПутьКДанным, "ПутьКДанным", Истина);
					Если НайденноеПоле = Неопределено Тогда
						ТекВыбранноеПоле.ПолноеПредставление = ТекПоле.Представление;
					Иначе
						ТекВыбранноеПоле.ПолноеПредставление = НайденноеПоле.ПолноеПредставление;
					КонецЕсли;
					
					ТекВыбранноеПоле.НеСвязанные = Ложь;
					Попытка
						ПолеИзПараметрыИспользования = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекПоле.ПутьКДанным, ПараметрыИспользованияПолейИПоказателей);
						ТекВыбранноеПоле.ШиринаКолонки = ПолеИзПараметрыИспользования["ШиринаКолонки"];
						Если ПолеИзПараметрыИспользования.Свойство("Формат") Тогда
							ТекВыбранноеПоле.Формат = ПолеИзПараметрыИспользования["Формат"];
						КонецЕсли;
					Исключение
						ТекВыбранноеПоле.ШиринаКолонки = 20;
					КонецПопытки;
				КонецЦикла;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаКонца));
	
	ТаблицаВерхнегоУровня = Неопределено;
	Если ТекстОтбораПоДокументу<>"" Тогда
		Запрос.Текст="
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОстаткиТоваровКомпании.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
		|ГДЕ
		|	ОстаткиТоваровКомпании.Период <= &ДатаКон И 
		|	ОстаткиТоваровКомпании.Количество>0
		|	"+ТекстОтбораПоДокументу+"
		|	"+ТекстОтбора+"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОстаткиТоваровОрдерныйСклад.Регистратор
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровОрдерныйСклад КАК ОстаткиТоваровОрдерныйСклад
		|ГДЕ
		|	ОстаткиТоваровОрдерныйСклад.Период<=&ДатаКон И 
		|	ОстаткиТоваровОрдерныйСклад.Количество>0
		|	"+СтрЗаменить(ТекстОтбораПоДокументу,"ОстаткиТоваровКомпании","ОстаткиТоваровОрдерныйСклад")+"
		|	"+ТекстОтбораОрдер;
		ТаблицаВерхнегоУровня = ПолучитьКонечныеРегистраторов(Запрос);
	КонецЕсли;
	
	Запрос.Текст="
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОстаткиТоваровКомпании.Регистратор
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
	|ГДЕ
	|	ОстаткиТоваровКомпании.Период <= &ДатаКон И 
	|	ОстаткиТоваровКомпании.Количество>0
	|"+ТекстОтбора+"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОстаткиТоваровОрдерныйСклад.Регистратор
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровОрдерныйСклад КАК ОстаткиТоваровОрдерныйСклад
	|ГДЕ
	|	ОстаткиТоваровОрдерныйСклад.Период<=&ДатаКон И 
	|	ОстаткиТоваровОрдерныйСклад.Количество>0
	|"+ТекстОтбораОрдер;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОбщаяТаблица		 	= Неопределено;
	КоличествоУровней = ПолучитьТаблицыРегистраторов(ТаблицаВерхнегоУровня, ОбщаяТаблица, Запрос);
	
	ТекстСоединения			= "";
	ТекстВыборка			= "";
	ТекстПсевдонимы			= "";
	ТекстВыборкаОбъединения	= "";
	ТекстПредставлений		= "";
	Если ТаблицаВерхнегоУровня.Количество()>0 Тогда
		ТекстСводногоОбъединения = "";
		Для Уровень=1 По КоличествоУровней Цикл
			ТекстУровень = Формат(Уровень,"ЧН=; ЧГ=");
			ТекстВыборка = ТекстВыборка + ",
			|	Уровень"+ТекстУровень+".Регистратор КАК ДокументДвиженияУровень"+ТекстУровень;
			ТекстСоединения = ТекстСоединения+"
			|	ЛЕВОЕ СОЕДИНЕНИЕ ОбщаяТаблица КАК Уровень"+ТекстУровень+"
			|ПО
			|	Уровень"+Формат(Уровень-1,"ЧН=; ЧГ=")+".Регистратор=Уровень"+ТекстУровень+".Основание";
			ТекстВыборкаОбъединения = ТекстВыборкаОбъединения+",
			|	ИерархияДокументов.ДокументДвиженияУровень"+ТекстУровень+" КАК ДокументДвиженияУровень"+ТекстУровень;
			ТекстПредставлений = ТекстПредставлений+",
			|	ПРЕДСТАВЛЕНИЕ(ИерархияДокументов.ДокументДвиженияУровень"+ТекстУровень+") КАК ДокументДвиженияУровень"+ТекстУровень+"Представление";
			ТекстПсевдонимы	= ТекстПсевдонимы + ",
			|	ДокументДвиженияУровень"+ТекстУровень;
			ТекстСводногоОбъединения = ТекстСводногоОбъединения + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ИерархияДокументов.ДокументДвижения,
			|	ИерархияДокументов.ДокументДвиженияУровень"+ТекстУровень+"
			|ИЗ 
			|	ИерархияДокументов КАК ИерархияДокументов";
		КонецЦикла;
		
		Запрос.Текст="
		|ВЫБРАТЬ
		|	ТаблицаВерхнегоУровня.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ТаблицаВерхнегоУровня
		|ИЗ
		|	&ТаблицаВерхнегоУровня КАК ТаблицаВерхнегоУровня
		|;
		|
		|ВЫБРАТЬ
		|	ОбщаяТаблица.Регистратор КАК Регистратор,
		|	ОбщаяТаблица.Основание КАК Основание
		|ПОМЕСТИТЬ ОбщаяТаблица
		|ИЗ
		|	&ОбщаяТаблица КАК ОбщаяТаблица
		|;
		|
		|ВЫБРАТЬ
		|	Уровень0.Регистратор КАК ДокументДвижения"+ТекстВыборка+"
		|ПОМЕСТИТЬ ИерархияДокументов
		|ИЗ
		|	ТаблицаВерхнегоУровня КАК Уровень0"+ТекстСоединения+"
		|;
		|ВЫБРАТЬ
		|	ИерархияДокументов.ДокументДвижения КАК ДокументДвижения"+ТекстВыборкаОбъединения+ТекстПредставлений+"
		|ИЗ
		|	ИерархияДокументов КАК ИерархияДокументов
		|ИТОГИ
		|ПО
		|	ДокументДвижения"+ТекстПсевдонимы+"
		|;
		|УНИЧТОЖИТЬ ОбщаяТаблица";
		Запрос.УстановитьПараметр("ТаблицаВерхнегоУровня", ТаблицаВерхнегоУровня);
		Запрос.УстановитьПараметр("ОбщаяТаблица", ОбщаяТаблица);
		ДеревоРегистраторов = Новый ДеревоЗначений;
		ДеревоРегистраторов.Колонки.Добавить("Документ");
		ДеревоРегистраторов.Колонки.Добавить("ДокументПредставление");
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ДеревоРегистраторов.Строки.Добавить();
			НоваяСтрока.Документ = Выборка.ДокументДвижения;
			ВыгрузитьДеревоДокументов(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам), НоваяСтрока);
		КонецЦикла;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	СводноеОбъединение.ДокументДвижения КАК ДокументДвижения,
		|	СводноеОбъединение.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ СводноеОбъединение
		|	ИЗ(ВЫБРАТЬ
		|		ТаблицаВерхнегоУровня.Регистратор КАК ДокументДвижения,
		|		ТаблицаВерхнегоУровня.Регистратор КАК Регистратор
		|	ИЗ
		|		ТаблицаВерхнегоУровня КАК ТаблицаВерхнегоУровня"+ТекстСводногоОбъединения+") КАК СводноеОбъединение
		|;
		|
		|ВЫБРАТЬ
		|	ОборотыТоваров.ДокументДвижения КАК ДокументДвижения,
		|	ОборотыТоваров.Номенклатура КАК Номенклатура,
		|	ОборотыТоваров.СкладКомпании КАК СкладКомпании,
		|	ОборотыТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПРЕДСТАВЛЕНИЕ(ОборотыТоваров.ДокументДвижения) КАК ДокументДвиженияПредставление,
		|	ПРЕДСТАВЛЕНИЕ(ОборотыТоваров.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕ(ОборотыТоваров.СкладКомпании) КАК СкладКомпанииПредставление,
		|	ПРЕДСТАВЛЕНИЕ(ОборотыТоваров.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатурыПредставление,
		|	СУММА(ОборотыТоваров.КоличествоСкладскойПриход) КАК КоличествоСкладскойПриход,
		|	СУММА(ОборотыТоваров.КоличествоСкладскойРасход) КАК КоличествоСкладскойРасход,
		|	СУММА(ОборотыТоваров.КоличествоОрдерныйПриход) КАК КоличествоОрдерныйПриход,
		|	СУММА(ОборотыТоваров.КоличествоОрдерныйРасход) КАК КоличествоОрдерныйРасход,
		|	СУММА(ОборотыТоваров.КоличествоОрдерныйПриход - ОборотыТоваров.КоличествоСкладскойПриход) КАК КоличествоРазницаПриход,
		|	СУММА(ОборотыТоваров.КоличествоОрдерныйРасход - ОборотыТоваров.КоличествоСкладскойРасход) КАК КоличествоРазницаРасход"+ТекстПолей+"
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		СводноеОбъединение.ДокументДвижения КАК ДокументДвижения,
		|		ОстаткиТоваровКомпании.Номенклатура,
		|		ОстаткиТоваровКомпании.СкладКомпании,
		|		ОстаткиТоваровКомпании.ХарактеристикаНоменклатуры,
		|		ВЫБОР
		|			КОГДА ОстаткиТоваровКомпании.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|				ОстаткиТоваровКомпании.Количество
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК КоличествоСкладскойПриход,
		|		ВЫБОР
		|			КОГДА ОстаткиТоваровКомпании.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|				0
		|			ИНАЧЕ
		|				ОстаткиТоваровКомпании.Количество
		|		КОНЕЦ КАК КоличествоСкладскойРасход,
		|		0 КАК КоличествоОрдерныйПриход,
		|		0 КАК КоличествоОрдерныйРасход
		|	ИЗ	СводноеОбъединение КАК СводноеОбъединение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании КАК ОстаткиТоваровКомпании
		|	ПО СводноеОбъединение.Регистратор = ОстаткиТоваровКомпании.Регистратор
		|	ГДЕ
		|		ОстаткиТоваровКомпании.Период <= &ДатаКон И 
		|		ОстаткиТоваровКомпании.Количество>0
		|		"+ТекстОтбора+"
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		СводноеОбъединение.ДокументДвижения КАК ДокументДвижения,
		|		ОстаткиТоваровОрдерныйСклад.Номенклатура,
		|		ОстаткиТоваровОрдерныйСклад.СкладКомпании,
		|		ОстаткиТоваровОрдерныйСклад.ХарактеристикаНоменклатуры,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ОстаткиТоваровОрдерныйСклад.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|				ОстаткиТоваровОрдерныйСклад.Количество
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОстаткиТоваровОрдерныйСклад.ВидДвижения=ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|				0
		|			ИНАЧЕ
		|				ОстаткиТоваровОрдерныйСклад.Количество
		|		КОНЕЦ
		|	ИЗ
		|		СводноеОбъединение КАК СводноеОбъединение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровОрдерныйСклад КАК ОстаткиТоваровОрдерныйСклад
		|	ПО СводноеОбъединение.Регистратор = ОстаткиТоваровОрдерныйСклад.Регистратор
		|	ГДЕ
		|		ОстаткиТоваровОрдерныйСклад.Период<=&ДатаКон И 
		|		ОстаткиТоваровОрдерныйСклад.Количество>0
		|		"+ТекстОтбораОрдер+") КАК ОборотыТоваров
		|СГРУППИРОВАТЬ ПО
		|	ОборотыТоваров.ДокументДвижения,
		|	ОборотыТоваров.Номенклатура,
		|	ОборотыТоваров.СкладКомпании,
		|	ОборотыТоваров.ХарактеристикаНоменклатуры
		|"+?(ВыводитьТолькоНеНулевуюРазницу, "ИМЕЮЩИЕ
		|	НЕ (СУММА(ОборотыТоваров.КоличествоОрдерныйПриход - ОборотыТоваров.КоличествоСкладскойПриход)=0 И
		|	СУММА(ОборотыТоваров.КоличествоОрдерныйРасход - ОборотыТоваров.КоличествоСкладскойРасход)=0)","")+"
		|ИТОГИ 
		|	СУММА(КоличествоСкладскойПриход),
		|	СУММА(КоличествоСкладскойРасход),
		|	СУММА(КоличествоОрдерныйПриход),
		|	СУММА(КоличествоОрдерныйРасход),
		|	СУММА(КоличествоРазницаПриход),
		|	СУММА(КоличествоРазницаРасход)
		|ПО
		|	СкладКомпании,
		|	ДокументДвижения,
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры";
	КонецЕсли;
	Если ТипЗнч(ДокументРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокументРезультат) <> Тип("ТабличныйДокумент") Тогда
		ДокументРезультат = Новый ТабличныйДокумент();
	Иначе
		ДокументРезультат.Очистить();
		ДокументРезультат.ФиксацияСверху = 0;
		ДокументРезультат.ФиксацияСлева  = 0;
	КонецЕсли;
	
	ШиринаТаблицы = 0;
	МакетОтчета = ПолучитьМакетПостроителя(КоличествоУровней, ШиринаТаблицы);
	//МакетОтчета = ПолучитьМакетПостроителя(КоличествоУровней);
	
	РазворачиватьЗаголовок = обПраво("РазворачиватьЗаголовокОтчета", глПрава);
	
	Если обЗначениеНеЗаполнено(РазворачиватьЗаголовок) Тогда
		РазворачиватьЗаголовок = Ложь;
	КонецЕсли;
	
	ШаблонЗаголовка = МакетОтчета.ПолучитьОбласть("ЗаголовокШаблон");
	Для к = 1 По ШаблонЗаголовка.ВысотаТаблицы Цикл
		Если к = 2 Тогда
			ДокументРезультат.НачатьГруппуСтрок("Заголовок", РазворачиватьЗаголовок);
		КонецЕсли;
		Секция = ШаблонЗаголовка.ПолучитьОбласть("R" + к);
		ДокументРезультат.Вывести(Секция, 1, "Заголовок");
	КонецЦикла;
	Если к > 1 Тогда
		ДокументРезультат.ЗакончитьГруппуСтрок();
	КонецЕсли;
	
	МакетШапкаТаблицы = МакетОтчета.ПолучитьОбласть("ШапкаТаблицы");
	ДокументРезультат.Вывести(МакетШапкаТаблицы, 1, "ШапкаТаблицы");
	Если ТаблицаВерхнегоУровня.Количество()>0 Тогда	
		МакетСкладКомпании				= МакетОтчета.ПолучитьОбласть("СкладКомпании");
		МакетДокументДвижения			= МакетОтчета.ПолучитьОбласть("ДокументДвижения");
		МакетШапкаТаблицыТоваров		= МакетОтчета.ПолучитьОбласть("ШапкаТаблицыТоваров");
		МакетНоменклатура				= МакетОтчета.ПолучитьОбласть("Номенклатура");
		МакетХарактеристикаНоменклатуры	= МакетОтчета.ПолучитьОбласть("ХарактеристикаНоменклатуры");
		СтруктураМакетов = Новый Структура;
		Для Уровень=1 По КоличествоУровней Цикл
			ИмяМакета = "ДокументДвиженияУровень"+Уровень;
			СтруктураМакетов.Вставить(ИмяМакета, МакетОтчета.ПолучитьОбласть(ИмяМакета));
		КонецЦикла;
		ВыборкаСкладов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Запрос.МенеджерВременныхТаблиц.Закрыть();
		ДокументРезультат.НачатьАвтогруппировкуСтрок();
		Пока ВыборкаСкладов.Следующий() Цикл
			МакетСкладКомпании.Параметры.Заполнить(ВыборкаСкладов);
			ДокументРезультат.Вывести(МакетСкладКомпании, 1, "Склад компании");
			ВыборкаДокументов = ВыборкаСкладов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДокументов.Следующий() Цикл
				МакетДокументДвижения.Параметры.Заполнить(ВыборкаДокументов);
				ДокументРезультат.Вывести(МакетДокументДвижения, 2, "Документ уровень 1");
				//Вывод иерархии
				ЦепочкаДокументов = ДеревоРегистраторов.Строки.Найти(ВыборкаДокументов.ДокументДвижения, "Документ", Ложь);
				ВывестиИерархиюДокументов(ЦепочкаДокументов.Строки, СтруктураМакетов, ДокументРезультат);
				//Вывод товарной таблицы
				ДокументРезультат.Вывести(МакетШапкаТаблицыТоваров, 2, "Шапка товаров");
				ВыборкаНоменклатура = ВыборкаДокументов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаНоменклатура.Следующий() Цикл
					МакетНоменклатура.Параметры.Заполнить(ВыборкаНоменклатура);
					ДокументРезультат.Вывести(МакетНоменклатура, 2, "Номенклатура");
					ВыборкаХарактеристикаНоменклатуры = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаХарактеристикаНоменклатуры.Следующий() Цикл
						Если ВыборкаХарактеристикаНоменклатуры.ХарактеристикаНоменклатуры.Пустая() Тогда Продолжить; КонецЕсли;
						МакетХарактеристикаНоменклатуры.Параметры.Заполнить(ВыборкаХарактеристикаНоменклатуры);
						ДокументРезультат.Вывести(МакетХарактеристикаНоменклатуры, 3, "Характеристика");
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	КонецЕсли;
	// исправим границы документа результата
	Попытка	   МакетОформления = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента);
	Исключение МакетОформления = ПолучитьОбщийМакет("МакетОформлениеОтчета");
	КонецПопытки;
	
	ОбластьОформлениеГраниц = Неопределено;
	Попытка 
		ОбластьОформлениеГраниц    = МакетОформления.ПолучитьОбласть("ОформлениеГраниц");
	Исключение
	КонецПопытки;
	
	Если ОбластьОформлениеГраниц<>Неопределено Тогда
		ЗначениеГраницОтчета  = ОбластьОформлениеГраниц.Области.ЗначениеГраницОтчета;
		ВключитьГраницу = ЗначениеГраницОтчета.АвтоОтступ=1;
		Если ВключитьГраницу Тогда
			// граница слева
			Если ДокументРезультат.Области.Найти("ШапкаТаблицы")<>Неопределено Тогда
				ДокументРезультат.Область(ШаблонЗаголовка.ВысотаТаблицы+1, 2,ДокументРезультат.ВысотаТаблицы-1,2).ГраницаСлева = ЗначениеГраницОтчета.ГраницаСлева;
			Иначе
				ДокументРезультат.Область(ШаблонЗаголовка.ВысотаТаблицы+1, 2,ДокументРезультат.ВысотаТаблицы,2).ГраницаСлева = ЗначениеГраницОтчета.ГраницаСлева;
			КонецЕсли;
			// граница сверху
			Если ДокументРезультат.Области.Найти("ШапкаТаблицы")<>Неопределено ИЛИ ДокументРезультат.Области.Найти("ШапкаСтрок")<>Неопределено Тогда
				ДокументРезультат.Область(ШаблонЗаголовка.ВысотаТаблицы+1, 2,ШаблонЗаголовка.ВысотаТаблицы+1, ШиринаТаблицы).ГраницаСверху = ЗначениеГраницОтчета.ГраницаСверху;
			КонецЕсли;
			//граница справа
			Если ДокументРезультат.Области.Найти("ШапкаТаблицы")<>Неопределено Тогда
				ДокументРезультат.Область(ШаблонЗаголовка.ВысотаТаблицы+1, ШиринаТаблицы, ДокументРезультат.ВысотаТаблицы-1, ШиринаТаблицы).ГраницаСправа = ЗначениеГраницОтчета.ГраницаСправа;
			Иначе
				ДокументРезультат.Область(ШаблонЗаголовка.ВысотаТаблицы+1, ШиринаТаблицы, ДокументРезультат.ВысотаТаблицы, ШиринаТаблицы).ГраницаСправа = ЗначениеГраницОтчета.ГраницаСправа;
			КонецЕсли;
			//граница снизу
			Если ДокументРезультат.Области.Найти("РесурсыИтогПоКолонке")=Неопределено и ДокументРезультат.Области.Найти("ШапкаТаблицы")<>Неопределено Тогда
				ДокументРезультат.Область(ДокументРезультат.ВысотаТаблицы-1, 2, ДокументРезультат.ВысотаТаблицы-1, ШиринаТаблицы).ГраницаСнизу = ЗначениеГраницОтчета.ГраницаСнизу;
			ИначеЕсли ДокументРезультат.Области.Найти("ШапкаСтрок")<>Неопределено Тогда
				ДокументРезультат.Область(ДокументРезультат.ВысотаТаблицы, 2, ДокументРезультат.ВысотаТаблицы, ШиринаТаблицы).ГраницаСнизу = ЗначениеГраницОтчета.ГраницаСнизу;
			Конецесли;
		КонецЕсли;
	Иначе
		ЛинияОбычная       = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,      1);
		Если ДокументРезультат.Области.Найти("ПодвалТаблицы") = Неопределено Тогда
			ОбластьПодвалТаблицы = МакетОтчета.ПолучитьОбласть("ПодвалТаблицы");
			ОбластьПодвалТаблицы.Область(1,2,1,ШиринаТаблицы).ГраницаСверху = ЛинияОбычная;
			ДокументРезультат.Вывести(ОбластьПодвалТаблицы);
		Иначе
			ДокументРезультат.Область(ДокументРезультат.ВысотаТаблицы, 2, ДокументРезультат.ВысотаТаблицы ,ШиринаТаблицы).ГраницаСверху = ЛинияОбычная;
		КонецЕсли;
	КонецЕсли;
	
	ОбластьФиксацииСверху = МакетОтчета.ПолучитьОбласть("ФиксированныеСтроки");
	// установим параметры документа результата
	ДокументРезультат.ФиксацияСверху = ОбластьФиксацииСверху.ВысотаТаблицы;
	ОбластьОформлениеЗаливки = Неопределено;
	Попытка 
		ОбластьОформлениеЗаливки    = МакетОформления.ПолучитьОбласть("ОформлениеЗаливки").Области.Заливка;
	Исключение
	КонецПопытки;
	
	к = ОбластьФиксацииСверху.ВысотаТаблицы + 1;
	Если ОбластьОформлениеЗаливки<>Неопределено Тогда
		Пока к <= ДокументРезультат.ВысотаТаблицы-1 Цикл
			ДокументРезультат.Область(к, 2, к, ШиринаТаблицы).ЦветФона = ОбластьОформлениеЗаливки.ЦветФона;
			к = к + 2;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДокументРезультат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()


//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();

#КонецЕсли

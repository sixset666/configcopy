
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПолучитьСвойства() Экспорт
	
	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("Контрагент");
	СтруктураСвойств.Вставить("Заказ");
	СтруктураСвойств.Вставить("Номенклатура");
	СтруктураСвойств.Вставить("ХарактеристикаНоменклатуры");
	
	Возврат СтруктураСвойств;
	
КонецФункции

// Стандартная процедура настройки схемы компоновщика данных
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ОтчетыСервер.ЗаполнитьНачальныеОстатки(ЭтотОбъект);
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

//Устанавливает параметр построителя отчета "СписокЗаказов"
//
//Без параметров
//
Процедура УстановитьПараметры() Экспорт
	
	НеПроверятьЗаказы = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("НеПроверятьЗаказы", КомпоновщикНастроек.Настройки);
	Если НеПроверятьЗаказы = Истина Тогда
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", Новый Массив);
	Иначе
		ПоляОграничения = Новый Массив;
		ПоляОграничения.Добавить("Контрагент");
		ПоляОграничения.Добавить("Заказ");
		ПоляОграничения.Добавить("Номенклатура");
		ПоляОграничения.Добавить("ХарактеристикаНоменклатуры");
		
		Запрос = Новый Запрос;
		
		ОтборКомпоновки = КомпоновщикНастроек.Настройки.Отбор;
		
		ТекстОтбора = ОтчетыСервер.КомпоновщикПолучитьТекстОтбора(ОтборКомпоновки.Элементы, ОтборКомпоновки.ДоступныеПоляОтбора, "", Запрос.Параметры,, ПоляОграничения);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказыПокупателейОстатки.Заказ КАК Заказ
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&ДатаКонГраница,"+ТекстОтбора+") КАК ЗаказыПокупателейОстатки
		|ГДЕ
		|	(НЕ ЗаказыПокупателейОстатки.ЗаказаноОстаток = 0)
		|";
		
		ДатаКон = ОтчетыСервер.КомпоновщикПолучитьЗначениеПараметра("ДатаОкончанияГраница", КомпоновщикНастроек.Настройки);
		Запрос.УстановитьПараметр("ДатаКонГраница", ДатаКон);
		
		Заказы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Заказ");
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СписокЗаказов", Заказы);
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметры()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка) Экспорт
	
	УстановитьПараметры();
	
	ОтчетыСервер.ВывестиОтчет(ЭтотОбъект, СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

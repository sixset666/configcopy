#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранится текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // структура связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранится перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // структура не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // дополнительные поля отчета
Перем ТаблицаПараметровТипаЦен;                        // таблица параметров типа цен


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Заполнить параметры использования полей и показателей
Процедура ЗаполнитьПараметрыИспользованияПолейИПоказателейМодели(ТекПараметр, ЗначениеФлага)
	
	Ресурсы = ТекПараметр.Использование.Ресурсы;
	
	Для Каждого ТекПоказатель Из Показатели Цикл
		Ресурсы.Вставить(ТекПоказатель.Имя, ЗначениеФлага);
	КонецЦикла;
	
	Если ТекПараметр.Свойство("Поля") Тогда
		Для Каждого ТекПоле Из ТекПараметр.Поля Цикл
			ЗаполнитьПараметрыИспользованияПолейИПоказателейМодели(ТекПоле.Значение, ЗначениеФлага);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура устанавливает параметры запроса для построителя
Процедура УстановитьОсновныеПараметры(ТолькоМодели) Экспорт
	
	//Заполним параметры
	КурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКонца, Новый Структура("Валюта", ВалютаОтчета));
	КурсВалютыОтчета = КурсВалюты.Курс/?(КурсВалюты.Кратность=0,1,КурсВалюты.Кратность);
	ПостроительОтчета.Параметры.Вставить("КурсВалютыОтчета", ?(КурсВалютыОтчета=0,1,КурсВалютыОтчета));
	ПостроительОтчета.Параметры.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ПостроительОтчета.Параметры.Вставить("ВариантКомплектации", Справочники.ВариантыКомплектации.ПустаяСсылка());
	ПостроительОтчета.Параметры.Вставить("ТолькоМодели", ТолькоМодели);
		
КонецПроцедуры	

// Стандартная процедура настройки построителя отчета 
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ТипЦен = обПраво("ОсновнойТипЦенПродажиАвтомобилей", глПрава);
	Если ТипЦен<>Неопределено Тогда
		ВалютаОтчета = ТипЦен.ВалютаЦены;
	КонецЕсли;
	
	Индекс = 0;
	Пока Индекс < ИзмеренияСтроки.Количество() Цикл
		
		ТекИзмерение = ИзмеренияСтроки.Получить(Индекс);
		НайденнаяСтрока = ДеревоДоступныхПолей.Строки.Найти(ТекИзмерение.ПутьКДанным,"ПутьКДанным", Истина);
		Если НайденнаяСтрока = Неопределено Тогда
			ИзмеренияСтроки.Удалить(ТекИзмерение);
			Продолжить;
		ИначеЕсли НЕ НайденнаяСтрока.Измерение Тогда 
			ИзмеренияСтроки.Удалить(ТекИзмерение);
			Продолжить;
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Индекс = 0;
		Пока Индекс < ИзмеренияКолонки.Количество() Цикл
			
		ТекИзмерение = ИзмеренияКолонки.Получить(Индекс);
		НайденнаяСтрока = ДеревоДоступныхПолей.Строки.Найти(ТекИзмерение.ПутьКДанным,"ПутьКДанным", Истина);
		Если НайденнаяСтрока = Неопределено Тогда
			ИзмеренияКолонки.Удалить(ТекИзмерение);
			Продолжить;
		ИначеЕсли НЕ НайденнаяСтрока.Измерение Тогда
			ИзмеренияКолонки.Удалить(ТекИзмерение);
			Продолжить;
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	// Дополним текст запроса
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ТипыЦен.Ссылка КАК ТипЦен,
	|	ТипыЦен.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ТипыЦен.Ссылка = &ОсновнойТипЦенПродажи
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВключенПоУмолчанию
	|ИЗ
	|	Справочник.ТипыЦен КАК ТипыЦен
	|ГДЕ
	|	ТипыЦен.ДляАвтомобилей = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование УБЫВ");
	
	Запрос.УстановитьПараметр("ОсновнойТипЦенПродажи",обПраво("ОсновнойТипЦенПродажиАвтомобилей", глПрава));
		
	СписокТиповЦен = Новый СписокЗначений;
	ТаблицаПараметровТипаЦен = Новый ТаблицаЗначений;
	ТаблицаПараметровТипаЦен.Колонки.Добавить("ИмяТипаЦены");
	ТаблицаПараметровТипаЦен.Колонки.Добавить("ШаблонРасчетаЦены");
	ТаблицаПараметровТипаЦен.Колонки.Добавить("ИмяБазовогоТипаЦены");
	ТаблицаПараметровТипаЦен.Колонки.Добавить("БазовыйТипЦены");
	
	Выборка = Запрос.Выполнить();
	Выборка = Выборка.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РабочийТипЦен = Выборка.ТипЦен;
		ШаблонЦена = "$ИмяЦены$";
		Пока РабочийТипЦен.Рассчитывается Цикл
			Если РабочийТипЦен.ПроцентСкидкиНаценки<>0 Тогда
				ШаблонЦена = "("+ШаблонЦена+"+"+ШаблонЦена+"*"+Формат(РабочийТипЦен.ПроцентСкидкиНаценки/100, "ЧРД=.")+")";
			КонецЕсли;
			РабочийТипЦен = РабочийТипЦен.БазовыйТипЦен;
		КонецЦикла;
		
		ИмяБазовогоТипаЦены = "";
		ПараметрыТипа = СписокТиповЦен.НайтиПоЗначению(РабочийТипЦен);
		Если ПараметрыТипа = Неопределено Тогда
			ИмяБазовогоТипаЦены = "ТипЦен" + СтрЗаменить(РабочийТипЦен.УникальныйИдентификатор(),"-","_");
			СписокТиповЦен.Добавить(РабочийТипЦен, ИмяБазовогоТипаЦены);
		Иначе
			ИмяБазовогоТипаЦены = ПараметрыТипа.Представление;
		КонецЕсли;
		
		ИмяПоля = "ТипЦен"+СтрЗаменить(Выборка.ТипЦен.УникальныйИдентификатор(),"-","_");
		ПредставлениеПоля	= Выборка.Наименование;
		отДобавитьФункциюПоказатель(Показатели, ИмяПоля, ПредставлениеПоля, Выборка.ВключенПоУмолчанию, "ЧЦ=15; ЧДЦ=2");
		
		ПостроительОтчета.Параметры.Вставить(ИмяБазовогоТипаЦены, РабочийТипЦен);

		НовыйПараметр = ТаблицаПараметровТипаЦен.Добавить();
		НовыйПараметр.ИмяТипаЦены = ИмяПоля;
		НовыйПараметр.ШаблонРасчетаЦены = ШаблонЦена;
		НовыйПараметр.ИмяБазовогоТипаЦены = ИмяБазовогоТипаЦены;
		НовыйПараметр.БазовыйТипЦены = РабочийТипЦен;
		
	КонецЦикла;

КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	НеНайденВариантКомплектации = Истина;
	
	Для Каждого ТекИзмерение Из ИзмеренияСтроки Цикл
		Если НЕ ТекИзмерение.Использование Тогда Продолжить; КонецЕсли;
		Если Найти(ТекИзмерение.ПутьКДанным, "ВариантКомплектации")> 0 Тогда
			НеНайденВариантКомплектации = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ВариантВыводаОтчета=0 Тогда
		ТекстЗапроса = ПолучитьМакет("ТекстЗапросаАвтомобили").ПолучитьТекст();
	ИначеЕсли ВариантВыводаОтчета=1 Тогда
		ТекстЗапроса = ПолучитьМакет("ТекстЗапросаМодели").ПолучитьТекст();
		Показатели.Найти("Заказано").Использование = Ложь;
		Показатели.Найти("ВНаличии").Использование = Ложь;
		
		ЗаполнитьПараметрыИспользованияПолейИПоказателейМодели(ПараметрыИспользованияПолейИПоказателей.Модель, НеНайденВариантКомплектации);
		ЗаполнитьПараметрыИспользованияПолейИПоказателейМодели(ПараметрыИспользованияПолейИПоказателей.ОбщийИтог, НеНайденВариантКомплектации);
		
	ИначеЕсли ВариантВыводаОтчета=2 Тогда
		ТекстЗапроса = ПолучитьМакет("ТекстЗапросаАвтомобилейИМоделей").ПолучитьТекст();
	Иначе
		ТекстЗапроса = ПолучитьМакет("ТекстЗапросаАвтомобили").ПолучитьТекст();
	КонецЕсли;
	
	ОбъединенныйЗапросМоделейШаблонИтоги = "СУММА($Имя$) КАК $Имя$//ЦЕНЫ ИТОГИ ОбъединенныйЗапросМоделей$";
	
	ВложенныйЗапросЦенПоСправочникамШаблонВыбрать = ",
	|ОбъединенныйЗапросАвтомобилеИМоделей.$Имя$ КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ОбъединенныйЗапросАвтомобилеИМоделей$";
	
	ВложенныйЗапросЦенПоСправочникамШаблонИтоги = ",
	|СУММА($Имя$) КАК $Имя$//ЦЕНЫ ИТОГИ ОбъединенныйЗапросАвтомобилеИМоделей$";
	
	ЦеныАвтомобилейШаблонВыбрать = ",
	|(СУММА(ЦеныАвтомобилей.$Имя$)*СУММА(ЦеныАвтомобилей.Количество))+ЕСТЬNULL(СУММА(ЦеныКомплектов.$Имя$),0) КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилей$";
	
	//ЦеныАвтомобилейПоТипамШаблонВыбрать = ",
	//|	ЕСТЬNULL(ЦеныАвтомобилейПоТипам.$Имя$, ЕСТЬNULL(ЦеныМоделейПоВариантуКомплектации.$Имя$, ЕСТЬNULL(ЦеныМоделей.$Имя$, 0)))+ЕСТЬNULL(ЦеныКомплектаций.$Имя$,0) КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейПоТипам$";
	
	ЦеныАвтомобилейПоТипамШаблонВыбрать = ",
	|ВЫБОР
	|	КОГДА НЕ ЦеныАвтомобилейПоТипам.$Имя$ ЕСТЬ NULL И ЦеныАвтомобилейПоТипам.$Имя$>0 ТОГДА
	|		ЦеныАвтомобилейПоТипам.$Имя$
	|	КОГДА НЕ ЦеныМоделейПоВариантуКомплектации.$Имя$ ЕСТЬ NULL И ЦеныМоделейПоВариантуКомплектации.$Имя$>0 ТОГДА
	|		ЦеныМоделейПоВариантуКомплектации.$Имя$
	|	КОГДА НЕ ЦеныМоделей.$Имя$ ЕСТЬ NULL И ЦеныМоделей.$Имя$>0 ТОГДА
	|		ЦеныМоделей.$Имя$
	|КОНЕЦ + ЕСТЬNULL(ЦеныКомплектаций.$Имя$, 0) КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейПоТипам$";
	
	ЦеныАвтомобилейСрезПоследнихШаблонВыбрать = ",
	| СУММА(ВЫБОР
	|		КОГДА ЦеныАвтомобилейСрезПоследних.ТипЦен = &$БазовыйТипЦен$ ТОГДА
	|			$ШаблонЦена$ * КурсыВалютСрезПоследних.Коэффициент
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейСрезПоследних$";
	
	ЦеныКомплектовШаблонВыбрать = ",
	|	СУММА((ЕСТЬNULL(ЦеныНоменклатуры.$Имя$, 0)+ЕСТЬNULL(ЦеныОпций.$Имя$, 0))*КомплектацияАвтомобилейОстатки.КоличествоОстаток) КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныКомплектов$";
	
	ЦеныАвтомобилей2ШаблонВыбрать = ",
	|ЦеныМоделей.$Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилей2$";
	
	ЦеныНоменклатурыШаблонВыбрать = ",
	|МАКСИМУМ(ВЫБОР
	|	КОГДА ЦеныНоменклатуры.ТипЦен = &$БазовыйТипЦен$ ТОГДА
	|		$ШаблонЦена$ * КурсыВалютСрезПоследних.Коэффициент
	|	ИНАЧЕ 0
	|КОНЕЦ) КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныНоменклатуры$";
	
	ЦеныОпцийШаблонВыбрать = ",
	|МАКСИМУМ(ВЫБОР
	|	КОГДА ЦеныНоменклатуры.ТипЦен = &$БазовыйТипЦен$ ТОГДА
	|		$ШаблонЦена$ * КурсыВалютСрезПоследних.Коэффициент
	|	ИНАЧЕ 0
	|КОНЕЦ) КАК $Имя$ //ЦЕНЫ ВЫБРАТЬ ЦеныОпций$";

	МассивТиповЦен = Новый Массив;
	Для Каждого ТекПоказатель из Показатели Цикл
		Если (не ТекПоказатель.Использование) ИЛИ (ТекПоказатель.Имя="Количество") Тогда Продолжить; КонецЕсли;
		
		ТекущиеПараметры = ТаблицаПараметровТипаЦен.Найти(ТекПоказатель.Имя, "ИмяТипаЦены");
		Если ТекущиеПараметры=Неопределено Тогда Продолжить; КонецЕсли;
		
		Если МассивТиповЦен.Найти(ТекущиеПараметры.БазовыйТипЦены)=Неопределено Тогда
			МассивТиповЦен.Добавить(ТекущиеПараметры.БазовыйТипЦены);
		КонецЕсли;
		
		ТекЦеныАвтомобилейСрезПоследнихШаблонВыбрать = СтрЗаменить(ЦеныАвтомобилейСрезПоследнихШаблонВыбрать,"$ШаблонЦена$", СтрЗаменить(ТекущиеПараметры.ШаблонРасчетаЦены, "$ИмяЦены$", "ЦеныАвтомобилейСрезПоследних.Цена"));
		ТекЦеныАвтомобилейСрезПоследнихШаблонВыбрать = СтрЗаменить(ТекЦеныАвтомобилейСрезПоследнихШаблонВыбрать,"$БазовыйТипЦен$", ТекущиеПараметры.ИмяБазовогоТипаЦены);
		
		ТекЦеныНоменклатурыШаблонВыбрать = СтрЗаменить(ЦеныНоменклатурыШаблонВыбрать,"$ШаблонЦена$", СтрЗаменить(ТекущиеПараметры.ШаблонРасчетаЦены, "$ИмяЦены$", "ЦеныНоменклатуры.Цена"));
		ТекЦеныНоменклатурыШаблонВыбрать = СтрЗаменить(ТекЦеныНоменклатурыШаблонВыбрать,"$БазовыйТипЦен$", ТекущиеПараметры.ИмяБазовогоТипаЦены);
		
		ТекЦеныОпцийШаблонВыбрать = СтрЗаменить(ЦеныОпцийШаблонВыбрать,"$ШаблонЦена$", СтрЗаменить(ТекущиеПараметры.ШаблонРасчетаЦены, "$ИмяЦены$", "ЦеныНоменклатуры.Цена"));
		ТекЦеныОпцийШаблонВыбрать = СтрЗаменить(ТекЦеныОпцийШаблонВыбрать,"$БазовыйТипЦен$", ТекущиеПараметры.ИмяБазовогоТипаЦены);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ОбъединенныйЗапросАвтомобилеИМоделей$"	,ВложенныйЗапросЦенПоСправочникамШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ИТОГИ ОбъединенныйЗапросАвтомобилеИМоделей$"	,ВложенныйЗапросЦенПоСправочникамШаблонИтоги);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ИТОГИ ОбъединенныйЗапросМоделей$"			,ВложенныйЗапросЦенПоСправочникамШаблонИтоги);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилей$"					,ЦеныАвтомобилейШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ЦеныКомплектов$"					,ЦеныКомплектовШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилей2$"					,ЦеныАвтомобилей2ШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейПоТипам$"			,ЦеныАвтомобилейПоТипамШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейСрезПоследних$"		,ТекЦеныАвтомобилейСрезПоследнихШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ЦеныНоменклатуры$"					,ТекЦеныНоменклатурыШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЦЕНЫ ВЫБРАТЬ ЦеныОпций$"							,ТекЦеныОпцийШаблонВыбрать);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"$Имя$"												,ТекПоказатель.Имя);	
		
	КонецЦикла;
	
	ПостроительОтчета.Текст = ТекстЗапроса;
	ПостроительОтчета.Параметры.Вставить("СписокТиповЦен", МассивТиповЦен);
	УстановитьОсновныеПараметры(НеНайденВариантКомплектации);
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат);
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает структуру форматирования полей
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

// Формирует список допустимых значений вида сравнения
Функция ПолучитьСписокВидовСравнения(ТекТипЗначения) Экспорт
	
	ВидыСравнения = Новый ТаблицаЗначений;
	ВидыСравнения.Колонки.Добавить("ВидСравнения");
	ВидыСравнения.Колонки.Добавить("ЧислоВида");
	
	Для Каждого ТекТип Из ТекТипЗначения.Типы() Цикл
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.Равно;
		ТекСтрока.ЧислоВида    = 1;
		
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.НеРавно;
		ТекСтрока.ЧислоВида    = 1;
		
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.ВСписке;
		ТекСтрока.ЧислоВида    = 1;
		
		ТекСтрока = ВидыСравнения.Добавить();
		ТекСтрока.ВидСравнения = ВидСравнения.НеВСписке;
		ТекСтрока.ЧислоВида    = 1;
		
		Если Справочники.ТипВсеСсылки().СодержитТип(ТекТип) И Метаданные.НайтиПоТипу(ТекТип).Иерархический Тогда
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ВСпискеПоИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.НеВСпискеПоИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ВИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.НеВИерархии;
			ТекСтрока.ЧислоВида    = 1;
			
		ИначеЕсли ТекТип = Тип("Число") или ТекТип = Тип("Строка") или ТекТип = Тип("Дата") Тогда
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Больше;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.БольшеИлиРавно;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Меньше;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.МеньшеИлиРавно;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Интервал;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ИнтервалВключаяНачало;
			ТекСтрока.ЧислоВида    = 1;
			
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.ИнтервалВключаяОкончание;
			ТекСтрока.ЧислоВида    = 1;
		КонецЕсли;
		
		Если ТекТип = Тип("Строка") Тогда
			ТекСтрока = ВидыСравнения.Добавить();
			ТекСтрока.ВидСравнения = ВидСравнения.Содержит;
			ТекСтрока.ЧислоВида    = 1;
		КонецЕсли;
	КонецЦикла;
	
	ВидыСравнения.Свернуть("ВидСравнения", "ЧислоВида");
	
	КоличествоТипов = ТекТипЗначения.Типы().Количество();
	
	СписокВидовСравнения = Новый СписокЗначений;
	Для Каждого ТекСтрока Из ВидыСравнения Цикл
		Если ТекСтрока.ЧислоВида = КоличествоТипов Тогда
			СписокВидовСравнения.Добавить(ТекСтрока.ВидСравнения);
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат СписокВидовСравнения;
	
Конецфункции // ПолучитьСписокВидовСравнения()

// Обработчик изменения вида сравнения панели быстрых отборов
// управляет видимостью элементов управления 
Процедура ВидСравненияОтборПриИзменении(ЭтаФорма, Элемент)
	
	ИмяОтбора = Сред(Элемент.Имя, Найти(Элемент.Имя, "ВидСравнения") + СтрДлина("ВидСравнения"));
	
	// Управление полями фильтра в зависимости от вида сравнения
	Если Элемент.Значение = ВидСравнения.Интервал или
		Элемент.Значение = ВидСравнения.ИнтервалВключаяГраницы или
		Элемент.Значение = ВидСравнения.ИнтервалВключаяНачало или
		Элемент.Значение = ВидСравнения.ИнтервалВключаяОкончание Тогда
		
		ЭтаФорма.ЭлементыФормы["Значение" + ИмяОтбора].Видимость = Ложь;
		ЭтаФорма.ЭлементыФормы["тДатаНачала" + ИмяОтбора].Видимость = Истина;
		ЭтаФорма.ЭлементыФормы["ДатаНачала" + ИмяОтбора].Видимость = Истина;
		ЭтаФорма.ЭлементыФормы["тДатаКонца" + ИмяОтбора].Видимость = Истина;
		ЭтаФорма.ЭлементыФормы["ДатаКонца" + ИмяОтбора].Видимость = Истина;
		
	Иначе
		ЭлементФильтра = ЭтаФорма.ЭлементыФормы["Значение" + ИмяОтбора];
		ЭлементФильтра.Видимость = Истина;
		Если Элемент.Значение = ВидСравнения.ВСписке или
			Элемент.Значение = ВидСравнения.НеВСписке или
			Элемент.Значение = ВидСравнения.ВСпискеПоИерархии или
			Элемент.Значение = ВидСравнения.НеВСпискеПоИерархии Тогда
			ЭлементФильтра.ОграничениеТипа = Новый ОписаниеТипов("СписокЗначений");
			
		Иначе
			ЭлементФильтра.ОграничениеТипа = Новый ОписаниеТипов(ЭлементФильтра.ТипЗначения,, "СписокЗначений");
		КонецЕсли;
		
		ЭтаФорма.ЭлементыФормы["тДатаНачала" + ИмяОтбора].Видимость = Ложь;
		ЭтаФорма.ЭлементыФормы["ДатаНачала" + ИмяОтбора].Видимость = Ложь;
		ЭтаФорма.ЭлементыФормы["тДатаКонца" + ИмяОтбора].Видимость = Ложь;
		ЭтаФорма.ЭлементыФормы["ДатаКонца" + ИмяОтбора].Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ТабличныйДокументОбработкаРасшифровки(ЭтаФорма, Элемент, Расшифровка, СтандартнаяОбработка) Экспорт
	
	НомерСтроки            = Элемент.ТекущаяОбласть.Низ;
	РасшифровкаГруппировки = Элемент.Область(НомерСтроки, 1, НомерСтроки, 1).Расшифровка;
	ПараметрыГруппировки   = Элемент.Область(НомерСтроки, 2, НомерСтроки, 2).Расшифровка;
	ИмяГруппировки         = Неопределено;
	ЗначениеГруппировки    = Неопределено;
	ИмяПоля                = Неопределено;
	ЗначениеПоля           = Неопределено;
	ПутьТекущейРасшифровки = Неопределено;
	ЭтоНовыйСтиль          = Ложь;
	
	ЭлементыФормы = ЭтаФорма.ЭлементыФормы;
	ОтчетОбъект   = ЭтаФорма.ОтчетОбъект;
	
	Если ТипЗнч(РасшифровкаГруппировки) = Тип("Структура") И ТипЗнч(ПараметрыГруппировки ) = Тип("Структура") Тогда
		
		ИмяГруппировки         = ПараметрыГруппировки.ИмяГруппировки;
		ЗначениеГруппировки    = Элемент.Область(НомерСтроки, ПараметрыГруппировки.Колонка, НомерСтроки, ПараметрыГруппировки.Колонка).Расшифровка;
		// если текущая группировка это детальная запись, то она не попадет в структуру расшифровок
		// добавим ее в расшифровку.
		Если ПараметрыГруппировки.ДетальнаяЗапись Тогда
			РасшифровкаГруппировки.Вставить(ПараметрыГруппировки.ИмяГруппировки, ЗначениеГруппировки);
		КонецЕсли;
		
		РасшифровкаШапки = Элемент.Область(Элемент.ФиксацияСверху, Элемент.ТекущаяОбласть.Лево).Расшифровка;
		ЭтоГруппировка = (РасшифровкаШапки = Неопределено);
		Если ЭтоГруппировка Тогда
			ПутьТекущейРасшифровки = ПараметрыГруппировки.ПутьКДанным;
			ИмяПоля                = ИмяГруппировки;
			ЗначениеПоля           = ЗначениеГруппировки;
		Иначе
			ПутьТекущейРасшифровки = РасшифровкаШапки.ПутьКДанным;
			ИмяПоля                = РасшифровкаШапки.ИмяПоля;
			ЗначениеПоля           = Расшифровка;
		КонецЕсли;
		
		ЭтоНовыйСтиль = Истина;
		
		Расшифровка                = РасшифровкаГруппировки;
		
	КонецЕсли;
		
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Расшифровка)) Тогда
		СтандартнаяОбработка=Ложь;
		Варианты=Новый СписокЗначений();
		Варианты.Добавить(1, "Открыть элемент");
		Варианты.Добавить(2, "Найти в справочнике");
		Если ТипЗнч(Расшифровка.Ссылка) = Тип("СправочникСсылка.Автомобили") Тогда
			Варианты.Добавить(12,"Ввести на основании");
		КонецЕсли;
		Выб = ЭтаФорма.ВыбратьИзМеню(Варианты);
		Если Выб = Неопределено Тогда
		Иначе
			Если Выб.Значение=1 Тогда
				СтандартнаяОбработка=Истина;
			ИначеЕсли Выб.Значение=2 Тогда
				СписокДокументов=Справочники[Расшифровка.Метаданные().Имя].ПолучитьФормуСписка();
				СписокДокументов.ПараметрТекущаяСтрока=Расшифровка;
				СписокДокументов.Открыть();
			ИначеЕсли Выб.Значение=12 Тогда
				МетаданныеРасшифровки = Расшифровка.Метаданные();
				СписокПодчиненных = Новый СписокЗначений();
				//Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
				//	Если МетаданныеДокумента.ВводитсяНаОсновании.Содержит(МетаданныеРасшифровки) Тогда
				//		СписокПодчиненных.Добавить(МетаданныеДокумента.Имя, МетаданныеДокумента.Синоним);
				//	КонецЕсли;
				//КонецЦикла;
				СписокПодчиненных.Добавить("ЗаказНаАвтомобиль",         "Заказ на автомобиль");
				СписокПодчиненных.Добавить("РезервированиеАвтомобилей", "Резервирование автомобилей");
					
				Подчиненный = ЭтаФорма.ВыбратьИзМеню(СписокПодчиненных);
				Если НЕ Подчиненный = Неопределено Тогда
					//НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
					НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
					НовыйДокумент.Заполнить(Расшифровка.Ссылка);
					ФормаДокумента = НовыйДокумент.ПолучитьФорму();
					ФормаДокумента.Открыть();
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		
		СтандартнаяОбработка = Ложь;
		Попытка
			// Это расшифровка по выбранной функции или показателю
			Если Расшифровка.Свойство("ПутьКДанным") И Расшифровка.Свойство("ИмяПоля") И (Расшифровка.Свойство("Показатель") ИЛИ Расшифровка.Свойство("Функция")) Тогда
				
				ФормаРасшифровки = Отчеты[ОтчетОбъект.Метаданные().Имя].ПолучитьФорму(ОтчетОбъект.ИмяФормыНастроек,);
				Для Каждого ТекРеквизит Из ОтчетОбъект.Метаданные().Реквизиты Цикл
					Если ТекРеквизит.Имя = "ПостроительОтчета" Тогда Продолжить; КонецЕсли;
					ФормаРасшифровки[ТекРеквизит.Имя] = ОтчетОбъект[ТекРеквизит.Имя];
				Конеццикла;
				
				РасшифровкаПоКолонкам = Элемент.Область(Элемент.ВысотаТаблицы-1, Элемент.ТекущаяОбласть.Лево, Элемент.ВысотаТаблицы-1, Элемент.ТекущаяОбласть.Лево).Расшифровка;
				
				РасшифровкаОтчета = Новый Структура;
				Если НЕ РасшифровкаПоКолонкам = Неопределено Тогда
					Для Каждого ТекЭлемент Из РасшифровкаПоКолонкам Цикл
						РасшифровкаОтчета.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
					КонецЦикла;
				КонецЕсли;
				
				ФормаРасшифровки.ЗаполнитьНачальныеНастройки(ФормаРасшифровки.ИмяМакета);
				ФормаРасшифровки.ИзмеренияСтроки.Загрузить(ОтчетОбъект.ИзмеренияСтроки.Выгрузить());
				ФормаРасшифровки.ИзмеренияКолонки.Очистить();
				ФормаРасшифровки.ПостроительОтчета.УстановитьНастройки(ОтчетОбъект.ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Ложь), Истина, Ложь, Ложь, Ложь, Ложь);
				ФормаРасшифровки.ПостроительОтчета.НастроитьРасшифровку(ФормаРасшифровки.ПостроительОтчета, РасшифровкаОтчета);
				ФормаРасшифровки.ЗаполнитьНастройки = Ложь;
				
				Попытка // некоторые отчеты могут не содержать в заголовке таблицы расшифровки показателя
					Попытка
						Для Каждого ТекФункция Из ФормаРасшифровки.ДеревоПоказателей.Строки Цикл
							Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
								ТекПоказатель.Использование = ТекПоказатель.ПутьКДанным = Расшифровка.ПутьКДанным;
							КонецЦикла;
						КонецЦикла;
					Исключение
						
						Если Расшифровка.Свойство("Показатель") Тогда
							Для Каждого ТекПоказатель Из ФормаРасшифровки.Показатели Цикл
								ТекПоказатель.Использование = (Расшифровка.Показатель.Имя = ТекПоказатель.Имя);
							КонецЦикла;
						Иначе
							ФормаРасшифровки.Показатели.Загрузить(ОтчетОбъект.Показатели.Выгрузить());
						КонецЕсли;
						
						ИмяФункции = "";
						Если Расшифровка.Свойство("Функция") Тогда
							ИмяФункции = Расшифровка.Функция.Имя;
						КонецЕсли;
						
						Для Каждого ТекФункция Из ФормаРасшифровки.Функции Цикл
							ТекФункция.Использование = (ТекФункция.Имя = ИмяФункции);
						КонецЦикла;
						
					КонецПопытки;
				Исключение КонецПопытки;
				
				к = 0;
				Пока к <= ФормаРасшифровки.ПостроительОтчета.Отбор.Количество() - 1 Цикл
					Если НЕ ФормаРасшифровки.ПостроительОтчета.Отбор[к].Использование Тогда
						ФормаРасшифровки.ПостроительОтчета.Отбор.Удалить(к);
					Иначе
						Попытка
							Если ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.ВИерархии И 
								обЗначениеНеЗаполнено(ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение) Тогда
								ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.Равно;
							ИначеЕсли ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение.ЭтоГруппа Тогда
								ВыбраннаяСтрока = ФормаРасшифровки.ПостроительОтчета.Отбор[к];
								СтрокаПоиска = ФормаРасшифровки.ДеревоДоступныхПолей.Строки.Найти(ВыбраннаяСтрока.ПутьКДанным+".Родитель","ПутьКДанным", Истина);
								Если СтрокаПоиска = Неопределено Тогда 
									к = к + 1;
									Продолжить; 
								КонецЕсли;
								ПутьКДанным = СтрокаПоиска.ПутьКДанным;
								Представление = СтрокаПоиска.Представление;
								Использование = ВыбраннаяСтрока.Использование;
								Значение = ВыбраннаяСтрока.Значение;
								Отборы = ФормаРасшифровки.ПостроительОтчета.Отбор;
								ТекИндекс = Отборы.Индекс(ВыбраннаяСтрока);
								Отборы.Удалить(ТекИндекс);
								
								Попытка
									ТекОтбор = Отборы.Добавить(ПутьКДанным);
									ТекОтбор.Представление = Представление;
									ТекОтбор.Использование = Использование;
									ТекОтбор.ВидСравнения = ВидСравнения.ВИерархии;;
									ТекОтбор.Значение = Значение;
								Исключение 
								КонецПопытки;
								Если Отборы.Количество() - 1 - ТекИндекс > 0 Тогда
									Отборы.Сдвинуть(Отборы.Количество() - 1, -(Отборы.Количество() - 1 - ТекИндекс));
								КонецЕсли;
							КонецЕсли;
						Исключение
						КонецПопытки;
						к = к + 1;
					КонецЕсли;
				КонецЦикла;
				
				отФорматированиеПостроителяОтчета(ФормаРасшифровки);
				отДействияФормыСформировать(ФормаРасшифровки, ФормаРасшифровки.ЭлементыФормы.ДействияФормы.Кнопки.Сформировать);
				
			Иначе
				РасшифровкаОтчета = Новый Структура();
				// Добавим расшифровку строки/колонки
				Попытка // если есть искуственно сделанная иерархия, то она описана в ТаблицаРасшифровокИерархии
					ТаблицаРасшифровокИерархии = ОтчетОбъект.ТаблицаРасшифровокИерархии;
					Для Каждого ТекЭлемент Из Расшифровка Цикл
						ОписаниеРасшифровки = ТаблицаРасшифровокИерархии.Найти(ТекЭлемент.Ключ, "ИмяИерархии");
						ИмяКлюча = ?(ОписаниеРасшифровки=Неопределено, ТекЭлемент.Ключ, ОписаниеРасшифровки.ИмяИзмерения);
						Если Не РасшифровкаОтчета.Свойство(ИмяКлюча) Тогда
							РасшифровкаОтчета.Вставить(ИмяКлюча, ТекЭлемент.Значение);
						КонецЕсли;
					КонецЦикла;
				Исключение
					Для Каждого ТекЭлемент Из Расшифровка Цикл
						РасшифровкаОтчета.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
					КонецЦикла;
				КонецПопытки;
				
				ОтборДетализации = Новый Структура();
				Для Каждого ТекСтрока Из ОтчетОбъект.ИзмеренияСтроки Цикл
					Если не ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
					ПозицияТочки = Найти(ТекСтрока.ПутьКДанным,".");
					ТекПутьКДанным = ?(ПозицияТочки>0, Лев(ТекСтрока.ПутьКДанным, ПозицияТочки-1), ТекСтрока.ПутьКДанным);
					ОтборДетализации.Вставить(ТекПутьКДанным);
				КонецЦикла;
				
				Для Каждого ТекКолонка Из ОтчетОбъект.ИзмеренияКолонки Цикл
					Если не ТекКолонка.Использование Тогда Продолжить; КонецЕсли;
					ПозицияТочки = Найти(ТекКолонка.ПутьКДанным,".");
					ТекПутьКДанным = ?(ПозицияТочки>0, Лев(ТекКолонка.ПутьКДанным, ПозицияТочки-1), ТекКолонка.ПутьКДанным);
					ОтборДетализации.Вставить(ТекПутьКДанным);
				КонецЦикла;
				
				// Инициализируем список выбора варианта детализации расшифровки
				ВыбДетализацияРасшифровки = "";
				ДетализацияРасшифровки = Новый СписокЗначений();
				
				ЗначениеТекущейГруппировки = Неопределено;
				ТекущийОтбор = Неопределено;
				МетаданныеОтчета = ОтчетОбъект.Метаданные();
				МетаданныеРасшифровки = Неопределено;
				ИмяМетаданныхОбъекта = Неопределено;
				Если ЭтоНовыйСтиль Тогда
					ЗначениеТекущейГруппировки = ЗначениеПоля;
				Иначе
					Попытка
						Если Элемент.Область(Элемент.ФиксацияСверху, Элемент.ТекущаяОбласть.Лево).Расшифровка = Неопределено Тогда
							МассивВключенныхСтрок  = ОтчетОбъект.ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование", Истина));
							ПутьТекущейРасшифровки = МассивВключенныхСтрок[Мин(РасшифровкаОтчета.Количество()-1, МассивВключенныхСтрок.Количество()-1)].ПутьКДанным;
							ИмяТекущейГруппировки  = СтрЗаменить(ПутьТекущейРасшифровки, ".", "");
							Если РасшифровкаОтчета.Свойство(ИмяТекущейГруппировки) Тогда
								ЗначениеТекущейГруппировки = РасшифровкаОтчета[ИмяТекущейГруппировки];
							КонецЕсли;
						КонецЕсли;
					Исключение
					КонецПопытки;
				КонецЕсли;
				
				ТекущаяВетвь = ОтчетОбъект.ДеревоДоступныхПолей.Строки.Найти(ПутьТекущейРасшифровки, "ПутьКДанным", Истина);
				Если (НЕ ТекущаяВетвь = Неопределено) И ТекущаяВетвь.Отбор = Истина И МетаданныеОтчета.Реквизиты.Найти("ПостроительОтчета") <> Неопределено Тогда
					ДетализацияРасшифровки.Добавить(1, "Установить отбор");
					Для Каждого ТекСтрокаОтбора Из ПостроительОтчета.Отбор Цикл
						Если ТекСтрокаОтбора.ПутьКДанным = ПутьТекущейРасшифровки Тогда
							ТекущийОтбор = ТекСтрокаОтбора;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если ТекущийОтбор<>Неопределено И Не обЗначениеНеЗаполнено(ТекущийОтбор.Значение) Тогда
						ДетализацияРасшифровки.Добавить(2, "Добавить в отбор");
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗначениеТекущейГруппировки) Тогда
					Попытка
						МетаданныеРасшифровки = ЗначениеТекущейГруппировки.Метаданные();
						ИмяОбъектаМетаданных = СтрЗаменить(МетаданныеРасшифровки.ПолноеИмя(), "."+МетаданныеРасшифровки.Имя,"");
						Если ИмяОбъектаМетаданных="Задача"
							ИЛИ ИмяОбъектаМетаданных="Справочник"
							ИЛИ ИмяОбъектаМетаданных="ПланСчетов"
							ИЛИ ИмяОбъектаМетаданных="БизнесПроцесс"
							ИЛИ ИмяОбъектаМетаданных="ПланВидовРасчета"
							ИЛИ ИмяОбъектаМетаданных="ПланВидовХарактеристик" Тогда
							ДетализацияРасшифровки.Добавить(3,"Открыть элемент");
							ДетализацияРасшифровки.Добавить(4,"Найти в списке");
							Если ТипЗнч(ЗначениеТекущейГруппировки) = Тип("СправочникСсылка.Автомобили") Тогда
								ДетализацияРасшифровки.Добавить(7,"Ввести на основании");
							КонецЕсли;
						КонецЕсли;
					Исключение
					КонецПопытки;
					
				КонецЕсли;
				
				Для Каждого ТекПоле Из ОтчетОбъект.ДеревоДоступныхПолей.Строки Цикл
					Если (ОтборДетализации.Свойство(ТекПоле.ПутьКДанным)) ИЛИ (НЕ ТекПоле.Измерение) ИЛИ (НЕ ТекПоле.Отбор) Тогда Продолжить; КонецЕсли;
					Попытка
						Если ОтчетОбъект.СтруктураСвязиСвойств.Свойство(ТекПоле.Имя) Тогда Продолжить; КонецЕсли;
					Исключение КонецПопытки;
					ТекЭлемент = ДетализацияРасшифровки.Добавить(ТекПоле, ТекПоле.Представление);
				КонецЦикла;
				Если ТипЗнч(ВыбДетализацияРасшифровки) = Тип("ЭлементСпискаЗначений") Тогда
					ДетализацияРасшифровки.Сдвинуть(ВыбДетализацияРасшифровки, ДетализацияРасшифровки.Количество() - ДетализацияРасшифровки.Индекс(ВыбДетализацияРасшифровки) - 1);
				КонецЕсли;
				
				// если необходимо, добавим возможность расшифровки по движениям с остатками
				ВыбДетализацияРасшифровки = ДетализацияРасшифровки.ВыбратьЭлемент("Выберите вариант расшифровки ...", ВыбДетализацияРасшифровки);
				Если ВыбДетализацияРасшифровки = Неопределено Тогда
					Возврат Истина;
				КонецЕсли;
				
				Если ВыбДетализацияРасшифровки.Значение=1 ИЛИ ВыбДетализацияРасшифровки.Значение=2 Тогда
					Отбор = ОтчетОбъект.ПостроительОтчета.Отбор;
					СчетчикОтбора = 0;
					КоличествоОтборов = Мин(Отбор.Количество() - 1, 2);
					ЕстьМестоДляОтбора = Ложь;
					Пока СчетчикОтбора <= КоличествоОтборов Цикл
						ПолеОтбора = Отбор[СчетчикОтбора];
						Если Не ПолеОтбора.Использование Тогда ЕстьМестоДляОтбора = Истина; Прервать; КонецЕсли;
						СчетчикОтбора = СчетчикОтбора+1;
					КонецЦикла;
					
					Если ТекущийОтбор=Неопределено Тогда
						ТекущийОтбор = Отбор.Добавить(ТекущаяВетвь.ПутьКДанным);
						Представление = ТекущаяВетвь.Представление;
						Если ТекущаяВетвь.Родитель <> Неопределено Тогда
							Если ТекущаяВетвь.Родитель.Имя = "Свойства" Тогда
								Представление = Представление + " (" + ТекущаяВетвь.Родитель.Родитель.Представление + ")";
							Иначе Представление = Представление + " (" + ТекущаяВетвь.Родитель.Представление + ")";
							КонецЕсли;
						КонецЕсли;
						ТекущийОтбор.Представление = Представление;
					КонецЕсли;
					
					ТекущийОтбор.Использование = Истина;
					Если ВыбДетализацияРасшифровки.Значение=1 Тогда
						ТекущийОтбор.ВидСравнения = ВидСравнения.Равно;
						ТекущийОтбор.Значение = ЗначениеТекущейГруппировки;
					Иначе
						Если ТипЗнч(ТекущийОтбор.Значение) = Тип("СписокЗначений") Тогда
							ТекущийОтбор.Значение.Добавить(ЗначениеТекущейГруппировки);
						Иначе
							СписокОтбора = Новый СписокЗначений;
							СписокОтбора.Добавить(ТекущийОтбор.Значение);
							СписокОтбора.Добавить(ЗначениеТекущейГруппировки);
							ТекущийОтбор.ВидСравнения = ВидСравнения.ВСписке;
							ТекущийОтбор.Значение = СписокОтбора;
						КонецЕсли;
					КонецЕсли;
					
					ИндексТекущегоОтбора = Отбор.Индекс(ТекущийОтбор);
					Если ЕстьМестоДляОтбора И ИндексТекущегоОтбора>=СчетчикОтбора Тогда
						Отбор.Сдвинуть(ИндексТекущегоОтбора, -(ИндексТекущегоОтбора-СчетчикОтбора));
					КонецЕсли;
					
					ВысотаПанели = 5;
					Для СчетчикОтбора = 0 По КоличествоОтборов Цикл
						ЭлементыФормы["ПанельФильтра" + (СчетчикОтбора+1)].Свертка = РежимСверткиЭлементаУправления.Нет;	
						ПолеОтбора = Отбор[СчетчикОтбора];
						ПутьОтбора = "ПостроительОтчета.Отбор." + ПолеОтбора.Имя;
						ЭлементыФормы["ИспользованиеОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".Использование";
						ЭлементыФормы["ИспользованиеОтбор" + (СчетчикОтбора + 1)].Заголовок = ПолеОтбора.Представление + ":";
						ЭлементыФормы["ВидСравненияОтбор" + (СчетчикОтбора + 1)].СписокВыбора = ПолучитьСписокВидовСравнения(ПолеОтбора.ТипЗначения);
						ЭлементыФормы["ВидСравненияОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".ВидСравнения";
						ЭлементыФормы["ЗначениеОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".Значение";
						ЭлементыФормы["ДатаНачалаОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".ЗначениеС";
						ЭлементыФормы["ДатаКонцаОтбор" + (СчетчикОтбора + 1)].Данные = ПутьОтбора + ".ЗначениеПо";
						ВидСравненияОтборПриИзменении(ЭтаФорма, ЭлементыФормы["ВидСравненияОтбор" + (СчетчикОтбора + 1)]);
						ВысотаПанели = ВысотаПанели + 26;
					КонецЦикла;
					
					ЭлементыФормы.ДействияФормы.Кнопки.Отбор.Доступность = Истина;
					ЭлементыФормы.ПанельФильтра.Свертка = РежимСверткиЭлементаУправления.Нет;
					ЭлементыФормы.ПанельФильтра.Высота = ВысотаПанели;
					ЭлементыФормы.ДействияФормы.Кнопки.Отбор.Пометка = Истина;
					
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=3 Тогда
					ОткрытьЗначение(ЗначениеТекущейГруппировки);
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=4 Тогда
					ФормаСпискаРасшифровки = Неопределено;
					Если ИмяОбъектаМетаданных="Задача" Тогда
						ФормаСпискаРасшифровки=Задачи[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="Справочник" Тогда
						ФормаСпискаРасшифровки=Справочники[ЗначениеТекущейГруппировки.Метаданные().Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="ПланСчетов" Тогда
						ФормаСпискаРасшифровки=ПланыСчетов[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="БизнесПроцесс" Тогда
						ФормаСпискаРасшифровки=БизнесПроцессы[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="ПланВидовРасчета" Тогда
						ФормаСпискаРасшифровки=ПланыВидовРасчета[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="ПланВидовХарактеристик" Тогда
						ФормаСпискаРасшифровки=ПланыВидовХарактеристик[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					ИначеЕсли ИмяОбъектаМетаданных="Документ" Тогда
						ФормаСпискаРасшифровки=Документы[МетаданныеРасшифровки.Имя].ПолучитьФормуСписка();
					КонецЕсли;
					Если ФормаСпискаРасшифровки<>Неопределено Тогда
						ФормаСпискаРасшифровки.ПараметрТекущаяСтрока=ЗначениеТекущейГруппировки;
						ФормаСпискаРасшифровки.Открыть();
					КонецЕсли;
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=5 Тогда
					ПечатныеФормы = дкПолучитьСписокПечатныхФорм(ЗначениеТекущейГруппировки, МетаданныеРасшифровки.Имя, Ложь);
					СписокФорм = Новый СписокЗначений();
					
					Для Каждого ТекМакет Из ПечатныеФормы Цикл
						СписокФорм.Добавить(ТекМакет.Ключ, ТекМакет.Значение);
					КонецЦикла;
					
					// пользователь выбирает
					Форма = ЭтаФорма.ВыбратьИзМеню(СписокФорм);
					
					Если НЕ Форма=Неопределено Тогда
						СохранитьЗначение(СокрЛП(МетаданныеРасшифровки.Имя)+"ПечатнаяФорма",Форма.Представление);
						ЗначениеТекущейГруппировки.ПолучитьОбъект().Печать(Форма.Представление);
					КонецЕсли;
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=6 Тогда
					
					дкДействияФормыДеревоДокумента(ЭтаФорма, ЗначениеТекущейГруппировки);
					
				ИначеЕсли ВыбДетализацияРасшифровки.Значение=7 Тогда
					//Метаданные.КритерииОтбора.ПодчиненныеДокументы.
					СписокПодчиненных = Новый СписокЗначений();
					//Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
					//	Если МетаданныеДокумента.ВводитсяНаОсновании.Содержит(МетаданныеРасшифровки) Тогда
					//		СписокПодчиненных.Добавить(МетаданныеДокумента.Имя, МетаданныеДокумента.Синоним);
					//	КонецЕсли;
					//КонецЦикла;
					
					СписокПодчиненных.Добавить("ЗаказНаАвтомобиль",         "Заказ на автомобиль");
					СписокПодчиненных.Добавить("РезервированиеАвтомобилей", "Резервирование автомобилей");
					
					Подчиненный = ЭтаФорма.ВыбратьИзМеню(СписокПодчиненных);
					Если НЕ Подчиненный = Неопределено Тогда
						//НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
						НовыйДокумент = Документы[Подчиненный.Значение].СоздатьДокумент();
						НовыйДокумент.Заполнить(ЗначениеТекущейГруппировки);
						ФормаДокумента = НовыйДокумент.ПолучитьФорму();
						ФормаДокумента.Открыть();
					КонецЕсли;
				Иначе
					
					Если ВыбДетализацияРасшифровки.Представление = "Документ движения (с остатками)" Тогда
						ФормаРасшифровки = Отчеты[ЭтаФорма.ИмяОтчетаДвиженийСОстатками].ПолучитьФорму("НастройкиДвиженияСОстатками",);
						ФормаРасшифровки.ДатаНачала = ОтчетОбъект.ДатаНачала;
						ФормаРасшифровки.ДатаКонца = ОтчетОбъект.ДатаКонца;
						ФормаРасшифровки.ИспользоватьАвтоотступ = ОтчетОбъект.ИспользоватьАвтоотступ;
					Иначе
						ФормаРасшифровки = Отчеты[ОтчетОбъект.Метаданные().Имя].ПолучитьФорму(ОтчетОбъект.ИмяФормыНастроек,);
						Для Каждого ТекРеквизит Из ОтчетОбъект.Метаданные().Реквизиты Цикл
							Если ТекРеквизит.Имя = "ПостроительОтчета" Тогда Продолжить; КонецЕсли;
							ФормаРасшифровки[ТекРеквизит.Имя] = ОтчетОбъект[ТекРеквизит.Имя];
						Конеццикла;
					КонецЕсли;
					
					ФормаРасшифровки.ЗаполнитьНачальныеНастройки(ФормаРасшифровки.ИмяМакета);
					Попытка ФормаРасшифровки.ИзмеренияСтроки.Очистить(); Исключение КонецПопытки;
					
					ФормаРасшифровки.Показатели.Загрузить(ОтчетОбъект.Показатели.Выгрузить());
					ФормаРасшифровки.Функции.Загрузить(ОтчетОбъект.Функции.Выгрузить());
					
					Попытка
						ФормаРасшифровки.ДеревоПоказателей = ОтчетОбъект.ДеревоПоказателей.Скопировать();
					Исключение
					КонецПопытки;
					
					ФормаРасшифровки.ПостроительОтчета.УстановитьНастройки(ОтчетОбъект.ПостроительОтчета.ПолучитьНастройки(Истина, Ложь, Ложь, Ложь, Ложь), Истина, Ложь, Ложь, Ложь, Ложь);
					ФормаРасшифровки.ПостроительОтчета.НастроитьРасшифровку(ФормаРасшифровки.ПостроительОтчета, РасшифровкаОтчета);
					ФормаРасшифровки.ЗаполнитьНастройки = Ложь;
					
					Попытка // некоторые отчеты могут не содержать в заголовке таблицы расшифровки показателя
						РасшифровкаПоказателя = Элемент.Область(Элемент.ФиксацияСверху, Элемент.ТекущаяОбласть.Лево).Расшифровка.Показатель;
						Попытка
							Для Каждого ТекФункция Из ФормаРасшифровки.ДеревоПоказателей.Строки Цикл
								Для Каждого ТекПоказатель Из ТекФункция.Строки Цикл
									ТекПоказатель.Использование = ТекПоказатель.ПутьКДанным = РасшифровкаПоказателя.ИмяРесурса;
								КонецЦикла;
							КонецЦикла;
						Исключение
							Для Каждого ТекПоказатель Из ФормаРасшифровки.Показатели Цикл
								ТекПоказатель.Использование = ТекПоказатель.Имя = РасшифровкаПоказателя.Имя;
							КонецЦикла;
						КонецПопытки;
					Исключение КонецПопытки;	
					
					к = 0;
					Пока к <= ФормаРасшифровки.ПостроительОтчета.Отбор.Количество() - 1 Цикл
						Если НЕ ФормаРасшифровки.ПостроительОтчета.Отбор[к].Использование Тогда
							ФормаРасшифровки.ПостроительОтчета.Отбор.Удалить(к);
						Иначе
							Попытка
								Если ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.ВИерархии И 
									обЗначениеНеЗаполнено(ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение) Тогда
									ФормаРасшифровки.ПостроительОтчета.Отбор[к].ВидСравнения = ВидСравнения.Равно;
								ИначеЕсли ФормаРасшифровки.ПостроительОтчета.Отбор[к].Значение.ЭтоГруппа Тогда
									ВыбраннаяСтрока = ФормаРасшифровки.ПостроительОтчета.Отбор[к];
									СтрокаПоиска = ФормаРасшифровки.ДеревоДоступныхПолей.Строки.Найти(ВыбраннаяСтрока.ПутьКДанным+".Родитель","ПутьКДанным", Истина);
									Если СтрокаПоиска = Неопределено Тогда 
										к = к + 1;
										Продолжить; 
									КонецЕсли;
									ПутьКДанным = СтрокаПоиска.ПутьКДанным;
									Представление = СтрокаПоиска.Представление;
									Использование = ВыбраннаяСтрока.Использование;
									Значение = ВыбраннаяСтрока.Значение;
									Отборы = ФормаРасшифровки.ПостроительОтчета.Отбор;
									ТекИндекс = Отборы.Индекс(ВыбраннаяСтрока);
									Отборы.Удалить(ТекИндекс);
									
									Попытка
										ТекОтбор = Отборы.Добавить(ПутьКДанным);
										ТекОтбор.Представление = Представление;
										ТекОтбор.Использование = Использование;
										ТекОтбор.ВидСравнения = ВидСравнения.ВИерархии;;
										ТекОтбор.Значение = Значение;
									Исключение 
									КонецПопытки;
									Если Отборы.Количество() - 1 - ТекИндекс > 0 Тогда
										Отборы.Сдвинуть(Отборы.Количество() - 1, -(Отборы.Количество() - 1 - ТекИндекс));
									КонецЕсли;
								КонецЕсли;
							Исключение
							КонецПопытки;
							к = к + 1;
						КонецЕсли;
					КонецЦикла;
					
					ТекИзмерение = ФормаРасшифровки.ИзмеренияСтроки.Добавить();
					ТекИзмерение.Использование = Истина;
					ТекИзмерение.ПутьКДанным   = ВыбДетализацияРасшифровки.Значение.ПутьКДанным;
					ТекИзмерение.Представление = ВыбДетализацияРасшифровки.Значение.Представление;
					ТекИзмерение.ТипИзмерения  = ТипИзмеренияПостроителяОтчета.Элементы;
					Если ВыбДетализацияРасшифровки.Представление = "Документ движения (с остатками)" Тогда
						ТекИзмерение.Фиксированное = Истина;
					Иначе
						Если ФормаРасшифровки.ВидОтчета = Перечисления.ВидыОтчетов.ДвиженияСОстатками Тогда
							ТекИзмерение = ФормаРасшифровки.ИзмеренияСтроки.Добавить();
							ТекИзмерение.Использование = Истина;
							ТекИзмерение.ПутьКДанным   = "ПериодРегистратор";
							ТекИзмерение.Представление = "Документ движения";
							ТекИзмерение.ТипИзмерения  = "Элементы";
							ТекИзмерение.Фиксированное = Истина;
						Иначе
							Попытка
								ФормаРасшифровки.ИзмеренияКолонки.Загрузить(ОтчетОбъект.ИзмеренияКолонки.Выгрузить());
								НайденнаяКолонка = ФормаРасшифровки.ИзмеренияКолонки.Найти(ВыбДетализацияРасшифровки.Значение.ПутьКДанным, "ПутьКДанным");
								Если НайденнаяКолонка<>Неопределено Тогда
									ФормаРасшифровки.ИзмеренияКолонки.Удалить(НайденнаяКолонка);
								КонецЕсли;
								
							Исключение КонецПопытки;
						КонецЕсли;
					КонецЕсли;
					
					отФорматированиеПостроителяОтчета(ФормаРасшифровки);
					отДействияФормыСформировать(ФормаРасшифровки, ФормаРасшифровки.ЭлементыФормы.ДействияФормы.Кнопки.Сформировать);
					
				КонецЕсли;
			КонецЕсли;
		Исключение КонецПопытки;
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("Строка") ИЛИ ТипЗнч(Расшифровка) = Тип("Дата") ИЛИ ТипЗнч(Расшифровка) = Тип("Число") ИЛИ Расшифровка=NULL Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции//	ТабличныйДокументОбработкаРасшифровки()


//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
#КонецЕсли
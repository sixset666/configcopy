ВЫБРАТЬ
	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	ВЫБОР
		КОГДА КурсыВалютСрезПоследних.Кратность = 0 ТОГДА
			КурсыВалютСрезПоследних.Курс
		ИНАЧЕ
			КурсыВалютСрезПоследних.Курс/КурсыВалютСрезПоследних.Кратность
	КОНЕЦ / &КурсВалютыОтчета КАК Коэффициент
ПОМЕСТИТЬ
	КурсыВалютСрезПоследних
ИЗ
	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКон, ) КАК КурсыВалютСрезПоследних
;

ВЫБРАТЬ
	КомплектацияАвтомобилейОстатки.Автомобиль //ЦЕНЫ ВЫБРАТЬ ЦеныКомплектов$
ПОМЕСТИТЬ
	ЦеныКомплектаций
ИЗ
	РегистрНакопления.КомплектацияАвтомобилей.Остатки(&ДатаКон, {Автомобиль.* КАК Автомобиль}) КАК КомплектацияАвтомобилейОстатки
ЛЕВОЕ СОЕДИНЕНИЕ
	(ВЫБРАТЬ
		ЦеныНоменклатуры.Номенклатура КАК Номенклатура//ЦЕНЫ ВЫБРАТЬ ЦеныНоменклатуры$
	ИЗ
		РегистрСведений.Цены.СрезПоследних(&ДатаКон, Контрагент = &Контрагент И ТипЦен в (&СписокТиповЦен)) КАК ЦеныНоменклатуры
	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	ПО
		(ЦеныНоменклатуры.ТипЦен.ВВалютеУчета И ЦеныНоменклатуры.Номенклатура.ВалютаУчета = КурсыВалютСрезПоследних.Валюта ИЛИ
		(НЕ ЦеныНоменклатуры.ТипЦен.ВВалютеУчета) И ЦеныНоменклатуры.ТипЦен.ВалютаЦены = КурсыВалютСрезПоследних.Валюта)
	СГРУППИРОВАТЬ ПО
		ЦеныНоменклатуры.Номенклатура) КАК ЦеныНоменклатуры
ПО
	КомплектацияАвтомобилейОстатки.Номенклатура = ЦеныНоменклатуры.Номенклатура
ЛЕВОЕ СОЕДИНЕНИЕ
	(ВЫБРАТЬ
		ЦеныНоменклатуры.Опция,
		ЦеныНоменклатуры.Модель,
		ЦеныНоменклатуры.ВариантКомплектации//ЦЕНЫ ВЫБРАТЬ ЦеныОпций$
	ИЗ
		РегистрСведений.ЦеныОпций.СрезПоследних(&ДатаКон,ТипЦен в (&СписокТиповЦен)) КАК ЦеныНоменклатуры
	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	ПО
		(ЦеныНоменклатуры.ТипЦен.ВВалютеУчета И ЦеныНоменклатуры.Опция.ВалютаУчета = КурсыВалютСрезПоследних.Валюта ИЛИ
		(НЕ ЦеныНоменклатуры.ТипЦен.ВВалютеУчета) И ЦеныНоменклатуры.ТипЦен.ВалютаЦены = КурсыВалютСрезПоследних.Валюта)
	СГРУППИРОВАТЬ ПО
		ЦеныНоменклатуры.Опция,
		ЦеныНоменклатуры.Модель,
		ЦеныНоменклатуры.ВариантКомплектации) КАК ЦеныОпций
ПО
	КомплектацияАвтомобилейОстатки.Номенклатура                   = ЦеныОпций.Опция И 
	КомплектацияАвтомобилейОстатки.Автомобиль.Модель              = ЦеныОпций.Модель И 
	КомплектацияАвтомобилейОстатки.Автомобиль.ВариантКомплектации = ЦеныОпций.ВариантКомплектации
СГРУППИРОВАТЬ ПО
	КомплектацияАвтомобилейОстатки.Автомобиль
ИНДЕКСИРОВАТЬ ПО
	Автомобиль
;

ВЫБРАТЬ
	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	МАКСИМУМ(ТаблицаАвтомобилей.СкладКомпании) КАК СкладКомпании,
	МАКСИМУМ(ТаблицаАвтомобилей.Организация) КАК Организация,
	МАКСИМУМ(ТаблицаАвтомобилей.Подразделение) КАК Подразделение,
	СУММА(ТаблицаАвтомобилей.Заказано) КАК Заказано,
	СУММА(ТаблицаАвтомобилей.ВНаличии) КАК ВНаличии
{ВЫБРАТЬ
	СкладКомпании КАК СкладКомпании,
	Организация КАК Организация,
	Подразделение КАК Подразделение
}
ПОМЕСТИТЬ
	ТаблицаАвтомобилей
ИЗ
	(ВЫБРАТЬ
		СправочникАвтомобили.Ссылка КАК Автомобиль,
		ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка) КАК СкладКомпании,
		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка) КАК Подразделение,
		0 КАК Заказано,
		0 КАК ВНаличии,
		ВЫБОР
			КОГДА СправочникАвтомобили.ВключатьВПрайсЛист = ЗНАЧЕНИЕ(Перечисление.ВидВключенияВПрайсЛист.ВсегдаВключатьВПрайсЛист) ТОГДА
				1
			ИНАЧЕ
				0
		КОНЕЦ КАК ВключатьВПрайс
	{ВЫБРАТЬ
		СкладКомпании КАК СкладКомпании,
		Организация КАК Организация,
		Подразделение КАК Подразделение
	}
	ИЗ
		Справочник.Автомобили КАК СправочникАвтомобили
	ГДЕ
		СправочникАвтомобили.ЭтоГруппа = ЛОЖЬ И 
		НЕ СправочникАвтомобили.ВключатьВПрайсЛист = ЗНАЧЕНИЕ(Перечисление.ВидВключенияВПрайсЛист.ПоУмолчанию)
	{ГДЕ
		СправочникАвтомобили.Ссылка.* КАК Автомобиль
	}
		
	ОБЪЕДИНИТЬ ВСЕ

	ВЫБРАТЬ
		ОстаткиАвтомобилейОстатки.Автомобиль КАК Автомобиль,
		ОстаткиАвтомобилейОстатки.СкладКомпании,
		ОстаткиАвтомобилейОстатки.СкладКомпании.Организация,
		ОстаткиАвтомобилейОстатки.СкладКомпании.Подразделение,
		0,
		ОстаткиАвтомобилейОстатки.КоличествоОстаток,
		1
	{ВЫБРАТЬ
		СкладКомпании КАК СкладКомпании,
		Организация КАК Организация,
		Подразделение КАК Подразделение
	}
	ИЗ
		РегистрНакопления.ОстаткиАвтомобилей.Остатки(&ДатаКон, {Автомобиль.* КАК Автомобиль}) КАК ОстаткиАвтомобилейОстатки

	ОБЪЕДИНИТЬ ВСЕ

	ВЫБРАТЬ
		ЗаказыПоставщикамНаАвтомобилиОстатки.Автомобиль КАК Автомобиль,
		ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка),
		ЗаказыПоставщикамНаАвтомобилиОстатки.ЗаказПоставщику.Организация,
		ЗаказыПоставщикамНаАвтомобилиОстатки.ЗаказПоставщику.ПодразделениеКомпании,
		ЗаказыПоставщикамНаАвтомобилиОстатки.КоличествоОстаток,
		0,
		1
	{ВЫБРАТЬ
		СкладКомпании КАК СкладКомпании,
		Организация КАК Организация,
		Подразделение КАК Подразделение
	}
	ИЗ
		РегистрНакопления.ЗаказыПоставщикамНаАвтомобили.Остатки(&ДатаКон, {Автомобиль.* КАК Автомобиль}) КАК ЗаказыПоставщикамНаАвтомобилиОстатки
		
	ОБЪЕДИНИТЬ ВСЕ

	ВЫБРАТЬ
		ЗаказыАвтомобилейОстатки.Автомобиль КАК Автомобиль,
		ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка),
		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
		ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка),
		0,
		0,
		0
	{ВЫБРАТЬ
		СкладКомпании КАК СкладКомпании,
		Организация КАК Организация,
		Подразделение КАК Подразделение
	}
	ИЗ
		РегистрНакопления.ЗаказыАвтомобилей.Остатки(&ДатаКон, {Автомобиль.* КАК Автомобиль}) КАК ЗаказыАвтомобилейОстатки
	ГДЕ
		ЗаказыАвтомобилейОстатки.КоличествоОстаток > 0 ИЛИ ЗаказыАвтомобилейОстатки.РезервОстаток>0) КАК ТаблицаАвтомобилей
{ГДЕ
	СкладКомпании.* КАК СкладКомпании,
	Организация.* КАК Организация,
	Подразделение.* КАК Подразделение,
	Автомобиль.* КАК Автомобиль
	}
СГРУППИРОВАТЬ ПО
	ТаблицаАвтомобилей.Автомобиль
ИМЕЮЩИЕ
	МИНИМУМ(ТаблицаАвтомобилей.ВключатьВПрайс)>0
ИНДЕКСИРОВАТЬ ПО
	Автомобиль
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	ТаблицаАвтомобилей.СкладКомпании КАК СкладКомпании,
	ТаблицаАвтомобилей.Организация КАК Организация,
	ТаблицаАвтомобилей.Подразделение КАК Подразделение,
	ТаблицаАвтомобилей.Заказано КАК Заказано,
	ТаблицаАвтомобилей.ВНаличии КАК ВНаличии
	//ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейПоТипам$
{ВЫБРАТЬ
	СкладКомпании.* КАК СкладКомпании,
	Организация.* КАК Организация,
	Подразделение.* КАК Подразделение,
	Автомобиль.* КАК Автомобиль
}
ИЗ
	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
ЛЕВОЕ СОЕДИНЕНИЕ
	(ВЫБРАТЬ
		ЦеныАвтомобилейСрезПоследних.Автомобиль КАК Автомобиль //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейСрезПоследних$
	ИЗ
		РегистрСведений.ЦеныАвтомобилей.СрезПоследних(&ДатаКон, Автомобиль ССЫЛКА Справочник.Автомобили И ТипЦен в (&СписокТиповЦен) И Автомобиль В (ВЫБРАТЬ Автомобиль ИЗ ТаблицаАвтомобилей) ) КАК ЦеныАвтомобилейСрезПоследних
	ЛЕВОЕ СОЕДИНЕНИЕ
		КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	ПО
		(ЦеныАвтомобилейСрезПоследних.ТипЦен.ВВалютеУчета И ЦеныАвтомобилейСрезПоследних.Автомобиль.ВалютаУчета = КурсыВалютСрезПоследних.Валюта
		ИЛИ (НЕ ЦеныАвтомобилейСрезПоследних.ТипЦен.ВВалютеУчета) И ЦеныАвтомобилейСрезПоследних.ТипЦен.ВалютаЦены = КурсыВалютСрезПоследних.Валюта)
	СГРУППИРОВАТЬ ПО
		ЦеныАвтомобилейСрезПоследних.Автомобиль)КАК ЦеныАвтомобилейПоТипам
ПО
	ТаблицаАвтомобилей.Автомобиль = ЦеныАвтомобилейПоТипам.Автомобиль
ЛЕВОЕ СОЕДИНЕНИЕ
	(ВЫБРАТЬ
		ЦеныАвтомобилейСрезПоследних.Автомобиль КАК Модель,
		ЦеныАвтомобилейСрезПоследних.ВариантКомплектации КАК ВариантКомплектации //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейСрезПоследних$
	ИЗ
		РегистрСведений.ЦеныАвтомобилей.СрезПоследних(&ДатаКон, Автомобиль ССЫЛКА Справочник.Модели И (НЕ ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка)) И ТипЦен в (&СписокТиповЦен)) КАК ЦеныАвтомобилейСрезПоследних
	ЛЕВОЕ СОЕДИНЕНИЕ
		КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	ПО
		(ЦеныАвтомобилейСрезПоследних.ТипЦен.ВВалютеУчета И 
		ЦеныАвтомобилейСрезПоследних.Автомобиль.ВалютаУчета = КурсыВалютСрезПоследних.Валюта ИЛИ 
		(НЕ ЦеныАвтомобилейСрезПоследних.ТипЦен.ВВалютеУчета) И 
		ЦеныАвтомобилейСрезПоследних.ТипЦен.ВалютаЦены = КурсыВалютСрезПоследних.Валюта)
	СГРУППИРОВАТЬ ПО
		ЦеныАвтомобилейСрезПоследних.Автомобиль,
		ЦеныАвтомобилейСрезПоследних.ВариантКомплектации) КАК ЦеныМоделейПоВариантуКомплектации
ПО
	ТаблицаАвтомобилей.Автомобиль.Модель              = ЦеныМоделейПоВариантуКомплектации.Модель И 
	ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации = ЦеныМоделейПоВариантуКомплектации.ВариантКомплектации
ЛЕВОЕ СОЕДИНЕНИЕ
	(ВЫБРАТЬ
		ЦеныАвтомобилейСрезПоследних.Автомобиль КАК Модель,
		ЦеныАвтомобилейСрезПоследних.ВариантКомплектации КАК ВариантКомплектации //ЦЕНЫ ВЫБРАТЬ ЦеныАвтомобилейСрезПоследних$
	ИЗ
		РегистрСведений.ЦеныАвтомобилей.СрезПоследних(&ДатаКон, Автомобиль ССЫЛКА Справочник.Модели И ВариантКомплектации = ЗНАЧЕНИЕ(Справочник.ВариантыКомплектации.ПустаяСсылка) И ТипЦен в (&СписокТиповЦен)) КАК ЦеныАвтомобилейСрезПоследних
	ЛЕВОЕ СОЕДИНЕНИЕ
		КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	ПО
		(ЦеныАвтомобилейСрезПоследних.ТипЦен.ВВалютеУчета И 
		ЦеныАвтомобилейСрезПоследних.Автомобиль.ВалютаУчета = КурсыВалютСрезПоследних.Валюта ИЛИ 
		(НЕ ЦеныАвтомобилейСрезПоследних.ТипЦен.ВВалютеУчета) И 
		ЦеныАвтомобилейСрезПоследних.ТипЦен.ВалютаЦены = КурсыВалютСрезПоследних.Валюта)
	СГРУППИРОВАТЬ ПО
		ЦеныАвтомобилейСрезПоследних.Автомобиль,
		ЦеныАвтомобилейСрезПоследних.ВариантКомплектации) КАК ЦеныМоделей
ПО
	ТаблицаАвтомобилей.Автомобиль.Модель = ЦеныМоделей.Модель
	
ЛЕВОЕ СОЕДИНЕНИЕ
	ЦеныКомплектаций КАК ЦеныКомплектаций
ПО
	ТаблицаАвтомобилей.Автомобиль = ЦеныКомплектаций.Автомобиль

{УПОРЯДОЧИТЬ ПО
	СкладКомпании.* КАК СкладКомпании,
	Организация.* КАК Организация,
	Подразделение.* КАК Подразделение,
	Автомобиль.* КАК Автомобиль
	}
ИТОГИ
	СУММА(Заказано),
	СУММА(ВНаличии) //ЦЕНЫ ИТОГИ ОбъединенныйЗапросАвтомобилеИМоделей$
ПО
	ОБЩИЕ

{ИТОГИ ПО
	СкладКомпании.* КАК СкладКомпании,
	Организация.* КАК Организация,
	Подразделение.* КАК Подразделение,
	Автомобиль.* КАК Автомобиль
	}
АВТОУПОРЯДОЧИВАНИЕ

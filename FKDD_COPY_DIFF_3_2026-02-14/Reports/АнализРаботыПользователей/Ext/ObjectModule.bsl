#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем СписокДокументовПоУмолчинию Экспорт;             // хранит список документов по умолчанию


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция формирует и возвращает сутруктуру отборов и соеденение для запроса
Функция ПолучитьСтрокиТабЧастей(Документ) Экспорт
	СтрокаСоединения = "";
	СтрокаКоличество = "";
	ТекстСоединения  = "ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДокументИмя.ТабЧастьИмя КАК ИмяТЧ ПО ДокументИмя.Ссылка = ИмяТЧ.Ссылка"+Символы.ПС;
	ТекстКоличество  = "КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИмяТЧ.НомерСтроки)";
	Если Документ.ТабличныеЧасти.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого ТабЧасть Из Документ.ТабличныеЧасти Цикл
		ТекстВременный = СтрЗаменить(ТекстСоединения, "ДокументИмя", Документ.Имя);
		ТекстВременный = СтрЗаменить(ТекстВременный, "ИмяТЧ", Документ.Имя+ТабЧасть.Имя);
		ТекстВременный = СтрЗаменить(ТекстВременный, "ТабЧастьИмя", ТабЧасть.Имя);
		СтрокаСоединения = СтрокаСоединения + ТекстВременный;
		СтрокаКоличество = СтрокаКоличество+?(ПустаяСтрока(СтрокаКоличество),"","+") + СтрЗаменить(ТекстКоличество, "ИмяТЧ", Документ.Имя+ТабЧасть.Имя);
	КонецЦикла;
	СтрокаКоличество = СтрокаКоличество + " КАК КоличествоСтрокДокумента";
	СтруктураСтрок = Новый Структура;
	СтруктураСтрок.Вставить("СтрокаСоединение", СтрокаСоединения);
	СтруктураСтрок.Вставить("СтрокаКоличество", СтрокаКоличество);
	Возврат СтруктураСтрок;
КонецФункции

// Функция формирует и возвращает текст запроса отчета
Функция ПолучитьТекстЗапроса() Экспорт
	// подготовим флаги и переменные обработки для цикла
	ТекстЗапросаДинамич = ПолучитьМакет("ОболочкаЗапроса").ПолучитьТекст();
	ТекстВнутреннегоЗапроса = "";
	ТекстВременный = "";
	ТекстПервого = ПолучитьМакет("ПервыйВСписке").ПолучитьТекст();
	ТекстОстальных = ПолучитьМакет("СписокДокумент").ПолучитьТекст();
	ПроверятьНаличиеВСписке = (СписокДокументов.НайтиПоЗначению("ВсеДокументы") = Неопределено);
	Первый =Истина;
	ДелатьСвязь = (Показатели.НайтиСтроки(Новый Структура("Использование,Имя",Истина,"КоличествоСтрокДокумента")).Количество() > 0);
	
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		Если ПроверятьНаличиеВСписке Тогда
			Если СписокДокументов.НайтиПоЗначению(Документ.Имя) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если Первый Тогда
			ТекстВременный = ТекстПервого;
		Иначе
			ТекстВременный = ТекстОстальных;
		КонецЕсли;
		
		Если НЕ Первый Тогда
			ТекстВременный = "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + ТекстВременный;
		КонецЕсли;
		
		ТекстВременный = СтрЗаменить(ТекстВременный, "ДокументИмя", Документ.Имя);
		ТекстВременный = СтрЗаменить(ТекстВременный, "ДокументСиноним", """"+Документ.Синоним+"""");
		СтрокиТЧ = ПолучитьСтрокиТабЧастей(Документ);
		Если НЕ СтрокиТЧ = Неопределено И ДелатьСвязь Тогда
			ТекстВременный = СтрЗаменить(ТекстВременный, "//СТРОКА_КОЛИЧЕСТВО_СТРОК_ДОКУМЕНТА", СтрокиТЧ.СтрокаКоличество);
			ТекстВременный = СтрЗаменить(ТекстВременный, "//СОЕДИТЕНИЯ_С_ТАБЛИЦАМИ", СтрокиТЧ.СтрокаСоединение);
		Иначе
			ТекстВременный = СтрЗаменить(ТекстВременный, "//СТРОКА_КОЛИЧЕСТВО_СТРОК_ДОКУМЕНТА", "СУММА(0) КАК КоличествоСтрокДокумента");
		КонецЕсли;
		ТекстВнутреннегоЗапроса = ТекстВнутреннегоЗапроса + ТекстВременный;
		Первый = Ложь;
	КонецЦикла;
	ТекстЗапросаДинамич = СтрЗаменить(ТекстЗапросаДинамич, "//ТЕКСТВНУТРЕННЕГОЗАПРОСА", ТекстВнутреннегоЗапроса);
	Возврат ТекстЗапросаДинамич;
КонецФункции

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента – передает ссылку на табличный документ 
//						или поле табличного документа
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент 
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт

	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// формирует отчет в виде сводной таблицы
Функция СформироватьСводнуюТаблицу(ДокументРезультат=Неопределено) Экспорт
	
	отСформироватьСводнуюТаблицу(ЭтотОбъект, ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции

// формирует диаграмму
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
	
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров);
	
	Возврат ПолеДиаграммы;
	
КонецФункции

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ИмяРегистра = "";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СписокДокументовПоУмолчинию = Новый СписокЗначений;
СписокДокументовПоУмолчинию.Добавить("ВсеДокументы", "<Все документы>");
СписокДокументов = СписокДокументовПоУмолчинию;
#КонецЕсли
#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных полей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем СтруктураКИ Экспорт;                            // хранит структуру контактной информации владельца

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция формирует и возвращает текст запроса отчета
Функция ПолучитьТекстЗапроса() Экспорт
	
	РезультатТекстЗапроса = ТекстЗапроса;
	
	ИзмерениеАвтомобиль = ИзмеренияСтроки.НайтиСтроки(Новый Структура("ПутьКДанным, Использование", "Автомобиль", Истина));
	Если ИзмерениеАвтомобиль.Количество() > 0 Тогда
		Если ПараметрыИзмеренийСтрок.Свойство("Автомобиль") Тогда
			
			Если ПараметрыИзмеренийСтрок.Автомобиль.Свойство("Поля") Тогда
				ТекстСоединений  = "";
				ТекстВыборки     = "";
				ТекстПсевданимов = "";
				ТекстИтогов      = "";
				Для Каждого ТекПоле Из ПараметрыИзмеренийСтрок.Автомобиль.Поля Цикл
					ИмяПоля = СтрЗаменить(ТекПоле.ПутьКДанным, ".", "");
					Если СтруктураКИ.Свойство(ИмяПоля) Тогда
						
						ДанныеКИ = СтруктураКИ[ИмяПоля];
						
						ИмяПараметраВид = "ПараметрВид" + ИмяПоля;
						ИмяПараметраТип = "ПараметрТип" + ИмяПоля;
						ИмяТаблицы      = "ТаблицаКИ"   + ИмяПоля;
						
						ТекстВыборки    = ТекстВыборки + ",
						|	ЕСТЬNULL(" + ИмяТаблицы + ".Представление, """") КАК " + ИмяПоля;
						
						ТекстПсевданимов = ТекстПсевданимов + ",
						|	" + ИмяПоля + " КАК " + ИмяПоля;
						
						ТекстИтогов = ТекстИтогов + ",
						|	МАКСИМУМ(" + ИмяПоля + ")";
						
						ТекстСоединений  = ТекстСоединений + "
						|{ЛЕВОЕ СОЕДИНЕНИЕ
						|	РегистрСведений.КонтактнаяИнформация КАК " + ИмяТаблицы + "
						|ПО
						|	ВЫРАЗИТЬ(АвтомобилиВладелец.Значение КАК Справочник.Контрагенты) = " + ИмяТаблицы + ".Объект И 
						|	" + ИмяТаблицы + ".Вид = &" + ИмяПараметраВид + " И 
						|	" + ИмяТаблицы + ".Тип = &" + ИмяПараметраТип + "
						|}";
						
						ПостроительОтчета.Параметры.Вставить(ИмяПараметраВид, ДанныеКИ.Вид);
						ПостроительОтчета.Параметры.Вставить(ИмяПараметраТип, ДанныеКИ.Тип);
						
					КонецЕсли;
				КонецЦикла;
				
				РезультатТекстЗапроса = СтрЗаменить(РезультатТекстЗапроса, "//КИ_ВЫБОРКА",    ТекстВыборки);
				РезультатТекстЗапроса = СтрЗаменить(РезультатТекстЗапроса, "//КИ_ПСЕВДАНИМ",  ТекстПсевданимов);
				РезультатТекстЗапроса = СтрЗаменить(РезультатТекстЗапроса, "//КИ_СОЕДИНЕНИЯ", ТекстСоединений);
				РезультатТекстЗапроса = СтрЗаменить(РезультатТекстЗапроса, "//КИ_ИТОГИ",      ТекстИтогов);
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатТекстЗапроса
	
КонецФункции

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ИмяУпрВалюты  = Константы.ВалютаУправленческогоУчетаКомпании.Получить().Наименование;
	ИмяРеглВалюты = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить().Наименование;
	
	СтруктураВалютПоказателей = Новый Структура();
	СтруктураВалютПоказателей.Вставить("СуммаРегл", ИмяРеглВалюты);
	СтруктураВалютПоказателей.Вставить("СуммаУпр", ИмяУпрВалюты);
	СтруктураВалютПоказателей.Вставить("СуммаНДС", ИмяРеглВалюты);
	СтруктураВалютПоказателей.Вставить("СуммаБезНДС", ИмяРеглВалюты);
	СтруктураВалютПоказателей.Вставить("СебестоимостьРегл", ИмяРеглВалюты);
	СтруктураВалютПоказателей.Вставить("СебестоимостьУпр", ИмяУпрВалюты);
	СтруктураВалютПоказателей.Вставить("СуммаНДСВходящий", ИмяРеглВалюты);
	СтруктураВалютПоказателей.Вставить("СебестоимостьБезНДС", ИмяРеглВалюты);
	СтруктураВалютПоказателей.Вставить("СуммаНаценкиРегл", ИмяРеглВалюты);
	СтруктураВалютПоказателей.Вставить("СуммаНаценкиУпр", ИмяУпрВалюты);
	СтруктураВалютПоказателей.Вставить("СуммаСкидки", ИмяРеглВалюты);
	
	Для Каждого ТекСтрока Из Показатели Цикл
		Если СтруктураВалютПоказателей.Свойство(ТекСтрока.Имя) Тогда
			ТекСтрока.Представление = ТекСтрока.Представление+" "+СтруктураВалютПоказателей[ТекСтрока.Имя];
		КонецЕсли;
	КонецЦикла;
	
	СтрокаЗаказНаряд = ДеревоДоступныхПолей.Строки.Найти("ЗаказНаряд", "ПутьКДанным", Ложь);
	Если НЕ СтрокаЗаказНаряд = Неопределено Тогда
		ДеревоДоступныхПолей.Строки.Удалить(СтрокаЗаказНаряд);
	КонецЕсли;
	отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Истина, "Автомобиль", "ЗаказНаряд", "Последнее ТО", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаряд"));
	
	СтрокаАвтомобильХозяин = ДеревоДоступныхПолей.Строки.Найти("АвтомобильХозяин", "ПутьКДанным", Ложь);
	Если НЕ СтрокаАвтомобильХозяин = Неопределено Тогда
		ДеревоДоступныхПолей.Строки.Удалить(СтрокаАвтомобильХозяин);
	КонецЕсли;
	
	отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Истина, "Автомобиль",       "АвтомобильХозяин",                    "Владелец",            Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Ложь,   "АвтомобильХозяин", "АвтомобильХозяин.НаименованиеПолное", "Полное наименование", Новый ОписаниеТипов("Строка"));
	отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Ложь,   "АвтомобильХозяин", "АвтомобильХозяин.ВидКонтрагента",     "Вид контрагента",     Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКонтрагентов"));
	отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Ложь,   "АвтомобильХозяин", "АвтомобильХозяин.РекламныйИсточник",  "Рекламный источник",  Новый ОписаниеТипов("СправочникСсылка.ИсточникиИнформации"));
	отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Ложь,   "АвтомобильХозяин", "АвтомобильХозяин.ФормаСобственности", "Форма собственности", Новый ОписаниеТипов("ПеречислениеСсылка.ФормыСобственности"));
	
	ВетвьВладельца = ДеревоДоступныхПолей.Строки.Найти("АвтомобильХозяин", "ПутьКДанным", Истина);
	Если НЕ ВетвьВладельца = Неопределено Тогда
		
		// Добавим в доступные поля
		ВетвьКИ = ВетвьВладельца.Строки.Найти("АвтомобильХозяин.КИ", "ПутьКДанным");
		Если ВетвьКИ = Неопределено Тогда
			
			ВетвьКИ = ВетвьВладельца.Строки.Добавить();
			ВетвьКИ.Имя           = "КИ";
			ВетвьКИ.ПутьКДанным   = "АвтомобильХозяин.КИ";
			ВетвьКИ.Представление = "Контактная информация";
			
		КонецЕсли;
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ВидыКонтактнойИнформации.Ссылка,
		|	ВидыКонтактнойИнформации.Наименование,
		|	ВидыКонтактнойИнформации.Тип
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|ГДЕ
		|	ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ");
		
		ТипКИ = Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации");
		
		РодительПоля = ВетвьКИ.Родитель.Родитель;
		ПредставлениеРодителя = ВетвьКИ.Родитель.Представление;
		Пока РодительПоля <> Неопределено Цикл
			ПредставлениеРодителя = РодительПоля.Представление+"->"+ПредставлениеРодителя;
			РодительПоля = РодительПоля.Родитель;
		КонецЦикла;
		
		ВыборкаКИ = Запрос.Выполнить().Выбрать();
		Пока ВыборкаКИ.Следующий() Цикл
			
			ИмяПоля = "АвтомобильКИ" + СтрЗаменить(ВыборкаКИ.Ссылка.УникальныйИдентификатор(),"-","_");
			СтруктураКИ.Вставить(ИмяПоля, Новый Структура("Вид, Тип", ВыборкаКИ.Ссылка, ВыборкаКИ.Тип));
			отДобавитьСтруктуруНеСвязанныхПоказателей(ЭтотОбъект, Ложь, "АвтомобильХозяин.КИ", ИмяПоля, ВыборкаКИ.Наименование, ТипКИ);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента – передает ссылку на табличный документ 
//						или поле табличного документа
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент 
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт

	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// формирует отчет в виде сводной таблицы
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента – передает ссылку на табличный документ 
//						или поле табличного документа
//
// Возвращаемое значение:
//  ДокументРезультат - ТабличныйДокумент
//
Функция СформироватьСводнуюТаблицу(ДокументРезультат=Неопределено) Экспорт
	
	отСформироватьСводнуюТаблицу(ЭтотОбъект, ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции // СформироватьСводнуюТаблицу()

// формирует диаграмму
//
// Параметры
//  ПолеДиаграммы – передает ссылку на поле диаграммы
//  СтруктураПараметров - передает структуру параметров диаграммы
//
// Возвращаемое значение:
//  ПолеДиаграммы - Диаграмма
//
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
	
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров);
	
	Возврат ПолеДиаграммы;
	
КонецФункции // СформироватьДиаграмму()

// формирует отчет в виде сводной диаграммы
//
// Параметры
//  ПолеСводнойДиаграммы – СводнаяДиаграмма
//
// Возвращаемое значение:
//  ПолеСводнойДиаграммы - СводнаяДиаграмма
//
Функция СформироватьСводнуюДиаграмму(ПолеСводнойДиаграммы=Неопределено) Экспорт
	
	отСформироватьСводнуюДиаграмму(ЭтотОбъект, ПолеСводнойДиаграммы);
	
	Возврат ПолеСводнойДиаграммы;
	
КонецФункции // СформироватьСводнуюДиаграмму()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт

	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);

	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;

КонецФункции	//	СтруктураФорматированияПолей()

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ИмяРегистра = "Продажи";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураКИ = Новый Структура();
#КонецЕсли
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ВложЗапрос.Автомобиль КАК Автомобиль,
	МИНИМУМ(ВложЗапрос.ПериодНач) КАК ПериодНач,
	МАКСИМУМ(ВложЗапрос.ПериодКон) КАК ПериодКон,
	МИНИМУМ(ВложЗапрос.ПробегНач) КАК ПробегНач,
	МАКСИМУМ(ВложЗапрос.ПробегКон) КАК ПробегКон
ПОМЕСТИТЬ
	ТаблицаАвтомобилей
ИЗ(ВЫБРАТЬ
	Автомобили.Автомобиль КАК Автомобиль,
	МАКСИМУМ(Автомобили.Период) КАК ПериодНач,
	МАКСИМУМ(Автомобили.Период) КАК ПериодКон,
	МАКСИМУМ(ВЫРАЗИТЬ(Автомобили.Значение КАК ЧИСЛО)) КАК ПробегНач,
	МАКСИМУМ(ВЫРАЗИТЬ(Автомобили.Значение КАК ЧИСЛО)) КАК ПробегКон
ИЗ
	РегистрСведений.Автомобили КАК Автомобили
ГДЕ
	Автомобили.Период < &ДатаНач И 
	Автомобили.ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Пробег)
СГРУППИРОВАТЬ ПО
	Автомобили.Автомобиль
	
ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	Автомобили.Автомобиль,
	МИНИМУМ(Автомобили.Период),
	МАКСИМУМ(Автомобили.Период),
	МИНИМУМ(ВЫРАЗИТЬ(Автомобили.Значение КАК ЧИСЛО)),
	МАКСИМУМ(ВЫРАЗИТЬ(Автомобили.Значение КАК ЧИСЛО))
ИЗ
	РегистрСведений.Автомобили КАК Автомобили
ГДЕ
	Автомобили.Период МЕЖДУ &ДатаНач И &ДатаКон И 
	Автомобили.ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Пробег)
СГРУППИРОВАТЬ ПО
	Автомобили.Автомобиль) КАК ВложЗапрос
СГРУППИРОВАТЬ ПО
	ВложЗапрос.Автомобиль
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
	ЗаказНаряды.Автомобиль КАК Автомобиль,
	ЗаказНаряды.Ссылка КАК ЗаказНаряд,
	ЗаказНаряды.ПробегСледующегоТО КАК ПробегСледующегоТО,
	ЗаказНаряды.ДатаСледующегоТО КАК ДатаСледующегоТО,
	ЗаказНаряды.ДатаОкончания КАК ДатаОкончания,
	ЗаказНаряды.ДатаНачала КАК ДатаНачала
ПОМЕСТИТЬ ТаблицаОборотов
ИЗ
	Документ.ЗаказНаряд КАК ЗаказНаряды
ГДЕ
	ЗаказНаряды.ВидРемонта.ТехОбслуживание
	И ЗаказНаряды.ДатаНачала МЕЖДУ &ДатаНач И &ДатаКон
{ГДЕ
	ЗаказНаряды.Автомобиль.* КАК Автомобиль,
	ЗаказНаряды.Автомобиль.Модель.* КАК Модель
}
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ТаблицаОборотов.Автомобиль КАК Автомобиль,
	МАКСИМУМ(ТаблицаОборотов.ДатаНачала) КАК ДатаНачала
ПОМЕСТИТЬ
	СрезПоследних
ИЗ
	ТаблицаОборотов КАК ТаблицаОборотов
СГРУППИРОВАТЬ ПО
	ТаблицаОборотов.Автомобиль
ИНДЕКСИРОВАТЬ ПО
	Автомобиль
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	СрезПоследних.Автомобиль КАК Автомобиль,
	ТаблицаОборотов.ЗаказНаряд КАК ЗаказНаряд,
	ТаблицаОборотов.ПробегСледующегоТО КАК ПробегСледующегоТО,
	ТаблицаОборотов.ДатаСледующегоТО КАК ДатаСледующегоТО,
	ТаблицаОборотов.ДатаОкончания КАК ДатаОкончания
ПОМЕСТИТЬ
	ТаблицаПоследнийЗН
ИЗ
	СрезПоследних КАК СрезПоследних
ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	ТаблицаОборотов КАК ТаблицаОборотов
ПО
	СрезПоследних.Автомобиль = ТаблицаОборотов.Автомобиль И 
	СрезПоследних.ДатаНачала = ТаблицаОборотов.ДатаНачала
;

УНИЧТОЖИТЬ
	ТаблицаОборотов
;

УНИЧТОЖИТЬ
	СрезПоследних
;

ВЫБРАТЬ
	ТаблицаАвтомобилей.Автомобиль КАК Автомобиль,
	ТаблицаПоследнийЗН.ЗаказНаряд КАК ЗаказНаряд,
	ВЫБОР
		КОГДА (НЕ ТаблицаПоследнийЗН.ПробегСледующегоТО = 0) И (НЕ ТаблицаПоследнийЗН.ПробегСледующегоТО=ТаблицаАвтомобилей.ПробегКон) ТОГДА
			ТаблицаПоследнийЗН.ПробегСледующегоТО-ТаблицаАвтомобилей.ПробегКон
		КОГДА (НЕ ТаблицаАвтомобилей.Автомобиль.Модель.МежсервисныйПробегТО ЕСТЬ NULL) И (НЕ ТаблицаАвтомобилей.Автомобиль.Модель.МежсервисныйПробегТО = 0) ТОГДА
			-(ТаблицаАвтомобилей.ПробегКон - (ВЫРАЗИТЬ((ТаблицаАвтомобилей.ПробегКон/ТаблицаАвтомобилей.Автомобиль.Модель.МежсервисныйПробегТО)-0.5 КАК Число(10, 0))+1)*ТаблицаАвтомобилей.Автомобиль.Модель.МежсервисныйПробегТО)
		КОГДА (НЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.МежсервисныйПробегТО ЕСТЬ NULL) И (НЕ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.МежсервисныйПробегТО = 0) ТОГДА
			-(ТаблицаАвтомобилей.ПробегКон - (ВЫРАЗИТЬ((ТаблицаАвтомобилей.ПробегКон/ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.МежсервисныйПробегТО)-0.5 КАК Число(10, 0))+1)*ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.МежсервисныйПробегТО)
		ИНАЧЕ
			0
	КОНЕЦ КАК ПробегМеждуТО,
	ВЫБОР
		КОГДА (НЕ ТаблицаПоследнийЗН.ДатаСледующегоТО = ДАТАВРЕМЯ(1, 1, 1)) И (НЕ ТаблицаПоследнийЗН.ДатаСледующегоТО = ТаблицаАвтомобилей.ПериодКон) ТОГДА
			ТаблицаПоследнийЗН.ДатаСледующегоТО
		КОГДА ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.МежсервисныйИнтервалТО ЕСТЬ NULL ИЛИ ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.МежсервисныйИнтервалТО = 0 ТОГДА
			ДОБАВИТЬКДАТЕ(ТаблицаПоследнийЗН.ДатаОкончания, МЕСЯЦ, ТаблицаАвтомобилей.Автомобиль.Модель.МежсервисныйИнтервалТО)
		ИНАЧЕ
			ДОБАВИТЬКДАТЕ(ТаблицаПоследнийЗН.ДатаОкончания, МЕСЯЦ, ТаблицаАвтомобилей.Автомобиль.ВариантКомплектации.МежсервисныйИнтервалТО)
	КОНЕЦ КАК ДатаСледующегоТО,
	ТаблицаАвтомобилей.ПробегНач КАК ПробегНач,
	ТаблицаАвтомобилей.ПробегКон-ТаблицаАвтомобилей.ПробегНач КАК Пробег,
	ТаблицаАвтомобилей.ПробегКон КАК ПробегКон,
	РАЗНОСТЬДАТ(ТаблицаАвтомобилей.ПериодНач, ТаблицаАвтомобилей.ПериодКон, ДЕНЬ) КАК Интервал,
	ТаблицаАвтомобилей.ПериодКон КАК ПериодКон
ПОМЕСТИТЬ
	ТаблицаТО
ИЗ
	ТаблицаАвтомобилей КАК ТаблицаАвтомобилей
ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	ТаблицаПоследнийЗН КАК ТаблицаПоследнийЗН
ПО
	ТаблицаАвтомобилей.Автомобиль = ТаблицаПоследнийЗН.Автомобиль
ИНДЕКСИРОВАТЬ ПО
	Автомобиль
;

УНИЧТОЖИТЬ
	ТаблицаПоследнийЗН
;

УНИЧТОЖИТЬ
	ТаблицаАвтомобилей
;

ВЫБРАТЬ РАЗРЕШЕННЫЕ
#Область ИзмеренияПостроителяДляСвойств
	ТаблицаТО.Автомобиль КАК Автомобиль,
	АвтомобилиВладелец.Значение КАК Хозяин,
#КонецОбласти
	АвтомобилиВладелец.Значение КАК АвтомобильХозяин,
	ТаблицаТО.ПробегНач КАК ПробегНач,
	ТаблицаТО.ПробегКон КАК ПробегКон,
	ТаблицаТО.Пробег КАК Пробег,
	ТаблицаТО.Интервал КАК Интервал,
	ТаблицаТО.ЗаказНаряд КАК ЗаказНаряд,
	ВЫБОР 
		КОГДА ТаблицаТО.Пробег = 0 ИЛИ ТаблицаТО.Интервал = 0 ТОГДА 
			ТаблицаТО.ДатаСледующегоТО
		КОГДА ((ТаблицаТО.ПробегМеждуТО*ТаблицаТО.Интервал)/ТаблицаТО.Пробег)>=РАЗНОСТЬДАТ(ТаблицаТО.ПериодКон, ТаблицаТО.ДатаСледующегоТО, ДЕНЬ) ТОГДА 
			ТаблицаТО.ДатаСледующегоТО
		ИНАЧЕ 
			ДОБАВИТЬКДАТЕ(ТаблицаТО.ПериодКон, ДЕНЬ, (ТаблицаТО.ПробегМеждуТО*ТаблицаТО.Интервал)/ТаблицаТО.Пробег)
	КОНЕЦ КАК ПлановаяДата,
	ВЫБОР
		КОГДА ТаблицаТО.Интервал > 0 ТОГДА
			ТаблицаТО.Пробег/ТаблицаТО.Интервал
		ИНАЧЕ
			0
	КОНЕЦ КАК Скорость
//СВОЙСТВА
//КИ_ВЫБОРКА

{ВЫБРАТЬ
	Хозяин.* КАК Хозяин,
	АвтомобильХозяин.* КАК АвтомобильХозяин,
	Автомобиль.* КАК Автомобиль,
	ПробегНач КАК ПробегНач,
	ПробегКон КАК ПробегКон,
	Пробег КАК Пробег,
	ПлановаяДата КАК ПлановаяДата,
	Скорость КАК Скорость,
	ЗаказНаряд КАК ЗаказНаряд
//СВОЙСТВА
//КИ_ПСЕВДАНИМ
}
	
ИЗ
	ТаблицаТО КАК ТаблицаТО
{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Автомобили.СрезПоследних(, ВидЗначения = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяИнформацияАвтомобилей.Хозяин)
		{Автомобиль.* КАК Автомобиль,
 		Автомобиль.Модель.* КАК Модель}) КАК АвтомобилиВладелец
ПО
	ТаблицаТО.Автомобиль = АвтомобилиВладелец.Автомобиль}
//СОЕДИНЕНИЯ
//КИ_СОЕДИНЕНИЯ
{ГДЕ
	(ВЫРАЗИТЬ(АвтомобилиВладелец.Значение КАК Справочник.Контрагенты)).* КАК Хозяин,
	ВЫБОР 
		КОГДА ТаблицаТО.Пробег = 0 ИЛИ ТаблицаТО.Интервал = 0 ТОГДА 
			ТаблицаТО.ДатаСледующегоТО
		КОГДА ((ТаблицаТО.ПробегМеждуТО*ТаблицаТО.Интервал)/ТаблицаТО.Пробег)>=РАЗНОСТЬДАТ(ТаблицаТО.ПериодКон, ТаблицаТО.ДатаСледующегоТО, ДЕНЬ) ТОГДА 
			ТаблицаТО.ДатаСледующегоТО
		ИНАЧЕ 
			ДОБАВИТЬКДАТЕ(ТаблицаТО.ПериодКон, ДЕНЬ, (ТаблицаТО.ПробегМеждуТО*ТаблицаТО.Интервал)/ТаблицаТО.Пробег)
	КОНЕЦ КАК ПлановаяДатаОтбор
	//СВОЙСТВА
}
{УПОРЯДОЧИТЬ ПО
	Автомобиль.* КАК Автомобиль
	//СВОЙСТВА
}

{ИТОГИ ПО
	Автомобиль.* КАК Автомобиль
	//СВОЙСТВА
}

ИТОГИ 
	ВЫБОР 
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА МИНИМУМ(Хозяин)
	КОНЕЦ КАК Хозяин,
	ВЫБОР 
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА МИНИМУМ(ЗаказНаряд)
	КОНЕЦ КАК ЗаказНаряд,
	ВЫБОР 
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА МИНИМУМ(АвтомобильХозяин)
	КОНЕЦ КАК АвтомобильХозяин,
	ВЫБОР 
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА 
			МИНИМУМ(ПробегНач)
		ИНАЧЕ
			0
	КОНЕЦ КАК ПробегНач,
	ВЫБОР 
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА 
			СУММА(Пробег)
		ИНАЧЕ
			0
	КОНЕЦ КАК Пробег,
	ВЫБОР 
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА 
			МАКСИМУМ(ПробегКон)
		ИНАЧЕ
			0
	КОНЕЦ КАК ПробегКон,
	ВЫБОР 
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА 
			МАКСИМУМ(ПлановаяДата)
		ИНАЧЕ
			0
	КОНЕЦ КАК ПлановаяДата,
	ВЫБОР
		КОГДА НЕ Автомобиль ЕСТЬ NULL ТОГДА
			МАКСИМУМ(Скорость)
		ИНАЧЕ
			0
	КОНЕЦ КАК Скорость
	//ИТОГИСВОЙСТВА
	//КИ_ИТОГИ
ПО
	Автомобиль

АВТОУПОРЯДОЧИВАНИЕ
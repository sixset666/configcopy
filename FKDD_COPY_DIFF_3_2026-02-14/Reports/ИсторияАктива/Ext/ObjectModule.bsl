#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем флЕстьНачалоПериода; // признак наличия начала периода
Перем флЕстьКонецПериода;  // признак наличия конца периода
Перем ВалютаУчета;         // Ссылка на валюту учета


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Получает результат запроса для вывода в шапку
//
// Параметры
//   ДатыВводаВыбытия - Дата
//
// Возвращаемое значение:
//   Запрос.Выполнить – РезультатЗапроса
//
Функция ПолучитьРезультатЗапросаШапка(ДатыВводаВыбытия)
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ТаблицаШапки.КоличествоНач) КАК КоличествоНач,
	|	СУММА(ТаблицаШапки.БалансоваяСтоимостьНач) КАК БалансоваяСтоимостьНач,
	|	СУММА(ТаблицаШапки.ОстаточнаяСтоимостьНач) КАК ОстаточнаяСтоимостьНач,
	|	СУММА(ТаблицаШапки.КоличествоКон) КАК КоличествоКон,
	|	СУММА(ТаблицаШапки.БалансоваяСтоимостьКон) КАК БалансоваяСтоимостьКон,
	|	СУММА(ТаблицаШапки.ОстаточнаяСтоимостьКон) КАК ОстаточнаяСтоимостьКон,
	|	СУММА(ТаблицаШапки.СуммаАмортизации) КАК СуммаАмортизации,
	|	СУММА(ТаблицаШапки.СуммаОбслуживания) КАК СуммаОбслуживания
	|ИЗ(
	|ВЫБРАТЬ
	|	ЭксплАктивыОиО.КоличествоОстаток КАК КоличествоНач,
	|	ЭксплАктивыОиО.БалансоваяСтоимостьУпрОстаток КАК БалансоваяСтоимостьНач,
	|	ЭксплАктивыОиО.БалансоваяСтоимостьУпрОстаток - ЭксплАктивыОиО.СуммаАмортизацииУпрОстаток КАК ОстаточнаяСтоимостьНач,
	|	0 КАК КоличествоКон,
	|	0 КАК БалансоваяСтоимостьКон,
	|	0 КАК ОстаточнаяСтоимостьКон,
	|	0 КАК СуммаАмортизации,
	|	0 КАК СуммаОбслуживания
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(&НачПериода, ПрочийАктив = &Актив) КАК ЭксплАктивыОиО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(ЭксплАктивыО.СуммаАмортизацииУпрПриход, 0),
	|	ЕСТЬNULL(ЭксплАктивыО.СуммаОбслуживанияУпрПриход, 0)
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Обороты(&НачПериода1, &КонПериода1, Регистратор, ПрочийАктив = &Актив) КАК ЭксплАктивыО
	|ГДЕ
	|	ЭксплАктивыО.Регистратор ССЫЛКА Документ.ВводОстатковПрочихАктивов ИЛИ
	|	ЭксплАктивыО.Регистратор ССЫЛКА Документ.ВводВЭксплуатацию ИЛИ
	|	ЭксплАктивыО.Регистратор ССЫЛКА Документ.ВводВЭксплуатациюАвтомобилей ИЛИ
	|	ЭксплАктивыО.Регистратор ССЫЛКА Документ.Амортизация ИЛИ
	|	ЭксплАктивыО.Регистратор ССЫЛКА Документ.ОбслуживаниеАктива
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	ЭксплАктивыОиО.КоличествоОстаток,
	|	ЭксплАктивыОиО.БалансоваяСтоимостьУпрОстаток,
	|	ЭксплАктивыОиО.БалансоваяСтоимостьУпрОстаток - ЭксплАктивыОиО.СуммаАмортизацииУпрОстаток,
	|	0,
	|	0
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(&КонПериода, ПрочийАктив = &Актив) КАК ЭксплАктивыОиО) КАК ТаблицаШапки";
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Актив", Актив);
	Запрос.УстановитьПараметр("НачПериода", ДатыВводаВыбытия.ДатаВвода + 1);
	Запрос.УстановитьПараметр("КонПериода", Макс((ДатыВводаВыбытия.ДатаВыбытия - 1), КонецДня(ДатаКонца)));
	Запрос.УстановитьПараметр("НачПериода1", ДатаНачала);
	Запрос.УстановитьПараметр("КонПериода1", КонецДня(ДатаКонца));
	
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаШапка()

// Получает результат запроса для вывода в таблицу
//
// Без параметров
//
// Возвращаемое значение:
//   Запрос.Выполнить – РезультатЗапроса
//
Функция ПолучитьРезультатЗапросаТаблица()
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПрочиеАктивыВЭксплуатации.Регистратор                            КАК Документ,
	|	ПРЕДСТАВЛЕНИЕ(ПрочиеАктивыВЭксплуатации.Регистратор)             КАК ДокументПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ПрочиеАктивыВЭксплуатации.Регистратор.Комментарий) КАК ДокументКомментарий,
	|	ПрочиеАктивыВЭксплуатации.ХозОперация                            КАК ХО,
	|	ПРЕДСТАВЛЕНИЕ(ПрочиеАктивыВЭксплуатации.ХозОперация)             КАК ХОПредставление,
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.БалансоваяСтоимостьУпр, 0)    КАК БалансоваяСтоимостьУпр,
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаАмортизацииУпр, 0)       КАК СуммаАмортизацииУпр,
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.СуммаОбслуживанияУпр, 0)      КАК СуммаОбслуживанияУпр,
	|	ЕСТЬNULL(ПрочиеАктивыВЭксплуатации.Количество, 0)				 КАК Количество,
	|	ПРЕДСТАВЛЕНИЕ(ПрочиеАктивыВЭксплуатации.ТипОбслуживания)         КАК ТипОбслуживания,
	|	ПРЕДСТАВЛЕНИЕ(ПрочиеАктивыВЭксплуатации.ТипЭксплуатации)         КАК ТипЭксплуатации,
	|	ПРЕДСТАВЛЕНИЕ(ПрочиеАктивыВЭксплуатации.МОЛ)                     КАК МОЛ,
	|	ПРЕДСТАВЛЕНИЕ(ПрочиеАктивыВЭксплуатации.ПодразделениеКомпании)   КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕ(ДоходыИРасходыОбороты.ПодразделениеКомпании) КАК ПодразделениеУслуги,
	|	ЕСТЬNULL(ДоходыИРасходыОбороты.ДоходУпрПриход, 0)          КАК СуммаСобственныхУслуг,
	|	ПРЕДСТАВЛЕНИЕ(ВзаиморасчетыКомпанииОбороты.Контрагент)            КАК Контрагент,
	|	ПРЕДСТАВЛЕНИЕ(ВзаиморасчетыКомпанииОбороты.ДоговорВзаиморасчетов) КАК Договор,
	|	ЕСТЬNULL(ВзаиморасчетыКомпанииОбороты.СуммаУпрРасход, 0)          КАК СуммаСтороннихУслуг,
	|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровКомпанииОбороты.СкладКомпании) КАК Склад,
	|	ЕСТЬNULL(ПартииТоваровКомпанииОбороты.СуммаУпрРасход, 0)  КАК СуммаСписания,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияАктивовАктивы.Ссылка.Контрагент)            КАК Покупатель,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияАктивовАктивы.Ссылка.ДоговорВзаиморасчетов) КАК ДоговорПокупателя,
	|	РеализацияАктивовАктивы.Количество КАК КоличествоПродажи,
	|	РеализацияАктивовАктивы.Сумма*РеализацияАктивовАктивы.Ссылка.КурсДокумента/РеализацияАктивовАктивы.Ссылка.КурсВалютыУпр КАК СуммаПродажи
    |ИЗ
	|	РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы.Обороты(&НачПериода, &КонПериода, Регистратор, ) КАК ДоходыИРасходыОбороты
	|		ПО ПрочиеАктивыВЭксплуатации.Регистратор = ДоходыИРасходыОбороты.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыКомпании.Обороты(&НачПериода, &КонПериода, Регистратор, ) КАК ВзаиморасчетыКомпанииОбороты
	|		ПО ПрочиеАктивыВЭксплуатации.Регистратор = ВзаиморасчетыКомпанииОбороты.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Обороты(&НачПериода, &КонПериода, Регистратор, ) КАК ПартииТоваровКомпанииОбороты
	|		ПО ПрочиеАктивыВЭксплуатации.Регистратор = ПартииТоваровКомпанииОбороты.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияАктивов.Активы КАК РеализацияАктивовАктивы
	|		ПО	ПрочиеАктивыВЭксплуатации.Регистратор = РеализацияАктивовАктивы.Ссылка И
	|			ПрочиеАктивыВЭксплуатации.ПрочийАктив = РеализацияАктивовАктивы.ПрочийАктив
	|ГДЕ
	|	ПрочиеАктивыВЭксплуатации.ПрочийАктив = &Актив
	|	И ПрочиеАктивыВЭксплуатации.Период МЕЖДУ &НачПериода И &КонПериода
	|ИТОГИ
	|	СУММА(БалансоваяСтоимостьУпр),
	|	СУММА(СуммаАмортизацииУпр),
	|	СУММА(СуммаОбслуживанияУпр),
	|	СУММА(КоличествоПродажи),
	|	СУММА(Количество)
	|ПО
	|	Документ";

	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Актив", Актив);
	Запрос.УстановитьПараметр("НачПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонПериода", КонецДня(ДатаКонца));
		
	Возврат Запрос.Выполнить();
КонецФункции // ПолучитьРезультатЗапросаТаблица()

// Получает дату ввода и дату выбытия актива
//
// Без параметров
//
// Возвращаемое значение:
//   СтруктураДаты - Структура
//
Функция ПолучитьРезультатЗапросаПоДатам() Экспорт
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	               |	ПрочиеАктивыВЭксплуатации.Период КАК Дата,
	               |	ПрочиеАктивыВЭксплуатации.Регистратор КАК Регистратор
	               |ИЗ
	               |	РегистрНакопления.ПрочиеАктивыВЭксплуатации КАК ПрочиеАктивыВЭксплуатации
	               |ГДЕ
	               |	ПрочиеАктивыВЭксплуатации.ПрочийАктив = &Актив
	               |	И (ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.ВводОстатковПрочихАктивов
				   |			ИЛИ ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.ВводВЭксплуатацию
	               |			ИЛИ ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.ВводВЭксплуатациюАвтомобилей
	               |			ИЛИ ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.СписаниеАктивов
	               |			ИЛИ ПрочиеАктивыВЭксплуатации.Регистратор ССЫЛКА Документ.РеализацияАктивов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПрочиеАктивыВЭксплуатации.Период";
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Актив", Актив);
	ТаблРез = Запрос.Выполнить().Выгрузить();
	
	СтруктураДаты = Новый Структура("ДатаВвода, ДатаВыбытия, ", Дата("00010101000000"), Дата("00010101000000"));
	Если ТаблРез.Количество() = 0 Тогда
		//оставляем пустые даты
		Возврат СтруктураДаты;
	КонецЕсли;
	
	Для Каждого Строка Из ТаблРез Цикл
		Если ТипЗнч(Строка.Регистратор) = Тип("ДокументССылка.ВводОстатковПрочихАктивов") ИЛИ 
			ТипЗнч(Строка.Регистратор) = Тип("ДокументССылка.ВводВЭксплуатацию") ИЛИ 
			ТипЗнч(Строка.Регистратор) = Тип("ДокументССылка.ВводВЭксплуатациюАвтомобилей") Тогда
			СтруктураДаты.ДатаВвода = Строка.Дата;
		Иначе
			СтруктураДаты.ДатаВыбытия = Строка.Дата;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураДаты;
КонецФункции // ПолучитьРезультатЗапросаПоДатам()

// Дозаполняет строку отчета
//
//	ТекОбласть   - текущая область (строка) отчета,
//	ВыборкаИтоги - выборка по документу(может содержать 1 или 2 вложения),
//	флПодробно   - если истина, то выводится более полная информация, если ложь, то главный суммовой показатель
//					по документу и комментарий.
//
Процедура ЗаполнитьСтрокуОтчета(ТекОбласть, ВыборкаИтоги, флПодробно = Истина)
	ТекДокумент = ВыборкаИтоги.Документ;
	ТипДокумента = ТипЗнч(ТекДокумент);
	
	ВыборкаДетали = ВыборкаИтоги.Выбрать();
	ВыборкаДетали.Следующий();
	ХО       = ВыборкаДетали.ХО;
	ХОПредст = ВыборкаДетали.ХОПредставление;
	
	// case на вид документа
	Если ТипДокумента = Тип("ДокументСсылка.ВводОстатковПрочихАктивов") Тогда
		ИнфСтрока = "Количество: " + Формат(ВыборкаИтоги.Количество, "ЧДЦ=3; ЧН=0") + ",
		|Сумма: " + Формат(ВыборкаИтоги.БалансоваяСтоимостьУпр-ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование + "
		|На момент ввода:
		|- балансовая стоимость: " + Формат(ВыборкаИтоги.БалансоваяСтоимостьУпр, "ЧДЦ=2; ЧН=0") + " " + ВалютаУчета.Наименование + ",
		|- сумма амортизации:	 " + Формат(ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2; ЧН=0") + " " + ВалютаУчета.Наименование + ",
		|- сумма обслуживания:	 " +  Формат(ВыборкаИтоги.СуммаОбслуживанияУпр, "ЧДЦ=2; ЧН=0") + " " + ВалютаУчета.Наименование;
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВводВЭксплуатацию") ИЛИ ТипДокумента = Тип("ДокументСсылка.ВводВЭксплуатациюАвтомобилей") Тогда
		флЕстьАмортизация = (ВыборкаДетали.Количество() = 2);
		Если флЕстьАмортизация Тогда
			ВыборкаДетали.Следующий();
			ХОПредст = Строка(ХОПредст) + ", " + Строка(ВыборкаДетали.ХОПредставление);
		КонецЕсли;
		ИнфСтрока = "Количество: " + Формат(ВыборкаИтоги.Количество, "ЧДЦ=3; ЧН=0") + ",
		|Сумма: "+Формат(ВыборкаИтоги.БалансоваяСтоимостьУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование + ?(флЕстьАмортизация, "
		|Начислена амортизация: " + Формат(ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование, "");
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПеремещениеАктивов") Тогда
		//считываем данные расхода
		П1 = ВыборкаДетали.Подразделение; ТЭ1 = ВыборкаДетали.ТипЭксплуатации; МОЛ1 = ВыборкаДетали.МОЛ;
		ВыборкаДетали.Следующий(); //считываем данные прихода
		П2 = ВыборкаДетали.Подразделение; ТЭ2 = ВыборкаДетали.ТипЭксплуатации; МОЛ2 = ВыборкаДетали.МОЛ;
		ИнфСтрока = "Количество: " + Формат(ВыборкаИтоги.Количество, "ЧДЦ=3; ЧН=0") + ",
		|Сумма: " + Формат(ВыборкаИтоги.БалансоваяСтоимостьУпр-ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование;
		ИнфСтрока1 = ?(П1 = П2, "", "
					|- подразделение: """ + П1 + """ -> """ + П2 + """, ") + 
					?(ТЭ1 = ТЭ2, "", "
					|- тип эксплуатации: """ + ТЭ1 + """ -> """ + ТЭ2 + """, ") + 
					?(МОЛ1 = МОЛ2, "", "
					|- МОЛ: """ + МОЛ1 + """ -> """ + МОЛ2 + """, ");
					 
		ИнфСтрока1 = ?(ИнфСтрока1 = "", "", Лев(ИнфСтрока1, СтрДлина(ИнфСтрока1)-2));
		ИнфСтрока = ?(ИнфСтрока1 = "", "", ИнфСтрока + "
		|Отправитель/получатель:" + ИнфСтрока1);
							 
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СписаниеАктивов") Тогда
		ИнфСтрока = "Количество: " + Формат(ВыборкаИтоги.Количество, "ЧДЦ=3; ЧН=0") + ",
		|Сумма: " + Формат(ВыборкаИтоги.БалансоваяСтоимостьУпр-ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование;
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РеализацияАктивов") Тогда
		ИнфСтрока ="Количество: " + Формат(ВыборкаИтоги.КоличествоПродажи, "ЧДЦ=3; ЧН=0") + ",
		|Сумма: " + Формат(ВыборкаДетали.СуммаПродажи, "ЧДЦ=2") + " " + ВалютаУчета.Наименование + "
		|Покупатель: 	  " + ВыборкаДетали.Покупатель + "
		|Договор продажи: " + ВыборкаДетали.ДоговорПокупателя + "
		|Сумма списания:  " + Формат(ВыборкаИтоги.БалансоваяСтоимостьУпр - ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование + 
		?(ВыборкаДетали.СуммаПродажи - ВыборкаИтоги.БалансоваяСтоимостьУпр + ВыборкаИтоги.СуммаАмортизацииУпр = 0, "", "
		|Выручка от реализации: " + Формат(ВыборкаДетали.СуммаПродажи - ВыборкаИтоги.БалансоваяСтоимостьУпр + ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.Амортизация") Тогда
		ИнфСтрока = "Сумма: " + Формат(ВыборкаИтоги.СуммаАмортизацииУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование;
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОбслуживаниеАктива") Тогда
		ИнфСтрока = "Сумма: " + Формат(ВыборкаИтоги.СуммаОбслуживанияУпр, "ЧДЦ=2") + " " + ВалютаУчета.Наименование + "
		|В том числе: " + ?(ВыборкаДетали.СуммаСобственныхУслуг = 0, "", "
		|- обслужен подразделением """ + ВыборкаДетали.ПодразделениеУслуги + """ на сумму " + Формат(ВыборкаДетали.СуммаСобственныхУслуг, "ЧДЦ=2") +  " " + ВалютаУчета.Наименование + ",") +
		?(ВыборкаДетали.СуммаСтороннихУслуг = 0, "", "
		|- обслужен контрагентом """ + ВыборкаДетали.Контрагент + """ на сумму " + Формат(ВыборкаДетали.СуммаСтороннихУслуг, "ЧДЦ=2") + " " + ВалютаУчета.Наименование + " по договору """ + ВыборкаДетали.Договор + """,") +
		?(ВыборкаДетали.СуммаСписания = 0, "", "
		|- списано номенкл. со склада """ + ВыборкаДетали.Склад + """ на сумму " + Формат(ВыборкаДетали.СуммаСписания, "ЧДЦ=2") +  " " + ВалютаУчета.Наименование + ".") + "
		|Тип обслуживания: """ + ВыборкаДетали.ТипОбслуживания + """";
	Иначе
		//ничего
		ИнфСтрока = "";
	КонецЕсли;
	// добавим комментарий
	ИнфСтрока = ИнфСтрока + ?(ВыборкаИтоги.ДокументКомментарий = "", "", "
						|Комментарий: " + ВыборкаИтоги.ДокументКомментарий);
						
	// заполним параметры области строки
	ТекОбласть.Параметры.ДокументПредставление = ВыборкаИтоги.ДокументПредставление;
	ТекОбласть.Параметры.Документ = ТекДокумент;
	ТекОбласть.Параметры.ХОПредставление = ХОПредст;
	ТекОбласть.Параметры.ХО = ХО;
	ТекОбласть.Параметры.Информация = ИнфСтрока;
КонецПроцедуры // ЗаполнитьСтрокуОтчета()

// Формирует отчет
//
// Параметры
//   ЭтаФорма - Форма - содержит данную форму
//   Кнопка - Кнопка
//
Процедура СформироватьОтчет(ЭтаФорма, Кнопка) Экспорт
	
	флЕстьКонецПериода  = (ДатаКонца  <> Дата("00010101"));
			
	//проверим корректность реквизитов
	Если Актив.Пустая() Тогда
		Предупреждение("Не указан прочий актив!", 10);
		Возврат;
	КонецЕсли;
	
	//подрихтуем даты, если не указаны
	Если НЕ флЕстьКонецПериода Тогда
		ДатаКонца = ТекущаяДата();
	КонецЕсли;
		
	Если ДатаНачала > ДатаКонца Тогда
		Предупреждение("Некорректный период", 10);
		Возврат;
	КонецЕсли;
	
	//получим даты ввода и выбытия
	РезультатЗапросаДатыВводаВыбытия = ПолучитьРезультатЗапросаПоДатам();
	Если РезультатЗапросаДатыВводаВыбытия.ДатаВвода = Дата("00010101000000") Тогда
		Предупреждение("Актив не введен в эксплуатацию", 10);
		Возврат;
	КонецЕсли;
	
	отДействияФормыСформировать(ЭтаФорма, Кнопка);
КонецПроцедуры // СформироватьОтчет()

// Формирует табличный документ   
//
// ТабДок - ТабличныйДокумент
//
Процедура СформироватьТабличныйДокумент(ТабДок = Неопределено) Экспорт
	флЕстьНачалоПериода = (ДатаНачала <> Дата("00010101"));
	флЕстьКонецПериода  = (ДатаКонца  <> Дата("00010101"));
	ВалютаУчета = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
	
	//проверим корректность реквизитов
	Если Актив.Пустая() Тогда
		Предупреждение("Не указан прочий актив!");
		Возврат;
	КонецЕсли;
	
	//подрихтуем даты, если не указаны
	Если НЕ флЕстьКонецПериода Тогда
		ДатаКонца = ТекущаяДата();
	КонецЕсли;
		
	Если ДатаНачала > ДатаКонца Тогда
		Сообщить("Некорректный период");
		Возврат;
	КонецЕсли;
	
	//получим даты ввода и выбытия
	РезультатЗапросаДатыВводаВыбытия = ПолучитьРезультатЗапросаПоДатам();
	Если РезультатЗапросаДатыВводаВыбытия.ДатаВвода = Дата("00010101000000") Тогда
		Предупреждение("Актив не введен в эксплуатацию");
		Возврат;
	КонецЕсли;
	
	Если обПраво("ПредупреждатьОНарушенииПоследовательностей", глПрава) Тогда
		
		ОбработкаВосстановлениеПоследовательностей = Обработки.ВосстановлениеПоследовательностей.Создать();
		
		ПоследовательностиОтчета = Новый Структура;
		ПоследовательностиОтчета = Новый Структура();
		ПоследовательностиОтчета.Вставить("ГраницыПартий");
		ПоследовательностиОтчета.Вставить("ГраницыВзаиморасчетов");
		
		ТаблицаПоследовательностей = ОбработкаВосстановлениеПоследовательностей.ТаблицаПоследовательностей;
		КоличествоПоследовательностей = ТаблицаПоследовательностей.Количество()-1;
		
		Пока КоличествоПоследовательностей>=0 Цикл
			ТекПоследовательность = ТаблицаПоследовательностей[КоличествоПоследовательностей];
			Если Не ПоследовательностиОтчета.Свойство(ТекПоследовательность.ИмяГраницы) Тогда
				ТаблицаПоследовательностей.Удалить(ТекПоследовательность);
			КонецЕсли;
			КоличествоПоследовательностей = КоличествоПоследовательностей-1;
		КонецЦикла;
		
		Если ТаблицаПоследовательностей.Количество()>0 Тогда
			ТаблицаГраниц = ОбработкаВосстановлениеПоследовательностей.ПолучитьТаблицуНеактуальныхДокументов(ДатаКонца);
			ТекстПредупреждения = "";
			Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
				Граница = ТаблицаГраниц.Найти(ТекПоследовательность.ИмяГраницы,"ИмяПоследовательности");
				Если Граница <> Неопределено Тогда
					ТекстАктуальна = "";
					Если Граница.Граница=Неопределено Тогда
						ТекстАктуальна	= Формат(Граница.МоментВремени,"ДФ=dd.MM.yyyy");
					Иначе
						ТекстАктуальна	= Формат(Граница.Граница.Дата,"ДФ=dd.MM.yyyy")+" - "+Граница.Граница.Метаданные().Синоним+" № "+СокрЛП(Граница.Граница.Номер);
					КонецЕсли;
					ТекстПредупреждения = ТекстПредупреждения+"
					| Последовательность: <" + ТекПоследовательность.Представление + "> актуальна " + ТекстАктуальна;
				КонецЕсли;
			КонецЦикла;
			Если Не ПустаяСтрока(ТекстПредупреждения) Тогда
				Предупреждение("Последовательности разрушены и возможны некорректные результаты отчетов!"+ТекстПредупреждения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//переопределим флаги периода
    флЕстьНачалоПериода = (ДатаНачала <> Дата("00010101000000"));
	флЕстьКонецПериода  = (ДатаКонца  <> Дата("00010101000000"));
				
	//получаем данные запросов      
	РезультатЗапросаПараметрыШапки = ПолучитьРезультатЗапросаШапка(РезультатЗапросаДатыВводаВыбытия).Выгрузить();
	РезультатЗапросаПараметрыТЧ    = ПолучитьРезультатЗапросаТаблица();
		
	//табличный документ
	Если ТабДок = Неопределено ИЛИ ТипЗнч(ТабДок) <> Тип("ПолеТабличногоДокумента") Тогда
		ТабДок = Новый ТабличныйДокумент;
	Иначе
		ТабДок.Очистить();
	КонецЕсли;
	
	//макет данных
	Макет = ПолучитьМакет("Макет");
	//макет оформления
	МакетОформления = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента);
	ОформлениеШапкаТаблицы = МакетОформления.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеСтрока       = МакетОформления.ПолучитьОбласть("ОформлениеИзмерений").Область(8, 1);
	ОформлениеПодвал       = МакетОформления.ПолучитьОбласть("ОформлениеИтоги").Область(1, 1);
	
	//выводим заголовок
	Область = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ТекстЗаголовка = "История актива """ + СокрЛП(Актив) + """ за период с ";
	ТекстЗаголовка = ТекстЗаголовка + ?(флЕстьНачалоПериода, Формат(ДатаНачала, "ДФ = дд.ММ.гггг"), "<не указан>");
	ТекстЗаголовка = ТекстЗаголовка + " по ";
	ТекстЗаголовка = ТекстЗаголовка + ?(флЕстьКонецПериода, Формат(ДатаКонца, "ДФ = дд.ММ.гггг"), "<не указан>");
	Область.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	Область.Параметры.ТекстВводВывод = "Введен в эксплуатацию: " + Формат(РезультатЗапросаДатыВводаВыбытия.ДатаВвода, "ДФ = дд.ММ.гггг") + 
		?(РезультатЗапросаДатыВводаВыбытия.ДатаВыбытия <> Дата("00010101000000") И РезультатЗапросаДатыВводаВыбытия.ДатаВыбытия <= ДатаКонца, ".  Выбыл из эксплуатации: " + Формат(РезультатЗапросаДатыВводаВыбытия.ДатаВыбытия, "ДФ = дд.ММ.гггг"), "");
	Область.Параметры.ПервоначальнаяСтоимость	= Актив.ПервоначальнаяСтоимость;
	Область.Параметры.СуммаАмортизации			= РезультатЗапросаПараметрыШапки.Итог("СуммаАмортизации");
	Область.Параметры.СуммаОбслуживания			= РезультатЗапросаПараметрыШапки.Итог("СуммаОбслуживания");
	Область.Параметры.БалансоваяСтоимостьНач	= РезультатЗапросаПараметрыШапки.Итог("БалансоваяСтоимостьНач");
	Область.Параметры.БалансоваяСтоимостьКон	= РезультатЗапросаПараметрыШапки.Итог("БалансоваяСтоимостьКон");
	Область.Параметры.ОстаточнаяСтоимостьНач	= РезультатЗапросаПараметрыШапки.Итог("ОстаточнаяСтоимостьНач");
	Область.Параметры.ОстаточнаяСтоимостьКон	= РезультатЗапросаПараметрыШапки.Итог("ОстаточнаяСтоимостьКон");
	Область.Параметры.КоличествоНач				= РезультатЗапросаПараметрыШапки.Итог("КоличествоНач");
	Область.Параметры.КоличествоКон				= РезультатЗапросаПараметрыШапки.Итог("КоличествоКон");
	
	Область.Параметры.УпрВалюта = СокрЛП(ВалютаУчета.Наименование);
	ТабДок.Вывести(Область);
	
	//выводим шапку таблицы
	Область = Макет.ПолучитьОбласть("ОбластьШапка");
	Область.Область(1, 2, 1, 5).Шрифт    = ОформлениеШапкаТаблицы.Шрифт;
	Область.Область(1, 2, 1, 5).ЦветФона = ОформлениеШапкаТаблицы.ЦветФона;
	ТабДок.Вывести(Область);
	
	//выводим строки таблицы
	ВыборкаДокументы = РезультатЗапросаПараметрыТЧ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Документ");
	Пока ВыборкаДокументы.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("ОбластьСтрока");
		Область.Область(1, 2, 1, 5).Шрифт    = ОформлениеСтрока.Шрифт;
		Область.Область(1, 2, 1, 5).ЦветФона = ОформлениеСтрока.ЦветФона;
		//заполняем строку
		ЗаполнитьСтрокуОтчета(Область, ВыборкаДокументы);
		ТабДок.Вывести(Область);
	КонецЦикла; 

	//выводим подвал
	Область = Макет.ПолучитьОбласть("ОбластьПодвал");
	Область.Область(1, 2, 1, 5).Шрифт    = ОформлениеПодвал.Шрифт;
	Область.Область(1, 2, 1, 5).ЦветФона = ОформлениеПодвал.ЦветФона;
	ТабДок.Вывести(Область);
	
	// выводим ТабДок
	//ТабДок.АвтоМасштаб = Истина;
	ТабДок.ФиксацияСверху = 12;
	//ТабДок.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	//ТабДок.ОтображатьСетку = Ложь;
	//ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ТолькоПросмотр  = обПраво("ЗащитаПечатныхФорм", глПрава);
	//ТабДок.Показать("Отчет по истории актива");
КонецПроцедуры // СформироватьТабличныйДокумент()


ВалютаУчета = Константы.ВалютаУправленческогоУчетаКомпании.Получить();

#КонецЕсли
#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем ИмяОтчетаДвиженийСОстатками Экспорт;             // хранит имя отчета движений с остатками
Перем ТаблицаЗапроса Экспорт;                          // хранит таблицу запроса
Перем ТаблицаВыраженийПостроителя Экспорт;             // хранит таблицу выражений построителя
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;   // хранит структуру итоговых выражений показателей


// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	
	отЗаполнитьНачальныеНастройкиДляТаблицыЗапросов(СтруктураПараметрыЗаполнения, глПрава);
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	УбратьИтогиДляЦен();
	ИзмеренияДляОтбора = Новый Структура();
	Для Каждого ТекИзмерение Из ИзмеренияСтроки Цикл
		Если НЕ ТекИзмерение.Использование Тогда Продолжить; КонецЕсли;
		Если Найти(ТекИзмерение.ПутьКДанным, "Контрагент") > 0 ИЛИ Найти(ТекИзмерение.ПутьКДанным, "Заказ")>0 Тогда
			ИмяПоля = СтрЗаменить(ТекИзмерение.ПутьКДанным, ".", "");
			ИзмеренияДляОтбора.Вставить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	
	ПустойКонтрагент = Справочники.Контрагенты.ПустаяСсылка();
	ПустойЗаказ		 = Документы.ЗаказПокупателя.ПустаяСсылка();
	
	// Удалим "пустышки" 	
	Если ИзмеренияСтроки.НайтиСтроки(Новый Структура("Использование", Истина)).Количество() > 0 Тогда
		
		к = Результат.ФиксацияСверху + 1;
		Пока к <= Результат.ВысотаТаблицы Цикл
			// Проверим каждую строку на наличие ключа (ПР)
			// если ключ найден, то это строка с регистратором или периодом, которая может являться "пустышкой"
			ТекПараметр = Результат.Область(к, 1).Текст;
			Если Лев(ТекПараметр, 3) = "СУ_" Тогда
				Результат.Область(к, 1).Текст = "";
				
				ТекПараметр  = Сред(ТекПараметр, 4);
				НомерКолонки = Число(Сред(ТекПараметр, Найти(ТекПараметр, "_") + 1));
				ИмяИзмерения = Лев(ТекПараметр, Найти(ТекПараметр, "_") - 1);
				
				Если ИзмеренияДляОтбора.Свойство(ИмяИзмерения) Тогда
					УдалятьОбласть = Ложь;
				КонецЕсли;
				
				Если НЕ УдалятьОбласть Тогда
					// "Пустышку" определяем по отсутствию расшифровки
					Попытка // вдруг в расшифровке что-нибудь непонятное
						Расшифровка = Результат.Область(к, НомерКолонки).Расшифровка;
						Если Расшифровка = Неопределено ИЛИ Расшифровка = NULL Тогда УдалятьОбласть = Истина;
						Иначе
							ЗначениеРасшифровки = Расшифровка[ИмяИзмерения];
							Если ЗначениеРасшифровки = Неопределено ИЛИ ЗначениеРасшифровки = NULL ИЛИ ЗначениеРасшифровки = ПустойКонтрагент ИЛИ ЗначениеРасшифровки = ПустойЗаказ Тогда
								УдалятьОбласть = Истина;
							КонецЕсли;
						КонецЕсли;
					Исключение КонецПопытки;	
				КонецЕсли;
				
				Если УдалятьОбласть Тогда
					Результат.УдалитьОбласть(Результат.Область(к,, к,), ТипСмещенияТабличногоДокумента.ПоВертикали);
				Иначе
					к = к + 1;
				КонецЕсли;
				
			Иначе
				УдалятьОбласть = Ложь;
				
				// Переходим на следующую строку отчета
				к = к + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// формирует отчет в виде сводной таблицы
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  ДокументРезультат - ТабличныйДокумент 
//
Функция СформироватьСводнуюТаблицу(ДокументРезультат=Неопределено) Экспорт
	
	отСформироватьСводнуюТаблицу(ЭтотОбъект, ДокументРезультат);
	
	Возврат ДокументРезультат;
	
КонецФункции // СформироватьСводнуюТаблицу()

// формирует диаграмму
//
// Параметры
//  ПолеДиаграммы – Диаграмма
//  СтруктураПараметров - структура
//
// Возвращаемое значение:
//  ПолеДиаграммы - Диаграмма
//
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
	
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров);
	
	Возврат ПолеДиаграммы;
	
КонецФункции // СформироватьДиаграмму()

// формирует отчет в виде сводной диаграммы
//
// Параметры
//  ПолеСводнойДиаграммы – СводнаяДиаграмма
//
// Возвращаемое значение:
//  ПолеСводнойДиаграммы - СводнаяДиаграмма
//
Функция СформироватьСводнуюДиаграмму(ПолеСводнойДиаграммы=Неопределено) Экспорт
	
	отСформироватьСводнуюДиаграмму(ЭтотОбъект, ПолеСводнойДиаграммы);
	
	Возврат ПолеСводнойДиаграммы;
	
КонецФункции // СформироватьСводнуюДиаграмму()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт
	
	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);
	
	// Если необходимо, добавим другие или переустановим строки форматирования полей
	
	Возврат СтруктураФорматаПолей;
	
КонецФункции	//	СтруктураФорматированияПолей()

// Процедура убирает вывод итогов для ресурсов ЦенаНачОстаток, ЦенаПриход, ЦенаРасход, ЦенаКонОстаток
// у передаваемого поля
//
// Параметры
//    Поле - Поле для которого необходимо убрать вывод итогов по вышеперечисленным ресурсам
Процедура УбратьИтогиДляЦен()
	Если НЕ Показатели.Найти("ЦенаРозн", "Имя").Использование Тогда Возврат; КонецЕсли;
	ПозицияНоменклатуры = 0;
	Для Каждого ТекСтрока Из ИзмеренияСтроки Цикл
		Если Найти(ТекСтрока.ПутьКДанным, "Номенклатура")>0 Тогда
			ПозицияНоменклатуры = ИзмеренияСтроки.Индекс(ТекСтрока);
			Прервать;
		КонецЕсли;
		ПозицияНоменклатуры = ПозицияНоменклатуры+1;
	КонецЦикла;
	
	Для Индекс=0 По ИзмеренияСтроки.Количество()-1 Цикл
		ТекСтрока = ИзмеренияСтроки[Индекс];
		ПараметрыПолейИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным, ПараметрыИспользованияПолейИПоказателей);
		Если ПараметрыПолейИзмерения=Неопределено Тогда Продолжить; КонецЕсли;
		
		Использование = Индекс >= ПозицияНоменклатуры;
		Если ПараметрыПолейИзмерения.Свойство("Использование") Тогда
			РесурсыПоля = ПараметрыПолейИзмерения.Использование.Ресурсы;
			Для Каждого ТекРесурс Из РесурсыПоля Цикл
				Если Найти(ТекРесурс.Ключ, "ЦенаРозн")=1 Тогда
					РесурсыПоля.Вставить(ТекРесурс.Ключ, Использование);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ТекСтрока.ТипИзмерения = "Иерархия" Тогда
			ПараметрыПолейИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекСтрока.ПутьКДанным+".Родитель", ПараметрыИспользованияПолейИПоказателей);
			Если ПараметрыПолейИзмерения=Неопределено Тогда Продолжить; КонецЕсли;
			РесурсыПоля = ПараметрыПолейИзмерения.Использование.Ресурсы;
			Для Каждого ТекРесурс Из РесурсыПоля Цикл
				Если Найти(ТекРесурс.Ключ, "ЦенаРозн")=1 Тогда
					РесурсыПоля.Вставить(ТекРесурс.Ключ, Ложь);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры //УбратьИтогиДляЦен()

// возвращает ключ
Функция ПолучитьКлюч(ПутьКДанным) Экспорт
	
	Ключ = "";
	Если Найти(ПутьКДанным, "Контрагент")>0 ИЛИ Найти(ПутьКДанным, "Заказ")>0 Тогда
		Ключ = "СУ_";
	ИначеЕсли ВидОтчета = Перечисления.ВидыОтчетов.ОстаткиИОбороты Тогда
		
		ПозицияРодителя = Найти(ПутьКДанным, ".");
		ПутьКДаннымРодителя = ПутьКДанным;
		Если ПозицияРодителя > 0 Тогда
			ПутьКДаннымРодителя = Лев(ПутьКДанным, ПозицияРодителя - 1);
		КонецЕсли;
		
		ОтсутствуетВОстатках = Ложь;
		Попытка // необходимых ключей может и не быть в структуре
			ОтсутствуетВОстатках = ПараметрыИспользованияПолейИПоказателей[ПутьКДаннымРодителя].Использование.ОтсутствуетВОстатках;
		Исключение КонецПопытки;
		
		Если Найти(ПутьКДанным, "ПериодРегистратор") = 1 ИЛИ
			ПутьКДанным = "ПериодДень" ИЛИ
			ПутьКДанным = "ПериодНеделя" ИЛИ
			ПутьКДанным = "ПериодМесяц" ИЛИ
			ПутьКДанным = "ПериодКвартал" ИЛИ
			ПутьКДанным = "ПериодГод" ИЛИ
			ОтсутствуетВОстатках Тогда
			Ключ = "ПР_";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ключ;
	
КонецФункции // ПолучитьКлюч()

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ИмяОтчетаДвиженийСОстатками = "ДвиженияТоваровСОстатками";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();

                                              
#КонецЕсли
#Если Клиент Тогда
// Модуль отчета
Перем СправочникСписок Экспорт; // справочник, список
Перем СправочникТабличноеПоле Экспорт; // справочник, табличное поле
Перем ПараметрыСправочника; // параметры справочника
Перем СписокОбъектов Экспорт; // список объектов печати
	
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
	
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА
	
// Получает список справочников
//
// Параметры
//	СписокСправочников - СписокЗначений
//
Процедура ПолучитьСписокСправочников(СписокСправочников) Экспорт
		
	СписокСправочников.Очистить();
	СписокОбъектов.Очистить();
	Если ПечатьСправочникаПВХ = 1 Тогда
		Для Каждого Справочник Из Метаданные.Справочники Цикл
			СписокСправочников.Добавить(Справочник.Имя,Справочник.Представление());
			СписокОбъектов.Вставить(Справочник.Имя, Справочники[Справочник.Имя].ПустаяСсылка());
		КонецЦикла;
	ИначеЕсли ПечатьСправочникаПВХ = 2 Тогда
		Для Каждого ПВХ Из Метаданные.ПланыВидовХарактеристик Цикл
			СписокСправочников.Добавить(ПВХ.Имя,ПВХ.Представление());
			СписокОбъектов.Вставить(ПВХ.Имя, ПланыВидовХарактеристик[ПВХ.Имя].ПустаяСсылка());
		КонецЦикла;
	ИначеЕсли ПечатьСправочникаПВХ = 3 Тогда
		Для Каждого ПВР Из Метаданные.ПланыВидовРасчета Цикл
			СписокСправочников.Добавить(ПВР.Имя,ПВР.Представление());
			СписокОбъектов.Вставить(ПВР.Имя, ПланыВидовРасчета[ПВР.Имя].ПустаяСсылка());
		КонецЦикла;
	ИначеЕсли ПечатьСправочникаПВХ = 4 Тогда
		Для Каждого ПланСчетов Из Метаданные.ПланыСчетов Цикл
			СписокСправочников.Добавить(ПланСчетов.Имя, ПланСчетов.Представление());
			СписокОбъектов.Вставить(ПланСчетов.Имя, ПланыСчетов[ПланСчетов.Имя].ПустаяСсылка());
		КонецЦикла;
	Иначе
		Для Каждого Справочник Из Метаданные.Справочники Цикл
			СписокСправочников.Добавить(Справочник.Имя, Справочник.Представление());
			СписокОбъектов.Вставить(Справочник.Имя, Справочники[Справочник.Имя].ПустаяСсылка());
		КонецЦикла;
			
		Для Каждого ПВХ Из Метаданные.ПланыВидовХарактеристик Цикл
			СписокСправочников.Добавить(ПВХ.Имя, ПВХ.Представление());
			СписокОбъектов.Вставить(ПВХ.Имя, ПланыВидовХарактеристик[ПВХ.Имя].ПустаяСсылка());
		КонецЦикла;
			
		Для Каждого ПВР Из Метаданные.ПланыВидовРасчета Цикл
			СписокСправочников.Добавить(ПВР.Имя,ПВР.Представление());
			СписокОбъектов.Вставить(ПВР.Имя, ПланыВидовРасчета[ПВР.Имя].ПустаяСсылка());
		КонецЦикла;
		
		Для Каждого ПланСчетов Из Метаданные.ПланыСчетов Цикл
			СписокСправочников.Добавить(ПланСчетов.Имя, ПланСчетов.Представление());
			СписокОбъектов.Вставить(ПланСчетов.Имя, ПланыСчетов[ПланСчетов.Имя].ПустаяСсылка());
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ПолучитьСписокСправочников()
	
// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета) Экспорт
	
	Если ПустаяСтрока(ИмяСправочника) Тогда
		ИмяСправочника = Метаданные.Справочники[0].Имя;
	КонецЕсли;
	Если СписокОбъектов.Количество() = 0 Тогда
		СписокОбъектов.Вставить(ИмяСправочника, Справочники[ИмяСправочника].ПустаяСсылка());
	КонецЕсли;
	СправочникСсылка = СписокОбъектов[ИмяСправочника];
	МетаданныеОбъекта = СправочникСсылка.Метаданные();
		
	Если Найти(Строка(СправочникСписок), ИмяСправочника) = 0 Тогда
		СправочникСписок = Неопределено;
	КонецЕсли; 
		
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(СправочникСсылка)) Тогда
		ПечатьСправочникаПВХ = 1;
	ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(СправочникСсылка)) Тогда
		ПечатьСправочникаПВХ = 2;
	ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(ТипЗнч(СправочникСсылка)) Тогда
		ПечатьСправочникаПВХ = 3; 
	ИначеЕсли ПланыСчетов.ТипВсеСсылки().СодержитТип(ТипЗнч(СправочникСсылка)) Тогда
		ПечатьСправочникаПВХ = 4; 
	КонецЕсли;
		
	Если ПечатьСправочникаПВХ = 1 Тогда
		НаименованиеОтчета = "Печать справочника ";
	ИначеЕсли ПечатьСправочникаПВХ = 2 Тогда
		НаименованиеОтчета = "Печать плана видов характеристик ";
	ИначеЕсли ПечатьСправочникаПВХ = 3 Тогда
		НаименованиеОтчета = "Печать плана видов расчета ";
	ИначеЕсли ПечатьСправочникаПВХ = 4 Тогда
		НаименованиеОтчета = "Печать плана счетов ";
	КонецЕсли;
		
	ПараметрыСправочника = Новый Структура("КодСуществует, ВидСправочника", );
	ПараметрыСправочника.Вставить("СправочникСсылка", СправочникСсылка);
	ПараметрыСправочника.Вставить("КодСуществует", (МетаданныеОбъекта.ДлинаКода <> 0));
	ПараметрыСправочника.Вставить("НаименованиеСуществует", (МетаданныеОбъекта.ДлинаНаименования <> 0));
	Если ПечатьСправочникаПВХ = 1 Тогда
		ПараметрыСправочника.Вставить("ВидСправочника", ?(МетаданныеОбъекта.Иерархический, ?(Строка(МетаданныеОбъекта.ВидИерархии) = "ИерархияГруппИЭлементов", "ИерархияГруппИЭлементов", "ИерархияЭлементов"), "БезИерархии"));
	ИначеЕсли ПечатьСправочникаПВХ = 2 Тогда
		ПараметрыСправочника.Вставить("ВидСправочника", ?(МетаданныеОбъекта.Иерархический, "ИерархияГруппИЭлементов", "БезИерархии"));
	ИначеЕсли ПечатьСправочникаПВХ = 3 Тогда
		ПараметрыСправочника.Вставить("ВидСправочника", "БезИерархии");
	ИначеЕсли ПечатьСправочникаПВХ = 4 Тогда
		ПараметрыСправочника.Вставить("ВидСправочника", "БезИерархии");
	КонецЕсли;
			
	// сформируем текст запроса
	//начало формирования текста построителя
	Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ" +
	?(ПараметрыСправочника.КодСуществует, "
	|" + ИмяСправочника + ".Код КАК Код,", "") + 
	?(ПараметрыСправочника.НаименованиеСуществует, "
	|" + ИмяСправочника + ".Наименование КАК Наименование,", "");
	Текст = Лев(Текст, СтрДлина(Текст)-1);
		
	//теперь добавляем все доступные поля в построитель
	Текст = Текст + "
	|{ВЫБРАТЬ";
	Текст = Текст + ?(ПараметрыСправочника.КодСуществует, "
	|" + ИмяСправочника + ".Код,", "") + "
	|" + ИмяСправочника + ".Наименование,";
	Если МетаданныеОбъекта.Реквизиты.Количество() > 0 Тогда
		Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
			Текст = Текст + "
			|" + ИмяСправочника + "." + Реквизит.Имя;
				
			// добавляем "вложенные" поля
			Попытка
				СправочникСсылка[Реквизит.Имя].Пустая();
				Текст = Текст + ".*,";
			Исключение
				Текст = Текст + ",";
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	Текст = Лев(Текст,СтрДлина(Текст)-1);
	Текст = Текст + "
	|}";
		
	//таблица справочника
	Если ПечатьСправочникаПВХ = 1 Тогда
		Текст = Текст + "
		|ИЗ Справочник." + ИмяСправочника + " КАК " + ИмяСправочника + "
		|";
	ИначеЕсли ПечатьСправочникаПВХ = 2 Тогда
		Текст = Текст + "
		|ИЗ ПланВидовХарактеристик." + ИмяСправочника + " КАК " + ИмяСправочника + "
		|";
	ИначеЕсли ПечатьСправочникаПВХ = 3 Тогда
		Текст = Текст + "
		|ИЗ ПланВидовРасчета." + ИмяСправочника + " КАК " + ИмяСправочника + "
		|";
	ИначеЕсли ПечатьСправочникаПВХ = 4 Тогда
		Текст = Текст + "
		|ИЗ ПланСчетов." + ИмяСправочника + " КАК " + ИмяСправочника + "
		|";
	КонецЕсли;
		
	//если иерархический, то доп. условие
	Текст = Текст + ?(ПараметрыСправочника.ВидСправочника = "ИерархияГруппИЭлементов", "
	|ГДЕ " + ИмяСправочника + ".ЭтоГруппа = ЛОЖЬ", "");
		
	//и все возможные отборы и сортировки
	Текст1 = "
	|{ГДЕ";
	Текст2 = "
	|{УПОРЯДОЧИТЬ ПО";
	Если СправочникСписок <> Неопределено Тогда	
		Для Каждого Колонка Из СправочникСписок.Колонки Цикл
			Если Колонка.Имя = "Ссылка" ИЛИ Колонка.Имя="Картинка" ИЛИ ПустаяСтрока(Колонка.Имя) Тогда // ИЛИ Колонка.Имя = "ЭтоГруппа"
				Продолжить;
			КонецЕсли;
			Текст1 = Текст1 + "
			|" + ИмяСправочника + "." + Колонка.Имя + ",";
			Текст2 = Текст2 + "
			|" + ИмяСправочника + "." + Колонка.Имя + ",";
		КонецЦикла;
	Иначе
		Текст1 = Текст1 +
		?(ПараметрыСправочника.КодСуществует, "
		|" + ИмяСправочника + ".Код,", "") +
		?(ПараметрыСправочника.НаименованиеСуществует, "
		|" + ИмяСправочника + ".Наименование,", "") + "
		|" + ИмяСправочника + ".Ссылка,";
		Текст2 = Текст2 +
		?(ПараметрыСправочника.КодСуществует, "
		|" + ИмяСправочника + ".Код,", "") +
		?(ПараметрыСправочника.НаименованиеСуществует, "
		|" + ИмяСправочника + ".Наименование,", "") + "
		|" + ИмяСправочника + ".Ссылка,";
	КонецЕсли;
	Если МетаданныеОбъекта.Реквизиты.Количество() > 0 Тогда
		Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
			Текст1 = Текст1 + "
			|" + ИмяСправочника + "." + Реквизит.Имя + ",";
			Текст2 = Текст2 + "
			|" + ИмяСправочника + "." + Реквизит.Имя + ",";
		КонецЦикла;
	КонецЕсли;
	Текст1 = Лев(Текст1,СтрДлина(Текст1)-1);
	Текст2 = Лев(Текст2,СтрДлина(Текст2)-1);
	Текст = Текст + Текст1 + "
	|}" + Текст2 + "
	|}";
	Текст = Текст + "
	|ИТОГИ ПО Ссылка " + ?(ПараметрыСправочника.ВидСправочника = "БезИерархии", "КАК", "ТОЛЬКО ИЕРАРХИЯ КАК") +  " Ссылка";
	Текст = Текст + "
	|АВТОУПОРЯДОЧИВАНИЕ"; //конец формирования текста построителя
		
	// установим текст построителя
	ПостроительОтчета.Текст = Текст;
		
	// перенесем настройки из текста в табл. поля настроек построителя
	ПостроительОтчета.ЗаполнитьНастройки();
		
	// для иерархического справочника установим тип измерения строки
	Если ПостроительОтчета.ИзмеренияСтроки.Количество() <> 0 Тогда
		ПостроительОтчета.ИзмеренияСтроки[0].ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия;
	КонецЕсли;
		
	// представления полей
	Для каждого ТекПоле Из ПостроительОтчета.ДоступныеПоля Цикл
		ПредставлениеРеквизита = МетаданныеОбъекта.Реквизиты.Найти(ТекПоле.Имя);
		ПредставлениеРеквизита = ?(ПредставлениеРеквизита = Неопределено, "", ПредставлениеРеквизита.Синоним);
		Если ПредставлениеРеквизита <> "" И ПредставлениеРеквизита <> ТекПоле.Имя Тогда
			ТекПоле.Представление = ПредставлениеРеквизита;
		КонецЕсли;
	КонецЦикла;
		
	// установим выбранные поля в зависимости от видимости колонок
	Если СправочникТабличноеПоле<>Неопределено Тогда
		ПостроительОтчета.ВыбранныеПоля.Очистить();
		Для Каждого Колонка Из СправочникТабличноеПоле.Колонки Цикл
			Если НЕ Колонка.Видимость Тогда Продолжить; КонецЕсли;
			Попытка НовоеПоле=ПостроительОтчета.ВыбранныеПоля.Добавить(Колонка.Данные); Исключение Продолжить; КонецПопытки;
			НовоеПоле.Представление=Колонка.ТекстШапки;
		КонецЦикла;
	КонецЕсли;
		
	Если СправочникСписок <> Неопределено Тогда
		// отборы
		Для Каждого ЭлементОтбора Из СправочникСписок.Отбор Цикл
			Если НЕ ЭлементОтбора.Использование Тогда Продолжить; КонецЕсли;
			Попытка
				НовыйОтбор=ПостроительОтчета.Отбор.Добавить(ЭлементОтбора.ПутьКДанным);
			Исключение
				Продолжить;
			КонецПопытки;
			НовыйОтбор.Использование=ЭлементОтбора.Использование;
			НовыйОтбор.ВидСравнения=ЭлементОтбора.ВидСравнения;
			НовыйОтбор.Значение=ЭлементОтбора.Значение;
			НовыйОтбор.ЗначениеПо=ЭлементОтбора.ЗначениеПо;
			НовыйОтбор.ЗначениеС=ЭлементОтбора.ЗначениеС;
			НовыйОтбор.Представление=ЭлементОтбора.Представление;
		КонецЦикла;
			
		// добавим отбор по родителю
		Если ПараметрыСправочника.ВидСправочника = "ИерархияГруппИЭлементов" И СправочникТабличноеПоле <> Неопределено Тогда
			Если НЕ СправочникТабличноеПоле.ТекущийРодитель.Пустая() Тогда
				НовыйОтбор=ПостроительОтчета.Отбор.Добавить("Родитель");
				НовыйОтбор.Использование=ИСТИНА;
				НовыйОтбор.Значение=СправочникТабличноеПоле.ТекущийРодитель;
			КонецЕсли;
		КонецЕсли;
			
		// порядок тоже попытаемся взять отсюда
		ПостроительОтчета.Порядок.Очистить();
		Для Каждого ЭлементПорядка Из СправочникСписок.Порядок Цикл
			Попытка
				ПостроительОтчета.Порядок.Добавить(ЭлементПорядка.ПутьКДанным,,ЭлементПорядка.Представление,ЭлементПорядка.Направление);
			Исключение
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры // ЗаполнитьНачальныеНастройки()
	
// Подготавливает макет вывода для построителя
//
// Параметры
//	ИсходныйМакет - ТабличныйДокумент
//	МакетОформления - ТабличныйДокумент
//
// Возвращаемое значение:
//	КонечныйМакет - ТабличныйДокумент
//
Функция СформатированныйМакетПостроителя(ИсходныйМакет, МакетОформления = Неопределено)
	// установим текст заголовка отчета
	МетаданныеОбъекта = ПараметрыСправочника.СправочникСсылка.Метаданные();
	Если ПечатьСправочникаПВХ = 1 Тогда
		ИсходныйМакет.Параметры.ЗаголовокОтчета = "Справочник """ + ?(МетаданныеОбъекта.Синоним = "", ИмяСправочника, МетаданныеОбъекта.Синоним) + """";
	ИначеЕсли ПечатьСправочникаПВХ = 2 Тогда
		ИсходныйМакет.Параметры.ЗаголовокОтчета = "План видов характеристик """ + ?(МетаданныеОбъекта.Синоним = "", ИмяСправочника, МетаданныеОбъекта.Синоним) + """";
	ИначеЕсли ПечатьСправочникаПВХ = 3 Тогда
		ИсходныйМакет.Параметры.ЗаголовокОтчета = "План видов расчета """ + ?(МетаданныеОбъекта.Синоним = "", ИмяСправочника, МетаданныеОбъекта.Синоним) + """";
	ИначеЕсли ПечатьСправочникаПВХ = 4 Тогда
		ИсходныйМакет.Параметры.ЗаголовокОтчета = "План счетов """ + ?(МетаданныеОбъекта.Синоним = "", ИмяСправочника, МетаданныеОбъекта.Синоним) + """";
	КонецЕсли;
	ТекстОтбор = "";
	Для каждого ТекОтбор Из ПостроительОтчета.Отбор Цикл
		ТекстОтбор = ТекстОтбор + ", " + ТекОтбор.Представление + " " + НРег(ТекОтбор.ВидСравнения) + " " + Строка(ТекОтбор.Значение);
	КонецЦикла;
	ИсходныйМакет.Параметры.Отбор = ?(ПостроительОтчета.Отбор.Количество() > 0, "Отбор: ", "") + Прав(ТекстОтбор, СтрДлина(ТекстОтбор) - 2);
	ТекстКолонки = "Колонки: ";
	Для каждого ТекВыбПоле Из ПостроительОтчета.ВыбранныеПоля Цикл
		ТекстКолонки = ТекстКолонки + ТекВыбПоле.Представление + ", ";
	КонецЦикла;
	ИсходныйМакет.Параметры.Колонки = Лев(ТекстКолонки, СтрДлина(ТекстКолонки) - 2);
		
	//инициализация новых областей
	ОбластьШапка  = Новый ТабличныйДокумент;
	ОбластьСтрока = Новый ТабличныйДокумент;
	ОбластьСтрокаИерархия = Новый ТабличныйДокумент;
	ОбластьДетальныеЗаписи = Новый ТабличныйДокумент;
	ОбластьПодвал = Новый ТабличныйДокумент;
	//начальная область
	ОбластьШапка.Вывести(ИсходныйМакет.ПолучитьОбласть("ШапкаТаблицы|ОбластьЛеваяГраница"));
	ОбластьСтрокаИерархия.Вывести(ИсходныйМакет.ПолучитьОбласть("СтрокаИерархия|ОбластьЛеваяГраница"));
	ОбластьСтрока.Вывести(ИсходныйМакет.ПолучитьОбласть("Строка|ОбластьЛеваяГраница"));
	ОбластьДетальныеЗаписи.Вывести(ИсходныйМакет.ПолучитьОбласть("Детали|ОбластьЛеваяГраница"));
	ОбластьПодвал.Вывести(ИсходныйМакет.ПолучитьОбласть("Подвал|ОбластьЛеваяГраница"));
	//ширина колонок таблицы
	мсвШирина = Новый Массив;
		
	// присоединяем колонки
	Для каждого ТекВыбПоле Из ПостроительОтчета.ВыбранныеПоля Цикл
		//определим тип реквизита
		ТипРеквизита = Неопределено;
		Попытка ТипРеквизита = МетаданныеОбъекта.Реквизиты.Найти(ТекВыбПоле.Имя).Тип;
		Исключение
		КонецПопытки;
		//найдем область макета, соответствующую типу реквизита
		Если ТекВыбПоле.Имя = "Код" Тогда
			Если НЕ ПараметрыСправочника.КодСуществует Тогда
				Продолжить;
			КонецЕсли;
			ИмяКолонки = "Код";
		ИначеЕсли ТекВыбПоле.Имя = "Наименование" Тогда
			Если НЕ ПараметрыСправочника.НаименованиеСуществует Тогда
				Продолжить;
			КонецЕсли;
			ИмяКолонки = "Наименование";
		ИначеЕсли ТипРеквизита = Тип("Число") Тогда
			ИмяКолонки = "Число";
		ИначеЕсли ТипРеквизита = Тип("Дата") Тогда
			ИмяКолонки = "Дата";
		ИначеЕсли Лев(Строка(ТипРеквизита), 17) = "Справочник ссылка" Тогда
			ИмяКолонки = "Справочник";
		ИначеЕсли Лев(Строка(ТипРеквизита), 15) = "Документ ссылка" Тогда
			ИмяКолонки = "Документ";
		Иначе
			ИмяКолонки = "Произвольный";
		КонецЕсли;
		//получаем области из исходного макета
		ОбластьШапкаКолонка = ИсходныйМакет.ПолучитьОбласть("ШапкаТаблицы|Область" + ИмяКолонки);
		ОбластьСтрокаИерархияКолонка = ИсходныйМакет.ПолучитьОбласть("СтрокаИерархия|Область" + ИмяКолонки);
		ОбластьСтрокаКолонка = ИсходныйМакет.ПолучитьОбласть("Строка|Область" + ИмяКолонки);
		ОбластьДеталиКолонка = ИсходныйМакет.ПолучитьОбласть("Детали|Область" + ИмяКолонки);
		ОбластьПодвалКолонка = ИсходныйМакет.ПолучитьОбласть("Подвал|Область" + ИмяКолонки);
		//заполняем ширину колонок
		мсвШирина.Добавить(ОбластьШапкаКолонка.Область().ШиринаКолонки);
		//устанавливаем текст
		ОбластьШапкаКолонка.Область().Текст = ТекВыбПоле.Представление;
		ОбластьСтрокаКолонка.Область().Текст = ТекВыбПоле.Имя;
		ОбластьСтрокаИерархияКолонка.Область().Текст = ТекВыбПоле.Имя;
		ОбластьДеталиКолонка.Область().Текст = ТекВыбПоле.Имя;
		//присоединяем полученные области к выходному макету		
		ОбластьШапка.Присоединить(ОбластьШапкаКолонка);
		ОбластьСтрока.Присоединить(ОбластьСтрокаКолонка);
		ОбластьСтрокаИерархия.Присоединить(ОбластьСтрокаИерархияКолонка);
		ОбластьДетальныеЗаписи.Присоединить(ОбластьДеталиКолонка);
		ОбластьПодвал.Присоединить(ОбластьПодвалКолонка);
	КонецЦикла;
		
	//присоединяем конечную колонку
	ОбластьШапка.Присоединить(ИсходныйМакет.ПолучитьОбласть("ШапкаТаблицы|ОбластьПраваяГраница"));
	ОбластьСтрокаИерархия.Присоединить(ИсходныйМакет.ПолучитьОбласть("СтрокаИерархия|ОбластьПраваяГраница"));
	ОбластьСтрока.Присоединить(ИсходныйМакет.ПолучитьОбласть("Строка|ОбластьПраваяГраница"));
	ОбластьДетальныеЗаписи.Присоединить(ИсходныйМакет.ПолучитьОбласть("Детали|ОбластьПраваяГраница"));
	ОбластьПодвал.Присоединить(ИсходныйМакет.ПолучитьОбласть("Подвал|ОбластьПраваяГраница"));
		
	//выводим в выходной макет
	КонечныйМакет = Новый ТабличныйДокумент;
	КонечныйМакет.Вывести(ИсходныйМакет.ПолучитьОбласть("Заголовок|ОбластьЛеваяГраница"));
	КонечныйМакет.Вывести(ОбластьШапка);
	КонечныйМакет.Вывести(ОбластьСтрокаИерархия);
	КонечныйМакет.Вывести(ОбластьСтрока);
	КонечныйМакет.Вывести(ОбластьДетальныеЗаписи);
	КонечныйМакет.Вывести(ОбластьПодвал);
		
	//именуем области
	КонечныйМакет.Область(1, , 3, ).Имя = "Заголовок";
	КонечныйМакет.Область(4, , 4, ).Имя = "ШапкаТаблицы";
	КонечныйМакет.Область(5, , 5, ).Имя = "СсылкаИерархия";
	КонечныйМакет.Область(6, , 6, ).Имя = "Ссылка";
	КонечныйМакет.Область(7, , 7, ).Имя = "Детали";
	КонечныйМакет.Область(8, , 8, ).Имя = "Подвал";
	//параметры заполнения
	КонечныйМакет.Область(5, 2, 5, мсвШирина.Количество()+1).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	КонечныйМакет.Область(6, 2, 6, мсвШирина.Количество()+1).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	КонечныйМакет.Область(7, 2, 7, мсвШирина.Количество()+1).Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	//расшифровка
	ЗначениеРасшифровки = "Ссылка";
	КонечныйМакет.Область(5, 2, 5, мсвШирина.Количество()+1).ПараметрРасшифровки = ЗначениеРасшифровки;
	КонечныйМакет.Область(6, 2, 6, мсвШирина.Количество()+1).ПараметрРасшифровки = ЗначениеРасшифровки;
	КонечныйМакет.Область(7, 2, 7, мсвШирина.Количество()+1).ПараметрРасшифровки = ЗначениеРасшифровки;
	//автоотступ для иерархии
	КонечныйМакет.Область(5, 2, 5, 2).АвтоОтступ = 1;
	КонечныйМакет.Область(6, 2, 6, 2).АвтоОтступ = 1;
	//корректируем ширину колонок
	Для НомКол = 2 По мсвШирина.Количество()+1 Цикл
		КонечныйМакет.Область(1, НомКол, 1, НомКол).ШиринаКолонки = мсвШирина[НомКол-2];
	КонецЦикла;
		
	//посмотрим, нужно ли оформлять макет
	Если МакетОформления = Неопределено Тогда
		Возврат КонечныйМакет;
	КонецЕсли;
	//оформление
	ОформлениеШапкаТаблицы = МакетОформления.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеСтрока = МакетОформления.ПолучитьОбласть("ОформлениеИзмерений").Область(8, 1);
	ОформлениеСтрокаИерархия = МакетОформления.ПолучитьОбласть("ОформлениеИерархии").Область(1, 1);
	ОформлениеПодвал = МакетОформления.ПолучитьОбласть("ОформлениеИтоги").Область(1, 1);
	КонечныйМакет.Область(4, 2, 4, мсвШирина.Количество()+1).Шрифт = ОформлениеШапкаТаблицы.Шрифт;
	КонечныйМакет.Область(4, 2, 4, мсвШирина.Количество()+1).ЦветФона = ОформлениеШапкаТаблицы.ЦветФона;
	КонечныйМакет.Область(5, 2, 5, мсвШирина.Количество()+1).Шрифт = ОформлениеСтрокаИерархия.Шрифт;
	КонечныйМакет.Область(5, 2, 5, мсвШирина.Количество()+1).ЦветФона = ОформлениеСтрокаИерархия.ЦветФона;
	КонечныйМакет.Область(6, 2, 6, мсвШирина.Количество()+1).Шрифт = ОформлениеСтрока.Шрифт;
	КонечныйМакет.Область(6, 2, 6, мсвШирина.Количество()+1).ЦветФона = ОформлениеСтрока.ЦветФона;
	КонечныйМакет.Область(7, 2, 7, мсвШирина.Количество()+1).Шрифт = ОформлениеСтрока.Шрифт;
	КонечныйМакет.Область(7, 2, 7, мсвШирина.Количество()+1).ЦветФона = ОформлениеСтрока.ЦветФона;
	КонечныйМакет.Область(8, 2, 8, мсвШирина.Количество()+1).Шрифт = ОформлениеПодвал.Шрифт;
	КонечныйМакет.Область(8, 2, 8, мсвШирина.Количество()+1).ЦветФона = ОформлениеПодвал.ЦветФона;
		
	Возврат КонечныйМакет;
КонецФункции // СформатированныйМакетПостроителя()
	
// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Отборы=ПостроительОтчета.Отбор;
	ПоследнийИндекс=Отборы.Количество()-1;
	Пока ПоследнийИндекс>=0 Цикл
		ТекОтбор=Отборы[ПоследнийИндекс];
		Если ПустаяСтрока(ТекОтбор.ПутьКДанным) Тогда
			Отборы.Удалить(ПоследнийИндекс);
		КонецЕсли;
		ПоследнийИндекс=ПоследнийИндекс-1;
	КонецЦикла;
	
	// проверим входной параметр на корректность
	Если ТипЗнч(ДокументРезультат)<>Тип("ПолеТабличногоДокумента") И
		ТипЗнч(ДокументРезультат)<>Тип("ТабличныйДокумент") Тогда
		ДокументРезультат = Новый ТабличныйДокумент();
	Иначе	
		ДокументРезультат.Очистить();	
	КонецЕсли;
	// если не выбрано ни одно поле
	Если ПостроительОтчета.ВыбранныеПоля.Количество() = 0 Тогда
		Сообщить("Не выбрано ни одно поле!");
		Возврат ДокументРезультат;
	КонецЕсли;
		
	// получим макеты оформления и результата и обработаем их
	ПостроительОтчета.Макет = Неопределено;
	Попытка МакетОформление = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента); Исключение МакетОформление = Неопределено; КонецПопытки;
	МакетРезультат  = ПолучитьМакет("МакетРезультат"); 
	ПостроительОтчета.Макет = СформатированныйМакетПостроителя(МакетРезультат, МакетОформление);
		
	// расшифровка
	ПостроительОтчета.ЗаполнениеРасшифровки = ВидЗаполненияРасшифровкиПостроителяОтчета.Расшифровка;
		
	// установим свойства построителя
	ПостроительОтчета.РазмещениеИзмеренийВСтроках = ТипРазмещенияИзмерений.Вместе;
	ПостроительОтчета.РазмещениеРеквизитовИзмеренийВСтроках = ТипРазмещенияРеквизитовИзмерений.Отдельно;
	ПостроительОтчета.РазмещениеРеквизитовИзмеренийВКолонках = ТипРазмещенияРеквизитовИзмерений.Отдельно;
		
	// выполним запрос
	ПостроительОтчета.Выполнить();
	
	// выведем что получилось
	ПостроительОтчета.Вывести(ДокументРезультат);
		
	// параметры табличного документа
	ДокументРезультат.ФиксацияСверху = 4;
	ДокументРезультат.ФиксацияСлева  = 2;
	ДокументРезультат.ТолькоПросмотр = обПраво("ЗащитаПечатныхФорм", глПрава);
	ДокументРезультат.АвтоМасштаб = Истина;
		
	Возврат ДокументРезультат;
КонецФункции // СформироватьТабличныйДокумент()
	
	
//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
СписокОбъектов = Новый Структура();
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
#КонецЕсли
#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ТекстЗапроса Экспорт;                            // хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // хранит параметры использования полей и показателей
Перем ДеревоДоступныхПолей Экспорт;                    // хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // хранит дополнительные поля отчета
Перем тзРасширенныйСписокПоказателей;                  // хранит расширенный список полей
Перем НастройкиФормированияОтчета;                     // хранит настройки формирования отчета


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Устанавливает интервал
//
// Параметры
//	Изменение - число
//	ДатаНачалаПериодаФорыОтчета - Дата
//	ДатаКонцаФормыПериодаОтчета - Дата
//
Процедура УстановитьИнтервал(Изменение=0, ДатаНачалаПериодаФорыОтчета="", ДатаКонцаФормыПериодаОтчета="") Экспорт
	
	Если ДатаНачалаПериодаФорыОтчета<>"" Тогда
		ДатаНачала = ДатаНачалаПериодаФорыОтчета;
	КонецЕсли;
	
	Если ДатаНачалаПериодаФорыОтчета<>"" Тогда
		ДатаКонца = ДатаКонцаФормыПериодаОтчета;
	КонецЕсли; 	
	
	Если СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Месяц Тогда
		ДатаНачала = НачалоМесяца(ДобавитьМесяц(ДатаНачала, Изменение));
		ДатаКонца  = КонецМесяца(ДобавитьМесяц(ДатаКонца,Изменение));
		Если ДатаКонца<ДатаНачала Тогда
			ДатаКонца = КонецМесяца(ДатаНачала);
		КонецЕсли;
		
	ИначеЕсли СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Квартал Тогда
		ДатаНачала = НачалоКвартала(ДобавитьМесяц(ДатаНачала, Изменение*3));
		ДатаКонца  = КонецКвартала(ДобавитьМесяц(ДатаКонца,Изменение*3));
		Если ДатаКонца<ДатаНачала Тогда
			ДатаКонца = КонецКвартала(ДатаНачала);
		КонецЕсли;
	ИначеЕсли СценарийПланирования.Периодичность=Перечисления.ПериодичностьПланирования.Год Тогда
		ДатаНачала = НачалоГода(ДобавитьМесяц(ДатаНачала, Изменение*12));
		ДатаКонца  = КонецГода(ДобавитьМесяц(ДатаКонца,Изменение*12));
		Если ДатаКонца<ДатаНачала Тогда
			ДатаКонца = КонецГода(ДатаНачала);
		КонецЕсли;
	КонецЕсли;
	
	ДатаНачалаПериодаФорыОтчета = ДатаНачала;
	ДатаКонцаФормыПериодаОтчета = ДатаКонца;
КонецПроцедуры // отПолучитьМакетПостроителя_Переопределение()

// Специальная настройка измерений
//
//Без параметров
//
Процедура СпециальнаяНастройкаИзмерений() Экспорт
	
	НайденнаяСтрока = ДеревоДоступныхПолей.Строки.Найти("Период", "ПутьКДанным");
	Если НайденнаяСтрока<> Неопределено Тогда
		ДеревоДоступныхПолей.Строки.Удалить(НайденнаяСтрока);
	КонецЕсли;
	
КонецПроцедуры // СпециальнаяНастройкаИзмерений()

// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – название макета
//  ИмяМакетаПараметров  – строка – название макета параметров
//
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	ПостроительОтчета.Параметры.Вставить("ВалютаРегл", Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить());
	СпециальнаяНастройкаИзмерений();
	
	МакетПараметры = ПолучитьМакет(ИмяМакетаПараметров);
	ОбластьГрупп = МакетПараметры.ПолучитьОбласть("Группы");
	
	// Заполним Группы отчета
	ДеревоПоказателей.Строки.Очистить();
	
	Для к = 1 ПО ОбластьГрупп.ВысотаТаблицы Цикл
		Идентификатор = ОбластьГрупп.Область(к, 1).Текст;
		Представление = ОбластьГрупп.Область(к, 2).Текст;
		Представление = ?(ПустаяСтрока(Представление), Идентификатор, Представление);
		Использование  = ВРег(ОбластьГрупп.Область(к, 3).Текст) = "ИСТИНА";
		
		НоваяГруппа = ДеревоПоказателей.Строки.Добавить();
		НоваяГруппа.Имя = Идентификатор;
		НоваяГруппа.ПутьКДанным = Идентификатор;
		НоваяГруппа.Представление = Представление;
		НоваяГруппа.Группа = Истина;
		НоваяГруппа.КартинкаЭлемента = 6;
	КонецЦикла;
	
	Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
		Для Каждого ТекПоказатель Из Показатели Цикл
			НоваяСтрока = ТекГруппа.Строки.Добавить();
			НоваяСтрока.Использование	= ?(ТекПоказатель.Использование,1,0);
			НоваяСтрока.Имя 			= ТекПоказатель.Имя;
			НоваяСтрока.ПутьКДанным 	= ТекПоказатель.Имя+ТекГруппа.Имя;
			НоваяСтрока.Представление	= ТекПоказатель.Представление;
			НоваяСтрока.ФорматнаяСтрока	= ТекПоказатель.ФорматнаяСтрока;
			НоваяСтрока.ШиринаКолонки	= ТекПоказатель.ШиринаКолонки;
			НоваяСтрока.Группа			= Ложь;
			НоваяСтрока.КартинкаЭлемента= 7;
		КонецЦикла;
		КоличествоВключенных = ТекГруппа.Строки.НайтиСтроки(Новый Структура("Использование", 1)).Количество();
		Если КоличествоВключенных=0 Тогда
			ТекГруппа.Использование = 0;
		ИначеЕсли ТекГруппа.Строки.Количество() = КоличествоВключенных Тогда
			ТекГруппа.Использование = 1;
		Иначе
			ТекГруппа.Использование = 2;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Если обЗначениеНеЗаполнено(СценарийПланирования) Тогда
		Предупреждение("Необходимо выбрать сценарий планирования!");
		Возврат ДокументРезультат;
	КонецЕсли;
	
	ВременныйТекстЗапроса = ТекстЗапроса;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Месяц", СокрЛП(СценарийПланирования.Периодичность));
	
	ИзмеренияКолонки.Очистить();
	Если РазворачиватьПоПериодам Тогда
		СтрокаПоиска = ИзмеренияКолонки.Добавить();
		СтрокаПоиска.Использование = Истина;
		СтрокаПоиска.ПутьКДанным   = "Период";
		СтрокаПоиска.Представление = СокрЛП(СценарийПланирования.Периодичность);
		СтрокаПоиска.ТипИзмерения  = "Элементы";
	КонецЕсли;
	
	// Установим отбор по сценарию планирования
	ПостроительОтчета.Параметры.Вставить("СценарийПланирования", СценарийПланирования);
	
	// Исправим текст запроса
	Если ВыводитьНезапланированныеДанные ИЛИ НЕ РазворачиватьПоПериодам Тогда
		ПостроительОтчета.Параметры.Вставить("ВыводитьВсеЗаписи", Истина);
	Иначе
		ПостроительОтчета.Параметры.Вставить("ВыводитьВсеЗаписи", Ложь);
	КонецЕсли;
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат,, глПрава);
	
	ТекстЗапроса = ВременныйТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции // ЗаполнитьНачальныеНастройки()

// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура
//
Функция СтруктураФорматированияПолей() Экспорт
	
	СтруктураФорматаПолей = Новый Структура;
	СтруктураФорматаПолей.Вставить("ПериодГод",     "ДФ='гггг ""г.""'");
	СтруктураФорматаПолей.Вставить("ПериодКвартал", "ДФ='к ""квартал"" гггг ""г.""'");
	СтруктураФорматаПолей.Вставить("ПериодМесяц",   "ДФ='ММММ гггг ""г.""'");
	СтруктураФорматаПолей.Вставить("ПериодНеделя",  "ДФ='""Неделя с"" дд.ММ.гггг ""г.""'");
	СтруктураФорматаПолей.Вставить("ПериодДень",    "ДФ=дд.ММ.гггг");
	
	Для Каждого ТекФункция Из Функции Цикл
		Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
		СтруктураФорматаПолей.Вставить(ТекФункция.Имя, "");
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			СтруктураФорматаПолей.Вставить(ТекГруппа.Имя, "");
			Для Каждого ТекПоказатель Из ТекГруппа.Строки Цикл
				Если НЕ ТекПоказатель.Использование Тогда Продолжить; КонецЕсли;
				СтруктураФорматаПолей.Вставить(ТекПоказатель.ПутьКДанным+ТекФункция.Имя, ТекПоказатель.ФорматнаяСтрока);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	Возврат СтруктураФорматаПолей;
	
КонецФункции	//	СтруктураФорматированияПолей()

// устанавливает флаг использования у измерений
Функция УстановитьИспользованиеИзмерений(Построитель) Экспорт
	
	Для Каждого ТекФункция Из Функции Цикл
		Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			Для Каждого ТекСтрока Из ТекГруппа.Строки Цикл
				Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
				ИмяРесурса = ТекСтрока.ПутьКДанным+ТекФункция.Имя;
				Если Построитель.ДоступныеПоля.Найти(ИмяРесурса) <> Неопределено Тогда
					Построитель.ВыбранныеПоля.Добавить(ИмяРесурса);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// корректировка таблицы заголовка
Процедура ОбработкаТаблицыЗаголовка(ТаблицаЗаголовка) Экспорт
	
	НоваяСтрока = ТаблицаЗаголовка.Вставить(1);
	НоваяСтрока.Текст = "Сценарий планирования: " + Строка(СценарийПланирования);
	НоваяСтрока.ЦветТекста = ЦветаСтиля.ЦветФонаВыделенияПоля;
	НоваяСтрока.Шрифт = Новый Шрифт(ТаблицаЗаголовка[0].Шрифт,, 10, Истина);
	
КонецПроцедуры

// проверка заполнения
//Функция КорректностьЗаполнения(ПродолжитьФормирование) Экспорт
//	
//	ПродолжитьФормирование = Истина;
//	Если обЗначениеНеЗаполнено(СценарийПланирования) Тогда
//		Предупреждение("Необходимо выбрать сценарий планирования!");
//		ПродолжитьФормирование = Ложь;
//	КонецЕсли;
//	
//	Возврат Ложь;
//	
//КонецФункции

// Функция возвращает оформленный табличный документ для использования в качестве макета построителя отчета
//
// Параметры:
//	ОтчетОбъект       - ОтчетОбъект       - Объект отчета
//	ПостроительОтчета - ПостроительОтчета - Построитель отчета
//
// Возвращаемое значение:
//	ТабличныйДокумент - Макет отчета
//
//Функция отПолучитьМакетПостроителя(ОтчетОбъект, ПостроительОтчета = Неопределено) Экспорт
Функция ПолучитьМакетПостроителя(ТекПостроительОтчета, МакетОтчета, ПараметрыОформления) Экспорт
	
	ОбластьОформлениеИзмерений	= ПараметрыОформления.ОбластьОформлениеИзмерений;
	ОформлениеШапки 			= ПараметрыОформления.ОформлениеШапки;
	ОформлениеИтоги				= ПараметрыОформления.ОформлениеИтоги;
	ГраницаМакета				= ПараметрыОформления.ГраницаМакета;
	
	Попытка	   МакетОформления = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента);
	Исключение МакетОформления = ПолучитьОбщийМакет("МакетОформлениеОтчета");
	КонецПопытки;
	Попытка	   ДополнительныеПоляВОтдельнойКолонке = ВыводитьДополнительныеПоляВОтдельнойКолонке;
	Исключение ДополнительныеПоляВОтдельнойКолонке = Ложь;
	КонецПопытки; 
	Попытка    СтруктураФорматаПолей = СтруктураФорматированияПолей();
	Исключение СтруктураФорматаПолей = Новый Структура();
	КонецПопытки;
	
	ОбластьОформлениеГраниц = Неопределено;
	
	Попытка 
		ОбластьОформлениеГраниц    = МакетОформления.ПолучитьОбласть("ОформлениеГраниц");
	Исключение
	КонецПопытки;
	
	Если ОбластьОформлениеГраниц<>Неопределено Тогда
		ЗначениеГраницОтчета  = ОбластьОформлениеГраниц.Области.ЗначениеГраницОтчета;
		ВключитьГраницуСправа = ЗначениеГраницОтчета.АвтоОтступ=1;
	Иначе
		ЗначениеГраницОтчета  = Новый Структура("ГраницаСправа", Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1));
		ВключитьГраницуСправа = Истина;
	КонецЕсли;
	
	ЗаголовокШапки    = "";
	КоличествоУровней = 0;
	Для Каждого ТекИзмерение Из ТекПостроительОтчета.ИзмеренияСтроки Цикл
		ЗаголовокШапки = ЗаголовокШапки + ?(ЗаголовокШапки = "", "", " / ") + ТекИзмерение.Представление;
		
		Если ТекИзмерение.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			КоличествоУровней = КоличествоУровней + 1;
		КонецЕсли;	
		
		КоличествоУровней = КоличествоУровней + 1;
	КонецЦикла;
	
	КоличествоРазверток = 0;
	Для Каждого ТекИзмерение Из ТекПостроительОтчета.ИзмеренияКолонки Цикл
		Если ТекИзмерение.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			КоличествоРазверток = КоличествоРазверток + 1;
		КонецЕсли;
		
		КоличествоРазверток = КоличествоРазверток + 1;
	КонецЦикла;
	
	ПеремГраницаСправа = ?(ВключитьГраницуСправа, ЗначениеГраницОтчета.ГраницаСправа, ?(КоличествоРазверток>0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева));
	
	РесурсыОтчета = Новый Массив();
	ДеревоРесурсов = Новый ДеревоЗначений;
	ДеревоРесурсов.Колонки.Добавить("ИмяРесурса");
	ДеревоРесурсов.Колонки.Добавить("Представление");
	ДеревоРесурсов.Колонки.Добавить("ФорматнаяСтрока");
	ДеревоРесурсов.Колонки.Добавить("ШиринаКолонки");
	ДеревоРесурсов.Колонки.Добавить("Одиночный");
	
	КоличествоУровнейДереваПоказателей = 0;
	Для Каждого ТекФункция Из Функции Цикл
		Если НЕ ТекФункция.Использование Тогда Продолжить; КонецЕсли;
		НоваяФункция = ДеревоРесурсов.Строки.Добавить();
		НоваяФункция.ИмяРесурса = ТекФункция.Имя;
		НоваяФункция.Представление = ТекФункция.Представление;
		НоваяФункция.ФорматнаяСтрока = "";
		НоваяФункция.ШиринаКолонки = 20;
		НоваяФункция.Одиночный = Ложь;
		КоличествоУровнейДереваПоказателей = Макс(1, КоличествоУровнейДереваПоказателей);
		Для Каждого ТекГруппа Из ДеревоПоказателей.Строки Цикл
			Если НЕ ТекГруппа.Использование Тогда Продолжить; КонецЕсли;
			НоваяГруппа = НоваяФункция.Строки.Добавить();
			НоваяГруппа.ИмяРесурса = ТекГруппа.ПутьКДанным;
			НоваяГруппа.Представление = ТекГруппа.Представление;
			НоваяГруппа.ФорматнаяСтрока = ТекГруппа.ФорматнаяСтрока;
			НоваяГруппа.ШиринаКолонки = ТекГруппа.ШиринаКолонки;
			НоваяГруппа.Одиночный = Ложь;
			КоличествоУровнейДереваПоказателей = Макс(2, КоличествоУровнейДереваПоказателей);
			Для Каждого ТекСтрока Из ТекГруппа.Строки Цикл
				Если НЕ ТекСтрока.Использование Тогда Продолжить; КонецЕсли;
				РесурсыОтчета.Добавить(ТекСтрока.ПутьКДанным+ТекФункция.Имя);
				НоваяСтрока = НоваяГруппа.Строки.Добавить();
				НоваяСтрока.ИмяРесурса = ТекСтрока.ПутьКДанным;
				НоваяСтрока.Представление = ТекСтрока.Представление;
				НоваяСтрока.ФорматнаяСтрока = ТекСтрока.ФорматнаяСтрока;
				НоваяСтрока.ШиринаКолонки = ТекСтрока.ШиринаКолонки;
				НоваяСтрока.Одиночный = Ложь;
				КоличествоУровнейДереваПоказателей = Макс(3, КоличествоУровнейДереваПоказателей);;
			КонецЦикла;
			Если НоваяГруппа.Строки.Количество()=0 Тогда
				РесурсыОтчета.Добавить(ТекГруппа.ПутьКДанным);
				НоваяГруппа.Одиночный = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Приступим к формированию макета
	ТаблицаЗаголовка = отПолучитьТаблицуЗаголовкаОтчета(ЭтотОбъект, ТекПостроительОтчета, МакетОтчета.Область(1, 2).Шрифт);
	
	Для Инд=0 По ТаблицаЗаголовка.Количество()-1 Цикл
		ТекСтрокаЗаголовка = ТаблицаЗаголовка[Инд];
		ОбластьЗаголовок = МакетОтчета.Область(Инд+1, 2);
		Для Каждого ТекКолонка Из ТаблицаЗаголовка.Колонки Цикл
			Если ТекКолонка.Имя="ИмяСтроки" Тогда Продолжить; КонецЕсли;
			ТекПараметрЗаголовка = ТекСтрокаЗаголовка[ТекКолонка.Имя];
			Если обЗначениеНеЗаполнено(ТекПараметрЗаголовка) Тогда Продолжить; КонецЕсли;
			ОбластьЗаголовок[ТекКолонка.Имя] = ТекПараметрЗаголовка;
		КонецЦикла;
	КонецЦикла;
	
	ВысотаЗаголовка = МакетОтчета.ВысотаТаблицы;
	МакетОтчета.Область(1, 1).ШиринаКолонки = 1;
	МакетОтчета.Область("R1:R" + ВысотаЗаголовка).Имя = "ЗаголовокШаблон";
	
	// Создадим отступы уровней измерений строк
	Если НЕ ИспользоватьАвтоотступ Тогда
		НачальныйНомерВыводаКолонок = 2;
		Для к = 1 По КоличествоУровней Цикл
			МакетОтчета.Область(ВысотаЗаголовка + 1, НачальныйНомерВыводаКолонок).ШиринаКолонки = ?(к = КоличествоУровней, 40, 1.5);
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
	Иначе
		НачальныйНомерВыводаКолонок = 3;
		МакетОтчета.Область(ВысотаЗаголовка + 1, 2).ШиринаКолонки = 40;
	КонецЕсли;
	
	ВысотаПоказателей = Макс(2, КоличествоУровнейДереваПоказателей);
	
	ОбластьЗаголовокСтрок = МакетОтчета.Область(ВысотаЗаголовка + 1, 2, ВысотаЗаголовка + ВысотаПоказателей + КоличествоРазверток, НачальныйНомерВыводаКолонок - 1);
	ОбластьЗаголовокСтрок.Объединить();
	ОбластьЗаголовокСтрок.Текст                   = ЗаголовокШапки;
	ОбластьЗаголовокСтрок.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьЗаголовокСтрок.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
	ОбластьЗаголовокСтрок.ГраницаСлева            = ГраницаМакета.Измерения.ГраницаСлева;
	ОбластьЗаголовокСтрок.ГраницаСверху           = ГраницаМакета.Измерения.ГраницаСверху;
	ОбластьЗаголовокСтрок.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
	
	ДоступныеПоля = ДеревоДоступныхПолей;
	
	Для Каждого ТекПоле Из ДополнительныеПоляОтчета Цикл
		
		Если (НЕ ТекПоле.НеСвязанные) И (НЕ ДополнительныеПоляВОтдельнойКолонке) Тогда Продолжить; КонецЕсли;
		
		ОбластьДопПоле = МакетОтчета.Область(ВысотаЗаголовка + 1, НачальныйНомерВыводаКолонок, ВысотаЗаголовка + ВысотаПоказателей + КоличествоРазверток, НачальныйНомерВыводаКолонок);
		ОбластьДопПоле.Объединить();
		ОбластьДопПоле.Текст                   = ТекПоле.ПолноеПредставление;
		ОбластьДопПоле.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.ШиринаКолонки           = ТекПоле.ШиринаКолонки;
		ОбластьДопПоле.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьДопПоле.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьДопПоле.ГраницаСлева            = ГраницаМакета.ДопПоля.ГраницаСлева;
		ОбластьДопПоле.ГраницаСверху           = ГраницаМакета.ДопПоля.ГраницаСверху;
		ОбластьДопПоле.ГраницаСнизу            = ГраницаМакета.Измерения.ГраницаСнизу;
		
		НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
	КонецЦикла;
	
	Если КоличествоРазверток > 0 Тогда
		// Для кросс-отчета 
		ОбластьШапкаСтрок = МакетОтчета.Область(ВысотаЗаголовка + 1, 1, ВысотаЗаголовка + ВысотаПоказателей + КоличествоРазверток, НачальныйНомерВыводаКолонок - 1);
		ОбластьШапкаСтрок.Имя = "ШапкаСтрок";
	Иначе
		ОбластьШапкаСтрок = МакетОтчета.Область("R" + (ВысотаЗаголовка + 1) + ":R" + (ВысотаЗаголовка + ВысотаПоказателей));
		ОбластьШапкаСтрок.Имя = "ШапкаТаблицы";
	КонецЕсли;
	
	НомерСтроки  = ВысотаЗаголовка + 1;
	НомерКолонки = НачальныйНомерВыводаКолонок;
	УровеньГруппировки=0;
	Для Каждого ТекКолонка Из ТекПостроительОтчета.ИзмеренияКолонки Цикл
		НомерПервойКолонки = НомерКолонки;
		ИмяТекИзмерения = ТекКолонка.Имя;
		Если ТекПостроительОтчета.Результат.Колонки.Найти(ИмяТекИзмерения+"Представление")<>Неопределено Тогда
			ИмяТекИзмерения = ИмяТекИзмерения+"Представление";
		КонецЕсли;
		ПутьОсновногоПредставления = "";
		ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекКолонка.ПутьКДанным, ПараметрыИспользованияПолейИПоказателей);
		Попытка
			Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
				ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
				ИмяТекИзмерения = СтрЗаменить(ПутьОсновногоПредставления,".","");
			ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
				ИмяТекИзмерения = ТекПараметрыИзмерения.СоставноеПредставление.Представление;
				ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Если ТекКолонка.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			ОбластьКолонкиПоказатель = Неопределено;
			Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
				НомерПервойКолонкиИзмерения = 0;
				НомерПервойКолонкиФункции = НомерКолонки;
				Для Каждого ТекГруппа Из ТекФункция.Строки Цикл
					НомерПервойКолонкиГруппы = НомерКолонки;
					Для Каждого ТекПоказатель Из ТекГруппа.Строки Цикл
						ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
						ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 3, НомерКолонки, НомерСтроки + 3 + ОстатокВысоты, НомерКолонки);
						Если ОстатокВысоты > 0 Тогда
							ОбластьКолонкиПоказатель.Объединить();
						КонецЕсли;
						ОбластьКолонкиПоказатель.Текст = ТекПоказатель.Представление;
						ОбластьКолонкиПоказатель.Расшифровка             = Новый Структура("Показатель,Функция", ТекПоказатель, ТекФункция);
						ОбластьКолонкиПоказатель.ШиринаКолонки	   	     = ТекПоказатель.ШиринаКолонки;
						ОбластьКолонкиПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
						ОбластьКолонкиПоказатель.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
						ОбластьКолонкиПоказатель.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						ОбластьКолонкиПоказатель.ГраницаСлева            = ?(НомерПервойКолонкиГруппы = НомерКолонки, ?(НомерПервойКолонкиИзмерения = 0,ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева,ГраницаМакета.Функции.ГраницаСправа), ГраницаМакета.Показатели.ГраницаСлева);
						ОбластьКолонкиПоказатель.ГраницаСверху           = ГраницаМакета.Показатели.ГраницаСверху;
						ОбластьКолонкиПоказатель.ГраницаСнизу            = ГраницаМакета.Показатели.ГраницаСнизу;
						
						НомерКолонки = НомерКолонки + 1;
					КонецЦикла;
					
					Если ТекГруппа.Одиночный Тогда
						ОбластьКолонкиГрупп = МакетОтчета.Область(НомерСтроки + 2, НомерПервойКолонкиГруппы, НомерСтроки + ВысотаПоказателей, НомерПервойКолонкиГруппы);
						НомерКолонки = НомерКолонки + 1;
					Иначе
						ОбластьКолонкиПоказатель.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
						ОбластьКолонкиГрупп = МакетОтчета.Область(НомерСтроки + 2, НомерПервойКолонкиГруппы, НомерСтроки + 2, НомерКолонки - 1);
					КонецЕсли;
					ОбластьКолонкиГрупп.Объединить();
					ОбластьКолонкиГрупп.Текст                   = ТекГруппа.Представление;
					ОбластьКолонкиГрупп.ПараметрРасшифровки     = "Расшифровка";
					ОбластьКолонкиГрупп.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
					ОбластьКолонкиГрупп.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
					ОбластьКолонкиГрупп.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					ОбластьКолонкиГрупп.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
					ОбластьКолонкиГрупп.ГраницаСверху           = ГраницаМакета.Функции.ГраницаСверху;
					ОбластьКолонкиГрупп.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
					НомерПервойКолонкиИзмерения=НомерПервойКолонкиИзмерения+1;
				КонецЦикла;
				
				ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 1, НомерКолонки - 1);
				ОбластьКолонкиФункция.Объединить();
				ОбластьКолонкиФункция.Текст                   = ТекФункция.Представление;
				ОбластьКолонкиФункция.ПараметрРасшифровки     = "Расшифровка";
				ОбластьКолонкиФункция.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ОбластьКолонкиФункция.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
				ОбластьКолонкиФункция.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				ОбластьКолонкиФункция.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
				ОбластьКолонкиФункция.ГраницаСверху           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
				ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
				НомерПервойКолонкиИзмерения=НомерПервойКолонкиИзмерения+1;
			КонецЦикла;
			Если ОбластьКолонкиПоказатель <> Неопределено Тогда
				ОбластьКолонкиПоказатель.ГраницаСправа	= ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
			КонецЕсли;
			
			Если ОбластьКолонкиГрупп <> Неопределено Тогда
				ОбластьКолонкиГрупп.ГраницаСправа       = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
			КонецЕсли;
			ОбластьКолонкиФункция.ГраницаСправа        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
			ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
			ОбластьКолонки.Объединить();
			ОбластьКолонки.Заполнение          = ТипЗаполненияОбластиИзмерения;
			ОбластьКолонки.Параметр            = ИмяТекИзмерения;
			ОбластьКолонки.ПараметрРасшифровки = "Расшифровка";
			ОбластьКолонки.ГраницаСлева        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
			ОбластьКолонки.ГраницаСверху       = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
			Попытка
				Если обЗначениеНеЗаполнено(ПутьОсновногоПредставления) Тогда
					ОбластьКолонки.Формат = ТекПараметрыИзмерения.Формат;
				Иначе
					ОбластьКолонки.Формат = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьОсновногоПредставления, ПараметрыИспользованияПолейИПоказателей).Формат;
				КонецЕсли;
			Исключение
			КонецПопытки;
			ОбластьПостроителя = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + ВысотаПоказателей + КоличествоРазверток, НомерКолонки - 1);
			ОбластьПостроителя.Имя = ТекКолонка.Имя + "Иерархия";
			
			НомерСтроки = НомерСтроки + 1;
			УровеньГруппировки = УровеньГруппировки+1;
			НомерПервойКолонки = НомерКолонки;
		КонецЕсли;
		НомерПервойКолонкиИзмерения = 0;
		ОбластьКолонкиПоказатель = Неопределено;
		Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
			НомерПервойКолонкиФункции = НомерКолонки;
			Для Каждого ТекГруппа Из ТекФункция.Строки Цикл
				НомерПервойКолонкиГруппы = НомерКолонки;
				Для Каждого ТекПоказатель Из ТекГруппа.Строки Цикл
					ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
					ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 3, НомерКолонки, НомерСтроки + 3 + ОстатокВысоты, НомерКолонки);
					Если ОстатокВысоты > 0 Тогда
						ОбластьКолонкиПоказатель.Объединить();
					КонецЕсли;
					ОбластьКолонкиПоказатель.Текст                   = ТекПоказатель.Представление;
					ОбластьКолонкиПоказатель.Расшифровка             = Новый Структура("Показатель,Функция", ТекПоказатель, ТекФункция);
					ОбластьКолонкиПоказатель.ШиринаКолонки	  	     = ТекПоказатель.ШиринаКолонки;
					ОбластьКолонкиПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
					ОбластьКолонкиПоказатель.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
					ОбластьКолонкиПоказатель.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					ОбластьКолонкиПоказатель.ГраницаСлева            = ?(НомерПервойКолонкиГруппы = НомерКолонки, ?(НомерПервойКолонкиИзмерения = 0,ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева,ГраницаМакета.Функции.ГраницаСправа), ГраницаМакета.Показатели.ГраницаСлева);
					ОбластьКолонкиПоказатель.ГраницаСверху           = ГраницаМакета.Показатели.ГраницаСверху;
					ОбластьКолонкиПоказатель.ГраницаСнизу           = ГраницаМакета.Показатели.ГраницаСнизу;
					НомерКолонки = НомерКолонки + 1;
				КонецЦикла;
				
				Если ТекГруппа.Одиночный Тогда
					ОбластьКолонкиГрупп = МакетОтчета.Область(НомерСтроки + 2, НомерПервойКолонкиГруппы, НомерСтроки + ВысотаПоказателей, НомерПервойКолонкиГруппы);
					НомерКолонки = НомерКолонки + 1;
				Иначе
					ОбластьКолонкиПоказатель.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
					ОбластьКолонкиГрупп = МакетОтчета.Область(НомерСтроки + 2, НомерПервойКолонкиГруппы, НомерСтроки + 2, НомерКолонки - 1);
				КонецЕсли;
				ОбластьКолонкиГрупп.Объединить();
				ОбластьКолонкиГрупп.Текст                   = ТекГруппа.Представление;
				ОбластьКолонкиГрупп.ПараметрРасшифровки     = "Расшифровка";
				ОбластьКолонкиГрупп.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ОбластьКолонкиГрупп.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
				ОбластьКолонкиГрупп.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				
				ОбластьКолонкиГрупп.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
				ОбластьКолонкиГрупп.ГраницаСверху           = ГраницаМакета.Функции.ГраницаСверху;
				ОбластьКолонкиГрупп.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
				НомерПервойКолонкиИзмерения=НомерПервойКолонкиИзмерения+1;
			КонецЦикла;
			ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1, НомерПервойКолонкиФункции, НомерСтроки + 1, НомерКолонки - 1);
			ОбластьКолонкиФункция.Объединить();
			ОбластьКолонкиФункция.Текст                   = ТекФункция.Представление;
			ОбластьКолонкиФункция.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонкиФункция.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьКолонкиФункция.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьКолонкиФункция.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьКолонкиФункция.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
			ОбластьКолонкиФункция.ГраницаСверху           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
			ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
			НомерПервойКолонкиИзмерения=НомерПервойКолонкиИзмерения+1;
		КонецЦикла;
		ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		Если ОбластьКолонкиПоказатель <> Неопределено Тогда
			ОбластьКолонкиПоказатель.ГраницаСправа        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		КонецЕсли;
		
		Если УровеньГруппировки >0 Тогда
			ТекУровень=1;
			Пока ТекУровень<УровеньГруппировки+1 Цикл
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки-ТекУровень, НомерПервойКолонки, НомерСтроки-ТекУровень, НомерКолонки - 1);
				ОбластьКолонки.ГраницаСверху       = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
				ТекУровень=ТекУровень+1;
			КонецЦикла;
		КонецЕсли;
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
		ОбластьКолонки.Объединить();
		ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиИзмерения;
		ОбластьКолонки.Параметр                = ИмяТекИзмерения;
		ОбластьКолонки.ПараметрРасшифровки     = "Расшифровка";
		ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонки.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьКолонки.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьКолонки.ГраницаСлева        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		ОбластьКолонки.ГраницаСверху       = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
		Попытка
			Если обЗначениеНеЗаполнено(ПутьОсновногоПредставления) Тогда
				ОбластьКолонки.Формат = ТекПараметрыИзмерения.Формат;
			Иначе
				ОбластьКолонки.Формат = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьОсновногоПредставления, ПараметрыИспользованияПолейИПоказателей).Формат;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Если обЗначениеНеЗаполнено(ОбластьКолонки.Формат) И СтруктураФорматаПолей.Свойство(ТекКолонка.Имя) Тогда
			ОбластьКолонки.Формат = СтруктураФорматаПолей[ТекКолонка.Имя];
		КонецЕсли;	
		ОбластьПостроителя = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + ВысотаПоказателей + КоличествоРазверток, НомерКолонки - 1);
		ОбластьПостроителя.Имя = ТекКолонка.Имя;
		
		НомерСтроки = НомерСтроки + 1;
		УровеньГруппировки = УровеньГруппировки+1;
	КонецЦикла;
	
	Если (ТекПостроительОтчета.ИзмеренияКолонки.Количество() > 0) И ВключитьГраницуСправа Тогда
		ОбластьКолонки.ГраницаСправа = ЗначениеГраницОтчета.ГраницаСправа;
	КонецЕсли;
	
	Если (ТекПостроительОтчета.ИзмеренияКолонки.Количество()>0) И (НЕ ТекПостроительОтчета.ВыводитьОбщиеИтоги) Тогда
		ОбластьКолонки.ГраницаСправа               = ПеремГраницаСправа;
		ОбластьКолонкиФункция.ГраницаСправа        = ПеремГраницаСправа;
	КонецЕсли;
	
	НомерСтроки = ВысотаЗаголовка + 1;
	НомерПервойКолонки = НомерКолонки;
	НомерПервойКолонкиИзмерения = 0;
	ОбластьКолонкиПоказатель = Неопределено;
	Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
		НомерПервойКолонкиФункции = НомерКолонки;
		Для Каждого ТекГруппа Из ТекФункция.Строки Цикл
			НомерПервойКолонкиГруппы = НомерКолонки;
			Для Каждого ТекПоказатель Из ТекГруппа.Строки Цикл
				Если КоличествоРазверток > 0 Тогда
					ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
					ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 3 + ОстатокВысоты, НомерКолонки, НомерСтроки + 3 + ОстатокВысоты, НомерКолонки);
				Иначе
					ОбластьКолонкиПоказатель = МакетОтчета.Область(НомерСтроки + 2, НомерКолонки, НомерСтроки + 2, НомерКолонки);
				КонецЕсли;
				ОбластьКолонкиПоказатель.Текст                   = ТекПоказатель.Представление;
				ОбластьКолонкиПоказатель.Расшифровка             = Новый Структура("Показатель,Функция", ТекПоказатель, ТекФункция);
				ОбластьКолонкиПоказатель.ШиринаКолонки	  	     = ТекПоказатель.ШиринаКолонки;
				ОбластьКолонкиПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ОбластьКолонкиПоказатель.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
				ОбластьКолонкиПоказатель.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				ОбластьКолонкиПоказатель.ГраницаСлева            = ?(НомерПервойКолонкиГруппы = НомерКолонки, ?(НомерПервойКолонкиИзмерения = 0,ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева,ГраницаМакета.Функции.ГраницаСправа), ГраницаМакета.Показатели.ГраницаСлева);
				ОбластьКолонкиПоказатель.ГраницаСверху           = ГраницаМакета.Показатели.ГраницаСверху;
				ОбластьКолонкиПоказатель.ГраницаСнизу            = ГраницаМакета.Показатели.ГраницаСнизу;
				НомерКолонки = НомерКолонки + 1;
			КонецЦикла;
			
			Если ТекГруппа.Одиночный Тогда
				Если КоличествоРазверток > 0 Тогда
					ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
					ОбластьКолонкиГруппа = МакетОтчета.Область(НомерСтроки + 2 + ОстатокВысоты, НомерПервойКолонкиГруппы, НомерСтроки + ВысотаПоказателей + ОстатокВысоты, НомерПервойКолонкиГруппы);
				Иначе	
					ОбластьКолонкиГруппа = МакетОтчета.Область(НомерСтроки+1, НомерПервойКолонкиГруппы, НомерСтроки+ВысотаПоказателей-1, НомерПервойКолонкиГруппы);
				КонецЕсли;	
				НомерКолонки = НомерКолонки+1;
			Иначе	
				ОбластьКолонкиПоказатель.ГраницаСправа = ГраницаМакета.Функции.ГраницаСправа;
				Если КоличествоРазверток > 0 Тогда
					ОбластьКолонкиГруппа = МакетОтчета.Область(НомерСтроки + 2 + ОстатокВысоты, НомерПервойКолонкиГруппы, НомерСтроки + 2 + ОстатокВысоты, НомерКолонки - 1);
				Иначе	
					ОбластьКолонкиГруппа = МакетОтчета.Область(НомерСтроки+1, НомерПервойКолонкиГруппы, НомерСтроки+1, НомерКолонки - 1);
				КонецЕсли;	
			КонецЕсли;	
			ОбластьКолонкиГруппа.Объединить();
			ОбластьКолонкиГруппа.Текст                   = ТекГруппа.Представление;
			ОбластьКолонкиГруппа.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонкиГруппа.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьКолонкиГруппа.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
			ОбластьКолонкиГруппа.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьКолонкиГруппа.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
			ОбластьКолонкиГруппа.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
			ОбластьКолонкиГруппа.ГраницаСверху           = ГраницаМакета.Функции.ГраницаСверху;
		КонецЦикла;
		
		Если ОбластьКолонкиПоказатель<>Неопределено Тогда
			ОбластьКолонкиПоказатель.ГраницаСправа       = ГраницаМакета.Функции.ГраницаСправа;
		КонецЕсли;
		Если КоличествоРазверток > 0 Тогда
			ОстатокВысоты = ВысотаЗаголовка + КоличествоРазверток - НомерСтроки;
			ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки + 1 + ОстатокВысоты, НомерПервойКолонкиФункции, НомерСтроки + 1 + ОстатокВысоты, НомерКолонки - 1);
		Иначе	
			ОбластьКолонкиФункция = МакетОтчета.Область(НомерСтроки, НомерПервойКолонкиФункции, НомерСтроки, НомерКолонки - 1);
		КонецЕсли;	
		
		ОбластьКолонкиФункция.Объединить();
		ОбластьКолонкиФункция.Текст                   = ТекФункция.Представление;
		ОбластьКолонкиФункция.ПараметрРасшифровки     = "Расшифровка";
		ОбластьКолонкиФункция.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонкиФункция.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьКолонкиФункция.РазмещениеТекста        = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьКолонкиФункция.ГраницаСлева            = ?(НомерПервойКолонкиИзмерения = 0, ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева, ГраницаМакета.Функции.ГраницаСлева);
		ОбластьКолонкиФункция.ГраницаСверху           = ГраницаМакета.Функции.ГраницаСверху;
		ОбластьКолонкиФункция.ГраницаСправа           = ГраницаМакета.Функции.ГраницаСправа;
		НомерПервойКолонкиИзмерения = НомерПервойКолонкиИзмерения+1;
	КонецЦикла;
	
	ОбластьКолонкиФункция.ГраницаСправа         = ПеремГраницаСправа;
	Если ОбластьКолонкиПоказатель <> Неопределено Тогда
		ОбластьКолонкиПоказатель.ГраницаСправа  = ПеремГраницаСправа;//ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
	КонецЕсли;
	
	Если ОбластьКолонкиГруппа <> Неопределено Тогда
		ОбластьКолонкиГруппа.ГраницаСправа		= ПеремГраницаСправа;//ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
	КонецЕсли;
	
	Если КоличествоРазверток > 0 Тогда
		ОбластьКолонки = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + КоличествоРазверток, НомерКолонки - 1);
		ОбластьКолонки.Объединить();
		ОбластьКолонки.Текст                   = "Итог";
		ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		ОбластьКолонки.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
		ОбластьКолонки.ГраницаСлева        = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСлева;
		ОбластьКолонки.ГраницаСверху       = ГраницаМакета.ИзмеренияПоГоризонтали.ГраницаСверху;
		ОбластьКолонки.ГраницаСправа       = ПеремГраницаСправа;
	КонецЕсли;
	
	Если ТекПостроительОтчета.ИзмеренияКолонки.Количество() > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(ВысотаЗаголовка + 1, НомерПервойКолонки, ВысотаЗаголовка + ВысотаПоказателей + КоличествоРазверток, НомерКолонки - 1);
		ОбластьПостроителя.Имя = "ЗаголовокИтогаПоСтроке";
	КонецЕсли;
	ОбластьШапкиТаблицы = МакетОтчета.Область(ВысотаЗаголовка + 1, 2, ВысотаЗаголовка + ВысотаПоказателей + КоличествоРазверток, НомерКолонки - 1);
	ОбластьШапкиТаблицы.Шрифт         = ОформлениеШапки.Шрифт;
	ОбластьШапкиТаблицы.ЦветТекста    = ОформлениеШапки.ЦветТекста;
	ОбластьШапкиТаблицы.ЦветФона      = ОформлениеШапки.ЦветФона;
	
	НачальноеОформление = Макс(0, 8 - КоличествоУровней);
	ПараметрыИзмерения  = Новый Структура("РесурсыОтчета,ДеревоРесурсов,КоличествоУровней,КоличествоРазверток,ОбластьОформлениеИзмерений,НачальноеОформление",
	РесурсыОтчета,ДеревоРесурсов,КоличествоУровней,КоличествоРазверток,ОбластьОформлениеИзмерений,НачальноеОформление);
	
	СчетчикУровня = 1;
	НачалоОформления = Макс(0, 8 - КоличествоУровней);
	
	//МакетОтчета.Показать();
	//Возврат Истина;
	
	Для Каждого ТекИзмерение Из ТекПостроительОтчета.ИзмеренияСтроки Цикл
		Если ТекИзмерение.ТипИзмерения = ТипИзмеренияПостроителяОтчета.Иерархия Тогда
			ПараметрыИзмерения.Вставить("ЭтоИерархия", Истина);
			СформироватьМакетИзмеренияСтроки(ЭтотОбъект, ТекПостроительОтчета, МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, ГраницаМакета);
			СчетчикУровня = СчетчикУровня + 1;
		КонецЕсли;
		ПараметрыИзмерения.Вставить("ЭтоИерархия", Ложь);
		СформироватьМакетИзмеренияСтроки(ЭтотОбъект, ТекПостроительОтчета, МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, ГраницаМакета);
		СчетчикУровня = СчетчикУровня + 1;
	КонецЦикла;
	НомерСтроки  = МакетОтчета.ВысотаТаблицы + 1;
	НомерКолонки = НачальныйНомерВыводаКолонок;
	Если КоличествоРазверток > 0 Тогда
		НомерКолонки = НомерКолонки + ((КоличествоРазверток - 1) * РесурсыОтчета.Количество());
	КонецЕсли;
	
	МассивДополнительныеПоляОтчета = ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные", Истина));
	КоличествоДополнительныеПоляОтчета = МассивДополнительныеПоляОтчета.Количество();
	НачальныйНомерВыводаИтогов = НачальныйНомерВыводаКолонок-КоличествоДополнительныеПоляОтчета;
	Для ИндПоля = 0 По КоличествоДополнительныеПоляОтчета-1 Цикл
		ТекПоле = МассивДополнительныеПоляОтчета[ИндПоля];
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаИтогов+ИндПоля, НомерСтроки, НачальныйНомерВыводаИтогов+ИндПоля);
		
		ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		Если ТекПоле.ВыводитьОбщийИтог Тогда
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьКолонки.Параметр                = ТекПоле.Имя;
			ОбластьКолонки.Формат                  = ТекПоле.Формат;
			ОбластьКолонки.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
		Иначе
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьКолонки.Текст                   = "";
			ОбластьКолонки.ПараметрРасшифровки     = "";
		КонецЕсли;
		ОбластьКолонки.ГраницаСлева  = ГраницаМакета.ИзмеренияИтог.ГраницаСлева;
		ОбластьКолонки.ГраницаСверху = ГраницаМакета.ИзмеренияИтог.ГраницаСверху;
		ОбластьКолонки.ГраницаСнизу  = ГраницаМакета.ИзмеренияИтог.ГраницаСнизу;
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмеренияИтог.ГраницаСправа;
	КонецЦикла;
	
	Попытка		
		ИспользованиеРесурсов=ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Использование.Ресурсы;
	Исключение КонецПопытки;
	
	НомерПервойКолонки = НомерКолонки;
	Для Каждого ТекРесурс Из РесурсыОтчета Цикл
		Если ПустаяСтрока(ТекРесурс) Тогда
			ОтображатьПоказатель = Ложь;
		Иначе
			Попытка    ОтображатьПоказатель = ИспользованиеРесурсов[ТекРесурс];
			Исключение ОтображатьПоказатель = Истина;
			КонецПопытки;
		КонецЕсли;
		
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
		ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		Если ОтображатьПоказатель Тогда
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьКолонки.Параметр                = ТекРесурс;
			ОбластьКолонки.Формат                  = СтруктураФорматаПолей[ТекРесурс];
			ОбластьКолонки.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
		Иначе
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьКолонки.Текст                   = "";
			ОбластьКолонки.ПараметрРасшифровки     = "";
		КонецЕсли;
		
		Если КоличествоРазверток > 0 Тогда
			ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки + РесурсыОтчета.Количество(), НомерСтроки, НомерКолонки + РесурсыОтчета.Количество());
			ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			Если ОтображатьПоказатель Тогда
				ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьКолонки.Параметр                = ТекРесурс;
				ОбластьКолонки.Формат                  = СтруктураФорматаПолей[ТекРесурс];
				ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			Иначе
				ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьКолонки.Текст                   = "";
				ОбластьКолонки.ПараметрРасшифровки     = "";
			КонецЕсли;
		КонецЕсли;
		
		НомерКолонки = НомерКолонки + 1;
	КонецЦикла;
	ТекНомерКолонки = НомерПервойКолонки;
	
	СчетчикФункций = 0;
	Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
		Для Каждого ТекГруппа Из ТекФункция.Строки Цикл
			СчетчикПоказателей = 0;
			Если ТекГруппа.Одиночный Тогда
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
				ОбластьКолонки.ГраницаСлева  = ?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева);
				ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
				ОбластьКолонки.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
				ОбластьКолонки.ГраницаСнизу  = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
				
				Если КоличествоРазверток > 0 Тогда
					ОбластьКолонкиИтога = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки + РесурсыОтчета.Количество(), НомерСтроки, ТекНомерКолонки + РесурсыОтчета.Количество());
					ОбластьКолонкиИтога.ГраницаСлева  = ?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева);
					ОбластьКолонкиИтога.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
					ОбластьКолонкиИтога.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
					ОбластьКолонкиИтога.ГраницаСнизу  = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
				КонецЕсли;
				
				ТекНомерКолонки = ТекНомерКолонки+1;
			Иначе
				Для Каждого ТекПоказатель Из ТекГруппа.Строки Цикл
					ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
					ОбластьКолонки.ГраницаСлева  = ?(СчетчикПоказателей=0,?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева), ГраницаМакета.ПоказателиИтог.ГраницаСлева);
					ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
					ОбластьКолонки.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
					ОбластьКолонки.ГраницаСнизу = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
					
					Если КоличествоРазверток > 0 Тогда
						ОбластьКолонкиИтога = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки + РесурсыОтчета.Количество(), НомерСтроки, ТекНомерКолонки + РесурсыОтчета.Количество());
						ОбластьКолонкиИтога.ГраницаСлева  = ?(СчетчикПоказателей=0,?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСлева,ГраницаМакета.ФункцииИтог.ГраницаСлева), ГраницаМакета.ПоказателиИтог.ГраницаСлева);
						ОбластьКолонкиИтога.ГраницаСверху = ГраницаМакета.ПоказателиИтог.ГраницаСверху;
						ОбластьКолонкиИтога.ГраницаСправа = ГраницаМакета.ПоказателиИтог.ГраницаСправа;
						ОбластьКолонкиИтога.ГраницаСнизу = ГраницаМакета.ПоказателиИтог.ГраницаСнизу;
					КонецЕсли;
					ТекНомерКолонки = ТекНомерКолонки+1;
					СчетчикПоказателей = СчетчикПоказателей+1;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ФункцииИтог.ГраницаСправа;
		СчетчикФункций = СчетчикФункций+1;
	КонецЦикла;
	ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСправа;
	
	Если КоличествоРазверток > 0 Тогда
		ОбластьКолонкиИтога.ГраницаСправа = ГраницаМакета.ИзмеренияПоГоризонталиИтог.ГраницаСправа;
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
		ОбластьПостроителя.Имя = "РесурсыИтогПоКолонке";
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки + РесурсыОтчета.Количество(), НомерСтроки, НомерКолонки - 1 + РесурсыОтчета.Количество());
	Иначе
		ОбластьПостроителя = МакетОтчета.Область("R" + НомерСтроки);
	КонецЕсли;
	ОбластьПостроителя.Имя = "ОбщиеИтоги";
	ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаИтогов - 1);
	ОбластьПостроителя.Объединить();
	ОбластьПостроителя.Текст                   = ПараметрыИспользованияПолейИПоказателей.ОбщийИтог.Представление; //"Итог";
	ОбластьПостроителя.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
	ОбластьПостроителя.ВертикальноеПоложение   = ВертикальноеПоложение.Центр;
	ОбластьПостроителя.ГраницаСлева  = ГраницаМакета.ИзмеренияИтог.ГраницаСлева;
	ОбластьПостроителя.ГраницаСверху = ГраницаМакета.ИзмеренияИтог.ГраницаСверху;
	ОбластьПостроителя.ГраницаСнизу  = ГраницаМакета.ИзмеренияИтог.ГраницаСнизу;
	ОбластьПостроителя.ГраницаСправа = ГраницаМакета.ИзмеренияИтог.ГраницаСправа;
	
	Если КоличествоРазверток > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		ОбластьПостроителя.Имя = "ЗаголовокИтогаПоКолонке";
	КонецЕсли;
	
	ОбластьПодвалаТаблицы = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НомерКолонки - 1 + ?(КоличествоРазверток > 0, РесурсыОтчета.Количество(), 0));
	ОбластьПодвалаТаблицы.Шрифт         = ОформлениеИтоги.Шрифт;
	ОбластьПодвалаТаблицы.ЦветТекста    = ОформлениеИтоги.ЦветТекста;
	ОбластьПодвалаТаблицы.ЦветФона      = ОформлениеИтоги.ЦветФона;
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "ПодвалТаблицы";
	
	ОбластьПостроителя = МакетОтчета.Область("R" + (МакетОтчета.ВысотаТаблицы + 1));
	ОбластьПостроителя.Имя = "Подвал";
	
	ОбластьПостроителя = МакетОтчета.Область("R1:R" + (ВысотаЗаголовка + КоличествоРазверток + ВысотаПоказателей));
	ОбластьПостроителя.Имя = "ФиксированныеСтроки";    
	
	ОбластьПостроителя = МакетОтчета.Область("C1:C" + (НачальныйНомерВыводаКолонок - ?(ВыводитьДополнительныеПоляВОтдельнойКолонке, МассивДополнительныеПоляОтчета.Количество(), КоличествоДополнительныеПоляОтчета) - 1));
	ОбластьПостроителя.Имя = "ФиксированныеКолонки";
	
	Для к = 1 По ВысотаЗаголовка Цикл
		ТекОбласть = МакетОтчета.Область(к, 2, к, МакетОтчета.ШиринаТаблицы);
		ТекОбласть.ПоВыделеннымКолонкам = Истина;
		ТекОбласть.РазмещениеТекста     = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции	//	отПолучитьМакетПостроителя()

// Процедура формирует строку измерений макета построителя отчета
//
// Параметры:
//	ОтчетОбъект        - ОтчетОбъект                - Объект отчета
//	ТекПостроительОтчета  - ПостроительОтчета          - Построитель отчета
//	МакетОтчета        - ТабличныйДокумент          - Макет отчета
//	ТекИзмерение       - ИзмерениеПостроителяОтчета - Текущее измерение
//	СчетчикУровня      - Число						- Счетчик уровня
//	ПараметрыИзмерения - Структура					- Параметры измерения
//	ГраницаМакета      - Структура					- Границы макета
//
Процедура СформироватьМакетИзмеренияСтроки(ОтчетОбъект, ТекПостроительОтчета, МакетОтчета, ТекИзмерение, СчетчикУровня, ПараметрыИзмерения, ГраницаМакета)
	
	РесурсыОтчета              = ПараметрыИзмерения.РесурсыОтчета;
	ДеревоРесурсов             = ПараметрыИзмерения.ДеревоРесурсов;
	КоличествоУровней          = ПараметрыИзмерения.КоличествоУровней;
	КоличествоРазверток        = ПараметрыИзмерения.КоличествоРазверток;
	ЭтоИерархия                = ПараметрыИзмерения.ЭтоИерархия;
	ОбластьОформлениеИзмерений = ПараметрыИзмерения.ОбластьОформлениеИзмерений;
	НачальноеОформление        = ПараметрыИзмерения.НачальноеОформление;
	
	Попытка	   ДополнительныеПоляВОтдельнойКолонке = ОтчетОбъект.ВыводитьДополнительныеПоляВОтдельнойКолонке;
	Исключение ДополнительныеПоляВОтдельнойКолонке = Ложь;
	КонецПопытки;
	
	Попытка СтруктураФорматаПолей = ОтчетОбъект.СтруктураФорматированияПолей();
	Исключение СтруктураФорматаПолей = Новый Структура();
	КонецПопытки;	
	
	СтруктураСвязиСвойств = Новый Структура();
	Попытка СтруктураСвязиСвойств = ОтчетОбъект.СтруктураСвязиСвойств;
	Исключение КонецПопытки;	
	Если ТипЗнч(СтруктураСвязиСвойств) <> Тип("Структура") Тогда
		СтруктураСвязиСвойств = Новый Структура();
	КонецЕсли;
	
	ПоследнееИзмерение = ТекПостроительОтчета.ИзмеренияСтроки.Индекс(ТекИзмерение) = ТекПостроительОтчета.ИзмеренияСтроки.Количество() - 1;
	НомерСтроки = МакетОтчета.ВысотаТаблицы + 1;
	НачальныйНомерВыводаКолонок = ?(ОтчетОбъект.ИспользоватьАвтоотступ, 3, КоличествоУровней + 2);
	
	ТекПараметрыИзмерения = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ТекИзмерение.ПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
	ИмяТекИзмерения = ТекИзмерение.Имя;
	Если ТекПостроительОтчета.Результат.Колонки.Найти(ИмяТекИзмерения+"Представление")<>Неопределено Тогда
		ИмяТекИзмерения = ИмяТекИзмерения+"Представление";
	КонецЕсли;
	ПутьОсновногоПредставления = "";
	ТипЗаполненияОбластиИзмерения =	ТипЗаполненияОбластиТабличногоДокумента.Параметр;
	Попытка
		Если ТекПараметрыИзмерения.Свойство("ОсновноеПредставление") Тогда
			ПутьОсновногоПредставления = ТекПараметрыИзмерения.ОсновноеПредставление;
			ИмяТекИзмерения = СтрЗаменить(ПутьОсновногоПредставления,".","");
		ИначеЕсли ТекПараметрыИзмерения.Свойство("СоставноеПредставление") Тогда
			ИмяТекИзмерения = ТекПараметрыИзмерения.СоставноеПредставление.Представление;
			ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	МассивДополнительныеПоляОтчета = ОтчетОбъект.ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные", Ложь));
	
	Если МассивДополнительныеПоляОтчета.Количество() > 0 И НЕ ДополнительныеПоляВОтдельнойКолонке Тогда
		
		ТекстШаблона = "";
		Для ИндПоля=0 По МассивДополнительныеПоляОтчета.Количество()-1 Цикл
			ТекПоле = МассивДополнительныеПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			Если (СтруктураСвязиСвойств.Свойство(ТекПоле.Имя)) И (НЕ ЭтоИерархия) Тогда
				ЗаполнятьПараметр = ТекИзмерение.Имя = СтруктураСвязиСвойств[ТекПоле.Имя].ИмяИзмерения;
			Иначе ЗаполнятьПараметр = (ТекПоле.ПутьКДанным <> ТекИзмерение.ПутьКДанным И Найти(ТекПоле.ПутьКДанным, ТекИзмерение.ПутьКДанным) > 0);
			КонецЕсли;
			Если ЗаполнятьПараметр Тогда ТекстШаблона = ТекстШаблона + ", [" + ТекПоле.Имя + "]"; КонецЕсли;
		КонецЦикла;
		
		Если НЕ обЗначениеНеЗаполнено(ТекстШаблона) Тогда
			ТипЗаполненияОбластиИзмерения =	ТипЗаполненияОбластиТабличногоДокумента.Шаблон;
			ИмяТекИзмерения = "[" + ИмяТекИзмерения + "]"+ТекстШаблона;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОтчетОбъект.ИспользоватьАвтоотступ Тогда
		Для к = 1 По СчетчикУровня - 1 Цикл
			ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление + к, 1);
			
			ОбластьПредУровня = МакетОтчета.Область(НомерСтроки, к + 1);
			ОбластьПредУровня.Шрифт        = ОформлениеУровня.Шрифт;
			ОбластьПредУровня.ЦветТекста   = ОформлениеУровня.ЦветТекста;
			ОбластьПредУровня.ЦветФона     = ОформлениеУровня.ЦветФона;
			ОбластьПредУровня.ГраницаСлева = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
			ОбластьПредУровня.ШиринаКолонки = 2;
		КонецЦикла;
		
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		
	Иначе
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
	КонецЕсли;	
	
	ОформлениеУровня = ОбластьОформлениеИзмерений.Область(НачальноеОформление + СчетчикУровня, 1);
	
	ОбластьКолонки.Объединить();
	ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	ОбластьКолонки.Заполнение    = ТипЗаполненияОбластиИзмерения;
	ОбластьКолонки.Параметр      = ИмяТекИзмерения;
	ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
	ОбластьКолонки.Шрифт         = ОформлениеУровня.Шрифт;
	
	Если ТипЗаполненияОбластиИзмерения = ТипЗаполненияОбластиТабличногоДокумента.Параметр Тогда
		Попытка
			Если обЗначениеНеЗаполнено(ПутьОсновногоПредставления) Тогда
				ОбластьКолонки.Формат = ТекПараметрыИзмерения.Формат;
			Иначе
				ОбластьКолонки.Формат = отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(ПутьОсновногоПредставления, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей).Формат;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	ОбластьКолонки.ЦветТекста    = ОформлениеУровня.ЦветТекста;
	ОбластьКолонки.ЦветФона      = ОформлениеУровня.ЦветФона;
	ОбластьКолонки.ГраницаСверху = ГраницаМакета.ИзмерениеСтрока.ГраницаСверху;
	ОбластьКолонки.ГраницаСлева  = ГраницаМакета.ИзмерениеСтрока.ГраницаСлева;
	Если ОтчетОбъект.ИспользоватьАвтоотступ Тогда
		ОбластьКолонки.Автоотступ = 1;
	КонецЕсли;	
	
	ОбластьКолонки.ПараметрРасшифровки = "Расшифровка";
	Если обЗначениеНеЗаполнено(ОбластьКолонки.Формат) И СтруктураФорматаПолей.Свойство(ТекИзмерение.Имя) Тогда
		ОбластьКолонки.Формат = СтруктураФорматаПолей[ТекИзмерение.Имя];
	КонецЕсли;
	
	Если МассивДополнительныеПоляОтчета.Количество() > 0 И ДополнительныеПоляВОтдельнойКолонке Тогда
		
		// Теперь добавим выбранные дополнительные поля
		Для ИндПоля=0 По МассивДополнительныеПоляОтчета.Количество()-1 Цикл
			
			ТекПоле = МассивДополнительныеПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			Если (СтруктураСвязиСвойств.Свойство(ТекПоле.Имя)) И (НЕ ЭтоИерархия) Тогда
				ЗаполнятьПараметр = ТекИзмерение.Имя = СтруктураСвязиСвойств[ТекПоле.Имя].ИмяИзмерения;
			Иначе ЗаполнятьПараметр = (ТекПоле.ПутьКДанным <> ТекИзмерение.ПутьКДанным И Найти(ТекПоле.ПутьКДанным, ТекИзмерение.ПутьКДанным) > 0);
			КонецЕсли;
			
			Если ЗаполнятьПараметр Тогда
				ОбластьДопПоле.Заполнение          = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьДопПоле.Параметр            = ТекПоле.Имя;
				ОбластьДопПоле.ПараметрРасшифровки = ТекПоле.Имя;
				ОбластьДопПоле.Формат              = ТекПоле.Формат;
			Иначе
				ОбластьДопПоле.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьДопПоле.Текст      = "";
			КонецЕсли;
			ОбластьДопПоле.ГраницаСверху = ГраницаМакета.ДопПоляСтрока.ГраницаСверху;
			ОбластьДопПоле.ГраницаСлева  = ГраницаМакета.ДопПоляСтрока.ГраницаСлева;
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		
		Если НЕ ОтчетОбъект.ИспользоватьАвтоотступ Тогда
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		Иначе ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		КонецЕсли;
		ОбластьДопПоле.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.Шрифт         = ОформлениеУровня.Шрифт;
		ОбластьДопПоле.ЦветТекста    = ОформлениеУровня.ЦветТекста;
		ОбластьДопПоле.ЦветФона      = ОформлениеУровня.ЦветФона;
	КонецЕсли;	
	
	НеСвязанныеДополнительныеПоляОтчета = ОтчетОбъект.ДополнительныеПоляОтчета.НайтиСтроки(Новый Структура("НеСвязанные", Истина));
	Если НеСвязанныеДополнительныеПоляОтчета.Количество() > 0 Тогда
		
		Для ИндПоля=0 По НеСвязанныеДополнительныеПоляОтчета.Количество()-1 Цикл
			
			ТекПоле = НеСвязанныеДополнительныеПоляОтчета[ИндПоля];
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, НачальныйНомерВыводаКолонок);
			ЗаполнятьПараметр = Ложь;
			
			Если ТекПоле.ВыводитьИтогДляВсехУровней Тогда
				ЗаполнятьПараметр = Истина;
			ИначеЕсли ОтчетОбъект.СтруктураНеСвязанныхПоказателей.Свойство(ТекИзмерение.Имя) Тогда
				Если ЭтоИерархия Тогда
					Если (ОтчетОбъект.СтруктураНеСвязанныхПоказателей[ТекИзмерение.Имя]["Показатели"].Свойство(ТекПоле.Имя)) Тогда
						ЗаполнятьПараметр = ОтчетОбъект.СтруктураНеСвязанныхПоказателей[ТекИзмерение.Имя]["Показатели"][ТекПоле.Имя].ВыводитьИтогПоИерархии;
					КонецЕсли;
				Иначе
					ЗаполнятьПараметр = ОтчетОбъект.СтруктураНеСвязанныхПоказателей[ТекИзмерение.Имя]["Показатели"].Свойство(ТекПоле.Имя);
				КонецЕсли;
			Иначе ЗаполнятьПараметр = (ТекПоле.ПутьКДанным <> ТекИзмерение.ПутьКДанным И Найти(ТекПоле.ПутьКДанным, ТекИзмерение.ПутьКДанным) > 0);
			КонецЕсли;
			
			Если ЗаполнятьПараметр Тогда
				ОбластьДопПоле.Заполнение          = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьДопПоле.Параметр            = ТекПоле.Имя;
				ОбластьДопПоле.ПараметрРасшифровки = ТекПоле.Имя;
				ОбластьДопПоле.Формат              = ТекПоле.Формат;
			Иначе
				ОбластьДопПоле.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьДопПоле.Текст      = "";
			КонецЕсли;
			ОбластьДопПоле.ГраницаСверху = ГраницаМакета.ДопПоляСтрока.ГраницаСверху;
			ОбластьДопПоле.ГраницаСлева  = ГраницаМакета.ДопПоляСтрока.ГраницаСлева;
			НачальныйНомерВыводаКолонок = НачальныйНомерВыводаКолонок + 1;
		КонецЦикла;
		
		Если НЕ ОтчетОбъект.ИспользоватьАвтоотступ Тогда
			ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, СчетчикУровня + 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		Иначе ОбластьДопПоле = МакетОтчета.Область(НомерСтроки, 2, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		КонецЕсли;
		ОбластьДопПоле.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьДопПоле.Шрифт         = ОформлениеУровня.Шрифт;
		ОбластьДопПоле.ЦветТекста    = ОформлениеУровня.ЦветТекста;
		ОбластьДопПоле.ЦветФона      = ОформлениеУровня.ЦветФона;
	КонецЕсли;
	
	НомерКолонки = НачальныйНомерВыводаКолонок;
	Если КоличествоРазверток > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, 1, НомерСтроки, НачальныйНомерВыводаКолонок - 1);
		НомерКолонки = НомерКолонки + ((КоличествоРазверток - 1) * РесурсыОтчета.Количество());
	Иначе
		ОбластьПостроителя = МакетОтчета.Область("R" + НомерСтроки);
	КонецЕсли;
	
	Если НЕ ТекПостроительОтчета.ВыводитьДетальныеЗаписи ИЛИ НЕ ПоследнееИзмерение ИЛИ ЭтоИерархия Тогда
		ОбластьПостроителя.Имя = ТекИзмерение.Имя + ?(ЭтоИерархия, "Иерархия", "");
		
	Иначе
		ОбластьПостроителя.Имя = "Детали";
	КонецЕсли;
	
	ИспользованиеРесурсов = Новый Структура();
	Попытка
		Если ЭтоИерархия Тогда
			СтрПутьКДанным = ТекИзмерение.ПутьКДанным+".Родитель";
		Иначе
			СтрПутьКДанным = ТекИзмерение.ПутьКДанным;
		КонецЕсли;
		ИспользованиеРесурсов=отПолучитьПолеИзПараметрыИспользованияПолейИПоказателей(СтрПутьКДанным, ОтчетОбъект.ПараметрыИспользованияПолейИПоказателей);
		ИспользованиеРесурсов=ИспользованиеРесурсов["Использование"]["Ресурсы"]
	Исключение КонецПопытки;
	
	НомерПервойКолонки = НомерКолонки;
	Для Каждого ТекРесурс Из РесурсыОтчета Цикл
		ОбластьКолонки = МакетОтчета.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
		Если ПустаяСтрока(ТекРесурс) Тогда
			ОтображатьПоказатель = Ложь;
		Иначе
			Попытка    ОтображатьПоказатель = ИспользованиеРесурсов[ТекРесурс];
			Исключение ОтображатьПоказатель = Истина;
			КонецПопытки;
		КонецЕсли;
		
		ОбластьКолонки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		
		Если ОтображатьПоказатель Тогда
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьКолонки.Параметр                = ТекРесурс;
			ОбластьКолонки.Формат                  = СтруктураФорматаПолей[ТекРесурс];
			ОбластьКолонки.ПараметрРасшифровки     = "Расшифровка";
			ОбластьКолонки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
		Иначе
			ОбластьКолонки.Заполнение              = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьКолонки.Текст                   = "";
			ОбластьКолонки.ПараметрРасшифровки     = "";
		КонецЕсли;
		
		НомерКолонки = НомерКолонки + 1;
	КонецЦикла;
	
	ТекНомерКолонки = НомерПервойКолонки;
	СчетчикФункций=0;
	Для Каждого ТекФункция Из ДеревоРесурсов.Строки Цикл
		Для Каждого ТекГруппа Из ТекФункция.Строки Цикл
			Если ТекГруппа.Одиночный Тогда
				ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
				ОбластьКолонки.ГраницаСлева  = ?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСлева, ГраницаМакета.ФункцииСтрока.ГраницаСлева);
				ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиСтрока.ГраницаСверху;
				ТекНомерКолонки = ТекНомерКолонки+1;
			Иначе
				СчетчикПоказателей = 0;
				Для Каждого ТекПоказатель Из ТекГруппа.Строки Цикл
					ОбластьКолонки = МакетОтчета.Область(НомерСтроки, ТекНомерКолонки, НомерСтроки, ТекНомерКолонки);
					ОбластьКолонки.ГраницаСлева  = ?(СчетчикПоказателей=0,?(СчетчикФункций=0,ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСлева,ГраницаМакета.ФункцииСтрока.ГраницаСлева), ГраницаМакета.ПоказателиСтрока.ГраницаСлева);
					ОбластьКолонки.ГраницаСверху = ГраницаМакета.ПоказателиСтрока.ГраницаСверху;
					ТекНомерКолонки = ТекНомерКолонки+1;
					СчетчикПоказателей = СчетчикПоказателей+1;
				КонецЦикла;
			КонецЕсли;
			
			ОбластьКолонки.ГраницаСправа = ГраницаМакета.ФункцииСтрока.ГраницаСправа;
		КонецЦикла;
		СчетчикФункций=СчетчикФункций+1;
		ОбластьКолонки.ГраницаСправа = ГраницаМакета.ФункцииСтрока.ГраницаСправа;
	КонецЦикла;
	ОбластьКолонки.ГраницаСправа = ГраницаМакета.ИзмеренияПоГоризонталиСтрока.ГраницаСправа;
	
	Если КоличествоРазверток > 0 Тогда
		ОбластьПостроителя = МакетОтчета.Область(НомерСтроки, НомерПервойКолонки, НомерСтроки, НомерКолонки - 1);
		ОбластьПостроителя.Имя = ТекИзмерение.Имя + "Ресурсы" + ?(ЭтоИерархия, "Иерархия", "");
	КонецЕсли;
	
	ОформлениеСтроки = ОбластьОформлениеИзмерений.Область(НачальноеОформление + СчетчикУровня, 1);
	
	ОбластьСтроки = МакетОтчета.Область(НомерСтроки, НачальныйНомерВыводаКолонок, НомерСтроки, МакетОтчета.ШиринаТаблицы);
	ОбластьСтроки.Шрифт         = ОформлениеСтроки.Шрифт;
	ОбластьСтроки.ЦветТекста    = ОформлениеСтроки.ЦветТекста;
	ОбластьСтроки.ЦветФона      = ОформлениеСтроки.ЦветФона;
	
КонецПроцедуры	//	отСформироватьМакетИзмеренияСтроки()

//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ИмяРегистра = "БюджетДДС";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();

#КонецЕсли
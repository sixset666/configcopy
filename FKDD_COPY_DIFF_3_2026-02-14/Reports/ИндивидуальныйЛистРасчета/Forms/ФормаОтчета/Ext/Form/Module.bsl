
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПериодРегистрации") Тогда
		Отчет.ПериодРегистрации = Параметры.ПериодРегистрации;
	КонецЕсли;   
	
	 Если Параметры.Свойство("Выработка") Тогда
		 Отчет.Выработка = Параметры.Выработка; 
	 КонецЕсли;	
	 
	Если Отчет.ПериодРегистрации = Дата('00010101') Тогда
		
		Отчет.ПериодРегистрации = НачалоМесяца(ТекущаяДата());
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.Организация) Тогда
		
		Отчет.Организация = Справочники.Организации.ОсновнаяОрганизация;
		
	КонецЕсли;	
	
	ДатаПланирования = Мотивация.ДатаКакМесяцПредставление(Отчет.ПериодРегистрации);

	Если НЕ Отчет.Выработка Тогда
		
		Если Отчет.ОПА Тогда
			СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("ОбщийЛистРасчетаОПА");
		Иначе
			СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("ОбщийЛистРасчета");
		КонецЕсли;
	
	Иначе
		СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("ВыборкаЛистРасчета");
	КонецЕсли;
	
	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Новый УникальныйИдентификатор());
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
	Отчет.КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЭлементПользовательскогоОтбораСКД(КомпоновщикНастроек, ВидСравнения, ИмяПоля, Значение)
	
	ПользовательскийОтбор = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(
		КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки);
	
	ЭлементОтбораПользовательский =  ПользовательскийОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбораПользовательский.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор();
	ЭлементОтбораПользовательский.ВидСравнения = ВидСравнения;
	ЭлементОтбораПользовательский.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ЭлементОтбораПользовательский.ПравоеЗначение = Значение;
	ЭлементОтбораПользовательский.Использование = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииПриИзменении(Элемент)
	
	Мотивация.ДатаКакМесяцПодобратьДатуПоТексту(ДатаПланирования, Отчет.ПериодРегистрации);
	ДатаПланирования = Мотивация.ДатаКакМесяцПредставление(Отчет.ПериодРегистрации);

КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораИзСпискаПредставленияПериодаРегистрации(ДатаПланирования, СтандартнаяОбработка, Отчет.ПериодРегистрации);

КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораИзСпискаПредставленияПериодаРегистрации(ПериодПланирования, СтандартнаяОбработка, ПериодРегистрацииВвод, НачальноеЗначение = Неопределено, МинГод=Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если НачальноеЗначение = Неопределено Тогда
		НачальноеЗначение = ПериодРегистрацииВвод;
	КонецЕсли; 
	
	СписокВыбора = Новый СписокЗначений;
	НачалоТекущегоГода = НачалоГода(НачальноеЗначение);
	НачалоПрошлогоГода = НачалоГода(НачалоТекущегоГода - 1);
	
	Если НЕ МинГод=Неопределено Тогда
		Если НачалоПрошлогоГода<МинГод Тогда
			НачалоПрошлогоГода =МинГод;
		КонецЕсли;
	КонецЕсли;
	
	СписокВыбора.Добавить(НачалоПрошлогоГода, (Формат(НачалоПрошлогоГода, "ДФ='yyyy'") + "..."));
	НачалоМесяцаЗаполнения = НачалоТекущегоГода;
	ЭлементПоУмолчанию = Неопределено;
	Для а = 1 По 12 Цикл
		Если НЕ МинГод=Неопределено Тогда
			
			Если НачалоМесяцазаполнения<МинГод Тогда
				НачалоМесяцаЗаполнения = ДобавитьМесяц(НачалоМесяцаЗаполнения, 1);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ДобавленныйЭлемент = СписокВыбора.Добавить(НачалоМесяцаЗаполнения, Мотивация.ДатаКакМесяцПредставление(НачалоМесяцаЗаполнения));
		Если НачальноеЗначение = НачалоМесяцаЗаполнения Тогда
			ЭлементПоУмолчанию = ДобавленныйЭлемент;
		КонецЕсли; 
		
		НачалоМесяцаЗаполнения = ДобавитьМесяц(НачалоМесяцаЗаполнения, 1);
	КонецЦикла;
	НачалоСледующегоГода = КонецГода(НачалоТекущегоГода) + 1;
	СписокВыбора.Добавить(НачалоСледующегоГода, (Формат(НачалоСледующегоГода, "ДФ='yyyy'") + "..."));
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ПериодПланирования",ПериодПланирования);
	ДопПараметры.Вставить("МинГод",МинГод);
	ДопПараметры.Вставить("НачальноеЗначение",НачальноеЗначение);
	ДопПараметры.Вставить("СтандартнаяОбработка",СтандартнаяОбработка);
	ДопПараметры.Вставить("ПериодРегистрации",ПериодРегистрацииВвод);
	
    ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ПериодаРегистрацииНачалоВыбораЗавершение", ЭтотОбъект, ДопПараметры), СписокВыбора, ПериодПланирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодаРегистрацииНачалоВыбораЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	ИначеЕсли Год(ВыбранныйЭлемент.Значение) <> Год(ДополнительныеПараметры.НачальноеЗначение) Тогда
		НачалоВыбораИзСпискаПредставленияПериодаРегистрации(ДополнительныеПараметры.ПериодПланирования, ДополнительныеПараметры.СтандартнаяОбработка, ДополнительныеПараметры.ПериодРегистрации, ВыбранныйЭлемент.Значение, ДополнительныеПараметры.МинГод);
		Возврат;
	КонецЕсли;
	
	Отчет.ПериодРегистрации = ВыбранныйЭлемент.Значение;
	ДатаПланирования  = Мотивация.ДатаКакМесяцПредставление(Отчет.ПериодРегистрации);

КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Отчет.ПериодРегистрации = ДобавитьМесяц(Отчет.ПериодРегистрации, Направление);
	ДатаПланирования = Мотивация.ДатаКакМесяцПредставление(Отчет.ПериодРегистрации);

КонецПроцедуры


&НаСервере
Процедура ВыработкаПриИзмененииНаСервере()
	
	Если НЕ Отчет.Выработка Тогда
		Если Отчет.ОПА Тогда
			СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("ОбщийЛистРасчетаОПА");
		Иначе
			СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("ОбщийЛистРасчета");
		КонецЕсли;
	Иначе
		СхемаКомпоновкиДанных = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("ВыборкаЛистРасчета");
	КонецЕсли;
	
	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Новый УникальныйИдентификатор());
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
	Отчет.КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыработкаПриИзменении(Элемент)
	ВыработкаПриИзмененииНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ВыработкаПриИзменении1(Элемент)
	ВыработкаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОПАПриИзмененииНаСервере()
	
	ВыработкаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОПАПриИзменении(Элемент)
	ОПАПриИзмененииНаСервере();
КонецПроцедуры


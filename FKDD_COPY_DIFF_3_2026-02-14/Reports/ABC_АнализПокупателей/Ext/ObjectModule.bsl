#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем ТекстЗапроса Экспорт;                            // Хранит текст запроса отчета
Перем ПараметрыИспользованияПолейИПоказателей Экспорт; // Хранит параметры использования полей  показателей
Перем ДеревоДоступныхПолей Экспорт;                    // Хранит дерево доступных полей отчета
Перем СтруктураСвязиСвойств Экспорт;                   // Хранит структуру связи свойств
Перем ПриемникПеретаскивания Экспорт;                  // Хранит перетаскиваемый объект
Перем СтруктураНеСвязанныхПоказателей Экспорт;         // Хранит структуру не связанных показателей
Перем ДополнительныеПоляОтчета Экспорт;                // Хранит дополнительные поля отчета
Перем мСортировка Экспорт;                             // Возможные сортировки
Перем СтруктураИтоговыхВыраженийПоказателей Экспорт;   // Хранит итоговые выражения показателей отчета
Перем ВыражениеРасчетаДоли; // Варианты детализации
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА
	
// Стандартная процедура настройки построителя отчета 
//
// Параметры
//  ИмяМакета  – строка – имя макета
//  ИмяМакетаПараметров  – строка – имя макета параметров
//  
Процедура ЗаполнитьНачальныеНастройки(ИмяМакета, ИмяМакетаПараметров="Параметры") Экспорт
	
	// Заполним структуру параметров
	СтруктураПараметрыЗаполнения = Новый Структура();
	СтруктураПараметрыЗаполнения.Вставить("ОтчетОбъект",            ЭтотОбъект);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаТекстаЗапроса", ИмяМакета);
	СтруктураПараметрыЗаполнения.Вставить("ИмяМакетаПараметров",    ИмяМакетаПараметров);
	отЗаполнитьНачальныеНастройки(СтруктураПараметрыЗаполнения, глПрава);
	
	ПостроительОтчета.Параметры.Вставить("ГруппаА");
	ПостроительОтчета.Параметры.Вставить("ГруппаВ");
	ПостроительОтчета.Параметры.Вставить("ГруппаС");
	
	Если ТипЗнч(СтруктураИтоговыхВыраженийПоказателей) = Тип("Структура") И СтруктураИтоговыхВыраженийПоказателей.Свойство("Доля") Тогда
		
		ВыражениеРасчетаДоли = СтруктураИтоговыхВыраженийПоказателей.Доля;
		
	КонецЕсли;
	
КонецПроцедуры	//	ЗаполнитьНачальныеНастройки()

// Формирует таблицу источника данных
Функция ПолучитьТаблицуИсточникаДанных(ТекПостроитель, ТаблицаВывода, СтруктураКолонок) Экспорт
	
	ТекПостроитель.ИзмеренияСтроки.Добавить("Покупатель");
	Выборка = ТекПостроитель.Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,ТекПостроитель.ИзмеренияСтроки[1].ПутьКДанным, "Все");
	
	ВсегоСумма = 0;
	Пока Выборка.Следующий() Цикл
		ВсегоСумма = ВсегоСумма+Выборка[Сортировка];
	КонецЦикла;
		
	Выборка.Сбросить();
	Если ВсегоСумма<0 Тогда
		ВсегоСумма=-ВсегоСумма;
	КонецЕсли;
	ПорогА = Окр((ВсегоСумма*ГруппаА/100),2);
	ПорогВ = Окр((ВсегоСумма*ГруппаВ/100),2);
	ГраницаПорогаВ = ПорогА + ПорогВ;
		
	НакопленнаяСумма = 0;
	
	СтруктураВычисляемыхПоказателей = Новый Структура ("КоэффициентВариации, Доля");
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(15,2));
	СтруктураНеобходимыхПоказателей = Новый Структура ("СебестоимостьУпр, СуммаУпр");
	Для Каждого ТекНужныйПоказатель Из СтруктураНеобходимыхПоказателей Цикл
		Если ТаблицаВывода.Колонки.Найти(ТекНужныйПоказатель.Ключ) = Неопределено Тогда
			ТаблицаВывода.Колонки.Добавить(ТекНужныйПоказатель.Ключ, ОписаниеТиповЧисло);
		КонецЕсли;
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл
		ТекСуммаПараметра = Выборка[Сортировка];
		НакопленнаяСумма = НакопленнаяСумма + ТекСуммаПараметра;
		Если НакопленнаяСумма <= ПорогА ИЛИ ПорогА < ТекСуммаПараметра Тогда
			ТекABCГруппа = "Группа А";
		ИначеЕсли НакопленнаяСумма <= ГраницаПорогаВ ИЛИ ПорогВ < ТекСуммаПараметра Тогда
			ТекABCГруппа = "Группа В";
		Иначе
			ТекABCГруппа = "Группа С";
		КонецЕсли;
		
		ВыборкаДетали = Выборка.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока ВыборкаДетали.Следующий() Цикл
			Если НЕ ВыборкаДетали.Группировка() = "" Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТаблицаВывода.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетали);
			НоваяСтрока.ABCГруппа = ТекABCГруппа;
		КонецЦикла;
		
	КонецЦикла;
		
	ЕстьДоля = Показатели.Найти("Доля", "Имя").Использование;
	Если ЕстьДоля Тогда
		ТаблицаВывода.Колонки.Добавить("ИтоговаяСуммаСортировки", Новый ОписаниеТипов("Число", , ,Новый КвалификаторыЧисла(15,3)));
		ТаблицаВывода.ЗаполнитьЗначения(ВсегоСумма, "ИтоговаяСуммаСортировки");
	КонецЕсли;
		
	Возврат ТаблицаВывода;
КонецФункции

// формирует отчет в виде табличного документа и возвращает его в качестве результата
//
// Параметры
//  ДокументРезультат – ТабличныйДокумент или ПолеТабличногоДокумента – передает ссылку на табличный документ 
//						или поле табличного документа
//
// Возвращаемое значение:
//  отчет в виде табличного документа 
//
Функция СформироватьТабличныйДокумент(ДокументРезультат=Неопределено) Экспорт
	
	Показатель = Показатели.Найти(Сортировка, "Имя");
	Показатель.Использование = Истина;
	СтруктураИтоговыхВыраженийПоказателей.Вставить("Доля", СтрЗаменить(ВыражениеРасчетаДоли, "ВыбСуммаСортировки", Сортировка));
	
	Результат = отСформироватьТабличныйДокумент(ЭтотОбъект, ДокументРезультат, 1, глПрава);
	Возврат Результат;
	
КонецФункции // СформироватьТабличныйДокумент()

// формирует структуру сортировки
// Возвращает список значений с сортировками
Функция СтруктураСортировки() Экспорт
	
	Если СокрЛП(Сортировка) = "" Тогда
		СписокСортировки = Неопределено;
	Иначе
		СписокСортировки = Новый СписокЗначений();
		СписокСортировки.Добавить(НаправлениеСортировки.Возр, "ABCГруппа");
		
		СписокСортировки.Добавить(НаправлениеСортировки.Убыв, Сортировка);
	КонецЕсли;
		
	Возврат СписокСортировки;
 	
КонецФункции

	
// формирует диаграмму и возвращает её 
//
// Параметры
//  ПолеДиаграммы – диаграмма -  содержит диаграмму
//  СтруктураПараметров - структура - передает структуру параметров диаграммы
//
// Возвращаемое значение:
//  поле диаграммы 
//
Функция СформироватьДиаграмму(ПолеДиаграммы=Неопределено, СтруктураПараметров=Неопределено) Экспорт
	
	Показатель = Показатели.Найти(Сортировка, "Имя");
	Показатель.Использование = Истина;
	
	СтруктураИтоговыхВыраженийПоказателей.Вставить("Доля", СтрЗаменить(ВыражениеРасчетаДоли, "ВыбСуммаСортировки", Сортировка));
	
	отСформироватьДиаграмму(ЭтотОбъект, ПолеДиаграммы, СтруктураПараметров,1);
		
	Возврат ПолеДиаграммы;
		
КонецФункции // СформироватьДиаграмму()
	
// Функция возвращает структуру форматирования полей
//
// Без параметров
//  
// Возвращаемое значение:
//  СтруктураФорматаПолей - структура 
//
Функция СтруктураФорматированияПолей() Экспорт
		
	СтруктураФорматаПолей = Новый Структура;
	отСтруктураФорматированияПолей(ЭтотОбъект, СтруктураФорматаПолей);
		
	// Если необходимо, добавим другие или переустановим строки форматирования полей
		
	Возврат СтруктураФорматаПолей;
		
КонецФункции	//	СтруктураФорматированияПолей()
	
//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	
мСортировка = Новый СписокЗначений;
мСортировка.Добавить("СуммаРегл", "Сумма продаж (Регл.)");
мСортировка.Добавить("СуммаУпр", "Сумма продаж (Упр.)");
мСортировка.Добавить("Количество", "Количество (в базовых ед.)");
мСортировка.Добавить("СуммаНаценкиРегл", "Сумма наценки (Регл.)");
мСортировка.Добавить("СуммаНаценкиУпр", "Сумма наценки (Упр.)");
мСортировка.Добавить("ПроцентНаценки", "Процент наценки");

ГруппаА = 75;
ГруппаВ = 20;
ГруппаС = 5;
	
Сортировка = мСортировка[0].Значение;
	
ИмяРегистра = "Продажи";
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
ПромежуточныеЗначенияПостроителя = Новый Структура();
СтруктураНеСвязанныхПоказателей = Новый Структура();
СтруктураИтоговыхВыраженийПоказателей = Новый Структура();
ВыражениеРасчетаДоли = "ВЫРАЗИТЬ(ВЫБОР КОГДА МАКСИМУМ(ИтоговаяСуммаСортировки)=0 ТОГДА 0 ИНАЧЕ (СУММА(ВыбСуммаСортировки)/МАКСИМУМ(ИтоговаяСуммаСортировки))*100 КОНЕЦ КАК Число(15,2))";

#КонецЕсли
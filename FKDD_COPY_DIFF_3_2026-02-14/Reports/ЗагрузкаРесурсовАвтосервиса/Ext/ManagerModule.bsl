
Процедура СоздатьВариантыОтчета(КлючОтчета, МассивИсключений = Неопределено, КлючВарианта = Неопределено) Экспорт
	
	Если МассивИсключений = Неопределено Тогда
		МассивИсключений = Новый Массив;
	КонецЕсли;
	
	Имя           = СтрЗаменить(КлючОтчета, "Отчет.", "");
	Представление = Метаданные.Отчеты[Имя].Синоним;
	
	ПользовательСеанса = ПараметрыСеанса.Пользователь;
	
	ОтчетОбъект = Отчеты[Имя].Создать();
	СхемаКомпоновки = ОтчетОбъект.СхемаКомпоновкиДанных;
	
	Для Каждого ТекВариантНастроек Из СхемаКомпоновки.ВариантыНастроек Цикл
		
		Если НЕ МассивИсключений.Найти(ТекВариантНастроек.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если (НЕ КлючВарианта = Неопределено) И (НЕ ТекВариантНастроек.Имя = КлючВарианта) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураНастроек = Обработки.АРМЗаписьНаРемонт.ПолучитьСписокНастроекАРМ();
		Если ТекВариантНастроек.Имя = "ПоЦехам" Тогда
			СтруктураНастроек.РежимКалендаря = 1;	
		ИначеЕсли ТекВариантНастроек.Имя = "ПоИсполнителям" Тогда
			СтруктураНастроек.РежимКалендаря = 2;	
		КонецЕсли;
		
		КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки));
		КомпоновщикНастроек.ЗагрузитьНастройки(ТекВариантНастроек.Настройки);
		НастройкиАРМ = Новый ХранилищеЗначения(СтруктураНастроек);
		
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НастройкиАРМ", НастройкиАРМ);
		
		Обновление = Ложь;
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВариантыОтчетов.Ссылка
		               |ИЗ
		               |	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		               |ГДЕ
		               |	ВариантыОтчетов.КлючВарианта = &КлючВарианта
		               |	И ВариантыОтчетов.КлючОтчета = &КлючОтчета
		               |	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("КлючВарианта",ТекВариантНастроек.Имя);					   
		Запрос.УстановитьПараметр("КлючОтчета",КлючОтчета);					   
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			НовыйЭлемент = Выборка.Ссылка.ПолучитьОбъект();
			Обновление = Истина;
		Иначе
			НовыйЭлемент = Справочники.ВариантыОтчетов.СоздатьЭлемент();
		КонецЕсли;
		
		НовыйЭлемент.Наименование           = ТекВариантНастроек.Представление;
		НовыйЭлемент.ИмяОтчета              = Имя;
		НовыйЭлемент.КлючОтчета             = КлючОтчета;
		НовыйЭлемент.ПредставлениеОтчета    = Представление;
		НовыйЭлемент.КлючВарианта           = ТекВариантНастроек.Имя;
		НовыйЭлемент.Автор                  = ПользовательСеанса;
		НовыйЭлемент.ЭтоПредопределенный    = Истина;
		НовыйЭлемент.НазначениеДоступа      = Перечисления.НазначениеПравИНастроек.Компания;
		НовыйЭлемент.Настройки              = Новый ХранилищеЗначения(КомпоновщикНастроек.Настройки);
		НовыйЭлемент.ВариантЗагрузкиПериода = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьВариантПериода(КлючОтчета, ТекВариантНастроек.Имя);
		Попытка
			НовыйЭлемент.Записать();
			Если Обновление Тогда
				Сообщить("Обновлен вариант отчета <"+НовыйЭлемент.ПредставлениеОтчета+"-"+НовыйЭлемент.Наименование+">");
			Иначе
				Сообщить("Создан вариант отчета <"+НовыйЭлемент.ПредставлениеОтчета+"-"+НовыйЭлемент.Наименование+">");
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Если НЕ КлючВарианта = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьПризнакПредопределенногоУВариантовОтчета(КлючОтчета, ТаблицаВариантов) Экспорт
	
	Имя = СтрЗаменить(КлючОтчета, "Отчет.", "");
	
	ОтчетОбъект = Отчеты[Имя].Создать();
	СхемаКомпоновки = ОтчетОбъект.СхемаКомпоновкиДанных;
	
	Для Каждого ТекВариант Из ТаблицаВариантов Цикл
		Если СхемаКомпоновки.ВариантыНастроек.Найти(ТекВариант.КлючВарианта) = Неопределено Тогда
			ВариантОбъект = ТекВариант.Ссылка.ПолучитьОбъект();
			ВариантОбъект.ЭтоПредопределенный = Ложь;
			Попытка
				ВариантОбъект.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

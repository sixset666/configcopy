#Если Клиент Тогда
// Модуль отчета

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем МакетОтчета, ОформлениеСтроки; // оформление отчета
Перем ВедетсяБалансПоПодразделению Экспорт; // флаг баланса по подразделению

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Формирует текст запроса
//
//Без параметров
//
// Возвращаемое значение:
//   ТекстЗапроса – текст
//
Функция ПолучитьТекстЗапросаАктив()
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаУчетныхБлоков.Разбиение1 КАК Разбиение1,
	|	ТаблицаУчетныхБлоков.Разбиение2 КАК Разбиение2,
	|	ВЫБОР
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Кассы""
	|			ТОГДА ВЫБОР
	|					КОГДА ДенежныеСредства.СтруктурнаяЕдиница ССЫЛКА Справочник.КассыКомпании
	|						ТОГДА ""Касса (Наличность в кассе компании)""
	|					КОГДА ДенежныеСредства.СтруктурнаяЕдиница ССЫЛКА Справочник.БанковскиеСчета
	|						ТОГДА ""Банк (ДС на расчетных счетах)""
	|					КОГДА ДенежныеСредства.СтруктурнаяЕдиница ССЫЛКА Справочник.ЦенныеБумаги
	|						ТОГДА ""Ценные бумаги""
	|					ИНАЧЕ ""Денежные средства компании""
	|				КОНЕЦ
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""КассыККМ""
	|			ТОГДА ""Касса (Текущая розничная выручка)""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Активы высокой ликвидности (дебиторская задолженность)""
	|			ТОГДА ВЫБОР
	|					КОГДА Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПокупатель
	|						ТОГДА ""Покупатели""
	|					КОГДА Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПодотчетноеЛицо
	|						ТОГДА ""Подотчетные лица""
	|					КОГДА Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПрочее
	|						ТОГДА ""Прочая дебиторская задолженность""
	|					ИНАЧЕ ""Дебиторская задолженность""
	|				КОНЕЦ
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Участие в капитале""
	|			ТОГДА ""Участие в капитале""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Товары""
	|			ТОГДА ""Товары собственные""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Товары, отданные на комиссию""
	|			ТОГДА ""Товары, отданные на комиссию""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Производство""
	|			ТОГДА ""Производство""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Автомобили""
	|			ТОГДА ""Автомобили собственные""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Автомобили, отданные на комиссию""
	|			ТОГДА ""Автомобили, отданные на комиссию""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Постоянные""
	|			ТОГДА ВЫБОР
	|					КОГДА ВложенныйЗапросАктивы.ТипОбъектаУчета = "" (балансовая стоимость)""
	|						ТОГДА ""Балансовая стоимость""
	|					КОГДА ВложенныйЗапросАктивы.ТипОбъектаУчета = "" (накопленная амортизация)""
	|						ТОГДА ""Накопленная амортизация""
	|					ИНАЧЕ ЕСТЬNULL(ВложенныйЗапросАктивы.ТипОбъектаУчета,""Прочие активы компании"")
	|				КОНЕЦ
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Нераспределенные расходы""
	|			ТОГДА ВЫБОР КОГДА ДоходыИРасходы.СтатьяДоходовИРасходов.ВидДоходаИРасхода = &ПустойВидДоходаИРасхода ТОГДА ""Расходы"" ИНАЧЕ ЕСТЬNULL(ДоходыИРасходы.СтатьяДоходовИРасходов.ВидДоходаИРасхода, ""Расходы"") КОНЕЦ
	|	КОНЕЦ КАК ТипОбъектаУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Кассы""
	|			ТОГДА ДенежныеСредства.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""КассыККМ""
	|			ТОГДА РегистрНакопленияКассыККМ.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Активы высокой ликвидности (дебиторская задолженность)""
	|			ТОГДА Взаиморасчеты.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Участие в капитале""
	|			ТОГДА Взаиморасчеты.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Товары""
	|			ТОГДА ПартииТоваров.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Товары, отданные на комиссию""
	|			ТОГДА ПартииТоваровОтданные.СуммаСебестоимостиУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Производство""
	|			ТОГДА ТоварыВПроизводстве.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Автомобили""
	|			ТОГДА ОстаткиАвтомобилей.СуммаУпрОстаток + ЕСТЬNULL(КомплектацияАвтомобилей.СуммаУпрОстаток, 0)
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение3 = ""Автомобили, отданные на комиссию""
	|			ТОГДА АвтомобилиОтданные.СуммаСебестоимостиУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Постоянные""
	|			ТОГДА ВложенныйЗапросАктивы.Остаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Нераспределенные расходы""
	|			ТОГДА ДоходыИРасходы.РасходУпрОстаток
	|	КОНЕЦ КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		""Текущие"" КАК Разбиение1,
	|		""Активы абсолютной ликвидности (денежные средства)"" КАК Разбиение2,
	|		""Кассы"" КАК Разбиение3
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы абсолютной ликвидности (денежные средства)"",
	|		""КассыККМ""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы высокой ликвидности (дебиторская задолженность)"",
	|		""""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы низкой ликвидности"",
	|		""Участие в капитале""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы низкой ликвидности"",
	|		""Товары""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы низкой ликвидности"",
	|		""Товары, отданные на комиссию""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы низкой ликвидности"",
	|		""Производство""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы низкой ликвидности"",
	|		""Автомобили""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Текущие"",
	|		""Активы низкой ликвидности"",
	|		""Автомобили, отданные на комиссию""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Постоянные"",
	|		""Прочие активы"",
	|		""""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Нераспределенные расходы"",
	|		""Расходы"",
	|		"""") КАК ТаблицаУчетныхБлоков
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КассыККМ.Остатки(&ДатаКонца,СпособОплаты.Объект В (&СписокСпособовОплат)) КАК РегистрНакопленияКассыККМ
	|		ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""КассыККМ"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (РегистрНакопленияКассыККМ.КассаККМ.Подразделение.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (РегистрНакопленияКассыККМ.КассаККМ.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваКомпании.Остатки(&ДатаКонца, ) КАК ДенежныеСредства
	|		ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""Кассы"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|		И (ВЫБОР
	|				КОГДА ДенежныеСредства.СтруктурнаяЕдиница ССЫЛКА Справочник.БанковскиеСчета
	|					ТОГДА ДенежныеСредства.СтруктурнаяЕдиница.Владелец = &ВыбОрганизация
	|				ИНАЧЕ ДенежныеСредства.СтруктурнаяЕдиница.Организация = &ВыбОрганизация
	|			КОНЕЦ)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|		И ДенежныеСредства.СтруктурнаяЕдиница.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение");
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&ДатаКонца, ) КАК Взаиморасчеты
	|		ПО ((ТаблицаУчетныхБлоков.Разбиение2 = ""Активы высокой ликвидности (дебиторская задолженность)""
	|					И (Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПокупатель
	|						ИЛИ Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПодотчетноеЛицо
	|						ИЛИ Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПрочее
	|							И Взаиморасчеты.СуммаУпрОстаток > 0))
	|				ИЛИ (ТаблицаУчетныхБлоков.Разбиение3 = ""Участие в капитале""
	|					И Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаФилиал))";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (Взаиморасчеты.ДоговорВзаиморасчетов.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + " 
	|			И (Взаиморасчеты.ДоговорВзаиморасчетов.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки(&ДатаКонца, СтатусПартии = &ТоварКупленный) КАК ПартииТоваров
	|       ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""Товары"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ПартииТоваров.СкладКомпании.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|		И (ПартииТоваров.СкладКомпании.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")"; КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровОтданные.Остатки(&ДатаКонца, ) КАК ПартииТоваровОтданные
	|		ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""Товары, отданные на комиссию"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ПартииТоваровОтданные.ДоговорВзаиморасчетов.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ПартииТоваровОтданные.ДоговорВзаиморасчетов.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВПроизводстве.Остатки(&ДатаКонца, СтатусПартии = &ТоварКупленный) КАК ТоварыВПроизводстве
	|       ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""Производство"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ТоварыВПроизводстве.Цех.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ТоварыВПроизводстве.Цех.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")"; КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиАвтомобилей.Остатки(&ДатаКонца, СтатусПартии = &ТоварКупленный) КАК ОстаткиАвтомобилей
	|       ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""Автомобили"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ОстаткиАвтомобилей.СкладКомпании.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ОстаткиАвтомобилей.СкладКомпании.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")"; КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КомплектацияАвтомобилей.Остатки(&ДатаКонца, СтатусПартии = &ТоварКупленный) КАК КомплектацияАвтомобилей
	|       ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""Автомобили"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (КомплектацияАвтомобилей.СкладКомпании.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (КомплектацияАвтомобилей.СкладКомпании.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")"; КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АвтомобилиОтданные.Остатки(&ДатаКонца, ) КАК АвтомобилиОтданные
	|		ПО (ТаблицаУчетныхБлоков.Разбиение3 = ""Автомобили, отданные на комиссию"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (АвтомобилиОтданные.ДоговорВзаиморасчетов.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (АвтомобилиОтданные.ДоговорВзаиморасчетов.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВЫБОР
	|				КОГДА ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ВидПрочегоАктива = &ВидАктиваОсновноеСредство
	|					ТОГДА ""Основные средства""
	|				КОГДА ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ВидПрочегоАктива = &ВидАктиваНМА
	|					ТОГДА ""НМА""
	|				КОГДА ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ВидПрочегоАктива = &ВидАктиваОборудование
	|					ТОГДА ""Оборудование""
	|				КОГДА ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ВидПрочегоАктива = &ВидАктиваСпецоснастка
	|					ТОГДА ""Спецоснастка""
	|				КОГДА ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ВидПрочегоАктива = &ВидАктиваИнструменты
	|					ТОГДА ""Инструменты""
	|				КОГДА ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ВидПрочегоАктива = &ВидАктиваОбъектСтроительства
	|					ТОГДА ""Объекты строительства""
	|				КОГДА ПрочиеАктивыВЭксплуатацииОстатки.ПрочийАктив.ВидПрочегоАктива = &ВидАктиваИнвестиционныйАктив
	|					ТОГДА ""Инвестиционные активы""
	|				ИНАЧЕ """"
	|			КОНЕЦ + ВидыСумм.ВидСуммы КАК ТипОбъектаУчета,
	|			ВЫБОР
	|				КОГДА ВидыСумм.ВидСуммы = "" (балансовая стоимость)""
	|					ТОГДА ПрочиеАктивыВЭксплуатацииОстатки.БалансоваяСтоимостьУпрОстаток
	|				КОГДА ВидыСумм.ВидСуммы = "" (накопленная амортизация)""
	|					ТОГДА -ПрочиеАктивыВЭксплуатацииОстатки.СуммаАмортизацииУпрОстаток
	|			КОНЕЦ КАК Остаток
	|		ИЗ
	|			РегистрНакопления.ПрочиеАктивыВЭксплуатации.Остатки(&ДатаКонца, ) КАК ПрочиеАктивыВЭксплуатацииОстатки
	|				ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					"" (балансовая стоимость)"" КАК ВидСуммы
	|				
	|				ОБЪЕДИНИТЬ ВСЕ
	|				
	|				ВЫБРАТЬ
	|					"" (накопленная амортизация)"") КАК ВидыСумм
	|				ПО (ИСТИНА)";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|					И (ПрочиеАктивыВЭксплуатацииОстатки.ПодразделениеКомпании.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|					И (ПрочиеАктивыВЭксплуатацииОстатки.ПодразделениеКомпании "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
    ТекстЗапроса = ТекстЗапроса + ") КАК ВложенныйЗапросАктивы
	|		ПО (ТаблицаУчетныхБлоков.Разбиение1 = ""Постоянные"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы.Остатки(&ДатаКонца, ) КАК ДоходыИРасходы
	|		ПО (ТаблицаУчетныхБлоков.Разбиение1 = ""Нераспределенные расходы"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ДоходыИРасходы.ПодразделениеКомпании.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ДоходыИРасходы.ПодразделениеКомпании "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
    ТекстЗапроса = ТекстЗапроса + "
	|ИТОГИ
	|	СУММА(Остаток)
	|ПО
	|	ОБЩИЕ,
	|	Разбиение1,
	|	Разбиение2,
	|	ТипОбъектаУчета
	|";
	Возврат ТекстЗапроса
КонецФункции // ПолучитьТекстЗапросаАктив()

//Формирует текст запроса
//
//Без параметров
//
// Возвращаемое значение:
//   ТекстЗапроса – текст
//
Функция ПолучитьТекстЗапросаПассив()
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаУчетныхБлоков.Разбиение1 КАК Разбиение1,
	|	ТаблицаУчетныхБлоков.Разбиение2 КАК Разбиение2,
	|	ВЫБОР
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Кредиторская задолженность""
	|			ТОГДА ВЫБОР
	|					КОГДА Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПоставщик
	|						ТОГДА ""Поставщики""
	|					КОГДА Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПрочее
	|						ТОГДА ""Прочая кредиторская задолженность""
	|					ИНАЧЕ ""Кредиторская задолженность""
	|				КОНЕЦ
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Реализованные товары""
	|			ТОГДА ""Реализованные товары""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Реализованные автомобили""
	|			ТОГДА ""Реализованные автомобили""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Собственные""
	|			ТОГДА ""Вложения собственников""
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Нераспределенные доходы""
	|			ТОГДА ВЫБОР КОГДА ДоходыИРасходы.СтатьяДоходовИРасходов.ВидДоходаИРасхода = &ПустойВидДоходаИРасхода ТОГДА ""Доходы"" ИНАЧЕ ЕСТЬNULL(ДоходыИРасходы.СтатьяДоходовИРасходов.ВидДоходаИРасхода, ""Доходы"") КОНЕЦ
	|	КОНЕЦ КАК ТипОбъектаУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Кредиторская задолженность""
	|			ТОГДА -Взаиморасчеты.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Реализованные товары""
	|			ТОГДА РеализованныеТовары.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение2 = ""Реализованные автомобили""
	|			ТОГДА РеализованныеАвтомобили.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Собственные""
	|			ТОГДА -Взаиморасчеты.СуммаУпрОстаток
	|		КОГДА ТаблицаУчетныхБлоков.Разбиение1 = ""Нераспределенные доходы""
	|			ТОГДА ДоходыИРасходы.ДоходУпрОстаток
	|	КОНЕЦ КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		""Заемные"" КАК Разбиение1,
	|		""Кредиторская задолженность"" КАК Разбиение2
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Заемные"",
	|		""Реализованные товары""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Заемные"",
	|		""Реализованные автомобили""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Собственные"",
	|		""Капитал""
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""Нераспределенные доходы"",
	|		""Доходы"") КАК ТаблицаУчетныхБлоков
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыКомпании.Остатки(&ДатаКонца, ) КАК Взаиморасчеты
	|		ПО (";
	ТекстЗапроса = ТекстЗапроса + "(ТаблицаУчетныхБлоков.Разбиение2 = ""Кредиторская задолженность""
	|						И (Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПоставщик
	|							ИЛИ Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаПрочее
	|								И Взаиморасчеты.СуммаУпрОстаток < 0))
	|				ИЛИ ТаблицаУчетныхБлоков.Разбиение1 = ""Собственные""
	|					И Взаиморасчеты.Контрагент.ВидКонтрагента = &ВидКонтрагентаСобственник)";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + " И Взаиморасчеты.ДоговорВзаиморасчетов.Организация = &ВыбОрганизация" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + " И Взаиморасчеты.ДоговорВзаиморасчетов.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение") КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РеализованныеТовары.Остатки(&ДатаКонца, ) КАК РеализованныеТовары
	|		ПО (ТаблицаУчетныхБлоков.Разбиение2 = ""Реализованные товары"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса +" 
	|			И (РеализованныеТовары.ДоговорВзаиморасчетов.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (РеализованныеТовары.ДоговорВзаиморасчетов.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РеализованныеАвтомобили.Остатки(&ДатаКонца, ) КАК РеализованныеАвтомобили
	|		ПО (ТаблицаУчетныхБлоков.Разбиение2 = ""Реализованные автомобили"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса +" 
	|			И (РеализованныеАвтомобили.ДоговорВзаиморасчетов.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (РеализованныеАвтомобили.ДоговорВзаиморасчетов.Подразделение "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоходыИРасходы.Остатки(&ДатаКонца, ) КАК ДоходыИРасходы
	|		ПО (ТаблицаУчетныхБлоков.Разбиение1 = ""Нераспределенные доходы"")";
	Если пкФильтр = 1 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ДоходыИРасходы.ПодразделениеКомпании.Организация = &ВыбОрганизация)" КонецЕсли;
	Если пкФильтр = 2 Тогда ТекстЗапроса = ТекстЗапроса + "
	|			И (ДоходыИРасходы.ПодразделениеКомпании "+?(ВедетсяБалансПоПодразделению," В (&СписокПодразделений)","= &ВыбПодразделение")+")" КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|ИТОГИ
	|	СУММА(Остаток)
	|ПО
	|	ОБЩИЕ,
	|	Разбиение1,
	|	Разбиение2,
	|	ТипОбъектаУчета
	|";
	Возврат ТекстЗапроса;
КонецФункции // ПолучитьТекстЗапросаПассив()

// Выводит выборку результата запроса в табличный документ
// 
// Параметры
//  ДокРезультат - ТабличныйДокумент
//  Выборка - Выборка
//
Процедура ВывестиГруппировки(ДокРезультат, Выборка)
	Пока Выборка.Следующий() Цикл
		Если (Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоГруппировке) Или 
			 (Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ДетальнаяЗапись) Тогда
			 
			Если (Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ДетальнаяЗапись) Тогда Продолжить; КонецЕсли;
			 
			Уровень = Выборка.Уровень();
			
			ОбластьСтроки = МакетОтчета.ПолучитьОбласть("Строка");
			ОбластьСтроки.Параметры.Остаток  = Выборка.Остаток;
			
			ЗначениеГруппировки = Выборка[Уровень - ?(Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ДетальнаяЗапись,0,1)];
			Если Найти(Строка(ЗначениеГруппировки)," (балансовая стоимость)")<>0 Тогда
				БалансоваяСтоимость = Выборка.Остаток;
				Выборка.Следующий();
				ЗначениеГруппировкиАмортизация = Выборка[Уровень - ?(Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ДетальнаяЗапись,0,1)];
				Амортизация = Выборка.Остаток;
				//выведем остаточную стоимость 
				ОбластьСтроки.Параметры.ЗначениеГруппировки = СтрЗаменить(ЗначениеГруппировки," (балансовая стоимость)","");
				ОбластьСтроки.Параметры.Остаток = БалансоваяСтоимость + Амортизация;
				ОбластьСтроки.Область(1, 2, 1, 3).Шрифт      = ОформлениеСтроки[Уровень+5].Шрифт;
				ОбластьСтроки.Область(1, 2, 1, 3).ЦветТекста = ОформлениеСтроки[Уровень+5].ЦветТекста;
				ОбластьСтроки.Область(1, 2, 1, 3).ЦветФона   = ОформлениеСтроки[Уровень+4].ЦветФона;
				ДокРезультат.Вывести(ОбластьСтроки, Уровень);				
				//выведем балансовую стоимость 
				ОбластьСтроки.Параметры.ЗначениеГруппировки = ЗначениеГруппировки;
				ОбластьСтроки.Параметры.Остаток = БалансоваяСтоимость;
				ОбластьСтроки.Область(1, 2, 1, 3).Шрифт      = ОформлениеСтроки[Уровень+4].Шрифт;
				ОбластьСтроки.Область(1, 2, 1, 3).ЦветТекста = ОформлениеСтроки[Уровень+4].ЦветТекста;
				ОбластьСтроки.Область(1, 2, 1, 3).ЦветФона   = ОформлениеСтроки[Уровень+4].ЦветФона;
				ДокРезультат.Вывести(ОбластьСтроки, Уровень+1);				
				//выведем остаточную стоимость 
				ОбластьСтроки.Параметры.ЗначениеГруппировки = ЗначениеГруппировкиАмортизация;
				ОбластьСтроки.Параметры.Остаток = Амортизация;
				ДокРезультат.Вывести(ОбластьСтроки, Уровень+1);				
			Иначе	
				ОбластьСтроки.Параметры.ЗначениеГруппировки = ЗначениеГруппировки;
				ОбластьСтроки.Область(1, 2, 1, 3).Шрифт      = ОформлениеСтроки[Уровень+4].Шрифт;
				ОбластьСтроки.Область(1, 2, 1, 3).ЦветТекста = ОформлениеСтроки[Уровень+4].ЦветТекста;
				ОбластьСтроки.Область(1, 2, 1, 3).ЦветФона   = ОформлениеСтроки[Уровень+4].ЦветФона;
				
				ДокРезультат.Вывести(ОбластьСтроки, Уровень);
			КонецЕсли;
		КонецЕсли;
			
		ВывестиГруппировки(ДокРезультат, Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам));
	КонецЦикла;
КонецПроцедуры // ВывестиГруппировки()


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// формирует отчет в виде табличного документа
//
// Параметры
//  ДокРезультат – ТабличныйДокумент или ПолеТабличногоДокумента
//
// Возвращаемое значение:
//  Результат - ТабличныйДокумент
//
Функция СформироватьТабличныйДокумент (ДокРезультат=Неопределено) Экспорт
	
	Если ТипЗнч(ДокРезультат) <> Тип("ПолеТабличногоДокумента") И ТипЗнч(ДокРезультат) <> Тип("ТабличныйДокумент") Тогда
		  ДокРезультат = Новый ТабличныйДокумент();
	Иначе ДокРезультат.Очистить();
	КонецЕсли;
	
		
	Если обПраво("ПредупреждатьОНарушенииПоследовательностей", глПрава) Тогда
		
		ОбработкаВосстановлениеПоследовательностей = Обработки.ВосстановлениеПоследовательностей.Создать();
		
		ПоследовательностиОтчета = Новый Структура;
		ПоследовательностиОтчета = Новый Структура();
		ПоследовательностиОтчета.Вставить("ГраницыПартий");
		ПоследовательностиОтчета.Вставить("ГраницыВзаиморасчетов");
		ПоследовательностиОтчета.Вставить("ГраницыАвтомобили");
		ПоследовательностиОтчета.Вставить("ГраницыКомплектация");
		ПоследовательностиОтчета.Вставить("ГраницыПроизводство");
		
		ТаблицаПоследовательностей = ОбработкаВосстановлениеПоследовательностей.ТаблицаПоследовательностей;
		КоличествоПоследовательностей = ТаблицаПоследовательностей.Количество()-1;
		
		Пока КоличествоПоследовательностей>=0 Цикл
			ТекПоследовательность = ТаблицаПоследовательностей[КоличествоПоследовательностей];
			Если Не ПоследовательностиОтчета.Свойство(ТекПоследовательность.ИмяГраницы) Тогда
				ТаблицаПоследовательностей.Удалить(ТекПоследовательность);
			КонецЕсли;
			КоличествоПоследовательностей = КоличествоПоследовательностей-1;
		КонецЦикла;
		
		Если ТаблицаПоследовательностей.Количество()>0 Тогда
			ТаблицаГраниц = ОбработкаВосстановлениеПоследовательностей.ПолучитьТаблицуНеактуальныхДокументов(ДатаКонца);
			ТекстПредупреждения = "";
			Для Каждого ТекПоследовательность Из ТаблицаПоследовательностей Цикл
				Граница = ТаблицаГраниц.Найти(ТекПоследовательность.ИмяГраницы,"ИмяПоследовательности");
				Если Граница <> Неопределено Тогда
					ТекстАктуальна = "";
					Если Граница.Граница=Неопределено Тогда
						ТекстАктуальна	= Формат(Граница.МоментВремени,"ДФ=dd.MM.yyyy");
					Иначе
						ТекстАктуальна	= Формат(Граница.Граница.Дата,"ДФ=dd.MM.yyyy")+" - "+Граница.Граница.Метаданные().Синоним+" № "+СокрЛП(Граница.Граница.Номер);
					КонецЕсли;
					ТекстПредупреждения = ТекстПредупреждения+"
					| Последовательность: <" + ТекПоследовательность.Представление + "> актуальна " + ТекстАктуальна;
				КонецЕсли;
			КонецЦикла;
			Если Не ПустаяСтрока(ТекстПредупреждения) Тогда
				Предупреждение("Последовательности разрушены и возможны некорректные результаты отчетов!"+ТекстПредупреждения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// получим макеты для результата и оформления
	МакетОтчета = ПолучитьМакет("Макет");
	МакетОтчетаПараметров = ПолучитьОбщийМакет(ИмяМакетаОформленияТабличногоДокумента);
	
	// элементы оформления 
	ОформлениеСтроки = Новый Массив;
	ОбластьОформленияИзмерений = МакетОтчетаПараметров.ПолучитьОбласть("ОформлениеИзмерений");
	Для Сч = 1 По ОбластьОформленияИзмерений.ВысотаТаблицы Цикл
		ОформлениеСтроки.Добавить(ОбластьОформленияИзмерений.Область(Сч, 1));
	КонецЦикла;
		
	ОформлениеШапки = МакетОтчетаПараметров.ПолучитьОбласть("ОформлениеШапки").Область(1, 1);
	ОформлениеИтоги = МакетОтчетаПараметров.ПолучитьОбласть("ОформлениеИтоги").Область(1, 1);
	
	// ПОСТРОИМ ЗАГОЛОВОК
	ОбластьЗаголовка 	= МакетОтчета.ПолучитьОбласть("Заголовок");
	ОбластьОтбора 		= МакетОтчета.ПолучитьоБласть("Отбор");
	ОбластьЗаголовка.Параметры.ЗаголовокОтчета =   "Управленческий баланс на " + Символы.ПС + Формат(ДатаКонца, "ДФ=dd.MM.yyyy");
	ОбластьОтбора.Параметры.Отбор = "Отбор: " + ?(пкФильтр = 0, "Все", ?(пкФильтр = 1, ВыбОрганизация, ВыбПодразделение));
	
	ДокРезультат.Вывести(ОбластьЗаголовка);
	
	ДокРезультат.НачатьГруппуСтрок("Заголовок",Истина);
	ДокРезультат.Вывести(ОбластьОтбора);
	ДокРезультат.ЗакончитьГруппуСтрок();

	// Таблица АКТИВ
	СписокПодразделений = Новый Массив;
	Если ВедетсяБалансПоПодразделению Тогда
		ЗапросПоПодразделениям = Новый Запрос("ВЫБРАТЬ
		|	ПодразделенияКомпании.Ссылка
		|ИЗ
		|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
		|ГДЕ
		|	"+?(ВключатьБалансовыеПодразделения,"","(ПодразделенияКомпании.Балансовое = &Балансовое ИЛИ ПодразделенияКомпании.Ссылка = &Родитель) И")+"
		|	ПодразделенияКомпании.Ссылка В ИЕРАРХИИ(&Родитель)");

		ЗапросПоПодразделениям.УстановитьПараметр("Балансовое",Ложь);
		ЗапросПоПодразделениям.УстановитьПараметр("Родитель",ВыбПодразделение);
			
		СписокПодразделений = ЗапросПоПодразделениям.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
		
	ОбластьШапки = МакетОтчета.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапки.Параметры.АктивПассив = "АКТИВ";
	ОбластьШапки.Параметры.СуммаУпр = "Сумма (" + Константы.ВалютаУправленческогоУчетаКомпании.Получить().Наименование + ")";
	ОбластьШапки.Параметры.Измерения = "Раздел актива / Вид актива / Учетный блок";
	ОбластьШапки.Область(1, 2, 2, 3).Шрифт      = ОформлениеШапки.Шрифт;
	ОбластьШапки.Область(1, 2, 2, 3).ЦветТекста = ОформлениеШапки.ЦветТекста;
	ОбластьШапки.Область(1, 2, 2, 3).ЦветФона   = ОформлениеШапки.ЦветФона;
	ДокРезультат.Вывести(ОбластьШапки);
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаАктив());
	Запрос.УстановитьПараметр("ДатаКонца",      КонецДня(ДатаКонца));	
	Запрос.УстановитьПараметр("ТоварКупленный", Перечисления.СтатусыПартий.ТоварКупленный);
	Запрос.УстановитьПараметр("ВидКонтрагентаПокупатель",Перечисления.ВидыКонтрагентов.Покупатель);
	Запрос.УстановитьПараметр("ВидКонтрагентаПодотчетноеЛицо",Перечисления.ВидыКонтрагентов.ПодотчетноеЛицо);		
	Запрос.УстановитьПараметр("ВидКонтрагентаПрочее",Перечисления.ВидыКонтрагентов.Прочее);			
	Запрос.УстановитьПараметр("ВидКонтрагентаФилиал",Перечисления.ВидыКонтрагентов.Филиал);				
	Запрос.УстановитьПараметр("ВидАктиваОсновноеСредство",Перечисления.ВидыПрочихАктивов.ОсновноеСредство);				
	Запрос.УстановитьПараметр("ВидАктиваНМА",Перечисления.ВидыПрочихАктивов.НМА);				
	Запрос.УстановитьПараметр("ВидАктиваОборудование",Перечисления.ВидыПрочихАктивов.Оборудование);				
	Запрос.УстановитьПараметр("ВидАктиваСпецоснастка",Перечисления.ВидыПрочихАктивов.Спецоснастка);				
	Запрос.УстановитьПараметр("ВидАктиваИнструменты",Перечисления.ВидыПрочихАктивов.Инструменты);				
	Запрос.УстановитьПараметр("ВидАктиваОбъектСтроительства",Перечисления.ВидыПрочихАктивов.ОбъектСтроительства);				
	Запрос.УстановитьПараметр("ВидАктиваИнвестиционныйАктив",Перечисления.ВидыПрочихАктивов.ИнвестиционныйАктив);
	Запрос.УстановитьПараметр("ВыбОрганизация",  	ВыбОрганизация);
	СписокСпособовОплат = Новый Массив();
	СписокСпособовОплат.Добавить(Перечисления.ТипыОплатыВРознице.Безналичные);
	СписокСпособовОплат.Добавить(Перечисления.ТипыОплатыВРознице.Наличные);
	Запрос.УстановитьПараметр("СписокСпособовОплат", СписокСпособовОплат);
	Если ВедетсяБалансПоПодразделению Тогда
		Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Иначе
		Запрос.УстановитьПараметр("ВыбПодразделение",   ВыбПодразделение);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПустойВидДоходаИРасхода",Перечисления.ВидыДоходовИРасходов.ПустаяСсылка());		
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда Возврат ДокРезультат; КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДокРезультат.НачатьАвтогруппировкуСтрок();
	ВывестиГруппировки(ДокРезультат, Выборка);
	ДокРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	// Выводим итоги по активу
	ОбластьИтогов = МакетОтчета.ПолучитьОбласть("Подвал");
	Выборка.Сбросить(); Выборка.Следующий();
	ОбластьИтогов.Параметры.Заполнить(Выборка);
	ОбластьИтогов.Область(1, 2, 1, 3).Шрифт      = ОформлениеИтоги.Шрифт;
	ОбластьИтогов.Область(1, 2, 1, 3).ЦветТекста = ОформлениеИтоги.ЦветТекста;
	ОбластьИтогов.Область(1, 2, 1, 3).ЦветФона   = ОформлениеИтоги.ЦветФона;
	ДокРезультат.Вывести(ОбластьИтогов);
	
	// Таблица ПАССИВ
	ОбластьШапки.Параметры.АктивПассив = "ПАССИВ";
	ОбластьШапки.Параметры.Измерения = "Раздел пассива / Вид пассива / Учетный блок";	
	ДокРезультат.Вывести(ОбластьШапки);
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаПассив());
	Запрос.УстановитьПараметр("ДатаКонца",     КонецДня(ДатаКонца));
	Запрос.УстановитьПараметр("ТоварКупленный", Перечисления.СтатусыПартий.ТоварКупленный);
	Запрос.УстановитьПараметр("ВидКонтрагентаПоставщик",Перечисления.ВидыКонтрагентов.Поставщик);		
	Запрос.УстановитьПараметр("ВидКонтрагентаПрочее",Перечисления.ВидыКонтрагентов.Прочее);			
	Запрос.УстановитьПараметр("ВидКонтрагентаСобственник",Перечисления.ВидыКонтрагентов.Собственник);
	Запрос.УстановитьПараметр("ВыбОрганизация",  	ВыбОрганизация);	
	Если ВедетсяБалансПоПодразделению Тогда
		Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Иначе
		Запрос.УстановитьПараметр("ВыбПодразделение",   ВыбПодразделение);
	КонецЕсли;

	Запрос.УстановитьПараметр("ПустойВидДоходаИРасхода",Перечисления.ВидыДоходовИРасходов.ПустаяСсылка());			
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда Возврат ДокРезультат; КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДокРезультат.НачатьАвтогруппировкуСтрок();
	ВывестиГруппировки(ДокРезультат, Выборка);
	ДокРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	// Выводим итоги по пассиву
	Выборка.Сбросить(); Выборка.Следующий();
	ОбластьИтогов.Параметры.Заполнить(Выборка);
	ДокРезультат.Вывести(ОбластьИтогов);
	
	// установим параметры документа результата
	ДокРезультат.ТолькоПросмотр = обПраво("ЗащитаПечатныхФорм", глПрава);
	ДокРезультат.ФиксацияСверху = 2;
	
	Возврат ДокРезультат;
	
КонецФункции // СформироватьТабличныйДокумент()


//////////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ВедетсяБалансПоПодразделению = Константы.СпособВеденияБаланса.Получить() = Перечисления.СпособВеденияБаланса.ПоПодразделению;
отОтчетПриИнициализацииОбъекта(ЭтотОбъект);
#КонецЕсли
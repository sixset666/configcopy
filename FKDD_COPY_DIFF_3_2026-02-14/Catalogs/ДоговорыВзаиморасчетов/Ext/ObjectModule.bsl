////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ПроверятьЗаполнение Экспорт; // Переменная объекта - флаг проверки на заполнение

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка корректности ИНН на ключ
// Если ИНН корректно возвращается пустая строка.
//
// Параметры:
// 	ПроверяемыйИНН  – Строка – Проверяемый ИНН
//
// Возвращаемое значение:
// 	Строка – Сообщение об ошибке в ИНН.
//
Функция ПроверкаИНН(ПроверяемыйИНН) Экспорт
	ПровИНН = СокрЛП(ПроверяемыйИНН);
	ДлинаИНН = СтрДлина(ПровИНН);
	
	// Проверка длины ИНН
	Если ДлинаИНН < 10 Тогда
		Возврат "ИНН не должен быть меньше 10-ти цифр";
	КонецЕсли;

	// Проверка ключа ИНН - сначала составим матрицу простых чисел,
	// потом проверим ключ для разной длины
	КодКонст = Новый Массив;
	КодКонст.Добавить(41);
	КодКонст.Добавить(37);
	КодКонст.Добавить(31);
	КодКонст.Добавить(29);
	КодКонст.Добавить(23);
	КодКонст.Добавить(19);
	КодКонст.Добавить(17);
	КодКонст.Добавить(13);
	КодКонст.Добавить(7);
	КодКонст.Добавить(5);
	КодКонст.Добавить(3);
	
	НСум = 0;
	Если ДлинаИНН = 10 Тогда
		// Для ИНН 10 знаков
		Для Сч = 1 По 9 Цикл
			НСум = НСум + Число(Сред(ПровИНН, Сч, 1)) * КодКонст[Сч + 1];
		КонецЦикла;
		//----------------------------------------------- 
		ННов = 11 - (НСум - (Цел(НСум / 11) * 11)); 
		КНов = Сред(ПровИНН, 1, 9) + ?(ННов < 10, Строка(ННов), "0");
		Если КНов = ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	
	ИначеЕсли ДлинаИНН = 11 Тогда
		// Для ИНН 11 знаков
		КНов = "1" + Сред(ПровИНН, 2, 9);
		ПровИНН = ВРег(ПровИНН);
		Для Сч = 1 По 10 Цикл
			НСум = НСум + Число(Сред(КНов,Сч,1)) * КодКонст[Сч];
		КонецЦикла; 
		//----------------------------------------------- 
		ННов = 11 - (НСум - (Цел(НСум/11)*11));
		КНов = "F" + Сред(КНов, 2, 9) + ?(ННов < 10, Строка(ННов), "0");
		Если КНов = ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	
	ИначеЕсли ДлинаИНН = 12 Тогда			// Для 12 знаков
		Если Число(ПровИНН) * Число(Сред(ПровИНН, 5, 6)) = 0 Тогда
			Возврат "Неправильный ИНН - ошибка ключа";
		КонецЕсли;	
		//----------------------------------------------- 
		Для Сч = 1 По 10 Цикл
			НСум = НСум + Число(Сред(ПровИНН, Сч, 1)) * КодКонст[Сч];
		КонецЦикла;	
		ННов = 11 - (НСум - (Цел(НСум / 11) * 11));
		КНов = Сред(ПровИНН, 1, 10) + ?(ННов < 10, Строка(ННов), "0");	
		НСум = 0;
		//----------------------------------------------- 
		Для Сч = 1 По 11 Цикл
			НСум = НСум + Число(Сред(ПровИНН, Сч, 1)) * КодКонст[Сч - 1];
		КонецЦикла;
		ННов = 11-(НСум - (Цел(НСум / 11) * 11));
		КНов = ?(ННов < 10,КНов+Строка(ННов), КНов + "0");	
		Если КНов = ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	КонецЕсли; 	
	Возврат "Неправильный ИНН - ошибка ключа";
КонецФункции // ПроверкаИНН()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ВидДоговора",1);
		ОбязательныеРеквизиты.Вставить("ТипДоговора",1);
		ОбязательныеРеквизиты.Вставить("ВалютаВзаиморасчетов",1);
		Если НЕ ТипДоговора = Перечисления.ТипыДоговоров.ВиртуальныйДоговор Тогда
			ОбязательныеРеквизиты.Вставить("ДатаНачала",1);
		КонецЕсли;
		ОбязательныеРеквизиты.Вставить("Организация",1);
		ОбязательныеРеквизиты.Вставить("Подразделение",1);
		ОбязательныеРеквизиты.Вставить("ВидОплаты",1);
		Если ЗначениеЗаполнено(ПризнакАгента) Тогда
			ОбязательныеРеквизиты.Вставить("ИННПоставщика",1);
		КонецЕсли;
		ОбязательныеРеквизиты.Вставить("ЕдиницаИзмеренияАвтоработВПечатныхФормах",1);
	КонецЕсли;
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Результат = Истина;
	
	// Для старой группы владельца менять нельзя
	Если (НЕ ЭтоНовый()) И ЭтоГруппа И Владелец <> Ссылка.Владелец Тогда
		Результат = Ложь;
		дкДобавитьОшибку(Ошибки, "Нельзя изменять контрагента для группы договоров.");
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		//anton
		//Запрос = Новый Запрос();
		//Запрос.Текст = "
		//|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		//|	ДоговорыВзаиморасчетов.Ссылка
		//|ИЗ
		//|	Справочник.ДоговорыВзаиморасчетов КАК ДоговорыВзаиморасчетов
		//|ГДЕ
		//|	ДоговорыВзаиморасчетов.Подразделение = &Подразделение И 
		//|	ДоговорыВзаиморасчетов.Наименование = &Наименование И 
		//|	ДоговорыВзаиморасчетов.Владелец = &Владелец И 
		//|	ДоговорыВзаиморасчетов.Ссылка <> &Ссылка";
		//
		//Запрос.УстановитьПараметр("Ссылка",        Ссылка);
		//Запрос.УстановитьПараметр("Владелец",      Владелец);
		//Запрос.УстановитьПараметр("Наименование",  СокрЛП(Наименование));
		//Запрос.УстановитьПараметр("Подразделение", Подразделение);
		//
		//РезультатЗапроса = Запрос.Выполнить();
		//Если Не РезультатЗапроса.Пустой() Тогда
		//	Результат = Ложь;
		//	дкДобавитьОшибку(Ошибки, "Сочетание наименования с подразделением не уникальны.");
		//КонецЕсли;
		//anton
		
		Если (НЕ ДляАвтосалона) И (НЕ ДляАвтосервиса) И (НЕ Внутренний) Тогда
			Результат = Ложь;
			дкДобавитьОшибку(Ошибки, "Установите флажок ""з\ч и сервис"" или ""т\с"".");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПризнакАгента) Тогда
		
		ТекстОшибки = ПроверкаИНН(ИННПоставщика);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Результат=Ложь;
			дкДобавитьОшибку(Ошибки, ТекстОшибки);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТелефонПоставщика) Тогда
			Телефон = СтрЗаменить(ТелефонПоставщика, " " ,"");
			Телефон = СтрЗаменить(Телефон, "(" ,"");
			Телефон = СтрЗаменить(Телефон, ")" ,"");
			Телефон = СтрЗаменить(Телефон, "-" ,"");	
			Если НЕ (обСтрокаСодержитТолькоДопустимыеСимволы(Телефон,"+0123456789") И СтрНайти(Телефон,"+") = 1) Тогда
				Результат=Ложь;
				дкДобавитьОшибку(Ошибки, "В поле ""Телефон поставщика"" введены не корректные данные");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат И спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
КонецФункции // ПроверитьКорректность()

// Проверяет участие в движениях
//
// Параметры:
//  ВидРегистра – Строка		– Имя регистра накопления.
//  Элемент		– ДокументОбъект – Переданный документ.
//  Отбор		– Структура		– Условие отбора данных в регистре.
//
// Возвращаемое значение:
//   Булево				   		– Участие в движениях.
//
Функция УчастиеВДвижениях(ВидРегистра, Элемент, Отбор) Экспорт
	Для Каждого ЭлементОтбора Из Отбор Цикл
		Запрос=Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	*
		|ИЗ
		|	РегистрНакопления."+ВидРегистра+" КАК Рег
		|ГДЕ
		|	Рег."+ЭлементОтбора.Ключ+"=&Ссылка
		|");
		Запрос.УстановитьПараметр("Ссылка",Элемент.Ссылка);
		Если НЕ Запрос.Выполнить().Пустой() Тогда Возврат Истина; КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции // УчастиеВДвижениях()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Если НЕ ЭтоНовый() Тогда
		БлокироватьРеквизиты = Новый Структура;
		Отбор = Новый Структура;
		Отбор.Вставить ("ДоговорВзаиморасчетов", ЭтотОбъект);
		Если УчастиеВДвижениях ("ВзаиморасчетыКомпании", ЭтотОбъект, Отбор)Тогда
			БлокироватьРеквизиты.Вставить ("ВидДоговора");
			БлокироватьРеквизиты.Вставить ("Организация");
			БлокироватьРеквизиты.Вставить ("Подразделение");
			БлокироватьРеквизиты.Вставить ("ВалютаВзаиморасчетов");
			Возврат Ложь;
		КонецЕсли; 	
	КонецЕсли; 
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Формирует стандартное наименование договора
// вида Договор поставки №ХХХХХ а Валюте от ДД.ММ.ГГГГ
// Используется как при интерактивном, так и при программном создании
Процедура СформироватьНаименование() Экспорт
	
	ТекстДеятельности = "";
	Если ДляАвтосалона И ДляАвтосервиса Тогда
		ТекстДеятельности = "з\ч, сервис и т\с";
	ИначеЕсли ДляАвтосервиса Тогда
		ТекстДеятельности = "з\ч и сервис";
	ИначеЕсли ДляАвтосалона Тогда
		ТекстДеятельности = "т\с";
	ИначеЕсли Внутренний Тогда
		ТекстДеятельности = "Прочее";
	КонецЕсли;
	
	//<<Павличенко 16.10.17
	Если Найти(ПараметрыСеанса.Пользователь.ШаблонПрав.Наименование, "Логист") <> 0 тогда
		Если ВидДоговора = Перечисления.ВидыДоговоров.Покупка тогда
			 ТекстДеятельности = "б\у а\м";
		ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоров.Продажа тогда
			 ТекстДеятельности = "а\м";
		КонецЕсли;	
	КонецЕсли;
	//>>Павличенко 16.10.17	
		
	Если (ТипДоговора = Перечисления.ТипыДоговоров.ВиртуальныйДоговор) Тогда
		Наименование = СокрЛП(Строка(ВидДоговора))
		             + " " + ТекстДеятельности
		             + " в " + ВалютаВзаиморасчетов.Наименование;
	Иначе
		Наименование = СокрЛП(Строка(ВидДоговора))
		             + " " + ТекстДеятельности
		             + ?(ПустаяСтрока(НомерДоговора),""," № " + СокрЛП(НомерДоговора))
		             + " в " + ВалютаВзаиморасчетов.Наименование
		             + " от "+Формат(ДатаНачала,"ДФ=дд.ММ.гг");
	КонецЕсли;
	
	//<<Павличенко 16.10.17
	Если Найти(ПараметрыСеанса.Пользователь.ШаблонПрав.Наименование, "Логист")<>0 тогда
			Наименование = СтрЗаменить(Наименование, "Поставка", "Договор купли-продажи");
			Наименование = СтрЗаменить(Наименование, "Продажа", "Договор купли-продажи");
	КонецЕсли;
	//>>Павличенко 16.10.17	
	
КонецПроцедуры // СформироватьНаименование()

//Функция возвращает Истина, если ВидДоговора соответствует ХозОперации документа
//
// Параметры:
//  ХозОперация				– СправочникСсылка – Хозяйственная операция.
//
// Возвращаемое значение:
//   Булево		   						– Соответствие.
//
Функция СоответствуетХозОперации(ХозОперация) Экспорт
	//Проверим на соответствие вида договора ХозОперации
	КорректныйВидДоговора=Неопределено;
	
	// заполним списки комиссионных операций	
	СписокКомиссионера	= Новый СписокЗначений();
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ВводОстатковАвтомобилейПереданныхНаКомиссию);
    СписокКомиссионера.Добавить(Справочники.ХозОперации.ВводОстатковТоваровПереданныхНаКомиссию);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ВозвратАвтомобилейОтПокупателяКомиссия);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ВозвратТоваровОтПокупателяКомиссия);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ИнвентаризацияАвтомобилейОтданныхНаКомиссию);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ИнвентаризацияТоваровОтданныхНаКомиссию);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ПереоценкаАвтомобилейОтданныхНаКомиссию);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ПереоценкаТоваровОтданныхНаКомиссию);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ОтчетКомиссионера);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.ОтчетКомиссионераЗаАвтомобили);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.РеализацияАвтомобилейКомиссия);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.РеализацияТоваровКомиссия);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.СписаниеАвтомобилейОтданныхНаКомиссию);
	СписокКомиссионера.Добавить(Справочники.ХозОперации.СписаниеТоваровОтданныхНаКомиссию);
	//СписокКомиссионера.Добавить(Справочники.ХозОперации.КорректировкаРеализацииАвтомобилейИсправлениеВПервичныхДокументах);
	//СписокКомиссионера.Добавить(Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейКорректировкаПоСогласованиюСторон);

	СписокКомитента	= Новый СписокЗначений();
	СписокКомитента.Добавить(Справочники.ХозОперации.ВводОстатковАвтомобилейПринятыхНаКомиссию);
	СписокКомитента.Добавить(Справочники.ХозОперации.ВводОстатковТоваровПринятыхИПроданных);
	СписокКомитента.Добавить(Справочники.ХозОперации.ВводОстатковТоваровПринятыхНаКомиссию);
	СписокКомитента.Добавить(Справочники.ХозОперации.ВозвратАвтомобилейПоставщикуКомиссия);
	СписокКомитента.Добавить(Справочники.ХозОперации.ВозвратТоваровПоставщикуКомиссия);
	СписокКомитента.Добавить(Справочники.ХозОперации.ОтчетКомитенту);
	СписокКомитента.Добавить(Справочники.ХозОперации.ОтчетКомитентуЗаАвтомобили);
	СписокКомитента.Добавить(Справочники.ХозОперации.ПереоценкаАвтомобилейПринятыхНаКомиссию);
	СписокКомитента.Добавить(Справочники.ХозОперации.ПереоценкаТоваровПринятыхНаКомиссию);
	СписокКомитента.Добавить(Справочники.ХозОперации.ПоступлениеАвтомобилейКомиссия);
	СписокКомитента.Добавить(Справочники.ХозОперации.ПоступлениеТоваровКомиссия);
	
	// поиск
	Если СписокКомиссионера.НайтиПоЗначению(ХозОперация)<>Неопределено Тогда
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.СКомиссионером;
		
	ИначеЕсли СписокКомитента.НайтиПоЗначению(ХозОперация)<>Неопределено Тогда
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.СКомитентом;
		
	ИначеЕсли ХозОперация=Справочники.ХозОперации.СчетФактураВыданный ИЛИ ХозОперация=Справочники.ХозОперации.СчетФактураПолученный Тогда
		//Для выданного счета-фактуры все договора корректные, т.к. документ не редактируется, да и для полученного тоже
		Возврат Истина;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ЗаказПокупателя ИЛИ
		ХозОперация = Справочники.ХозОперации.ЗаказРезервированиеПокупателя ИЛИ
		ХозОперация = Справочники.ХозОперации.РезервированиеПокупателя Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомиссионером Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Продажа;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ЗаказПоставщику Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Покупка;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.СчетОтПоставщика Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Покупка;

	ИначеЕсли ХозОперация = Справочники.ХозОперации.СчетОтПоставщикаЗаАвтомобили Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Покупка;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупки ИЛИ
		ХозОперация = Справочники.ХозОперации.ЧекНаОплатуПокупкиВозврат Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Покупка;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.СчетНаОплату Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомиссионером Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Продажа;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ЧекНаОплату ИЛИ
		ХозОперация = Справочники.ХозОперации.ЧекНаОплатуВозврат Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомиссионером Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Продажа;
	ИначеЕсли ХозОперация.Родитель = Справочники.ХозОперации.ОтгрузкаТМЦ ИЛИ 
		ХозОперация.Родитель = Справочники.ХозОперации.ОтгрузкаАвтомобилей Тогда
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Продажа;
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомиссионером И
			(ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииАвтомобилейИсправлениеВПервичныхДокументах ИЛИ
			ХозОперация = Справочники.ХозОперации.КорректировкаРеализацииАвтомобилейКорректировкаПоСогласованиюСторон) Тогда
			Возврат Истина;
		КонецЕсли;

		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ВыплатаЗарплатыРаботнику Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.Зарплата Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Зарплата;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ВыдачаДенежныхСредствПодотчетнику
			Или ХозОперация = Справочники.ХозОперации.ВозвратДенежныхСредствОтПодотчетника Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.Подотчет Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Подотчет;		
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщику Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.Подотчет Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Покупка;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.РеализацияАгентскихУслуг Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.СКомитентом;
		
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ОтчетАгента Тогда
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом Тогда
			Возврат Истина;
		КонецЕсли; 
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.СКомитентом;
		
	ИначеЕсли ХозОперация.Родитель = Справочники.ХозОперации.ПоступлениеТМЦ ИЛИ 
		ХозОперация.Родитель = Справочники.ХозОперации.ПоставкаАвтомобилей Тогда
		КорректныйВидДоговора = Перечисления.ВидыДоговоров.Покупка;
		Если ВидДоговора=Перечисления.ВидыДоговоров.СКомитентом И
			(ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияИсправлениеВПервичныхДокументах ИЛИ
			ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияКорректировкаПоСогласованиюСторон ИЛИ
			ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейИсправлениеВПервичныхДокументах ИЛИ
			ХозОперация = Справочники.ХозОперации.КорректировкаПоступленияАвтомобилейКорректировкаПоСогласованиюСторон) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	//
	Если (ВидДоговора <> КорректныйВидДоговора) И (ВидДоговора<>Перечисления.ВидыДоговоров.Прочее) И (КорректныйВидДоговора<>Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Возврат Истина;
КонецФункции // СоответствуетХозОперации()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового и на основании
// Может так же вызываться при копировании и программном создании элемента
// в т.ч. из справочника контрагентов и взаиморасчетных документов
Процедура ОбработкаЗаполнения(Основание)
	
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() И (НЕ ЭтоГруппа) Тогда
		Если ВидДоговора.Пустая() Тогда
			Если ЭтотОбъект.Владелец.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель Тогда
				ВидДоговора = Перечисления.ВидыДоговоров.Продажа;
			ИначеЕсли ЭтотОбъект.Владелец.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Поставщик Тогда
				ВидДоговора = Перечисления.ВидыДоговоров.Покупка;
			Иначе
				ВидДоговора = Перечисления.ВидыДоговоров.Прочее;
			КонецЕсли;
		КонецЕсли;
		
		Если Подразделение.Пустая() Тогда
			Подразделение = ПараметрыСеанса.ПодразделениеКомпании;
		КонецЕсли;
		Если Организация.Пустая() Тогда
			Организация = ПараметрыСеанса.Организация;
		КонецЕсли;
		
		Если ТипДоговора.Пустая() Тогда
			ТипДоговора = Перечисления.ТипыДоговоров.Договор;
		КонецЕсли;
		
		Если ВидОплаты.Пустая() Тогда
			ВидОплаты=обПраво("ВидОплатыПоУмолчанию",Права,,Подразделение);
			Если ВидОплаты.Пустая() Тогда
				ВидОплаты=Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			КонецЕсли; 
		КонецЕсли; 
		
		Если ВалютаВзаиморасчетов.Пустая() Тогда
			ВалютаВзаиморасчетов = обПраво("ОсновнаяВалютаВзаиморасчетов",Права,,Подразделение);
			Если ВалютаВзаиморасчетов.Пустая() Тогда
				ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			КонецЕсли; 
		КонецЕсли; 
		
		Если (НЕ ДляАвтосалона) И (НЕ ДляАвтосервиса) Тогда
			ЕстьПодсистемаАвтосалон = зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон");
			
			ДляАвтосервиса = Истина;
			ДляАвтосалона  = ЕстьПодсистемаАвтосалон;
		КонецЕсли;
		
		// Установим флаг отмены контроля проверки превышения макс. суммы кредита
		ОтменаКонтроляСуммыКредита = обПраво("ОтменаКонтроляСуммыКредита", Права,,Подразделение);
		
		#Если Клиент Тогда
			// На сервере не работает
			ДатаНачала = РабочаяДата;
		#КонецЕсли
		СформироватьНаименование();
		
		АвтоЗакрытиеСделок=обПраво("АвтоЗакрытиеСделок",Права,,Организация);
		ЕдиницаИзмеренияАвтоработВПечатныхФормах=обПраво("ЕдиницаИзмеренияАвтоработВПечатныхФормах",Права,,Подразделение);
		
	КонецЕсли; 
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ, ,ПроверятьЗаполнение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Автор        = ПараметрыСеанса.Пользователь;
		ДатаСоздания = ТекущаяДата();
	КонецЕсли;
	
	Если (НЕ ЭтоГруппа) И (НЕ ДляАвтосервиса) Тогда
		ТипЦенРабот           = Справочники.ТипыЦен.ПустаяСсылка();
		ТипСкидкиНаценкиРабот = Справочники.ТипыСкидок.ПустаяСсылка();
	КонецЕсли;
	
	Если (ТипДоговора = Перечисления.ТипыДоговоров.ВиртуальныйДоговор) Тогда
		ДатаНачала    = Дата(1, 1, 1);
		ДатаКонца     = Дата(1, 1, 1);
		НомерДоговора = "";
	КонецЕсли;
	
	Если ВалютаВзаиморасчетов = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить() Тогда
		РасчетыВУсловныхЕдиницах = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
ПроверятьЗаполнение = Истина;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

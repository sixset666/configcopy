
//Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)    
	УстановитьВидимостьРеквизитовЗаполнения();
	ДатаОстатков = ТекущаяДата();
	ОбновитьСписокНаСервере();
	
	ns_ОбщегоНазначенияФормы.УстановитьТипСклад(Элементы);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ns_ОбщегоНазначенияФормы.ОбработкаВыбора(Элементы, ВыбранноеЗначение, ИсточникВыбора, "ns_МотоциклыИМототехника"); 
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаОстатковПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПоДокументу(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаОтметкиПоДокументу",, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура VehicleTypeПриИзменении(Элемент)
	УстановитьВидимостьРеквизитовЗаполнения();
	СброситьРеквизитыЗаполнения();
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сбросить(Команда)
	СброситьРеквизитыЗаполнения();
	УстановитьВидимостьРеквизитовЗаполнения();
КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)
	Если ЗначениеЗаполнено(VehicleType) Тогда
		ТекстВопроса = "Переустановить VehicleType у всех выделеных элементов, продолжить?";
	Иначе
		ТекстВопроса = "Переустановить установленные реквизиты у всех выделеных элементов, продолжить?";
	КонецЕсли; 
	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьЗавершение", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60); 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
		УстановитьНаСервере(ВыделенныеСтроки);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СнятьБезОстатковСВыгрузки(Команда)
	СнятьБезОстатковСВыгрузкиНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры
 
//КонецОбласти

//Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьРеквизитовЗаполнения()
	Если НЕ ЗначениеЗаполнено(VehicleType) Тогда
		Элементы.MotoType.Видимость = Ложь;
	Иначе
		Элементы.MotoType.Видимость = ns_МотоциклыИМототехникаВалидация.УстановитьВидимость_MotoType(VehicleType);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СброситьРеквизитыЗаполнения()
	Если ns_МотоциклыИМототехникаВалидация.УстановитьВидимость_MotoType(VehicleType) = Ложь Тогда
		MotoType = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьНаСервере(ВыделенныеСтроки)
	Попытка
		НачатьТранзакцию();
		
		ЗначенияСопоставленныхРеквизитов = Неопределено;
		Если ЗначениеЗаполнено(СопоставлениеРеквизитов) Тогда
			ЗначенияСопоставленныхРеквизитов = ns_Переопределяемый.ПолучитьЗначенияСопоставленныхРеквизитов(ВыделенныеСтроки);
		КонецЕсли; 
		
		Для каждого СтрокаСписка Из ВыделенныеСтроки Цикл
			Если СтрокаСписка.ЭтоГруппа Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ns_МотоциклыИМототехника.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.ns_МотоциклыИМототехника КАК ns_МотоциклыИМототехника
				|ГДЕ
				|	ns_МотоциклыИМототехника.Родитель В ИЕРАРХИИ(&Родитель)
				|	И ns_МотоциклыИМототехника.ЭтоГруппа = ЛОЖЬ";
				Запрос.УстановитьПараметр("Родитель", СтрокаСписка.Ссылка);
				РезультатЗапроса = Запрос.Выполнить();
				
				Если РезультатЗапроса.Пустой() Тогда
					Возврат;
				КонецЕсли;  
				
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					УстановитьЗначениеЭлементу(Выборка.Ссылка, ЗначенияСопоставленныхРеквизитов);
				КонецЦикла; 
			Иначе	
				УстановитьЗначениеЭлементу(СтрокаСписка, ЗначенияСопоставленныхРеквизитов);
			КонецЕсли;
		КонецЦикла;  
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
	    ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки; 
	 
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеЭлементу(Ссылка, ЗначенияСопоставленныхРеквизитов)
	
	Изменен = Ложь;
	ТекущийОбъект = Ссылка.ПолучитьОбъект();
	
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "VehicleType", VehicleType);
	Если ns_МотоциклыИМототехникаВалидация.УстановитьВидимость_MotoType(VehicleType) = Ложь Тогда
		ТекущийОбъект.MotoType = Неопределено;
	Иначе	
		ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "MotoType", MotoType);
	КонецЕсли;
	
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Condition", ConditionМотоциклыИМототехника);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Шаблон", ШаблонОбъявления);
	ns_ОбщегоНазначенияВызовСервера.ЗаполнитьСопоставлениеРеквизитов(Изменен, ТекущийОбъект, ЗначенияСопоставленныхРеквизитов, СопоставлениеРеквизитов);
	
	Если ЗначениеЗаполнено(ШаблонЗаголовка) Тогда
		НовыйЗаголовок = ns_ШаблонизаторСервер.СформироватьTitle(ШаблонЗаголовка, ТекущийОбъект.Title, ТекущийОбъект.Номенклатура, ТекущийОбъект.Характеристика);
		ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Title", НовыйЗаголовок);
	КонецЕсли;
	
	Если Изменен Тогда
		ТекущийОбъект.Записать();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Истина, "ns_МотоциклыИМототехника");
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Ложь, "ns_МотоциклыИМототехника");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТолькоВыгружаемые(Команда)
	ns_ОбщегоНазначенияФормы.ПоказатьТолькоВыгружаемые(Элементы, Список);	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Список.ТекстЗапроса = ns_Переопределяемый.ПолучитьТекстЗапроса_МотоциклыИМототехника(Склад);
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Склад", Склад);
	КонецЕсли; 
	
	Список.Параметры.УстановитьЗначениеПараметра("НаДату", КонецДня(ДатаОстатков));
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
	Иначе
		СсылкаНаСтроку = ТекущиеДанные.Ссылка;
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
		Элементы.Список.ТекущаяСтрока = СсылкаНаСтроку;
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СнятьБезОстатковСВыгрузкиНаСервере()
	ns_АвитоВалидация.СнятьБезОстатковСВыгрузки("ns_МотоциклыИМототехника");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	ns_ОбщегоНазначенияВызовСервера.СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
КонецПроцедуры

//КонецОбласти 












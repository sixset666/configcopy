
&НаКлиенте
Процедура УстановитьОграничениеТипа()
	
	Элементы.ОбъектДоступа.Доступность = Истина;
	Если НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Организация") Тогда
		ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Организации");
		Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		ОбъектДоступа = ДопустимыеТипы.ПривестиЗначение(ОбъектДоступа);
	ИначеЕсли НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Подразделение") Тогда
		ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.ПодразделенияКомпании");
		Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		ОбъектДоступа = ДопустимыеТипы.ПривестиЗначение(ОбъектДоступа);
	ИначеЕсли НазначениеДоступа = ПредопределенноеЗначение("Перечисление.НазначениеПравИНастроек.Пользователь") Тогда
		ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
		Элементы.ОбъектДоступа.ОграничениеТипа = ДопустимыеТипы;
		ОбъектДоступа = ДопустимыеТипы.ПривестиЗначение(ОбъектДоступа);
	Иначе
		Элементы.ОбъектДоступа.Доступность           = Ложь;
		ОбъектДоступа = Неопределено;
	КонецЕсли;
	
	Если НазначениеДоступа.Пустая() Тогда
		Список.Параметры.УстановитьЗначениеПараметра("НазначениеДоступа", "");
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("НазначениеДоступа", НазначениеДоступа);
	КонецЕсли;
	
	ОбъектДоступаПриИзменении(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтборыСписка(КлючОтчета)
	
	Список.Параметры.УстановитьЗначениеПараметра("КлючОтчета",    КлючОтчета);
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь",  ПараметрыСеанса.Пользователь);
	Список.Параметры.УстановитьЗначениеПараметра("Подразделение", ПараметрыСеанса.ПодразделениеКомпании);
	Список.Параметры.УстановитьЗначениеПараметра("Организация",   ПараметрыСеанса.Организация);
	Список.Параметры.УстановитьЗначениеПараметра("ОтборНаименование", "");
	Список.Параметры.УстановитьЗначениеПараметра("ПоискПоВарианту",   Ложь);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДеревоОтчетов(СтруктураПодсистем, СтруктураОтчетов)
	
	Для Каждого ТекСтрока Из СтруктураПодсистем.Подсистемы Цикл
		НоваяСтрока = СтруктураОтчетов.Строки.Добавить();
		НоваяСтрока.Представление = ТекСтрока.Синоним;
		ЗаполнитьДеревоОтчетов(ТекСтрока, НоваяСтрока);
		Для Каждого ОтчетПодсистемы Из ТекСтрока.Состав Цикл
			
			Если (НЕ Метаданные.Отчеты.Содержит(ОтчетПодсистемы)) ИЛИ 
				(НЕ ОтчетПодсистемы.ОсновнаяФорма.ТипФормы = Метаданные.СвойстваОбъектов.ТипФормы.Управляемая) Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйОтчет = НоваяСтрока.Строки.Добавить();
			НовыйОтчет.Представление = ОтчетПодсистемы.Синоним;
			НовыйОтчет.КлючОтчета    = ОтчетПодсистемы.ПолноеИмя();
		КонецЦикла;
	КонецЦикла;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если обПраво("ПроверкаДоступаКСправочникамИДокументам") Тогда
		СправочникИмя=Метаданные.Справочники.ВариантыОтчетов.Имя;
		ПравоДоступа = обПраво("Право доступа "+СправочникИмя);
		Если ПравоДоступа=Перечисления.ВидыПравДляСправочников.НетДоступа Тогда
			Отказ=Истина;
			Возврат;
		ИначеЕсли ПравоДоступа=Перечисления.ВидыПравДляСправочников.Чтение Тогда
			ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураОтчетов = РеквизитФормыВЗначение("ДеревоОтчетов", Тип("ДеревоЗначений"));
	
	ЗаполнитьДеревоОтчетов(Метаданные.Подсистемы.СтруктураКонфигурации.Подсистемы.Отчеты, СтруктураОтчетов);
	
	ЗначениеВРеквизитФормы(СтруктураОтчетов, "ДеревоОтчетов");
	
	ЭтаФорма.ДеревоОтчетовТекущаяСтрока = -1;
	Элементы.ДеревоОтчетов.ТекущаяСтрока = 0;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборАвтор",        ОтборАвтор);
	
	ДеревоОтчетовУстановитьПараметрыСписка();
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборНаименование", ОтборНаименование);
	Если ЗначениеЗаполнено(ОтборНаименование) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ПоискПоВарианту", Истина);
	КонецЕсли;
	
КонецПроцедуры

//&НаКлиенте
//Процедура ПриОткрытии(Отказ)
//	Если РежимРаботыФормы = "ВсеОтчетыРаздела" Тогда
//		Элементы.ДеревоПодсистем.Развернуть(ДеревоПодсистемТекущаяСтрока, Истина);
//	КонецЕсли;
//КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	// Вызывается после загрузки данных из настроек в реквизиты формы
	
	ОтборНаименованиеСписокВыбора = Настройки.Получить("ОтборНаименованиеСписокВыбора");
	Если ТипЗнч(ОтборНаименованиеСписокВыбора) = Тип("Массив") Тогда
		Элементы.ОтборНаименование.СписокВыбора.ЗагрузитьЗначения(ОтборНаименованиеСписокВыбора);
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборНаименование",  ОтборНаименование);
	Список.Параметры.УстановитьЗначениеПараметра("ОтборАвтор",         ОтборАвтор);
	Если ЗначениеЗаполнено(ОтборНаименование) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ПоискПоВарианту", Истина);
	КонецЕсли;
	//Список.Параметры.УстановитьЗначениеПараметра("ВключаяПодчиненные", ВключаяПодчиненные);
КонецПроцедуры

//&НаСервере
//Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
//	Настройки.Вставить("ОтборНаименованиеСписокВыбора", Элементы.ОтборНаименование.СписокВыбора.ВыгрузитьЗначения());
//КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОтборНаименованиеПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ОтборНаименование) Тогда
		СписокВыбора = Элементы.ОтборНаименование.СписокВыбора;
		ЭлементСписка = СписокВыбора.НайтиПоЗначению(ОтборНаименование);
		Если ЭлементСписка = Неопределено Тогда
			СписокВыбора.Вставить(0, ОтборНаименование);
			Если СписокВыбора.Количество() > 10 Тогда
				СписокВыбора.Удалить(10);
			КонецЕсли;
		Иначе
			Индекс = СписокВыбора.Индекс(ЭлементСписка);
			Если Индекс <> 0 Тогда
				СписокВыбора.Сдвинуть(Индекс, -Индекс);
			КонецЕсли;
		КонецЕсли;
		ТекущийЭлемент = Элементы.ОтборНаименование;
		Список.Параметры.УстановитьЗначениеПараметра("ПоискПоВарианту", Истина);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ПоискПоВарианту", Ложь);
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборНаименование", ОтборНаименование);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНаименованиеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтборНаименованиеПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторПриИзменении(Элемент)
	Список.Параметры.УстановитьЗначениеПараметра("ОтборАвтор", ОтборАвтор);
КонецПроцедуры

//&НаКлиенте
//Процедура ВключаяПодчиненныеПриИзменении(Элемент)
//	Список.Параметры.УстановитьЗначениеПараметра("ВключаяПодчиненные", ВключаяПодчиненные);
//КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоПодсистем


&НаКлиенте
Процедура ДеревоОтчетовПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ДеревоОтчетовОбработчикАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры


//////////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Список


//////////////////////////////////////////////////////////////////////////////////
//// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ДеревоОтчетовОбработчикАктивизацииСтроки()
	
	ДеревоОтчетовУстановитьПараметрыСписка();
	
КонецПроцедуры

&НаСервере
Процедура ДеревоОтчетовУстановитьПараметрыСписка()
	
	Если ДеревоОтчетовТекущаяСтрока = Элементы.ДеревоОтчетов.ТекущаяСтрока Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.ДеревоОтчетовТекущаяСтрока = Элементы.ДеревоОтчетов.ТекущаяСтрока;
	
	СтрокаДерева = ЭтаФорма.ДеревоОтчетов.НайтиПоИдентификатору(ЭтаФорма.ДеревоОтчетовТекущаяСтрока);
	Если СтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОтборыСписка(СтрокаДерева.КлючОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеДоступаПриИзменении(Элемент)
	
	УстановитьОграничениеТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОграничениеТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектДоступаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОбъектДоступа) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ОбъектДоступа", ОбъектДоступа);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ОбъектДоступа", "");
	КонецЕсли
	
КонецПроцедуры


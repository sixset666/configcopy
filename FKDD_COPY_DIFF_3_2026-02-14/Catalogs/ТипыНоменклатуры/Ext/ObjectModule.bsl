// Модуль справочника ТипыНоменклатуры
Перем СозданКопированием Экспорт; // признак кого что элемент создан копированием
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ВидНоменклатуры",1);
		ОбязательныеРеквизиты.Вставить("ИспользованиеЕдиницИзмерения",1);
		ОбязательныеРеквизиты.Вставить("ИспользованиеХарактеристик",1);
		Если ИспользованиеЕдиницИзмерения = 1 И 
			 ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.ПрочиеАктивы Тогда
			 ОбязательныеРеквизиты.Вставить("ОсновнаяБазоваяЕдиницаИзмерения",1);
		КонецЕсли;
		Если ВедетсяМаркировка Тогда
			ОбязательныеРеквизиты.Вставить("ТипМаркировки", 1);
		КонецЕсли;
		ОбязательныеРеквизиты.Вставить("ПризнакПредметаРасчета",1);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	РезультатСтандарт = спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Возврат РезультатСтандарт;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Доступность = 1;
	Если ЭтотОбъект.ЭтоНовый() Тогда
	    Возврат Доступность;	
	КонецЕсли;
	
	БлокироватьРеквизиты=Новый Структура();
	Если (ЭтотОбъект.ИспользованиеЕдиницИзмерения = 2) ИЛИ (ЭтотОбъект.ИспользованиеХарактеристик = 2) Тогда
		спрНоменклатура = Справочники.Номенклатура;
    	Отбор = Новый Структура("ТипНоменклатуры");
    	Отбор.ТипНоменклатуры = ЭтотОбъект.Ссылка;
		ВыборкаНоменклатура = спрНоменклатура.Выбрать(,,Отбор,);
		Если НЕ ВыборкаНоменклатура.Следующий() Тогда
			Доступность = Доступность*1;
		Иначе
            Если (ЭтотОбъект.ИспользованиеЕдиницИзмерения = 2) Тогда
				БлокироватьРеквизиты.Вставить("ИспользованиеЕдиницИзмерения","ИспользованиеЕдиницИзмерения");
			КонецЕсли;
			Если (ЭтотОбъект.ИспользованиеХарактеристик = 2) Тогда
				БлокироватьРеквизиты.Вставить("ИспользованиеХарактеристик","ИспользованиеХарактеристик");
			КонецЕсли;
			Доступность = Доступность*0;
		КонецЕсли;
	КонецЕсли;
	Если (ЭтотОбъект.ИспользованиеЕдиницИзмерения = 1) Тогда
		спрЕдиницы = Справочники.ЕдиницыИзмерения;
		ВыборкаЕдиницы = спрЕдиницы.Выбрать(,ЭтотОбъект.Ссылка,,);
		Если НЕ ВыборкаЕдиницы.Следующий() Тогда
			Доступность = Доступность*1;
		Иначе
			БлокироватьРеквизиты.Вставить("ИспользованиеЕдиницИзмерения","ИспользованиеЕдиницИзмерения");
			Доступность = Доступность*0;
		КонецЕсли;
	КонецЕсли;
	Если (ЭтотОбъект.ИспользованиеХарактеристик = 1) Тогда
		спрХарактеристики = Справочники.ХарактеристикиНоменклатуры;
		ВыборкаХарактеристики = спрХарактеристики.Выбрать(,ЭтотОбъект.Ссылка,,);
		Если НЕ ВыборкаХарактеристики.Следующий() Тогда
			Доступность = Доступность*1;
		Иначе
			БлокироватьРеквизиты.Вставить("ИспользованиеХарактеристик","ИспользованиеХарактеристик");
			Доступность = Доступность*0;
		КонецЕсли;
	КонецЕсли;
	
	//Проверяем состояние переключателей
	РезультатИспользованиеЕдиницИзмерения = Ложь;
	РезультатИспользованиеХарактеристик = Ложь;
	Если НЕ ЭтоНовый() Тогда
		//Получим старые значения
		Если ИспользованиеЕдиницИзмерения=2 Тогда
			РезультатИспользованиеЕдиницИзмерения = Истина;
		КонецЕсли;	
		Если ИспользованиеХарактеристик=2 Тогда
			РезультатИспользованиеХарактеристик = Истина;
		КонецЕсли;	
		Если (НЕ (РезультатИспользованиеЕдиницИзмерения)) ИЛИ (НЕ (РезультатИспользованиеХарактеристик)) Тогда
			//Создаем массив элементов справочника Номенклатура с данным типом номенклатуры
			СписокНоменклатуры = Новый Массив();
			спрНоменклатура = Справочники.Номенклатура;
			ЗначениеТипаНоменклатуры = Новый Структура("ТипНоменклатуры",ЭтотОбъект.Ссылка);
			Выборка = спрНоменклатура.Выбрать(,,ЗначениеТипаНоменклатуры);
			Пока Выборка.Следующий() Цикл
				СписокНоменклатуры.Добавить(Выборка.Ссылка);
			КонецЦикла;
			#Если Клиент Тогда
				Состояние("Выполняется поиск движений регистров по данному типу номенклатуры.");
			#КонецЕсли
			Если СписокНоменклатуры.Количество()>0 Тогда
				//Проверяем наличие движений для элементов массива по регистрам накоплений
				ФлагУчастияВДвижениях = Ложь;
				Для Н = 0 По Метаданные.РегистрыНакопления.Количество()-1 Цикл
					Регистр = Метаданные.РегистрыНакопления.Получить(Н);
					Измерения = Регистр.Измерения;
					Для Каждого Измерение Из Измерения Цикл
						Если Измерение.Тип.СодержитТип(ТипЗнч(СписокНоменклатуры[0])) Тогда
							Если спУчастиеВДвижениях(Регистр.Имя, Измерение.Имя, СписокНоменклатуры)Тогда
								ФлагУчастияВДвижениях = Истина;
								Прервать;
							КонецЕсли;	
						КонецЕсли;	
					КонецЦикла;
					Если ФлагУчастияВДвижениях Тогда
						Прервать;
					КонецЕсли;	
				КонецЦикла;
				//Проверяем Изменение ИспользованиеЕдиницИзмерения
				Если (НЕ (РезультатИспользованиеЕдиницИзмерения))
				   И (НЕ (ФлагУчастияВДвижениях)) Тогда
					РезультатИспользованиеЕдиницИзмерения = Истина;
				КонецЕсли;
				//Проверяем Изменение ИспользованиеЕдиницИзмерения
				Если (НЕ (РезультатИспользованиеХарактеристик))
				   И (НЕ (ФлагУчастияВДвижениях)) Тогда
					РезультатИспользованиеХарактеристик = Истина;
				КонецЕсли;   
				#Если Клиент Тогда
					Состояние();
				#КонецЕсли
			Иначе
				//Ни одной номенклатуре не присвоен этот тип. Можно проводить.
				РезультатИспользованиеЕдиницИзмерения = Истина;
				РезультатИспользованиеХарактеристик = Истина;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	Если НЕ (РезультатИспользованиеЕдиницИзмерения) Тогда
		БлокироватьРеквизиты.Вставить("ИспользованиеЕдиницИзмерения","ИспользованиеЕдиницИзмерения");
		Доступность = Доступность*0;
	КонецЕсли;
	Если НЕ (РезультатИспользованиеХарактеристик) Тогда
		БлокироватьРеквизиты.Вставить("ИспользованиеХарактеристик","ИспользованиеХарактеристик");
		Доступность = Доступность*0;
	КонецЕсли;
	
	Возврат Доступность;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда
	
// процедура открытия формы справочника с установкой отбора
//
// Параметры:
// ВидСправочника 	- СправочникСсылка 	- Выбранный справочник
// ВидОтбора 		- ЭлементОтбора 	- Отбор по справочнику
//
Процедура ОткрытьФормуСОтбором(ВидСправочника,ВидОтбора) Экспорт
	Если ЭтотОбъект.ЭтоНовый() Тогда
		Предупреждение("Запишите элемент!");
		Возврат;	    	
	КонецЕсли; 
	спрМенеджер = Новый("СправочникМенеджер."+ВидСправочника);
	фСписка = спрМенеджер.ПолучитьФормуСписка();
	фСписка.Отбор[ВидОтбора].Значение = ЭтотОбъект.Ссылка;
	фСписка.Отбор[ВидОтбора].Использование = Истина;
	фСписка.Открыть();
КонецПроцедуры // ОткрытьФормуСОтбором()

// процедура открытия формы справочника
//
// Параметры:
// ВидСправочника 	- СправочникСсылка 	- Выбранный справочник
//
Процедура ОткрытьФормуСВладельцем(ВидСправочника) Экспорт
	Если ЭтотОбъект.ЭтоНовый() Тогда
		Предупреждение("Запишите элемент!");
		Возврат;	    	
	КонецЕсли; 
	спрМенеджер = Новый("СправочникМенеджер."+ВидСправочника);
	фСписка = спрМенеджер.ПолучитьФормуСписка();
	фСписка.ПараметрОтборПоВладельцу = ЭтотОбъект.Ссылка;
	фСписка.Открыть();
КонецПроцедуры // ОткрытьФормуСВладельцем()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар;
		Весовой = Ложь;
		ИспользованиеХарактеристик = 3;	 	// Не вести учет по характеристикам
		ИспользованиеЕдиницИзмерения = 2;	// Единицы подчинены позициям номенклатуры
		ИспользованиеШтрихКодов = 1;		// Штрих-коды формируются для номенклатуры
		ОграничениеДанныхХарактеристик=Перечисления.ОграниченияДанныхХарактеристики.Все;		// Без ограничений характеристик (хоть они пока и не используются)
		АвтоСписаниеХарактеристик=Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание;	// Без автоматического списания характеристик (хоть они пока и не используются)
		Если обЗначениеНеЗаполнено(ПризнакПредметаРасчета) Тогда
			ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
		КонецЕсли;
	КонецЕсли; 
	
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	СозданКопированием = Истина;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ) 
	//БизТех 20.03.2025г по ТЗ от Тихонов Р. нельзя менять этот справочник без определенных прав! <<
	Если Не (РольДоступна("ПолныеПрава") или РольДоступна("БТ_ПолныйНоменклатура")) Тогда
		Сообщить("У Вас нет прав изменять справочник ""Типы номенклатуры""! Изменения не записаны!");
		Отказ = истина;
	КонецЕсли;
	//>>

	Если (НЕ ЭтоГруппа) И ВидНоменклатуры=Перечисления.ВидыНоменклатуры.ПрочиеАктивы Тогда
		ОсновнаяБазоваяЕдиницаИзмерения=Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("шт",Истина).Ссылка;
	КонецЕсли;
	
	Если (НЕ ЭтоГруппа) И УникальностьСерийногоНомера Тогда // Проверим не нужно ли снять флажок уникальности серийного номера
		Если (ИспользованиеХарактеристик<>2)
			ИЛИ ((ОграничениеДанныхХарактеристик<>Перечисления.ОграниченияДанныхХарактеристики.Все)
				И(ОграничениеДанныхХарактеристик<>Перечисления.ОграниченияДанныхХарактеристики.СерийныйНомер_Свойства)) Тогда
			УникальностьСерийногоНомера = ЛОЖЬ;
		КонецЕсли;
	КонецЕсли;
	
	Если (НЕ ЭтоГруппа) И НЕ ВедетсяМаркировка Тогда
		МаркировкаОбязательная = Ложь;
	КонецЕсли;
	
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ
СозданКопированием = Ложь;

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
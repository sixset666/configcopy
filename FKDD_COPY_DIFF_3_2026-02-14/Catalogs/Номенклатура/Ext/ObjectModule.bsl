// Модуль справочника Номенклатура

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем ОсновнаяЕдиницаИзмеренияОбязательная Экспорт; // Основная единица измерения
Перем НовыйЭлемент;

// Рекурсивно перебирает состав всех наборов, входящих в табличную часть переданного элемента
//
// Параметры:
//   Набор - СправочникСсылка.Номенклатура, набор/комплект, в составе которого делает поиск.
//
// Возвращаемое значение:
//   Булево - Истина - Текущий объект входит в состав набора или его составляющих  
//   	      Ложь   - Текущий объект не входит в состав набора или его составляющих  
//
Функция	ПроверитьКольцевыеСсылки(Набор) Экспорт
	Для Каждого Строка Из Набор.СоставНабора Цикл
		Если Строка.Номенклатура = ЭтотОбъект.Ссылка Тогда
			Возврат Истина;
		ИначеЕсли Строка.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор ИЛИ Строка.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Комплект Тогда
			Если ПроверитьКольцевыеСсылки(Строка.Номенклатура) Тогда
				Возврат Истина;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	Возврат Ложь;
КонецФункции // ПроверитьКольцевыеСсылки()	

#Если Клиент Тогда
	
	// Процедура формирует отчет "Остатки и обороты партий товаров",
// с отбором по указанной номенклатуре
Процедура ОтчетОстаткиИОборотыПартийТоваров() Экспорт
	
	Если Ссылка.Пустая() ИЛИ Ссылка.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли; 
	
	// заполним параметры отчета
	Отчет = Отчеты.ОстаткиИОборотыПартийТоваров.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.ОстаткиИОбороты;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания",        КонецДня(ТекущаяДата()));
	
	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	Показатели      = НастройкиВарианта.Выбор;
	ОтборОтчета     = НастройкиВарианта.Отбор;
	СтруктураОтчета = НастройкиВарианта.Структура;
	
	Для Каждого ТекПоказатель Из Показатели.Элементы Цикл
		Если СокрЛП(ТекПоказатель.Поле) = "Приход" ИЛИ СокрЛП(ТекПоказатель.Поле) = "Расход" Тогда
			ТекПоказатель.Использование = Истина;
		Иначе
			ТекПоказатель.Использование = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// фильтры
	ОтборОтчета.Элементы.Очистить();
	
	ТекОтбор = ОтборОтчета.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ТекОтбор.Использование  = Истина;
	ТекОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Номенклатура");
	ТекОтбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ТекОтбор.ПравоеЗначение = Ссылка;
	
	СтруктураОтчета.Очистить();
	
	ТаблицаОтчета = СтруктураОтчета.Добавить(Тип("ТаблицаКомпоновкиДанных"));
	
	ГруппировкаСклад = ОтчетыСервер.СКД_ДобавитьГруппировку(ТаблицаОтчета.Строки, "СкладКомпании");
	ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаСклад.Структура, "ПериодРегистратор");
	
	НастройкиВарианта.ПараметрыВывода.УстановитьЗначениеПараметра("РасположениеРеквизитов", РасположениеРеквизитовКомпоновкиДанных.Отдельно);
	
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
	
КонецПроцедуры // ОтчетОстаткиИОборотыПартийТоваров()

#КонецЕсли

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);

    Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ТипНоменклатуры",1);
		ОбязательныеРеквизиты.Вставить("ВидНоменклатуры",1);
		ОбязательныеРеквизиты.Вставить("СтавкаНДС",1);
		ОбязательныеРеквизиты.Вставить("БазоваяЕдиницаИзмерения",1);
		//Если ЭтоНовый()=Ложь Тогда
		Если ОсновнаяЕдиницаИзмеренияОбязательная Тогда
			ОбязательныеРеквизиты.Вставить("ОсновнаяЕдиницаИзмерения",1);
		КонецЕсли;
		Если ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			ОбязательныеРеквизиты.Вставить("СпособРаспределенияДопРасходов",1);
		КонецЕсли;
		Если СпособРаспределенияДопРасходов = Перечисления.СпособыРаспределенияДопРасходов.НаДоходыИРасходы Тогда
			ОбязательныеРеквизиты.Вставить("СтатьяДопРасходов",1);
		КонецЕсли;
		Если Производитель.Пустая() Тогда
			Если обПраво("НомерПоКаталогуУникальный",Права) Тогда
				ОбязательныеРеквизиты.Вставить("АртикулДляПоиска",3);
			КонецЕсли;
		Иначе
			Если Производитель.АртикулыОбязательны Тогда
				ОбязательныеРеквизиты.Вставить("АртикулДляПоиска",1);
			КонецЕсли;
		КонецЕсли;
		ОбязательныеРеквизиты.Вставить("ВалютаУчета",1);
		Если СоставНабора.Количество() > 0 Тогда
			Если ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Комплект ИЛИ ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор Тогда
				ОбязательныеСоставНабора = Новый Структура();
				ОбязательныеСоставНабора.Вставить("Номенклатура",3);
				ОбязательныеСоставНабора.Вставить("Количество",1);
				ОбязательныеРеквизиты.Вставить("СоставНабора",ОбязательныеСоставНабора);
			КонецЕсли;
		КонецЕсли;
		
		// Проверка на уникальность строк в табличной части "Состав набора".
		Если (ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Комплект) ИЛИ (ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Набор) Тогда 
			ОбязательныеТовары = Новый Структура();
			ОбязательныеТовары.Вставить("Номенклатура", 3);
			ОбязательныеТовары.Вставить("ХарактеристикаНоменклатуры", 2);
			ОбязательныеРеквизиты.Вставить("СоставНабора", ОбязательныеТовары);
		КонецЕсли;
		
		Если Прослеживаемый И ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга
			И ТипНоменклатуры.ВидНоменклатуры <> Перечисления.ВидыНоменклатуры.Услуга Тогда
			ОбязательныеРеквизиты.Вставить("КодТНВЭД",1);
		КонецЕсли;
		
	КонецЕсли;
		
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Результат = спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Если НЕ ЭтоГруппа Тогда
		Если НЕ Производитель.Пустая() Тогда
			Если Производитель.НаименованияУникальны Тогда
				НайденнаяНоменклатура = Справочники.Номенклатура.НайтиНоменклатуру(АртикулДляПоиска, Производитель, Наименование, Ссылка);
				ТекстОшибки = ?(ЗначениеЗаполнено(АртикулДляПоиска), "Номенклатура производителя "+Производитель+" с артикулом """+Артикул+""" уже есть в справочнике", "Номенклатура производителя "+Производитель+" с пустым артикулом и наименованием """+Наименование+""" уже есть в справочнике");
			Иначе
				НайденнаяНоменклатура = Справочники.Номенклатура.НайтиНоменклатуру(АртикулДляПоиска, Производитель,, Ссылка);
				ТекстОшибки = "Номенклатура производителя "+Производитель+" с артикулом """+Артикул+""" уже есть в справочнике";
			КонецЕсли;
			Если НЕ НайденнаяНоменклатура.Пустая() Тогда
				дкДобавитьОшибку(Ошибки, ТекстОшибки);
				Результат = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если (ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Комплект) ИЛИ (ВидНоменклатуры=Перечисления.ВидыНоменклатуры.Набор) Тогда 
			Для Каждого ТекСтрока Из СоставНабора Цикл
				Если ТекСтрока.Номенклатура.ТипНоменклатуры.ИспользованиеХарактеристик<=2 Тогда
					Если ТекСтрока.ХарактеристикаНоменклатуры.Пустая() И 
						(обЗначениеНеЗаполнено(ТекСтрока.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик) ИЛИ
						ТекСтрока.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание) Тогда
						дкДобавитьОшибку(Ошибки, "Таблица <Состав набора> значение колонки <Характеристика номенклатуры> не заполнено! Строка: " + ТекСтрока.НомерСтроки);	
						Результат = Ложь;
					ИначеЕсли (НЕ обЗначениеНеЗаполнено(ТекСтрока.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик)) И
						(НЕ ТекСтрока.Номенклатура.ТипНоменклатуры.АвтоСписаниеХарактеристик = Перечисления.РежимыАвтоСписанияХарактеристик.РучноеСписание) Тогда
						 Результат = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
		
		Если НЕ ПроверитьКорректностьСтавкиНДС() Тогда
			дкДобавитьОшибку(Ошибки, "Указана недействительная ставка НДС!");
			Результат = Ложь;
		КонецЕсли;
		
		Если НЕ ПроверитьКорректностьПризнаковПрослеживаемостиИМаркировки() Тогда
			дкДобавитьОшибку(Ошибки, "Товар не может быть одновременно прослеживаемым и маркируемым!");
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Результат=Истина;
	
	Если (НЕ ЭтоНовый()) И (НЕ ЭтоГруппа) Тогда
		//Попытаемся найти движения регистров накопления, в которых участвовала эта номенклатура
		ФлагУчастияВДвижениях = Ложь;
		Для Н = 0 По Метаданные.РегистрыНакопления.Количество()-1 Цикл
			Регистр = Метаданные.РегистрыНакопления.Получить(Н);
			Измерения = Регистр.Измерения;
			Для Каждого Измерение Из Измерения Цикл
				Если Измерение.Тип.СодержитТип(ТипЗнч(ЭтотОбъект.Ссылка)) Тогда
					Если спУчастиеВДвижениях(Регистр.Имя, Измерение.Имя, ЭтотОбъект.Ссылка)Тогда
						ФлагУчастияВДвижениях = Истина;
						Прервать;
					КонецЕсли;	
				КонецЕсли;	
			КонецЦикла;
			Если ФлагУчастияВДвижениях Тогда
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		Если ФлагУчастияВДвижениях Тогда
			//Проверяем возможное изменение типа номенклатуры
			Если ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 1 Тогда
				Результат = Ложь;
			КонецЕсли;
			Если ТипНоменклатуры.ИспользованиеХарактеристик = 1 Тогда
				Результат = Ложь;
			КонецЕсли;
		КонецЕсли;   		
		БлокироватьРеквизиты = Новый Структура;
		Если НЕ Результат Тогда
			БлокироватьРеквизиты.Вставить("ТипНоменклатуры");
		КонецЕсли; 	
		//Проверка прочих активов. Если введен в эксплуатацию, то нельзя изменять вид номенклатуры
		ВидНоменклатурыПрочийАктив = Перечисления.ВидыНоменклатуры.ПрочиеАктивы;
		Если ВидНоменклатуры=ВидНоменклатурыПрочийАктив Тогда
			//Проверяем, были ли движения по прочим активам
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПрочиеАктивыВЭксплуатацииОбороты.ПрочийАктив КАК Актив
			|ИЗ
			|	РегистрНакопления.ПрочиеАктивыВЭксплуатации.Обороты КАК ПрочиеАктивыВЭксплуатацииОбороты
			|ГДЕ
			|	ПрочиеАктивыВЭксплуатацииОбороты.ПрочийАктив.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Номенклатура", Ссылка);
			Рез = Запрос.Выполнить();
			Если НЕ Рез.Пустой() Тогда
				БлокироватьРеквизиты.Вставить("ВидНоменклатуры");
				Результат = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Результат;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя="СоставНабора.Номенклатура" Тогда
		Если ТекСтрока.Номенклатура.Пустая() Тогда
			ТекСтрока.ХарактеристикаНоменклатуры = Неопределено;
			Возврат Истина;
		Иначе
			Если ТекСтрока.Количество=0 Тогда
				ТекСтрока.Количество  = 1;	
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ТекСтрока.ХарактеристикаНоменклатуры.Пустая() Тогда
			Если (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура) И (ТекСтрока.ХарактеристикаНоменклатуры.Владелец<>ТекСтрока.Номенклатура.ТипНоменклатуры) Тогда
				ТекСтрока.ХарактеристикаНоменклатуры = Неопределено;
				ЭтотОбъект.ОбработкаРеквизита("СоставНабора.ХарактеристикаНоменклатуры", ТекСтрока, ЭтаФорма);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Имя="СоставНабора.ХарактеристикаНоменклатуры" Тогда
		
	ИначеЕсли Имя="Артикул"
		ИЛИ Имя="Производитель" Тогда
		АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул,Производитель);   
		
	ИначеЕсли Имя="СтавкаНДС" Тогда
		
		// Выведем предупреждение
		#Если Клиент Тогда
			Если НЕ ПроверитьКорректностьСтавкиНДС() Тогда
				Сообщить("Выбрана недействительная ставка НДС!", СтатусСообщения.Внимание);
			КонецЕсли;
		#КонецЕсли
		
	Иначе
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

// функция ищет в справочнике "Единицы измерения" единицу для объекта которая соответствует его
// базовой единице измерения
//
// Параметры:
//	Номенклатура - СправочникСсылка - Ссылка на элемент номенклатуры
//
// Возвращаемое значение:
//	СправочникСсылка - Единица измерения
//
Функция ПолучитьЕдиницуИзмеренияПоБазовой() Экспорт
	
	// посмотрим как у нас хранятся единицы измерения
	Если ЭтотОбъект.ТипНоменклатуры.ИспользованиеЕдиницИзмерения=1 Тогда // подчинены типу номенклатуры
		ВладелецЕдиниц = ЭтотОбъект.ТипНоменклатуры;
		БазоваяЕдиница = ЭтотОбъект.ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения;
	Иначе // подчинены номенклатуре
		ВладелецЕдиниц = ЭтотОбъект.Ссылка;
		БазоваяЕдиница = ЭтотОбъект.БазоваяЕдиницаИзмерения;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВладелецЕдиниц) Тогда
		Возврат Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;
	
	// ищем единицу
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕдиницыИзмерения.Ссылка,
	|	ЕдиницыИзмерения.Коэффициент КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.Наименование = &ЕдиницаПоКлассификатору ТОГДА
	|			0
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ КАК ПолеСортировки
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	ЕдиницыИзмерения.Владелец = &ВладелецЕдиниц И 
	|	ЕдиницыИзмерения.ПометкаУдаления = ЛОЖЬ И 
	|	ЕдиницыИзмерения.ЕдиницаПоКлассификатору = &БазоваяЕдиницаИзмерения
	|УПОРЯДОЧИТЬ ПО
	|	Коэффициент ВОЗР,
	|	ПолеСортировки ВОЗР
	|");
	
	Запрос.УстановитьПараметр("ВладелецЕдиниц",          ВладелецЕдиниц);
	Запрос.УстановитьПараметр("БазоваяЕдиницаИзмерения", БазоваяЕдиница);
	Запрос.УстановитьПараметр("ЕдиницаПоКлассификатору", БазоваяЕдиница.Наименование);
	Выборка = Запрос.Выполнить().Выбрать();
	// если нашли, то вернем единицу, иначе создадим новую
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		// не нашли - создаем новую
		НоваяЕдиница=Справочники.ЕдиницыИзмерения.СоздатьЭлемент();
		НоваяЕдиница.Владелец=ВладелецЕдиниц;
		НоваяЕдиница.УстановитьНовыйКод("");
		НоваяЕдиница.ЕдиницаПоКлассификатору=БазоваяЕдиница;
		НоваяЕдиница.Коэффициент=1;
		НоваяЕдиница.Точность=2;
		НоваяЕдиница.Наименование=СокрЛП(БазоваяЕдиница);
		// ++ Alexku 02.02.2016
		Если ЭтотОбъект.ТипНоменклатуры.ИспользованиеЕдиницИзмерения = 2 Тогда
			// подчинены номенклатуре
			// перенесем вес и объем из карточки номенклатуры в основную единицу измерения
			НоваяЕдиница.Вес = ЭтотОбъект.Вес;
			НоваяЕдиница.Объем = ЭтотОбъект.Объем;
			ЭтотОбъект.Вес = 0;
			ЭтотОбъект.Объем = 0;
		КонецЕсли;
		// -- Alexku 
		НоваяЕдиница.Записать();
		Возврат НоваяЕдиница.Ссылка;
	КонецЕсли;
	
КонецФункции // ПолучитьЕдиницуИзмеренияПоБазовой()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	РодительскаяГруппа=ЭтотОбъект.Родитель;
	Если ЭтотОбъект.ЭтоГруппа=Ложь Тогда
		Пока РодительскаяГруппа<>Новый(ТипЗнч(РодительскаяГруппа)) Цикл
			Если РодительскаяГруппа.СтавкаНДС<>Новый(ТипЗнч(СтавкаНДС)) И СтавкаНДС=Новый(ТипЗнч(СтавкаНДС)) Тогда
				СтавкаНДС=РодительскаяГруппа.СтавкаНДС;
			КонецЕсли; 
			Если РодительскаяГруппа.ТипНоменклатуры<>Новый(ТипЗнч(ТипНоменклатуры)) И ТипНоменклатуры=Новый(ТипЗнч(ТипНоменклатуры)) Тогда
				ТипНоменклатуры = РодительскаяГруппа.ТипНоменклатуры;
				БазоваяЕдиницаИзмерения = ТипНоменклатуры.ОсновнаяБазоваяЕдиницаИзмерения;
			КонецЕсли; 
			Если РодительскаяГруппа.ВидНоменклатуры<>Новый(ТипЗнч(ВидНоменклатуры)) И ВидНоменклатуры=Новый(ТипЗнч(ВидНоменклатуры)) Тогда
				ВидНоменклатуры = РодительскаяГруппа.ВидНоменклатуры;
			КонецЕсли; 
			Если РодительскаяГруппа.ВалютаУчета<>Новый(ТипЗнч(ВалютаУчета)) И ВалютаУчета=Новый(ТипЗнч(ВалютаУчета)) Тогда
				ВалютаУчета = РодительскаяГруппа.ВалютаУчета;
			КонецЕсли;
			Если (НЕ обЗначениеНеЗаполнено(РодительскаяГруппа.СпособРаспределенияДопРасходов)) И (обЗначениеНеЗаполнено(СпособРаспределенияДопРасходов)) Тогда
				СпособРаспределенияДопРасходов=РодительскаяГруппа.СпособРаспределенияДопРасходов;
			КонецЕсли; 
			Если (НЕ обЗначениеНеЗаполнено(РодительскаяГруппа.СтатьяДопРасходов)) И (обЗначениеНеЗаполнено(СтатьяДопРасходов)) Тогда
				СтатьяДопРасходов=РодительскаяГруппа.СтатьяДопРасходов;
			КонецЕсли; 
			Если (НЕ обЗначениеНеЗаполнено(РодительскаяГруппа.ПроцентНаценки)) И (обЗначениеНеЗаполнено(ПроцентНаценки)) Тогда
				ПроцентНаценки=РодительскаяГруппа.ПроцентНаценки;
			КонецЕсли; 
			РодительскаяГруппа=РодительскаяГруппа.Родитель;
		КонецЦикла; 

		Если СтавкаНДС=Новый(ТипЗнч(СтавкаНДС)) Тогда
			СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		КонецЕсли; 
		Если ВидНоменклатуры=Неопределено Тогда
			ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар;
		КонецЕсли;
	Иначе	// Это группа
		// копируем параметры по умолчанию от группы верхнего уровня
		ТипНоменклатуры =	РодительскаяГруппа.ТипНоменклатуры;
		ВидНоменклатуры =	РодительскаяГруппа.ВидНоменклатуры;
		ВалютаУчета =		РодительскаяГруппа.ВалютаУчета;
		СтавкаНДС =			РодительскаяГруппа.СтавкаНДС;
		ПроцентНаценки =	РодительскаяГруппа.ПроцентНаценки;
	КонецЕсли; 
	Если (НЕ обЗначениеНеЗаполнено(ТипНоменклатуры)) И (обЗначениеНеЗаполнено(ВидНоменклатуры)) Тогда
		ОбработкаРеквизита("ТипНоменклатуры");
	КонецЕсли; 
	Если ЭтотОбъект.ЭтоГруппа=Ложь И БазоваяЕдиницаИзмерения=Новый(ТипЗнч(БазоваяЕдиницаИзмерения)) Тогда
		БазоваяЕдиницаИзмерения = Константы.ОсновнаяЕдиницаИзмеренияКоличества;
	КонецЕсли;
	Если обЗначениеНеЗаполнено(ВалютаУчета) Тогда
		ВалютаУчета = Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли; 
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если ЭтотОбъект.ЭтоГруппа=Ложь И ЭтоНовый() Тогда
		ОсновнаяЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;	
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Если НЕ ОбъектКопирования.ЭтоГруппа Тогда
		// Затираем артикул - все равно он будет новый
		Артикул = "";
		// ++ Alexku 29.06.2015 С000705215
		АртикулДляПоиска = "";
		// -- Alexku 
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	
	Если НЕ ЭтоГруппа Тогда
		// Надо принудительно сформировать артикул для поиска
		АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, Производитель);
		
		Если Прослеживаемый И (ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга
			ИЛИ ТипНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга) Тогда
			Прослеживаемый = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоНовый() И ПустаяСтрока(Код) Тогда
		УстановитьНовыйКод();
	КонецЕсли; 
	
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Набор И ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Комплект Тогда
			СоставНабора.Очистить();
		КонецЕсли; 
	КонецЕсли;
	
	Если ВидНоменклатуры<>Перечисления.ВидыНоменклатуры.Услуга Тогда
		СпособРаспределенияДопРасходов=Неопределено;
		СтатьяДопРасходов=Неопределено;
		Если НЕ ЭтоГруппа Тогда
			ВидПлатежа=Неопределено;
		КонецЕсли; 
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга ИЛИ ТипНоменклатуры.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			Если ЗначениеЗаполнено(КодТНВЭД) Тогда
				КодТНВЭД = Справочники.КлассификаторТНВЭД.ПустаяСсылка();
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	Если (НЕ ЭтоГруппа) И (ЭтоНовый()) Тогда
		Автор=ПараметрыСеанса.Пользователь;
		Дата=ТекущаяДата();
	КонецЕсли; 
	
	//БизТех 19.03.2025г. Аляль по ТЗ от Тихонов Р. <<  
	Если ЭтоНовый() и НЕ РольДоступна("ПолныеПрава") Тогда   
		//БизТех 15.07.2025г. Тихонов Р. возвращаем проверку Производителя, добавляем проверку Артикула <<
		Если НЕ ЭтоГруппа Тогда 
			Если обЗначениеНеЗаполнено(Производитель) Тогда
				Сообщить("Не заполнено значение ""Производитель"". Если его нет, то выберите значение ""Неопределено"".");
				Отказ=Истина;
			КонецЕсли;    
				
			Если НЕ (ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга или ВидНоменклатуры = Перечисления.ВидыНоменклатуры.ПрочиеАктивы) Тогда
				Если обЗначениеНеЗаполнено(Артикул) Тогда
					Сообщить("Не заполнено поле ""Артикул"". Используйте код карточки товара, если артикул не известен.");
					Отказ=Истина;
				КонецЕсли;
            КонецЕсли;
		КонецЕсли;  
		Если Отказ = Истина Тогда Возврат КонецЕсли;
		//БизТех 15.07.2025г. >>
		Если ЗначениеЗаполнено(ТипНоменклатуры.Родитель) Тогда
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
			#Если КлиентскоеПриложение Тогда
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
			Предупреждение("Тип номенклатуры из архива! Объект не записан! 
							|Выберите Тип номенклатуры из списка ",10,"Тип номенклатуры");
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
			#КонецЕсли
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
			Отказ = истина;
		КонецЕсли;
		Если НЕ РольДоступна("БТ_ПолныйНоменклатура") и НЕ Родитель = Справочники.Номенклатура.НайтиПоКоду("ЦБ065400") Тогда
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
			#Если КлиентскоеПриложение Тогда
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
			Предупреждение("У вас нет прав создавать новую номенклатуру в этой папке! 
							|Новая номенклатура будет автоматически перемещена в папку ""Новая номенклатура""!",10,"Новая номенклатура");
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++
			#КонецЕсли
			//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
			Родитель = Справочники.Номенклатура.НайтиПоКоду("ЦБ065400");	
		КонецЕсли;
	КонецЕсли;
	//>>
	НовыйЭлемент = ЭтоНовый();
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// зарегистрируем изменение товара в оборудовании
	уоУстановитьМоментИзменения(Ссылка,ТекущаяДата());
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

#Если Клиент Тогда
	
// Процедура открывает автокаталог
Процедура ОткрытьАвтокаталог() Экспорт
	Если обЗначениеНеЗаполнено(ЭтотОбъект) Тогда Возврат; КонецЕсли;
	Если ЭтотОбъект.ЭтоГруппа Тогда Возврат; КонецЕсли;
	
	//Если объект автокаталога не создан - создадим
	Если глАвтокаталог=Неопределено Тогда
		Попытка
			глАвтокаталог=Новый COMОбъект("AutoCat10.AutoDealer");
		Исключение
			Сообщить("Ошибка подключения к системе автокаталога: "+ОписаниеОшибки());
			Возврат;
		КонецПопытки; 
	КонецЕсли; 
	
	//Получим применяемость детали
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НоменклатураПрименяемость.Модель КАК Модель
	|ИЗ
	|	РегистрСведений.НоменклатураПрименяемость КАК НоменклатураПрименяемость
	|ГДЕ
	|	НоменклатураПрименяемость.Номенклатура = &Номенклатура";
	Запрос.УстановитьПараметр("Номенклатура",ЭтотОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Сообщить("У номенклатуры <"+СокрЛП(ЭтотОбъект.Ссылка)+"> применяемость по моделям не задана.");
		Возврат;
	КонецЕсли;
	Модель=Выборка.Модель.Наименование;
	//Получим производителя модели
	Позиция=Найти(Модель," ");
	Завод=?(Позиция=0,Модель,Лев(Модель,Позиция-1));
	//Формирование параметра запуска автокаталога
	МассивПараметров = Новый Массив();
	Если обЗначениеНеЗаполнено(ЭтотОбъект.КартинкаАвтокаталога) Тогда
	Иначе
		//разберем на параметры
		Картинка = ЭтотОбъект.КартинкаАвтокаталога + ",";
		Пока Истина Цикл
			ПозицияРазделителя = Найти(Картинка, ",");
			Если ПозицияРазделителя = 0 Тогда
				Прервать;
			КонецЕсли;
			МассивПараметров.Добавить(Лев(Картинка, ПозицияРазделителя - 1));
			Картинка = Сред(Картинка, ПозицияРазделителя + 1);
		КонецЦикла;
	КонецЕсли;
	//проверим достаточно ли параметров для запуска
	Если МассивПараметров.Количество() <> 4 Тогда
		Сообщить("Неверный идентификатор детали автокаталога.");
		Возврат;
	КонецЕсли;
	
	Попытка
		//Попытаемся запустить автокаталог
		глАвтокаталог.GetPicture(Число(МассивПараметров[0]), Число(МассивПараметров[1]), Число(МассивПараметров[2]),
								Строка(МассивПараметров[3]));
		//Запуск удался - выходим
		Возврат;
	Исключение
		//При запуске автокаталога произошла ошибка
		глАвтокаталог=Неопределено;
		//Попробуем создать объект автокаталога еще раз
		Попытка
			глАвтокаталог=Новый COMОбъект("AutoCat10.AutoDealer");
		Исключение
			Сообщить("Ошибка подключения к системе автокаталога: "+ОписаниеОшибки());
			Возврат;
		КонецПопытки; 
	КонецПопытки; 
	Попытка
		//Попытаемся запустить автокаталог еще раз
		глАвтокаталог.GetPicture(Число(МассивПараметров[0]), Число(МассивПараметров[1]), Число(МассивПараметров[2]),
								Строка(МассивПараметров[3]));
		//Запуск удался - выходим
		Возврат;
	Исключение
		//Повторный запуск тоже не удался
		Сообщить("Ошибка при инициализации автокаталога: "+ОписаниеОшибки());
		Возврат;
	КонецПопытки; 
КонецПроцедуры // ОткрытьАвтокаталог()

#КонецЕсли

// Функция возвращает идентификатор группы аналогов
Функция ИдентификаторГруппыАналогов() Экспорт
	
	ВедениеУчетаАналоговВРазрезеПроизводителей = обПраво("ВедениеУчетаАналоговВРазрезеПроизводителей");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ГруппыАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы
	               |ИЗ
	               |	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	               |ГДЕ
	               |	ГруппыАналогов.АртикулДляПоиска = &Артикул
	               |	" + ?(ВедениеУчетаАналоговВРазрезеПроизводителей, "И ГруппыАналогов.Производитель = &Производитель", "") + "
				   |";
	Запрос.УстановитьПараметр("Производитель",	Производитель);
	Запрос.УстановитьПараметр("Артикул",		АртикулДляПоиска);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИдентификаторГруппы;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // ИдентификаторГруппыАналогов() 

//Получает идентификатор группы аналогов и если группы нет, генерирует новый идентификатор
Функция ПолучитьИдентификаторГруппыАналогов() Экспорт
	ИДГруппы=ИдентификаторГруппыАналогов();
	Если ИДГруппы=Неопределено Тогда
		ИДГруппы=СокрЛП(Новый УникальныйИдентификатор);
	КонецЕсли; 
	Возврат ИДГруппы;
КонецФункции // ПолучитьИдентификаторГруппыАналогов() 

// Проверим корректность ставки НДС для устранения ситуации выбора ставки НДС 18%
//
Функция ПроверитьКорректностьСтавкиНДС() Экспорт
	
	// По закону с 01.01.2019 вместо ставки НДС 18% необходимо использовать ставку НДС 20%.
	// По закону с 01.01.2026 вместо ставки НДС 20% необходимо использовать ставку НДС 22%.	
	
	ТекДата = ТекущаяДатаСеанса();
	
	СтавкаНДСНоменклатуры = СтавкаНДС.Ставка;
	
	// !!!_Переход на ставку НДС 20%
	// После 01.01.2019 убрать проверку на ввод ставки НДС 20%
	
	//Снимченко_bizteh++
	Если ТекДата < Дата("20260101") Тогда
		Если СтавкаНДСНоменклатуры = 22 Тогда
			Возврат Ложь;
		КонецЕсли;
	//Снимченко_bizteh--
	ИначеЕсли ТекДата < Дата("20190101") Тогда
		Если СтавкаНДСНоменклатуры = 20 Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Если СтавкаНДСНоменклатуры = 18 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьСтавкиНДС()

Функция ПроверитьКорректностьПризнаковПрослеживаемостиИМаркировки()
	
	Возврат НЕ (Прослеживаемый И ТипНоменклатуры.ВедетсяМаркировка);
	
КонецФункции
////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

ОсновнаяЕдиницаИзмеренияОбязательная=Ложь;
НовыйЭлемент = Ложь;

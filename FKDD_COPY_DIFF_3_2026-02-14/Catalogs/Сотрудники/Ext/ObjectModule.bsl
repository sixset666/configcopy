// Модуль справочника Сотрудники

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем СозданКопированием Экспорт; // Признак того что элемент создан копированием


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

//Формирование отчета по взаиморасчетам с контрагентом, связанным с сотрудником
Процедура ВзаиморасчетыССотрудником() Экспорт
	// проверки
	Если Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
	
	Отчет = Отчеты.Взаиморасчеты.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.Остатки;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	Если НЕ НастройкиВарианта.ПараметрыДанных.Элементы.Найти("ДатаНачала") = Неопределено Тогда
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",    Дата(1,1,1));
	КонецЕсли;
	
	Если НЕ НастройкиВарианта.ПараметрыДанных.Элементы.Найти("ДатаОкончания") = Неопределено Тогда
		НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", РабочаяДата);
	КонецЕсли;
	
	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	Отбор           = НастройкиВарианта.Отбор;
	СтруктураОтчета = НастройкиВарианта.Структура;
	Показатели      = НастройкиВарианта.Выбор;
	
	Для Каждого ТекПоказатель Из Показатели.Элементы Цикл
		Если ТипЗнч(ТекПоказатель) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			ТекПоказатель.Использование = Истина;
			ИмяФункции = СокрЛП(ТекПоказатель.Поле);
			Для Каждого ТекВложенныйПоказатель Из ТекПоказатель.Элементы Цикл
				ИмяПоказателя = СтрЗаменить(ТекВложенныйПоказатель.Поле, ИмяФункции+".", "");
				ТекВложенныйПоказатель.Использование = (ИмяПоказателя="ИтогоСумма" ИЛИ ИмяПоказателя="ИтогоСуммаБаз");
			КонецЦикла;
		ИначеЕсли НЕ ТипЗнч(ТекПоказатель) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			
			ПозицияТочки = Найти(ТекПоказатель.Поле, ".");
			Если ПозицияТочки > 0 Тогда
				ИмяПоказателя = Сред(ТекПоказатель.Поле, ПозицияТочки+1);
			Иначе
				ИмяПоказателя = СокрЛП(ТекПоказатель.Поле);
			КонецЕсли;
			
			ТекПоказатель.Использование = (ИмяПоказателя="ИтогоСумма" ИЛИ ИмяПоказателя="ИтогоСуммаБаз");
		КонецЕсли;
	КонецЦикла;
	
	Отбор.Элементы.Очистить();
	
	ДобавитьКонтрагента = Истина;
	// фильтры
	ТекОтбор = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ТекОтбор.Использование  = Истина;
	ТекОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Контрагент.Сотрудник");
	ТекОтбор.ПравоеЗначение = Ссылка;
	
	Если Ссылка.ЭтоГруппа Тогда
		ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
	Иначе
		ТекОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ДобавитьКонтрагента = Ложь;
	КонецЕсли; 
	
	СтруктураОтчета.Очистить();
	
	ТаблицаОтчета = СтруктураОтчета.Добавить(Тип("ТаблицаКомпоновкиДанных"));
	
	//добавляем измерения строки
	Если ДобавитьКонтрагента Тогда
		ГруппировкаКонтрагент = ОтчетыСервер.СКД_ДобавитьГруппировку(ТаблицаОтчета.Строки, "Контрагент");
		ГруппировкаДоговор    = ОтчетыСервер.СКД_ДобавитьГруппировку(ГруппировкаКонтрагент.Структура, "ДоговорВзаиморасчетов");
	Иначе
		ГруппировкаДоговор    = ОтчетыСервер.СКД_ДобавитьГруппировку(ТаблицаОтчета.Строки, "ДоговорВзаиморасчетов");
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
	
	#КонецЕсли
	
КонецПроцедуры // ВзаиморасчетыССотрудником()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("Должность",1);
		ОбязательныеРеквизиты.Вставить("Подразделение",1);
		ОбязательныеРеквизиты.Вставить("Организация",1);
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	СозданКопированием = Истина;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ И НЕ ЭтоГруппа Тогда
		ДеньРождения = Формат(ДатаРождения,"ДФ=MM:dd");
	КонецЕсли; 
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

СозданКопированием = Ложь;
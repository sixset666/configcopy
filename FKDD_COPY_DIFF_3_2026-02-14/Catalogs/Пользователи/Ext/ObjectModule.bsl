// Модуль справочника Пользователи

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем СозданКопированием Экспорт; // Признак того что элемент создан копированием
Перем ПользовательИБ Экспорт; // Пользователь базы, соответствующий пользователю компании

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка корректности ИНН на ключ
// Если ИНН корректно возвращается пустая строка.
//
// Параметры:
// 	ПроверяемыйИНН  – Строка – Проверяемый ИНН
//
// Возвращаемое значение:
// 	Строка – Сообщение об ошибке в ИНН.
//
Функция ПроверкаИНН(ПроверяемыйИНН) Экспорт
	ПровИНН = СокрЛП(ПроверяемыйИНН);
	ДлинаИНН = СтрДлина(ПровИНН);
	
	// Проверка длины ИНН
	Если ДлинаИНН <> 12 Тогда
		Возврат "ИНН физического лица должен состоять из 12 цифр.";
	КонецЕсли;

	// Проверка ключа ИНН - сначала составим матрицу простых чисел,
	// потом проверим ключ для разной длины	
	КонтрольнаяСумма11 = 0;
	КонтрольнаяСумма12 = 0;
	
	Для Индекс = 1 По 11 Цикл
		
		// Расчет множителя для 11-го и 12-го разрядов.
		Если Индекс = 1 Тогда
			Множитель11 = 7;
			Множитель12 = 3;
		ИначеЕсли Индекс = 2 Тогда
			Множитель11 = 2;
			Множитель12 = 7;
		ИначеЕсли Индекс = 3 Тогда
			Множитель11 = 4;
			Множитель12 = 2;
		ИначеЕсли Индекс = 4 Тогда
			Множитель11 = 10;
			Множитель12 = 4;
		ИначеЕсли Индекс = 5 Тогда
			Множитель11 = 3;
			Множитель12 = 10;
		ИначеЕсли Индекс = 6 Тогда
			Множитель11 = 5;
			Множитель12 = 3;
		ИначеЕсли Индекс = 7 Тогда
			Множитель11 = 9;
			Множитель12 = 5;
		ИначеЕсли Индекс = 8 Тогда
			Множитель11 = 4;
			Множитель12 = 9;
		ИначеЕсли Индекс = 9 Тогда
			Множитель11 = 6;
			Множитель12 = 4;
		ИначеЕсли Индекс = 10 Тогда
			Множитель11 = 8;
			Множитель12 = 6;
		ИначеЕсли Индекс = 11 Тогда
			Множитель11 = 0;
			Множитель12 = 8;
		КонецЕсли;
		
		Цифра = Число(Сред(ПровИНН, Индекс, 1));
		КонтрольнаяСумма11 = КонтрольнаяСумма11 + Цифра * Множитель11;
		КонтрольнаяСумма12 = КонтрольнаяСумма12 + Цифра * Множитель12;
		
	КонецЦикла;
	
	КонтрольныйРазряд11 = (КонтрольнаяСумма11 %11) %10;
	КонтрольныйРазряд12 = (КонтрольнаяСумма12 %11) %10;
	
	Если КонтрольныйРазряд11 <> Число(Сред(ПровИНН,11,1)) ИЛИ КонтрольныйРазряд12 <> Число(Сред(ПровИНН,12,1)) Тогда
		Возврат "Контрольное число для ИНН не совпадает с рассчитанным.";
	КонецЕсли;
	
	Возврат "";
КонецФункции // ПроверкаИНН()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	ОбязательныеРеквизиты.Вставить("Подразделение",1);
	ОбязательныеРеквизиты.Вставить("Организация",1);

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = Истина;
	
	//в данном справочнике реквизит "Код" - обязательный для заполнения независимо от прав
	Если Заполнение и обЗначениеНеЗаполнено(ЭтотОбъект.Код) Тогда
		Результат=Ложь;
		Ошибки=Ошибки+"Реквизит ""Имя(идентификатор)"" не заполнен !"+Символы.ПС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		ТекстОшибки = ПроверкаИНН(ИНН);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Результат=Ложь;
			Ошибки=Ошибки+ТекстОшибки+Символы.ПС;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Результат И спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	СозданКопированием = Истина;
	ЭтотОбъект.Сотрудник=Неопределено;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Попытка Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли; Исключение КонецПопытки;
	
	// Попытаемся записать пользователя базы 
	Если НЕ Отказ И ПользовательИБ <> Неопределено И ПравоДоступа("Администрирование", Метаданные) Тогда
		Попытка
			ПользовательИБ.Записать();
		Исключение
			Сообщить(ОписаниеОшибки());
			Отказ = Истина;
			Возврат;
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	#Если Клиент Тогда
	Попытка
		ТекущийПользователь = ПараметрыСеанса.Пользователь;
	Исключение
		ТекущийПользователь = Неопределено;
	КонецПопытки;

	Если Ссылка = ТекущийПользователь Тогда
		ОбновлятьПрава=Ложь;
		Если Ссылка.Подразделение<>ПараметрыСеанса.ПодразделениеКомпании Тогда
			//У текущего пользователя сменилось подразделение
			ПараметрыСеанса.ПодразделениеКомпании=Ссылка.Подразделение;
			ОбновлятьПрава=Истина;
		КонецЕсли; 	
		Если Ссылка.Организация<>ПараметрыСеанса.Организация Тогда
			//У текущего пользователя сменилась организация
			ПараметрыСеанса.Организация=Ссылка.Организация;
			ОбновлятьПрава=Истина;
		КонецЕсли; 	
		Если ОбновлятьПрава Тогда
			// Обновим текущий набор прав
			Состояние ("Обновляю права текущего пользователя");
			глПрава=обПолучитьПраваИНастройкиПользователя(ПараметрыСеанса.Пользователь);
			Сообщить("Права текущего пользователя обновлены !",СтатусСообщения.Информация);
			// Поскольку на экране могут быть открыты формы объектов, скажем им, что надо бы обновить кэш прав..
			Оповестить("ОбновитьПрава",ПараметрыСеанса.Пользователь);
		КонецЕсли;
		//Обновим заголовок главного окна
		УстановитьЗаголовокСистемы(Метаданные.Синоним);
		Если обПраво("ИндикацияПользователяВЗаголовкеСистемы",глПрава) Тогда
			УстановитьЗаголовокСистемы(ПолучитьЗаголовокСистемы()+"    Пользователь: <"+ПараметрыСеанса.Пользователь+">");
		КонецЕсли;
		Если обПраво("ИндикацияПодразделенияВЗаголовкеСистемы",глПрава) Тогда
			УстановитьЗаголовокСистемы(ПолучитьЗаголовокСистемы()+"    Подразделение: <"+ПараметрыСеанса.ПодразделениеКомпании+">");
		КонецЕсли;
	КонецЕсли; 
	#КонецЕсли
КонецПроцедуры

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

СозданКопированием = Ложь;
Если ПравоДоступа("Администрирование", Метаданные) И (НЕ ПустаяСтрока(Ссылка.Код)) Тогда
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(СокрЛП(Ссылка.Код))	
КонецЕсли;

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
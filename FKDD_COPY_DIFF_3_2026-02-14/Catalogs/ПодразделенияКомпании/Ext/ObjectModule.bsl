// Модуль справочника ПодразделенияКомпании

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
		Если НЕ (Ссылка=Справочники.ПодразделенияКомпании.ОсновноеПодразделение) Тогда
			ОбязательныеРеквизиты.Вставить("Организация",1);
		КонецЕсли;
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = Истина;
	
	// Строгое правило. В одной ветке может быть только одно балансовое подразделение.
	Если (Ссылка<>Справочники.ПодразделенияКомпании.ОсновноеПодразделение) И Балансовое Тогда
		// Сначала поднимаемся вверх
		БалансовыйРодитель = НайтиБалансовогоРодителя(Родитель);
		Если (БалансовыйРодитель<>Неопределено) И (БалансовыйРодитель<>Справочники.ПодразделенияКомпании.ОсновноеПодразделение) Тогда
			Результат = Ложь;
			Ошибки = Ошибки + "У подразделения, по которому ведется балансовый учет, не может
			|быть родительских подразделений, по которым также ведется балансовый учет (кроме корневого подразделения)." + Символы.ПС;
			Ошибки = Ошибки + " - Балансовое подразделение: " + БалансовыйРодитель + Символы.ПС;
		КонецЕсли;	
		
		// Теперь опускаемся вниз.
		МассивБалансовыхДетей = НайтиБалансовыхДетей(ЭтотОбъект.Ссылка);
		Если МассивБалансовыхДетей.Количество()<>0 Тогда
			Результат = Ложь;
			Ошибки = Ошибки + "У подразделения, по которому ведется балансовый учет, не может
			|быть подчиненных подразделений, по которым также ведется балансовый учет." + Символы.ПС;
			Для каждого ТекПодразделение Из МассивБалансовыхДетей Цикл  	
				Ошибки = Ошибки + " - Балансовое подразделение: " + ТекПодразделение + Символы.ПС;	
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Результат = Результат И спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность); 
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

// Рекурсивная функция по поиску балансовых родителей у элемента.
//
// Параметры:
//  Ссылка – "СправочникСсылка.ПодразделенияКомпании" - текущее подразделение компании.
//
// Возвращаемое значение:
//   Ссылка - ссылка на балансового родителя.
// 
Функция НайтиБалансовогоРодителя(ТекущийРодитель)  	
	Если ТекущийРодитель.Пустая() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТекущийРодитель.Балансовое Тогда
		Возврат ТекущийРодитель;
	Иначе
		Возврат НайтиБалансовогоРодителя(ТекущийРодитель.Родитель);
	КонецЕсли; 	
КонецФункции // НайтиБалансовогоРодителя() 

// Рекурсивная функция по поиску балансовых подразделений, подчиненных текущему
//
// Параметры:
//  Ссылка – "СправочникСсылка.ПодразделенияКомпании" - текущее подразделение.
//
// Возвращаемое значение:
//   Массив - массив подчиненных подразделений, по которым ведется балансовый учет.
// 
Функция НайтиБалансовыхДетей(Ссылка)	
	Массив = Новый Массив;  	
	Если Ссылка.Пустая() Тогда
		Возврат Массив;
	КонецЕсли;
	
	Выборка = Справочники.ПодразделенияКомпании.ВыбратьИерархически(Ссылка);
	Пока Выборка.Следующий() Цикл  	
		Если Выборка.Балансовое Тогда
			Массив.Добавить(Строка(Выборка));	
		КонецЕсли;
	КонецЦикла; 	
	
	Возврат Массив;
КонецФункции // НайтиБалансовыхДетей() 


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	
	БТ_НазваниеДляССП = БТ_ПодразделениеССП; //<<БАА 04.07.25
	
	Загрузка = ЭтотОбъект.ОбменДанными.Загрузка;
	Если Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// На нулевом уровне может находится только одно подразделение "Вся компания"
	Если Ссылка<>Справочники.ПодразделенияКомпании.ОсновноеПодразделение Тогда
		Если Родитель.Пустая() Тогда
			Родитель = Справочники.ПодразделенияКомпании.ОсновноеПодразделение;
		КонецЕсли;
		
		// Если выбран способ ведения баланса не "По подразделению", то
		// снимем признак балансовости у текущего подразделения. 
		// Т.к. фактически баланс по нему все равно не ведется. Флаг только смущает.
		Если Константы.СпособВеденияБаланса.Получить()<>Перечисления.СпособВеденияБаланса.ПоПодразделению Тогда
			Если Балансовое Тогда
				Сообщить(" - У подразделения <" + Ссылка + "> снят признак балансовости.
				|Т.к. в настройке параметров выбран способ ведения балансового учета не ""По подразделению""");	
			КонецЕсли;
			Балансовое = Ложь;	
		КонецЕсли;
	Иначе
		Если НЕ Балансовое Тогда
			Сообщить("Корневое подразделения не может быть небалансовым!");
			Балансовое = Истина;	
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли; 	   	
	
	Если ЗначениеЗаполнено(ВидНалога) Тогда
		ОсвобожденОтНДС=Истина;
	Иначе
		ОсвобожденОтНДС=Ложь;
	КонецЕсли; 
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
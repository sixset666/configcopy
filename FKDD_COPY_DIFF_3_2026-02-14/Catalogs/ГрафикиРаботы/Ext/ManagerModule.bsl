// процедура заполнения предопределенных элементов справочника
//
// Параметры:
// ЗаполнятьВсе - Булево - Признак заполнение всей информации
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	//Заполним основной график работы компании
	ОсновнойГрафикРаботыКомпании = Справочники.ГрафикиРаботы.ГрафикРаботыКомпании.ПолучитьОбъект();
	//будем искать с таким же наименованием
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ГрафикиРаботы.Ссылка КАК ГрафикРаботы
		|ИЗ
		|	Справочник.ГрафикиРаботы КАК ГрафикиРаботы
		|ГДЕ
		|	ГрафикиРаботы.Наименование = &Наименование И ГрафикиРаботы.Предопределенный = Ложь
		|");
	Запрос.УстановитьПараметр("Наименование",ОсновнойГрафикРаботыКомпании.Наименование);
	Выборка = Запрос.Выполнить().Выбрать();
	Номер = 1;
	//если с таким именем есть, будем заменять
	Пока Выборка.Следующий() Цикл
		//используем стандартную обработку
		ФормаЗаменаЗначений = Обработки.ПоискИЗаменаЗначений.ПолучитьФорму();
		ФормаЗаменаЗначений.Программно = Истина;
		СтрокаЗамены = ФормаЗаменаЗначений.ЗаменяемыеЗначения.Добавить();
		СтрокаЗамены.ЧтоЗаменять = Выборка.ГрафикРаботы;
		СтрокаЗамены.НаЧтоЗаменять = Справочники.ГрафикиРаботы.ГрафикРаботыКомпании;
		СтрокаЗамены.Пометка = Истина;
		Сообщить("Справочник ""Графики работы"". Произведена замена ссылок на элемент """ + Выборка.ГрафикРаботы + """ ссылками на элемент """ + Справочники.ГрафикиРаботы.ГрафикРаботыКомпании + """");
		ФормаЗаменаЗначений.Открыть();
		//
		ОбъектГрафикРаботы = Выборка.ГрафикРаботы.ПолучитьОбъект();
		ОбъектГрафикРаботы.Наименование = ОбъектГрафикРаботы.Наименование + "_" + Номер;
		ОбъектГрафикРаботы.Записать();
		Номер = Номер + 1;
	КонецЦикла;
	//перезаполняем реквизиты
	Если ЗаполнятьВсе Тогда
		ОсновнойГрафикРаботыКомпании.Периодичность = Перечисления.Периодичность.Произвольный;
		ОсновнойГрафикРаботыКомпании.ДатаНачала = ТекущаяДата();
		ОсновнойГрафикРаботыКомпании.НачалоРабочегоВремениПоУмолчанию = '00010101090000';
		ОсновнойГрафикРаботыКомпании.КонецРабочегоВремениПоУмолчанию =  '00010101180000';
		ОсновнойГрафикРаботыКомпании.ПродолжительностьПоУмолчанию = '00010101080000';
	Иначе
		Если обЗначениеНеЗаполнено(ОсновнойГрафикРаботыКомпании.Периодичность) Тогда
			ОсновнойГрафикРаботыКомпании.Периодичность = Перечисления.Периодичность.Произвольный;
		КонецЕсли;
	    Если обЗначениеНеЗаполнено(ОсновнойГрафикРаботыКомпании.ДатаНачала) Тогда
			ОсновнойГрафикРаботыКомпании.ДатаНачала = ТекущаяДата();
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ОсновнойГрафикРаботыКомпании.НачалоРабочегоВремениПоУмолчанию) Тогда
			ОсновнойГрафикРаботыКомпании.НачалоРабочегоВремениПоУмолчанию = '00010101090000';
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ОсновнойГрафикРаботыКомпании.КонецРабочегоВремениПоУмолчанию) Тогда
			ОсновнойГрафикРаботыКомпании.КонецРабочегоВремениПоУмолчанию =  '00010101180000';
		КонецЕсли;
		Если обЗначениеНеЗаполнено(ОсновнойГрафикРаботыКомпании.ПродолжительностьПоУмолчанию) Тогда
			ОсновнойГрафикРаботыКомпании.ПродолжительностьПоУмолчанию = '00010101090000';
		КонецЕсли;
	КонецЕсли;
	//Сохранение справочника
	НовыйЭлемент = ОсновнойГрафикРаботыКомпании.ЭтоНовый();
	Если ОсновнойГрафикРаботыКомпании.Модифицированность() Тогда
		Попытка
			ОсновнойГрафикРаботыКомпании.Записать();
			Сообщить("Справочник ""Графики работы"". "+?(НовыйЭлемент,"Добавлен","Обновлен")+" элемент """+СокрЛП(ОсновнойГрафикРаботыКомпании.Наименование)+"""");
		Исключение
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьМагазинПоУмолчанию() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ns_Магазины.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ns_Магазины КАК ns_Магазины";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НовыйМагазин = Справочники.ns_Магазины.СоздатьЭлемент();
		НовыйМагазин.Наименование = "Магазин Авито №1";
		НовыйМагазин.ИмяФайлаВыгрузки = "avito.xml";
		НовыйМагазин.ПапкаВыгрузки = "store1";
		НовыйМагазин.Префикс = "st1";
		НовыйМагазин.Записать();
	КонецЕсли;  
	
КонецПроцедуры

Функция ПолучитьТаблицуМагазинов(Повтор = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_Магазины.Наименование КАК Наименование,
	|	ns_Магазины.Ссылка КАК Магазин,
	|	ns_Магазины.Префикс КАК Префикс,
	|	ns_Магазины.Код КАК Код,
	|	ns_Магазины.ПапкаВыгрузки КАК ПапкаВыгрузки,
	|	ns_Магазины.Шаблон КАК Шаблон,
	|	ns_Магазины.СхемаПроезда КАК СхемаПроезда
	|ИЗ
	|	Справочник.ns_Магазины КАК ns_Магазины";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Если Повтор = Ложь Тогда
			СоздатьМагазинПоУмолчанию();
			Возврат ПолучитьТаблицуМагазинов(Истина);
		КонецЕсли; 
		РегистрыСведений.ns_ЖурналАвито.ЗаписатьОшибкуВЖурнал(Перечисления.ns_ВидыСобытийЖурнала.ВыгрузкаОтменена, "Не заполнен справочник ""Магазины Авито""");
		Возврат Неопределено;
	КонецЕсли; 

	Возврат РезультатЗапроса.Выгрузить();
КонецФункции

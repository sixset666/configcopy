// Модуль справочника Контрагенты

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем СогласиеНаОбработкуПерсональныхДанныхТек;	//Текущее значение реквизита СогласиеНаОбработкуПерсональныхДанных

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка корректности ИНН на ключ
// Если ИНН корректно возвращается пустая строка.
//
// Параметры:
// 	ПроверяемыйИНН  – Строка – Проверяемый ИНН
//
// Возвращаемое значение:
// 	Строка – Сообщение об ошибке в ИНН.
//
Функция ПроверкаИНН(ПроверяемыйИНН) Экспорт
	ПровИНН = СокрЛП(ПроверяемыйИНН);
	ДлинаИНН = СтрДлина(ПровИНН);
	
	// Проверка длины ИНН
	Если ДлинаИНН < 10 Тогда
		Возврат "ИНН не должен быть меньше 10-ти цифр";
	КонецЕсли;

	// Проверка ключа ИНН - сначала составим матрицу простых чисел,
	// потом проверим ключ для разной длины
	КодКонст = Новый Массив;
	КодКонст.Добавить(41);
	КодКонст.Добавить(37);
	КодКонст.Добавить(31);
	КодКонст.Добавить(29);
	КодКонст.Добавить(23);
	КодКонст.Добавить(19);
	КодКонст.Добавить(17);
	КодКонст.Добавить(13);
	КодКонст.Добавить(7);
	КодКонст.Добавить(5);
	КодКонст.Добавить(3);
	
	НСум = 0;
	Если ДлинаИНН = 10 Тогда
		// Для ИНН 10 знаков
		Для Сч = 1 По 9 Цикл
			НСум = НСум + Число(Сред(ПровИНН, Сч, 1)) * КодКонст[Сч + 1];
		КонецЦикла;
		//----------------------------------------------- 
		ННов = 11 - (НСум - (Цел(НСум / 11) * 11)); 
		КНов = Сред(ПровИНН, 1, 9) + ?(ННов < 10, Строка(ННов), "0");
		Если КНов = ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	
	ИначеЕсли ДлинаИНН = 11 Тогда
		// Для ИНН 11 знаков
		КНов = "1" + Сред(ПровИНН, 2, 9);
		ПровИНН = ВРег(ПровИНН);
		Для Сч = 1 По 10 Цикл
			НСум = НСум + Число(Сред(КНов,Сч,1)) * КодКонст[Сч];
		КонецЦикла; 
		//----------------------------------------------- 
		ННов = 11 - (НСум - (Цел(НСум/11)*11));
		КНов = "F" + Сред(КНов, 2, 9) + ?(ННов < 10, Строка(ННов), "0");
		Если КНов = ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	
	ИначеЕсли ДлинаИНН = 12 Тогда			// Для 12 знаков
		Если Число(ПровИНН) * Число(Сред(ПровИНН, 5, 6)) = 0 Тогда
			Возврат "Неправильный ИНН - ошибка ключа";
		КонецЕсли;	
		//----------------------------------------------- 
		Для Сч = 1 По 10 Цикл
			НСум = НСум + Число(Сред(ПровИНН, Сч, 1)) * КодКонст[Сч];
		КонецЦикла;	
		ННов = 11 - (НСум - (Цел(НСум / 11) * 11));
		КНов = Сред(ПровИНН, 1, 10) + ?(ННов < 10, Строка(ННов), "0");	
		НСум = 0;
		//----------------------------------------------- 
		Для Сч = 1 По 11 Цикл
			НСум = НСум + Число(Сред(ПровИНН, Сч, 1)) * КодКонст[Сч - 1];
		КонецЦикла;
		ННов = 11-(НСум - (Цел(НСум / 11) * 11));
		КНов = ?(ННов < 10,КНов+Строка(ННов), КНов + "0");	
		Если КНов = ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	КонецЕсли; 	
	Возврат "Неправильный ИНН - ошибка ключа";
КонецФункции // ПроверкаИНН()

// Процедура формирует отчет "ИсторияПоЗаказНарядам"
Процедура ОтчетИсторияПоЗаказНарядам() Экспорт
	
	#Если Клиент Тогда
	
	Отчет = Отчеты.ИсторияПоЗаказНарядам.Создать();
	Отчет.ЗаполнитьНачальныеНастройки();
	
	МетаданныеОтчета  = Отчет.Метаданные();
	ВариантОтчета     = Отчет.СхемаКомпоновкиДанных.ВариантыНастроек.Основной;
	НастройкиВарианта = ВариантОтчета.Настройки;
	
	// параметры
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачала",    НачалоГода(ТекущаяДата()));
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаОкончания", ТекущаяДата());
	НастройкиВарианта.ПараметрыДанных.УстановитьЗначениеПараметра("ВалютаОтчета",  Константы.ВалютаУправленческогоУчетаКомпании.Получить());
	// фильтры
	ОтчетыСервер.КомпоновщикУстановитьОтбор(НастройкиВарианта, "Заказчик", Ссылка);
	
	ИмяФормыОтчета = МетаданныеОтчета.ПолноеИмя() + ".Форма";
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Вариант",                 НастройкиВарианта);
	СтруктураПараметров.Вставить("ЗаполнитьНастроки",       Ложь);
	СтруктураПараметров.Вставить("КлючВарианта",            ВариантОтчета.Имя);
	СтруктураПараметров.Вставить("ПредставлениеВарианта",   ВариантОтчета.Представление);
	СтруктураПараметров.Вставить("СформироватьПриОткрытии", Истина);
	СтруктураПараметров.Вставить("ВариантЗагрузкиПериода",  "ВариантСтандартногоПериода.ПроизвольныйПериод");
	
	ОткрытьФорму(ИмяФормыОтчета, СтруктураПараметров);
	
	#КонецЕсли
	
КонецПроцедуры // ОтчетИсторияПоЗаказНарядам()

// Процедура формирует отчет "ПрайсЛистНаАвтоработы"
Процедура ОтчетПрайсЛистНаАвтоработы() Экспорт
	
	#Если Клиент Тогда
	Если Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетПрайсЛистНаАвтоработы = Отчеты.ПрайсЛистНаАвтоработы.Создать();
	ФормаОтчета = ОтчетПрайсЛистНаАвтоработы.ПолучитьФорму("ФормаНастройкиОтчета");
	ФормаОтчета.ЗаполнитьНастройки       = Ложь;
	ФормаОтчета.ВосстанавливатьНастройки = Ложь;
	ОтчетПрайсЛистНаАвтоработы.ЗаполнитьНачальныеНастройки(ФормаОтчета.ИмяМакета);
	
	СтруктураОтчета = ФормаОтчета.КомпоновщикНастроек.Настройки.Структура;
	СтруктураОтчета.Очистить();
	
	ГруппировкаАвторабота = СтруктураОтчета.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ПолеГруппировки = ГруппировкаАвторабота.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование  = Истина;
	ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Авторабота");
	ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
	
	ГруппировкаМодель = ГруппировкаАвторабота.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ПолеГруппировки = ГруппировкаМодель.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование = Истина;
	ПолеГруппировки.Поле          = Новый ПолеКомпоновкиДанных("Модель");
	
	ГруппировкаЦех = ГруппировкаМодель.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ПолеГруппировки = ГруппировкаЦех.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование = Истина;
	ПолеГруппировки.Поле          = Новый ПолеКомпоновкиДанных("Цех");
	
	ГруппировкаВидРемонта = ГруппировкаЦех.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ПолеГруппировки = ГруппировкаВидРемонта.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование = Истина;
	ПолеГруппировки.Поле          = Новый ПолеКомпоновкиДанных("ВидРемонта");
	
	ГруппировкаДоговорВзаиморасчетов = ГруппировкаВидРемонта.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ПолеГруппировки = ГруппировкаДоговорВзаиморасчетов.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование = Истина;
	ПолеГруппировки.Поле          = Новый ПолеКомпоновкиДанных("ДоговорВзаиморасчетов");
	
	ОтборОтчета = ФормаОтчета.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	ОтборОтчета.Очистить();
	ОтборПоКонтрагенту = ОтборОтчета.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборПоКонтрагенту.Использование = Истина;
	ОтборПоКонтрагенту.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДоговорВзаиморасчетов.Владелец");
	ОтборПоКонтрагенту.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборПоКонтрагенту.ПравоеЗначение = Ссылка;
	
	//отДействияФормыСформировать(ФормаОтчета, Истина);
	ОтчетПрайсЛистНаАвтоработы.ИнициализироватьКомпоновщикНастроек(ФормаОтчета);
	ФормаОтчета.ДействияФормыСформировать(Неопределено);
	#КонецЕсли
	
КонецПроцедуры

// Печать бланка согласия на обработку персональных данных
//
Процедура СогласиеНаОбработкуПерсональныхДанныхПечать() Экспорт
	Если СогласиеНаОбработкуПерсональныхДанных<>Перечисления.ВариантыОтветов.Да Тогда
		Возврат;
	КонецЕсли; 
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("СогласиеНаОбработкуПерсональныхДанных");
	
	СтруктураПредставления=Новый Структура(
		"ИНН,КПП,АдресЮридический,ТелефонРабочий",
		"ИНН ","КПП ","Адрес: ","тел.: "
	);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.КонтрагентНаименование	= НаименованиеПолное;
	ОбластьМакета.Параметры.КонтрагентАдрес			= киПолучитьПредставлениеКИ(Ссылка,Справочники.ВидыКонтактнойИнформации.АдресФактический);
	ОбластьМакета.Параметры.КонтрагентДокумент		= спПолучитьПодтверждающийДокументОбъектаПоВиду(Ссылка,Перечисления.ВидыДокументов.Паспорт);
	ОбластьМакета.Параметры.ОрганизацияНаименование	= спПолучитьПредставление(ПараметрыСеанса.Организация,Новый Структура("Наименование",""));
	ОбластьМакета.Параметры.ОрганизацияАдрес		= спПолучитьПредставление(ПараметрыСеанса.Организация,СтруктураПредставления);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Согласие");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.КонтрагентНаименование	= Наименование;
	ОбластьМакета.Параметры.СогласиеДата			= Формат(СогласиеНаОбработкуПерсональныхДанныхДата,"ДФ=dd.MM.yyyy");
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабличныйДокумент.Защита			= обПраво("ЗащитаПечатныхФорм",Права);
	ТабличныйДокумент.ТолькоПросмотр	= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	// Выведем печатную форму	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ОбъектПредставление			= Строка(ЭтотОбъект);
	ФормаПечати.ДополнительноКПредставлению	= "Согласие на обработку персональных данных";
	ФормаПечати.Открыть();
КонецПроцедуры // СогласиеНаОбработкуПерсональныхДанныхПечать()
 
// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код");
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ВидКонтрагента",1);
		ОбязательныеРеквизиты.Вставить("ФормаСобственности",1);
		Если ВидКонтрагента=Перечисления.ВидыКонтрагентов.ПодотчетноеЛицо Тогда
			ОбязательныеРеквизиты.Вставить("Сотрудник",1);
		ИначеЕсли ВидКонтрагента=Перечисления.ВидыКонтрагентов.Филиал Тогда
			ОбязательныеРеквизиты.Вставить("Филиал",1);
		КонецЕсли;
		//Если ФормаСобственности=Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		//	ОбязательныеРеквизиты.Вставить("Фамилия",1);
		//	ОбязательныеРеквизиты.Вставить("Имя",1);
		//	ОбязательныеРеквизиты.Вставить("Отчество",1);
		//КонецЕсли;
		ОбязательныеРеквизиты.Вставить("НаименованиеПолное",1);
		//anton
		//ГДЕ ПРАВО?
		//Если ЭтоНовый() И обПраво("РазрешитьВводКонтрагентаБезРекламы", Права) = Перечисления.ВариантыОтветов.Нет Тогда
		//	ОбязательныеРеквизиты.Вставить("РекламныйИсточник",1);
		//КонецЕсли;
		//anton
		Если ЭтоНовый() И обПраво("РазрешитьВводКонтрагентаБезОсновногоТелефона", Права) = Перечисления.ВариантыОтветов.Нет Тогда
			ОбязательныеРеквизиты.Вставить("ОсновнойТелефон",1);
		КонецЕсли;
		Если ФормаСобственности=Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
			ОбязательныеРеквизиты.Вставить("СогласиеНаОбработкуПерсональныхДанных",1);
		ИначеЕсли ФормаСобственности=Перечисления.ФормыСобственности.ОбособленноеПодразделение Тогда
			ОбязательныеРеквизиты.Вставить("ГоловнойКонтрагент", 1);
		КонецЕсли;
	КонецЕсли; 
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Рез = спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Если ФормаСобственности=Перечисления.ФормыСобственности.ЧастноеЛицо И НЕ ЭтоГруппа Тогда
		Если обЗначениеНеЗаполнено(Фамилия) И обЗначениеНеЗаполнено(Имя) И обЗначениеНеЗаполнено(Отчество) Тогда
			Ошибки = Ошибки + "Одно из полей ФИО должно быть заполнено.";
			Рез = Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции // ПроверитьКорректность()

// Проверяет участие в движениях
Функция УчастиеВДвижениях(ВидРегистра, Элемент, Отбор) Экспорт
	Для Каждого ЭлементОтбора Из Отбор Цикл
		Запрос=Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	*
		|ИЗ
		|	РегистрНакопления."+ВидРегистра+" КАК Рег
		|ГДЕ
		|	Рег."+ЭлементОтбора.Ключ+"=&Ссылка
		|");
		Запрос.УстановитьПараметр("Ссылка",Элемент.Ссылка);
		Если НЕ Запрос.Выполнить().Пустой() Тогда Возврат Истина; КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции // УчастиеВДвижениях()

//Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Если НЕ ЭтоНовый() Тогда
		//БлокироватьРеквизиты = Новый Структура;
		//Отбор = Новый Структура;
		//Отбор.Вставить ("Контрагент", ЭтотОбъект);
		//Если УчастиеВДвижениях ("ВзаиморасчетыКомпании", ЭтотОбъект, Отбор)Тогда
		//	БлокироватьРеквизиты.Вставить ("ОсвобожденОтНДС");
		//	Возврат Ложь;
		//КонецЕсли;
		
		// На данный момент принято решение не блокировать реквизиты контрагентов,
		// в случае возникновения приватной необходимости разблокировать фрагмент кода
		// и прописать необходимые реквизиты.
	КонецЕсли;
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

//Функция создает основной договор контрагента, если он отсутствует
// Тип договора подставляется в зависимости от типа контрагента
Функция СоздатьОсновнойДоговор() Экспорт
	Выборка = Справочники.ДоговорыВзаиморасчетов.Выбрать(,ЭтотОбъект.Ссылка);
	Если НЕ Выборка.Следующий() Тогда	// Если договоров нет...
		Если ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель Тогда
			ВидДоговора   = Перечисления.ВидыДоговоров.Продажа;
			ТипЦенПокупки = Справочники.ТипыЦен.ПустаяСсылка();
			ТипЦенПродажи = обПраво("ОсновнойТипЦенПродажи", Права);
			ТипЦенРабот   = обПраво("ОсновнойТипЦенРабот",   Права);
		ИначеЕсли ВидКонтрагента = Перечисления.ВидыКонтрагентов.Поставщик Тогда	
			ВидДоговора   = Перечисления.ВидыДоговоров.Покупка;
			ТипЦенПокупки = обПраво("ОсновнойТипЦенЗакупки", Права);
			ТипЦенПродажи = Справочники.ТипыЦен.ПустаяСсылка();
			ТипЦенРабот   = Справочники.ТипыЦен.ПустаяСсылка();
		Иначе
			ВидДоговора   = Перечисления.ВидыДоговоров.Прочее;
			ТипЦенПокупки = Справочники.ТипыЦен.ПустаяСсылка();
			ТипЦенПродажи = обПраво("ОсновнойТипЦенПродажи", Права);
			ТипЦенРабот   = обПраво("ОсновнойТипЦенРабот",   Права);
		КонецЕсли;
		ОсновнойДоговор = Справочники.ДоговорыВзаиморасчетов.СоздатьЭлемент();
		ОсновнойДоговор.УстановитьНовыйКод();
		ОсновнойДоговор.Владелец = ЭтотОбъект.Ссылка;
		ОсновнойДоговор.ВидДоговора = ВидДоговора;
		ОсновнойДоговор.ТипЦенПокупки = ТипЦенПокупки;
		ОсновнойДоговор.ТипЦенПродажи = ТипЦенПродажи;
		ОсновнойДоговор.ВидОплаты = Перечисления.ВидыОплаты.ПроизвольнаяОплата;
		ОсновнойДоговор.Основной=Истина;
		ОсновнойДоговор.Заполнить(Неопределено);
		ОсновнойДоговор.АвтоЗакрытиеСделок = обПраво("АвтоЗакрытиеСделок",,,ОсновнойДоговор.Организация);
		
		//++бизтех++
		ОсновнойДоговор.ЕдиницаИзмеренияАвтоработВПечатныхФормах = Перечисления.ЕдиницаИзмеренияАвтоработВПечатныхФормах.ЕдиницаИзмеренияЧас;
		//--бизтех--
		
		ОсновнойДоговор.Записать();
	КонецЕсли;
КонецФункции // СоздатьОсновнойДоговор()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя="СогласиеНаОбработкуПерсональныхДанных" Тогда
		Если СогласиеНаОбработкуПерсональныхДанныхТек<>СогласиеНаОбработкуПерсональныхДанных Тогда
			Если ФормаСобственности=Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
				Если обЗначениеНеЗаполнено(СогласиеНаОбработкуПерсональныхДанных) Тогда
					СогласиеНаОбработкуПерсональныхДанных=Перечисления.ВариантыОтветов.Спрашивать;
				КонецЕсли;
				Если СогласиеНаОбработкуПерсональныхДанных=Перечисления.ВариантыОтветов.Спрашивать Тогда
					СогласиеНаОбработкуПерсональныхДанныхДата=Неопределено;
				ИначеЕсли СогласиеНаОбработкуПерсональныхДанных=Перечисления.ВариантыОтветов.Да ИЛИ
					      СогласиеНаОбработкуПерсональныхДанных=Перечисления.ВариантыОтветов.Нет Тогда
					#Если Клиент Тогда
					СогласиеНаОбработкуПерсональныхДанныхДата=РабочаяДата;
					#Иначе
					СогласиеНаОбработкуПерсональныхДанныхДата=ТекущаяДата();
					#КонецЕсли
				КонецЕсли; 
			Иначе
				СогласиеНаОбработкуПерсональныхДанных=Неопределено;
				СогласиеНаОбработкуПерсональныхДанныхДата=Неопределено;
			КонецЕсли; 
			СогласиеНаОбработкуПерсональныхДанныхТек=СогласиеНаОбработкуПерсональныхДанных;
		КонецЕсли; 
		Возврат Истина;
		
	Иначе
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли; 
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если Основание<>Неопределено Тогда
		Если ТипЗнч(Основание)=Тип("СправочникСсылка.Сотрудники") И (НЕ Основание.ЭтоГруппа) Тогда
			Наименование=Основание.Наименование;
			НаименованиеПолное=Основание.Наименование;
			ВидКонтрагента=Перечисления.ВидыКонтрагентов.ПодотчетноеЛицо;
			ФормаСобственности=Перечисления.ФормыСобственности.ЧастноеЛицо;
			Сотрудник=Основание;
			Родитель=Справочники.Контрагенты.НайтиПоНаименованию("Сотрудники",Истина);
		КонецЕсли; 
	КонецЕсли; 
	Если ВидКонтрагента.Пустая() Тогда
		ВидКонтрагента = Родитель.ВидКонтрагента;
	КонецЕсли; 
	Если ВидКонтрагента.Пустая() Тогда
		ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель;
	КонецЕсли; 
	Если ФормаСобственности.Пустая() Тогда
		ФормаСобственности = Родитель.ФормаСобственности;
	КонецЕсли; 
	Если ФормаСобственности.Пустая() Тогда
		ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо;
	КонецЕсли; 
	Если НЕ ЭтоГруппа Тогда
		Если ФормаСобственности.Пустая() Тогда
			ФормаСобственности = Перечисления.ФормыСобственности.Прочее;
		КонецЕсли;
		СогласиеНаОбработкуПерсональныхДанных=Перечисления.ВариантыОтветов.Спрашивать;
		СогласиеНаОбработкуПерсональныхДанныхТек=СогласиеНаОбработкуПерсональныхДанных;
		СогласиеНаОбработкуПерсональныхДанныхДата=Неопределено;
		ОбработкаРеквизита("СогласиеНаОбработкуПерсональныхДанных");
		Если СтранаРегистрации.Пустая() Тогда
			СтранаРоссия = Справочники.КлассификаторСтранМира.НайтиПоКоду("643");
			СтранаРегистрации = СтранаРоссия;
		КонецЕсли;
	КонецЕсли;
	
	Автор = Неопределено;
	ДатаРегистрации = Неопределено;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		СогласиеНаОбработкуПерсональныхДанных=Перечисления.ВариантыОтветов.Спрашивать;
		СогласиеНаОбработкуПерсональныхДанныхТек=СогласиеНаОбработкуПерсональныхДанных;
		СогласиеНаОбработкуПерсональныхДанныхДата=Неопределено;
		ОбработкаРеквизита("СогласиеНаОбработкуПерсональныхДанных");
	КонецЕсли; 
	
	Автор = Неопределено;
	ДатаРегистрации = Неопределено;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	// Если был очищен GUIDСайта, то сбросим флаг выгрузки автомобилей и заказ-нарядов на сайт
	Если (НЕ ЭтоГруппа) И (НЕ Отказ) И ЭтотОбъект.GUIDСайта = "" И ЭтотОбъект.Ссылка.GUIDСайта <> "" Тогда
		ЭтотОбъект.ВыгружатьАвтомобилиИЗаказНарядыНаСайт = ЛОЖЬ;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		Если ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо  Тогда 
			//Для формы собственности "частное лицо" всегда устанавливаем "ОсвобожденОтНДС" в Истина
			ОсвобожденОтНДС= Истина;	
		КонецЕсли;  
		
		// Сформируем дату строкой для сортировки по дням рождения 
		Если обЗначениеНеЗаполнено(ДатаРождения) Тогда
			ДеньРождения = "";
		Иначе
			ДеньРождения = Формат(ДатаРождения, "ДФ=ММ") + "." + Формат(ДатаРождения, "ДФ=дд") ;
		КонецЕсли;
		
		Если НЕ ФормаСобственности=Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
			СогласиеНаОбработкуПерсональныхДанных=Неопределено;
			СогласиеНаОбработкуПерсональныхДанныхДата=Неопределено;
		КонецЕсли; 
		
		Если НЕ СогласиеНаОбработкуПерсональныхДанных = Перечисления.ВариантыОтветов.Да Тогда
			СогласиеНаПолучениеSMS = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	// Заполним автора и дату регистрации, если объект новый
	Если ЭтоНовый() Тогда
		Автор = ПараметрыСеанса.Пользователь;
		ДатаРегистрации = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

 //<<БАА 11.10.23 - процедура вынесена в модуль объекта
Процедура ПроверкаКонтактнойИнформации(ТЗНаборКИ, Отказ, БезСообщений = Ложь) Экспорт
	ИскомаяСтрокаТелефон = ТЗНаборКИ.Найти(Справочники.ВидыКонтактнойИнформации.ТелефонРабочий);
	Если  ЗначениеЗаполнено(ИскомаяСтрокаТелефон) И Лев((СокрЛП(ИскомаяСтрокаТелефон.Поле5)), 2) = "+7" тогда
		телефон =  ИскомаяСтрокаТелефон.Поле5;
	иначе
		ИскомаяСтрокаТелефон = ТЗНаборКИ.Найти(Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
		Если  ЗначениеЗаполнено(ИскомаяСтрокаТелефон) И Лев((СокрЛП(ИскомаяСтрокаТелефон.Поле5)), 2) = "+7" тогда
			телефон =  ИскомаяСтрокаТелефон.Поле5;	
		Иначе
			ИскомаяСтрокаТелефон = ТЗНаборКИ.Найти(Справочники.ВидыКонтактнойИнформации.ТелефонКонтактный);
			Если  ЗначениеЗаполнено(ИскомаяСтрокаТелефон) И Лев((СокрЛП(ИскомаяСтрокаТелефон.Поле5)), 2) = "+7" тогда
				телефон =  ИскомаяСтрокаТелефон.Поле5;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если ИскомаяСтрокаТелефон = Неопределено тогда 
		Отказ = Истина;
		Если Не БезСообщений Тогда
			Сообщить("Не заполнен номер телефона. Запись отменена.");
		КонецЕсли;	
	иначе
		Если телефон = Неопределено  Тогда 
			Если Не БезСообщений Тогда
				Сообщить("Телефон заполнен по  по украинскому образцу");
			КонецЕсли;
			Отказ = Истина;
		КонецЕсли;	
	КонецЕсли;
	ИскомаяСтрокаАдрес = ТЗНаборКИ.Найти(Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	Если  ЗначениеЗаполнено(ИскомаяСтрокаАдрес) тогда
		Адрес =  ИскомаяСтрокаАдрес.Представление;
	иначе
		ИскомаяСтрокаАдрес = ТЗНаборКИ.Найти(Справочники.ВидыКонтактнойИнформации.АдресФактический);
		Если  ЗначениеЗаполнено(ИскомаяСтрокаАдрес) тогда
			Адрес =  ИскомаяСтрокаАдрес.Представление;
		КонецЕсли;
	КонецЕсли;
	Если  Адрес = Неопределено тогда 
	иначе
		 //ЧалапАЮ  БизТех 19.03.2024 - убрала проверку
		//Если не НайтиИндексИУкраинскиеСимволыВСтроке(Адрес) Тогда 
		//	Если Не БезСообщений Тогда
		//		Сообщить("Адрес заполнен по украинскому образцу");
		//	КонецЕсли;	
		//	//Отказ = Истина;        //ЧалапАЮ  БизТех 19.03.2024 - убрала проверку
		//КонецЕсли;	
	КонецЕсли;
	Если ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо Тогда
		ИскомаяСтрока = ТЗНаборКИ.Найти(Справочники.ВидыКонтактнойИнформации.АдресФактический, "Вид");
		Если ИскомаяСтрока <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(ИскомаяСтрока.Представление) Тогда
				Отказ = Истина;
				Если Не БезСообщений Тогда
					Сообщить("Не заполнен фактический адрес контрагента. Запись отменена.");
				КонецЕсли;	
			КонецЕсли;
		Иначе
			//Отказ = Истина;
			Если Не БезСообщений Тогда
				Сообщить("Не заполнен фактический адрес контрагента. Запись отменена.");
			КонецЕсли;	
		КонецЕсли;
	ИначеЕсли ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо Тогда
		ИскомаяСтрока = ТЗНаборКИ.Найти(Справочники.ВидыКонтактнойИнформации.АдресЮридический, "Вид");
		Если ИскомаяСтрока <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(ИскомаяСтрока.Представление) Тогда
				Отказ = Истина;
				
				Если Не БезСообщений Тогда
					Сообщить("Не заполнен юридический адрес контрагента. Запись отменена.");
				КонецЕсли;	
			КонецЕсли;
		Иначе
			//Отказ = Истина;
			Если Не БезСообщений Тогда
				Сообщить("Не заполнен юридический адрес контрагента. Запись отменена.");
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

СогласиеНаОбработкуПерсональныхДанныхТек=Ссылка.СогласиеНаОбработкуПерсональныхДанных;

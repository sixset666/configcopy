// процедура заполнения предопределенных элементов справочника
//
// Параметры:
// ЗаполнятьВсе - Булево - Признак заполнение всей информации
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	//Заполним основного поставщика
	КонтрагентыОсновнойПоставщик = Справочники.Контрагенты.ОсновнойПоставщик.ПолучитьОбъект();
	//перезаполняем реквизиты
	Если ЗаполнятьВсе Тогда
		КонтрагентыОсновнойПоставщик.НаименованиеПолное = КонтрагентыОсновнойПоставщик.Наименование;
		КонтрагентыОсновнойПоставщик.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Поставщик;
		КонтрагентыОсновнойПоставщик.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо;
	Иначе
		Если обЗначениеНеЗаполнено(КонтрагентыОсновнойПоставщик.НаименованиеПолное) Тогда
			КонтрагентыОсновнойПоставщик.НаименованиеПолное = КонтрагентыОсновнойПоставщик.Наименование;
		КонецЕсли;
	    Если обЗначениеНеЗаполнено(КонтрагентыОсновнойПоставщик.ВидКонтрагента) Тогда
			КонтрагентыОсновнойПоставщик.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Поставщик;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(КонтрагентыОсновнойПоставщик.ФормаСобственности) Тогда
			КонтрагентыОсновнойПоставщик.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо;
		КонецЕсли;
	КонецЕсли;
	//Сохранение справочника
	НовыйЭлемент = КонтрагентыОсновнойПоставщик.ЭтоНовый();
	Если КонтрагентыОсновнойПоставщик.Модифицированность() Тогда
		Попытка
			КонтрагентыОсновнойПоставщик.Записать();
			Сообщить("Справочник ""Контрагенты"". " + ?(НовыйЭлемент,"Добавлен","Обновлен")
			+ " элемент """ + СокрЛП(КонтрагентыОсновнойПоставщик.Наименование) + """");
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	//Заполним основного покупателя
	КонтрагентыОсновнойПокупатель = Справочники.Контрагенты.ОсновнойПокупатель.ПолучитьОбъект();
	//перезаполняем реквизиты
	Если ЗаполнятьВсе Тогда
		КонтрагентыОсновнойПокупатель.НаименованиеПолное = ОсновнойПокупатель.Наименование;
		КонтрагентыОсновнойПокупатель.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель;
		КонтрагентыОсновнойПокупатель.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо;
	Иначе
		Если обЗначениеНеЗаполнено(КонтрагентыОсновнойПокупатель.НаименованиеПолное) Тогда
			КонтрагентыОсновнойПокупатель.НаименованиеПолное = КонтрагентыОсновнойПокупатель.Наименование;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(КонтрагентыОсновнойПокупатель.ВидКонтрагента) Тогда
			КонтрагентыОсновнойПокупатель.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Покупатель;
		КонецЕсли;
		Если обЗначениеНеЗаполнено(КонтрагентыОсновнойПокупатель.ФормаСобственности) Тогда
			КонтрагентыОсновнойПокупатель.ФормаСобственности = Перечисления.ФормыСобственности.ЮридическоеЛицо;
		КонецЕсли;
	КонецЕсли;
	//Сохранение справочника
	НовыйЭлемент = КонтрагентыОсновнойПокупатель.ЭтоНовый();
	Если КонтрагентыОсновнойПокупатель.Модифицированность() Тогда
		Попытка
			КонтрагентыОсновнойПокупатель.Записать();
			Сообщить("Справочник ""Контрагенты"". " + ?(НовыйЭлемент,"Добавлен","Обновлен")
			+ " элемент """ + СокрЛП(КонтрагентыОсновнойПокупатель.Наименование) + """");
		Исключение
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()

// Функция убирает из номера телефона все префиксы, например, 999-99*99, будет 9999999 
//  префиксы передаются в СтрокеПрефиксов
Функция УбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона, СтрокаПрефиксов=" /*-+=_.\,!;%:?()") Экспорт
	Для НомерПрефикс = 1 По СтрДлина(СтрокаПрефиксов) Цикл
		ТекущийПрефикс = Сред(СтрокаПрефиксов, НомерПрефикс, 1);
		НомерТелефона = СтрЗаменить(НомерТелефона, ТекущийПрефикс, "");
	КонецЦикла;
	Возврат НомерТелефона;
КонецФункции

// Получение договора взаиморасчетов контрагента с заданными параметрами.
// Если договора заданного типа нет - создается новый договор.
//
// Параметры:
// Контрагент	– СправочникСсылка.Контрагенты – Ссылка на справочник контрагентов,
//				 договор которого требуется получить.
// ВидДоговора	– ПеречислениеСсылка.ВидыДоговоров – Ссылка на перечисление вида получаемого договора.
//	ЭтотОбъект	- ДокументОбъект - Документ, в рамках которого производится создание нового договора.
//				 Если значение неопределено параметры договора беруться из настроек.
//	ДопПараметры - Структура - Структура, содержащая тип цен покупки и продажи для заполнения нового договора.
//				 Если значение неопределено или не определен тот или иной тип цен - они берутся из настроек.
// УчитыватьПринадлежность - Булево, определяет нужно ли дополнительно проверять соответствие подразделения, организации и т.д.
//				 основного договора с данными объекта
//
// Возвращаемое значение:
// СправочникСсылка.ДоговорыВзаиморасчетов – Ссылка на справочник ДоговорыВзаиморасчетов с найденным договором
//											 или вновь созданным.
Функция ПолучитьДоговорВзаиморасчетов(Контрагент, ВидДоговора = Неопределено, ЭтотОбъект = Неопределено, ДопПараметры = Неопределено, УчитыватьПринадлежность = Ложь, СоздаватьТолькоПродажи = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	КонецЕсли;
	
	//Проверим есть ли уже договора данного вида
	ПустаяДата = Дата("00010101");
	
	Попытка
		Организация = ЭтотОбъект.Организация;
	Исключение
		Организация = Справочники.Организации.ПустаяСсылка();
	КонецПопытки;
	
	Попытка
		ПодразделениеКомпании = ЭтотОбъект.ПодразделениеКомпании;
	Исключение
		ПодразделениеКомпании = Справочники.ПодразделенияКомпании.ПустаяСсылка();
	КонецПопытки;
	
	Попытка
		ДатаДоговора = ЭтотОбъект.Дата;
	Исключение
		ДатаДоговора = ПустаяДата;
	КонецПопытки;
	
	ПраваПользователя = ?(ЭтотОбъект = Неопределено, Неопределено, ЭтотОбъект.Права);
	
	Если ДопПараметры = Неопределено Тогда
		ВалютаВзаиморасчетов = Неопределено;
	Иначе
		ДопПараметры.Свойство("ВалютаВзаиморасчетов", ВалютаВзаиморасчетов);
	КонецЕсли;
	
	ПоискПоВидуДеятельности = (НЕ ЭтотОбъект = Неопределено);
	
	//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API ++ //Обрамил вызов
    #Если НЕ КлиентскоеПриложение Тогда
	ЕстьПодсистемаАвтосалон = Истина;
	#Иначе
	ЕстьПодсистемаАвтосалон = зфЗащищенныеФункцииСервер.ПодсистемаДоступна("ЗащитаАвтосалон");
    #КонецЕсли
	//БИЗТЕХ КОВАЛЬ 10.11.2025 О2 API --
	
	Если НЕ ПоискПоВидуДеятельности Тогда
		ТекстОтбора = "";
		ДляАвтосалона  = ЕстьПодсистемаАвтосалон;
		ДляАвтосервиса = Истина;
		Внутренний     = Истина;
	Иначе
		
		МассивПрочие = Новый Массив;
		МассивСервис = Новый Массив;
		МассивСалон  = Новый Массив;
		
		МакетВидыДеятельности = Справочники.ДоговорыВзаиморасчетов.ПолучитьМакет("ВидыДеятельности");
		
		ОбластьПрочее = МакетВидыДеятельности.ПолучитьОбласть("Прочее");
		Для Ном = 1 По ОбластьПрочее.ВысотаТаблицы Цикл
			МассивПрочие.Добавить(ОбластьПрочее.Область(Ном, 1, Ном, 1).Текст);
		КонецЦикла;
		
		ОбластьСервис = МакетВидыДеятельности.ПолучитьОбласть("Сервис");
		Для Ном = 1 По ОбластьСервис.ВысотаТаблицы Цикл
			МассивСервис.Добавить(ОбластьСервис.Область(Ном, 1, Ном, 1).Текст);
		КонецЦикла;
		
		Если ЕстьПодсистемаАвтосалон Тогда
			ОбластьСалон = МакетВидыДеятельности.ПолучитьОбласть("Салон");
			Для Ном = 1 По ОбластьСалон.ВысотаТаблицы Цикл
				МассивСалон.Добавить(ОбластьСалон.Область(Ном, 1, Ном, 1).Текст);
			КонецЦикла;
		КонецЕсли;
		
		ОбластьУниверсальный = МакетВидыДеятельности.ПолучитьОбласть("Универсальный");
		Для Ном = 1 По ОбластьУниверсальный.ВысотаТаблицы Цикл
			ИмяДокумента = ОбластьУниверсальный.Область(Ном, 1, Ном, 1).Текст;
			МассивПрочие.Добавить(ИмяДокумента);
			МассивСервис.Добавить(ИмяДокумента);
			Если ЕстьПодсистемаАвтосалон Тогда
				МассивСалон.Добавить(ИмяДокумента);
			КонецЕсли;
		КонецЦикла;
		
		ТекстОтбора    = "";
		ДляАвтосалона  = Ложь;
		ДляАвтосервиса = Ложь;
		Внутренний     = Ложь;
		
		МетаданныеДокумента = ЭтотОбъект.Метаданные();
		
		ИмяДокумента = МетаданныеДокумента.Имя;
		
		Если НЕ МассивСалон.Найти(ИмяДокумента) = Неопределено Тогда
			ТекстОтбора   = "ДоговорыВзаиморасчетов.ДляАвтосалона = ИСТИНА";
			ДляАвтосалона = Истина;
		КонецЕсли;
		
		Если НЕ МассивСервис.Найти(ИмяДокумента) = Неопределено Тогда
			ТекстОтбора    = ТекстОтбора + ?(ТекстОтбора = "", "", " ИЛИ 
			|	") + "ДоговорыВзаиморасчетов.ДляАвтосервиса = ИСТИНА";
			ДляАвтосервиса = Истина;
		КонецЕсли;
		
		Если НЕ МассивПрочие.Найти(ИмяДокумента) = Неопределено Тогда
			ТекстОтбора = ТекстОтбора + ?(ТекстОтбора = "", "", " ИЛИ 
			|	") + "ДоговорыВзаиморасчетов.Внутренний = ИСТИНА";
			Внутренний  = Истина;
		КонецЕсли;
		
		Если ТекстОтбора = "" Тогда
			// документ не был определен
			ДляАвтосалона  = ЕстьПодсистемаАвтосалон;
			ДляАвтосервиса = Истина;
			Внутренний     = Истина;
		Иначе
			ТекстОтбора = " И 
			|	(" + ТекстОтбора + ")";
		КонецЕсли;
		
	КонецЕсли;
	
	// В первую очередь необходимо получить договор, удовлетворяющий 
	// всем условиям, в т.ч. у которого совпадают реквизиты "Подразделение компании" и "Организация".
	// Если такого договора нет, то необходимо попытаться получить договор, у которого хотя бы организация совпадет.
	// Если и такого договора нет, то пытаемся получить договор, у которого подразделение совпадает. 
	// Если и такого нет, то получаем любой договор (в пределах подчинения, разумеется).
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДоговорыВзаиморасчетов.Ссылка КАК Договор,
	|	ВЫБОР
	|		КОГДА ДоговорыВзаиморасчетов.Организация = &Организация
	|			И ДоговорыВзаиморасчетов.Подразделение = &Подразделение ТОГДА
	|			0
	|		КОГДА ДоговорыВзаиморасчетов.Организация = &Организация ТОГДА
	|			1
	|		КОГДА ДоговорыВзаиморасчетов.Подразделение = &Подразделение ТОГДА
	|			2
	|		ИНАЧЕ
	|			3
	|	КОНЕЦ КАК ПорядокПоПринадлежности,
	|	ДоговорыВзаиморасчетов.Основной КАК Основной,
	|	ДоговорыВзаиморасчетов.ДатаКонца КАК ДатаКонца,
	|	ДоговорыВзаиморасчетов.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	Справочник.ДоговорыВзаиморасчетов КАК ДоговорыВзаиморасчетов
	|ГДЕ
	|	ДоговорыВзаиморасчетов.Владелец = &Владелец И 
	|	ДоговорыВзаиморасчетов.ЭтоГруппа = ЛОЖЬ" + ?(обЗначениеНеЗаполнено(ВидДоговора), "", " И 
	|	ДоговорыВзаиморасчетов.ВидДоговора = &ВидДоговора") + ТекстОтбора + "
	|	И (ДоговорыВзаиморасчетов.ДатаКонца = &ПустаяДата
	|				И ДоговорыВзаиморасчетов.ДатаНачала <= &ДатаДоговора
	|			ИЛИ &ДатаДоговора МЕЖДУ ДоговорыВзаиморасчетов.ДатаНачала И ДоговорыВзаиморасчетов.ДатаКонца)
	|	И ДоговорыВзаиморасчетов.ПометкаУдаления = ЛОЖЬ"+ ?(ВалютаВзаиморасчетов = Неопределено, "", "
	|	И ДоговорыВзаиморасчетов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов") + "
	|УПОРЯДОЧИТЬ ПО
	|	" + ?(УчитыватьПринадлежность, "ПорядокПоПринадлежности ВОЗР,
	|	Основной УБЫВ,", "Основной УБЫВ,
	|	ПорядокПоПринадлежности ВОЗР,") + "
	|	ДатаКонца ВОЗР,
	|	ДатаНачала УБЫВ";
	
	Запрос.УстановитьПараметр("ВидДоговора",          ВидДоговора);
	Запрос.УстановитьПараметр("Владелец",             Контрагент);
	Запрос.УстановитьПараметр("Организация",          ?(обЗначениеНеЗаполнено(Организация), ПараметрыСеанса.Организация, Организация));
	Запрос.УстановитьПараметр("Подразделение",        ?(обЗначениеНеЗаполнено(ПодразделениеКомпании), ПараметрыСеанса.ПодразделениеКомпании, ПодразделениеКомпании));
	Запрос.УстановитьПараметр("ДатаДоговора",         ДатаДоговора);
	Запрос.УстановитьПараметр("ПустаяДата",           ПустаяДата);
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", ВалютаВзаиморасчетов);
	Результат = Запрос.Выполнить();
	
	РазрешеноСоздавать = ((НЕ СоздаватьТолькоПродажи) ИЛИ ВидДоговора = Перечисления.ВидыДоговоров.Продажа ИЛИ ВидДоговора = Перечисления.ВидыДоговоров.СКомиссионером);
	РазрешеноСоздавать = РазрешеноСоздавать И обПраво("АвтоматическоеСозданиеОсновногоДоговора",ПраваПользователя,,ЭтотОбъект);
	
	Если НЕ Результат.Пустой() Тогда
		//Договор требуемого вида уже есть - вернем его
		Выборка = Результат.Выбрать(); 
		Выборка.Следующий();
		Возврат Выборка.Договор;
		
	ИначеЕсли РазрешеноСоздавать И (НЕ Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.КонтактноеЛицо) Тогда
		//иначе создадим новый договор
		Если ЭтотОбъект = Неопределено Тогда //Если документа нет, параметры договора будут подставлены по умолчанию
			Организация          = Справочники.Организации.ПустаяСсылка();
			Подразделение        = Справочники.ПодразделенияКомпании.ПустаяСсылка();
			ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			#Если Клиент Тогда
				ДатаНачала= РабочаяДата+(Час(ТекущаяДата())*60*60)+(Минута(ТекущаяДата())*60)+(Секунда(ТекущаяДата()));
			#Иначе
				ДатаНачала = ТекущаяДата();
			#КонецЕсли
			ДатаНачала = НачалоДня(ДатаНачала);
			ВидОплаты = Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			ТипСкидкиНаценки = Справочники.ТипыСкидок.ПустаяСсылка();
			
		Иначе //Если есть документ, параметры договора берутся из него
			Организация = ЭтотОбъект.Организация;
			Подразделение = ЭтотОбъект.ПодразделениеКомпании;
			Попытка
				ВалютаВзаиморасчетов = ЭтотОбъект.ВалютаДокумента;
			Исключение
				ВалютаВзаиморасчетов = Константы.ВалютаУправленческогоУчетаКомпании.Получить();
			КонецПопытки; 
			
			ДатаНачала = ЭтотОбъект.Дата;
			Попытка
				Если ТипЗнч(ЭтотОбъект) = Тип("ДокументОбъект.ЗаявкаНаРемонт") И ЭтотОбъект.Ссылка.Пустая() Тогда
					ВидОплаты = обПраво("ВидОплатыПоУмолчанию",ПраваПользователя,,ЭтотОбъект);
				Иначе
					ВидОплаты = ЭтотОбъект.ВидОплаты;
				КонецЕсли;
			Исключение
				ВидОплаты = Перечисления.ВидыОплаты.ПроизвольнаяОплата;
			КонецПопытки;
			
			Попытка
				ТипСкидкиНаценки = ЭтотОбъект.СкидкаНаценка;
				Если НЕ ТипСкидкиНаценки.РучнаяСкидка Тогда
					ТипСкидкиНаценки = Справочники.ТипыСкидок.ПустаяСсылка();
				КонецЕсли; 
			Исключение
				ТипСкидкиНаценки = Справочники.ТипыСкидок.ПустаяСсылка();
			КонецПопытки;
			
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(Организация) Тогда
			Организация = ПараметрыСеанса.Организация;
		КонецЕсли;
		
		Если обЗначениеНеЗаполнено(Подразделение) Тогда
			Подразделение = ПараметрыСеанса.ПодразделениеКомпании;
		КонецЕсли;
		
		ТипЦенПокупки = обПраво("ОсновнойТипЦенЗакупки", ПраваПользователя,,ЭтотОбъект);
		ТипЦенПродажи = обПраво("ОсновнойТипЦенПродажи", ПраваПользователя,,ЭтотОбъект);
		ТипЦенРабот   = обПраво("ОсновнойТипЦенРабот",   ПраваПользователя,,ЭтотОбъект);
		
		Если ДопПараметры <> Неопределено Тогда
			ДопПараметры.Свойство("ТипЦенПокупки", ТипЦенПокупки);
			ДопПараметры.Свойство("ТипЦенПродажи", ТипЦенПродажи);
			ДопПараметры.Свойство("ТипЦенРабот",   ТипЦенРабот);
		КонецЕсли;
		
		НовыйДоговор = Справочники.ДоговорыВзаиморасчетов.СоздатьЭлемент();
		НовыйДоговор.УстановитьНовыйКод();
		НовыйДоговор.Владелец             = Контрагент;
		НовыйДоговор.Организация          = Организация;
		НовыйДоговор.Подразделение        = Подразделение;
		НовыйДоговор.ВидДоговора          = ВидДоговора;
		НовыйДоговор.ТипДоговора          = Перечисления.ТипыДоговоров.Договор;
		НовыйДоговор.ДляАвтосалона        = ДляАвтосалона;
		НовыйДоговор.ДляАвтосервиса       = ДляАвтосервиса;
		НовыйДоговор.Внутренний           = Внутренний;
		НовыйДоговор.ВалютаВзаиморасчетов = ВалютаВзаиморасчетов;
		НовыйДоговор.ДатаНачала           = ДатаНачала;
		НовыйДоговор.ТипЦенПокупки        = ТипЦенПокупки;
		НовыйДоговор.ТипЦенПродажи        = ТипЦенПродажи;
		НовыйДоговор.ТипЦенРабот          = ТипЦенРабот;
		НовыйДоговор.ВидОплаты            = ВидОплаты;
		НовыйДоговор.ТипСкидкиНаценки     = ТипСкидкиНаценки;
		НовыйДоговор.Основной             = Истина;
		НовыйДоговор.ОтменаКонтроляСуммыКредита = обПраво("ОтменаКонтроляСуммыКредита",ПраваПользователя,,Подразделение);
		НовыйДоговор.СформироватьНаименование();
		НовыйДоговор.ПроверятьЗаполнение  = Ложь;
		НовыйДоговор.АвтоЗакрытиеСделок   = обПраво("АвтоЗакрытиеСделок",,,Организация);
		НовыйДоговор.ЕдиницаИзмеренияАвтоработВПечатныхФормах = обПраво("ЕдиницаИзмеренияАвтоработВПечатныхФормах",ПраваПользователя,,Подразделение);
		
		Попытка
			НовыйДоговор.Записать();
		Исключение
		КонецПопытки;
		
		Возврат НовыйДоговор.Ссылка;
	Иначе
		Возврат Справочники.ДоговорыВзаиморасчетов.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции // ПолучитьДоговорВзаиморасчетов()

// Процедура формирует отчет "ВзаиморасчетыКомпании"
Процедура ОтчетВзаиморасчетыКомпании(КонтрагентСсылка,ДоговорВзаиморасчетов=Неопределено) Экспорт
	
	// проверки
	Если КонтрагентСсылка.Пустая() ИЛИ КонтрагентСсылка.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
	
	дкОстаткиПоВзаиморасчетам(КонтрагентСсылка, ДоговорВзаиморасчетов);
	
	#КонецЕсли
	
КонецПроцедуры

//БИЗТЕХ КОВАЛЬ
#Область КИ
// Получить почту контрагента
Функция ПолучитьПочту(СсылкаКонтрагент) Экспорт
	
	ЭлПочта = киПолучитьПредставлениеКИ(СсылкаКонтрагент,Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний);
	Если ЭлПочта="" Тогда
		ЭлПочта = киПолучитьПредставлениеКИ(СсылкаКонтрагент,Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий);
	КонецЕсли;
	Возврат ЭлПочта;

КонецФункции

#КонецОбласти
//БИЗТЕХ КОВАЛЬ

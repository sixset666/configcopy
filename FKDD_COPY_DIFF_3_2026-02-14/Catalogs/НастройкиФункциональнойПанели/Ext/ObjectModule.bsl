// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// функция пробует создать объект внешней обработки, если не получается - объект внутренней, 
// иначе возвращает неопределено
//
// Параметры -
//  ИмяОбработки - Строка, имя обработки, экземпляр которой пытаемся создать
Функция обПолучитьВнешнююОбработкуОбъект(ИмяОбработки) Экспорт
	// попробуем найти обработку в предопределённых элементах справочник Внешние печатные формы
	НайденнаяОбработка = Неопределено;
	ИтоговаяОбработка = Неопределено;

	#Если Клиент Тогда
		Попытка
			НайденнаяОбработка = Справочники.ВнешниеПечатныеФормы[ИмяОбработки];
		Исключение
		КонецПопытки;

		Если ЗначениеЗаполнено(НайденнаяОбработка) Тогда
			// проверим есть ли внешняя обработка
			ЕстьВнешняяОбработка = Ложь;
			Если НайденнаяОбработка.ХранитьВоВнешнемФайле Тогда
				// проверим, а есть ли файл?
				ФайлОбработки = Новый Файл(НайденнаяОбработка.ПутьКВнешнемуФайлу);
				ЕстьВнешняяОбработка =  Не обЗначениеНеЗаполнено(НайденнаяОбработка.ПутьКВнешнемуФайлу) И ФайлОбработки.Существует();
			Иначе
				Если (НайденнаяОбработка.Хранилище.Получить() = Неопределено) ИЛИ (НайденнаяОбработка.Хранилище.Получить() = "") Тогда
					ЕстьВнешняяОбработка = Ложь
				Иначе
					ЕстьВнешняяОбработка = Истина;
				КонецЕсли;
			КонецЕсли;

			Если ЕстьВнешняяОбработка Тогда
				// найдена внешняя обработка, которая хранится во внешнем файле - используем её
				Если НайденнаяОбработка.ХранитьВоВнешнемФайле Тогда
					ИмяФайлаВнешнейОбработки = НайденнаяОбработка.ПутьКВнешнемуФайлу;
				Иначе
					ИмяФайлаВнешнейОбработки = ПолучитьИмяВременногоФайла();
					НайденнаяОбработка.Хранилище.Получить().Записать(ИмяФайлаВнешнейОбработки);
				КонецЕсли;

				ИтоговаяОбработка = ВнешниеОбработки.Создать(ИмяФайлаВнешнейОбработки);
			Иначе
				НайденнаяОбработка = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НайденнаяОбработка) Тогда
			// попробуем создать встроенную обработку
			Попытка
				ИтоговаяОбработка = Обработки[ИмяОбработки].Создать();
			Исключение
				ИтоговаяОбработка = Неопределено;
			КонецПопытки;
		КонецЕсли;
	#КонецЕсли

	Возврат ИтоговаяОбработка;
КонецФункции

// Процедура заполняет предопределённые элементы справочника из макета обработки "Функциональная панель"
Процедура ЗаполнитьПредопределенныеЭлементы() Экспорт
	// создадим обработку "функиональная панель", чтобы прочитать предопределённые
	// настройки из макета
	ИмяОбработки = "ФункциональнаяПанель";
	ОбработкаФункцПанель = обПолучитьВнешнююОбработкуОбъект(ИмяОбработки);
	Если ОбработкаФункцПанель = Неопределено Тогда
		ОбработкаФункцПанель = Обработки[ИмяОбработки].Создать();
	КонецЕсли;

	ТаблицаКнопок = ОбработкаФункцПанель.ПолучитьТаблицуКнопок();
	ТаблицаИнформации = ОбработкаФункцПанель.ПолучитьТаблицуИнформации();
	// создадим массив колонок макета, не содержащих настроек предопределённых элементов
	МассивЗапрещенныхКолонок = Новый Массив;
	МассивЗапрещенныхКолонок.Добавить("Имя");
	МассивЗапрещенныхКолонок.Добавить("Картинка");
	МассивЗапрещенныхКолонок.Добавить("Представление");
	МассивЗапрещенныхКолонок.Добавить("Шрифт");
	МассивЗапрещенныхКолонок.Добавить("Цвет");

	ПредопределенныеНастройки = Новый Соответствие;

	Для Каждого СтрокаКолонки Из ТаблицаКнопок.Колонки Цикл
		Если МассивЗапрещенныхКолонок.Найти(СтрокаКолонки.Имя) = Неопределено Тогда
			Попытка
				Настройка = Справочники.НастройкиФункциональнойПанели[СтрокаКолонки.Имя];
				ПредопределенныеНастройки.Вставить(СтрокаКолонки.Имя, Настройка.Наименование);
			Исключение
				Настройка = Справочники.НастройкиФункциональнойПанели.ПустаяСсылка();
			КонецПопытки;
			Если Настройка = Справочники.НастройкиФункциональнойПанели.ПустаяСсылка() Тогда
				// такого предопределённого элемента нет
			Иначе
				// заполняем предопределённый элемент
				Настройка = Настройка.ПолучитьОбъект();
				Если Настройка.ДоступныеКнопки.Количество() = 0 Тогда
					Для Каждого СтрокаТЗ Из ТаблицаКнопок Цикл
						Попытка
							НоваяСтрока = Настройка.ДоступныеКнопки.Добавить();
							НоваяСтрока.Имя   = СтрокаТЗ.Имя;
							ХранилищеЦвета = Новый ХранилищеЗначения(СтрокаТЗ.Цвет);
							НоваяСтрока.Цвет = ХранилищеЦвета;
							ХранилищеШрифт = Новый ХранилищеЗначения(СтрокаТЗ.Шрифт);
							НоваяСтрока.Шрифт = ХранилищеШрифт;
							НоваяСтрока.Картинка = Новый ХранилищеЗначения(СтрокаТЗ.Картинка);
							НоваяСтрока.Представление = СтрокаТЗ.Представление;
							НоваяСтрока.Выбран = СтрокаТЗ[СтрокаКолонки.Имя] = "Истина";
						Исключение
						КонецПопытки;
					КонецЦикла;
				КонецЕсли;
				Если Настройка.ДоступнаяИнформация.Количество() = 0 Тогда
					Для Каждого СтрокаТЗ Из ТаблицаИнформации Цикл
						Если СтрокаТЗ[СтрокаКолонки.Имя] = "Истина" Тогда
							НоваяСтрока = Настройка.ДоступнаяИнформация.Добавить();
							НоваяСтрока.Имя = СтрокаТЗ.Имя;
						КонецЕсли;
					КонецЦикла;
					Настройка.ВысотаКнопок = 35;
					Настройка.РасстояниеМеждуКнопками = 5;
					Настройка.ОтображатьВВидеВкладок = Истина;
					Настройка.ГлубинаПросмотраНапоминаний = 12;
					Настройка.ГлубинаПросмотраПросроченныхНапоминаний = 1;
					Настройка.Записать();
					Сообщить("Справочник ""НастройкиФункциональнойПанели"". Обновлен элемент """ + СокрЛП(СтрокаКолонки.Имя) + """");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры// ЗаполнитьПредопределенныеЭлементы() Экспорт

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Наименование");
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено, производится
//	программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового объекта на основании
//
// Параметры:
//  Основание - ссылка на объект основания для данного справочника
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
	ВысотаКнопок = 30;
	РасстояниеМеждуКнопками = 4;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АРМПоУмолчанию) Тогда
		НайденнаяСтрока = ДоступныеКнопки.Найти(АРМПоУмолчанию,"Имя");
		Если НайденнаяСтрока<>Неопределено И НЕ НайденнаяСтрока.Выбран Тогда
			АРМПоУмолчанию = "";	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
// Модуль справочника Внешние печатные формы

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// получение родителя верхнего уровня
//
// Возвращаемое значение:
//   Ссылка – Ссылка на элемент справочника.
//
Функция ПолучитьРодителяВерхнегоУровня() Экспорт
	Если Родитель.Пустая() Тогда
		Если ЭтоГруппа Тогда
			Возврат Ссылка;
		Иначе
			Возврат Справочники.ВнешниеПечатныеФормы.ПустаяСсылка();
		КонецЕсли; 
	Иначе
		Возврат Родитель.ПолучитьОбъект().ПолучитьРодителяВерхнегоУровня();
	КонецЕсли; 
КонецФункции // ПолучитьРодителяВерхнегоУровня()

// Загрузка внешней печатной формы в справочник
//
// Параметры:
//  ПутьКФорме 		– Строка		– Путь с именем файла загружаемой формы.
//
// Возвращаемое значение:
//   Булево				   			– Результат загрузки формы.
//
Функция ЗагрузитьВнешнююПечатнуюФорму(ПутьКФорме=Неопределено) Экспорт
	Если обЗначениеНеЗаполнено(ПутьКФорме) Тогда
		Возврат Ложь;
	КонецЕсли;
	ПечатнаяФорма=Новый ДвоичныеДанные(ПутьКФорме);
	Хранилище=Новый ХранилищеЗначения(ПечатнаяФорма, Новый СжатиеДанных(9));
	ВерсияФормы=ТекущаяДата();
	Возврат Истина;
КонецФункции // ЗагрузитьВнешнююПечатнуюФорму()

// Выгрузка печатной формы в файл
//
// Параметры:
//  ПутьКФорме 		– Строка		– Путь с именем файла выгрузки формы.
//
// Возвращаемое значение:
//   Булево				   			– Результат выгрузки формы.
//
Функция ВыгрузитьВнешнююПечатнуюФорму(ПутьКФорме=Неопределено) Экспорт 
	Если обЗначениеНеЗаполнено(ПутьКФорме) Тогда
		Возврат Ложь;
	КонецЕсли;
	ПечатнаяФорма=Хранилище.Получить();
	Если ТипЗнч(ПечатнаяФорма)<>Тип("ДвоичныеДанные") Тогда
		Возврат Ложь;
	КонецЕсли; 
	ПечатнаяФорма.Записать(ПутьКФорме);
	Возврат Истина;
КонецФункции // ВыгрузитьВнешнююПечатнуюФорму()

// удаляем хранимую форму
Функция УдалитьПечатнуюФорму() Экспорт
	Хранилище=Новый ХранилищеЗначения("");
КонецФункции // УдалитьПечатнуюФорму()

#Если Клиент Тогда
	
// Печатаем прицепленный объект
//
// Параметры:
//  ДокументОбъект 	– ДокументОбъект	– Документ для печати.
//
// Возвращаемое значение:
//  ТабДокумент 	– ТабличныйДокумент	– Печатная форма документа.
//
Функция Печать(ДокументОбъект=Неопределено,ТабДокумент=Неопределено) Экспорт
	Если ХранитьВоВнешнемФайле Тогда
		ИмяФайлаПечатнойФормы=ПутьКВнешнемуФайлу;
		Если обЗначениеНеЗаполнено(ИмяФайлаПечатнойФормы) Тогда
			Предупреждение("Отсутствует внешняя печатная форма!");
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		ИмяФайлаПечатнойФормы = ПолучитьИмяВременногоФайла();
		Попытка
			Хранилище.Получить().Записать(ИмяФайлаПечатнойФормы);
		Исключение
			Предупреждение("Внешняя печатная форма не загружена в базу!");
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	//Попытка
	//	ТабДокумент = ВнешниеОбработки.Создать(ИмяФайлаПечатнойФормы).Печать(ДокументОбъект,ТабДокумент);
	//Исключение
	//	ТабДокумент = ВнешниеОтчеты.Создать(ИмяФайлаПечатнойФормы).Печать(ДокументОбъект,ТабДокумент);
	//КонецПопытки;
	
	//внешние печатные формы в виде внешнего отчета не допускаются
	ТабДокумент = ВнешниеОбработки.Создать(ИмяФайлаПечатнойФормы).Печать(ДокументОбъект,ТабДокумент);
	
	Попытка
		Если НЕ ХранитьВоВнешнемФайле Тогда
			УдалитьФайлы(ИмяФайлаПечатнойФормы);
		КонецЕсли; 
	Исключение
	КонецПопытки; 
	Возврат ТабДокумент;
КонецФункции // Печать()

// Открываем форму прицепленного объекта
Функция ОткрытьФормуОбъекта() Экспорт
	Отказ=Ложь;
	спПроверкаПраваДоступа(ЭтотОбъект,Отказ,,Права);
	Если Отказ Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Если ХранитьВоВнешнемФайле Тогда
		ИмяФайлаПечатнойФормы=ПутьКВнешнемуФайлу;
	Иначе
		Попытка
			ИмяФайлаПечатнойФормы=ПолучитьИмяВременногоФайла();
			Хранилище.Получить().Записать(ИмяФайлаПечатнойФормы);
		Исключение			
			Сообщение =?(СокрЛП(Хранилище.Получить())="","Форма НЕ загружена!","Нельзя открыть форму");
			Сообщить(Сообщение);
			Возврат Ложь;
		КонецПопытки;		
	КонецЕсли;
	Попытка
		ВнешниеОбработки.Создать(ИмяФайлаПечатнойФормы).ПолучитьФорму().Открыть();
		Возврат Истина;
	Исключение
		Попытка
			ВнешниеОтчеты.Создать(ИмяФайлаПечатнойФормы).ПолучитьФорму().Открыть();
			Возврат Истина;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	КонецПопытки;
КонецФункции // ОткрытьФормуОбъекта()

#КонецЕсли

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	Если (НЕ ЭтоГруппа) Тогда
		ОбязательныеРеквизиты.Вставить("ВидОбработки",1);
		Если (НЕ ДляВсехДокументов) И (ВидОбработки = Перечисления.ВидыВнешнихОбработок.ПечатнаяФорма ИЛИ ВидОбработки = Перечисления.ВидыВнешнихОбработок.ЗаполнениеТабличнойЧасти) Тогда
			Унакал=(ВидОбработки = Перечисления.ВидыВнешнихОбработок.ПечатнаяФорма);
			СтруктураПринадлежность= Новый Структура;
			СтруктураПринадлежность.Вставить("ИмяОбъекта",?(Унакал,3,1));
			СтруктураПринадлежность.Вставить("ПредставлениеОбъекта",?(Унакал,3,1));
			Если ВидОбработки = Перечисления.ВидыВнешнихОбработок.ЗаполнениеТабличнойЧасти Тогда
				СтруктураПринадлежность.Вставить("ИмяТабличнойЧастиПредставление",1);
				СтруктураПринадлежность.Вставить("ИмяТабличнойЧасти",1);
			КонецЕсли;
			ОбязательныеРеквизиты.Вставить("Принадлежность", СтруктураПринадлежность);
		КонецЕсли;
	КонецЕсли;
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	
	Результат = Истина;
	
	//проверка, что наименования печатных форм для одного и того же вида документа не пересекаются
	Если ВидОбработки = Перечисления.ВидыВнешнихОбработок.ПечатнаяФорма 
		ИЛИ ВидОбработки = Перечисления.ВидыВнешнихОбработок.ШаблонПечатнойФормы
		ИЛИ ВидОбработки = Перечисления.ВидыВнешнихОбработок.ЗаполнениеТабличнойЧасти Тогда
		 
		//найдем все объекты печатных форм с таким же наименованием и видом обработки
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВнешниеПечатныеФормы.Ссылка,
		               |	ВЫБОР
		               |		КОГДА ВнешниеПечатныеФормы.ДляВсехДокументов = ИСТИНА
		               |			ТОГДА ""ДляВсехДокументов""
		               |		ИНАЧЕ ВнешниеПечатныеФормыПринадлежность.ИмяОбъекта
		               |	КОНЕЦ КАК ИмяОбъекта
		               |ИЗ
		               |	Справочник.ВнешниеПечатныеФормы КАК ВнешниеПечатныеФормы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПечатныеФормы.Принадлежность КАК ВнешниеПечатныеФормыПринадлежность
		               |		ПО (ВнешниеПечатныеФормыПринадлежность.Ссылка = ВнешниеПечатныеФормы.Ссылка)
		               |ГДЕ
		               |	ВнешниеПечатныеФормы.Наименование = &Наименование
		               |	И ВнешниеПечатныеФормы.ВидОбработки = &ВидОбработки
		               |	И ВнешниеПечатныеФормы.ПометкаУдаления = ЛОЖЬ
		               |	И ВнешниеПечатныеФормы.ЭтоГруппа = ЛОЖЬ
		               |	И ВнешниеПечатныеФормы.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("Наименование",Наименование);
		Запрос.УстановитьПараметр("ВидОбработки",ВидОбработки);
		ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();

		//если найдены объекты печатных форм с таким же наименованием, будем искать совпадающие объкты
		Если ТаблицаОбъектов.Количество() > 0 Тогда
			Если ДляВсехДокументов Тогда
				Результат = Ложь; 
				дкДобавитьОшибку(Ошибки,"Реквизит ""Наименование"" не уникален. " + ВидОбработки + " с таким наименованием уже имеется для других документов.");
			Иначе
				Если ТаблицаОбъектов.Найти("ДляВсехДокументов","ИмяОбъекта") <> Неопределено Тогда
					Результат = Ложь; 
					дкДобавитьОшибку(Ошибки,"Реквизит ""Наименование"" не уникален. " + ВидОбработки + " с таким наименованием уже имеется для других документов.");
				Иначе
					Для Каждого ТекОбъект ИЗ Принадлежность Цикл
						Если ТаблицаОбъектов.Найти(ТекОбъект.ИмяОбъекта,"ИмяОбъекта") <> Неопределено Тогда
							Результат = Ложь; 
							дкДобавитьОшибку(Ошибки,"Реквизит ""Наименование"" не уникален. " + ВидОбработки + " с таким наименованием уже имеется для объекта """ + СокрЛП(ТекОбъект.ИмяОбъекта) + """.");
						КонецЕсли;
					КонецЦикла
				КонецЕсли;
			КонецЕсли;		
		КонецЕсли;		
		
	КонецЕсли;
	
	Возврат Результат И спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя="" Тогда
		
	Иначе
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли

// Функция записи параметров в регистр сведений
// Параметры
//  Автомобиль – СправочникСсылка.Автомобили – Автомобиль для которого за
//  Значение   – Произвольный – Значение регистра сведений
//  ВидОбъекта – ПеречислениеСсылка.ДополнительнаяИнформацияАвтомобилей – 
//                 Описание вида значения регистра сведений
// Возвращаемое значение:
//   Булево   – ошибка записи значения
Функция ЗаписьЗначенияРегистраСведения(Автомобиль, Значение,ВидЗначения,НаДату=Неопределено) Экспорт
	
	Отказ=Ложь;
	
	Если НаДату=Неопределено Тогда
		ДатаЗаписи=ТекущаяДата();
	Иначе
		ДатаЗаписи=НаДату;
	КонецЕсли; 
	
	Если ВидЗначения=Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег Тогда
		//При записи нового пробега
		//Получить последний пробег и следующий
		СтруктураОтбора=Новый Структура("Автомобиль,ВидЗначения", Автомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Пробег);
		СтруктураСведений=РегистрыСведений.Автомобили.ПолучитьПоследнее(Новый Граница(ДатаЗаписи,ВидГраницы.Исключая),СтруктураОтбора);
		Попытка ПробегДо = Число(СтруктураСведений.Значение); Исключение ПробегДо = 0; КонецПопытки;
		СтруктураСведений=РегистрыСведений.Автомобили.ПолучитьПервое(Новый Граница(ДатаЗаписи,ВидГраницы.Исключая),СтруктураОтбора);
		Попытка ПробегПосле = Число(СтруктураСведений.Значение); Исключение ПробегПосле = 9999999999; КонецПопытки;
		Если Значение<ПробегДо Тогда
			Сообщить("Значение пробега автомобиля не может быть менее старого значения ("+СокрЛП(ПробегДо)+").");
			Отказ=Истина; Возврат Отказ;
		КонецЕсли; 
		Если Значение>ПробегПосле Тогда
			Сообщить("Значение пробега автомобиля не может быть более следующего значения ("+СокрЛП(ПробегПосле)+").");
			Отказ=Истина; Возврат Отказ;
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ Отказ Тогда
		//Чтение старого значения регистра
		СтруктураОтбора = Новый Структура("Автомобиль,ВидЗначения", Автомобиль,ВидЗначения);
		СтруктураСведений = РегистрыСведений.Автомобили.ПолучитьПоследнее(ДатаЗаписи,СтруктураОтбора);
		ЗначениеСтарое    = СтруктураСведений.Значение;
		Записывать        = Ложь;
		//Введено значение, а старое отсутствует
		Если НЕ обЗначениеНеЗаполнено(Значение) И ЗначениеСтарое=Неопределено Тогда Записывать=Истина; КонецЕсли; 
		//Значение стерто, а старое значение было введено
		Если обЗначениеНеЗаполнено(Значение) И ЗначениеСтарое<>Неопределено Тогда Записывать=Истина; КонецЕсли; 
		//Введено значение и было введено старое
		Если НЕ обЗначениеНеЗаполнено(Значение) И ЗначениеСтарое<>Неопределено Тогда
			//Значение изменилось
			Если Значение<>ЗначениеСтарое Тогда Записывать=Истина; КонецЕсли; 
		КонецЕсли; 
		Если Записывать Тогда
			//Значение параметра изменилось
			ЗаписьРегСведений=РегистрыСведений.Автомобили.СоздатьМенеджерЗаписи();
			ЗаписьРегСведений.Автомобиль  = Автомобиль;
			ЗаписьРегСведений.ВидЗначения = ВидЗначения;
			ЗаписьРегСведений.Значение    = Значение;
			ЗаписьРегСведений.Период      = ДатаЗаписи;
			Попытка
				ЗаписьРегСведений.Записать();
			Исключение
				Сообщить("Ошибка записи параметра автомобиля в регистр сведений");
				Отказ=Истина;
			КонецПопытки; 
		КонецЕсли; 
	КонецЕсли; 
		
	Возврат Отказ;
КонецФункции // ЗаписьЗначенияРегистраСведения()

// Функция чтения параметров в регистр сведений
// Параметры
//  Автомобиль – СправочникСсылка.Автомобили – Автомобиль для которого за
//  ВидОбъекта – ПеречислениеСсылка.ДополнительнаяИнформацияАвтомобилей – 
//                 Описание вида значения регистра сведений
// Возвращаемое значение:
//   Булево   – ошибка записи значения
Функция ЧтениеЗначенияРегистраСведения(Автомобиль, ВидЗначения, НаДату = Неопределено) Экспорт
	Если НаДату = Неопределено Тогда
		ДатаЗаписи = Неопределено;
	Иначе
		//ДатаЗаписи = НачалоДня(НаДату);
		ДатаЗаписи = НаДату;
	КонецЕсли;
	СтруктураОтбора   = Новый Структура("Автомобиль, ВидЗначения", Автомобиль, ВидЗначения);
	СтруктураСведений = РегистрыСведений.Автомобили.ПолучитьПоследнее(ДатаЗаписи, СтруктураОтбора);
	Возврат СтруктураСведений.Значение;
КонецФункции

Функция СформироватьНаименованиеАвтомобиляПоПолям(Модель="", Цвет="", ГосНомер="", VIN="") Экспорт
	НовоеНаименование="";;
	Если НЕ обЗначениеНеЗаполнено(Модель) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+СокрЛП(Модель);
	КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(Цвет) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+СокрЛП(Цвет);
		КонецЕсли; 
	Если НЕ обЗначениеНеЗаполнено(ГосНомер) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+"№ "+СокрЛП(ГосНомер);
		КонецЕсли;
	Если НЕ обЗначениеНеЗаполнено(VIN) Тогда
		Если НЕ ПустаяСтрока(НовоеНаименование) Тогда
			НовоеНаименование=НовоеНаименование+" ";
		КонецЕсли; 
		НовоеНаименование=НовоеНаименование+"VIN "+СокрЛП(VIN);
	КонецЕсли;
	
	Возврат НовоеНаименование;
КонецФункции

//БИЗТЕХ КОВАЛЬ 10.03.2025 ++
Функция БТ_ВладелецАвтомобиля(СсылкаНаАвтомобиль) Экспорт
	Если ЗначениеЗаполнено(СсылкаНаАвтомобиль) И ТипЗнч(СсылкаНаАвтомобиль) = Тип("СправочникСсылка.Автомобили") Тогда
		Возврат Справочники.Автомобили.ЧтениеЗначенияРегистраСведения(СсылкаНаАвтомобиль, Перечисления.ДополнительнаяИнформацияАвтомобилей.Хозяин); 
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции
//БИЗТЕХ КОВАЛЬ 10.03.2025 --

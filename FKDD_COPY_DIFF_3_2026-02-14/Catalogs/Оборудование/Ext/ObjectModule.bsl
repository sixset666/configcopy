// Модуль справочника Оборудование

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",3);
	
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ИдентификаторОборудования",3);	// должно быть заполнено, иначе смысла никакого элемент не имеет
		ОбязательныеРеквизиты.Вставить("КлассОборудования",1);			// без класса вообще никуда
		ОбязательныеРеквизиты.Вставить("МодельОборудования",1);			// хоть нужно только для отображения пользователю, но чтоб он сам знал четко с чем дело имеет
		ОбязательныеРеквизиты.Вставить("Компьютер",1);					// на каком именно компьютере оборудование подключается тоже знать нужно обязательно
		ОбязательныеРеквизиты.Вставить("Таймаут",1);					// с нулем нельзя вызывать
		// обязательные реквизиты для загружаемого оборудования
		Если 	КлассОборудования=Перечисления.КлассыОборудования.ТСД
			ИЛИ КлассОборудования=Перечисления.КлассыОборудования.Весы Тогда
			ОбязательныеРеквизиты.Вставить("ГруппаТоваровОборудования",1);
			ОбязательныеРеквизиты.Вставить("ОсновнойОтдел",1);
		КонецЕсли;
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// формирует наименование для элемента оборудования
//
// Параметры:
//	Представление 	- Строка	- Наименование
//
Функция СформироватьНаименование(Представление="") Экспорт
	НовоеНаименование=СокрЛП(Представление);
	// Если сверху не передали, то сформируем из представления модели
	Если ПустаяСтрока(НовоеНаименование) Тогда
		Если ПустаяСтрока(МодельОборудования) Тогда
			// а если модель пустая то тогда из класса
			Если обЗначениеНеЗаполнено(КлассОборудования) Тогда
				НовоеНаименование="Устройство";
			Иначе
				НовоеНаименование=СокрЛП(КлассОборудования);
			КонецЕсли;
		Иначе
			НовоеНаименование=СокрЛП(МодельОборудования);
		КонецЕсли;
	КонецЕсли;
	ИмяКомпьютера="";
	Если НЕ обЗначениеНеЗаполнено(Компьютер) Тогда
		ИмяКомпьютера=СокрЛП(Компьютер);
		Если (Найти(ВРЕГ(НовоеНаименование),ВРЕГ(ИмяКомпьютера)) = 0) И (Найти(ВРЕГ(НовоеНаименование),ВРЕГ(" на ")) = 0)  Тогда
			ИмяКомпьютера=" на "+ИмяКомпьютера;
			НовоеНаименование=НовоеНаименование+ИмяКомпьютера;
		КонецЕсли;
	КонецЕсли;
	// Проверим нет ли уже такого наименования
	ЭлементСсылка=Справочники.Оборудование.НайтиПоНаименованию(НовоеНаименование,Истина);
	Пока НЕ (ЭлементСсылка.Пустая() ИЛИ (НЕ ЭтоНовый() И ЭтотОбъект.Ссылка=ЭлементСсылка)) Цикл
		// Будем слегка модифицировать название оборудования прибавляя номер устройства пока не создадим уникальное
		ПозицияСИменемКомпьютера=Найти(НовоеНаименование,ИмяКомпьютера);
		// Если хвост названия (до компьютера) у нас уже цифра, то выделим ее
		ЧастьНазвания=СокрЛП(?(ПозицияСИменемКомпьютера>0,Лев(НовоеНаименование,ПозицияСИменемКомпьютера),НовоеНаименование));
		ПозицияНачалаМодификатора=СтрДлина(ЧастьНазвания);
		Символ=Сред(ЧастьНазвания,ПозицияНачалаМодификатора,1);
		Пока (ПозицияНачалаМодификатора > 0) И (Символ <> "_") И (Найти("0123456789",Символ) > 0) Цикл
			ПозицияНачалаМодификатора=ПозицияНачалаМодификатора-1;
			Символ=Сред(ЧастьНазвания,ПозицияНачалаМодификатора,1);
		КонецЦикла;
		МодификаторНазвания="_2";
		Если (Символ = "_") И (ПозицияНачалаМодификатора > 0) И (ПозицияНачалаМодификатора <= СтрДлина(ЧастьНазвания)) Тогда
			Попытка // если нашлось цифровое окончание названия, то увеличим его
				МодификаторНазвания=Число(Сред(ЧастьНазвания,ПозицияНачалаМодификатора+1))+1;
				МодификаторНазвания="_"+Строка(МодификаторНазвания);
				ЧастьНазвания=Лев(ЧастьНазвания,ПозицияНачалаМодификатора-1);
			Исключение КонецПопытки;
		КонецЕсли;
		// соберем воедино новое наименование			
		НовоеНаименование=СокрЛП(ЧастьНазвания)+СокрЛП(МодификаторНазвания+ИмяКомпьютера);
		// Поищем с новым вариантом названия
		ЭлементСсылка=Справочники.Оборудование.НайтиПоНаименованию(НовоеНаименование,Истина);
	КонецЦикла;
	// вернем что получилось
	Возврат НовоеНаименование;
КонецФункции // СформироватьНаименование()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	// не копируем идентификатор
	ИдентификаторОборудования="";
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	Если НЕ Отказ Тогда
		МассивСсылок = Новый Массив;
		МассивСсылок.Добавить(ЭтотОбъект.Ссылка);
		ТаблицаСсылок =	НайтиПоСсылкам(МассивСсылок);
		Если ТаблицаСсылок.Количество() > 0 Тогда
			Отказ = Истина;
			Сообщить("Отменено удаление оборудования: "+СокрП(Наименование)+", GUID="+СокрП(ИдентификаторОборудования)+" - в базе имеются ссылки на этот элемент.",СтатусСообщения.Внимание);
			Сообщить("Проверьте настройки рабочего места и/или пользователя. Очистите все ссылки на это оборудование чтобы его можно было удалить из системы.",СтатусСообщения.Информация);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
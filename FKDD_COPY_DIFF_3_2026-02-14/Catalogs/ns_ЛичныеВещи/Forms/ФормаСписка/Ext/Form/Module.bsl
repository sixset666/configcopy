
//Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)    
	УстановитьВидимостьРеквизитовЗаполнения();
	ДатаОстатков = ТекущаяДата();
	ОбновитьСписокНаСервере();
	Элементы.Category.СписокВыбора.ЗагрузитьЗначения(ns_ЛичныеВещиФормы.ПолучитьСписокКатегорий());
	Элементы.GoodsType.СписокВыбора.ЗагрузитьЗначения(ns_ЛичныеВещиФормы.ПолучитьСписокПодкатегорий(Category));  
	
	ns_ОбщегоНазначенияФормы.УстановитьТипСклад(Элементы);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ns_ОбщегоНазначенияФормы.ОбработкаВыбора(Элементы, ВыбранноеЗначение, ИсточникВыбора, "ns_ЛичныеВещи"); 
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаОстатковПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПоДокументу(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаОтметкиПоДокументу",, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура CategoryПриИзменении(Элемент)
	СброситьРеквизитыЗаполнения(, Истина);
	УстановитьВидимостьРеквизитовЗаполнения();
	Элементы.GoodsType.СписокВыбора.ЗагрузитьЗначения(ns_ЛичныеВещиФормы.ПолучитьСписокПодкатегорий(Category));
КонецПроцедуры

&НаКлиенте
Процедура GoodsTypeПриИзменении(Элемент)
	СброситьРеквизитыЗаполнения();
	УстановитьВидимостьРеквизитовЗаполнения();
	Элементы.Apparel.СписокВыбора.ЗагрузитьЗначения(ns_ЛичныеВещиФормы.ПолучитьСписокApparel(GoodsType));  
	Элементы.Size.СписокВыбора.ЗагрузитьЗначения(ns_ЛичныеВещиФормы.ПолучитьСписокSize(GoodsType, Apparel));
КонецПроцедуры

&НаКлиенте
Процедура ApparelПриИзменении(Элемент)
	ApparelType = Неопределено;
	УстановитьВидимостьРеквизитовЗаполнения();
	Элементы.ApparelType.СписокВыбора.ЗагрузитьЗначения(ns_ЛичныеВещиФормы.ПолучитьСписокApparelType(Apparel));
	Элементы.Size.СписокВыбора.ЗагрузитьЗначения(ns_ЛичныеВещиФормы.ПолучитьСписокSize(GoodsType, Apparel));
КонецПроцедуры

&НаКлиенте
Процедура ApparelНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(GoodsType) Тогда
		ЗначениеОтбора = Новый Структура("Владелец", GoodsType);
		ПараметрыФормы = Новый Структура("Отбор", ЗначениеОтбора);                    	
		ОткрытьФорму("Справочник.ns_Apparel.ФормаВыбора", ПараметрыФормы);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ApparelTypeНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Apparel) Тогда
		ЗначениеОтбора = Новый Структура("Владелец", Apparel);
		ПараметрыФормы = Новый Структура("Отбор", ЗначениеОтбора);                    	
		ОткрытьФорму("Справочник.ns_ApparelType.ФормаВыбора", ПараметрыФормы);
	КонецЕсли; 
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сбросить(Команда)
	СброситьРеквизитыЗаполнения(Истина, Истина);
	УстановитьВидимостьРеквизитовЗаполнения();
КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)
	Если ЗначениеЗаполнено(Category) Тогда
		ТекстВопроса = "При изменении Category, у всех выделеных элементов сотрутся GoodsType. Продолжить?";
	Иначе
		ТекстВопроса = "При изменении реквизитов, у всех выделеных элементов сотрутся GoodsType установленные ранее. Продолжить?";
	КонецЕсли; 
	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьЗавершение", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60); 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
		УстановитьНаСервере(ВыделенныеСтроки);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СнятьБезОстатковСВыгрузки(Команда)
	СнятьБезОстатковСВыгрузкиНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры
 
//КонецОбласти

//Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьРеквизитовЗаполнения()
	Если НЕ ЗначениеЗаполнено(Category) Тогда
		Элементы.GoodsType.Видимость = Ложь;
		Элементы.Apparel.Видимость = Ложь;
		Элементы.ApparelType.Видимость = Ложь;
		Элементы.Size.Видимость = Ложь;
	Иначе
		Элементы.GoodsType.Видимость = Истина;
		Элементы.Apparel.Видимость = ns_ЛичныеВещиВалидация.УстановитьВидимость_Apparel(Category, GoodsType);
		Элементы.ApparelType.Видимость = ns_ЛичныеВещиВалидация.УстановитьВидимость_ApparelType(Category, GoodsType, Apparel);
		Элементы.Size.Видимость = ns_ЛичныеВещиВалидация.УстановитьВидимость_Size(Category, GoodsType, Apparel);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СброситьРеквизитыЗаполнения(ОчищатьCategory = Ложь, ОчищатьGoodsType = Ложь)
	Если ОчищатьCategory Тогда 
		Category = Неопределено;
	КонецЕсли;          
	Если ОчищатьGoodsType Тогда
		GoodsType = Неопределено;
		Apparel = Неопределено;
		ApparelType = Неопределено;
	КонецЕсли;
	СопоставлениеРеквизитов = Неопределено;
	ConditionЛичныеВещи = Неопределено; 
	AdType = Неопределено; 
	ШаблонЗаголовка = Неопределено;
	ШаблонОбъявления = Неопределено;
	Size = Неопределено;
КонецПроцедуры

&НаСервере
Процедура УстановитьНаСервере(ВыделенныеСтроки)
	Попытка
		НачатьТранзакцию();
		
		ЗначенияСопоставленныхРеквизитов = Неопределено;
		Если ЗначениеЗаполнено(СопоставлениеРеквизитов) Тогда
			ЗначенияСопоставленныхРеквизитов = ns_Переопределяемый.ПолучитьЗначенияСопоставленныхРеквизитов(ВыделенныеСтроки);
		КонецЕсли; 
		
		Для каждого СтрокаСписка Из ВыделенныеСтроки Цикл
			Если СтрокаСписка.ЭтоГруппа Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ns_ЛичныеВещи.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.ns_ЛичныеВещи КАК ns_ЛичныеВещи
				|ГДЕ
				|	ns_ЛичныеВещи.Родитель В ИЕРАРХИИ(&Родитель)
				|	И ns_ЛичныеВещи.ЭтоГруппа = ЛОЖЬ";
				Запрос.УстановитьПараметр("Родитель", СтрокаСписка.Ссылка);
				РезультатЗапроса = Запрос.Выполнить();
				
				Если РезультатЗапроса.Пустой() Тогда
					Возврат;
				КонецЕсли;  
				
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					УстановитьЗначениеЭлементу(Выборка.Ссылка, ЗначенияСопоставленныхРеквизитов);
				КонецЦикла; 
			Иначе	
				УстановитьЗначениеЭлементу(СтрокаСписка, ЗначенияСопоставленныхРеквизитов);
			КонецЕсли;
		КонецЦикла;  
		
		ЗафиксироватьТранзакцию();		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
	    ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки; 
	 
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеЭлементу(Ссылка, ЗначенияСопоставленныхРеквизитов)
	
	Изменен = Ложь;
	ТекущийОбъект = Ссылка.ПолучитьОбъект();
	
	Если ЗначениеЗаполнено(Category) И (НЕ ТекущийОбъект.Category = Category) Тогда
		ТекущийОбъект.Category = Category;  
		ТекущийОбъект.GoodsType = Неопределено;
		Изменен = Истина;
	КонецЕсли;

	Если ЗначениеЗаполнено(GoodsType) И (НЕ ТекущийОбъект.GoodsType = GoodsType) Тогда
		ТекущийОбъект.GoodsType = GoodsType;
		Изменен = Истина;
	КонецЕсли;
	
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Apparel", Apparel);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "ApparelType", ApparelType);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Size", Size);
	
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Condition", ConditionЛичныеВещи);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Шаблон", ШаблонОбъявления);
	ns_ОбщегоНазначенияВызовСервера.ЗаполнитьСопоставлениеРеквизитов(Изменен, ТекущийОбъект, ЗначенияСопоставленныхРеквизитов, СопоставлениеРеквизитов);
	
	Если ЗначениеЗаполнено(ШаблонЗаголовка) Тогда
		НовыйЗаголовок = ns_ШаблонизаторСервер.СформироватьTitle(ШаблонЗаголовка, ТекущийОбъект.Title, ТекущийОбъект.Номенклатура, ТекущийОбъект.Характеристика);
		ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Title", НовыйЗаголовок);
	КонецЕсли;
	
	Если Изменен Тогда
		ТекущийОбъект.Записать();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Истина, "ns_ЛичныеВещи");
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Ложь, "ns_ЛичныеВещи");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТолькоВыгружаемые(Команда)
	ns_ОбщегоНазначенияФормы.ПоказатьТолькоВыгружаемые(Элементы, Список);	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Список.ТекстЗапроса = ns_Переопределяемый.ПолучитьТекстЗапроса_ЛичныеВещи(Склад);
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Склад", Склад);
	КонецЕсли; 
	
	Список.Параметры.УстановитьЗначениеПараметра("НаДату", КонецДня(ДатаОстатков));
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
	Иначе
		СсылкаНаСтроку = ТекущиеДанные.Ссылка;
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
		Элементы.Список.ТекущаяСтрока = СсылкаНаСтроку;
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СнятьБезОстатковСВыгрузкиНаСервере()
	ns_АвитоВалидация.СнятьБезОстатковСВыгрузки("ns_ЛичныеВещи");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	ns_ОбщегоНазначенияВызовСервера.СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
КонецПроцедуры

//КонецОбласти 












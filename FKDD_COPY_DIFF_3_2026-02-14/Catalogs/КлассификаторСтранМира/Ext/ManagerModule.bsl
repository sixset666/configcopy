 
#Область ОбновлениеИнформационнойБазы
 
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры=Неопределено) Экспорт
	
	#Если Клиент Тогда
	Сообщить("Обновление справочника ""Классификатор стран мира""");
	#КонецЕсли
	
	Макет = Справочники.КлассификаторСтранМира.ПолучитьМакет("Классификатор");
	КлассификаторXML = Макет.ПолучитьТекст();
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(КлассификаторXML);
	ТаблицаСтран = СериализаторXDTO.ПрочитатьXML(Чтение);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КлассификаторСтранМира.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторСтранМира КАК КлассификаторСтранМира";
	
	Выборка = Запрос.Выполнить().Выбрать();
	УдаляемыеОбъекты = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ЕстьСоответствие = Ложь;
		КлассификаторСтранМира = Выборка.Ссылка.ПолучитьОбъект();
		ПараметрыОтбора = Новый Структура("Наименование");
		ПараметрыОтбора.Наименование = ВРег(КлассификаторСтранМира.Наименование);
		НайденныеСтроки = ТаблицаСтран.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество()>0 Тогда
			ЗаполнитьЗначенияСвойств(КлассификаторСтранМира, НайденныеСтроки[0]);
			Если ПустаяСтрока(КлассификаторСтранМира.НаименованиеПолное) Тогда
				КлассификаторСтранМира.НаименованиеПолное=КлассификаторСтранМира.Наименование;
			КонецЕсли;
			ЕстьСоответствие = Истина;
		Иначе
			ПараметрыОтбора = Новый Структура("Код");
			ПараметрыОтбора.Код = КлассификаторСтранМира.Код;
			НайденныеСтроки = ТаблицаСтран.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество()>0 Тогда
				ЗаполнитьЗначенияСвойств(КлассификаторСтранМира, НайденныеСтроки[0]);
				Если ПустаяСтрока(КлассификаторСтранМира.НаименованиеПолное) Тогда
					КлассификаторСтранМира.НаименованиеПолное=КлассификаторСтранМира.Наименование;
				КонецЕсли;
				ЕстьСоответствие = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЕстьСоответствие Тогда
			
			УдаляемыеОбъекты.Добавить(КлассификаторСтранМира.Ссылка);
			Продолжить;
			
		КонецЕсли;
		
		КлассификаторСтранМира.ОбменДанными.Загрузка = Истина;
		Попытка
			КлассификаторСтранМира.Записать();
			Сообщить("Обновлен справочник <"+КлассификаторСтранМира+">.");
		Исключение
			Сообщить("Ошибка обновления справочника <"+КлассификаторСтранМира+">: "+ОписаниеОшибки(), СтатусСообщения.Важное);
		КонецПопытки;
		
	КонецЦикла;
	
	Для Каждого КлассификаторСтранМира Из УдаляемыеОбъекты Цикл
		
		МассивСсылок = Новый Массив();
		МассивСсылок.Добавить(КлассификаторСтранМира);
		НайденныеСсылки = Неопределено;
		УдалитьОбъекты(МассивСсылок,Истина,НайденныеСсылки);
		
		Если НайденныеСсылки.Количество() > 0  Тогда
			
			Сообщить("В классификаторе стран мира нет записи с кодом <" + КлассификаторСтранМира.Код+"> и наименованием <"+КлассификаторСтранМира.Наименование+">.");
		    			
			ТекстСообщения = "Используется в следующих объектах:";
			
			Для Каждого Ссылка из НайденныеСсылки Цикл
							
				ТекстСообщения = ТекстСообщения+Символы.ПС+Символы.Таб+Строка(Ссылка.Метаданные)+" "+Строка(Ссылка.Данные);
												
			КонецЦикла;
			
			Сообщить(ТекстСообщения);			
			
		КонецЕсли;		
		
	КонецЦикла;
		
	// Поиск задвоенных объектов не выполняем так как для справочника включен контроль уникальности кода	
	
	#Если Клиент Тогда
	Сообщить("Обновление справочника ""Классификатор стран мира"" завершено.");
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти
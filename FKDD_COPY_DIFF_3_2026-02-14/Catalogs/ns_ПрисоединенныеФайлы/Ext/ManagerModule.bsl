Функция ПолучитьФото(Владелец) Экспорт
	
	ТаблицаФото = Новый ТаблицаЗначений;
	ТаблицаФото.Колонки.Добавить("ВладелецФайла");
	ТаблицаФото.Колонки.Добавить("ИмяФайла");
	ТаблицаФото.Колонки.Добавить("АдресИзображения");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ПрисоединенныеФайлы.Ссылка КАК Ссылка,
	|	ns_ПрисоединенныеФайлы.ВладелецФайла КАК ВладелецФайла,
	|	ns_ПрисоединенныеФайлы.Расширение КАК Расширение,
	|	ns_ПрисоединенныеФайлы.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ns_ПрисоединенныеФайлы КАК ns_ПрисоединенныеФайлы
	|ГДЕ
	|	ns_ПрисоединенныеФайлы.ВладелецФайла В(&ВладелецФайла)";
	Запрос.УстановитьПараметр("ВладелецФайла", Владелец);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НовСтр = ТаблицаФото.Добавить();
		НовСтр.ВладелецФайла = Владелец;
		НовСтр.ИмяФайла = Выборка.Наименование;
		НовСтр.АдресИзображения = ПолучитьНавигационнуюСсылкуФайлаВыборка(Выборка);
	КонецЦикла; 
	
	Возврат ТаблицаФото;
КонецФункции

Процедура ПолучитьФото_Хранилище(Владелец, ТаблицаФото) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ПрисоединенныеФайлы.ВладелецФайла,
	|	ns_ПрисоединенныеФайлы.Наименование КАК ИмяФайла,
	|	nsПрисоединенныеФайлыРегистр.Хранилище,
	|	ns_ПрисоединенныеФайлы.Ссылка,
	|	ns_ПрисоединенныеФайлы.Расширение
	|ИЗ
	|	Справочник.ns_ПрисоединенныеФайлы КАК ns_ПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ns_ПрисоединенныеФайлы КАК nsПрисоединенныеФайлыРегистр
	|		ПО ns_ПрисоединенныеФайлы.Ссылка = nsПрисоединенныеФайлыРегистр.ПрисоединенныйФайл
	|ГДЕ
	|	ns_ПрисоединенныеФайлы.ВладелецФайла В(&ВладелецФайла)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ns_ПрисоединенныеФайлы.ПорядокФайлов";
	Запрос.УстановитьПараметр("ВладелецФайла", Владелец);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НовСтр = ТаблицаФото.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
	КонецЦикла; 
	
КонецПроцедуры

Функция ПолучитьНавигационнуюСсылкуФайлаВыборка(Выборка)
	
	Если ns_ОбщегоНазначения.ЭтоГрафическийФайл(Выборка.Расширение) Тогда
		ЗначениеКлюча = Новый Структура;
		ЗначениеКлюча.Вставить("ПрисоединенныйФайл", Выборка.Ссылка);
		КлючЗаписи = РегистрыСведений.ns_ПрисоединенныеФайлы.СоздатьКлючЗаписи(ЗначениеКлюча);
		Возврат ПолучитьНавигационнуюСсылку(КлючЗаписи, "Хранилище");
	КонецЕсли; 
	
	Возврат "";
КонецФункции

Процедура ЗаписатьФотоВБазу(Данные) Экспорт
	
	Попытка
		НачатьТранзакцию();
		
		НовЭлемент = Справочники.ns_ПрисоединенныеФайлы.СоздатьЭлемент();
		НовЭлемент.ВладелецФайла = Данные.Владелец;
		НовЭлемент.Наименование = Данные.Наименование;
		НовЭлемент.Расширение = Данные.Расширение;
		НовЭлемент.ПорядокФайлов = ПолучитьПоследнийПорядокФайла(Данные.Владелец) + 1;
		НовЭлемент.Записать();
		
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Данные.АдресФайла);
		
		ЗаписьРегистра = РегистрыСведений.ns_ПрисоединенныеФайлы.СоздатьМенеджерЗаписи();
		ЗаписьРегистра.ПрисоединенныйФайл = НовЭлемент.Ссылка;
		ЗаписьРегистра.Хранилище = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		ЗаписьРегистра.Записать();
		
		НовЭлемент.УИД = НовЭлемент.Ссылка.УникальныйИдентификатор();
		НовЭлемент.СсылкаДляИзображения = ПолучитьНавигационнуюСсылкуФайла(НовЭлемент.Ссылка);
		НовЭлемент.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
	    ns_ОбщегоНазначения.СообщениеПользователю("Ошибка добавления фото в базу." + Символы.ПС + ОписаниеОшибки());
	КонецПопытки; 
	
КонецПроцедуры

Функция ПолучитьПоследнийПорядокФайла(Владелец) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ns_ПрисоединенныеФайлы.ПорядокФайлов КАК ПорядокФайлов
	|ИЗ
	|	Справочник.ns_ПрисоединенныеФайлы КАК ns_ПрисоединенныеФайлы
	|ГДЕ
	|	ns_ПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокФайлов УБЫВ";
	Запрос.УстановитьПараметр("ВладелецФайла", Владелец);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;	
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ПорядокФайлов;
КонецФункции

Функция ПолучитьСсылкуПоНомеруПорядка(Владелец, Порядок) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ns_ПрисоединенныеФайлы.Ссылка
	|ИЗ
	|	Справочник.ns_ПрисоединенныеФайлы КАК ns_ПрисоединенныеФайлы
	|ГДЕ
	|	ns_ПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	|	И ns_ПрисоединенныеФайлы.ПорядокФайлов = &ПорядокФайлов";
	Запрос.УстановитьПараметр("ВладелецФайла", Владелец);
	Запрос.УстановитьПараметр("ПорядокФайлов", Порядок);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;   
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
КонецФункции
 
Функция ПолучитьНавигационнуюСсылкуФайла(Ссылка) Экспорт
	
	ЗначениеКлюча = Новый Структура;
	ЗначениеКлюча.Вставить("ПрисоединенныйФайл", Ссылка);
	КлючЗаписи = РегистрыСведений.ns_ПрисоединенныеФайлы.СоздатьКлючЗаписи(ЗначениеКлюча);
	Возврат ПолучитьНавигационнуюСсылку(КлючЗаписи, "Хранилище");
	
	Возврат "";
КонецФункции

Функция ИнициализироватьТаблицуФото() Экспорт
	ТаблицаФото = Новый ТаблицаЗначений;
	ТаблицаФото.Колонки.Добавить("ВладелецФайла");
	ТаблицаФото.Колонки.Добавить("Ссылка");
	ТаблицаФото.Колонки.Добавить("Расширение");
	ТаблицаФото.Колонки.Добавить("ИмяФайла");
	ТаблицаФото.Колонки.Добавить("Хранилище");
	ТаблицаФото.Колонки.Добавить("ПутьНаДиске");
	ТаблицаФото.Колонки.Добавить("Счетчик");
	
	Возврат ТаблицаФото;
КонецФункции
  

// Модуль справочника ПодтверждающиеДокументы

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем Дата Экспорт;     // Дата

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ВидПодтверждающегоДокумента",1);
		ОбязательныеРеквизиты.Вставить("Номер",1);
		ОбязательныеРеквизиты.Вставить("ДатаВыдачи",1);
		
		ОбязательныеТовары=Новый Структура();
		ОбязательныеТовары.Вставить("Номенклатура",3);
		ОбязательныеТовары.Вставить("Количество",1);
		ОбязательныеРеквизиты.Вставить("Товары",ОбязательныеТовары);
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции

// формирует наименование
Функция СформироватьНаименование() Экспорт
	СтрокаВыдачи = "";
	Если НЕ ОбЗначениеНеЗаполнено(ДатаВыдачи) Тогда
		Если ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.Доверенность ИЛИ ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.Лицензия ИЛИ ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.Справка Тогда
			СтрокаВыдачи = " выдана " + Формат(ДатаВыдачи,"ДФ=dd.MM.yy")
		ИначеЕсли ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.Удостоверение Тогда
			СтрокаВыдачи = " выдано " + Формат(ДатаВыдачи,"ДФ=dd.MM.yy")
		Иначе	
			СтрокаВыдачи = " выдан " + Формат(ДатаВыдачи,"ДФ=dd.MM.yy")
		КонецЕсли;
	КонецЕсли;
	Стр=СокрЛП(ВидПодтверждающегоДокумента)+" "+СокрЛП(Серия)+" № "+СокрЛП(Номер) + СтрокаВыдачи;
	Возврат Стр;
КонецФункции

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ"
	Если Имя="" Тогда
		
	ИначеЕсли Имя = "Товары.Номенклатура" Тогда
		Если ТипЗнч(ТекСтрока.Номенклатура)=Тип("Строка") Тогда
			ТекСтрока.ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
			ТекСтрока.Коэффициент = 0;
			ТекСтрока.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		ИначеЕсли ТипЗнч(ТекСтрока.Номенклатура)=Тип("СправочникСсылка.Номенклатура") Тогда
			Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		КонецЕсли;
		
	ИначеЕсли Имя = "Товары.ЕдиницаИзмерения" Тогда
		ДопПараметры = Новый Структура("ПересчитатьЦену",Ложь);
		Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	ИначеЕсли Имя = "ДополнительнаяФорма" Тогда
		Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		
	Иначе Возврат дкОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита()

#Если Клиент Тогда

// функция 	перевод числа в пропись
//
// Параметры:
//  Количество	– Число			– Переданное число.
//
// Возвращаемое значение:
//   Строка   – Число прописью.
//
Функция КоличествоПрописью(Количество) Экспорт
	ЦелаяЧасть   = Цел(Количество);
	ДробнаяЧасть = Окр(Количество - ЦелаяЧасть, 3);
	Если ДробнаяЧасть = Окр(ДробнаяЧасть,0) Тогда
		ПараметрыПрописи = ", , , , , , , , 0";
	ИначеЕсли ДробнаяЧасть = Окр(ДробнаяЧасть, 1) Тогда
		ПараметрыПрописи = "целая, целых, целых, ж, десятая, десятых, десятых, м, 1";
	ИначеЕсли ДробнаяЧасть = Окр(ДробнаяЧасть, 2) Тогда
		ПараметрыПрописи = "целая, целых, целых, ж, сотая, сотых, сотых, м, 2";
	Иначе
		ПараметрыПрописи = "целая, целых, целых, ж, тысячная, тысячных, тысячных, м, 3";
	КонецЕсли;
	Возврат ЧислоПрописью(Количество, ,ПараметрыПрописи);
КонецФункции 

// процедура печати доверенности
//
// Параметры:
//  ИмяБланка	- Строка	– Имя документа для печати.
//
Процедура ПечатьДоверенностьМ2(ИмяБланка) Экспорт
	Если ВидПодтверждающегоДокумента<>Перечисления.ВидыДокументов.Доверенность Тогда
		Возврат;
	КонецЕсли;
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("М2");
	
	Поставщик=обПолучитьЗначениеСвойства(ЭтотОбъект,"Грузоотправитель");
	Если обЗначениеНеЗаполнено(Поставщик) Тогда
		Поставщик = Контрагент;
	КонецЕсли;
	
	// часть для отреза
	Если ИмяБланка = "ПечатьДоверенностьМ2" Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Отрез");
		ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
		Если ТипЗнч(Владелец)=Тип("СправочникСсылка.Контрагенты") ИЛИ
			 ТипЗнч(Владелец)=Тип("СправочникСсылка.Организации") Тогда
			 ОбластьМакета.Параметры.ВладелецПредставление = спПолучитьНаименование(Владелец);
		ИначеЕсли ТипЗнч(Владелец)=Тип("СправочникСсылка.Сотрудники") Тогда
			 ОбластьМакета.Параметры.ВладелецПредставление = ?(обЗначениеНеЗаполнено(Владелец.Должность),"",спПолучитьНаименование(Владелец.Должность)+", ")+спПолучитьНаименование(Владелец);
		Иначе
			ОбластьМакета.Параметры.Владелец=Неопределено;
			ОбластьМакета.Параметры.ВладелецПредставление=Неопределено;
		КонецЕсли;
		Если Поставщик<>Неопределено Тогда
			ОбластьМакета.Параметры.Поставщик=Поставщик;
			ОбластьМакета.Параметры.ПоставщикПредставление=спПолучитьНаименование(Поставщик);
		КонецЕсли; 
		//РеквизитыНаряда
		ТабДокумент.Вывести(ОбластьМакета);
		НазваниеФормы = "Типовая межотраслевая форма № М-2";
	Иначе
		НазваниеФормы = "Типовая межотраслевая форма № М-2а";
	КонецЕсли;
	
	//Шапка документа
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	ОбластьМакета.Параметры.НазваниеФормы            = НазваниеФормы;
	ОбластьМакета.Параметры.Организация				 = ПараметрыСеанса.Организация;
	ОбластьМакета.Параметры.ОрганизацияПредставление = спПолучитьНаименование(ПараметрыСеанса.Организация)+
													   ", ИНН "+ПараметрыСеанса.Организация.ИНН+
													   ", "+киПолучитьПредставлениеКИ(ПараметрыСеанса.Организация,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	ОбластьМакета.Параметры.ОрганизацияКодПоОКПО	 = ПараметрыСеанса.Организация.КодПоОКПО;
	ОбластьМакета.Параметры.РеквизитыПотребителя     = спПолучитьПредставление(ПараметрыСеанса.Организация);
	ОбластьМакета.Параметры.РасчетныйСчет			 = ПараметрыСеанса.Организация.ОсновнойБанковскийСчет;
	ОбластьМакета.Параметры.РеквизитыСчета           = "р/с "+ПараметрыСеанса.Организация.ОсновнойБанковскийСчет.НомерСчета+
													   " в банке "+спПолучитьНаименование(ПараметрыСеанса.Организация.ОсновнойБанковскийСчет.Банк)+
													   ", БИК "+ПараметрыСеанса.Организация.ОсновнойБанковскийСчет.Банк.Код;
	Если ТипЗнч(Владелец)=Тип("СправочникСсылка.Контрагенты") ИЛИ
		 ТипЗнч(Владелец)=Тип("СправочникСсылка.Организации") Тогда
		 ОбластьМакета.Параметры.Должность				= Неопределено;
		 ОбластьМакета.Параметры.ВладелецПредставление	= спПолучитьНаименование(Владелец);
	ИначеЕсли ТипЗнч(Владелец)=Тип("СправочникСсылка.Сотрудники") Тогда
		 ОбластьМакета.Параметры.Должность				= Владелец.Должность;
		 ОбластьМакета.Параметры.ВладелецПредставление	= спПолучитьНаименование(Владелец);
	Иначе
		ОбластьМакета.Параметры.Владелец=Неопределено;
		ОбластьМакета.Параметры.ВладелецПредставление=Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументДоверенности) Тогда
		// заполним по указанному паспорту
		ОбластьМакета.Параметры.ПаспортСерия		 = ДокументДоверенности.Серия;
		ОбластьМакета.Параметры.ПаспортНомер		 = ДокументДоверенности.Номер;
		ОбластьМакета.Параметры.ПаспортВыдан		 = ДокументДоверенности.КемВыдан;
		ОбластьМакета.Параметры.ПаспортДатаВыдачи	 = ДокументДоверенности.ДатаВыдачи;
	Иначе
		// подставим первый попавшийся паспорт
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ПодтверждающиеДокументы.Серия,
		             |	ПодтверждающиеДокументы.Номер,
		             |	ПодтверждающиеДокументы.КемВыдан,
		             |	ПодтверждающиеДокументы.ДатаВыдачи
		             |ИЗ
		             |	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		             |ГДЕ
		             |	ПодтверждающиеДокументы.Владелец = &Владелец
		             |	И ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = &ВидПодтверждающегоДокумента
		             |	И ПодтверждающиеДокументы.Устаревший = ЛОЖЬ
		             |
		             |УПОРЯДОЧИТЬ ПО
		             |	ПодтверждающиеДокументы.Текущий";
		Запрос.УстановитьПараметр("Владелец",Владелец);
		Запрос.УстановитьПараметр("ВидПодтверждающегоДокумента",Перечисления.ВидыДокументов.Паспорт);
		Выборка=Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ОбластьМакета.Параметры.ПаспортСерия		 = Выборка.Серия;
			ОбластьМакета.Параметры.ПаспортНомер		 = Выборка.Номер;
			ОбластьМакета.Параметры.ПаспортВыдан		 = Выборка.КемВыдан;
			ОбластьМакета.Параметры.ПаспортДатаВыдачи	 = Выборка.ДатаВыдачи;
		КонецЕсли; 
	КонецЕсли;
	
	Если Поставщик<>Неопределено Тогда
		ОбластьМакета.Параметры.Поставщик=Поставщик;
		ОбластьМакета.Параметры.ПоставщикПредставление=спПолучитьНаименование(Поставщик);
	КонецЕсли; 
	//РеквизитыДокументаНаПолучение
	ТабДокумент.Вывести(ОбластьМакета);
	
	//Вывод шапки таблицы
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);
	
	//Вывод табличной части
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для каждого СтрокаТоваров Из Товары Цикл
		ОбластьМакета.Параметры.Заполнить(СтрокаТоваров);
		ОбластьМакета.Параметры.КоличествоПрописью = Формат(СтрокаТоваров.Количество,"ЧДЦ=3; ЧН=0,00") + " (" + 
													 КоличествоПрописью(СтрокаТоваров.Количество) + ")";
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	//Вывод подвала
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	Дата = ТекущаяДата();
	Руководитель = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.Руководитель,ПараметрыСеанса.Организация);
	ОбластьМакета.Параметры.Заполнить(Руководитель);
	ГлавныйБухгалтер = дкОтветственноеЛицо(ЭтотОбъект,Перечисления.ВидыОбъектовСведений.ГлавныйБухгалтер,ПараметрыСеанса.Организация);
	ОбластьМакета.Параметры.Заполнить(ГлавныйБухгалтер);
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабличныйДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	ТабличныйДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабличныйДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",Права);
	ТабличныйДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	ТабличныйДокумент.ПолеСлева				= 9;
	ТабличныйДокумент.ПолеСправа			= 6;
	//Выведем печатную форму	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ДополнительноКПредставлению	= НазваниеФормы;
	ФормаПечати.Открыть();
КонецПроцедуры

// Формирует печатную форму "Договор"
Процедура ПечатьДоговор() Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Если обЗначениеНеЗаполнено(Владелец) ИЛИ ТипЗнч(Владелец)<>Тип("СправочникСсылка.Автомобили") Тогда
		Сообщить("Не выбран автомобиль-владелец");
		Возврат;
	КонецЕсли;
	
	Макет = ПолучитьМакет("Договор");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Страница1");
	
	//Получим ФИО директора
	СтруктураОтбора=Новый Структура("Организация,Объект",Организация,Перечисления.ВидыОбъектовСведений.Руководитель);
	СтруктураСведений=РегистрыСведений.СведенияКомпании.ПолучитьПоследнее(ДатаВыдачи,СтруктураОтбора);
	Руководитель=СтруктураСведений.Значение;
	ФИОРуководитель = ?(обЗначениеНеЗаполнено(Руководитель),"",Руководитель.Наименование);
	
	ОбластьМакета.Параметры.ОрганизацияНаименование = спПолучитьНаименование(Организация);
	ОбластьМакета.Параметры.Договор = "КУПЛИ-ПРОДАЖИ АВТОМОБИЛЯ от "+ Формат(ДатаВыдачи,"ДФ=dd.MM.yyyy");
	ОбластьМакета.Параметры.Организация = "        " + спПолучитьНаименование(Организация) + ", именуемое в дальнейшем  """"ПРОДАВЕЦ"""" в лице " +
		ФИОРуководитель + ", действующего на основании Устава, с одной стороны и " + спПолучитьНаименование(Контрагент) +
		" (паспорт серии " + ДокументДоверенности.Серия + " № "+ ДокументДоверенности.Номер + " выдан " + ДокументДоверенности.КемВыдан + " " + Формат(ДокументДоверенности.ДатаВыдачи,"ДФ='dd MMMM yyyy'") + ", именуемый в дальнейшем """"ПОКУПАТЕЛЬ"""", заключили настоящий Договор о нижеследующем:";
	ОбластьМакета.Параметры.СтрокаСуммы = "          1.7. оплатил стоимость автомобиля в размере " + Строка(ЭтотОбъект.СуммаДокумента) + " " + ЭтотОбъект.ВалютаДокумента.Наименование;
	ОбластьМакета.Параметры.Автомобиль = "          1.5. продал Покупателю один новый автомобиль, модель " + спПолучитьНаименование(ЭтотОбъект.Владелец.Модель) + " № кузова " + ЭтотОбъект.Владелец.НомерКузова + " № двигателя " + ЭтотОбъект.Владелец.НомерДвигателя + " цвет " + ЭтотОбъект.Владелец.Цвет + " с установленным сроком гарантии 12 месяцев (или 20 тыс. км пробега).";
	ОбластьМакета.Параметры.ФирмаОбязуется = "2." + ЭтотОбъект.Организация.НаименованиеПолное + " ОБЯЗУЕТСЯ:";
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Страница2");
	
	ОбластьМакета.Параметры.ОрганизацияНаименование = спПолучитьНаименование(Организация);
	ОбластьМакета.Параметры.ОрганизацияЮридическийАдрес = киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	ОбластьМакета.Параметры.ОрганизацияИНН = спПолучитьПредставление(Организация,Новый Структура("ИНН","ИНН "));
	ОбластьМакета.Параметры.СчетОрганизации = спПолучитьПредставление(Организация,Новый Структура("БанковскийСчет,Банк,БИК,КоррСчет","р/с ","в банке ","БИК ","к/с "));
	
	ОбластьМакета.Параметры.КонтрагентНаименование = спПолучитьНаименование(Контрагент);
	ОбластьМакета.Параметры.КонтрагентЮридическийАдрес = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	ОбластьМакета.Параметры.КонтрагентИНН = спПолучитьПредставление(Контрагент,Новый Структура("ИНН","ИНН "));
	ОбластьМакета.Параметры.СчетКонтрагента = спПолучитьПредставление(Контрагент,Новый Структура("БанковскийСчет,Банк,БИК,КоррСчет","р/с ","в банке ","БИК ","к/с "));
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабличныйДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	ТабличныйДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабличныйДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",Права);
	ТабличныйДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	ТабличныйДокумент.ПолеСлева				= 9;
	ТабличныйДокумент.ПолеСправа			= 6;
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ДополнительноКПредставлению	= "Договор";
	ФормаПечати.Открыть();
КонецПроцедуры

// Формирует печатную форму "РегистрационныйЗнак"
Процедура ПечатьРегистрационныйЗнак() Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Если обЗначениеНеЗаполнено(Владелец) ИЛИ ТипЗнч(Владелец)<>Тип("СправочникСсылка.Автомобили") Тогда
		Сообщить("Не выбран автомобиль-владелец");
		Возврат;
	КонецЕсли;
	
	Макет = ПолучитьМакет("РегистрационныйЗнак");
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьПечати");
	
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	
	ОбластьМакета.Параметры.Модель = ЭтотОбъект.Владелец.Модель.Наименование;
	ОбластьМакета.Параметры.VIN = ЭтотОбъект.Владелец.VIN;
	ОбластьМакета.Параметры.НомерШасси = ЭтотОбъект.Владелец.НомерШасси;
	ОбластьМакета.Параметры.Цвет = ЭтотОбъект.Владелец.Цвет.Наименование;
	ОбластьМакета.Параметры.Производитель = ЭтотОбъект.Владелец.Модель.Производитель.Наименование;
	ОбластьМакета.Параметры.ДокументНаСобственность="справка-счет";
	
	ОбластьМакета.Параметры.ГодВыпуска = Формат(ЭтотОбъект.Владелец.ГодВыпуска,"ДФ=yyyy");
	ОбластьМакета.Параметры.НомерДвигателя = ЭтотОбъект.Владелец.НомерДвигателя;
	ОбластьМакета.Параметры.НомерКузова = ЭтотОбъект.Владелец.НомерКузова;
	ОбластьМакета.Параметры.КатегорияТС = "С";
	
	ОбластьМакета.Параметры.СерияПТС = ЭтотОбъект.Серия;
	ОбластьМакета.Параметры.НомерПТС = ЭтотОбъект.Номер;
	ВременнаяДата = Формат(ЭтотОбъект.ДатаВыдачи,"ДЛФ=DD");
	ОбластьМакета.Параметры.СтрокаЧислоДатаПТС = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	ОбластьМакета.Параметры.СтрокаМесяцДатаПТС = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	ОбластьМакета.Параметры.СтрокаГодДатаПТС = Сред(ВременнаяДата,1,Найти(ВременнаяДата,"г")-2);
	
	ОбластьМакета.Параметры.Владелец = спПолучитьНаименование(Контрагент);
	ОбластьМакета.Параметры.АдресВладельца = киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
	
	ОбластьМакета.Параметры.НомерДок = НомерБланка;
	ВременнаяДата = Формат(ЭтотОбъект.ДатаВыдачи,"ДЛФ=DD");
	ОбластьМакета.Параметры.СтрокаЧислоДатаДок = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	ОбластьМакета.Параметры.СтрокаМесяцДатаДок = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	ОбластьМакета.Параметры.СтрокаГодДатаДок = Сред(ВременнаяДата,1,Найти(ВременнаяДата,"г")-2);
	ОбластьМакета.Параметры.Организация = спПолучитьНаименование(Организация);
	//ОбластьМакета.Параметры.СвоимХодом = ЭтотОбъект.СвоимХодом;
	
	//ВременнаяДата = Формат(ЭтотОбъект.ДатаВыдачиРегистрационногоЗнака,"ДЛФ=DD");
	ОбластьМакета.Параметры.СтрокаЧислоДатаВыдачи = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	ОбластьМакета.Параметры.СтрокаМесяцДатаВыдачи = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	ОбластьМакета.Параметры.СтрокаГодДатаВыдачи = Сред(ВременнаяДата,1,Найти(ВременнаяДата,"г")-2);
	
	//ВременнаяДата = Формат(ЭтотОбъект.ДействителенДо,"ДЛФ=DD");
	//ОбластьМакета.Параметры.СтрокаЧислоДействительноДо = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	//ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	//ОбластьМакета.Параметры.СтрокаМесяцДействительноДо = Сред(ВременнаяДата,1,Найти(ВременнаяДата," ")-1);
	//ВременнаяДата = Сред(ВременнаяДата,Найти(ВременнаяДата," ")+1,СтрДлина(ВременнаяДата));
	//ОбластьМакета.Параметры.СтрокаГодДействительноДо = Сред(ВременнаяДата,1,Найти(ВременнаяДата,"г")-2);
	
	ТабДокумент.Вывести(ОбластьМакета);

	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабличныйДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	ТабличныйДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабличныйДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",Права);
	ТабличныйДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	ТабличныйДокумент.ПолеСлева				= 9;
	ТабличныйДокумент.ПолеСправа			= 6;
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ДополнительноКПредставлению	= "Регистрационный знак";
	ФормаПечати.Открыть();
КонецПроцедуры

// Формирует печатную форму "ПТС"
Процедура ПечатьПТС(ДокументРеализации=Неопределено) Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Если обЗначениеНеЗаполнено(Владелец) ИЛИ ТипЗнч(Владелец)<>Тип("СправочникСсылка.Автомобили") Тогда
		Сообщить("Не указан автомобиль для печати ПТС!");
		Возврат;
	КонецЕсли;
	
	ДоговорРеализации = Неопределено;
	ДоговорПоступления = Неопределено;
	ДатаРеализации = Неопределено;
	ДатаПоступления = Неопределено;
	КонтрагентВыбораСтраницыПТС = Неопределено;
	ОрганизацияВыбораСтраницыПТС = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Автомобиль",Владелец);
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияАвтомобилей.ДоговорВзаиморасчетов,
	               |	РеализацияАвтомобилей.Дата КАК Дата,
	               |	РеализацияАвтомобилей.Контрагент
	               |ИЗ
	               |	Документ.РеализацияАвтомобилей КАК РеализацияАвтомобилей
	               |ГДЕ
	               |	РеализацияАвтомобилей.Автомобили.Автомобиль = &Автомобиль
	               |	И &ДопУсловие
	               |	И РеализацияАвтомобилей.Проведен = Истина
	               |	И РеализацияАвтомобилей.ПометкаУдаления = Ложь
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата УБЫВ";
	Если ЗначениеЗаполнено(ДокументРеализации) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ДопУсловие","РеализацияАвтомобилей.Ссылка = &ДокументРеализации");	
		Запрос.УстановитьПараметр("ДокументРеализации",ДокументРеализации);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ДопУсловие","1=1");	
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();				   
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		КонтрагентВыбораСтраницыПТС = Выборка.Контрагент;
		ДатаРеализации = Выборка.Дата;
		ДоговорРеализации = Выборка.ДоговорВзаиморасчетов; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Автомобиль",Владелец);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоступлениеАвтомобилей.ДоговорВзаиморасчетов,
	               |	ПоступлениеАвтомобилей.Дата КАК Дата,
	               |	ПоступлениеАвтомобилей.Организация
	               |ИЗ
	               |	Документ.ПоступлениеАвтомобилей КАК ПоступлениеАвтомобилей
	               |ГДЕ
	               |	ПоступлениеАвтомобилей.Автомобили.Автомобиль = &Автомобиль
	               |	И ПоступлениеАвтомобилей.Проведен = Истина
	               |	И ПоступлениеАвтомобилей.ПометкаУдаления = Ложь
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();				   
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		ОрганизацияВыбораСтраницыПТС = Выборка.Организация;
		ДатаПоступления = Выборка.Дата;
		ДоговорПоступления = Выборка.ДоговорВзаиморасчетов; 
	КонецЕсли;
	
	Макет = ПолучитьМакет("ПТС");
	НомерДляПечати=дкПолучитьНомерДляПечати(ЭтотОбъект);
	ДатаДляПечати=ЭтотОбъект.ДатаВыдачи;
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьПечати");
	
	ФормаВыбораСтраницыПТС=ПолучитьФорму("ФормаВыбораСекцииПТС");
	ФормаВыбораСтраницыПТС.Контрагент = КонтрагентВыбораСтраницыПТС;
	ФормаВыбораСтраницыПТС.Организация = ОрганизацияВыбораСтраницыПТС;
	ПараметрыПечати = ФормаВыбораСтраницыПТС.ОткрытьМодально();
	Если ТипЗнч(ПараметрыПечати)<>Тип("Структура") Тогда
		Возврат;
	КонецЕсли; 
	
	МассивСекций = Новый Массив; 
	Если ЗначениеЗаполнено(ПараметрыПечати) И ТипЗнч(ПараметрыПечати)=Тип("Структура") Тогда
		ПараметрыПечати.Свойство("МассивСекций",МассивСекций);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(МассивСекций) ИЛИ МассивСекций.Количество()=0 Тогда
		Сообщить("Не указана секция для печати ПТС!");
		Возврат;
	КонецЕсли; 
	
	Инд = 0;
	
	
	Для Каждого СекцияПТС ИЗ МассивСекций Цикл
		Инд	= Инд + 1;
		Если СекцияПТС.Значение = "СекцияДилера" Тогда
			ОбластьМакета.Параметры["Собственник"+Инд]				= спПолучитьНаименование(Организация);
			ОбластьМакета.Параметры["Адрес"+Инд]					= киПолучитьПредставлениеКИ(Организация,Справочники.ВидыКонтактнойИнформации.АдресЮридический);
			ОбластьМакета.Параметры["ДатаПродажи"+Инд] 				= Формат(ДатаПоступления,"ДФ='dd ММММ yyyy'");
			ОбластьМакета.Параметры["ДокументНаСобственность"+Инд]	= ДоговорПоступления;
		ИначеЕсли СекцияПТС.Значение = "СекцияПокупателя" Тогда
			ОбластьМакета.Параметры["Собственник"+Инд]				= спПолучитьНаименование(Контрагент);
			ОбластьМакета.Параметры["Адрес"+Инд]					= киПолучитьПредставлениеКИ(Контрагент,Справочники.ВидыКонтактнойИнформации.АдресФактический);
			ОбластьМакета.Параметры["ДатаПродажи"+Инд] 				= Формат(ДатаРеализации,"ДФ='dd ММММ yyyy'");
			ОбластьМакета.Параметры["ДокументНаСобственность"+Инд]	= ДоговорРеализации;			
		КонецЕсли;
	КонецЦикла;	
		
	ТабДокумент.Вывести(ОбластьМакета);

	// Отобразить печатный документ на экране
	КлючУникальности	= Новый УникальныйИдентификатор();
	ФормаПечати			= ПолучитьОбщуюФорму("ПечатнаяФормаДокументов",,КлючУникальности);
	ТабличныйДокумент	= ФормаПечати.ЭлементыФормы.ТабличныйДокумент;
	ТабличныйДокумент.ОтображатьЗаголовки	= ЛОЖЬ;
	ТабличныйДокумент.ОтображатьСетку		= ЛОЖЬ;
	ТабличныйДокумент.Защита				= обПраво("ЗащитаПечатныхФорм",Права);
	ТабличныйДокумент.ТолькоПросмотр		= обПраво("ОткрытиеПечатныхФормДокументовВРежимеПросмотра",Права);
	ТабличныйДокумент.ПолеСлева				= 10;
	ТабличныйДокумент.ПолеСправа			= 10;
	ТабличныйДокумент.ПолеСверху			= 5;
	ТабличныйДокумент.ПолеСнизу				= 10;
	ТабличныйДокумент.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт; 	
	ФормаПечати.ЭлементыФормы.ТабличныйДокумент.Вывести(ТабДокумент);
	// Заполним необходимые переменные и откроем форму
	ФормаПечати.Объект						= ЭтотОбъект;
	ФормаПечати.ДополнительноКПредставлению	= "ПТС";
	ФормаПечати.Открыть();
КонецПроцедуры

#КонецЕсли
		
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	Если Основание<>Неопределено И
		 (ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщику") ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров")) И
		 НЕ Основание.Пустая() Тогда
		 Владелец = Основание.Автор.Сотрудник; 
	ИначеЕсли (Основание<>Неопределено) И (НЕ Основание.Пустая()) И
		 (ТипЗнч(Основание)=Тип("ДокументСсылка.РеализацияАвтомобилей")) Тогда
		ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.ПТС;
		Контрагент = Основание.Контрагент;
		Если обЗначениеНеЗаполнено(Владелец) Тогда
			СписокДляВыбора = Основание.Автомобили.Выгрузить();
			АвтомобильЗаполнения=Неопределено;
			Если ДополнительныеСвойства.Свойство("Автомобиль",АвтомобильЗаполнения) Тогда
				СписокДляВыбора.Очистить();
				НоваяСтрока=СписокДляВыбора.Добавить();
				НоваяСтрока.Автомобиль=АвтомобильЗаполнения;
				СтрокаВДокументе=Основание.Автомобили.Найти(АвтомобильЗаполнения,"Автомобиль");
				Если СтрокаВДокументе<>Неопределено Тогда
					Для каждого КолонкаТЧ Из СписокДляВыбора.Колонки Цикл
						НоваяСтрока[КолонкаТЧ.Имя]=СтрокаВДокументе[КолонкаТЧ.Имя];
					КонецЦикла; 
				КонецЕсли; 
			КонецЕсли; 
			Если СписокДляВыбора.Количество() > 0 Тогда
				Если СписокДляВыбора.Количество() = 1 Тогда
					СтрокаВыбранная = СписокДляВыбора.Получить(0);
				Иначе
					#Если Клиент Тогда
						СтрокаВыбранная = СписокДляВыбора.ВыбратьСтроку("Выберите строку документа");
						Если СтрокаВыбранная = Неопределено Тогда
							Возврат;
						КонецЕсли;
					#Иначе
						СтрокаВыбранная = СписокДляВыбора.Получить(0);
					#КонецЕсли
				КонецЕсли;
				Владелец = СтрокаВыбранная.Автомобиль;
				ВалютаДокумента = Основание.ВалютаДокумента;
				СуммаДокумента = СтрокаВыбранная.СуммаВсего;
				ИдентификаторАвтомобиля = СтрокаВыбранная.ИдентификаторАвтомобиля;
				ОборудованиеАвтомобиля = Основание.Товары.НайтиСтроки(Новый Структура("ИдентификаторАвтомобиля",ИдентификаторАвтомобиля));
				Для каждого СтрокаОборудования Из ОборудованиеАвтомобиля Цикл
					СуммаДокумента = СуммаДокумента + СтрокаОборудования.СуммаВсего;
				КонецЦикла;
			КонецЕсли; 
		КонецЕсли; 
	ИначеЕсли (Основание<>Неопределено) И (НЕ Основание.Пустая()) И
		 (ТипЗнч(Основание)=Тип("ДокументСсылка.РеализацияАктивов")) Тогда
		ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.ПТС;
		Контрагент = Основание.Контрагент;
		//найдем автомобиль, соответствующий данному активу, через документ ВводВЭксплуатациюАвтомобилей
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВводВЭксплуатациюАвтомобилейАвтомобили.Автомобиль КАК Автомобиль,
		               |	Док.Количество КАК Количество,
		               |	Док.СтавкаНДС КАК СтавкаНДС,
		               |	Док.СуммаНДС КАК СуммаНДС,
		               |	Док.Сумма КАК Всего
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		РеализацияАктивовАктивы.ПрочийАктив КАК ПрочийАктив,
		               |		РеализацияАктивовАктивы.Количество КАК Количество,
		               |		РеализацияАктивовАктивы.СтавкаНДС КАК СтавкаНДС,
		               |		РеализацияАктивовАктивы.СуммаНДС КАК СуммаНДС,
		               |		РеализацияАктивовАктивы.Сумма КАК Сумма
		               |	ИЗ
		               |		Документ.РеализацияАктивов.Активы КАК РеализацияАктивовАктивы
		               |	ГДЕ
		               |		РеализацияАктивовАктивы.Ссылка = &ДокОснование
		               |		И РеализацияАктивовАктивы.ПрочийАктив.Номенклатура = &Автомобиль) КАК Док
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводВЭксплуатациюАвтомобилей.Автомобили КАК ВводВЭксплуатациюАвтомобилейАвтомобили
		               |		ПО Док.ПрочийАктив = ВводВЭксплуатациюАвтомобилейАвтомобили.Актив";
		Запрос.УстановитьПараметр("ДокОснование",Основание);
		Запрос.УстановитьПараметр("Автомобиль",Справочники.Номенклатура.Автомобиль);
		СписокДляВыбора = Запрос.Выполнить().Выгрузить();
		Если СписокДляВыбора.Количество() = 0 Тогда
			Возврат;
		ИначеЕсли СписокДляВыбора.Количество() = 1 Тогда
			СтрокаВыбранная = СписокДляВыбора.Получить(0);
		Иначе
			#Если Клиент Тогда
				СтрокаВыбранная = СписокДляВыбора.ВыбратьСтроку("Выберите строку документа");
				Если СтрокаВыбранная = Неопределено Тогда
					Возврат;
				КонецЕсли;
			#Иначе
				СтрокаВыбранная = СписокДляВыбора.Получить(0);
			#КонецЕсли
		КонецЕсли;
		Владелец = СтрокаВыбранная.Автомобиль;
		ВалютаДокумента = Основание.ВалютаДокумента;
		СуммаДокумента = СтрокаВыбранная.Всего;
	ИначеЕсли (Основание<>Неопределено) И (НЕ Основание.Пустая()) И
		 (ТипЗнч(Основание)=Тип("СправочникСсылка.Контрагенты")) Тогда
		ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.Доверенность;
		Контрагент = Основание;
	ИначеЕсли (Основание<>Неопределено) И (НЕ Основание.Пустая()) И
		 (ТипЗнч(Основание)=Тип("СправочникСсылка.Автомобили")) Тогда
		ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.ПТС;
		Владелец = Основание;
	КонецЕсли;
	//если неопределен - открыть форму выбора
	Если обЗначениеНеЗаполнено(Владелец) Тогда
		ФормаВыбора = Справочники.Сотрудники.ПолучитьФормуВыбора(,);
		Владелец = ФормаВыбора.ОткрытьМодально();	
	КонецЕсли;	
	Если Основание<>Неопределено И
		 (ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщику") ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров")) И
		 НЕ Основание.Пустая() Тогда
		 Контрагент = Основание.Контрагент; 
	КонецЕсли; 
	Если обЗначениеНеЗаполнено(ВидПодтверждающегоДокумента) Тогда
		ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.Доверенность;
	КонецЕсли; 
	Попытка
		ДатаВыдачи = Основание.Дата;
	Исключение
	КонецПопытки; 
	Если Основание<>Неопределено И
		 (ТипЗнч(Основание)=Тип("ДокументСсылка.ЗаказПоставщику") ИЛИ ТипЗнч(Основание)=Тип("ДокументСсылка.ПоступлениеТоваров")) И
		 НЕ Основание.Пустая() Тогда
		Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
			ИмяТаблицы = "ПоступлениеТоваров";
		ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ИмяТаблицы = "ЗаказПоставщику";
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДокОсн",Основание);
		Запрос.Текст = "ВЫБРАТЬ "
		               + ИмяТаблицы + "Товары.Номенклатура, "
		               + ИмяТаблицы + "Товары.Количество, "
		               + ИмяТаблицы + "Товары.ЕдиницаИзмерения, "
		               + ИмяТаблицы + "Товары.Коэффициент, "
		               + ИмяТаблицы + "Товары.ХарактеристикаНоменклатуры
		               |ИЗ
		               |	Документ." + ИмяТаблицы + ".Товары КАК "+ ИмяТаблицы +"Товары
		               |ГДЕ "
		               + ИмяТаблицы + "Товары.Ссылка = &ДокОсн";
		Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	Если обЗначениеНеЗаполнено(Организация) Тогда
		Организация=ПараметрыСеанса.Организация;
	КонецЕсли; 
	Если обЗначениеНеЗаполнено(ВалютаДокумента) Тогда
		ВалютаДокумента=Константы.ВалютаРегламентированногоУчетаОрганизаций.Получить();
	КонецЕсли;
	
	Если (НЕ обЗначениеНеЗаполнено(Контрагент)) И обЗначениеНеЗаполнено(ДокументДоверенности) Тогда
		//получим паспорт контрагента
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодтверждающиеДокументы.Ссылка КАК ПаспортКонтрагента
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.Владелец = &Контрагент
		|	И ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = &Паспорт
		|	И ПодтверждающиеДокументы.Текущий = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодтверждающиеДокументы.ДатаВыдачи УБЫВ";
		Запрос.УстановитьПараметр("Контрагент",Контрагент);
		Запрос.УстановитьПараметр("Паспорт",Перечисления.ВидыДокументов.Паспорт);
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			ДокументДоверенности = Выборка.ПаспортКонтрагента;
		КонецЕсли;
	КонецЕсли; 
		
	Если (НЕ обЗначениеНеЗаполнено(Организация)) И обЗначениеНеЗаполнено(Лицензия) Тогда
		//получим лицензию
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодтверждающиеДокументы.Ссылка КАК Лицензия
		|ИЗ
		|	Справочник.ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
		|ГДЕ
		|	ПодтверждающиеДокументы.Владелец = &Организация
		|	И ПодтверждающиеДокументы.ВидПодтверждающегоДокумента = &Лицензия
		|	И ПодтверждающиеДокументы.Текущий = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодтверждающиеДокументы.ДатаВыдачи УБЫВ";
		Запрос.УстановитьПараметр("Организация",Организация);
		Запрос.УстановитьПараметр("Лицензия",Перечисления.ВидыДокументов.Лицензия);
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Лицензия = Выборка.Лицензия;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	Номер = Неопределено;
КонецПроцедуры

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если ВидПодтверждающегоДокумента<>Перечисления.ВидыДокументов.Доверенность Тогда
		Если Товары.Количество()>0 Тогда
			Товары.Очистить();
		КонецЕсли; 
	КонецЕсли;
	Если ВидПодтверждающегоДокумента<>Перечисления.ВидыДокументов.Доверенность И
		 ВидПодтверждающегоДокумента<>Перечисления.ВидыДокументов.ПТС Тогда
		 Контрагент=Неопределено;
		 ДокументДоверенности=Неопределено;
	КонецЕсли;
	
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
	
	ТипВладельца = ТипЗнч(Владелец);
	Если ВидПодтверждающегоДокумента = Перечисления.ВидыДокументов.ВидНаЖительствоИностранногоГражданина 
		 И НЕ (ТипВладельца = Тип("СправочникСсылка.Сотрудники") ИЛИ (ТипВладельца = Тип("СправочникСсылка.Контрагенты") И Владелец.ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо)) Тогда
		Отказ = Истина;
		Сообщить("Ввод подтверждающего документа с видом """+ВидПодтверждающегоДокумента+""" предназначен только для сотрудников и контрагентов с типом ""Частное лицо"".");
	КонецЕсли;
	
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
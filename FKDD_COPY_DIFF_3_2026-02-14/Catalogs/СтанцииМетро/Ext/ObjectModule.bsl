// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	
	Если ДляЭлемента Тогда
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

// Процедура загружает элементы справочника из макета обработки "Метрополитен"
Процедура ЗагрузитьСтанцииМетроИзМакета() Экспорт
	Отказ = Ложь;
	спПроверкаПраваДоступа(ЭтотОбъект, Отказ);
	Если Отказ Тогда
		Возврат
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СтанцииМетро.Ссылка,
	               |	СтанцииМетро.Наименование
	               |ИЗ
	               |	Справочник.СтанцииМетро КАК СтанцииМетро
	               |ГДЕ
	               |	СтанцииМетро.ПометкаУдаления = ЛОЖЬ
	               |	И СтанцииМетро.ЭтоГруппа = ИСТИНА";

	ТаблицаГруппВСправочнике = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СтанцииМетро.Наименование,
	               |	СтанцииМетро.Ссылка,
	               |	СтанцииМетро.Родитель
	               |ИЗ
	               |	Справочник.СтанцииМетро КАК СтанцииМетро
	               |ГДЕ
	               |	СтанцииМетро.ПометкаУдаления = ЛОЖЬ
	               |	И СтанцииМетро.ЭтоГруппа = ЛОЖЬ";

	ТаблицаСтанцийВСправочнике = Запрос.Выполнить().Выгрузить();
	
	
	СписокМакетов = Новый СписокЗначений;
	
	Для Каждого МакетСтанций Из Метаданные.Справочники.СтанцииМетро.Макеты Цикл
		СписокМакетов.Добавить(МакетСтанций.Имя, МакетСтанций.Синоним);
	КонецЦикла;
	
	ЗагружаемыйМакет = СписокМакетов.ВыбратьЭлемент("Выберите набор станций для загрузки");
	
	Если ЗагружаемыйМакет = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	#Если Клиент Тогда
	Состояние("Загружаются станции метро из макета в справочник...");
	#КонецЕсли		
	Макет = Справочники.СтанцииМетро.ПолучитьМакет(ЗагружаемыйМакет.Значение);
	Попытка
		ОбластьТаблицаСтанций = Макет.ПолучитьОбласть("ТаблицаСтанций");
					
		#Если Клиент Тогда
//		Отображаем форму индикации
		ФормаИндикатора = ПолучитьОбщуюФорму("Индикация", Справочники.СтанцииМетро.ПолучитьФормуСписка());
		ФормаИндикатора.СтрокаСостояния = "Загрузка справочника станции метро...";
		Сч = (ОбластьТаблицаСтанций.ВысотаТаблицы - 3) * 2;
		ФормаИндикатора.ЭлементыФормы.Индикатор.МаксимальноеЗначение = Сч;
		ФормаИндикатора.ЭлементыФормы.Индикатор.Шаг = Сч / 100;
		ФормаИндикатора.Открыть();
		Сч = 0;
		#КонецЕсли		
	
	//   получим список всех станций
		ТаблицаСтанций = Новый ТаблицаЗначений;
		ТаблицаСтанций.Колонки.Добавить("Код");
		ТаблицаСтанций.Колонки.Добавить("Наименование");
		ТаблицаСтанций.Колонки.Добавить("Ветка");
		ТаблицаСтанций.Колонки.Добавить("Цвет");
		ТаблицаСтанций.Колонки.Добавить("Комментарий");
				
		Для НомерСтрокиОбласти = 3 По ОбластьТаблицаСтанций.ВысотаТаблицы Цикл
			КодСтанции = ОбластьТаблицаСтанций.Область(НомерСтрокиОбласти, 1).Текст;
			
			#Если Клиент Тогда
			Сч = Сч + 1;
			ФормаИндикатора.ЭлементыФормы.Индикатор.Значение = Сч;
			#КонецЕсли		
			
	//		 проверим заполнение текущей строки макета
			Если ПустаяСтрока(КодСтанции) Тогда
				Прервать;
			КонецЕсли;
			СтрокаТаблицыСтанций = ТаблицаСтанций.Добавить();
			СтрокаТаблицыСтанций.Код = КодСтанции;
			СтрокаТаблицыСтанций.Наименование = ОбластьТаблицаСтанций.Область(НомерСтрокиОбласти, 2).Текст;
			СтрокаТаблицыСтанций.Ветка = ОбластьТаблицаСтанций.Область(НомерСтрокиОбласти, 3).Текст;
			СтрокаТаблицыСтанций.Цвет = ОбластьТаблицаСтанций.Область(НомерСтрокиОбласти, 3).ЦветФона;
			СтрокаТаблицыСтанций.Комментарий = ОбластьТаблицаСтанций.Область(НомерСтрокиОбласти, 5).Текст;
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыСтанций Из ТаблицаСтанций Цикл
			СтрокаТаблицыГруппВСправочнике = ТаблицаГруппВСправочнике.Найти(СокрЛП(СтрокаТаблицыСтанций.Ветка), "Наименование");
			
			#Если Клиент Тогда
			Сч = Сч + 1;
			ФормаИндикатора.ЭлементыФормы.Индикатор.Значение = Сч;
			#КонецЕсли		
			
			Если СтрокаТаблицыГруппВСправочнике = Неопределено Тогда
				ТекущийРодитель = Справочники.СтанцииМетро.СоздатьГруппу();
				ТекущийРодитель.УстановитьНовыйКод("");
				ТекущийРодитель.Наименование = СокрЛП(СтрокаТаблицыСтанций.Ветка);
				ТекущийРодитель.ЦветФона = Строка(СтрокаТаблицыСтанций.Цвет.Красный) + "," + Строка(СтрокаТаблицыСтанций.Цвет.Зеленый) + "," + Строка(СтрокаТаблицыСтанций.Цвет.Синий);
				ТекущийРодитель.Записать();
								
				СтрокаТаблицыГруппВСправочнике = ТаблицаГруппВСправочнике.Добавить();
				СтрокаТаблицыГруппВСправочнике.Ссылка = ТекущийРодитель.Ссылка;
				СтрокаТаблицыГруппВСправочнике.Наименование = ТекущийРодитель.Наименование;
			КонецЕсли;
			ТекущийРодитель = СтрокаТаблицыГруппВСправочнике.Ссылка;
			
			СтрокаТаблицыСтанцийВСправочнике = ТаблицаСтанцийВСправочнике.Найти(СокрЛП(СтрокаТаблицыСтанций.Наименование), "Наименование");
			Если СтрокаТаблицыСтанцийВСправочнике = Неопределено Тогда
				ТекущаяСтанция = Справочники.СтанцииМетро.СоздатьЭлемент();
				ТекущаяСтанция.УстановитьНовыйКод("");
				ТекущаяСтанция.Родитель = ТекущийРодитель;
				ТекущаяСтанция.Наименование = СокрЛП(СтрокаТаблицыСтанций.Наименование);
				ТекущаяСтанция.КодВМакете =  СтрокаТаблицыСтанций.Код;
				ТекущаяСтанция.Комментарий =  СтрокаТаблицыСтанций.Комментарий;
				ТекущаяСтанция.Записать();
			Иначе
				Если Не СтрокаТаблицыСтанцийВСправочнике.Родитель = ТекущийРодитель Тогда
					ТекущаяСтанция = СтрокаТаблицыСтанцийВСправочнике.Ссылка.ПолучитьОбъект();
					ТекущаяСтанция.Родитель = ТекущийРодитель;
					ТекущаяСтанция.Записать();
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;	
	Исключение
		#Если Клиент Тогда
		Предупреждение("Не заполнен макет!");
		#КонецЕсли		
	КонецПопытки;	
	#Если Клиент Тогда
	Состояние("");
	Если ФормаИндикатора.Открыта() Тогда ФормаИндикатора.Закрыть(); КонецЕсли;
	#КонецЕсли		
КонецПроцедуры// ЗагрузитьСтанцииМетроИзМакета() Экспорт	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ЗагрузитьСтанцииМетроИзМакета()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда 
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
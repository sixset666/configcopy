// Модуль справочника Настройка АРМ-Отчеты

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// удаляет строку дерев, если такого отчёта больше не существует в конфигурации
// Параметра -
//  Дерево - Дерево значений. Дерево, строки которого проверяются
Процедура УдалитьСтрокуДерева(Дерево)
	Если Дерево.Строки.Количество() <> 0 Тогда
		Индекс = Дерево.Строки.Количество()-1;
		Пока Индекс>=0 Цикл
			СтрокаДерева = Дерево.Строки[Индекс];
			УдалитьСтрокуДерева(СтрокаДерева);
			Если (НЕ СтрокаДерева.Группа) И (НЕ СтрокаДерева.Вариант) И Метаданные.Отчеты.Найти(СтрокаДерева.Отчет) = Неопределено Тогда
				Дерево.Строки.Удалить(СтрокаДерева);
			КонецЕсли;
			Индекс = Индекс-1;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// восстанавливает дерево очетов из ранее сохраненных данных
Функция ВосстановитьДеревоОтчетов() Экспорт
	
	Дерево = Настройка.Получить();
	// сделаем проверку актуальности отчётов
	Если Дерево <> Неопределено Тогда
		
		Если Дерево.Колонки.Найти("Вариант") = Неопределено Тогда
			Дерево.Колонки.Добавить("Вариант", Новый ОписаниеТипов("Булево"));
		КонецЕсли;
		
		Для Каждого СтрокаДерева Из Дерево.Строки Цикл
			УдалитьСтрокуДерева(СтрокаДерева);
		КонецЦикла;
	КонецЕсли;
	Если НЕ ТипЗнч(Дерево) = Тип("ДеревоЗначений") Тогда
		
		//Создаем и заполняем дерево отчетов
		Дерево = Новый ДеревоЗначений;
		Дерево.Колонки.Добавить("Отчет",         Новый ОписаниеТипов("Строка"));
		Дерево.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
		Дерево.Колонки.Добавить("Группа",        Новый ОписаниеТипов("Булево"));
		Дерево.Колонки.Добавить("Вариант",       Новый ОписаниеТипов("Булево"));
		Дерево.Колонки.Добавить("Настройка");
		Дерево.Колонки.Добавить("Использование", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(1,0)));
		
		НоваяСтрокаОтчеты = Дерево.Строки.Добавить();
		НоваяСтрокаОтчеты.Отчет = "Все отчеты";
		НоваяСтрокаОтчеты.Представление = "Все отчеты";
		НоваяСтрокаОтчеты.Группа = Истина;
		НоваяСтрокаОтчеты.Использование = 1;
		
		Для Каждого ТекОтчет Из Метаданные.Отчеты Цикл
			НоваяСтрока = НоваяСтрокаОтчеты.Строки.Добавить();
			НоваяСтрока.Отчет = ТекОтчет.Имя;
			НоваяСтрока.Представление = ТекОтчет.Синоним;
			НоваяСтрока.Использование = 1;
			
			Если НЕ ТекОтчет.ОсновнаяФорма = Неопределено И 
				ТекОтчет.ОсновнаяФорма.ТипФормы = Метаданные.СвойстваОбъектов.ТипФормы.Управляемая Тогда
				
				КлючОтчета = ТекОтчет.ПолноеИмя();
				СписокВариантов = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьСписок(КлючОтчета);
				Для Каждого ТекущийВариант Из СписокВариантов Цикл
					
					ВариантСтрока = НоваяСтрока.Строки.Добавить();
					ВариантСтрока.Отчет         = ТекущийВариант.Значение;
					ВариантСтрока.Представление = ТекущийВариант.Представление;
					ВариантСтрока.Вариант       = Истина;
					ВариантСтрока.Использование = 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		//Обновим список отчетов
		//Новые строки добавляются в список без пометки использования
		СтрокаВсеОтчеты = Дерево.Строки.Найти("Все отчеты","Отчет",Истина);
		СоздаватьСтрокуВсеОтчеты = (СтрокаВсеОтчеты = Неопределено);
		
		Для Каждого ТекОтчет Из Метаданные.Отчеты Цикл
			НайденнаяСтрока = Дерево.Строки.Найти(ТекОтчет.Имя,"Отчет",Истина);
			Если НайденнаяСтрока = Неопределено Тогда
				Если СоздаватьСтрокуВсеОтчеты Тогда
					СтрокаВсеОтчеты = Дерево.Строки.Добавить();
					СтрокаВсеОтчеты.Отчет = "Все отчеты";
					СтрокаВсеОтчеты.Представление = "Все отчеты";
					СтрокаВсеОтчеты.Группа = Истина;
					СтрокаВсеОтчеты.Использование = 0;
					
					СоздаватьСтрокуВсеОтчеты = Ложь;
				КонецЕсли;
				
				НайденнаяСтрока = СтрокаВсеОтчеты.Строки.Добавить();
				НайденнаяСтрока.Отчет = ТекОтчет.Имя;
				НайденнаяСтрока.Представление = ТекОтчет.Синоним;
				НайденнаяСтрока.Использование = 0;
				
				Если (НЕ ТекОтчет.ОсновнаяФорма = Неопределено) И 
					ТекОтчет.ОсновнаяФорма.ТипФормы = Метаданные.СвойстваОбъектов.ТипФормы.Управляемая Тогда
					КлючОтчета = ТекОтчет.ПолноеИмя();
					СписокВариантов = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьСписок(КлючОтчета);
					Для Каждого ТекущийВариант Из СписокВариантов Цикл
						
						ВариантСтрока = НайденнаяСтрока.Строки.Добавить();
						ВариантСтрока.Отчет         = ТекущийВариант.Значение;
						ВариантСтрока.Представление = ТекущийВариант.Представление;
						ВариантСтрока.Вариант       = Истина;
						ВариантСтрока.Использование = 0;
						
					КонецЦикла;
					
				КонецЕсли;
				
			ИначеЕсли (НЕ ТекОтчет.ОсновнаяФорма = Неопределено) И 
				ТекОтчет.ОсновнаяФорма.ТипФормы = Метаданные.СвойстваОбъектов.ТипФормы.Управляемая Тогда
				
				КлючОтчета = ТекОтчет.ПолноеИмя();
				СписокВариантов = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьСписок(КлючОтчета);
				
				// Удалим неактуальные варианты
				КоличествоСтрок = НайденнаяСтрока.Строки.Количество()-1;
				Пока КоличествоСтрок>=0 Цикл
					
					ТекущаяСтрока = НайденнаяСтрока.Строки[КоличествоСтрок];
					СтрокаПоиска = СписокВариантов.НайтиПоЗначению(ТекущаяСтрока.Отчет);
					Если СтрокаПоиска = Неопределено Тогда
						НайденнаяСтрока.Строки.Удалить(ТекущаяСтрока);
					Иначе
						СписокВариантов.Удалить(СтрокаПоиска);
					КонецЕсли;
					
					КоличествоСтрок = КоличествоСтрок - 1;
					
				КонецЦикла;
				
				// Добавим новые варианты
				Для Каждого ТекущийВариант Из СписокВариантов Цикл
					
					ВариантСтрока = НайденнаяСтрока.Строки.Добавить();
					ВариантСтрока.Отчет         = ТекущийВариант.Значение;
					ВариантСтрока.Представление = ТекущийВариант.Представление;
					ВариантСтрока.Вариант       = Истина;
					ВариантСтрока.Использование = 0;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Дерево.Строки.Сортировать("Представление ВОЗР", Истина);
	
	Возврат Дерево;
	
КонецФункции

// ИЗМЕНЕНО Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	ОбязательныеРеквизиты.Вставить("Настройка",1);

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// ИЗМЕНЕНО Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт

	Результат = спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Возврат Результат;
КонецФункции // ПроверитьКорректность()

// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	// Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено, производится
//	программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового объекта на основании
//
// Параметры:
//  Основание - ссылка на объект основания для данного справочника
//
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	// зарегистрируем изменение товара в оборудовании
	уоУстановитьМоментИзменения(Ссылка,ТекущаяДата());
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
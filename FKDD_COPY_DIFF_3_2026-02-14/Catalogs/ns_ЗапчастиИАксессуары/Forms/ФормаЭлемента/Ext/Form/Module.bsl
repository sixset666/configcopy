//Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьПроверкуЗаполненияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ns_ЗапчастиИАксессуарыФормы.УстановитьВидимостьЭлементовШиныДиски(Элементы, Объект.TypeId);
	ns_ЗапчастиИАксессуарыФормы.УстановитьТипыДляМножественныхПолей(Объект);
КонецПроцедуры
 
&НаКлиенте
Процедура TypeIdПриИзменении(Элемент)
	ОчиститьЭлементыПриСменеTypeId();
	ОбновитьРеквизиты();
	УстановитьПроверкуЗаполненияНаСервере();
	ns_ЗапчастиИАксессуарыФормы.УстановитьВидимостьЭлементовШиныДиски(Элементы, Объект.TypeId);
	ns_ЗапчастиИАксессуарыФормы.УстановитьТипыДляМножественныхПолей(Объект);
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура BrandОчистка(Элемент, СтандартнаяОбработка)
	Объект.Brand = Неопределено;
	ns_ЗапчастиИАксессуарыФормы.УстановитьТипыДляМножественныхПолей(Объект);
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьTitle(Команда)
	Объект.Title = СформироватьTitleНаСервере(Объект.ШаблонЗаголовка, Объект.Title, Объект.Номенклатура, Объект.Характеристика);
КонецПроцедуры

//КонецОбласти

//Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПроверкуЗаполненияНаСервере()
	ОбязательныеПоля = ns_ЗапчастиИАксессуарыВалидация.ПолучитьОбязательныеПоля();
	Для каждого ИмяПоля Из ОбязательныеПоля Цикл
		Элементы[ИмяПоля].АвтоОтметкаНезаполненного = Истина;
	КонецЦикла; 
	
	ДополнительныеОбязательныеПоля = ns_ЗапчастиИАксессуарыВалидация.ПолучитьДополнительныеОбязательныеПоля(Объект.TypeId);
	Для каждого ИмяПоля Из ДополнительныеОбязательныеПоля Цикл
		Элементы[ИмяПоля].АвтоОтметкаНезаполненного = Истина;
	КонецЦикла; 
КонецПроцедуры

&НаСервере
Процедура ОбновитьРеквизиты()
	Для каждого Реквизит Из Метаданные.Справочники.ns_ЗапчастиИАксессуары.Реквизиты Цикл
		Если НЕ Элементы.Найти(Реквизит.Имя) = Неопределено Тогда
			Если Реквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку Тогда
				Элементы[Реквизит.Имя].АвтоОтметкаНезаполненного = Истина;
			КонецЕсли;
			Элементы[Реквизит.Имя].Видимость = Истина;	
		КонецЕсли; 
	КонецЦикла; 
	Объект.Brand = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЭлементыПриСменеTypeId()
	МассивПолей = ns_ЗапчастиИАксессуарыВалидация.ПолучитьПоляШиныДиски();
	Для каждого Поле Из МассивПолей Цикл
		Объект[Поле] = Неопределено;
	КонецЦикла; 
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьTitleНаСервере(ШаблонЗаголовка, Заголовок, Номенклатура, Характеристика)
	Возврат ns_ШаблонизаторСервер.СформироватьTitle(ШаблонЗаголовка, Заголовок, Номенклатура, Характеристика);
КонецФункции


//КонецОбласти 
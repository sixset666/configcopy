
//Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьРеквизитовЗаполнения();
	ДатаОстатков = ТекущаяДата();
	ОбновитьСписокНаСервере();   
	
	ns_ОбщегоНазначенияФормы.УстановитьТипСклад(Элементы);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ns_ОбщегоНазначенияФормы.ОбработкаВыбора(Элементы, ВыбранноеЗначение, ИсточникВыбора, "ns_ЗапчастиИАксессуары"); 
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаОстатковПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	ОбновитьСписок();
КонецПроцедуры

//КонецОбласти

//Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сбросить(Команда)
	СброситьРеквизитыЗаполнения(Истина);
	УстановитьВидимостьРеквизитовЗаполнения();
КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)
	Если ЗначениеЗаполнено(TypeId) Тогда
		ТекстВопроса = "При изменении TypeId, у всех выделеных элементов сотрутся данные по шинам и дискам. Продолжить?";
	Иначе
		ТекстВопроса = "При изменении реквизитов, у всех выделеных элементов сотрутся ранее установленные данные. Продолжить?";
	КонецЕсли; 
	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьЗавершение", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60); 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
		УстановитьНаСервере(ВыделенныеСтроки);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СнятьБезОстатковСВыгрузки(Команда)
	СнятьБезОстатковСВыгрузкиНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры
 
//КонецОбласти

//Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура TypeId1ПриИзменении(Элемент)
	СброситьРеквизитыЗаполнения();
	УстановитьВидимостьРеквизитовЗаполнения();
КонецПроцедуры

&НаКлиенте
Процедура СброситьРеквизитыЗаполнения(ОчищатьTypeId = Ложь)
	Если ОчищатьTypeId Тогда
		TypeId = Неопределено;
	КонецЕсли; 
	RimBolts = Неопределено;
	RimBoltsDiameter = Неопределено;
	RimDiameter = Неопределено;
	RimOffset = Неопределено;
	RimType = Неопределено;
	RimWidth = Неопределено;
	TireAspectRatio = Неопределено;
	TireSectionWidth = Неопределено;
	TireType = Неопределено;
	WheelAxle = Неопределено;
	СопоставлениеРеквизитов = Неопределено;
	ConditionЗапчасти = Неопределено;
	AdType = Неопределено;
	ШаблонЗаголовка = Неопределено;
	ШаблонОбъявления = Неопределено; 
	Производитель = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьРеквизитовЗаполнения()
	Если НЕ ЗначениеЗаполнено(TypeId) Тогда
		Элементы.ГруппаЗаполнениеШиныДиски.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаЗаполнениеШиныДиски.Видимость = ns_ЗапчастиИАксессуарыВалидация.ЭтоПодкатегорияШиныДиски(TypeId);
	Если Элементы.ГруппаЗаполнениеШиныДиски.Видимость = Ложь Тогда
		Возврат;
	КонецЕсли; 
	
	ВсеПоляШиныДиски = ns_ЗапчастиИАксессуарыВалидация.ПолучитьПоляШиныДиски();
	ДополнительныеОбязательныеПоля = ns_ЗапчастиИАксессуарыВалидация.ПолучитьДополнительныеОбязательныеПоля(TypeId);
	
	Для каждого ПолеФормы Из ВсеПоляШиныДиски Цикл
		Попытка
			Если ДополнительныеОбязательныеПоля.Найти(ПолеФормы) = Неопределено Тогда
				Элементы["Заполнение" + ПолеФормы].Видимость = Ложь;	
			Иначе	
				Элементы["Заполнение" + ПолеФормы].Видимость = Истина;
			КонецЕсли; 
		Исключение
		КонецПопытки; 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНаСервере(ВыделенныеСтроки)
	Попытка
		ЗначенияСопоставленныхРеквизитов = Неопределено;
		Если ЗначениеЗаполнено(СопоставлениеРеквизитов) Тогда
			МассивСтрок = ОбработатьВыделенныеСтроки(ВыделенныеСтроки);
			ЗначенияСопоставленныхРеквизитов = ns_Переопределяемый.ПолучитьЗначенияСопоставленныхРеквизитов(МассивСтрок);
		КонецЕсли; 
		
		Для каждого СтрокаСписка Из ВыделенныеСтроки Цикл
			Если СтрокаСписка.ЭтоГруппа Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЗапчастиИАксессуары.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.ns_ЗапчастиИАксессуары КАК ЗапчастиИАксессуары
				|ГДЕ
				|	ЗапчастиИАксессуары.Родитель В ИЕРАРХИИ(&Родитель)
				|	И ЗапчастиИАксессуары.ЭтоГруппа = ЛОЖЬ";
				Запрос.УстановитьПараметр("Родитель", СтрокаСписка.Ссылка);
				РезультатЗапроса = Запрос.Выполнить();
				
				Если РезультатЗапроса.Пустой() Тогда
					Возврат;
				КонецЕсли;  
				
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					УстановитьЗначениеЭлементу(Выборка.Ссылка, ЗначенияСопоставленныхРеквизитов);
				КонецЦикла; 
			Иначе	
				УстановитьЗначениеЭлементу(СтрокаСписка, ЗначенияСопоставленныхРеквизитов);
			КонецЕсли;
		КонецЦикла;  
	Исключение
	    ns_ОбщегоНазначения.СообщениеПользователю(ОписаниеОшибки());
	КонецПопытки; 
	 
КонецПроцедуры

&НаСервере
Функция ОбработатьВыделенныеСтроки(ВыделенныеСтроки)
	МассивСтрок = Новый Массив;
	Для каждого Строка Из ВыделенныеСтроки Цикл
		Если Строка.ЭтоГруппа Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ns_ЗапчастиИАксессуары.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ns_ЗапчастиИАксессуары КАК ns_ЗапчастиИАксессуары
			|ГДЕ
			|	ns_ЗапчастиИАксессуары.Родитель = &Родитель";
			Запрос.УстановитьПараметр("Родитель", Строка.Ссылка);
			РезультатЗапроса = Запрос.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					МассивСтрок.Добавить(Выборка.Ссылка);
				КонецЦикла;
			КонецЕсли; 
		Иначе
			МассивСтрок.Добавить(Строка);
		КонецЕсли; 	
	КонецЦикла;
	
	Возврат МассивСтрок;
КонецФункции

&НаСервере
Процедура УстановитьЗначениеЭлементу(Ссылка, ЗначенияСопоставленныхРеквизитов)
	
	Изменен = Ложь;
	ТекущийОбъект = Ссылка.ПолучитьОбъект();
	
	Если ЗначениеЗаполнено(TypeId) И (НЕ ТекущийОбъект.TypeId = TypeId) Тогда
		ТекущийОбъект.TypeId = TypeId;
		МассивПолейШиныДиски = ns_ЗапчастиИАксессуарыВалидация.ПолучитьПоляШиныДиски();
		Для каждого Поле Из МассивПолейШиныДиски Цикл
			ТекущийОбъект[Поле] = Неопределено;
		КонецЦикла; 
		Изменен = Истина;
	КонецЕсли;
	
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Condition", ConditionЗапчасти);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "AdType", AdType);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "RimBolts", RimBolts);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "RimBoltsDiameter", RimBoltsDiameter);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "RimDiameter", RimDiameter);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "RimOffset", RimOffset);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "RimType", RimType);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "RimWidth", RimWidth);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "TireAspectRatio", TireAspectRatio);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "TireSectionWidth", TireSectionWidth);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "TireType", TireType);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "WheelAxle", WheelAxle);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Шаблон", ШаблонОбъявления);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Brand", Производитель);
	
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "SpeedIndex", SpeedIndex);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "LoadIndex", LoadIndex);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "ResidualTread", ResidualTread);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "RunFlat", RunFlat);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "TiresModels", TiresModels);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Homologation", Homologation);
	ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Quantity", Quantity);
	
	ns_ОбщегоНазначенияВызовСервера.ЗаполнитьСопоставлениеРеквизитов(Изменен, ТекущийОбъект, ЗначенияСопоставленныхРеквизитов, СопоставлениеРеквизитов);
	
	Если ЗначениеЗаполнено(ШаблонЗаголовка) Тогда
		НовыйЗаголовок = ns_ШаблонизаторСервер.СформироватьTitle(ШаблонЗаголовка, ТекущийОбъект.Title, ТекущийОбъект.Номенклатура, ТекущийОбъект.Характеристика);
		ns_ОбщегоНазначенияВызовСервера.УстановитьНовоеЗначениеРеквизита(Изменен, ТекущийОбъект, "Title", НовыйЗаголовок);
	КонецЕсли;
	

	Если Изменен Тогда
		ТекущийОбъект.Записать();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Истина, "ns_ЗапчастиИАксессуары");
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыгружать(Команда)
	ns_ОбщегоНазначенияФормы.УстановитьВыгружать(Элементы, Ложь, "ns_ЗапчастиИАксессуары");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТолькоВыгружаемые(Команда)
	ns_ОбщегоНазначенияФормы.ПоказатьТолькоВыгружаемые(Элементы, Список);
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()

	Список.ТекстЗапроса = ns_Переопределяемый.ПолучитьТекстЗапроса_ЗапчастиИАксессуары(Склад);
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Склад", Склад);
	КонецЕсли; 
	
	Список.Параметры.УстановитьЗначениеПараметра("НаДату", КонецДня(ДатаОстатков));
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
	Иначе
		СсылкаНаСтроку = ТекущиеДанные.Ссылка;
		ОбновитьСписокНаСервере();
		Элементы.Список.Обновить();
		Элементы.Список.ТекущаяСтрока = СсылкаНаСтроку;
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СнятьБезОстатковСВыгрузкиНаСервере()
	ns_АвитоВалидация.СнятьБезОстатковСВыгрузки("ns_ЗапчастиИАксессуары");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	ns_ОбщегоНазначенияВызовСервера.СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
КонецПроцедуры

//КонецОбласти 













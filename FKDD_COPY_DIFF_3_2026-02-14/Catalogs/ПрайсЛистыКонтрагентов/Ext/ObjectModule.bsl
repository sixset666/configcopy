// Модуль справочника ПрайсЛистыКонтрагентов

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем КодВебПрайсЛиста Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

Функция ПроверитьСтруктуруСтраницПрайсЛиста(ОписаниеИзменений) Экспорт
	
	ОписаниеИзменений = Новый Массив;
	ЕстьИзменения = Ложь;
	
	// Сначала установим имя таблицы, если у нас текстовый файл!
	// Найдем первую строку с ИмяЛиста = "Текст" или с пустым ИмяЛиста
	
	Если ФайлИсточникДанных Тогда
		
		РасширениеФайла = ВРег(Сред(СтрокаПодключения, НайтиСимволСКонца(СтрокаПодключения, ".")+1));
		
		Если РасширениеФайла = "TXT" ИЛИ РасширениеФайла = "CSV" Тогда
			Если ИмяТаблицы <> "Текст" Тогда
				ОписаниеИзменений.Добавить("Установлено Имя таблицы = ""Текст""");
				ЕстьИзменения = Истина;
				ИмяТаблицы = "Текст";
			КонецЕсли;
			Если СтруктураСтраницПрайсЛиста.Количество() > 1 Тогда
				ОписаниеИзменений.Добавить("В структуре страниц удалены лишние строки");
				ЕстьИзменения = Истина;
				Пока СтруктураСтраницПрайсЛиста.Количество() > 1 Цикл
					СтруктураСтраницПрайсЛиста.Удалить(1);
				КонецЦикла;
			КонецЕсли;
			Если СтруктураСтраницПрайсЛиста.Количество() = 1 И СтруктураСтраницПрайсЛиста[0].ИмяЛиста <> ИмяТаблицы Тогда
				ОписаниеИзменений.Добавить("Установлен реквизит в строке Имя листа = ""Текст""");
				ЕстьИзменения = Истина;
				СтруктураСтраницПрайсЛиста[0].ИмяЛиста = ИмяТаблицы;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИмяТаблицы) И СтруктураСтраницПрайсЛиста.Найти(ИмяТаблицы, "ИмяЛиста") = Неопределено Тогда
		ЕстьИзменения = Истина;
		СтрокаИмяТаблицы = СтруктураСтраницПрайсЛиста.Найти("", "ИмяЛиста");
		Если СтрокаИмяТаблицы <> Неопределено Тогда
			ОписаниеИзменений.Добавить("Установлен реквизит в строке Имя листа = """+ИмяТаблицы+"""");
			СтрокаИмяТаблицы.ИмяЛиста = ИмяТаблицы;
			СтрокаИмяТаблицы.Использовать = Истина;
		Иначе
			ОписаниеИзменений.Добавить("В структуру страниц добавлена новая строка");
			НоваяСтрока = СтруктураСтраницПрайсЛиста.Добавить();
			НоваяСтрока.ИмяЛиста = ИмяТаблицы;
			НоваяСтрока.Использовать = Истина;
			НоваяСтрока.СтрокаЗаголовковКолонок = ?(ФайлИсточникДанных, 1, 0);
			НоваяСтрока.СтрокаНачало = НоваяСтрока.СтрокаЗаголовковКолонок + 1;
			НоваяСтрока.СтрокаКонец = 0;
		КонецЕсли;
	КонецЕсли;
	
	ПустыеСтроки = СтруктураСтраницПрайсЛиста.НайтиСтроки(Новый Структура("ИмяЛиста", ""));
	Если ПустыеСтроки.Количество() > 0 Тогда
		ОписаниеИзменений.Добавить("Из структуры страниц удалены строки с пустым Именем листа");
		ЕстьИзменения = Истина;
		Для Каждого ПустаяСтрока Из ПустыеСтроки Цикл
			СтруктураСтраницПрайсЛиста.Удалить(ПустаяСтрока);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураСтраницПрайсЛиста.Количество() > 0 Тогда
		
		СтрокаСЗаголовками = СтруктураСтраницПрайсЛиста.Найти(ИмяТаблицы, "ИмяЛиста");
		СтрокаИспользовать = СтруктураСтраницПрайсЛиста.Найти(Истина, "Использовать");
		
		Для Каждого СтрокаСтруктуры Из СтруктураСтраницПрайсЛиста Цикл
			Если СтрокаСтруктуры.Использовать И СтрокаСтруктуры.СтрокаЗаголовковКолонок > 0 И СтрокаСЗаголовками = Неопределено Тогда
				СтрокаСЗаголовками = СтрокаСтруктуры;
			КонецЕсли;
			Если СтрокаСЗаголовками <> Неопределено И СтрокаСтруктуры <> СтрокаСЗаголовками И СтрокаСтруктуры.СтрокаЗаголовковКолонок > 0 Тогда
				ОписаниеИзменений.Добавить("В строке Имя листа """ + СтрокаСтруктуры.ИмяЛиста + """ очищен номер строки с заголовками");
				ЕстьИзменения = Истина;
				СтрокаСтруктуры.СтрокаЗаголовковКолонок = 0;
			КонецЕсли;
			Если СтрокаСтруктуры.СтрокаНачало > 0 И СтрокаСтруктуры.СтрокаНачало < СтрокаСтруктуры.СтрокаЗаголовковКолонок Тогда
				ОписаниеИзменений.Добавить("В строке Имя листа """ + СтрокаСтруктуры.ИмяЛиста + """ исправлен номер строки начала данных");
				ЕстьИзменения = Истина;
				СтрокаСтруктуры.СтрокаНачало = СтрокаСтруктуры.СтрокаЗаголовковКолонок + 1;
			КонецЕсли;
			Если СтрокаСтруктуры.СтрокаКонец > 0 И СтрокаСтруктуры.СтрокаКонец < СтрокаСтруктуры.СтрокаНачало Тогда
				ОписаниеИзменений.Добавить("В строке Имя листа """ + СтрокаСтруктуры.ИмяЛиста + """ исправлен номер строки конца данных");
				ЕстьИзменения = Истина;
				СтрокаСтруктуры.СтрокаКонец = СтрокаСтруктуры.СтрокаНачало;
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокаСЗаголовками = Неопределено Тогда
			Если СтрокаИспользовать <> Неопределено Тогда
				СтрокаСЗаголовками = СтрокаИспользовать;
			Иначе
				СтрокаСЗаголовками = СтруктураСтраницПрайсЛиста[0];
			КонецЕсли;
		КонецЕсли;
		
		Если Не СтрокаСЗаголовками.Использовать Тогда
			ОписаниеИзменений.Добавить("В строке Имя листа """ + СтрокаСЗаголовками.ИмяЛиста + """ установлен признак использования при загрузке");
			ЕстьИзменения = Истина;
			СтрокаСЗаголовками.Использовать = Истина;
		КонецЕсли;
		
		Если ИмяТаблицы <> СтрокаСЗаголовками.ИмяЛиста Тогда
			ОписаниеИзменений.Добавить("Установлено Имя таблицы = """+СтрокаСЗаголовками.ИмяЛиста+"""");
			ЕстьИзменения = Истина;
			ИмяТаблицы = СтрокаСЗаголовками.ИмяЛиста;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтрокаСЗаголовками = Неопределено И ЗаголовкиКолонокВПрайсЛисте.Количество() > 0 Тогда
		ОписаниеИзменений.Добавить("Не найдена страница с заголовкам. Очищена таблица ""Заголовки колонок""");
		ЕстьИзменения = Истина;
		ЗаголовкиКолонокВПрайсЛисте.Очистить();
	КонецЕсли;
	
	СтрокиНаименования = СтруктураФайлаПрайсЛиста.НайтиСтроки(Новый Структура("ИмяРеквизитаПрайсЛиста", "Наименование"));
	Если СтрокиНаименования.Количество() > 1 Тогда
		ЕстьРазделитель = Ложь;
		Для Каждого СтрокаНаименования Из СтрокиНаименования Цикл
			Если Найти(СтрокаНаименования.ИмяПоляФайла, """") > 0 Тогда
				ЕстьРазделитель = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не ЕстьРазделитель Тогда
			ОписаниеИзменений.Добавить("В настройках соответствий колонок для реквизита ""Наименование"" добавлены разделители полей");
			ЕстьИзменения = Истина;
			Для Сч = 1 По СтрокиНаименования.Количество() - 1 Цикл
				НоваяСтрока = СтруктураФайлаПрайсЛиста.Вставить(СтруктураФайлаПрайсЛиста.Индекс(СтрокиНаименования[Сч-1])+1);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиНаименования[Сч-1]);
				НоваяСтрока.ИмяПоляФайла = """"+" "+"""";
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	СтрокиНаименования = СтруктураФайлаПрайсЛиста.НайтиСтроки(Новый Структура("ИмяРеквизитаПрайсЛиста", "НаименованиеИностранное"));
	Если СтрокиНаименования.Количество() > 1 Тогда
		ЕстьРазделитель = Ложь;
		Для Каждого СтрокаНаименования Из СтрокиНаименования Цикл
			Если Найти(СтрокаНаименования.ИмяПоляФайла, """") > 0 Тогда
				ЕстьРазделитель = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не ЕстьРазделитель Тогда
			ОписаниеИзменений.Добавить("Добавлены разделители полей для реквизита ""Иностранное наименование"" в настройке соответствий колонок файла");
			ЕстьИзменения = Истина;
			Для Сч = 1 По СтрокиНаименования.Количество() - 1 Цикл
				НоваяСтрока = СтруктураФайлаПрайсЛиста.Вставить(СтруктураФайлаПрайсЛиста.Индекс(СтрокиНаименования[Сч-1])+1);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиНаименования[Сч-1]);
				НоваяСтрока.ИмяПоляФайла = """"+" "+"""";
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Не ЕстьИзменения;
КонецФункции

//Получает HTTP ответ 
Функция ПолучитьHTTPОтвет(ПутьКФайлуНаСервере, ПутьДляСохранения, Соединение, Заголовки)
	
	HTTPЗапрос = Новый HTTPЗапрос(ПутьКФайлуНаСервере, Заголовки);
	HTTPЗапрос.Заголовки.Вставить("Accept-Charset", "utf-8");
	HTTPОтвет = Соединение.Получить(HTTPЗапрос, ПутьДляСохранения);
	
	Если HTTPОтвет.КодСостояния = 301 Тогда
		// Перемещено навсегда
		ПутьКФайлуНаСервере = HTTPОтвет.Заголовки["Location"];
		HTTPОтвет = ПолучитьHTTPОтвет(ПутьКФайлуНаСервере, ПутьДляСохранения, Соединение, Заголовки);
	КонецЕсли;
	
	Возврат HTTPОтвет;
	
КонецФункции //ПолучитьHTTPОтвет()

//Проверяет подключение к ресурсам при автообновлении
Функция ПроверитьТранспорт(стрОшибки = "") Экспорт
	
	СценарийАвтозагрузки = Справочники.ПрайсЛистыКонтрагентов.ПолучитьСценарийАвтозагрузки(Ссылка);
	
	Если НЕ СценарийАвтозагрузки.ПрайсИзИнтернета ИЛИ СценарийАвтозагрузки.СохранятьВКаталог Тогда
		ЛокКаталог = СценарийАвтозагрузки.КаталогФайлаПрайсЛиста;
	Иначе
		ЛокКаталог = рфПолучитьКаталогФайла(ПолучитьИмяВременногоФайла());
	КонецЕсли;
	
	Если ПустаяСтрока(ЛокКаталог) Тогда
		стрОшибки = "Не указан каталог хранения файлов прайс-листа!";
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ СценарийАвтозагрузки.ПрайсИзИнтернета Тогда
		// Транспорта нет!
		// проверили только запись в лок каталог
		
		ИмяТестовогоФайла = "test_" + Формат(ТекущаяДата(), "ДФ = ггггММдд") + ".txt";
		
		ПолноеИмяТестовогоФайла = ЛокКаталог + "\" + ИмяТестовогоФайла;
		
		ТекстДок = Новый ТекстовыйДокумент;
		Попытка
			ТекстДок.Записать(ПолноеИмяТестовогоФайла);
			стрОшибки = стрОшибки + "Каталог доступен" + Символы.ПС;
		Исключение
			стрОшибки = стрОшибки + "Ошибка при создании тестового файла
			|" + ИнформацияОбОшибке().Описание + Символы.ПС;
			Возврат Ложь;
		КонецПопытки;
		
		Попытка
			УдалитьФайлы(ПолноеИмяТестовогоФайла);
		Исключение
			стрОшибки = стрОшибки + "Ошибка при удалении тестового файла:
				|" + ИнформацияОбОшибке().Описание + Символы.ПС;
			Возврат Ложь;
		КонецПопытки;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если СценарийАвтозагрузки.флФайлВАрхиве Тогда
		МаскаФайлов = ?(ПустаяСтрока(СценарийАвтозагрузки.МаскаАрхива), "*", СценарийАвтозагрузки.МаскаАрхива) + СценарийАвтозагрузки.ФорматМаскиАрхива;
	Иначе 
		МаскаФайлов = ?(ПустаяСтрока(СценарийАвтозагрузки.МаскаФайлаПрайсЛиста), "*", СценарийАвтозагрузки.МаскаФайлаПрайсЛиста) + СценарийАвтозагрузки.ФорматМаскиФайла;
	КонецЕсли;
	
	Если СценарийАвтозагрузки.ТранспортПротокол = "IMAP" ИЛИ СценарийАвтозагрузки.ТранспортПротокол = "POP3" Тогда
		СтрокаПоиска = СокрЛП(СценарийАвтозагрузки.EmailАдресОтправителя + СценарийАвтозагрузки.EmailТекстПисьма + СценарийАвтозагрузки.EmailТемаПисьма);
		Если ПустаяСтрока(СтрокаПоиска) Тогда
			стрОшибки = "Не указана строка для фильтрации писем!";
			Возврат Ложь;
		КонецЕсли;
		
		Состояние_("Подключение к почтовому серверу ...");
		
		Почта = Новый ИнтернетПочта(); 
		ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
		
		EmailПротокол = "POP3";
		Если СценарийАвтозагрузки.Свойство("ТранспортПротокол") И СценарийАвтозагрузки.ТранспортПротокол = "IMAP" Тогда
			EmailПротокол = "IMAP";
		КонецЕсли;
		
		Если EmailПротокол = "IMAP" Тогда
			ПочтовыйПрофиль.АдресСервераIMAP = СценарийАвтозагрузки.ТранспортСервер;
			ПочтовыйПрофиль.ПортIMAP         = СценарийАвтозагрузки.ТранспортПорт;
			ПочтовыйПрофиль.ПользовательIMAP = СценарийАвтозагрузки.ТранспортЛогин;
			ПочтовыйПрофиль.ПарольIMAP       = СценарийАвтозагрузки.ТранспортПароль;
			ПочтовыйПрофиль.ИспользоватьSSLIMAP = СценарийАвтозагрузки.SSL;
		Иначе
			ПочтовыйПрофиль.АдресСервераPOP3 = СценарийАвтозагрузки.ТранспортСервер;
			ПочтовыйПрофиль.ПортPOP3         = СценарийАвтозагрузки.ТранспортПорт;
			ПочтовыйПрофиль.Пользователь     = СценарийАвтозагрузки.ТранспортЛогин;
			ПочтовыйПрофиль.Пароль           = СценарийАвтозагрузки.ТранспортПароль;
			ПочтовыйПрофиль.ИспользоватьSSLPOP3 = СценарийАвтозагрузки.SSL;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СценарийАвтозагрузки.Таймаут) Тогда
			ПочтовыйПрофиль.Таймаут = СценарийАвтозагрузки.Таймаут;
		Иначе
			ПочтовыйПрофиль.Таймаут = 30;
		КонецЕсли;
		
		Попытка
			Почта.Подключиться(ПочтовыйПрофиль, ПротоколИнтернетПочты[EmailПротокол]);
			
			Если EmailПротокол = "IMAP" Тогда
				// Для POP3 не имеет смысла получать письма, там не работаю отборы при получении
				Если СценарийАвтозагрузки.Свойство("ПочтовыйЯщик") И ЗначениеЗаполнено(СценарийАвтозагрузки.ПочтовыйЯщик) Тогда
					Попытка
						Почта.ТекущийПочтовыйЯщик = СценарийАвтозагрузки.ПочтовыйЯщик;
					Исключение
						Почта.Отключиться();
						стрОшибки = "Ошибка при выборе папки "+СценарийАвтозагрузки.ПочтовыйЯщик+" в почтовом ящике!";
						Возврат Ложь;
					КонецПопытки;
				КонецЕсли;
				СтруктураЗаголовков = Новый Структура;
				СтруктураЗаголовков.Вставить("AfterDateOfPosting", НачалоМесяца(ТекущаяДата()));
				
				//Если ЗначениеЗаполнено(СценарийАвтозагрузки.EmailАдресОтправителя) Тогда
				//	СтруктураЗаголовков.Вставить("From", СценарийАвтозагрузки.EmailАдресОтправителя);
				//КонецЕсли;
				//Если ЗначениеЗаполнено(СценарийАвтозагрузки.EmailТемаПисьма) Тогда
				//	СтруктураЗаголовков.Вставить("Subject", СценарийАвтозагрузки.EmailТемаПисьма);
				//КонецЕсли;
				//Если ЗначениеЗаполнено(СценарийАвтозагрузки.EmailТекстПисьма) Тогда
				//	СтруктураЗаголовков.Вставить("Body", СценарийАвтозагрузки.EmailТекстПисьма);
				//КонецЕсли;
				// Тест получения заголовков
				Заголовки = Почта.ПолучитьЗаголовки(СтруктураЗаголовков);
			КонецЕсли;
			
			Почта.Отключиться();
			стрОшибки = "Сервер: " + EmailПротокол + ": " + СценарийАвтозагрузки.ТранспортСервер + " доступен";
		Исключение
			стрОшибки = "Ошибка при подключении к серверу: " + EmailПротокол + ": " + СценарийАвтозагрузки.ТранспортСервер + "
				|" + ИнформацияОбОшибке().Описание;
			Возврат Ложь;
		КонецПопытки;
		
	ИначеЕсли СценарийАвтозагрузки.ТранспортПротокол = "FTP" ИЛИ СценарийАвтозагрузки.ТранспортПротокол = "FTPS" Тогда // Обрабатываем протоколы FTP и FTPS
		
		Состояние_("Подключение к FTP серверу ...");
		ЗащищенноеСоединение = Неопределено;
		Если СценарийАвтозагрузки.ТранспортПротокол = "FTPS" Тогда
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
		КонецЕсли;
		
		//Определим параметры FTP соединения
		Сервер = СценарийАвтозагрузки.ТранспортСервер;
		Порт = СценарийАвтозагрузки.ТранспортПорт;
		Если СценарийАвтозагрузки.ТребуетсяАвторизация Тогда
			Логин = СценарийАвтозагрузки.ТранспортЛогин;
			Пароль = СценарийАвтозагрузки.ТранспортПароль;
		Иначе
			Логин = "";
			Пароль = "";
		КонецЕсли;
		
		Прокси = Новый ИнтернетПрокси;
		
		ПассивноеСоединение  = СценарийАвтозагрузки.FtpПассивныйРежим;
		Если ЗначениеЗаполнено(СценарийАвтозагрузки.Таймаут) Тогда
			Таймаут = СценарийАвтозагрузки.Таймаут;
		Иначе
			Таймаут = 30;
		КонецЕсли;
		
		ЗащищенноеСоединение = Неопределено;
		Если СценарийАвтозагрузки.ТранспортПротокол = "FTPS" Тогда
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
			Если НЕ ЗначениеЗаполнено(Порт) Тогда
				Порт = 990;
			КонецЕсли;
		Иначе
			ЗащищенноеСоединение = Неопределено;
			Если НЕ ЗначениеЗаполнено(Порт) Тогда
				Порт = 21;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			FTP = Новый FTPСоединение(Сервер, Порт, Логин, Пароль, Прокси, ПассивноеСоединение, Таймаут, ЗащищенноеСоединение);
		Исключение
			ТекстОшибки = ИнформацияОбОшибке().Описание;
			Если Найти(ВРег(ТекстОшибки),ВРег("couldn't connect to server")) Тогда
				ТекстОшибки = " на указанном сервере не запущен FTP-сервер";
				
			ИначеЕсли Найти(ВРег(ТекстОшибки),ВРег("couldn't resolve host name")) Тогда 	
				ТекстОшибки = " указанный сервер не доступен";
				
			КонецЕсли;
			стрОшибки = "Ошибка подключения к серверу: ftp://" + СценарийАвтозагрузки.ТранспортСервер + Символы.ПС + ТекстОшибки;
			Возврат Ложь;
		КонецПопытки;
		
		Состояние_("Поиск файлов на FTP-сервере ...");
		FTPКаталог = КодироватьСтроку(СценарийАвтозагрузки.FtpКаталог, СпособКодированияСтроки.КодировкаURL, "windows1251");
		Попытка
			НайденныеФайлы = FTP.НайтиФайлы(FtpКаталог, МаскаФайлов, Ложь);
			стрОшибки = "Сервер: ftp://" + СценарийАвтозагрузки.ТранспортСервер + " доступен";
		Исключение
			стрОшибки = "Ошибка поиска на сервере: ftp://" + СценарийАвтозагрузки.ТранспортСервер + " файлов по маске <"+МаскаФайлов+">
			|" + ИнформацияОбОшибке().Описание + Символы.ПС;
			Возврат Ложь;
		КонецПопытки;
		
	ИначеЕсли СценарийАвтозагрузки.ТранспортПротокол = "HTTP" ИЛИ СценарийАвтозагрузки.ТранспортПротокол = "HTTPS" Тогда // Обрабатываем протоколы HTTP и HTTPS
		
		Если СценарийАвтозагрузки.ТранспортПротокол = "HTTPS" Тогда
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
			Порт = 443; 
		Иначе
			ЗащищенноеСоединение = Неопределено;
			Порт = 80;
		КонецЕсли;
		
		Сервер = СценарийАвтозагрузки.ТранспортСервер;
		Логин = "";
		Пароль = "";
		Если СценарийАвтозагрузки.ТребуетсяАвторизация Тогда
			Логин = СценарийАвтозагрузки.ТранспортЛогин;
			Пароль = СценарийАвтозагрузки.ТранспортПароль;
		КонецЕсли;
		
		Попытка
			Соединение = Новый HTTPСоединение(Сервер, Порт, Логин, Пароль,,, ЗащищенноеСоединение);
		Исключение
			стрОшибки = "Ошибка соединения с сервером " + СценарийАвтозагрузки.ТранспортСервер + "
			|" + ИнформацияОбОшибке().Описание + Символы.ПС;
			Возврат Ложь;
		КонецПопытки;
		Заголовки = Новый Соответствие;
		
		ПолнаяСтруктураURL = ПроцедурыОбщегоНазначенияКлиентСервер.СтруктураURI(СценарийАвтозагрузки.URL);
		
		Если СценарийАвтозагрузки.флФайлВАрхиве Тогда
			ФорматФайла = СценарийАвтозагрузки.ФорматМаскиАрхива;
			ИмяТестовогоФайла = СтрЗаменить(СтрЗаменить(СценарийАвтозагрузки.МаскаАрхива, "?", ""), "*", "");
		Иначе
			ФорматФайла = СценарийАвтозагрузки.ФорматМаскиФайла;
			ИмяТестовогоФайла = СтрЗаменить(СтрЗаменить(СценарийАвтозагрузки.МаскаФайлаПрайсЛиста, "?", ""), "*", "");
		КонецЕсли;
		
		ПолноеИмяТестовогоФайла = ЛокКаталог + ?(Прав(ЛокКаталог, 1) = "\", "", "\") + СокрЛП(ИмяТестовогоФайла + " " + Формат(ТекущаяДата(),"ДФ=yyyy_MM_dd_HH_mm_ss") + ФорматФайла);
		
		Попытка
			HTTPОтвет = ПолучитьHTTPОтвет(ПолнаяСтруктураURL.ПутьНаСервере, ПолноеИмяТестовогоФайла, Соединение, Заголовки);
			Если HTTPОтвет.КодСостояния < 200 Или HTTPОтвет.КодСостояния >= 300 Тогда
				Попытка УдалитьФайлы(ПолноеИмяТестовогоФайла); Исключение КонецПопытки;
				стрОшибки = "Не удалось обработать ответ сервера: " + СценарийАвтозагрузки.ТранспортСервер + "
				|запрос: " + СценарийАвтозагрузки.URL;
				Возврат Ложь;
			КонецЕсли;
			стрОшибки = "Файл с сервера: " + СценарийАвтозагрузки.ТранспортСервер + " успешно получен.";
		Исключение
			Попытка УдалитьФайлы(ПолноеИмяТестовогоФайла); Исключение КонецПопытки;
			стрОшибки = "Не удалось получить файл с сервера: " + СценарийАвтозагрузки.ТранспортСервер + "
			|запрос: " + СценарийАвтозагрузки.URL + "
			|" + ИнформацияОбОшибке().Описание + Символы.ПС;
			Возврат Ложь;
		КонецПопытки;
		
		Попытка
			УдалитьФайлы(ПолноеИмяТестовогоФайла);
		Исключение
			стрОшибки = стрОшибки + "
			|Не смогли удалить тестовый файл: " + ПолноеИмяТестовогоФайла + "
			|" + ИнформацияОбОшибке().Описание + Символы.ПС;
			Возврат Ложь;
		КонецПопытки;
		
	КонецЕсли;
	
	Состояние_("");
	
	Возврат Истина;
	
КонецФункции //ПроверитьТранспорт()

//Проверяет наличие новых файлов с различных ресурсов при автообновлении
Функция ПроверитьНовыеФайлы(ТекстСообщения = "") Экспорт
	
	//Проверим наличие новых файлов.
	//Если архив, то надо смотреть самый новый архив
	//Если взведен флаг "СохранитьФайлы" то производим транспортировку файла и сохраним его в указанном каталоге.
	//Для прайс-листа устанавливаем статус Ожидает загрузки
	
	СценарийАвтозагрузки = Справочники.ПрайсЛистыКонтрагентов.ПолучитьСценарийАвтозагрузки(Ссылка);  //Менеджер
	
	ЖурналЗагрузки = РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СформироватьСтруктуруЖурналаЗагрузки();
	
	//Установим по умолчанию статус: "Ошибка"
	ЖурналЗагрузки.Период = ТекущаяДата();
	ЖурналЗагрузки.Статус = Перечисления.СтатусыЗагрузкиПрайсЛистов.НовыйФайлОшибка;
	ЖурналЗагрузки.Автор = ПараметрыСеанса.Пользователь;
	
	ХешФайлаАвтозагрузки = СценарийАвтозагрузки.ХешФайла;   // Хеш архива или файла
	ДатаФайлаАвтозагрузки = СценарийАвтозагрузки.ДатаФайла; // Дата архива или файла
	
	Если СценарийАвтозагрузки.флФайлВАрхиве Тогда
		МаскаФайла = ?(ПустаяСтрока(СценарийАвтозагрузки.МаскаАрхива), "*", СценарийАвтозагрузки.МаскаАрхива) + СценарийАвтозагрузки.ФорматМаскиАрхива;
	Иначе 
		МаскаФайла = ?(ПустаяСтрока(СценарийАвтозагрузки.МаскаФайлаПрайсЛиста), "*", СценарийАвтозагрузки.МаскаФайлаПрайсЛиста) + СценарийАвтозагрузки.ФорматМаскиФайла;
	КонецЕсли;
	
	Если НЕ СценарийАвтозагрузки.ПрайсИзИнтернета ИЛИ СценарийАвтозагрузки.СохранятьВКаталог Тогда
		ЛокКаталог = СценарийАвтозагрузки.КаталогФайлаПрайсЛиста;
	Иначе
		ЛокКаталог = рфПолучитьКаталогФайла(ПолучитьИмяВременногоФайла());
	КонецЕсли;
	
	ИмяНовогоФайла = Неопределено;
	
	Если НЕ СценарийАвтозагрузки.ПрайсИзИнтернета Тогда
		// Просто проверяем файлы в каталоге
		Состояние_("Поиск файлов ...");
		
		Попытка
			НайденныеФайлы = НайтиФайлы(ЛокКаталог, МаскаФайла, Ложь);
		Исключение
			ТекстСообщения = НСтр("ru='Ошибка при поиске в каталоге: "+ЛокКаталог + " файлов по маске <'") + МаскаФайла + ">
				|" + ИнформацияОбОшибке().Описание;
			
			ЖурналЗагрузки.Примечание = ТекстСообщения;
			РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
			Возврат Ложь;
		КонецПопытки;
		
		Состояние_("Проверка файлов ...");
		
		Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
			// Сравним сохраненные данные и данные найденного файла
			Если НайденныйФайл.ЭтоКаталог() Тогда
				Продолжить;
			КонецЕсли;
			
			ДатаПроверяемогоФайла = НайденныйФайл.ПолучитьВремяИзменения();
			Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
			
			Попытка
				Хеш.ДобавитьФайл(НайденныйФайл.ПолноеИмя);
			Исключение
				// В ошибки записывать не будем, так как доступ к файлу может восстановиться и надо будет провести загрузку
				// а сейчас попробуем найти самый свежий из доступных файлов
				ТекстСообщения = ТекстСообщения + НСтр("ru='Ошибка доступа к файлу: '") + НайденныйФайл.ПолноеИмя + "
					|" + ИнформацияОбОшибке().Описание + Символы.ПС;
				Продолжить;
			КонецПопытки;
			
			ХешПроверяемогоФайла = СтрЗаменить(Строка(Хеш.ХешСумма), " ", "");
			
			Если обЗначениеНеЗаполнено(ДатаФайлаАвтозагрузки) Тогда
				Если ХешПроверяемогоФайла <> ХешФайлаАвтозагрузки Тогда
					ДатаФайлаАвтозагрузки = ДатаПроверяемогоФайла;
					ХешФайлаАвтозагрузки = ХешПроверяемогоФайла;
					ИмяНовогоФайла = НайденныйФайл.ПолноеИмя;
					ИмяФайлаFtp = НайденныйФайл.Имя;
				КонецЕсли;
			Иначе
				// Сохранена дата загруженного ранее файла
				Если обЗначениеНеЗаполнено(ДатаПроверяемогоФайла) Тогда
					// Почему-то у найденного файла нет даты?
					Продолжить;
				Иначе
					Если ДатаПроверяемогоФайла > ДатаФайлаАвтозагрузки И ХешПроверяемогоФайла <> ХешФайлаАвтозагрузки Тогда
						// Сохраним контрольные данные проверяемого файла в текущие данные
						// что бы на выходе иметь самый свежий файл
						ДатаФайлаАвтозагрузки = ДатаПроверяемогоФайла;
						ХешФайлаАвтозагрузки = ХешПроверяемогоФайла;
						ИмяНовогоФайла = НайденныйФайл.ПолноеИмя;
						ИмяФайлаFtp = НайденныйФайл.Имя;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СценарийАвтозагрузки.ТранспортПротокол = "FTP" ИЛИ СценарийАвтозагрузки.ТранспортПротокол = "FTPS" Тогда
		
		Состояние_("Подключение к FTP серверу ...");
		
		Сервер = СценарийАвтозагрузки.ТранспортСервер;
		Порт = СценарийАвтозагрузки.ТранспортПорт;
		Логин  = "anonymous";
		Пароль = "";
		
		Если СценарийАвтозагрузки.ТребуетсяАвторизация Тогда
			Логин  = СценарийАвтозагрузки.ТранспортЛогин;
			Пароль = СценарийАвтозагрузки.ТранспортПароль;
		КонецЕсли;
		
		ПассивноеСоединение  = СценарийАвтозагрузки.FtpПассивныйРежим;
		Если ЗначениеЗаполнено(СценарийАвтозагрузки.Таймаут) Тогда
			Таймаут = СценарийАвтозагрузки.Таймаут;
		Иначе
			Таймаут = 30;
		КонецЕсли;
		
		ЗащищенноеСоединение = Неопределено;
		Если СценарийАвтозагрузки.ТранспортПротокол = "FTPS" Тогда
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
			Если НЕ ЗначениеЗаполнено(Порт) Тогда
				Порт = 990;
			КонецЕсли;
		Иначе
			ЗащищенноеСоединение = Неопределено;
			Если НЕ ЗначениеЗаполнено(Порт) Тогда
				Порт = 21;
			КонецЕсли;
		КонецЕсли;
		
		// Для Ftp маска будет своя
		Если СценарийАвтозагрузки.флФайлВАрхиве Тогда
			МаскаДляПоиска = СценарийАвтозагрузки.МаскаАрхива + "*";
		Иначе
			МаскаДляПоиска = СценарийАвтозагрузки.МаскаФайлаПрайсЛиста + "*";
		КонецЕсли;
		
		Прокси = Новый ИнтернетПрокси;
		
		Попытка
			Состояние_("Поиск файлов на FTP-сервере ...");
			FTP = Новый FTPСоединение(Сервер, Порт, Логин, Пароль, Прокси, ПассивноеСоединение, Таймаут, ЗащищенноеСоединение);
			FTPКаталог = КодироватьСтроку(СценарийАвтозагрузки.FtpКаталог, СпособКодированияСтроки.КодировкаURL, "windows1251");
			
			Попытка
				НайденныеФайлы = FTP.НайтиФайлы(FtpКаталог, МаскаДляПоиска, Ложь);
			Исключение
				ТекстСообщения = НСтр("ru='Ошибка при поиске на сервере: ftp://'") + Сервер + НСтр("ru=' файлов по маске <") + МаскаДляПоиска + ">
				|" + ИнформацияОбОшибке().Описание;
				
				ЖурналЗагрузки.Примечание = ТекстСообщения;
				РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
				Возврат Ложь;
			КонецПопытки;
			
		Исключение
			ТекстСообщения = НСтр("ru='Ошибка соединения с сервером: ftp://'") + Сервер + "
			|" + ИнформацияОбОшибке().Описание;
			
			ЖурналЗагрузки.Примечание = ТекстСообщения;
			РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
			Возврат Ложь;
		КонецПопытки;
		
		Состояние_("Проверка файлов ...");
		Для Каждого НайденныйФайл Из НайденныеФайлы Цикл
			// Сравним сохраненные данные и данные найденного файла
			Если НайденныйФайл.ЭтоКаталог() Тогда
				Продолжить;
			КонецЕсли;
			
			ДатаПроверяемогоФайла = НайденныйФайл.ПолучитьВремяИзменения();
			ХешПроверяемогоФайла = СокрЛП(НайденныйФайл.Размер());
			
			Если обЗначениеНеЗаполнено(ДатаФайлаАвтозагрузки) Тогда
				Если ХешПроверяемогоФайла <> ХешФайлаАвтозагрузки Тогда
					ДатаФайлаАвтозагрузки = ДатаПроверяемогоФайла;
					ХешФайлаАвтозагрузки = ХешПроверяемогоФайла;
					ИмяНовогоФайла = НайденныйФайл.ПолноеИмя;
					ИмяФайлаFtp = НайденныйФайл.Имя;
				КонецЕсли;
			Иначе
				// Сохранена дата загруженного ранее файла
				Если обЗначениеНеЗаполнено(ДатаПроверяемогоФайла) Тогда
					// Почему-то у найденного файла нет даты?
					Продолжить;
				Иначе
					Если ДатаПроверяемогоФайла > ДатаФайлаАвтозагрузки И ХешПроверяемогоФайла <> ХешФайлаАвтозагрузки Тогда
						// Сохраним контрольные данные проверяемого файла в текущие данные
						// что бы на выходе иметь самый свежий файл
						ДатаФайлаАвтозагрузки = ДатаПроверяемогоФайла;
						ХешФайлаАвтозагрузки = ХешПроверяемогоФайла;
						ИмяНовогоФайла = НайденныйФайл.ПолноеИмя;
						ИмяФайлаFtp = НайденныйФайл.Имя;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ИмяНовогоФайла) Тогда
			
			Состояние_("Копирование файла с FTP-сервера ...");
			
			ИмяВыходногоФайла = рфПолучитьИмяФайла(ЛокКаталог, ИмяФайлаFtp);
			
			Попытка
				FTP.Получить(ИмяНовогоФайла, ИмяВыходногоФайла);
			Исключение
				ТекстСообщения = НСтр("ru='Не смогли получить файл "+ИмяФайлаFtp+" с сервера: ftp://'") + Сервер + "
				|" + ИнформацияОбОшибке().Описание;
				
				// Укажем все параметры файла, так как теперь они нам известны и ошибка должна быть к ним привязана
				ЖурналЗагрузки.СтрокаПодключения = ИмяНовогоФайла;
				ЖурналЗагрузки.ДатаФайла = ДатаФайлаАвтозагрузки;
				ЖурналЗагрузки.ХешФайла = ХешФайлаАвтозагрузки;
				ЖурналЗагрузки.Примечание = ТекстСообщения;
				РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
				Возврат Ложь;
			КонецПопытки;
			
			ИмяНовогоФайла = ИмяВыходногоФайла;
			
		КонецЕсли;
		
	ИначеЕсли СценарийАвтозагрузки.ТранспортПротокол = "IMAP" ИЛИ СценарийАвтозагрузки.ТранспортПротокол = "POP3" Тогда
		
		Состояние_("Подключение к почтовому серверу ...");
		
		Почта = Новый ИнтернетПочта(); 
		ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
		
		EmailПротокол = "POP3";
		Если СценарийАвтозагрузки.Свойство("ТранспортПротокол") И СценарийАвтозагрузки.ТранспортПротокол = "IMAP" Тогда
			EmailПротокол = "IMAP";
		КонецЕсли;
		
		Если EmailПротокол = "IMAP" Тогда
			ПочтовыйПрофиль.АдресСервераIMAP = СценарийАвтозагрузки.ТранспортСервер;
			ПочтовыйПрофиль.ПортIMAP         = СценарийАвтозагрузки.ТранспортПорт;
			ПочтовыйПрофиль.ПользовательIMAP = СценарийАвтозагрузки.ТранспортЛогин;
			ПочтовыйПрофиль.ПарольIMAP       = СценарийАвтозагрузки.ТранспортПароль;
			ПочтовыйПрофиль.ИспользоватьSSLIMAP = СценарийАвтозагрузки.SSL;
		Иначе
			ПочтовыйПрофиль.АдресСервераPOP3 = СценарийАвтозагрузки.ТранспортСервер;
			ПочтовыйПрофиль.ПортPOP3         = СценарийАвтозагрузки.ТранспортПорт;
			ПочтовыйПрофиль.Пользователь     = СценарийАвтозагрузки.ТранспортЛогин;
			ПочтовыйПрофиль.Пароль           = СценарийАвтозагрузки.ТранспортПароль;
			ПочтовыйПрофиль.ИспользоватьSSLPOP3 = СценарийАвтозагрузки.SSL;
		КонецЕсли;
		
		ПочтовыйПрофиль.Таймаут = СценарийАвтозагрузки.Таймаут;
		
		Попытка
			Почта.Подключиться(ПочтовыйПрофиль, ПротоколИнтернетПочты[EmailПротокол]);
		Исключение
			ТекстСообщения = НСтр("ru='Ошибка подключения к серверу: '") + EmailПротокол + ": " + СценарийАвтозагрузки.ТранспортСервер + " 
			|" + ИнформацияОбОшибке().Описание;
			
			ЖурналЗагрузки.Примечание = ТекстСообщения;
			РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
			Возврат Ложь;
		КонецПопытки;
		
		Если EmailПротокол = "IMAP"
			И СценарийАвтозагрузки.Свойство("ПочтовыйЯщик")
			И ЗначениеЗаполнено(СценарийАвтозагрузки.ПочтовыйЯщик) Тогда
			Попытка
				Почта.ТекущийПочтовыйЯщик = СценарийАвтозагрузки.ПочтовыйЯщик;
			Исключение
				Почта.Отключиться();
				стрОшибки = "Ошибка при выборе папки "+СценарийАвтозагрузки.ПочтовыйЯщик+" в почтовом ящике!";
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;
		
		ДатаПроверяемогоФайла = ДатаФайлаАвтозагрузки;
		
		Состояние_("Получение заголовков писем ...");
		
		// Для IMAP ящиков это работает. Может когда-нибудь и POP3 тоже станут поддерживать отборы!
		СтруктураЗаголовков = Новый Структура;
		Если ЗначениеЗаполнено(ДатаФайлаАвтозагрузки) Тогда
			СтруктураЗаголовков.Вставить("AfterDateOfPosting", ДатаФайлаАвтозагрузки);
		КонецЕсли;
		//Если ЗначениеЗаполнено(СценарийАвтозагрузки.EmailАдресОтправителя) Тогда
		//	СтруктураЗаголовков.Вставить("From", СценарийАвтозагрузки.EmailАдресОтправителя);
		//КонецЕсли;
		//Если ЗначениеЗаполнено(СценарийАвтозагрузки.EmailТемаПисьма) Тогда
		//	СтруктураЗаголовков.Вставить("Subject", СценарийАвтозагрузки.EmailТемаПисьма);
		//КонецЕсли;
		//Если ЗначениеЗаполнено(СценарийАвтозагрузки.EmailТекстПисьма) Тогда
		//	СтруктураЗаголовков.Вставить("Body", СценарийАвтозагрузки.EmailТекстПисьма);
		//КонецЕсли;
		
		Заголовки = Почта.ПолучитьЗаголовки(СтруктураЗаголовков);
		
		// Надо проверить на совпадение по фильтрам!
		// Так как при получении по протоколу POP3 не срабатывают отборы!
		
		ТаблицаЗаголовков = Новый ТаблицаЗначений;
		ТаблицаЗаголовков.Колонки.Добавить("ЗаголовокПисьма");
		ТаблицаЗаголовков.Колонки.Добавить("ДатаОтправления");
		
		Если Заголовки.Количество() > 0 Тогда
			Для Каждого ЗаголовокПисьма Из Заголовки Цикл
				
				Если ЗначениеЗаполнено(ДатаФайлаАвтозагрузки) И ЗаголовокПисьма.ДатаОтправления <= ДатаФайлаАвтозагрузки Тогда
					Продолжить;
				КонецЕсли;
				
				Если (Найти(ВРег(ЗаголовокПисьма.ИмяОтправителя), ВРег(СценарийАвтозагрузки.EmailАдресОтправителя)) > 0
						ИЛИ (НЕ обЗначениеНеЗаполнено(ЗаголовокПисьма.Отправитель) И Найти(ВРег(ЗаголовокПисьма.Отправитель.Адрес), ВРег(СценарийАвтозагрузки.EmailАдресОтправителя)) > 0))
					И ?(ЗначениеЗаполнено(СценарийАвтозагрузки.EmailТемаПисьма), Найти(ВРег(ЗаголовокПисьма.Тема), ВРег(СценарийАвтозагрузки.EmailТемаПисьма)) > 0, Истина) Тогда
					
					СтрокаЗаголовка = ТаблицаЗаголовков.Добавить();
					СтрокаЗаголовка.ЗаголовокПисьма = ЗаголовокПисьма;
					СтрокаЗаголовка.ДатаОтправления = ЗаголовокПисьма.ДатаОтправления;
					
				Иначе
					Продолжить;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		Если ТаблицаЗаголовков.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru='На сервере: '") + EmailПротокол + ": " + СценарийАвтозагрузки.ТранспортСервер + НСтр("ru=' нет новых сообщений!'");
			Почта.Отключиться();
			Возврат Ложь;
		КонецЕсли;
		
		ТаблицаЗаголовков.Сортировать("ДатаОтправления Убыв");
		
		Состояние_("Получение и проверка писем ...");
		
		ТаблицаВложенийПисьма = Новый ТаблицаЗначений;
		ТаблицаВложенийПисьма.Колонки.Добавить("ИмяФайла", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(256)));
		ТаблицаВложенийПисьма.Колонки.Добавить("ИндексСтроки", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10)));
		
		ТаблицаВложений = Новый ТаблицаЗначений;
		ТаблицаВложений.Колонки.Добавить("ПочтовоеСообщение");
		ТаблицаВложений.Колонки.Добавить("ДатаСообщения");
		ТаблицаВложений.Колонки.Добавить("Данные");
		ТаблицаВложений.Колонки.Добавить("ИмяФайла");
		
		СчПодходящихПисем = 0;
		
		// Будем получать по одному письму!
		Для Каждого СтрокаЗаголовка Из ТаблицаЗаголовков Цикл
			
			ТаблицаВложенийПисьма.Очистить();
			ТаблицаВложений.Очистить();
			
			МассивЗаголовков = Новый Массив;
			МассивЗаголовков.Добавить(СтрокаЗаголовка.ЗаголовокПисьма);
			
			Письма = Почта.Выбрать(Ложь, МассивЗаголовков);
			
			Для Каждого ПочтовоеСообщение Из Письма Цикл
				
				Если ПочтовоеСообщение.Вложения.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				// Надо проверить на совпадение по фильтрам!
				// Так как при получении по протоколу POP3 не срабатывают отборы!
				Если НЕ ПустаяСтрока(СценарийАвтозагрузки.EmailТекстПисьма) Тогда
					Пропустить = Истина;
					
					Для Каждого ТекстСообщения ИЗ ПочтовоеСообщение.Тексты Цикл
						
						Если ТекстСообщения.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст
							И Найти(ВРег(ТекстСообщения.Текст), ВРег(СценарийАвтозагрузки.EmailТекстПисьма)) > 0 Тогда
							Пропустить = Ложь;
						КонецЕсли;
						
					КонецЦикла;
					
					Если Пропустить Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				СчПодходящихПисем = СчПодходящихПисем + 1;
				
				Сч = 0;
				Для Каждого Вложение Из ПочтовоеСообщение.Вложения Цикл
					Если ЗначениеЗаполнено(Вложение.ИмяФайла) Тогда
						НоваяСтрока = ТаблицаВложенийПисьма.Добавить();
						НоваяСтрока.ИмяФайла = Вложение.ИмяФайла;
						НоваяСтрока.ИндексСтроки = Сч;
					КонецЕсли;
					Сч = Сч + 1;
				КонецЦикла;
				
				ЗапросВложений = Новый Запрос;
				ЗапросВложений.Текст = 
					"ВЫБРАТЬ
					|	Таб.ИмяФайла,
					|	Таб.ИндексСтроки
					|ПОМЕСТИТЬ Вложения
					|ИЗ
					|	&Таб КАК Таб
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	Вложения.ИмяФайла,
					|	Вложения.ИндексСтроки
					|ИЗ
					|	Вложения КАК Вложения
					|ГДЕ
					|	Вложения.ИмяФайла ПОДОБНО &ИмяФайла СПЕЦСИМВОЛ ""\""
					|";
				ЗапросВложений.УстановитьПараметр("Таб", ТаблицаВложенийПисьма);
				ЗапросВложений.УстановитьПараметр("ИмяФайла", СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(МаскаФайла, "%", "\%"), "_", "\_"), "?", "_"), "*", "%"));
				Выборка = ЗапросВложений.Выполнить().Выбрать();
				
				Пока Выборка.Следующий() Цикл
					СтрокаВложения = ТаблицаВложений.Добавить();
					СтрокаВложения.ДатаСообщения = ПочтовоеСообщение.ДатаОтправления;
					СтрокаВложения.Данные = ПочтовоеСообщение.Вложения[Выборка.ИндексСтроки].Данные;
					СтрокаВложения.ИмяФайла = Выборка.ИмяФайла;
				КонецЦикла;
				
			КонецЦикла;
			
			Если ТаблицаВложений.Количество() > 0 Тогда
				Состояние_("Сохранение нового файла ...");
				
				ТаблицаВложений.Сортировать("ДатаСообщения Убыв");
				
				ИмяНовогоФайла = рфПолучитьИмяФайла(ЛокКаталог, ТаблицаВложений[0].ИмяФайла);
				
				Если НЕ рфСохранитьФайлНаДиске(ТаблицаВложений[0].Данные, ИмяНовогоФайла,, "ДА") Тогда
					ТекстСообщения = НСтр("ru='Не смогли сохранить вложение письма: '") + ИмяНовогоФайла;
					
					ЖурналЗагрузки.Примечание = ТекстСообщения;
					ЖурналЗагрузки.ДатаФайла = ТаблицаВложений[0].ДатаСообщения;
					РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
					
					Почта.Отключиться();
					Возврат Ложь;
				КонецЕсли;
				
				Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
				Хеш.ДобавитьФайл(ИмяНовогоФайла);
				
				ХешФайлаАвтозагрузки = СтрЗаменить(Строка(Хеш.ХешСумма), " ", "");
				ДатаФайлаАвтозагрузки = ТаблицаВложений[0].ДатаСообщения;
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Почта.Отключиться();
		
		Если СчПодходящихПисем = 0 Тогда
			ТекстСообщения = НСтр("ru='На сервере: '") + EmailПротокол + ": " + СценарийАвтозагрузки.ТранспортСервер + НСтр("ru=' нет новых сообщений для прайс-листа!'");
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли СценарийАвтозагрузки.ТранспортПротокол = "HTTP" ИЛИ СценарийАвтозагрузки.ТранспортПротокол = "HTTPS" Тогда // Обрабатываем протоколы HTTP и HTTPS
		Если СценарийАвтозагрузки.ТранспортПротокол = "HTTPS" Тогда
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
			Порт = 443; 
		Иначе
			ЗащищенноеСоединение = Неопределено;
			Порт = 80;
		КонецЕсли;
		
		Сервер = СценарийАвтозагрузки.ТранспортСервер;
		Логин = "";
		Пароль = "";
		
		Если СценарийАвтозагрузки.ТребуетсяАвторизация Тогда
			Логин = СценарийАвтозагрузки.ТранспортЛогин;
			Пароль = СценарийАвтозагрузки.ТранспортПароль;
		КонецЕсли;
		
		Попытка
			Соединение = Новый HTTPСоединение(Сервер, Порт, Логин, Пароль,,, ЗащищенноеСоединение);
		Исключение
			ТекстСообщения = НСтр("ru='Ошибка соединения с сервером: '") + Сервер + "
			|" + ИнформацияОбОшибке().Описание;
			
			ЖурналЗагрузки.Примечание = ТекстСообщения;
			РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
			Возврат Ложь;
		КонецПопытки;
		
		Заголовки = Новый Соответствие;
		
		ПолнаяСтруктураURL = ПроцедурыОбщегоНазначенияКлиентСервер.СтруктураURI(СценарийАвтозагрузки.URL);
		
		Если СценарийАвтозагрузки.флФайлВАрхиве Тогда
			ФорматФайла = СценарийАвтозагрузки.ФорматМаскиАрхива;
			ИмяТестовогоФайла = СтрЗаменить(СтрЗаменить(СценарийАвтозагрузки.МаскаАрхива, "?", ""), "*", "");
		Иначе
			ФорматФайла = СценарийАвтозагрузки.ФорматМаскиФайла;
			ИмяТестовогоФайла = СтрЗаменить(СтрЗаменить(СценарийАвтозагрузки.МаскаФайлаПрайсЛиста, "?", ""), "*", "");
		КонецЕсли;
		
		ПолноеИмяТестовогоФайла = ЛокКаталог + ?(Прав(ЛокКаталог, 1) = "\", "", "\") + СокрЛП(ИмяТестовогоФайла + " " + Формат(ТекущаяДата(),"ДФ=yyyy_MM_dd_HH_mm_ss") + ФорматФайла);
		
		Попытка
			HTTPОтвет = ПолучитьHTTPОтвет(ПолнаяСтруктураURL.ПутьНаСервере, ПолноеИмяТестовогоФайла, Соединение, Заголовки);
			Если HTTPОтвет.КодСостояния < 200 Или HTTPОтвет.КодСостояния >= 300 Тогда
				Попытка УдалитьФайлы(ПолноеИмяТестовогоФайла); Исключение КонецПопытки;
				ТекстСообщения = НСтр("ru='Не удалось обработать ответ сервера: '") + Сервер + "
				|запрос: " + СценарийАвтозагрузки.URL;
				ЖурналЗагрузки.Примечание = ТекстСообщения;
				РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
				Возврат Ложь;
			КонецЕсли;
		Исключение
			Попытка УдалитьФайлы(ПолноеИмяТестовогоФайла); Исключение КонецПопытки;
			ТекстСообщения = НСтр("ru='Не удалось выполнить запрос на сервер: '") + Сервер + НСтр("ru='
			|запрос: '") + СценарийАвтозагрузки.URL + "
			|" + ИнформацияОбОшибке().Описание;
			ЖурналЗагрузки.Примечание = ТекстСообщения;
			РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
			Возврат Ложь;
		КонецПопытки;
		
		ФайлОтвета = Новый Файл(ПолноеИмяТестовогоФайла);
		
		Если Не ФайлОтвета.Существует() Тогда
			ТекстСообщения = НСтр("ru='Не удалось получить данные с сервера: '") + Сервер + НСтр("ru='
			|запрос: '") + СценарийАвтозагрузки.URL;
			ЖурналЗагрузки.Примечание = ТекстСообщения;
			РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
			Возврат Ложь;
		КонецЕсли;
		
		Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
		Хеш.ДобавитьФайл(ПолноеИмяТестовогоФайла);
		ХешПроверяемогоФайла = СтрЗаменить(Строка(Хеш.ХешСумма), " ", "");
		
		Если ХешПроверяемогоФайла <> ХешФайлаАвтозагрузки Тогда
			ИмяНовогоФайла = ПолноеИмяТестовогоФайла;
			ХешФайлаАвтозагрузки = ХешПроверяемогоФайла;
			ДатаФайлаАвтозагрузки = ФайлОтвета.ПолучитьВремяИзменения();
		Иначе
			УдалитьФайлы(ПолноеИмяТестовогоФайла);
		КонецЕсли;
		
	КонецЕсли;
	
	Состояние_("");
	
	Если обЗначениеНеЗаполнено(ИмяНовогоФайла) Тогда
		Если обЗначениеНеЗаполнено(ТекстСообщения) Тогда
			ТекстСообщения = НСтр("ru='Новые файлы не найдены!'");
		Иначе
			ТекстСообщения = НСтр("ru='Новые файлы не найдены!'") + Символы.ПС + Символы.ПС + ТекстСообщения;
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	// Проверки новых данных завершены успешно
	
	// сохраним в сценарий автозагрузки
	СценарийАвтозагрузки.ДатаФайла = ДатаФайлаАвтозагрузки;
	СценарийАвтозагрузки.ХешФайла = ХешФайлаАвтозагрузки;
	Если СценарийАвтозагрузки.ПериодичностьПроверкиНовыхДанных = 0 Тогда
		СценарийАвтозагрузки.ДатаСледующейПроверки = ТекущаяДата() + 2*60*60;
	Иначе
		СценарийАвтозагрузки.ДатаСледующейПроверки = НачалоДня(ТекущаяДата() + СценарийАвтозагрузки.ПериодичностьПроверкиНовыхДанных * 24*60*60);
	КонецЕсли;
	Справочники.ПрайсЛистыКонтрагентов.СохранитьСценарийАвтозагрузки(Ссылка, СценарийАвтозагрузки);
	
	Если СценарийАвтозагрузки.флФайлВАрхиве Тогда
		Состояние_("Распаковка архива ...");
		
		МаскаФайлаПрайса = ?(ПустаяСтрока(СценарийАвтозагрузки.МаскаФайлаПрайсЛиста), "*", СценарийАвтозагрузки.МаскаФайлаПрайсЛиста) + СценарийАвтозагрузки.ФорматМаскиФайла;
		
		МассивИменФайлов = РаспаковатьZIPАрхивПрайсЛиста(ИмяНовогоФайла, ЛокКаталог, МаскаФайлаПрайса, ТекстСообщения);
		
		ЖурналЗагрузки.СтрокаПодключения = ИмяНовогоФайла;
		ЖурналЗагрузки.ДатаФайла = ДатаПроверяемогоФайла;
		ЖурналЗагрузки.ХешФайла = ХешПроверяемогоФайла;
		
		Если (СценарийАвтозагрузки.ПрайсИзИнтернета И Не СценарийАвтозагрузки.СохранятьВКаталог)
			ИЛИ СценарийАвтозагрузки.ДействиеСОбработаннымиФайлами = "удалить" Тогда
			Попытка
				УдалитьФайлы(ИмяНовогоФайла);
			Исключение КонецПопытки;
		ИначеЕсли СценарийАвтозагрузки.ДействиеСОбработаннымиФайлами = "upload" Тогда
			// Перенесем не зависимо от результата разбора!
			ИмяКаталога = ЛокКаталог + "\Upload"; // ЛокКаталог - это на самом деле это КаталогФайлаПрайсЛиста
			КаталогОтработанныхФайлов = Новый Файл(ИмяКаталога);
			Попытка
				Если НЕ КаталогОтработанныхФайлов.Существует() Тогда
					СоздатьКаталог(ИмяКаталога);
				КонецЕсли;
				Файл = Новый Файл(ИмяНовогоФайла);
				ФайлПриемник = Файл.Путь +  "Upload\" + Файл.Имя;
				ПереместитьФайл(Файл.ПолноеИмя, ФайлПриемник);
			Исключение КонецПопытки;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			// Ошибки при распаковке архива
			ЖурналЗагрузки.Примечание = ТекстСообщения;
			РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
			Возврат Ложь;
		Иначе
			Если МассивИменФайлов.Количество() = 0  Тогда
				ТекстСообщения = НСтр("ru='В архиве нет файлов по маске <'") + СценарийАвтозагрузки.МаскаФайлаПрайсЛиста + ">";
				
				ЖурналЗагрузки.Примечание = ТекстСообщения;
				РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		ИмяНовогоФайла = МассивИменФайлов[0];
		
	КонецЕсли;
	
	// Получим реальные данные про файл из которого произойдет загрузка!
	Файл = Новый Файл(ИмяНовогоФайла);
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	Хеш.ДобавитьФайл(ИмяНовогоФайла);
	
	// Теперь создадим в журнал запись про загружаемый файл
	ЖурналЗагрузки.Статус = ?(СценарийАвтозагрузки.ИспользоватьАвтообновление, Перечисления.СтатусыЗагрузкиПрайсЛистов.Ожидает, Перечисления.СтатусыЗагрузкиПрайсЛистов.НовыйФайл);
	ЖурналЗагрузки.СтрокаПодключения = ИмяНовогоФайла;
	ЖурналЗагрузки.ДатаФайла = Файл.ПолучитьВремяИзменения();
	ЖурналЗагрузки.ХешФайла = СтрЗаменить(Строка(Хеш.ХешСумма), " ", "");
	
	РегистрыСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СохранитьЖурналЗагрузкиПрайсЛиста(ЖурналЗагрузки, Ссылка);
	
	ТекстСообщения = ИмяНовогоФайла;
	
	Возврат Истина;
	
КонецФункции //ПроверитьНовыеФайлы()


//Автозагрузка прайс листа
Функция ЗагрузитьНовыйПрайсЛист(ИдентификаторФЗ = Неопределено) Экспорт
	
	// Получает данные о текущем статусе прайс-листа. Если статус - "Есть новый файл" или "Ожидает загрузку", то производим загрузку
	// Если указано, что надо перемещать обработанные файлы, то все файлы из каталога прайс-листа переместим в "Upload"
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЖурналЗагрузки.СтрокаПодключения КАК СтрокаПодключения,
	|	ЖурналЗагрузки.ДатаФайла,
	|	ЖурналЗагрузки.ХешФайла
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовЖурналЗагрузки.СрезПоследних(, ПрайсЛист = &ПрайсЛист) КАК ЖурналЗагрузки
	|ГДЕ
	|	ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.НовыйФайл)
	|	ИЛИ ЖурналЗагрузки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗагрузкиПрайсЛистов.Ожидает)";
	
	Запрос.УстановитьПараметр("ПрайсЛист", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		// Это не ошибка. Просто удалена запись про новый файл и все.
		Возврат Ложь;
	КонецЕсли;
	
	Состояние_("Загрузка прайс-листа из файла ...");
	
	// Так или на сервере или на клиенте в зависимости от контекста вызова
	Результат = фзФоновыеЗадания.ЗагрузитьПрайсЛист(Ссылка, Выборка.СтрокаПодключения, ИдентификаторФЗ);
	
	Состояние_("");
	
	Если Не Результат = Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СценарийАвтозагрузки = Справочники.ПрайсЛистыКонтрагентов.ПолучитьСценарийАвтозагрузки(Ссылка);
	
	//Перенос обработанных файлов
	Если (СценарийАвтозагрузки.ПрайсИзИнтернета И Не СценарийАвтозагрузки.СохранятьВКаталог) ИЛИ
		СценарийАвтозагрузки.ДействиеСОбработаннымиФайлами = "удалить" Тогда
		// Не сохраняем временные файлы прайс-листов
		Попытка
			УдалитьФайлы(Выборка.СтрокаПодключения);
		Исключение КонецПопытки;
	ИначеЕсли СценарийАвтозагрузки.ДействиеСОбработаннымиФайлами = "upload" Тогда
		// Требуется переместить загруженный файл в служебный каталог
		// Проверим существование каталога и если его нет, то создадим
		ИмяКаталога = СценарийАвтозагрузки.КаталогФайлаПрайсЛиста + "\Upload";
		КаталогОтработанныхФайлов = Новый Файл(ИмяКаталога);
		Попытка
			Если НЕ КаталогОтработанныхФайлов.Существует() Тогда
				СоздатьКаталог(ИмяКаталога);
			КонецЕсли;
			Файл = Новый Файл(Выборка.СтрокаПодключения);
			ФайлПриемник = ИмяКаталога + "\" + Файл.Имя;
			ПереместитьФайл(Файл.ПолноеИмя, ФайлПриемник);
		Исключение КонецПопытки;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ЗагрузитьНовыйПрайсЛист()

Функция НайтиСимволСКонца(Знач Строка, Знач Символ)
	
	Для Позиция = -СтрДлина(Строка) По -1 Цикл
		Если Сред(Строка, -Позиция, СтрДлина(Символ)) = Символ Тогда
			Возврат -Позиция;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

//Пыподит текущее состояние пользователю
Процедура Состояние_(СтрокаСостояния)
	#Если Клиент Тогда
		Состояние(СтрокаСостояния);
	#КонецЕсли
КонецПроцедуры //Состояние_()

//Определяет порт, по которому производится подключение в зависимости от используемого протокола и наличия SSL
Функция ОпределитьПортПоУмолчанию(ТранспортПротокол, SSL) Экспорт 
	ПортПоУмолчанию = Неопределено;
	Если ТранспортПротокол = "IMAP" Тогда
		Если SSL Тогда
			Возврат 993;
		Иначе
			Возврат 143;
		КонецЕсли;
	ИначеЕсли ТранспортПротокол = "POP3" Тогда
		Если SSL Тогда
			Возврат 995;
		Иначе
			Возврат 110;
		КонецЕсли;
	ИначеЕсли ТранспортПротокол = "HTTP" Тогда
		Возврат 80;
	ИначеЕсли ТранспортПротокол = "HTTPS" Тогда
		Возврат 443;
	ИначеЕсли ТранспортПротокол = "FTP" Тогда
		Возврат 21;
	ИначеЕсли ТранспортПротокол = "FTPS" Тогда
		Возврат 990;
	КонецЕсли;
	Возврат ПортПоУмолчанию;
КонецФункции //ОпределитьПортПоУмолчанию()

#Если Клиент Тогда

// Регистрация цен контрагента на основании прайс листа
Процедура ЗаполнитьЦеныКонтрагентаНаОснованииПрайсЛиста(ПрайсЛист = Неопределено) Экспорт
	
	Если ПрайсЛист = Неопределено Тогда
		ПрайсЛист = ЭтотОбъект;
	КонецЕсли;
	
	Если ТипЗнч(ПрайсЛист) = Тип("СправочникОбъект.ПрайсЛистыКонтрагентов") Тогда
		Если ПрайсЛист.ЭтоНовый() Тогда
			Ответ = Вопрос("Элемент еще не записан. Записать элемент?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Попытка
					Записать();
				Исключение
					Возврат;
				КонецПопытки;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		ПрайсЛист = ПрайсЛист.Ссылка;
	КонецЕсли;
	
	// создаем новый документ "ИзменениеЦен" и заполняем его по прайс-листу
	ДокументИзменениеЦен = Документы.ИзменениеЦен.СоздатьДокумент();
	ДокументИзменениеЦен.Заполнить(Неопределено);
	ДокументИзменениеЦен.Контрагент = ПрайсЛист.Владелец;
	ДокументИзменениеЦен.РасчетЦенОт = 4;
	ДокументИзменениеЦен.ПрайсЛистКонтрагента = ПрайсЛист;
	ДокументИзменениеЦен.ЗаполнениеПоПрайсЛистуКонтрагента(Истина);
	
	ФормаИзменениеЦен=ДокументИзменениеЦен.ПолучитьФорму();
	ФормаИзменениеЦен.ПараметрОснование = ПрайсЛист;
	ФормаИзменениеЦен.Открыть();
	
КонецПроцедуры // ЗаполнитьЦеныКонтрагентаНаОснованииПрайсЛиста()

// Функция создания/поиска производителя
Функция ПолучитьПроизводителя(ИмяПроизводителя = "") Экспорт
	// посмотрим что за параметр нам передали
	Если обЗначениеНеЗаполнено(ИмяПроизводителя) Тогда
		Возврат "";
	КонецЕсли;
	
	// попробуем найти данного производителя в базе
	ПроизводительСсылка = Справочники.Производители.НайтиПоНаименованию(СокрЛП(ИмяПроизводителя));
	Если НЕ обЗначениеНеЗаполнено(ПроизводительСсылка) Тогда
		Возврат ПроизводительСсылка;
	КонецЕсли;
	
	// если в базе тагого производителя не создадим его
	НовыйПроизводитель = Справочники.Производители.СоздатьЭлемент();
	НовыйПроизводитель.Наименование = ИмяПроизводителя;
	НовыйПроизводитель.УстановитьНовыйКод();
	НовыйПроизводитель.ОбменДанными.Загрузка = Истина;
	НовыйПроизводитель.Записать();
	Сообщить("Создан новый справочник Производители: <" + НовыйПроизводитель.Наименование + ">!", СтатусСообщения.Информация);
	Возврат НовыйПроизводитель.Ссылка;
КонецФункции //ПолучитьПроизводителя()

#КонецЕсли

// Проверим корректность ставки НДС для устранения ситуации выбора ставки НДС 18%
//
Функция ПроверитьКорректностьСтавкиНДС() Экспорт
	
	// По закону с 01.01.2019 вместо ставки НДС 18% необходимо использовать ставку НДС 20%.
	// По закону с 01.01.2026 вместо ставки НДС 20% необходимо использовать ставку НДС 22%.	
	
	ТекДата = ТекущаяДатаСеанса();
	
	СтавкаНДСНоменклатуры = СтавкаНДС.Ставка;
	
	// !!!_Переход на ставку НДС 20%
	// После 01.01.2019 убрать проверку на ввод ставки НДС 20%
	
	//Снимченко_bizteh++
	Если ТекДата < Дата("20260101") Тогда
		Если СтавкаНДСНоменклатуры = 22 Тогда
			Возврат Ложь;
		КонецЕсли;
	//Снимченко_bizteh--
	ИначеЕсли ТекДата < Дата("20190101") Тогда
		Если СтавкаНДСНоменклатуры = 20 Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Если СтавкаНДСНоменклатуры = 18 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьСтавкиНДС()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",1);
	ОбязательныеРеквизиты.Вставить("Валюта",1);
	ОбязательныеРеквизиты.Вставить("КодоваяСтраница",1);
	ОбязательныеРеквизиты.Вставить("ВидПрайсЛиста",1);
	Если ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ОстаткиДляКаталогаПредложений Тогда
		ОбязательныеРеквизиты.Вставить("БазовыйПрайсЛист",1);
	КонецЕсли;
	ОбязательныеРеквизиты.Вставить("СтавкаНДС",1);
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Результат = спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	Если НЕ ПроверитьКорректностьСтавкиНДС() Тогда
		дкДобавитьОшибку(Ошибки, "Указана недействительная ставка НДС!");
		Результат = Ложь;
	КонецЕсли;
	Возврат Результат;
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов.
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Формирует стандартное наименование единицы измерения
// в формате <Коэффициент>_<БазоваяЕдиница>
// Используется как при интерактивном, так и при программном создании
Процедура СформироватьНаименование() Экспорт
	Наименование = "Прайс-лист <"+СокрЛП(Владелец.Наименование)+">";
КонецПроцедуры // СформироватьНаименование()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Если Имя="СтавкаНДС" Тогда
		// Выведем предупреждение
		#Если Клиент Тогда
			Если НЕ ПроверитьКорректностьСтавкиНДС() Тогда
				Сообщить("Выбрана недействительная ставка НДС!", СтатусСообщения.Внимание);
			КонецЕсли;
		#КонецЕсли
	КонецЕсли;
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если обЗначениеНеЗаполнено(КодоваяСтраница) Тогда
		КодоваяСтраница="cpCP1251";
	КонецЕсли;
	Если обЗначениеНеЗаполнено(Разделитель) Тогда
		Разделитель=";";
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(СтавкаНДС) Тогда
		СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		ЦенаВключаетНДС = Истина;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("Массив") Тогда
		ВебПрайсЛистСсылка = Основание[0];
		// заполнение на основании веб-прайс-листа
		ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ВебПрайсЛист;
		КодВебПрайсЛиста = ВебПрайсЛистСсылка.Код;
		Наименование = ВебПрайсЛистСсылка.Наименование;
		Владелец = ВебПрайсЛистСсылка.Владелец;
		СрокАктуальностиПредложений = 1*24*60*60; // по-умолчанию 1 день
		ДополнительныеСвойства.Вставить("ОснованиеМассив", Основание);
	КонецЕсли;
	
	Если обЗначениеНеЗаполнено(ВидПрайсЛиста) Тогда
		ВидПрайсЛиста = Перечисления.ВидыПрайсЛистов.ПрайсЛистПоставщика;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()

//возвращает в структуре параметры номенклатуры
Функция ПолучитьПараметрыНоменклатуры(Знач Артикул, Знач ПроизводительАртикула=Неопределено, ИмяКлючевогоПоля=Неопределено, ТипЦен=Неопределено, ПодразделениеКомпании=Неопределено, Дата=Неопределено, ВозвращатьВсеНайденныеПозиции=Ложь) Экспорт
	
	СтрокаПрайсЛиста = Новый Структура;
	СтруктураПрайса = Метаданные.РегистрыСведений.ПрайсЛистыКонтрагентов.Измерения;
	Для каждого Рек Из СтруктураПрайса Цикл
		СтрокаПрайсЛиста.Вставить(Рек.Имя);
 	КонецЦикла;
	СтруктураПрайса = Метаданные.РегистрыСведений.ПрайсЛистыКонтрагентов.Ресурсы;
	Для каждого Рек Из СтруктураПрайса Цикл
		СтрокаПрайсЛиста.Вставить(Рек.Имя);
	КонецЦикла;
	СтруктураПрайса = Метаданные.РегистрыСведений.ПрайсЛистыКонтрагентов.Реквизиты;
	Для каждого Рек Из СтруктураПрайса Цикл
		СтрокаПрайсЛиста.Вставить(Рек.Имя);
	КонецЦикла;
	
	ФиксСтрокаПрайсЛиста = Новый ФиксированнаяСтруктура(СтрокаПрайсЛиста);
	МассивСтрок = Новый Массив;
	
	Если ТипЗнч(Артикул) = Тип("СправочникСсылка.Номенклатура") Тогда
		Номенклатура = Артикул;
		Артикул = Номенклатура.Артикул;
		АртикулДляПоиска = Номенклатура.АртикулДляПоиска;
		ПроизводительАртикула = Номенклатура.Производитель;
	Иначе
		Артикул = СокрЛП(Артикул);
		АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(Артикул, ПроизводительАртикула);
		Номенклатура = Справочники.Номенклатура.НайтиНоменклатуру(АртикулДляПоиска, ПроизводительАртикула);
		Если Не ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	//Попробуем найти эту номенклатуру в базе
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.*
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи КАК НедоступныеЗаписи
	|		ПО ПрайсЛистыКонтрагентов.ПрайсЛист = НедоступныеЗаписи.ПрайсЛист
	|			И ПрайсЛистыКонтрагентов.ДатаЗаписи = НедоступныеЗаписи.ДатаЗаписи
	|ГДЕ
	|	ПрайсЛистыКонтрагентов.ПрайсЛист = &ПрайсЛист
	|	И ПрайсЛистыКонтрагентов.АртикулДляПоиска = &АртикулДляПоиска
	|	И ПрайсЛистыКонтрагентов.АртикулДляПоиска <> """"
	|	"+ ?(ПроизводительАртикула = Null ИЛИ ПроизводительАртикула = Неопределено, "", " И ПрайсЛистыКонтрагентов.Производитель = &Производитель")+"
	|	И НедоступныеЗаписи.ПрайсЛист ЕСТЬ NULL 
	|
	|"+?(Номенклатура = Неопределено, "", "
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПрайсЛистыКонтрагентов.*
	|ИЗ
	|	РегистрСведений.ПрайсЛистыКонтрагентовПравилаЗагрузки КАК ПравилаЗагрузки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентов КАК ПрайсЛистыКонтрагентов
	|		ПО (ПрайсЛистыКонтрагентов.КлючСтрокиПоставщика = (ВЫРАЗИТЬ(ПравилаЗагрузки.ОбъектПравила КАК СТРОКА(32))))
	|			И (ПрайсЛистыКонтрагентов.ПрайсЛист = ПравилаЗагрузки.ПрайсЛист)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрайсЛистыКонтрагентовНедоступныеЗаписи КАК НедоступныеЗаписи
	|		ПО (ПрайсЛистыКонтрагентов.ПрайсЛист = НедоступныеЗаписи.ПрайсЛист)
	|			И (ПрайсЛистыКонтрагентов.ДатаЗаписи = НедоступныеЗаписи.ДатаЗаписи)
	|ГДЕ
	|	ПравилаЗагрузки.ПрайсЛист = &ПрайсЛист
	|	И ПравилаЗагрузки.НазначениеПравила = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилЗагрузки.КлючСтроки)
	|	И ПравилаЗагрузки.ИмяРеквизитаПрайсЛиста = ""Номенклатура""
	|	И ВЫРАЗИТЬ(ПравилаЗагрузки.Значение КАК Справочник.Номенклатура).Ссылка = &Номенклатура
	|	И НедоступныеЗаписи.ПрайсЛист ЕСТЬ NULL "));
	
	Запрос.УстановитьПараметр("Номенклатура",     Номенклатура);
	Запрос.УстановитьПараметр("АртикулДляПоиска", АртикулДляПоиска);
	Запрос.УстановитьПараметр("Производитель",    ПроизводительАртикула);
	Запрос.УстановитьПараметр("ПрайсЛист",        Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтрокаПрайсЛиста = Неопределено;
	
	Если Выборка.Количество() > 0 Тогда
		
		Пока Выборка.Следующий() Цикл
			
			СтрокаПрайсЛиста = Новый Структура(ФиксСтрокаПрайсЛиста);
			
			//Есть такой артикул в базе - возвращаем его
			Для каждого РеквизитПрайсЛиста Из СтрокаПрайсЛиста Цикл
				СтрокаПрайсЛиста[РеквизитПрайсЛиста.Ключ] = Выборка[РеквизитПрайсЛиста.Ключ];
			КонецЦикла;
			
			СтрокаПрайсЛиста.Вставить("ВоВнешнемИсточнике", Ложь);
			
			ДанныеЦены = Справочники.ПрайсЛистыКонтрагентов.РассчитатьЦеныПрайсЛиста(Ссылка, Истина, Истина, ТипЦен, ПодразделениеКомпании, Дата, СтрокаПрайсЛиста.Цена, Номенклатура, СтрокаПрайсЛиста.ТегПозиции, СтрокаПрайсЛиста.Производитель);
			Для Каждого КлючЗначение Из ДанныеЦены Цикл
				СтрокаПрайсЛиста.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
			КонецЦикла;
			
			Если ВозвращатьВсеНайденныеПозиции Тогда
				МассивСтрок.Добавить(СтрокаПрайсЛиста);
			Иначе
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли НЕ ФайлИсточникДанных Тогда
		//В базе такого артикула нет - поищем во внешнем источнике
		Попытка
			Connection = Новый COMОбъект("ADODB.Connection");
			Коннект = Connection.Open(СтрокаПодключения);
		Исключение
			Сообщить("Ошибка подключения к прайс-листу <"+Наименование+">: "+ОписаниеОшибки(),СтатусСообщения.Внимание); 
			Возврат Неопределено;
		КонецПопытки;
		Command = Новый  COMОбъект("ADODB.Command");
		Recordset = Новый  COMОбъект("ADODB.Recordset");
		Recordset.ActiveConnection = Connection;
		
		//найдем имя ключевого поля
		Если ИмяКлючевогоПоля = Неопределено Тогда
			СтрокаСКлючевымПолем = СтруктураФайлаПрайсЛиста.Найти(Истина, "Ключевое");
			Если СтрокаСКлючевымПолем = Неопределено Тогда
				Сообщить("Не задано ключевое поле прайс-листа "+СокрЛП(Наименование), СтатусСообщения.Внимание); 
				Возврат Неопределено;
			КонецЕсли;
			ИмяПоля = СтрокаСКлючевымПолем.ИмяПоляФайла;
		Иначе
			СтрокаСКлючевымПолем = СтруктураФайлаПрайсЛиста.Найти(ИмяКлючевогоПоля, "ИмяРеквизитаПрайсЛиста");
			Если СтрокаСКлючевымПолем = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
			ИмяПоля = СтрокаСКлючевымПолем.ИмяПоляФайла;
		КонецЕсли;
		
		СтрокаПоляПроизводитель = СтруктураФайлаПрайсЛиста.Найти("Производитель", "ИмяРеквизитаПрайсЛиста");
		Если СтрокаПоляПроизводитель <> Неопределено И ЗначениеЗаполнено(СтрокаПоляПроизводитель.ИмяПоляФайла) Тогда
			ИмяПоляПроизводитель = СтрокаПоляПроизводитель.ИмяПоляФайла;
		КонецЕсли;
		
		Если ПроизводительАртикула = Null ИЛИ ПроизводительАртикула = Неопределено Тогда
			// Нам подойдет любая запись с артикулом!
			ТекстЗап="
				|SELECT TOP 1 "+ИмяТаблицы+".*
				|FROM "+ИмяТаблицы+"
				|WHERE (("+ИмяТаблицы+".["+ИмяПоля+"]) = %ОтборКаталожныйНомер
				|	 OR ("+ИмяТаблицы+".["+ИмяПоля+"]) = %ОтборАртикулДляПоиска)";
		Иначе
			// Будем определять производителя в прайс-листе
			Если Не обЗначениеНеЗаполнено(Производитель) И Производитель <> ПроизводительАртикула Тогда
				// У прайс-листа указан конкретный производитель и он не равен запрашиваемому
				// Ничего не сможем найти
				Возврат Неопределено;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Производитель) ИЛИ ИмяПоляПроизводитель = Неопределено Тогда
				// Ищем только по артикулу
				ТекстЗап="
					|SELECT TOP 1 "+ИмяТаблицы+".*
					|FROM "+ИмяТаблицы+"
					|WHERE (("+ИмяТаблицы+".["+ИмяПоля+"]) = %ОтборКаталожныйНомер
					|	 OR ("+ИмяТаблицы+".["+ИмяПоля+"]) = %ОтборАртикулДляПоиска)";
			Иначе
				// Надо добавить отбор по производителю
				ТекстЗап="
					|SELECT TOP 1 "+ИмяТаблицы+".*
					|FROM "+ИмяТаблицы+"
					|WHERE ((("+ИмяТаблицы+".["+ИмяПоля+"]) = %ОтборКаталожныйНомер
					|	 OR ("+ИмяТаблицы+".["+ИмяПоля+"]) = %ОтборАртикулДляПоиска)
					|	 AND (("+ИмяТаблицы+".["+ИмяПоляПроизводитель+"]) = '"+ПроизводительАртикула.Наименование+"'))";
					
			КонецЕсли;
		КонецЕсли;
		
		ОтборАртикул = "'"+Артикул+"'";
		ОтборАртикулДляПоиска = "'"+АртикулДляПоиска+"'";
		ТекстЗапСтрока = СтрЗаменить(СтрЗаменить(ТекстЗап, "%ОтборКаталожныйНомер", ОтборАртикул), "%ОтборАртикулДляПоиска", ОтборАртикулДляПоиска);
		
		Command.CommandText = ТекстЗапСтрока;
		Command.ActiveConnection = Connection;
		
		Попытка
			Recordset = Command.Execute();
		Исключение
			
			ЧисловойАртикул = Ложь;
			Попытка 
				Артикул = Число(АртикулДляПоиска);
				ЧисловойАртикул = Истина;
			Исключение
				Возврат Неопределено;
			КонецПопытки;
			
			ОтборАртикул = Формат(Артикул, "ЧГ=0");
			ТекстЗапСтрока = СтрЗаменить(СтрЗаменить(ТекстЗап, "%ОтборКаталожныйНомер", ОтборАртикул), "%ОтборАртикулДляПоиска", ОтборАртикул);
			Command.CommandText = ТекстЗапСтрока;
			Command.ActiveConnection = Connection;
			
			//снова попытаемся выполнить запрос, теперь с числовым артикулом
			Попытка
				Recordset = Command.Execute();
			Исключение
				Сообщить("Ошибка при выборке из прайс-листа  <"+Наименование+"> во внешнем источнике данных: "+ОписаниеОшибки(), СтатусСообщения.Внимание);
				Возврат Неопределено;
			КонецПопытки;
			
		КонецПопытки; 
		
		КолПолей = Recordset.Fields.Count;
		//Переберем результат запроса ко внешнему источнику
		Пока НЕ Recordset.EOF Цикл
			
			СтрокаПрайсЛиста = Новый Структура(ФиксСтрокаПрайсЛиста);
			
			СтрокаПрайсЛиста.ПрайсЛист = Ссылка;
			
			Для К = 0 По КолПолей - 1 Цикл
				ИмяПоляПрайса = Recordset.Fields(К).Name;
				Value = Recordset.Fields(К).Value;
				Если ЗначениеЗаполнено(Value) Тогда
					СтрокиНастройки = СтруктураФайлаПрайсЛиста.НайтиСтроки(Новый Структура("ИмяПоляФайла", ИмяПоляПрайса));
					Если СтрокиНастройки.Количество() > 0 Тогда
						Для Каждого СтрокаНастройки Из СтрокиНастройки Цикл
							ИмяРеквизитаПрайсЛиста = СтрокаНастройки.ИмяРеквизитаПрайсЛиста;
							Если НЕ ПустаяСтрока(ИмяРеквизитаПрайсЛиста) Тогда
								Если ИмяРеквизитаПрайсЛиста = "Производитель" Тогда
									НайденныйПроизводитель = Справочники.Номенклатура.НайтиПроизводителяПоНаименованию(Value);
									Если ЗначениеЗаполнено(НайденныйПроизводитель) Тогда
										Value = НайденныйПроизводитель;
									КонецЕсли;
								КонецЕсли;
								СтрокаПрайсЛиста[ИмяРеквизитаПрайсЛиста] = Value;
							КонецЕсли; 
						КонецЦикла; 
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			СтрокаПрайсЛиста.Вставить("ВоВнешнемИсточнике",Истина);
			
			Если Не ЗначениеЗаполнено(СтрокаПрайсЛиста.Валюта) Тогда
				СтрокаПрайсЛиста.Валюта = Ссылка.Валюта;
			КонецЕсли;
			
			ДанныеЦены = Справочники.ПрайсЛистыКонтрагентов.РассчитатьЦеныПрайсЛиста(Ссылка, Истина, Истина, ТипЦен, ПодразделениеКомпании, Дата, СтрокаПрайсЛиста.Цена, Номенклатура, СтрокаПрайсЛиста.ТегПозиции, СтрокаПрайсЛиста.Производитель);
			Для Каждого КлючЗначение Из ДанныеЦены Цикл
				СтрокаПрайсЛиста.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
			КонецЦикла;
			
			Если ВозвращатьВсеНайденныеПозиции Тогда
				МассивСтрок.Добавить(СтрокаПрайсЛиста);
			Иначе
				Прервать;
			КонецЕсли;
			
			RecordSet.MoveNext();
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ВозвращатьВсеНайденныеПозиции Тогда
		Возврат ?(МассивСтрок.Количество()=0, Неопределено, МассивСтрок);
	Иначе
		Возврат СтрокаПрайсЛиста;
	КонецЕсли;
	
КонецФункции // ПолучитьПараметрыНоменклатуры() 

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
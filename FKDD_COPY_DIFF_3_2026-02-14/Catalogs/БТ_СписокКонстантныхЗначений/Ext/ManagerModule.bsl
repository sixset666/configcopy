//БИЗТЕХ КОВАЛЬ 16.07.2025 ++

// Функция - Получить таблицу константы
//
// Параметры:
//  НаименованиеКонстантногоЗначения - Наименование константного значения в виде строки
// 
// Возвращаемое значение:
//   - Таблица значений
//
Функция ПолучитьТаблицуКонстанты(НаименованиеКонстантногоЗначения) Экспорт
	СсылкаНаЭлемент = Справочники.БТ_СписокКонстантныхЗначений.НайтиПоНаименованию(НаименованиеКонстантногоЗначения,Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Ссылка КАК Ссылка,
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.НомерСтроки КАК НомерСтроки,
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Значение КАК Значение,
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Значение1 КАК Значение1,
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Значение2 КАК Значение2,
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Значение3 КАК Значение3,
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Значение4 КАК Значение4
		|ИЗ
		|	Справочник.БТ_СписокКонстантныхЗначений.ТаблицаДанных КАК БТ_СписокКонстантныхЗначенийТаблицаДанных
		|ГДЕ
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаЭлемент);
	
	Возврат Запрос.Выполнить().Выгрузить();

Конецфункции

// Функция - Есть ли значение в таблице
// Выполняет проверку на существование значения в определенной колонке в определенном элементе справочника
// Параметры:
//  НаименованиеКонстантногоЗначения - Наименование константного значения в виде строки - Строка
//  НаименованиеКолонки				 - Наименование колонки в таблице. Первая колонка "Значение", вторая "Значение1", последняя "Значение4" - Строка 
//  Значение						 - Значение которое нужно найти в указанной колонке - Любой
// 
// Возвращаемое значение:
//  Если значение есть в колонке то возвращается ИСТИНА, в противном случае ЛОЖЬ - Булево
//
Функция ЕстьЛиЗначениеВТаблице(НаименованиеКонстантногоЗначения,НаименованиеКолонки,Значение) Экспорт 
	
	ТаблицаДЛяПоиска = ПолучитьТаблицуКонстанты(НаименованиеКонстантногоЗначения);
	
	СтруктураПоиска = новый Структура;
	СтруктураПоиска.Вставить(НаименованиеКолонки,Значение);
	
	Если ТаблицаДЛяПоиска.НайтиСтроки(СтруктураПоиска).Количество() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
//БИЗТЕХ КОВАЛЬ 16.07.2025 --

//БИЗТЕХ КОВАЛЬ 25.11.2025 ++
Функция ПолучитьЗначениеКонстанты(НаименованиеКонстантногоЗначения,НаименованиеКолонки) Экспорт

	СсылкаНаЭлемент = Справочники.БТ_СписокКонстантныхЗначений.НайтиПоНаименованию(НаименованиеКонстантногоЗначения,Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Значение1 КАК Значение
		|ИЗ
		|	Справочник.БТ_СписокКонстантныхЗначений.ТаблицаДанных КАК БТ_СписокКонстантныхЗначенийТаблицаДанных
		|ГДЕ
		|	БТ_СписокКонстантныхЗначенийТаблицаДанных.Ссылка = &Ссылка
		|	И БТ_СписокКонстантныхЗначенийТаблицаДанных.Значение = &НаименованиеКолонки";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаЭлемент);
	Запрос.УстановитьПараметр("НаименованиеКолонки", НаименованиеКолонки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Возврат Выборка.Значение;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	
//БИЗТЕХ КОВАЛЬ 25.11.2025 --


Функция ПодразделениеО2Ли(Подразделение) Экспорт
	
	Возврат ЕстьЛиЗначениеВТаблице("ПодразделенияО2","Значение",Подразделение);
	
КонецФункции



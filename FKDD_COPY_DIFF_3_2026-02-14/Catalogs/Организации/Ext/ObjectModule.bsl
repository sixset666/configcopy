// Модуль справочника

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения
Перем СписокФормСобственности Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Проверка корректности ИНН на ключ
// Если ИНН корректно возвращается пустая строка.
//
// Параметры:
// 	ПроверяемыйИНН  – Строка – Проверяемый ИНН
//
// Возвращаемое значение:
// 	Строка – Сообщение об ошибке в ИНН.
//
Функция ПроверкаИНН(ПроверяемыйИНН) Экспорт
	ПровИНН=СокрЛП(ПроверяемыйИНН);
	ДлинаИНН=СтрДлина(ПровИНН);
	
	// Проверка длины ИНН
	Если ДлинаИНН<10 Тогда
		Возврат "ИНН не должен быть меньше 10-ти цифр";
	КонецЕсли;

	// Проверка ключа ИНН - сначала составим матрицу простых чисел,
	// потом проверим ключ для разной длины
	КодКонст=Новый Массив;
	КодКонст.Добавить(41);
	КодКонст.Добавить(37);
	КодКонст.Добавить(31);
	КодКонст.Добавить(29);
	КодКонст.Добавить(23);
	КодКонст.Добавить(19);
	КодКонст.Добавить(17);
	КодКонст.Добавить(13);
	КодКонст.Добавить(7);
	КодКонст.Добавить(5);
	КодКонст.Добавить(3);
	
	НСум=0;
	Если ДлинаИНН=10 Тогда
		// Для ИНН 10 знаков
		Для Сч=1 По 9 Цикл
			НСум=НСум+Число(Сред(ПровИНН,Сч,1))*КодКонст[Сч+1];
		КонецЦикла;
		//----------------------------------------------- 
		ННов=11-(НСум-(Цел(НСум/11)*11)); 
		КНов=Сред(ПровИНН,1,9)+?(ННов<10,Строка(ННов),"0");
		Если КНов=ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	
	ИначеЕсли ДлинаИНН=11 Тогда
		// Для ИНН 11 знаков
		КНов="1"+Сред(ПровИНН,2,9);	ПровИНН=ВРег(ПровИНН);
		Для Сч=1 По 10 Цикл
			НСум=НСум+Число(Сред(КНов,Сч,1))*КодКонст[Сч];
		КонецЦикла; 
		//----------------------------------------------- 
		ННов=11-(НСум-(Цел(НСум/11)*11));
		КНов="F"+Сред(КНов,2,9)+?(ННов<10,Строка(ННов),"0");
		Если КНов=ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	
	ИначеЕсли ДлинаИНН=12 Тогда			// Для 12 знаков
		Если Число(ПровИНН)*Число(Сред(ПровИНН,5,6))=0 Тогда
			Возврат "Неправильный ИНН - ошибка ключа";
		КонецЕсли;	
		//----------------------------------------------- 
		Для Сч=1 По 10 Цикл
			НСум=НСум+Число(Сред(ПровИНН,Сч,1))*КодКонст[Сч];
		КонецЦикла;	
		ННов=11-(НСум-(Цел(НСум/11)*11));
		КНов=Сред(ПровИНН,1,10)+?(ННов<10,Строка(ННов),"0");	
		НСум=0;
		//----------------------------------------------- 
		Для Сч=1 По 11 Цикл
			НСум=НСум+Число(Сред(ПровИНН,Сч,1))*КодКонст[Сч-1];
		КонецЦикла;
		ННов=11-(НСум-(Цел(НСум/11)*11));
		КНов=?(ННов<10,КНов+Строка(ННов),КНов+"0");	
		Если КНов=ПровИНН Тогда
			Возврат "";
		КонецЕсли;	
	
	КонецЕсли;
	
	Возврат "Неправильный ИНН - ошибка ключа";
КонецФункции // ПроверкаИНН()

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты=Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",3);

	Если ДляЭлемента Тогда
		ОбязательныеРеквизиты.Вставить("ФормаСобственности",1);
		ОбязательныеРеквизиты.Вставить("СистемаНалогообложения",1);
		Если СистемаНалогообложения=Перечисления.СистемыНалогообложения.Упрощенная Тогда
			ОбязательныеРеквизиты.Вставить("ОбъектНалогообложения",1);
		КонецЕсли;
		Если ФормаСобственности = Перечисления.ФормыСобственности.ЧастноеЛицо
			ИЛИ ФормаСобственности = Перечисления.ФормыСобственности.ЧастныйПредприниматель Тогда
			ОбязательныеРеквизиты.Вставить("Фамилия",1);
			ОбязательныеРеквизиты.Вставить("Имя",1);
		КонецЕсли;
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;

	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов/
//
// Параметры:
//  БлокироватьРеквизиты	– Строка	– Блокируемые на изменение реквизиты объекта.
//
// Возвращаемое значение:
//   Булево		   					– Допустимость изменения объекта.
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
    // Здесь может быть прописано определение наличия ссылок
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Обработка изменения реквизитов справочников
//
// Параметры:
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры	– Структура		– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	// ОБРАБОТКА РЕКВИЗИТОВ СПРАВОЧНИКОВ
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФормаСобственности) Тогда
		ФормаСобственности=Перечисления.ФормыСобственности.ЮридическоеЛицо;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(СистемаНалогообложения) Тогда
		СистемаНалогообложения=Перечисления.СистемыНалогообложения.Общая;
	КонецЕсли; 
КонецПроцедуры // ОбработкаЗаполнения()

// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
	ЭтотОбъект.ИНН=Неопределено;
	ЭтотОбъект.ДатаРегистрации=Неопределено;
	ЭтотОбъект.КодПоКОПФ=Неопределено;
	ЭтотОбъект.КодПоОКПО=Неопределено;
	ЭтотОбъект.Префикс=Неопределено;
	ЭтотОбъект.ОсновнойБанковскийСчет=Неопределено;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если СистемаНалогообложения=Перечисления.СистемыНалогообложения.Упрощенная Тогда
		ОсвобожденОтНДС=Истина;
	Иначе
		ОбъектНалогообложения=Неопределено;
		ОсвобожденОтНДС=Ложь;
	КонецЕсли; 
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()

// Стандартный обработчик ПриУстановкеНовогоКода элемента
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ спПриУстановкеНовогоКода(ЭтотОбъект, СтандартнаяОбработка, Префикс) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
Права = глПрава;
#КонецЕсли
СписокФормСобственности = Новый СписокЗначений();
СписокФормСобственности.Добавить(Перечисления.ФормыСобственности.ЮридическоеЛицо, "Юридическое лицо");
СписокФормСобственности.Добавить(Перечисления.ФормыСобственности.ЧастныйПредприниматель, "Частный предприниматель");
СписокФормСобственности.Добавить(Перечисления.ФормыСобственности.ЧастноеЛицо, "Частное лицо");
СписокФормСобственности.Добавить(Перечисления.ФормыСобственности.Прочее, "Прочее");


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Удаляет настройку из хранилища.
//
// Параметры:
//   КлючОтчета   (...) Ключ объекта настройки. 
//       |- (Неопределено) Удаляются настройки всех отчетов.
//       |- (Строка)       Полное имя отчета с точкой.
//   КлючВарианта (...) Ключ удаляемых настроек.
//       |- (Неопределено) Удаляются все варианты отчета.
//       |- (Строка)       Ключ варианта отчета.
//   Пользователь (...) Пользователь, настройки которого удаляются.
//       |- (Неопределено)                   Удаляются настройки всех пользователей
//       |- (Строка)                         Имя пользователя ИБ
//       |- (УникальныйИдентификатор)        Идентификатор пользователя ИБ
//       |- (ПользовательИнформационнойБазы) Пользователь ИБ
//       |- (СправочникСсылка.Пользователи)  Пользователь
//
// Подробнее - см. СтандартноеХранилищеНастроекМенеджер.Удалить()
//
Процедура Удалить(КлючОтчета, КлючВарианта, Пользователь) Экспорт
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Варианты.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК Варианты
	|ГДЕ
	|	Варианты.КлючОтчета = &КлючОтчета
	|	И Варианты.Автор = &Автор
	//|	И Варианты.Автор.ИдентификаторПользователяИБ = &GUID
	|	И Варианты.КлючВарианта = &КлючВарианта
	|	И Варианты.ПометкаУдаления = ЛОЖЬ";
	
	Запрос = Новый Запрос;
	
	Если КлючОтчета = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Варианты.КлючОтчета = &КлючОтчета", "ИСТИНА");
	Иначе
		//ОтчетИнформация = ВариантыОтчетов.СформироватьИнформациюОбОтчетеПоПолномуИмени(КлючОтчета);
		//Если ТипЗнч(ОтчетИнформация.ТекстОшибки) = Тип("Строка") Тогда
		//	ВызватьИсключение ОтчетИнформация.ТекстОшибки;
		//КонецЕсли;
		Запрос.УстановитьПараметр("КлючОтчета", КлючОтчета);
	КонецЕсли;
	
	Если КлючВарианта = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Варианты.КлючВарианта = &КлючВарианта", "");
	Иначе
		Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	КонецЕсли;
	
	Если Пользователь = Неопределено Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Варианты.Автор = &Автор", "");
		//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Варианты.Автор.ИдентификаторПользователяИБ = &GUID", "");
	
	ИначеЕсли ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Запрос.УстановитьПараметр("Администратор", Пользователь);
		//ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Варианты.Автор.ИдентификаторПользователяИБ = &GUID", "");
		
	Иначе
		
		Если ТипЗнч(Пользователь) = Тип("УникальныйИдентификатор") Тогда
			ИдентификаторПользователя = Пользователь;
		Иначе
			Если ТипЗнч(Пользователь) = Тип("Строка") Тогда
				ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(Пользователь);
				Если ПользовательИБ = Неопределено Тогда
					Возврат;
				КонецЕсли;
			ИначеЕсли ТипЗнч(Пользователь) = Тип("ПользовательИнформационнойБазы") Тогда
				ПользовательИБ = Пользователь;
			Иначе
				Возврат;
			КонецЕсли;
			ИдентификаторПользователя = ПользовательИБ.УникальныйИдентификатор;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("GUID", ИдентификаторПользователя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Варианты.Автор = &Автор", "");
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
	
#КонецЕсли

КонецПроцедуры

// Получает список настроек из хранилища. Значениями элементов списка являются ключи настроек.
//
// Параметры:
//   КлючОтчета   (Строка) Полное имя отчета с точкой.
//   Пользователь (...) Пользователь, настройки которого получаются. Необязательный параметр.
//       |- (Неопределено)                   Получаются настройки текущего пользователя
//       |- (Строка)                         Имя пользователя ИБ
//       |- (УникальныйИдентификатор)        Идентификатор пользователя ИБ
//       |- (ПользовательИнформационнойБазы) Пользователь ИБ
//       |- (СправочникСсылка.Пользователи)  Пользователь
//
// Отличия от механизма платформы:
//   Вместо права "АдминистрированиеДанных" проверяются права доступа к отчету.
//
// Возвращаемое значение: 
//   (СписокЗначений) Список вариантов отчета
//       |- Значение      (Строка) Ключ варианта.
//       |- Представление (Строка) Представление варианта.
//
Функция ПолучитьСписок(КлючОтчета, Пользователь = Неопределено, ПовторныйЗапуск = Ложь, ТолькоПредопределенные = Ложь) Экспорт
	
	Результат = Новый СписокЗначений;
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Наименование,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.ЭтоПредопределенный = ИСТИНА ТОГДА
	|			1
	|		ИНАЧЕ
	|			2
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.КлючОтчета = &КлючОтчета И 
	|	ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ И 
	|	(ВариантыОтчетов.Автор = &Пользователь ИЛИ 
	|	(ВариантыОтчетов.ПользовательскаяНастройка = ЛОЖЬ И 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Пользователь) И ВариантыОтчетов.ОбъектДоступа = &Пользователь) ИЛИ 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Подразделение) И ВариантыОтчетов.ОбъектДоступа = &Подразделение) ИЛИ 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Организация) И ВариантыОтчетов.ОбъектДоступа = &Организация) ИЛИ 
	|	(ВариантыОтчетов.НазначениеДоступа = ЗНАЧЕНИЕ(Перечисление.НазначениеПравИНастроек.Компания) И ВариантыОтчетов.ОбъектДоступа = НЕОПРЕДЕЛЕНО))) " + ?(ТолькоПредопределенные, " И 
	|	ВариантыОтчетов.ЭтоПредопределенный = ИСТИНА", "") + "
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Наименование";
	
	Запрос.УстановитьПараметр("КлючОтчета",    КлючОтчета);
	Запрос.УстановитьПараметр("Организация",   ПараметрыСеанса.Организация);
	Запрос.УстановитьПараметр("Подразделение", ПараметрыСеанса.ПодразделениеКомпании);
	
	ТекущийПользователь = ПараметрыСеанса.Пользователь;
	
	Если Пользователь = Неопределено Тогда
		Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Иначе
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.КлючВарианта, Выборка.Наименование);
	КонецЦикла;
	
	// Если это новый отчет
	Если Результат.Количество() = 0 И ПовторныйЗапуск = Ложь Тогда
		СоздатьВариантыОтчета(КлючОтчета);
		Результат = ПолучитьСписок(КлючОтчета, Пользователь = Неопределено, Истина);
	КонецЕсли;
	
#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

// Проверяет свободно ли наименование варианта отчета.
//
Функция НаименованиеЗанято(КлючОтчета, Ссылка, Наименование) Экспорт
	
	Возврат Ложь;
	
	Результат = Ложь;
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.КлючОтчета = &КлючОтчета И 
	|	ВариантыОтчетов.Ссылка <> &Ссылка И 
	|	ВариантыОтчетов.Наименование = &Наименование";
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("Ссылка",       Ссылка);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = НЕ Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьНастройки(КлючОтчета, КлючВарианта) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВариантыОтчетов.Настройки
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.КлючОтчета   = &КлючОтчета И
	|	ВариантыОтчетов.КлючВарианта = &КлючВарианта И
	|	ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|";
	
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Настройки.Получить();
	Иначе
		ИмяОтчета = СтрЗаменить(КлючОтчета, "Отчет.", "");
		ОтчетОбъект = Отчеты[ИмяОтчета].Создать();
		ПредопределенныйВариантНастроек = ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек.Найти(КлючВарианта);
		Если НЕ ПредопределенныйВариантНастроек = Неопределено Тогда
			Результат = ПредопределенныйВариантНастроек.Настройки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьВариантПериода(КлючОтчета, КлючВарианта) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВариантыОтчетов.ВариантЗагрузкиПериода
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.КлючОтчета   = &КлючОтчета И
	|	ВариантыОтчетов.КлючВарианта = &КлючВарианта И
	|	ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|";
	
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ВариантЗагрузкиПериода;
	Иначе
		
		Попытка
			МенеджерОтчета = Отчеты[СтрЗаменить(КлючОтчета, "Отчет.", "")];
			Результат = МенеджерОтчета.ПолучитьВариантПериода();
			Если ТипЗнч(Результат) = Тип("ВариантСтандартногоПериода") Тогда
				Если Результат = ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
					Результат = "ВариантСтандартногоПериода.ЭтотМесяц";
				Иначе
					Результат = ПолучитьПолноеИмяПредопределенногоЗначения(Результат);
				КонецЕсли;
			КонецЕсли;
		Исключение
			Результат = "ВариантСтандартногоПериода.ЭтотМесяц";
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обработчик чтения настроек варианта отчета
//
// Параметры:
//   КлючОтчета       (Строка) Полное имя отчета с точкой
//   КлючВарианта     (Строка) Ключ варианта отчета
//   Настройки        (*) Настройки варианта отчета
//   ОписаниеНастроек (ОписаниеНастроек)
//   Пользователь     (Строка) Имя пользователя ИБ
//       Не используется, так как подсистема "Варианты отчетов" не разделяет варианты по авторам.
//       Уникальность хранения и выборки гарантируется уникальностью пар ключей отчетов и вариантов.
//
Процедура ОбработкаЗагрузки(КлючОтчета, КлючВарианта, Настройки, ОписаниеНастроек, Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВариантыОтчетов.Наименование,
	|	ВариантыОтчетов.Настройки,
	|	ВариантыОтчетов.ВариантЗагрузкиПериода
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.КлючОтчета = &КлючОтчета И 
	|	ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ И 
	|	ВариантыОтчетов.КлючВарианта = &КлючВарианта";
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ОписаниеНастроек = Неопределено Тогда
			ОписаниеНастроек = Новый ОписаниеНастроек;
			ОписаниеНастроек.КлючОбъекта  = КлючОтчета;
			ОписаниеНастроек.КлючНастроек = КлючВарианта;
			ОписаниеНастроек.Пользователь = Пользователь;
		КонецЕсли;
		ОписаниеНастроек.Представление = Выборка.Наименование;
		
		ВариантЗагрузкиПериода = ?(Выборка.ВариантЗагрузкиПериода = "", "ВариантСтандартногоПериода.ЭтотМесяц", Выборка.ВариантЗагрузкиПериода);
		ОписаниеНастроек.ДополнительныеСвойства.Вставить("ВариантЗагрузкиПериода", ВариантЗагрузкиПериода);
		Настройки = Выборка.Настройки.Получить();
	Иначе
		ОтчетОбъект = Отчеты[КлючОтчета].Создать();
		СхемаКомпоновки = ОтчетОбъект.СхемаКомпоновкиДанных;
		ПредопределенныйВариантНастроек = СхемаКомпоновки.ВариантыНастроек.Найти(КлючВарианта);
		Если НЕ ПредопределенныйВариантНастроек = Неопределено Тогда
			Если ОписаниеНастроек = Неопределено Тогда
				ОписаниеНастроек = Новый ОписаниеНастроек;
				ОписаниеНастроек.КлючОбъекта  = КлючОтчета;
				ОписаниеНастроек.КлючНастроек = КлючВарианта;
				ОписаниеНастроек.Пользователь = Пользователь;
			КонецЕсли;
			ОписаниеНастроек.Представление = ПредопределенныйВариантНастроек.Представление;
			ОписаниеНастроек.ДополнительныеСвойства.Вставить("ВариантЗагрузкиПериода", "ВариантСтандартногоПериода.ЭтотМесяц");
			Настройки = ПредопределенныйВариантНастроек.Настройки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик записи настроек варианта отчета
//
// Параметры:
//   КлючОтчета       (Строка) Полное имя отчета с точкой
//   КлючВарианта     (Строка) Ключ варианта отчета
//   Настройки        (*) Настройки варианта отчета
//   ОписаниеНастроек (ОписаниеНастроек, Неопределено)
//   Пользователь     (Строка, Неопределено) Имя пользователя ИБ
//       Не используется, так как подсистема "Варианты отчетов" не разделяет варианты по авторам.
//       Уникальность хранения и выборки гарантируется уникальностью пар ключей отчетов и вариантов.
//
Процедура ОбработкаСохранения(КлючОтчета, КлючВарианта, Настройки, ОписаниеНастроек, Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.КлючОтчета = &КлючОтчета И 
	|	ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ И 
	|	ВариантыОтчетов.КлючВарианта = &КлючВарианта";
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.Настройки = Новый ХранилищеЗначения(Настройки);
		Если ОписаниеНастроек <> Неопределено Тогда
			ВариантОбъект.Наименование = ОписаниеНастроек.Представление;
		КонецЕсли;
		ВариантОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Обработчик получения описания настроек варианта отчета
//
// Параметры:
//   КлючОтчета       (Строка) Полное имя отчета с точкой
//   КлючВарианта     (Строка) Ключ варианта отчета
//   ОписаниеНастроек (ОписаниеНастроек)
//   Пользователь     (Строка, Неопределено) Имя пользователя ИБ
//       Не используется, так как подсистема "Варианты отчетов" не разделяет варианты по авторам.
//       Уникальность хранения и выборки гарантируется уникальностью пар ключей отчетов и вариантов.
//
Процедура ОбработкаПолученияОписания(КлючОтчета, КлючВарианта, ОписаниеНастроек, Пользователь)
	
	Если ОписаниеНастроек = Неопределено Тогда
		ОписаниеНастроек = Новый ОписаниеНастроек;
	КонецЕсли;
	
	ОписаниеНастроек.КлючОбъекта  = КлючОтчета;
	ОписаниеНастроек.КлючНастроек = КлючВарианта;
	
	Если ТипЗнч(Пользователь) = Тип("Строка") Тогда
		ОписаниеНастроек.Пользователь = Пользователь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Варианты.Представление КАК Представление,
	|	Варианты.ПометкаУдаления,
	|	Варианты.ПользовательскаяНастройка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК Варианты
	|ГДЕ
	|	Варианты.КлючОтчета      = &КлючОтчета И 
	|	Варианты.ПометкаУдаления = ЛОЖЬ И 
	|	Варианты.КлючВарианта    = &КлючВарианта";
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ОписаниеНастроек.Представление = Выборка.Представление;
		ОписаниеНастроек.ДополнительныеСвойства.Вставить("ПометкаУдаления",           Выборка.ПометкаУдаления);
		ОписаниеНастроек.ДополнительныеСвойства.Вставить("ПользовательскаяНастройка", Выборка.ПользовательскаяНастройка);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик установки описания настроек варианта отчета
//
// Параметры:
//   КлючОтчета       (Строка) Полное имя отчета с точкой
//   КлючВарианта     (Строка) Ключ варианта отчета
//   ОписаниеНастроек (ОписаниеНастроек)
//   Пользователь     (Строка) Имя пользователя ИБ.
//       Не используется, так как подсистема "Варианты отчетов" не разделяет варианты по авторам.
//       Уникальность хранения и выборки гарантируется уникальностью пар ключей отчетов и вариантов.
//
Процедура ОбработкаУстановкиОписания(КлючОтчета, КлючВарианта, ОписаниеНастроек, Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Варианты.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК Варианты
	|ГДЕ
	|	Варианты.КлючОтчета   = &КлючОтчета И 
	|	Варианты.ПометкаУдаления = ЛОЖЬ И 
	|	Варианты.КлючВарианта = &КлючВарианта";
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.Наименование = ОписаниеНастроек.Представление;
		ВариантОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВариантыОтчета(КлючОтчета, МассивИсключений = Неопределено, КлючВарианта = Неопределено)
	
	Если МассивИсключений = Неопределено Тогда
		МассивИсключений = Новый Массив;
	КонецЕсли;
	
	Имя           = СтрЗаменить(КлючОтчета, "Отчет.", "");
	Представление = Метаданные.Отчеты[Имя].Синоним;
	
	ПользовательСеанса = ПараметрыСеанса.Пользователь;
	
	ОтчетОбъект = Отчеты[Имя].Создать();
	СхемаКомпоновки = ОтчетОбъект.СхемаКомпоновкиДанных;
	
	Для Каждого ТекВариантНастроек Из СхемаКомпоновки.ВариантыНастроек Цикл
		
		Если НЕ МассивИсключений.Найти(ТекВариантНастроек.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если (НЕ КлючВарианта = Неопределено) И (НЕ ТекВариантНастроек.Имя = КлючВарианта) Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйЭлемент = Справочники.ВариантыОтчетов.СоздатьЭлемент();
		НовыйЭлемент.Наименование           = ТекВариантНастроек.Представление;
		НовыйЭлемент.ИмяОтчета              = Имя;
		НовыйЭлемент.КлючОтчета             = КлючОтчета;
		НовыйЭлемент.ПредставлениеОтчета    = Представление;
		НовыйЭлемент.КлючВарианта           = ТекВариантНастроек.Имя;
		НовыйЭлемент.Автор                  = ПользовательСеанса;
		НовыйЭлемент.ЭтоПредопределенный    = Истина;
		НовыйЭлемент.НазначениеДоступа      = Перечисления.НазначениеПравИНастроек.Компания;
		НовыйЭлемент.Настройки              = Новый ХранилищеЗначения(ТекВариантНастроек.Настройки);
		НовыйЭлемент.ВариантЗагрузкиПериода = ПолучитьВариантПериода(КлючОтчета, ТекВариантНастроек.Имя);
		Попытка
			НовыйЭлемент.Записать();
			Сообщить("Создан вариант отчета <"+НовыйЭлемент.ПредставлениеОтчета+"-"+НовыйЭлемент.Наименование+">");
		Исключение
		КонецПопытки;
		
		Если НЕ КлючВарианта = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьПризнакПредопределенногоУВариантовОтчета(КлючОтчета, ТаблицаВариантов)
	
	Имя = СтрЗаменить(КлючОтчета, "Отчет.", "");
	
	ОтчетОбъект = Отчеты[Имя].Создать();
	СхемаКомпоновки = ОтчетОбъект.СхемаКомпоновкиДанных;
	
	Для Каждого ТекВариант Из ТаблицаВариантов Цикл
		Если СхемаКомпоновки.ВариантыНастроек.Найти(ТекВариант.КлючВарианта) = Неопределено Тогда
			ВариантОбъект = ТекВариант.Ссылка.ПолучитьОбъект();
			ВариантОбъект.ЭтоПредопределенный = Ложь;
			Попытка
				ВариантОбъект.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьВариантыОтчетов() Экспорт
	
	Сообщить("Обновление справочника ""Варианты отчетов""");
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
	               |	ВариантыОтчетов.Ссылка
	               |ИЗ
	               |	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	               |ГДЕ
	               |	ВариантыОтчетов.КлючОтчета = &КлючОтчета И 
	               |	ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ И 
	               |	ВариантыОтчетов.ЭтоПредопределенный";
	
   НачатьТранзакцию();
   Попытка
	   Для Каждого ОтчетПодсистемы Из Метаданные.Отчеты Цикл
		   // Использует общую управляемую форму отчета
		   Если НЕ (ОтчетПодсистемы.ОсновнаяФорма.ТипФормы = Метаданные.СвойстваОбъектов.ТипФормы.Управляемая) Тогда
			   Продолжить;
		   КонецЕсли;
		   
		   КлючОтчета    = ОтчетПодсистемы.ПолноеИмя();
		   Запрос.УстановитьПараметр("КлючОтчета", КлючОтчета);
		   ТаблицаВариантов = Запрос.Выполнить().Выгрузить();
		   МассивИсключений = ТаблицаВариантов.ВыгрузитьКолонку("КлючВарианта");
		   
		   СоздатьВариантыОтчета(КлючОтчета, МассивИсключений);
		   
		   ОбновитьПризнакПредопределенногоУВариантовОтчета(КлючОтчета, ТаблицаВариантов);
		   
	   КонецЦикла;
   Исключение
	   ОтменитьТранзакцию();
   КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
	Сообщить("Обновление справочника ""Варианты отчетов"" завершено.");
	
КонецПроцедуры

// Функция получает варианта отчета по ключу
//
// Параметры:
//   КлючОтчета       (Строка) Полное имя отчета с точкой
//   КлючВарианта     (Строка) Ключ варианта отчета
//
Функция ПолучитьВариантОтчета(КлючОтчета, КлючВарианта) Экспорт
	
	Результат = Справочники.ВариантыОтчетов.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Варианты.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК Варианты
	|ГДЕ
	|	Варианты.КлючОтчета   = &КлючОтчета И 
	|	Варианты.ПометкаУдаления = ЛОЖЬ И 
	|	Варианты.КлючВарианта = &КлючВарианта";
	Запрос.УстановитьПараметр("КлючОтчета",   КлючОтчета);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	Иначе
		СоздатьВариантыОтчета(КлючОтчета, , КлючВарианта);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли
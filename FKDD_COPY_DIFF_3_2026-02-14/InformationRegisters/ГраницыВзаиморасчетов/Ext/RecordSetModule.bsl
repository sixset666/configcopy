Перем ДоговорВзаиморасчетов Экспорт;   // Справочник.ДоговорыВзаиморасчетов ИЛИ Массив. Содержит договор(ы) по которым сдвигается граница последовательности
Перем МоментВремени Экспорт;   // Дата-время документа
Перем ДокументСсылка Экспорт;  // Ссылка на документ
Перем ДоговорВзаиморасчетовБыл Экспорт; // Договор(ы) до изменения
Перем МоментВремениБыл Экспорт;   // Дата-время документа до изменения

// проверяет, надо ли двигать границу последовательности и если надо - сдвигает
Функция ИзменитьГраницу(БезПроверок=Ложь) Экспорт
	
	МассивДоговоровНовые = Новый Массив();
	МассивДоговоровСтарые = Новый Массив();
	// фильтр на договоры
	МассивДоговоровФильтр = Новый Массив();
	Если ТипЗнч(ДоговорВзаиморасчетов)<>Тип("Массив") Тогда
		МассивДоговоровФильтр.Добавить(ДоговорВзаиморасчетов);
		МассивДоговоровНовые.Добавить(ДоговорВзаиморасчетов);
	Иначе
		Для Каждого Договор Из ДоговорВзаиморасчетов Цикл
			МассивДоговоровФильтр.Добавить(Договор);
			МассивДоговоровНовые.Добавить(Договор);
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДоговорВзаиморасчетовБыл) И ТипЗнч(ДоговорВзаиморасчетовБыл)<>Тип("Массив") И МассивДоговоровНовые.Найти(ДоговорВзаиморасчетовБыл)=Неопределено Тогда
		МассивДоговоровФильтр.Добавить(ДоговорВзаиморасчетовБыл);
		МассивДоговоровСтарые.Добавить(ДоговорВзаиморасчетовБыл);
	ИначеЕсли ЗначениеЗаполнено(ДоговорВзаиморасчетовБыл) Тогда
		Для Каждого Договор Из ДоговорВзаиморасчетовБыл Цикл
			Если МассивДоговоровНовые.Найти(Договор)=Неопределено Тогда
				МассивДоговоровФильтр.Добавить(Договор);
				МассивДоговоровСтарые.Добавить(Договор);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// получаем текущие границы
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	МоментВремени КАК МоментВремени,
	|	ДокументСсылка КАК ДокументСсылка
	|ИЗ
	|	РегистрСведений.ГраницыВзаиморасчетов
	|ГДЕ
	|	ДоговорВзаиморасчетов В(&ДоговорыВзаиморасчетов)
	|");
	Запрос.УстановитьПараметр("ДоговорыВзаиморасчетов",МассивДоговоровФильтр);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	МоментВремениДокумента = ?(ТипЗнч(МоментВремени)=Тип("МоментВремени") И ДокументСсылка<>Неопределено, Новый МоментВремени(Мин(ДокументСсылка.Дата, МоментВремени.Дата), ДокументСсылка), Новый МоментВремени(МоментВремени, ДокументСсылка));
	Если ЗначениеЗаполнено(МоментВремениБыл) Тогда
		МоментВремениДокумента = ?(МоментВремениДокумента.Сравнить(МоментВремениБыл)=1,МоментВремениБыл,МоментВремениДокумента);
	КонецЕсли;
	// идем по новым договорам и смотрим надо ли сдвинуть границу
	Для Каждого Договор Из МассивДоговоровНовые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(Договор,"ДоговорВзаиморасчетов");
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениДокумента) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьМенеджерЗаписи();
			НоваяЗапись.ДоговорВзаиморасчетов = Договор;
			НоваяЗапись.МоментВремени = МоментВремениДокумента.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениДокумента.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// теперь тот-же проход, но по старым договорам
	Для Каждого Договор Из МассивДоговоровСтарые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(Договор,"ДоговорВзаиморасчетов");
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениБыл) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьМенеджерЗаписи();
			НоваяЗапись.ДоговорВзаиморасчетов = Договор;
			НоваяЗапись.МоментВремени = МоментВремениБыл.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениБыл.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// убираем циклические ссылки
	ДокументСсылка = Неопределено;
	
	Возврат Истина;
КонецФункции

// вызывается при непосредственном удалении проведенного документа
// проверяет есть ли граница с таким документом, если есть, то очищает ссылку
Процедура УбратьСсылку(Ссылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ДоговорВзаиморасчетов,
	|	МоментВремени
	|ИЗ
	|	РегистрСведений.ГраницыВзаиморасчетов 
	|ГДЕ
	|	ДокументСсылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = РегистрыСведений.ГраницыВзаиморасчетов.СоздатьМенеджерЗаписи();
			НоваяЗапись.ДоговорВзаиморасчетов = Выборка.ДоговорВзаиморасчетов;
			НоваяЗапись.МоментВремени = Выборка.МоментВремени;
			НоваяЗапись.ДокументСсылка = Неопределено;
			НоваяЗапись.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
Перем Автомобиль Экспорт;   // Справочник.Автомобили ИЛИ Массив. Содержит автомобили по которым сдвигается граница последовательности
Перем АвтомобильБыл Экспорт;   // Справочник.Автомобили ИЛИ Массив. Содержит автомобили по которым сдвигается граница последовательности
Перем МоментВремени Экспорт;   // Дата-время документа
Перем МоментВремениБыл Экспорт;   // Дата-время документа до изменения
Перем ДокументСсылка Экспорт;  // Ссылка на документ

// проверяет, надо ли двигать границу последовательности и если надо - сдвигает
Функция ИзменитьГраницу(БезПроверок=Ложь) Экспорт
	
	// если Автомобиль один, то преобразуем его в массив
	МассивАвтомобилейНовые = Новый Массив();
	МассивАвтомобилейСтарые = Новый Массив();
	// фильтр на автомобили
	МассивАвтомобилейФильтр = Новый Массив();
	Если ТипЗнч(Автомобиль)<>Тип("Массив") Тогда
		МассивАвтомобилейНовые.Добавить(Автомобиль);
		МассивАвтомобилейФильтр.Добавить(Автомобиль);
	Иначе
		Для Каждого ТекАвтомобиль Из Автомобиль Цикл
			МассивАвтомобилейНовые.Добавить(ТекАвтомобиль);
			МассивАвтомобилейФильтр.Добавить(ТекАвтомобиль);
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(АвтомобильБыл) И ТипЗнч(АвтомобильБыл)<>Тип("Массив") И МассивАвтомобилейНовые.Найти(АвтомобильБыл)=Неопределено Тогда
		МассивАвтомобилейСтарые.Добавить(АвтомобильБыл);
		МассивАвтомобилейФильтр.Добавить(АвтомобильБыл);
	ИначеЕсли ЗначениеЗаполнено(АвтомобильБыл) Тогда
		Для Каждого ТекАвтомобиль Из АвтомобильБыл Цикл
			Если МассивАвтомобилейНовые.Найти(ТекАвтомобиль)=Неопределено Тогда
				МассивАвтомобилейСтарые.Добавить(ТекАвтомобиль);
				МассивАвтомобилейФильтр.Добавить(ТекАвтомобиль);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// получаем текущие границы
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Автомобиль КАК Автомобиль,
	|	МоментВремени КАК МоментВремени,
	|	ДокументСсылка КАК ДокументСсылка
	|ИЗ
	|	РегистрСведений.ГраницыЗаказыНаАвтомобили
	|ГДЕ
	|	Автомобиль В(&Автомобили)

	|");
	Запрос.УстановитьПараметр("Автомобили",МассивАвтомобилейФильтр);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	МоментВремениДокумента = ?(ТипЗнч(МоментВремени)=Тип("МоментВремени") И ДокументСсылка<>Неопределено, Новый МоментВремени(Мин(ДокументСсылка.Дата, МоментВремени.Дата), ДокументСсылка), Новый МоментВремени(МоментВремени, ДокументСсылка));
	Если ЗначениеЗаполнено(МоментВремениБыл) Тогда
		МоментВремениДокумента = ?(МоментВремениДокумента.Сравнить(МоментВремениБыл)=1,МоментВремениБыл,МоментВремениДокумента);
	КонецЕсли;
	// идем по новым автомобилям и смотрим надо ли сдвинуть границу
	Для Каждого СледующийАвтомобиль Из МассивАвтомобилейНовые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(СледующийАвтомобиль,"Автомобиль");
	
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениДокумента) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ЗначениеЗаполнено(СледующийАвтомобиль) И (ДвигаемГраницу ИЛИ БезПроверок) Тогда
			НоваяЗапись = РегистрыСведений.ГраницыЗаказыНаАвтомобили.СоздатьМенеджерЗаписи();
			НоваяЗапись.Автомобиль = СледующийАвтомобиль;
			НоваяЗапись.МоментВремени = МоментВремениДокумента.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениДокумента.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// теперь тот-же проход, но по старым автомобилям
	Для Каждого СледующийАвтомобиль Из МассивАвтомобилейСтарые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(СледующийАвтомобиль,"Автомобиль");
		
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениБыл) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ЗначениеЗаполнено(СледующийАвтомобиль) И (ДвигаемГраницу ИЛИ БезПроверок) Тогда
			НоваяЗапись = РегистрыСведений.ГраницыЗаказыНаАвтомобили.СоздатьМенеджерЗаписи();
			НоваяЗапись.Автомобиль = СледующийАвтомобиль;
			НоваяЗапись.МоментВремени = МоментВремениБыл.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениБыл.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// убираем циклические ссылки
	ДокументСсылка = Неопределено;
	
	Возврат Истина;
КонецФункции

// вызывается при непосредственном удалении проведенного документа
// проверяет есть ли граница с таким документом, если есть, то очищает ссылку
Процедура УбратьСсылку(Ссылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	Автомобиль КАК Автомобиль,
	|	МоментВремени КАК МоментВремени 
	|ИЗ
	|	РегистрСведений.ГраницыЗаказыНаАвтомобили 
	|ГДЕ
	|	ДокументСсылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = РегистрыСведений.ГраницыЗаказыНаАвтомобили.СоздатьМенеджерЗаписи();
			НоваяЗапись.Автомобиль = Выборка.Автомобиль;
			НоваяЗапись.МоментВремени = Выборка.МоментВремени;
			НоваяЗапись.ДокументСсылка = Неопределено;
			НоваяЗапись.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры


Процедура ДобавитьЗаписиВРегистр(
		Ссылка,
		Товары,
		КодыМаркировки,
		Состояние,
		ОписаниеОшибки,
		ДобавитьШтрихКод = Ложь,
		ДляПечати = Ложь) Экспорт
	
	НаборЗаписей = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(Ссылка);
	
	// Таблица полученных кодов маркировки
	ТаблицаКодовМаркировки = Новый ТаблицаЗначений();
	ТаблицаКодовМаркировки.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаКодовМаркировки.Колонки.Добавить(
		"ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаКодовМаркировки.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка"));
	
	Организация = Ссылка.Организация;
	
	НаборЗаписей.Прочитать();
	ТаблицаНабора = НаборЗаписей.Выгрузить();
	
	ОбъектШтрихКоды = Обработки.ШтрихКоды.Создать();
	СимволГруппировки = ОбъектШтрихКоды.РазделительGS1();
	СимволЭкранирования = ОбъектШтрихКоды.ЭкранированныйСимволGS1();
	
	Если ТипЗнч(КодыМаркировки) = Тип("Массив") Тогда
		
		УдалениеНеЗагруженных = Новый Массив;
		
		Для Инд = 0 По КодыМаркировки.Количество() - 1 Цикл
			
			ТекущийКодМаркировки = КодыМаркировки[Инд];
			КодыМаркировки[Инд] = СтрЗаменить(КодыМаркировки[Инд], СимволГруппировки, СимволЭкранирования);
			
			// Найдем номенклатуру по GTIM
			СтруктураМаркировки = ОбъектШтрихКоды.РазобратьСтрокуШтрихкодаGS1(КодыМаркировки[Инд]);
			Если НЕ СтруктураМаркировки.Разобран Тогда
				ОписаниеОшибки = ОписаниеОшибки + Символы.ПС +
					"Некорректно указан код маркировки """+ТекущийКодМаркировки+""". Код маркировки не загружен.";
				УдалениеНеЗагруженных.Добавить(КодыМаркировки[Инд]);
				Продолжить;
			КонецЕсли;
			
			ШК = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
			СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
			
			Если ДляПечати Тогда
				КодМаркировкиПоиска = СтрШаблон("(01)%1(21)%2", ШК, СерийныйНомер);
			Иначе
				КодМаркировкиПоиска = КодыМаркировки[Инд];
			КонецЕсли;
			
			// Проверим, что ранее не было записи с таким кодом маркировки
			Если ТаблицаНабора.Найти(КодМаркировкиПоиска, "КодМаркировки") <> Неопределено Тогда
				ОписаниеОшибки = ОписаниеОшибки + Символы.ПС +
					"Код маркировки """+КодМаркировкиПоиска+""" ранее был загружен.";
				УдалениеНеЗагруженных.Добавить(КодыМаркировки[Инд]);
				Продолжить;
			КонецЕсли;
			
			Номенклатура = Неопределено;
			Характеристика = Неопределено;
			
			Если Товары <> Неопределено Тогда
				СтрокаТоваров = Товары.Найти(ШК, "GTIN");
				Если СтрокаТоваров <> Неопределено Тогда
					Номенклатура = СтрокаТоваров.Номенклатура;
					Характеристика = СтрокаТОваров.ХарактеристикаНоменклатуры;
				Иначе
					ОписаниеОшибки = ОписаниеОшибки + Символы.ПС +
						"Код маркировки """+КодМаркировкиПоиска+""" не соответствует заказанным по документу. Код маркировки не загружен.";
					УдалениеНеЗагруженных.Добавить(КодыМаркировки[Инд]);
					Продолжить;
				КонецЕсли;
			Иначе
				ШК = Прав(ШК, 13);
				Номенклатура = ОбъектШтрихКоды.НайтиПоШтрихКоду(ШК);
				Если Номенклатура = Неопределено Тогда
					ОписаниеОшибки = ОписаниеОшибки + Символы.ПС +
						"Для данного кода маркировки нет номенклатуры в базе. Отменена ее загрузка.";
					УдалениеНеЗагруженных.Добавить(КодыМаркировки[Инд]);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаНабора.Добавить();
			НоваяСтрока.Период = ТекущаяДатаСеанса();
			НоваяСтрока.ДокументОснование = Ссылка;
			НоваяСтрока.Организация = Организация;
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = Характеристика;
			НоваяСтрока.GTIN = ШК;
			НоваяСтрока.СерийныйНомер = СерийныйНомер;
			НоваяСтрока.Состояние = Состояние;
			
			Если ДляПечати Тогда
				НоваяСтрока.КодМаркировки = СтрШаблон("(01)%1(21)%2", НоваяСтрока.GTIN, НоваяСтрока.СерийныйНомер);
			Иначе
				НоваяСтрока.КодМаркировки = КодыМаркировки[Инд];
			КонецЕсли;
			
			НоваяСтрока = ТаблицаКодовМаркировки.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = Характеристика;
			НоваяСтрока.КодМаркировки = КодыМаркировки[Инд];
			
		КонецЦикла;
		
		Для Каждого ТекущаяМаркировка Из УдалениеНеЗагруженных Цикл
			КодыМаркировки.Удалить(КодыМаркировки.Найти(ТекущаяМаркировка));
		КонецЦикла;
		
	Иначе
		СтруктураПоиска = Новый Структура("ИдентификаторТовара");
		Для Каждого ТекущаяСтрока Из Товары Цикл
			СтруктураПоиска.ИдентификаторТовара = ТекущаяСтрока.ИдентификаторТовара;
			НайденныеСтроки = КодыМаркировки.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого ТекущаяМаркировка Из НайденныеСтроки Цикл
				
				// Найдем номенклатуру по GTIM
				СтруктураМаркировки = ОбъектШтрихКоды.РазобратьСтрокуШтрихкодаGS1(КодыМаркировки[Инд]);
				Если НЕ СтруктураМаркировки.Разобран Тогда
					ОписаниеОшибки = ОписаниеОшибки + Символы.ПС +
					"Некорректно указан код маркировки """+ТекущийКодМаркировки+""".";
					Продолжить;
				КонецЕсли;
				
				ШК = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
				СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
				
				НоваяСтрока = ТаблицаНабора.Добавить();
				НоваяСтрока.Период = ТекущаяДатаСеанса();
				НоваяСтрока.ДокументОснование = Ссылка;
				НоваяСтрока.Организация = Организация;
				НоваяСтрока.Номенклатура = ТекущаяСтрока.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатуры;
				НоваяСтрока.GTIN = ШК;
				НоваяСтрока.СерийныйНомер = СерийныйНомер;
				НоваяСтрока.Состояние = Состояние;
				
				Если ДляПечати Тогда
					НоваяСтрока.КодМаркировки = СтрШаблон("(01)%1(21)%2", НоваяСтрока.GTIN, НоваяСтрока.СерийныйНомер);
				Иначе
					НоваяСтрока.КодМаркировки = ТекущаяМаркировка.КодМаркировки;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаКодовМаркировки.Добавить();
				НоваяСтрока.Номенклатура = ТекущаяСтрока.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатуры;
				НоваяСтрока.КодМаркировки = ТекущаяМаркировка.КодМаркировки;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	Если ДобавитьШтрихКод Тогда
		ТаблицаНоменклатуры = ТаблицаНабора.Скопировать();
		ТаблицаНоменклатуры.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,GTIN");
		ЗаписатьНовыеШтрихКоды(ТаблицаНоменклатуры);
	КонецЕсли;
	
	НаборЗаписей.Загрузить(ТаблицаНабора);
	НаборЗаписей.Записать();
	
	// Заполним регистр для печати кодов маркировок
	Если ДляПечати И ТаблицаКодовМаркировки.Количество() > 0 Тогда
		УправлениеКодамиМаркировкиКлиентСервер.ДобавитьЗаписиВРегистр(Ссылка, ТаблицаКодовМаркировки);
	КонецЕсли;
	
КонецПроцедуры

Функция КодМаркировкиЗапрещенДляВывода(ТаблицаМаркировок, ДокументОбъект, РежимПроведения) Экспорт
	
	Результат = Ложь;
	ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах(,ДокументОбъект).Имя;
	СписокСостоянийВводаВОборот = Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки();
	
	ТаблицаРазбораМаркировки = Новый ТаблицаЗначений;
	ТаблицаРазбораМаркировки.Колонки.Добавить("GTIN", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	ТаблицаРазбораМаркировки.Колонки.Добавить("СерийныйНомер", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	ТаблицаРазбораМаркировки.Колонки.Добавить("КодМаркировки", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(200)));
	
	ОбъектШК = Обработки.ШтрихКоды.Создать();
	
	// Разберем маркировку на состовляющие для поиска
	Для Каждого ТекущаяСтрока Из ТаблицаМаркировок Цикл
		
		СтруктураМаркировки = ОбъектШК.РазобратьСтрокуШтрихкодаGS1(ТекущаяСтрока.КодМаркировки);
		
		Если НЕ СтруктураМаркировки.Разобран Тогда
			дкСообщитьРезультат("Код маркировки <"+СокрЛП(ТекущаяСтрока.КодМаркировки)+"> не разобран. Проверьте его корректность.","Важное&Состояние кодов маркировки:",ДокументОбъект);
			Результат = Истина;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРазбораМаркировки.Добавить();
		НоваяСтрока.GTIN = СтруктураМаркировки.ДанныеШтрихкода["01"].Значение;
		НоваяСтрока.СерийныйНомер = СтруктураМаркировки.ДанныеШтрихкода["21"].Значение;
		НоваяСтрока.КодМаркировки = ТекущаяСтрока.КодМаркировки;
		
	КонецЦикла;
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
		МоментВремени = Неопределено;
	ИначеЕсли ТипЗнч(ДокументОбъект.Ссылка) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		
		Если ДокументОбъект.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Закрыт Тогда 
			МоментВремени = Новый МоментВремени(ДокументОбъект.ДатаЗакрытия, ДокументОбъект.Ссылка);
		ИначеЕсли ДокументОбъект.Состояние = Справочники.ВидыСостоянийЗаказНарядов.Выполнен Тогда
			МоментВремени = Новый МоментВремени(ДокументОбъект.ДатаОкончания, ДокументОбъект.Ссылка);
		Иначе
			МоментВремени = ДокументОбъект.МоментВремени();
		КонецЕсли;
		
	Иначе
		МоментВремени = ДокументОбъект.МоментВремени();
	КонецЕсли;
	
	// Получим текущие статусы маркировки
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписокКодовМаркировок.GTIN КАК GTIN,
	               |	СписокКодовМаркировок.СерийныйНомер КАК СерийныйНомер,
	               |	СписокКодовМаркировок.КодМаркировки КАК КодМаркировки
	               |ПОМЕСТИТЬ ТаблицаМаркировок
	               |ИЗ
	               |	&СписокКодовМаркировок КАК СписокКодовМаркировок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СостоянияКодовМаркировкиСрезПоследних.Период КАК Период,
	               |	СостоянияКодовМаркировкиСрезПоследних.Организация.ИНН КАК ИННОрганизации,
	               |	СостоянияКодовМаркировкиСрезПоследних.Номенклатура КАК Номенклатура,
	               |	СостоянияКодовМаркировкиСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаМаркировок.КодМаркировки КАК КодМаркировки,
	               |	СостоянияКодовМаркировкиСрезПоследних.Состояние КАК Состояние
	               |ПОМЕСТИТЬ ТаблицаСостоянийКодовМаркировок
	               |ИЗ
	               |	ТаблицаМаркировок КАК ТаблицаМаркировок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияКодовМаркировки.СрезПоследних(
	               |				&МоментВремени,
	               |				(GTIN, СерийныйНомер) В
	               |					(ВЫБРАТЬ
	               |						ТаблицаМаркировок.GTIN КАК GTIN,
	               |						ТаблицаМаркировок.СерийныйНомер КАК СерийныйНомер
	               |					ИЗ
	               |						ТаблицаМаркировок КАК ТаблицаМаркировок)) КАК СостоянияКодовМаркировкиСрезПоследних
	               |		ПО ТаблицаМаркировок.GTIN = СостоянияКодовМаркировкиСрезПоследних.GTIN
	               |			И ТаблицаМаркировок.СерийныйНомер = СостоянияКодовМаркировкиСрезПоследних.СерийныйНомер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки КАК КодМаркировки,
	               |	МАКСИМУМ(ТаблицаСостоянийКодовМаркировок.Период) КАК Период
	               |ПОМЕСТИТЬ ТаблицаПоследнегоПериода
	               |ИЗ
	               |	ТаблицаСостоянийКодовМаркировок КАК ТаблицаСостоянийКодовМаркировок
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаСостоянийКодовМаркировок.ИННОрганизации КАК ИННОрганизации,
	               |	ТаблицаСостоянийКодовМаркировок.Номенклатура КАК Номенклатура,
	               |	ТаблицаСостоянийКодовМаркировок.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки КАК КодМаркировки,
	               |	ТаблицаСостоянийКодовМаркировок.Состояние КАК Состояние
	               |ИЗ
	               |	ТаблицаСостоянийКодовМаркировок КАК ТаблицаСостоянийКодовМаркировок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоследнегоПериода КАК ТаблицаПоследнегоПериода
	               |		ПО ТаблицаСостоянийКодовМаркировок.Период = ТаблицаПоследнегоПериода.Период
	               |			И ТаблицаСостоянийКодовМаркировок.КодМаркировки = ТаблицаПоследнегоПериода.КодМаркировки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МаркировкаТоваровВПроизводствеОстатки.ЗаказНаряд КАК ЗаказНаряд,
	               |	МаркировкаТоваровВПроизводствеОстатки.Номенклатура КАК Номенклатура,
	               |	МаркировкаТоваровВПроизводствеОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	МаркировкаТоваровВПроизводствеОстатки.GTIN КАК GTIN,
	               |	МаркировкаТоваровВПроизводствеОстатки.СерийныйНомер КАК СерийныйНомер,
	               |	МаркировкаТоваровВПроизводствеОстатки.КоличествоОстаток КАК КоличествоОстаток,
	               |	ТаблицаМаркировок.КодМаркировки КАК КодМаркировки
	               |ИЗ
	               |	ТаблицаМаркировок КАК ТаблицаМаркировок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.МаркировкаТоваровВПроизводстве.Остатки(
	               |				&МоментВремени,
	               |				(GTIN, СерийныйНомер) В
	               |					(ВЫБРАТЬ
	               |						ТаблицаМаркировок.GTIN КАК GTIN,
	               |						ТаблицаМаркировок.СерийныйНомер КАК СерийныйНомер
	               |					ИЗ
	               |						ТаблицаМаркировок КАК ТаблицаМаркировок)) КАК МаркировкаТоваровВПроизводствеОстатки
	               |		ПО ТаблицаМаркировок.GTIN = МаркировкаТоваровВПроизводствеОстатки.GTIN
	               |			И ТаблицаМаркировок.СерийныйНомер = МаркировкаТоваровВПроизводствеОстатки.СерийныйНомер";
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Документ", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("КодыМаркировки", ТаблицаМаркировок.ВыгрузитьКолонку("КодМаркировки"));
	Запрос.УстановитьПараметр("СписокКодовМаркировок", ТаблицаРазбораМаркировки);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	Выборка = ПакетЗапросов[3].Выбрать();
	
	ИННОрганизации = ДокументОбъект.Организация.ИНН;
	Пока Выборка.Следующий() Цикл
		Если ИННОрганизации <> Выборка.ИННОрганизации Тогда
			//Если осуществляется снятие резерва, снятие резерва не должно превышать остаток резерва
			Если обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
				дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> принадлежит другой организации.","Важное&Состояние кодов маркировки:",ДокументОбъект,Выборка.Номенклатура);
			Иначе
				дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с характеристикой <"+СокрЛП(Выборка.ХарактеристикаНоменклатуры)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> принадлежит другой организации.","Важное&Состояние кодов маркировки:",ДокументОбъект,Выборка.Номенклатура);
			КонецЕсли;
			Результат = Истина;
		ИначеЕсли СписокСостоянийВводаВОборот.Найти(Выборка.Состояние) = Неопределено Тогда
			//Если осуществляется снятие резерва, снятие резерва не должно превышать остаток резерва
			Если обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
				дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> не был введен в оборот или был выведен из оборота ранее.","Важное&Состояние кодов маркировки:",ДокументОбъект,Выборка.Номенклатура);
			Иначе
				дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с характеристикой <"+СокрЛП(Выборка.ХарактеристикаНоменклатуры)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> не был введен в оборот или был выведен из оборота ранее.","Важное&Состояние кодов маркировки:",ДокументОбъект,Выборка.Номенклатура);
			КонецЕсли;
			Результат = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Выборка = ПакетЗапросов[4].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ТипЗнч(ДокументОбъект.Ссылка) = Тип("ДокументСсылка.ЗаказНаряд")
			И ДокументОбъект.Ссылка = Выборка.ЗаказНаряд Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.КоличествоОстаток > 0 Тогда
			Если обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
				дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+
					"> ранее перемещен в производство для заказ-наряда <"+СокрЛП(Выборка.ЗаказНаряд)+">",, ДокументОбъект);
			Иначе
				дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с характеристикой <"+СокрЛП(Выборка.ХарактеристикаНоменклатуры)+
					"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> ранее перемещен в производство для заказ-наряда <"+СокрЛП(Выборка.ЗаказНаряд)+">",, ДокументОбъект);
			КонецЕсли; 
			Результат = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция КодМаркировкиВведенВОборот(ТаблицаМаркировок, ДокументОбъект, РежимПроведения) Экспорт
	
	КодыРанееВведеныВОборот = Ложь;
	
	// Удалим введенные в оборот коды маркировки
	НаборЗаписейСостоянияКодовМаркировки = РегистрыСведений.СостоянияКодовМаркировки.СоздатьНаборЗаписей();
	НаборЗаписейСостоянияКодовМаркировки.ДокументОбъект = ДокументОбъект.Ссылка;
	НаборЗаписейСостоянияКодовМаркировки.ОтменаПроведения();
	
	// Проверим, что на текущий момент маркировка не введена в оборот
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СостоянияКодовМаркировкиСрезПоследних.Период КАК Период,
	               |	СостоянияКодовМаркировкиСрезПоследних.КодМаркировки КАК КодМаркировки,
	               |	СостоянияКодовМаркировкиСрезПоследних.Состояние КАК Состояние,
	               |	СостоянияКодовМаркировкиСрезПоследних.Номенклатура КАК Номенклатура,
	               |	СостоянияКодовМаркировкиСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |ПОМЕСТИТЬ СостоянияКодовМаркировки
	               |ИЗ
	               |	РегистрСведений.СостоянияКодовМаркировки.СрезПоследних(&МоментВремени, КодМаркировки В (&КодыМаркировки)) КАК СостоянияКодовМаркировкиСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СостоянияКодовМаркировки.КодМаркировки КАК КодМаркировки,
	               |	МАКСИМУМ(СостоянияКодовМаркировки.Период) КАК Период
	               |ПОМЕСТИТЬ ПоследнееСостояние
	               |ИЗ
	               |	СостоянияКодовМаркировки КАК СостоянияКодовМаркировки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СостоянияКодовМаркировки.КодМаркировки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СостоянияКодовМаркировки.КодМаркировки КАК КодМаркировки,
	               |	СостоянияКодовМаркировки.Номенклатура КАК Номенклатура,
	               |	СостоянияКодовМаркировки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СостоянияКодовМаркировки.Состояние КАК Состояние
	               |ИЗ
	               |	СостоянияКодовМаркировки КАК СостоянияКодовМаркировки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследнееСостояние КАК ПоследнееСостояние
	               |		ПО СостоянияКодовМаркировки.КодМаркировки = ПоследнееСостояние.КодМаркировки
	               |			И СостоянияКодовМаркировки.Период = ПоследнееСостояние.Период
	               |ГДЕ
	               |	СостоянияКодовМаркировки.Состояние В(&СписокСостоянийВОбороте)";
	Запрос.УстановитьПараметр("МоментВремени", ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, ДокументОбъект.МоментВремени()));
	Запрос.УстановитьПараметр("КодыМаркировки", ТаблицаМаркировок.ВыгрузитьКолонку("КодМаркировки"));
	Запрос.УстановитьПараметр("СписокСостоянийВОбороте", Перечисления.СостоянияКодовМаркировки.СостоянияВводаВОборотМаркировки());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если обЗначениеНеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
			дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> введен в оборот ранее.","Важное&Состояние кодов маркировки:", ДокументОбъект, Выборка.Номенклатура);
		Иначе
			дкСообщитьРезультат("Товар <"+СокрЛП(Выборка.Номенклатура)+"> с характеристикой <"+СокрЛП(Выборка.ХарактеристикаНоменклатуры)+"> с маркировкой <"+СокрЛП(Выборка.КодМаркировки)+"> введен в оборот ранее.","Важное&Состояние кодов маркировки:", ДокументОбъект, Выборка.Номенклатура);
		КонецЕсли;
		КодыРанееВведеныВОборот = Истина;
	КонецЦикла;
	
	Возврат КодыРанееВведеныВОборот;
	
КонецФункции

Функция ТекущиеСтатусыКодовМаркировки(СписокКодовМаркировок, НаДату) Экспорт
	
	ЕстьКодМаркировкиBase64 = (СписокКодовМаркировок.Колонки.Найти("КодМаркировкиBase64") <> Неопределено);
	
	// Добавим колонку с полным кодом маркировки
	Если НЕ ЕстьКодМаркировкиBase64 Тогда
		
		СписокКодовМаркировок.Колонки.Добавить("КодМаркировкиBase64", Новый ОписаниеТипов("Строка"));
		
	КонецЕсли;
	
	// Получим текущие статусы маркировки
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписокКодовМаркировок.GTIN КАК GTIN,
	               |	СписокКодовМаркировок.СерийныйНомер КАК СерийныйНомер,
	               |	СписокКодовМаркировок.КодМаркировки КАК КодМаркировки,
	               |	СписокКодовМаркировок.КодМаркировкиBase64 КАК КодМаркировкиBase64
	               |ПОМЕСТИТЬ ТаблицаМаркировок
	               |ИЗ
	               |	&СписокКодовМаркировок КАК СписокКодовМаркировок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СостоянияКодовМаркировкиСрезПоследних.Период КАК Период,
	               |	СостоянияКодовМаркировкиСрезПоследних.Номенклатура КАК Номенклатура,
	               |	СостоянияКодовМаркировкиСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаМаркировок.КодМаркировки КАК КодМаркировки,
	               |	ТаблицаМаркировок.КодМаркировкиBase64 КАК КодМаркировкиBase64,
	               |	СостоянияКодовМаркировкиСрезПоследних.Состояние КАК Состояние
	               |ПОМЕСТИТЬ ТаблицаСостоянийКодовМаркировок
	               |ИЗ
	               |	ТаблицаМаркировок КАК ТаблицаМаркировок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияКодовМаркировки.СрезПоследних(
	               |				&Дата,
	               |				(GTIN, СерийныйНомер) В
	               |					(ВЫБРАТЬ
	               |						ТаблицаМаркировок.GTIN КАК GTIN,
	               |						ТаблицаМаркировок.СерийныйНомер КАК СерийныйНомер
	               |					ИЗ
	               |						ТаблицаМаркировок КАК ТаблицаМаркировок)) КАК СостоянияКодовМаркировкиСрезПоследних
	               |		ПО ТаблицаМаркировок.GTIN = СостоянияКодовМаркировкиСрезПоследних.GTIN
	               |			И ТаблицаМаркировок.СерийныйНомер = СостоянияКодовМаркировкиСрезПоследних.СерийныйНомер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки КАК КодМаркировки,
	               |	МАКСИМУМ(ТаблицаСостоянийКодовМаркировок.Период) КАК Период
	               |ПОМЕСТИТЬ ТаблицаПоследнегоПериода
	               |ИЗ
	               |	ТаблицаСостоянийКодовМаркировок КАК ТаблицаСостоянийКодовМаркировок
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаСостоянийКодовМаркировок.Номенклатура КАК Номенклатура,
	               |	ТаблицаСостоянийКодовМаркировок.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаСостоянийКодовМаркировок.КодМаркировки КАК КодМаркировки,
	               |	ТаблицаСостоянийКодовМаркировок.КодМаркировкиBase64 КАК КодМаркировкиBase64,
	               |	ТаблицаСостоянийКодовМаркировок.Состояние КАК Состояние
	               |ИЗ
	               |	ТаблицаСостоянийКодовМаркировок КАК ТаблицаСостоянийКодовМаркировок
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоследнегоПериода КАК ТаблицаПоследнегоПериода
	               |		ПО ТаблицаСостоянийКодовМаркировок.Период = ТаблицаПоследнегоПериода.Период
	               |			И ТаблицаСостоянийКодовМаркировок.КодМаркировки = ТаблицаПоследнегоПериода.КодМаркировки";
	Запрос.УстановитьПараметр("СписокКодовМаркировок", СписокКодовМаркировок);
	Запрос.УстановитьПараметр("Дата", НаДату);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаписатьНовыеШтрихКоды(СписокШтрихКодов)
	
	Для Каждого ТекущаяСтрока Из СписокШтрихКодов Цикл
		
		ТекущийGTIN = ТекущаяСтрока.GTIN;
		
		Пока Лев(ТекущийGTIN, 1) = "0" Цикл
			ТекущийGTIN = Прав(ТекущийGTIN, СтрДлина(ТекущийGTIN) - 1);
		КонецЦикла;
		
		МенеджерЗаписи = РегистрыСведений.ШтрихКоды.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = ТекущаяСтрока.Номенклатура;
		МенеджерЗаписи.ХарактеристикаНоменклатуры = ТекущаяСтрока.ХарактеристикаНоменклатуры;
		МенеджерЗаписи.ШтрихКод = ТекущийGTIN;
		МенеджерЗаписи.GTIN = Истина;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
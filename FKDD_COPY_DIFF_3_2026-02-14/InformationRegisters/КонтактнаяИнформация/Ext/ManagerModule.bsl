             //<<БАА 08.10.23
Процедура ОбновитьКонтИнфоКонтрагента(Контрагент, БезИстории = Ложь, БезПроверок = Ложь) Экспорт
	
	ТЧ = Контрагент.БТ_КонтактнаяИнформация.Выгрузить();
	ТЧ.Колонки.Добавить("Проверена", Новый ОписаниеТипов("Булево"));
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КонтактнаяИнформация.Тип КАК Тип,
	                      |	КонтактнаяИнформация.Вид КАК Вид,
	                      |	КонтактнаяИнформация.Представление КАК Представление,
	                      |	КонтактнаяИнформация.Поле1 КАК Поле1,
	                      |	КонтактнаяИнформация.Поле2 КАК Поле2,
	                      |	КонтактнаяИнформация.Поле4 КАК Поле4,
	                      |	КонтактнаяИнформация.Поле3 КАК Поле3,
	                      |	КонтактнаяИнформация.Поле5 КАК Поле5,
	                      |	КонтактнаяИнформация.Поле6 КАК Поле6,
	                      |	КонтактнаяИнформация.Поле7 КАК Поле7,
	                      |	КонтактнаяИнформация.Поле8 КАК Поле8,
	                      |	КонтактнаяИнформация.Поле9 КАК Поле9,
	                      |	КонтактнаяИнформация.Поле10 КАК Поле10,
	                      |	КонтактнаяИнформация.Поле11 КАК Поле11,
	                      |	КонтактнаяИнформация.Поле12 КАК Поле12
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Объект = &Объект");
	Запрос.УстановитьПараметр("Объект", Контрагент);
	
	Изменен = Ложь;
	КонтрагентОбъект = Контрагент.ПолучитьОбъект(); 

	ТЗНаборКИ = ЗАпрос.Выполнить().Выгрузить();
	Отказ = Ложь;
	
	Если Не БезПроверок Тогда
		КонтрагентОбъект.ПроверкаКонтактнойИнформации(ТЗНаборКИ, Отказ, Истина);
		
		Если Отказ Тогда
			
			Возврат;
			
		КонецЕсли;	
	КонецЕсли;	
	
	//Выборка = Запрос.Выполнить().Выбрать();
	Для Каждого Выборка Из ТЗНаборКИ Цикл
		
		СтруктураПоиска = Новый Структура("Тип, Вид", Выборка.Тип, Выборка.Вид);
		СтрокаПоиска = ТЧ.НайтиСтроки(СтруктураПоиска);
		
		Если СтрокаПоиска.Количество() = 0 Тогда
			
			Изменен = Истина;
			НоваяСтрока = КонтрагентОбъект.БТ_КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка); 
			
		Иначе
			
			СтрокаПоиска[0].Проверена = Истина;
			Если СтрокаПоиска[0].Представление <> Выборка.Представление Тогда
				
				СтрокаПоискаКонтрагента = КонтрагентОбъект.БТ_КонтактнаяИнформация.НайтиСтроки(СтруктураПоиска);
				Если СтрокаПоискаКонтрагента.Количество() Тогда
					
					СтрокаПоискаКонтрагента[0].Представление = Выборка.Представление;
					Изменен = Истина;
					
				КонецЕсли;	
				
			КонецЕсли;	
			
		КонецЕсли;	
	КонецЦикла;	
	
	МассивУдаленных = ТЧ.НайтиСтроки(Новый Структура("Проверена", Ложь));
		
	Для Каждого СтрокаНаУдаление Из МассивУдаленных Цикл
		
		СтрокаПоискаКонтрагента = КонтрагентОбъект.БТ_КонтактнаяИнформация.НайтиСтроки(Новый Структура("Тип, Вид", СтрокаНаУдаление.Тип, СтрокаНаУдаление.Вид));
		Если СтрокаПоискаКонтрагента.Количество() Тогда
			
			КонтрагентОбъект.БТ_КонтактнаяИнформация.Удалить(СтрокаПоискаКонтрагента[0]);
			Изменен = Истина;
			
		КонецЕсли;	
		
	КонецЦикла;	

	
	Если Изменен Тогда
		
		КонтрагентОбъект.ОбменДанными.Загрузка = Истина;
		
		Если БезИстории Тогда
			КонтрагентОбъект.ДополнительныеСвойства.Вставить("ПропустьВерсию");
		КонецЕсли;	
		
		КонтрагентОбъект.Записать();                    
		
	КонецЕсли;	
	
КонецПроцедуры	

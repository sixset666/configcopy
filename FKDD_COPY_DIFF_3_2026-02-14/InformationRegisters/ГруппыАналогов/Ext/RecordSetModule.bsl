// Процедура объединения группы
Процедура ОбъединитьГруппы(ГлавнаяГруппа,ПрисоединяемаяГруппа) Экспорт
	//Получим элементы основной группы
	НаборЗаписейГлавнойГруппы=РегистрыСведений.ГруппыАналогов.СоздатьНаборЗаписей();
	НаборЗаписейГлавнойГруппы.Отбор.ИдентификаторГруппы.Значение=ГлавнаяГруппа;
	НаборЗаписейГлавнойГруппы.Отбор.ИдентификаторГруппы.Использование=Истина;
	НаборЗаписейГлавнойГруппы.Прочитать();
	тзНаборЗаписейГлавнойГруппы=НаборЗаписейГлавнойГруппы.Выгрузить();
	
	//Создадим набор записей присоединяемой
	НаборЗаписейПрисоединяемойГруппы=РегистрыСведений.ГруппыАналогов.СоздатьНаборЗаписей();
	НаборЗаписейПрисоединяемойГруппы.Отбор.ИдентификаторГруппы.Значение=ПрисоединяемаяГруппа;
	НаборЗаписейПрисоединяемойГруппы.Отбор.ИдентификаторГруппы.Использование=Истина;
	НаборЗаписейПрисоединяемойГруппы.Прочитать();
	
	//Будем просматривать присоединяемую группу и переливать ее в главную
	Пока НаборЗаписейПрисоединяемойГруппы.Количество()>0 Цикл
		ПерваяСтрока = НаборЗаписейПрисоединяемойГруппы[0];
		СтруктураПоиска = Новый Структура("АртикулДляПоиска,Производитель", ПерваяСтрока.АртикулДляПоиска, ПерваяСтрока.Производитель);
		Если тзНаборЗаписейГлавнойГруппы.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
		    НоваяЗапись = НаборЗаписейГлавнойГруппы.Добавить();
			НоваяЗапись.ИдентификаторГруппы	= ГлавнаяГруппа;
			НоваяЗапись.Артикул				= ПерваяСтрока.Артикул;
			НоваяЗапись.АртикулДляПоиска	= ПерваяСтрока.АртикулДляПоиска;
			НоваяЗапись.Производитель		= ПерваяСтрока.Производитель;
			НоваяЗапись.Наименование		= ПерваяСтрока.Наименование;
			НоваяЗапись.ГлавныйПоГруппе		= ПерваяСтрока.ГлавныйПоГруппе;
			НоваяЗапись.ГлавныйПоПроизводителю = ПерваяСтрока.ГлавныйПоПроизводителю;
		Иначе
			
		КонецЕсли; 
		НаборЗаписейПрисоединяемойГруппы.Удалить(НаборЗаписейПрисоединяемойГруппы[0]);
	КонецЦикла; 
	НаборЗаписейПрисоединяемойГруппы.Записать();
	НаборЗаписейГлавнойГруппы.Записать();
КонецПроцедуры

// Процедура добавления аналога
// Параметры:
//	ИдентификаторГруппы - Идентификатор группы в которую добавляем
//	Артикул - Артикул аналога
//
Процедура ДобавленАналог(ИдентификаторГруппы,АртикулДляПоиска,Производитель) Экспорт
	//Получим группы аналогов, к которым еще принадлежит данный
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ГруппыАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы
	|ИЗ
	|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
	|ГДЕ
	|	ГруппыАналогов.АртикулДляПоиска = &Артикул
	|	И ГруппыАналогов.Производитель = &Производитель
	|	И ГруппыАналогов.ИдентификаторГруппы <> &ИдентификаторГруппы";
	
	Запрос.УстановитьПараметр("Артикул", АртикулДляПоиска);
	Запрос.УстановитьПараметр("Производитель", Производитель);
	Запрос.УстановитьПараметр("ИдентификаторГруппы",ИдентификаторГруппы);
	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъединитьГруппы(ИдентификаторГруппы,Выборка.ИдентификаторГруппы);
	КонецЦикла;
КонецПроцедуры

// Процедура установки главного аналога
Процедура УстановитьГлавныйАналог(ИдентификаторГруппы,АртикулДляПоиска,ИмяРеквизита,Производитель) Экспорт
	НаборЗаписейГруппыАналогов=РегистрыСведений.ГруппыАналогов.СоздатьНаборЗаписей();
	НаборЗаписейГруппыАналогов.Отбор.ИдентификаторГруппы.Значение=ИдентификаторГруппы;
	НаборЗаписейГруппыАналогов.Отбор.ИдентификаторГруппы.Использование=Истина;
	НаборЗаписейГруппыАналогов.Прочитать();
	Для каждого ЗаписьГруппыАналогов Из НаборЗаписейГруппыАналогов Цикл
		Если ЗаписьГруппыАналогов.АртикулДляПоиска = АртикулДляПоиска И
			ЗаписьГруппыАналогов.Производитель = Производитель Тогда
			ЗаписьГруппыАналогов[ИмяРеквизита]=Истина;
		Иначе
			ЗаписьГруппыАналогов[ИмяРеквизита]=Ложь;
		КонецЕсли; 
	КонецЦикла; 
	НаборЗаписейГруппыАналогов.Записать();
КонецПроцедуры

// Обработчик события при записи формы
Процедура ПриЗаписи(Отказ, Замещение)
	#Если Клиент Тогда
	Оповестить("Изменение элемента группы аналогов",ЭтотОбъект.Отбор.ИдентификаторГруппы);
	#КонецЕсли
КонецПроцедуры

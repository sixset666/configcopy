
//Процедура заполняет артикул и преобразует артикул для поиска
Процедура ЗаполнитьАртикулДляПоиска() Экспорт 
	#Если Клиент Тогда
	Состояние(НСтр("ru='Проверка групп аналогов.'"));
	#КонецЕсли
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыАналогов.ИдентификаторГруппы КАК ИдентификаторГруппы
		|ИЗ
		|	РегистрСведений.ГруппыАналогов КАК ГруппыАналогов
		|ГДЕ
		|	ГруппыАналогов.Артикул = """"";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		#Если Клиент Тогда
		Сообщить(НСтр("ru='Обновление регистра сведений ""Группы аналогов"" не требуется'"));
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Группы аналогов""'"));
	#КонецЕсли
	
	Выборка = Результат.Выбрать();
	РазмерВыборки = Выборка.Количество();
	
	НачатьТранзакцию();
	СчЦикла = 0;
	СчВсего = 0;
	ПроцентПрогресса = 0;
	
	Пока Выборка.Следующий()Цикл
		СчВсего = СчВсего + 1;
		#Если Клиент Тогда
		Если Цел(СчВсего*100/РазмерВыборки) > ПроцентПрогресса Тогда
			ПроцентПрогресса = Цел(СчВсего*100/РазмерВыборки);
			Состояние(НСтр("ru='Обновление регистра сведений ""Группы аналогов"": '") + Строка(ПроцентПрогресса)+"%");
		КонецЕсли;
		#КонецЕсли
		
		НаборЗаписей = РегистрыСведений.ГруппыАналогов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторГруппы.Установить(Выборка.ИдентификаторГруппы);
		НаборЗаписей.Прочитать();
		
		ТаблПакета = НаборЗаписей.ВыгрузитьКолонки();
		ТаблПакета.Индексы.Добавить("АртикулДляПоиска,Производитель");
		
		Для Каждого СтрокаНабора ИЗ НаборЗаписей Цикл
			АртикулДляПоиска = Справочники.Номенклатура.ПреобразоватьВАртикулДляПоиска(СтрокаНабора.АртикулДляПоиска, СтрокаНабора.Производитель); 
			Если ПустаяСтрока(АртикулДляПоиска)Тогда
				АртикулДляПоиска = СтрокаНабора.АртикулДляПоиска;
			КонецЕсли;
			
			//Если АртикулДляПоиска и Производитель строки набора повторяются, то такую строку не будем дублировать
			СтруктураПоиска = Новый Структура("АртикулДляПоиска,Производитель", АртикулДляПоиска, СтрокаНабора.Производитель);
			Если ТаблПакета.НайтиСтроки(СтруктураПоиска).Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаПакета = ТаблПакета.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПакета, СтрокаНабора);
			
			СтрокаПакета.Артикул = СтрокаНабора.АртикулДляПоиска;
			СтрокаПакета.АртикулДляПоиска = АртикулДляПоиска;
			
			СчЦикла = СчЦикла + 1;
		КонецЦикла;	
		
		НаборЗаписей.Загрузить(ТаблПакета);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать(Истина);
		
		ТаблПакета.Очистить();
		
		Если СчЦикла > 10000 Тогда
			ЗафиксироватьТранзакцию();
			НачатьТранзакцию();
			СчЦикла = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	        	
	#Если Клиент Тогда
	Сообщить(НСтр("ru='Обновление регистра сведений ""Группы аналогов"" завершено.'"));
	#КонецЕсли
	
КонецПроцедуры //ОбновитьАртикулыГруппАналогов()

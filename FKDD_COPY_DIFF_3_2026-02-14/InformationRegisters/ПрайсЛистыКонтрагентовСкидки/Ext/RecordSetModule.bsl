Перем ДокументОбъект              Экспорт;  // документ объект для записи
Перем РезультатЗапросаПоСкидкам   Экспорт;  // РезультатЗапроса или ТаблицаЗначений. Устанавливается если документ имеет "необычную" ТЧ
Перем ДатаНачалаДействия          Экспорт;  // дата начала действия

// получаем таблицу прайс-листов и скидок на них
Функция ПолучитьТаблицуСкидок()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаСкидок.ПрайсЛист,
	|	ТаблицаСкидок.ТегПозиции,
	|	ТаблицаСкидок.Производитель,
	|	ТаблицаСкидок.Скидка
	|ПОМЕСТИТЬ втСкидкиДокумента
	|ИЗ
	|	&ТаблицаСкидок КАК ТаблицаСкидок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СкидкиДокумента.ПрайсЛист, Скидки.ПрайсЛист) КАК ПрайсЛист,
	|	ЕСТЬNULL(СкидкиДокумента.ТегПозиции, Скидки.ТегПозиции) КАК ТегПозиции,
	|	ЕСТЬNULL(СкидкиДокумента.Производитель, Скидки.Производитель) КАК Производитель,
	|	СкидкиДокумента.Скидка КАК Скидка,
	|	Скидки.Скидка КАК СкидкаВБазе
	|ИЗ
	|	втСкидкиДокумента КАК СкидкиДокумента
	|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Рег.ПрайсЛист КАК ПрайсЛист,
	|			Рег.ТегПозиции КАК ТегПозиции,
	|			Рег.Производитель КАК Производитель,
	|			Рег.Скидка КАК Скидка,
	|			Рег.Отменена КАК Отменена
	|		ИЗ
	|			РегистрСведений.ПрайсЛистыКонтрагентовСкидки.СрезПоследних(
	|					&ДатаКон,
	|					ПрайсЛист В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							СкидкиДокумента.ПрайсЛист
	|						ИЗ
	|							втСкидкиДокумента КАК СкидкиДокумента)) КАК Рег
	|		ГДЕ
	|			НЕ Рег.Отменена) КАК Скидки
	|		ПО СкидкиДокумента.ПрайсЛист = Скидки.ПрайсЛист
	|			И СкидкиДокумента.ТегПозиции = Скидки.ТегПозиции
	|			И СкидкиДокумента.Производитель = Скидки.Производитель";
	
	Запрос.УстановитьПараметр("ТаблицаСкидок", РезультатЗапросаПоСкидкам);
	Запрос.УстановитьПараметр("ДатаКон", Новый Граница(Новый МоментВремени(ДатаНачалаДействия, ДокументОбъект.Ссылка), ВидГраницы.Исключая));
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции //ПолучитьТаблицуСкидок()

// Функция производит установку скидок.
Функция УстановитьСкидки() Экспорт
	
	// заполним незаполненные переменные.
	ДатаПереоценки = ?(ТипЗнч(ДатаНачалаДействия) = Тип("Дата"), ДатаНачалаДействия, ДокументОбъект.Дата);
	
	Если РезультатЗапросаПоСкидкам = Неопределено Тогда
		РезультатЗапросаПоСкидкам = ДокументОбъект.ПрайсЛисты.Выгрузить();
	Иначе
		Если ТипЗнч(РезультатЗапросаПоСкидкам) = Тип("РезультатЗапроса") Тогда
			РезультатЗапросаПоСкидкам = РезультатЗапросаПоСкидкам.Выгрузить();
		КонецЕсли;
	КонецЕсли;
	
	//Сформируем таблицу, содержащую как новые, так и старые скидки
	РезультатЗапросаПоСкидкам = ПолучитьТаблицуСкидок();
	
	// ускоряющие переменные
	СообщатьОбИзменении = обПраво("СообщатьОбИзмененииЦен", ДокументОбъект.Права,, ДокументОбъект);
	
	// Устанавливаем скидки
	Результат = Истина;
	
	Для Каждого СтрокаПрайса Из РезультатЗапросаПоСкидкам Цикл
		// если скидка не была установлена или изменилась тогда установим ее
		Если СтрокаПрайса.Скидка = NULL Тогда
			// Отменена
			НоваяЗапись = Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаПрайса);
			НоваяЗапись.Скидка = СтрокаПрайса.СкидкаВБазе;
			НоваяЗапись.Отменена = Истина;
			НоваяЗапись.Период = ДатаПереоценки;
			НоваяЗапись.Регистратор = ДокументОбъект.Ссылка;
			// сообщим, если надо
			Если СообщатьОбИзменении Тогда
				дкСообщитьРезультат("Прайс-лист: " + СокрЛП(СтрокаПрайса.ПрайсЛист.Наименование)
				+ ?(ЗначениеЗаполнено(СтрокаПрайса.ТегПозиции), "; Тег: """ + СокрЛП(СтрокаПрайса.ТегПозиции) + """", "")
				+ ?(ЗначениеЗаполнено(СтрокаПрайса.Производитель), "; Произв.: """ + СокрЛП(СтрокаПрайса.Производитель) + """", "")
				+ "; Старая скидка: " + Формат(СтрокаПрайса.СкидкаВБазе, "ЧЦ=15; ЧДЦ=2; ЧН=0,00") 
				+ "; Новая скидка: Отменена",, ДокументОбъект);
			КонецЕсли;
		ИначеЕсли СтрокаПрайса.Скидка <> СтрокаПрайса.СкидкаВБазе Тогда
			НоваяЗапись = Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись,СтрокаПрайса);
			НоваяЗапись.Период = ДатаПереоценки;
			НоваяЗапись.Регистратор = ДокументОбъект.Ссылка;
			// сообщим, если надо
			Если СообщатьОбИзменении Тогда
				дкСообщитьРезультат("Прайс-лист: " + СокрЛП(СтрокаПрайса.ПрайсЛист.Наименование)
				+ ?(ЗначениеЗаполнено(СтрокаПрайса.ТегПозиции), "; Тег: """ + СокрЛП(СтрокаПрайса.ТегПозиции) + """", "")
				+ ?(ЗначениеЗаполнено(СтрокаПрайса.Производитель), "; Произв.: """ + СокрЛП(СтрокаПрайса.Производитель) + """", "")
				+ "; Старая скидка: " + ?(СтрокаПрайса.СкидкаВБазе = NULL, "Нет", Формат(СтрокаПрайса.СкидкаВБазе,"ЧЦ=15; ЧДЦ=2; ЧН=0,00"))
				+ "; Новая скидка: " + Формат(СтрокаПрайса.Скидка,"ЧЦ=15; ЧДЦ=2; ЧН=0,00"),, ДокументОбъект);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// скинем некоторые переменные
	РезультатЗапросаПоСкидкам  = Неопределено;
	ДатаНачалаДействия         = Неопределено;
	
	// убиваем циклическую ссылку
	ДокументОбъект = Неопределено;  
	
	Возврат Результат;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект);
КонецПроцедуры	//	ПриЗаписи()


//////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ
//////////////////////////////////////////////////////////////////////////////////


Перем СкладКомпании Экспорт;   // Справочник.СкладыКомпании ИЛИ Массив. Содержит склад(ы) по которым сдвигается граница последовательности
Перем СкладКомпанииБыл Экспорт;   // Справочник.СкладыКомпании ИЛИ Массив. Содержит склад(ы) по которым сдвигается граница последовательности
Перем МоментВремени Экспорт;   // Дата-время документа
Перем МоментВремениБыл Экспорт;   // Дата-время документа до изменения
Перем ДокументСсылка Экспорт;  // Ссылка на документ

// проверяет, надо ли двигать границу последовательности и если надо - сдвигает
Функция ИзменитьГраницу(БезПроверок=Ложь) Экспорт
	
	// если СкладКомпании один, то преобразуем его в массив
	МассивСкладовНовые = Новый Массив();
	МассивСкладовСтарые = Новый Массив();
	// фильтр на СкладыКомпании
	МассивСкладовФильтр = Новый Массив();
	Если ТипЗнч(СкладКомпании)<>Тип("Массив") Тогда
		МассивСкладовНовые.Добавить(СкладКомпании);
		МассивСкладовФильтр.Добавить(СкладКомпании);
	Иначе
		Для Каждого ТекСклад Из СкладКомпании Цикл
			МассивСкладовНовые.Добавить(ТекСклад);
			МассивСкладовФильтр.Добавить(ТекСклад);
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(СкладКомпанииБыл) И ТипЗнч(СкладКомпанииБыл)<>Тип("Массив") И МассивСкладовНовые.Найти(СкладКомпанииБыл)=Неопределено Тогда
		МассивСкладовСтарые.Добавить(СкладКомпанииБыл);
		МассивСкладовФильтр.Добавить(СкладКомпанииБыл);
	ИначеЕсли ЗначениеЗаполнено(СкладКомпанииБыл) Тогда
		Для Каждого ТекСклад Из СкладКомпанииБыл Цикл
			Если МассивСкладовНовые.Найти(ТекСклад)=Неопределено Тогда
				МассивСкладовСтарые.Добавить(ТекСклад);
				МассивСкладовФильтр.Добавить(ТекСклад);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// получаем текущую границу
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СкладКомпании КАК СкладКомпании,
	|	МоментВремени КАК МоментВремени,
	|	ДокументСсылка КАК ДокументСсылка
	|ИЗ
	|	РегистрСведений.ГраницыАвтомобили
	|ГДЕ
	|	СкладКомпании В(&СкладыКомпании)
	|");
	Запрос.УстановитьПараметр("СкладыКомпании",МассивСкладовФильтр);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	МоментВремениДокумента = ?(ТипЗнч(МоментВремени)=Тип("МоментВремени") И ДокументСсылка<>Неопределено, Новый МоментВремени(Мин(ДокументСсылка.Дата, МоментВремени.Дата), ДокументСсылка), Новый МоментВремени(МоментВремени, ДокументСсылка));
	Если ЗначениеЗаполнено(МоментВремениБыл) Тогда
		МоментВремениДокумента = ?(МоментВремениДокумента.Сравнить(МоментВремениБыл)=1,МоментВремениБыл,МоментВремениДокумента);
	КонецЕсли;
	
	// идем по новым складам и смотрим надо ли сдвинуть границу
	Для Каждого СледующийСклад Из МассивСкладовНовые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(СледующийСклад,"СкладКомпании");
	
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениДокумента) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыАвтомобили.СоздатьМенеджерЗаписи();
			НоваяЗапись.СкладКомпании = СледующийСклад;
			НоваяЗапись.МоментВремени = МоментВремениДокумента.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениДокумента.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// теперь тот-же проход, но по старым складам
	Для Каждого СледующийСклад Из МассивСкладовСтарые Цикл
		ДвигаемГраницу = Ложь;
		СтрокаРезультата = ТаблицаРезультата.Найти(СледующийСклад,"СкладКомпании");
		
		Если СтрокаРезультата=Неопределено Тогда
			ДвигаемГраницу = Истина;
		Иначе
			Если НЕ обЗначениеНеЗаполнено(СтрокаРезультата.ДокументСсылка) Тогда
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени,СтрокаРезультата.ДокументСсылка);
			Иначе
				МоментВремениГраницы = Новый МоментВремени(СтрокаРезультата.МоментВремени);
			КонецЕсли;
			ДвигаемГраницу = (МоментВремениГраницы.Сравнить(МоментВремениБыл) = 1);
		КонецЕсли;
		
		// если границу надо двигать, двигаем
		Если ДвигаемГраницу ИЛИ БезПроверок Тогда
			НоваяЗапись = РегистрыСведений.ГраницыАвтомобили.СоздатьМенеджерЗаписи();
			НоваяЗапись.СкладКомпании = СледующийСклад;
			НоваяЗапись.МоментВремени = МоментВремениБыл.Дата;
			НоваяЗапись.ДокументСсылка = МоментВремениБыл.Ссылка;
			НоваяЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	// убираем циклические ссылки
	ДокументСсылка = Неопределено;
	
	Возврат Истина;
КонецФункции

// вызывается при непосредственном удалении проведенного документа
// проверяет есть ли граница с таким документом, если есть, то очищает ссылку
Процедура УбратьСсылку(Ссылка) Экспорт
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	СкладКомпании,
	|	МоментВремени
	|ИЗ
	|	РегистрСведений.ГраницыАвтомобили 
	|ГДЕ
	|	ДокументСсылка=&Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = РегистрыСведений.ГраницыАвтомобили.СоздатьМенеджерЗаписи();
			НоваяЗапись.СкладКомпании = Выборка.СкладКомпании;
			НоваяЗапись.МоментВремени = Выборка.МоментВремени;
			НоваяЗапись.ДокументСсылка = Неопределено;
			НоваяЗапись.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Получение минимального складского остатка номенклатуры
// Параметры
//  Номенклатура – СправочникСсылка.Номенклатура – ссылка на номенклатуру
//  ПодразделениеКомпании – СправочникСсылка.ПодразделенияКомпании – ссылка на подразделение компании
//  НаДату – Дата – дата расчетов
// Возвращаемое значение:
//   Число или Неопределено – минимальный складской остаток
//
Функция ПолучитьМинимальныйСкладскойОстаток(Номенклатура, ПодразделениеКомпании, НаДату = Неопределено) Экспорт
	Если НаДату = Неопределено Тогда
		ДатаРасчетов = КонецДня(ТекущаяДата());
	Иначе
		ДатаРасчетов = КонецДня(НаДату);
	КонецЕсли; 
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ЗначениеРеквизита КАК МинимальныйОстаток
		|ИЗ
		|	РегистрСведений.ДополнительныеРеквизитыНоменклатуры.СрезПоследних КАК ДополнительныеРеквизитыНоменклатурыСрезПоследних
		|ГДЕ
		|	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ДополнительныйРеквизит = &ДополнительныйРеквизит И
		|	ДополнительныеРеквизитыНоменклатурыСрезПоследних.Номенклатура = &Номенклатура И
		|	ДополнительныеРеквизитыНоменклатурыСрезПоследних.ПодразделениеКомпании = &ПодразделениеКомпании
		|");
	Запрос.УстановитьПараметр("ДополнительныйРеквизит",Перечисления.ДополнительныеРеквизитыНоменклатуры.МинимальныйОстаток);
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПодразделениеКомпании);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.МинимальныйОстаток;
	Иначе
		Если ПодразделениеКомпании<>Справочники.ПодразделенияКомпании.ПустаяСсылка() Тогда
			Возврат ПолучитьМинимальныйСкладскойОстаток(Номенклатура, ПодразделениеКомпании.Родитель, НаДату);
		Иначе
			Возврат Неопределено;
		КонецЕсли; 
	КонецЕсли; 
КонецФункции // ПолучитьМинимальныйСкладскойОстаток()

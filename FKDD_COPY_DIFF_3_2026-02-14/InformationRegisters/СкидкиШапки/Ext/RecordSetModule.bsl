// Модуль набора записей регистра "СкидкиШапки"

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА
Перем ДокументОбъект			Экспорт;	// Документ объект
Перем РезультатЗапросаПоСкидкам	Экспорт;	// РезультатЗапроса или ТаблицаЗначений.
Перем ПодразделениеКомпании		Экспорт;	// Подразделение компании

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Функция выполняет отмену проведения
Функция ОтменаПроведения() Экспорт
	// проверим значения ключевых объектов
	Отказ = ((ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.НазначениеСкидокШапки")) ИЛИ ДокументОбъект.ЭтоНовый());
	
	// приступим к очистки движений
	Если НЕ Отказ Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	*
		|ИЗ
		|	РегистрСведений.СкидкиШапки КАК СкидкиШапки
		|ГДЕ
		|	СкидкиШапки.РегистраторСкидки = &РегистраторСкидки";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("РегистраторСкидки", ДокументОбъект.Ссылка);
		
		Запись = РегистрыСведений.СкидкиШапки.СоздатьМенеджерЗаписи();
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Попытка
				Запись.Прочитать();
				Запись.Удалить();
			Исключение
				Отказ = ИСТИНА;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли; 
	
	Возврат Отказ;
КонецФункции

// Установить скидки
Функция УстановитьСкидки() Экспорт
	Отказ = ЛОЖЬ;
	
	// проверим значения ключевых объектов
	Если (ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.НазначениеСкидокШапки")) ИЛИ ДокументОбъект.ЭтоНовый() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если (ТипЗнч(ПодразделениеКомпании) <> Тип("СправочникСсылка.ПодразделенияКомпании")) ИЛИ ПодразделениеКомпании.Пустая() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	
	// получаем таблицу товаров
	Если (РезультатЗапросаПоСкидкам = Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("ТаблицаЗначений")) Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоСкидкам) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоСкидкам = РезультатЗапросаПоСкидкам.Выгрузить();
	КонецЕсли;
	
	// если были обнаружены ошибки, то движения делать не будем
	Если НЕ Отказ Тогда
		ТабЗаписей = ПолучитьДаныеПоСкидкам(ДокументОбъект.Ссылка);
		НужнаПроверка = ТабЗаписей.Количество() > 0;
		стрОшибок = "";
		
		// Осуществляем запись данных в регистр
		Для Каждого ТекСтрока Из РезультатЗапросаПоСкидкам Цикл
			
			// регистр СкидкиШапки 
			НоваяЗапись = Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
			НоваяЗапись.Период				= ДокументОбъект.ДатаНачалаДействия;
			НоваяЗапись.Подразделение		= ПодразделениеКомпании;
			НоваяЗапись.Действует			= ИСТИНА;
			НоваяЗапись.РегистраторСкидки	= ДокументОбъект.Ссылка;
			НоваяЗапись.Свойство			= ?(обЗначениеНеЗаполнено(НоваяЗапись.Свойство), Неопределено, НоваяЗапись.Свойство);
			НоваяЗапись.ИдентификаторСкидки	= ТекСтрока.ИдентификаторСкидки; 
			НоваяЗапись.СкидкаНаТовары		= ТекСтрока.СкидкаНаТовары; 
			НоваяЗапись.СкидкаНаРаботы		= ТекСтрока.СкидкаНаРаботы; 
			НоваяЗапись.МоментИзменения		= ТекущаяДата(); 
			НоваяЗапись.РучнаяСкидка		= ТекСтрока.Скидка.РучнаяСкидка;
			
			// проверим возможность добавления строки
			Если НужнаПроверка Тогда
				стрПоиск = Новый Структура;
				стрПоиск.Вставить("Подразделение");
				стрПоиск.Вставить("РучнаяСкидка");
				стрПоиск.Вставить("НачВремя");
				стрПоиск.Вставить("КонВремя");
				стрПоиск.Вставить("ДниНедели");
				стрПоиск.Вставить("ЭтоПодарок");
				стрПоиск.Вставить("ДисконтнаяКарта");
				стрПоиск.Вставить("ОтСуммыНакопленияНаКарте");
				стрПоиск.Вставить("ЗалОбслуживания");
				стрПоиск.Вставить("Свойство");
				стрПоиск.Вставить("ОтСуммыЧека");
				стрПоиск.Вставить("Скидка");
				стрПоиск.Вставить("СкидкаНаТовары");
				стрПоиск.Вставить("СкидкаНаРаботы");
				
				ЗаполнитьЗначенияСвойств(стрПоиск,НоваяЗапись);
				
				РезПоиска = ТабЗаписей.НайтиСтроки(стрПоиск);
				Если РезПоиска.Количество() > 0 Тогда
					Отказ = Истина;
					дкДобавитьОшибку(стрОшибок, "Для скидки <"+ТекСтрока.Скидка+"> уже есть запись на дату """+ Формат(ДокументОбъект.ДатаНачалаДействия,"ДЛФ=DDT") +""".");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Произведем запись набора (т.к. автоматической записи по окончанию транзакции не будет)
		Если Отказ Тогда
			дкСообщитьРезультат(стрОшибок, "Важное&Скидки (на документ в целом):", ДокументОбъект);//ОписаниеОшибки()
		Иначе
			Попытка
				Записать(ЛОЖЬ);
			Исключение
				Отказ = ИСТИНА;
				дкСообщитьРезультат("В процессе записи данных в регистр возникла ошибка.", "Важное&Скидки (на документ в целом):", ДокументОбъект);//ОписаниеОшибки()
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	// Очистим основные переменные
	ДокументОбъект				= Неопределено;
	РезультатЗапросаПоСкидкам	= Неопределено;
	ПодразделениеКомпании		= Неопределено;
	
	Возврат Отказ;
КонецФункции

// Отменить скидки
Функция ОтменитьСкидки() Экспорт
	Отказ = ЛОЖЬ;
	
	// проверим значения ключевых объектов
	Если (ТипЗнч(ДокументОбъект) <> Тип("ДокументОбъект.НазначениеСкидокШапки")) ИЛИ ДокументОбъект.ЭтоНовый() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если (ТипЗнч(ПодразделениеКомпании) <> Тип("СправочникСсылка.ПодразделенияКомпании")) ИЛИ ПодразделениеКомпании.Пустая() Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	
	// получаем таблицу товаров
	Если (РезультатЗапросаПоСкидкам = Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоСкидкам)<>Тип("ТаблицаЗначений")) Тогда
		Отказ = ИСТИНА;
	КонецЕсли;
	Если ТипЗнч(РезультатЗапросаПоСкидкам) = Тип("РезультатЗапроса") Тогда
		РезультатЗапросаПоСкидкам = РезультатЗапросаПоСкидкам.Выгрузить();
	КонецЕсли;
	
	// Осуществляем запись данных в регистр
	Для Каждого ТекСтрока Из РезультатЗапросаПоСкидкам Цикл
		Если НЕ ТекСтрока.СкидкаУстановлена Тогда
			Отказ = ИСТИНА;
			дкСообщитьРезультат("Скидка """+СокрЛП(ТекСтрока.Скидка)+""" ранее не была установлена. Отмена скидки не возможно!", "Важное&Скидки (на документ в целом):", ДокументОбъект, ТекСтрока.Скидка);
			Продолжить;
		КонецЕсли;
		
		// регистр СкидкиШапки 
		НоваяЗапись = Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока);
		НоваяЗапись.Период				= ДокументОбъект.ДатаНачалаДействия;
		НоваяЗапись.Подразделение		= ПодразделениеКомпании;
		НоваяЗапись.Действует			= ЛОЖЬ;
		НоваяЗапись.РегистраторСкидки	= ДокументОбъект.Ссылка;
		НоваяЗапись.ИдентификаторСкидки = ТекСтрока.ИдентификаторСкидки; //TruA
		НоваяЗапись.СкидкаНаТовары		= ТекСтрока.СкидкаНаТовары;
		НоваяЗапись.СкидкаНаРаботы		= ТекСтрока.СкидкаНаРаботы;
		НоваяЗапись.МоментИзменения		= ТекущаяДата(); //TruA
	КонецЦикла;
	
	// если были обнаружены ошибки, то движения записывать не будем
	Если НЕ Отказ Тогда
		// Произведем запись набора (т.к. автоматической записи по окончанию транзакции не будет)
		Попытка
			Записать(ЛОЖЬ);
		Исключение
			Отказ = ИСТИНА;
			дкСообщитьРезультат("В процессе записи данных в регистр возникла ошибка.", "Важное&Скидки (на документ в целом):", ДокументОбъект);//ОписаниеОшибки()
		КонецПопытки;
	КонецЕсли;
	
	// Очистим основные переменные
	ДокументОбъект				= Неопределено;
	РезультатЗапросаПоСкидкам	= Неопределено;
	ПодразделениеКомпании		= Неопределено;
	
	Возврат Отказ;
КонецФункции

// получение скидок на дату
Функция ПолучитьДаныеПоСкидкам(ДокСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СкидкиШапки.Подразделение,
	|	СкидкиШапки.РучнаяСкидка,
	|	СкидкиШапки.НачВремя,
	|	СкидкиШапки.КонВремя,
	|	СкидкиШапки.ДниНедели,
	|	СкидкиШапки.ЭтоПодарок,
	|	СкидкиШапки.ДисконтнаяКарта,
	|	СкидкиШапки.ОтСуммыНакопленияНаКарте,
	|	СкидкиШапки.ЗалОбслуживания,
	|	СкидкиШапки.ОтСуммыЧека,
	|	СкидкиШапки.Скидка,
	|	СкидкиШапки.СкидкаНаТовары,
	|	СкидкиШапки.СкидкаНаРаботы,
	|	СкидкиШапки.Свойство
	|ИЗ
	|	РегистрСведений.СкидкиШапки КАК СкидкиШапки
	|ГДЕ
	|	СкидкиШапки.Скидка В
	|			(ВЫБРАТЬ
	|				НазначениеСкидокШапкиСкидки.Скидка
	|			ИЗ
	|				Документ.НазначениеСкидокШапки.Скидки КАК НазначениеСкидокШапкиСкидки
	|			ГДЕ
	|				НазначениеСкидокШапкиСкидки.Ссылка = &ДокСсылка
	|			СГРУППИРОВАТЬ ПО
	|						НазначениеСкидокШапкиСкидки.Скидка)
	|	И СкидкиШапки.Период = &ДатаНачалаДействия";
	
	Запрос.УстановитьПараметр("ДокСсылка", ДокСсылка);
	Запрос.УстановитьПараметр("ДатаНачалаДействия",ДокСсылка.ДатаНачалаДействия);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

Перем ДокЗаказНаряд Экспорт; //Документ заказ-наряд

//Выполняет движения по заказ-наряду
Процедура ВыполнитьДвиженияПоЗН() Экспорт
	Если обЗначениеНеЗаполнено(ДокЗаказНаряд) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписейФактическоеВремя = РегистрыСведений.УРВ_ФактическоеВремяРабот.СоздатьНаборЗаписей();
	НаборЗаписейФактическоеВремя.Отбор.ЗаказНаряд.Установить(ДокЗаказНаряд);
	НаборЗаписейФактическоеВремя.Прочитать();
	НаборЗаписейФактическоеВремя.Очистить();
	НаборЗаписейФактическоеВремя.Записать();
	
	// получим данные для проведения
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблОтметокВремени.Сотрудник       КАК Сотрудник,
	|	ТаблОтметокВремени.Цех             КАК РабочееМесто,
	|	ТаблОтметокВремени.ПакетРабот      КАК ПакетРабот,
	|	ТаблОтметокВремени.ВремяПоПакету   КАК ВремяПоПакету,
	|	ТаблДок.Работа                     КАК Работа,
	|	ЕСТЬNULL(ТаблДок.НормаПоРаботе, 0) КАК НормаПоРаботе
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияВремениРабот.Сотрудник КАК Сотрудник,
	|		РегистрацияВремениРабот.РабочееМесто КАК Цех,
	|		СУММА(РегистрацияВремениРабот.Продолжительность) КАК ВремяПоПакету,
	|		РегистрацияВремениРабот.ПакетРабот КАК ПакетРабот
	|	ИЗ
	|		РегистрСведений.РегистрацияВремениРабот КАК РегистрацияВремениРабот
	|	ГДЕ
	|		РегистрацияВремениРабот.ЗаказНаряд = &ЗаказНаряд
	|		И РегистрацияВремениРабот.ВидОтметки = ЗНАЧЕНИЕ(Справочник.ВидыОтметокВремени.Работа)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РегистрацияВремениРабот.Сотрудник,
	|		РегистрацияВремениРабот.РабочееМесто,
	|		РегистрацияВремениРабот.ПакетРабот) КАК ТаблОтметокВремени
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗаказНарядРаботы.ПакетРабот КАК ПакетРабот,
	|			ЗаказНарядРаботы.Работа КАК Работа,
	|			СУММА(ЗаказНарядРаботы.Количество * ЗаказНарядРаботы.Коэффициент) КАК НормаПоРаботе
	|		ИЗ
	|			Документ.ЗаказНаряд.Работы КАК ЗаказНарядРаботы
	|		ГДЕ
	|			ЗаказНарядРаботы.Ссылка = &ЗаказНаряд
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ЗаказНарядРаботы.Работа,
	|			ЗаказНарядРаботы.ПакетРабот) КАК ТаблДок
	|		ПО ТаблОтметокВремени.ПакетРабот = ТаблДок.ПакетРабот
	|ГДЕ
	|	ТаблОтметокВремени.ВремяПоПакету <> 0
	|ИТОГИ
	|	МАКСИМУМ(ВремяПоПакету),
	|	СУММА(НормаПоРаботе)
	|ПО
	|	ПакетРабот,
	|	Сотрудник,
	| 	РабочееМесто";
	Запрос.УстановитьПараметр("ЗаказНаряд", ДокЗаказНаряд);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаПакеты = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПакеты.Следующий() Цикл
			ВыборкаСотрудники = ВыборкаПакеты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСотрудники.Следующий() Цикл
				ВыроботкаРабМеста = ВыборкаСотрудники.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыроботкаРабМеста.Следующий() Цикл
					ОбщееВреняПоПакету = ВыроботкаРабМеста.ВремяПоПакету;
					ОбщеяНормаПоПакету = ВыроботкаРабМеста.НормаПоРаботе;
					
					Если ОбщеяНормаПоПакету = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					ВыборкаРаботы = ВыроботкаРабМеста.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Пока ВыборкаРаботы.Следующий() Цикл
						НоваяЗапись = Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаРаботы);
						НоваяЗапись.ЗаказНаряд = ДокЗаказНаряд;
						НоваяЗапись.Время = ОбщееВреняПоПакету*(ВыборкаРаботы.НормаПоРаботе/ОбщеяНормаПоПакету);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		Записать(Ложь);
	КонецЕсли;
КонецПроцедуры
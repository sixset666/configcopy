Перем ДокументОбъект Экспорт;  // документ объект для записи
Перем Контрагент     Экспорт;  // поставщик
Перем ТипЦен         Экспорт;  // тип устанавливаемых цен
Перем ПодразделениеКомпании                  Экспорт;   // Подразделение компании.
Перем ИмяРеквизитаНоменклатура               Экспорт;   // Заполняется в случае, если реквизит содержащий номенклатуру имеет имя отличное от "Номенклатура"
Перем ИмяРеквизитаХарактеристикаНоменклатуры Экспорт;	// Заполняется в случае, если реквизит содержащий характеристику номенклатуры имеет имя отличное от "ХарактеристикаНоменклатуры"
Перем ИмяРеквизитаЕдиницаИзмерения           Экспорт;	// Заполняется в случае, если реквизит содержащий единицу измерения имеет имя отличное от "ЕдиницаИзмерения".
Перем ИмяРеквизитаЦена           Экспорт;	// Заполняется в случае, если реквизит содержащий устанавливаемую цену имеет имя отличное от "ЦенаРозничная"
Перем РезультатЗапросаПоТоварам  Экспорт;	// РезультатЗапроса или ТаблицаЗначений. Устанавливается если документ имеет "необычную" ТЧ
Перем УстанавливатьЦеныУслуг     Экспорт;	// Булево. Если Ложь, то услуги исключаются из рассмотрения.
Перем ПроверятьОдинаковыеЦены    Экспорт;	// Булево. Если Истина, то перед установкой цен проверяется не дублируются ли цены
Перем ДатаНачалаДействия         Экспорт;   // дата начала действия
Перем МожноСделатьОтменуЦены     Экспорт;   // Признак учета нулевых цен

// ускоряющие переменные
Перем ВалютаТипаЦен,КурсВалютыТипаЦен,ТочностьТипаЦен,ОкруглятьВБольшуюСторону;
Перем ВыгружатьВОборудованиеПриИзменении;
Перем АлгоритмПолученияЦены;
Перем УчетЦенПоХарактеристикам, УчетЦенПоЕдиницамИзмерения;
Перем ИмяДокумента; // Имя объекта метаданных.

// получаем таблицу товаров и их цен
Функция ПолучитьТаблицуТоваров()
	// В данном запросе, помимо содержимого табличной части, получается базовая цена.
	// Это цена, которая уже была назначена для номенклатуры с учетом аналитик учета цены (харак-ки, ед. измерений).
	// При проведении документа, данная цена "ЦенаВБазе" сравнивается с ценой из ТЧ. Если
	// эти цены различаются, то создаются записи в регистре "Цены", в противном случае записи не делаются.
	// В связи с этим, в данном запросе нет необходимости "гибкого" получения текущей цены из регистра "Цены".
	// Т.е. если в документе выбран тип цен с аналитикой учета по характеристикам номенклатуры, то для 
	// номенклатуры ищется цена СТРОГО с учетом указанной для нее характеристикой. А если такой цены не найдено, то
	// поиск цены без учета характеристики НЕ ВЫПОЛНЯЕТСЯ. 
	
	Запрос = Новый Запрос;			
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	МИНИМУМ(ДокументТовары.НомерСтроки) КАК НомерСтроки,
	|	ДокументТовары." + ИмяРеквизитаНоменклатура + ".Код	         КАК Код,
	|	ДокументТовары." + ИмяРеквизитаНоменклатура + "		         КАК " + ИмяРеквизитаНоменклатура+",
	|	ДокументТовары." + ИмяРеквизитаНоменклатура + ".Наименование КАК НоменклатураНаименование,
	|	МАКСИМУМ(ДокументТовары." + ИмяРеквизитаЦена + ") КАК НоваяЦена,";
	
	Если УчетЦенПоХарактеристикам Тогда
		Запрос.Текст = Запрос.Текст + " ДокументТовары." + ИмяРеквизитаХарактеристикаНоменклатуры + " КАК " + ИмяРеквизитаХарактеристикаНоменклатуры +", "; 
		Запрос.Текст = Запрос.Текст + " ДокументТовары." + ИмяРеквизитаНоменклатура + ".ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров КАК УчетЦенТолькоВРазрезеДопПараметров, ";
	ИначеЕсли УчетЦенПоЕдиницамИзмерения Тогда
		Запрос.Текст = Запрос.Текст + " ДокументТовары." + ИмяРеквизитаЕдиницаИзмерения + " КАК " + ИмяРеквизитаЕдиницаИзмерения +", ";
		Запрос.Текст = Запрос.Текст + " ДокументТовары." + ИмяРеквизитаНоменклатура + ".ТипНоменклатуры.УчетЦенТолькоВРазрезеДопПараметров КАК УчетЦенТолькоВРазрезеДопПараметров, ";
	КонецЕсли;

	Запрос.Текст = Запрос.Текст + "
	|	МАКСИМУМ(ЕСТЬNULL(ЦеныВБазе.Цена,0))	КАК ЦенаВБазе
	|ИЗ
	|	Документ." + ИмяДокумента + ".Товары КАК ДокументТовары
	|	ЛЕВОЕ ВНЕШНЕЕ СОЕДИНЕНИЕ
	|	РегистрСведений.Цены.СрезПоследних(&Момент, ТипЦен=&ТипЦен 
	|												И Номенклатура В (ВЫБРАТЬ " + ИмяРеквизитаНоменклатура +" ИЗ Документ." + ИмяДокумента + ".Товары ГДЕ Ссылка=&Ссылка) 
	|												И Контрагент=&Контрагент
	|												И ПодразделениеКомпании=&ПодразделениеКомпании) КАК ЦеныВБазе
	|
	|ПО
	|	(ДокументТовары." + ИмяРеквизитаНоменклатура + " = ЦеныВБазе.Номенклатура) И 
	|	ЦеныВБазе.Цена > 0";
	
	Если УчетЦенПоХарактеристикам Тогда
		Запрос.Текст = Запрос.Текст + " И (ДокументТовары." + ИмяРеквизитаХарактеристикаНоменклатуры + " = ЦеныВБазе.ХарактеристикаНоменклатуры)"; 
	ИначеЕсли УчетЦенПоЕдиницамИзмерения Тогда
		Запрос.Текст = Запрос.Текст + " И (ДокументТовары." + ИмяРеквизитаЕдиницаИзмерения + " = ЦеныВБазе.ЕдиницаИзмерения)"
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|ГДЕ
	|	(ДокументТовары.Ссылка = &Ссылка)" + ?(МожноСделатьОтменуЦены, "", " И 
	|	(ДокументТовары." + ИмяРеквизитаЦена + " > 0)") + "
	|	" + ?(УстанавливатьЦеныУслуг, "", "И(ДокументТовары." + ИмяРеквизитаНоменклатура + ".ВидНоменклатуры<>ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга))")+"
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары." + ИмяРеквизитаНоменклатура;
	
	Если УчетЦенПоХарактеристикам Тогда
		Запрос.Текст = Запрос.Текст + ",
		|	ДокументТовары." + ИмяРеквизитаХарактеристикаНоменклатуры; 
	ИначеЕсли УчетЦенПоЕдиницамИзмерения Тогда
		Запрос.Текст = Запрос.Текст + ",
		|	ДокументТовары." + ИмяРеквизитаЕдиницаИзмерения;
	КонецЕсли;

	Запрос.УстановитьПараметр("Ссылка",                ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ТипЦен",                ТипЦен);
	Запрос.УстановитьПараметр("Контрагент",            ?(Контрагент=Неопределено, Справочники.Контрагенты.ПустаяСсылка(), Контрагент));
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Момент",                ДатаНачалаДействия);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

// Функция проверяет на отсутствие в таблице товаров одинаковых записей.
//
// Параметры:
//  ТаблицаТоваров – "ТаблицаЗначений" - Таблица значений содержащая устанавливаемые цены.
//
// Возвращаемое значение:
//   Булево - Истина - проверка прошла успешно, Ложь - ошибка.
// 
Функция ПроверитьНаОдинаковыеЗаписи(ТаблицаТоваров)
	Результат = Истина;
	
	ТаблицаКопия = ТаблицаТоваров.Скопировать();
	ТаблицаКопия.Колонки.Добавить("КоличествоПовторов", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный)));
	ТаблицаКопия.ЗаполнитьЗначения(1, "КоличествоПовторов");
	
	// Будем сворачивать таблицу. Сначала сформируем строку сворачивания.
	КолонкиГруппировки = ИмяРеквизитаНоменклатура;
	Если УчетЦенПоХарактеристикам Тогда
		КолонкиГруппировки = КолонкиГруппировки + ", "+ИмяРеквизитаХарактеристикаНоменклатуры;
	ИначеЕсли УчетЦенПоЕдиницамИзмерения Тогда
		КолонкиГруппировки = КолонкиГруппировки + ", "+ИмяРеквизитаЕдиницаИзмерения;		
	КонецЕсли;   	
	ТаблицаКопия.Свернуть(КолонкиГруппировки, "КоличествоПовторов");
	
	Ошибки = "Ошибка! Тип цен <" + ТипЦен + ">, аналитика учета цен <" + АлгоритмПолученияЦены + "> - попытка назначения одинаковых цен для следующих позиций:";
	Для каждого ТекСтрокаТЗ Из ТаблицаКопия Цикл  	
		Если ТекСтрокаТЗ.КоличествоПовторов > 1 Тогда
			Результат   = Ложь;
			ТекстОшибки = " - Номенклатура <" + ТекСтрокаТЗ[ИмяРеквизитаНоменклатура] + ">";
			Если УчетЦенПоХарактеристикам Тогда
				ТекстОшибки = ТекстОшибки + ", Характеристика номенклатуры <" + ТекСтрокаТЗ[ИмяРеквизитаХарактеристикаНоменклатуры] + ">";
			ИначеЕсли УчетЦенПоЕдиницамИзмерения Тогда					
				ТекстОшибки = ТекстОшибки + ", Единица измерения <" + ТекСтрокаТЗ[ИмяРеквизитаЕдиницаИзмерения] + ">";
			КонецЕсли;
			дкДобавитьОшибку(Ошибки, ТекстОшибки);			
		КонецЕсли;
	КонецЦикла;
	
	Если Не Результат Тогда
		дкСообщитьРезультат(Ошибки, "Ошибка", ДокументОбъект);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПроверитьНаОдинаковыеЗаписи() 

// Функция производит установку цен.
Функция УстановитьЦены() Экспорт
	// Получим имя документа.	
	ИмяДокумента = ДокументОбъект.Метаданные().Имя;
	
	МожноСделатьОтменуЦены = ?(МожноСделатьОтменуЦены = Неопределено, Ложь, МожноСделатьОтменуЦены);
	
	// заполним незаполненные переменные.
	ДатаПереоценки = ?(ТипЗнч(ДатаНачалаДействия) = Тип("Дата"), ДатаНачалаДействия, ДокументОбъект.Дата);
	
	Если ПодразделениеКомпании = Неопределено Тогда
		Попытка
			ПодразделениеКомпании = ДокументОбъект.ПодразделениеКомпании;
		Исключение КонецПопытки;
	КонецЕсли;
	
	// Не устанавливаем расчетные цены.
	// Если цена расчетная выходим.
	Если ТипЦен.Рассчитывается Тогда
		дкСообщитьРезультат("Невозможно установить расчетную цену " + ТипЦен, "Важное:", ДокументОбъект);
		Возврат Истина;
	КонецЕсли;
	
	// Получим алгоритм получения цены для данного типа цен.
	АлгоритмПолученияЦены      = ?(ТипЦен.АлгоритмПолученияЦены.Пустая(), Перечисления.АлгоритмПолученияЦены.ПоНоменклатуре, ТипЦен.АлгоритмПолученияЦены);
	УчетЦенПоХарактеристикам   = (АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоХарактеристике);  	
	УчетЦенПоЕдиницамИзмерения = (АлгоритмПолученияЦены = Перечисления.АлгоритмПолученияЦены.ПоЕдиницеИзмерения);
	
	// Получаем таблицу товаров. Независимо как будет получена данная таблица, названия колонок должны быть стандартными.
	Если (РезультатЗапросаПоТоварам = Неопределено) ИЛИ (ТипЗнч(РезультатЗапросаПоТоварам)<> Тип("РезультатЗапроса")) И (ТипЗнч(РезультатЗапросаПоТоварам)<>Тип("ТаблицаЗначений")) Тогда
		// Значит получаем из табличной части документа.
		// Проверим, а если у нас в ТЧ соответствующие реквизиты.
		ТЧМетаданные = ДокументОбъект.Метаданные().ТабличныеЧасти.Товары;
        УчетЦенПоХарактеристикам   = УчетЦенПоХарактеристикам И (ТЧМетаданные.Реквизиты.Найти(ИмяРеквизитаХарактеристикаНоменклатуры)<>Неопределено);
		УчетЦенПоЕдиницамИзмерения = УчетЦенПоЕдиницамИзмерения И (ТЧМетаданные.Реквизиты.Найти(ИмяРеквизитаЕдиницаИзмерения)<>Неопределено);
		
		// Получаем таблицу.
		РезультатЗапросаПоТоварам = ПолучитьТаблицуТоваров();		
	Иначе
		// Список необходимых движений по ценам пришел извне. Проверим, таблица значений ли это.
		Если ТипЗнч(РезультатЗапросаПоТоварам) = Тип("РезультатЗапроса") Тогда
			РезультатЗапросаПоТоварам = РезультатЗапросаПоТоварам.Выгрузить();
		КонецЕсли;
		
		// Теперь также определим, если в данной таблице нужные нам колонки.
		УчетЦенПоХарактеристикам   = УчетЦенПоХарактеристикам И (РезультатЗапросаПоТоварам.Колонки.Найти(ИмяРеквизитаХарактеристикаНоменклатуры)<>Неопределено);
		УчетЦенПоЕдиницамИзмерения = УчетЦенПоЕдиницамИзмерения И (РезультатЗапросаПоТоварам.Колонки.Найти(ИмяРеквизитаЕдиницаИзмерения)<>Неопределено);
	КонецЕсли;    		
	
	// Проверим повторную установку одной и той же цены.
	Если Не ПроверитьНаОдинаковыеЗаписи(РезультатЗапросаПоТоварам) Тогда
		РезультатЗапросаПоТоварам = Неопределено; 
		УстанавливатьЦеныУслуг    = Ложь;
		ПроверятьОдинаковыеЦены   = Истина;
		
		// Убиваем циклическую ссылку.
		ДокументОбъект = Неопределено;
		Возврат Ложь;  	
	КонецЕсли; 		
	
	// ускоряющие переменные
	СтрТипЦен = ТипЦен.Наименование;
	СообщатьОбИзменении = обПраво("СообщатьОбИзмененииЦен", ДокументОбъект.Права,,ДокументОбъект);
	
	// посмотрим надо ли выгружать цены в оборудование
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Оборудование.Ссылка
	|ИЗ 
	|	Справочник.Оборудование КАК Оборудование
	|ГДЕ
	|	Оборудование.ОсновнойОтдел.Розничный И Оборудование.ОсновнойОтдел.ТипЦенРозничнойТорговли=&ТипЦен");
	Запрос.УстановитьПараметр("ТипЦен" ,ТипЦен);
	ВыгружатьВОборудованиеПриИзменении = (НЕ Запрос.Выполнить().Пустой() И обЗначениеНеЗаполнено(Контрагент));
		
	ВалютаТипаЦен = ТипЦен.ВалютаЦены; СтрВалютаТипаЦен = СокрЛП(ВалютаТипаЦен.Наименование);
	СтруктураКурса = обКурсДляВалюты(ВалютаТипаЦен,ДокументОбъект.Дата);
	КурсВалютыТипаЦен = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	// Устанавливаем цены
	Результат = Истина;
    Права = ДокументОбъект.Права;
    ИмяКолонкиКода = дкПолучитьПараметрыРежимаВыводаКодаВДокументах( ,ДокументОбъект).Имя;
	Для Каждого СтрокаТовар Из РезультатЗапросаПоТоварам Цикл
			
		Если ТипЦен.ВВалютеУчета Тогда
			ВалютаТипаЦен     = СтрокаТовар[ИмяРеквизитаНоменклатура].ВалютаУчета;
			СтрВалютаТипаЦен  = СокрЛП(ВалютаТипаЦен.Наименование);
			СтруктураКурса    = обКурсДляВалюты(ВалютаТипаЦен,ДокументОбъект.Дата);
			КурсВалютыТипаЦен = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
		КонецЕсли;
		
		Если НЕ обЗначениеНеЗаполнено(ВалютаТипаЦен) Тогда
			Если ИмяРеквизитаЦена = "ЦенаРозничная" ИЛИ ИмяРеквизитаЦена = "ЦенаРозничнаяПриход" ИЛИ ИмяРеквизитаЦена = "ЦенаОтпускная" Тогда  //пересчитываем только не розничные цены
				НоваяЦена = Окр(СтрокаТовар.НоваяЦена,2); 	
			Иначе	
				НоваяЦена = Окр(обПересчет(СтрокаТовар.НоваяЦена, ДокументОбъект.ВалютаДокумента, ДокументОбъект.КурсДокумента,ВалютаТипаЦен,КурсВалютыТипаЦен),2);
			КонецЕсли;
			// если цена не была установлена или изменилась тогда установим ее
			Если НоваяЦена <> СтрокаТовар.ЦенаВБазе Тогда
				НоваяЗапись = Добавить();
				НоваяЗапись.Период       = ДатаПереоценки;
				НоваяЗапись.Регистратор  = ДокументОбъект.Ссылка;
				НоваяЗапись.Контрагент   = Контрагент;
				НоваяЗапись.ТипЦен       = ТипЦен;
				НоваяЗапись.Номенклатура = СтрокаТовар[ИмяРеквизитаНоменклатура];
				НоваяЗапись.ПодразделениеКомпании = ПодразделениеКомпании;
				НоваяЗапись.Цена         = НоваяЦена;
				
				// Если для типа номенклатуры, текущей номенклатуры, учет по ценам ведется строго в разрезе доп. характеристик, то
				// необходимо чтобы эти доп. характеристики были указаны (СТРОГО!).
				Если УчетЦенПоХарактеристикам Тогда
					УчетЦен = "По характеристикам";
					ЗначениеУчетаЦен = Строка(СтрокаТовар.ХарактеристикаНоменклатуры);
					Если СтрокаТовар.УчетЦенТолькоВРазрезеДопПараметров
						И СтрокаТовар[ИмяРеквизитаХарактеристикаНоменклатуры].Пустая() Тогда
						Результат = Ложь;
						Попытка 
							СтрокаНомер = " Строка №" + СтрокаТовар.НомерСтроки
						Исключение 
							СтрокаНомер = "";
						КонецПопытки;
						дкСообщитьРезультат("Невозможно установить цену для номенклатуры " + СтрокаТовар.НоменклатураНаименование
						+ " - не выбрана характеристика номенклатуры." + СтрокаНомер, "Ошибка", ДокументОбъект);
						Продолжить; 
					Иначе
						НоваяЗапись.ХарактеристикаНоменклатуры = СтрокаТовар[ИмяРеквизитаХарактеристикаНоменклатуры]; 
					КонецЕсли;						
				ИначеЕсли УчетЦенПоЕдиницамИзмерения Тогда
					УчетЦен = "По единицам измерения";
					ЗначениеУчетаЦен = Строка(СтрокаТовар.ЕдиницаИзмерения); 
					Если СтрокаТовар.УчетЦенТолькоВРазрезеДопПараметров И СтрокаТовар[ИмяРеквизитаЕдиницаИзмерения].Пустая() Тогда
						Результат = Ложь;
						Попытка 
							СтрокаНомер = " Строка №" + СтрокаТовар.НомерСтроки
						Исключение
							СтрокаНомер = "";
						КонецПопытки;
						дкСообщитьРезультат("Невозможно установить цену для номенклатуры " + СтрокаТовар.НоменклатураНаименование
						+ " - не задана единица измерения номенклатуры." + СтрокаНомер, "Ошибка", ДокументОбъект);
						Продолжить; 
					Иначе 						
						НоваяЗапись.ЕдиницаИзмерения = СтрокаТовар[ИмяРеквизитаЕдиницаИзмерения];
					КонецЕсли;
				Иначе
					УчетЦен = "По номенклатуре";
				КонецЕсли;
				
				// зарегистрируем изменение цены в оборудовании
				Если ВыгружатьВОборудованиеПриИзменении Тогда
					уоУстановитьМоментИзменения(СтрокаТовар[ИмяРеквизитаНоменклатура],ТекущаяДата());
				КонецЕсли;
				
				// сообщим, если надо
				Если СообщатьОбИзменении Тогда
					дкСообщитьРезультат("["+дкПолучитьЗначениеКолонкиКода(СтрокаТовар.Номенклатура, ИмяКолонкиКода, ДокументОбъект,Истина)
					+ "] Товар """ + СокрЛП(СтрокаТовар.НоменклатураНаименование)
					+ """; Старая цена: " + Формат(СтрокаТовар.ЦенаВБазе,"ЧЦ=15; ЧДЦ=2; ЧН=0,00") + " " + СтрВалютаТипаЦен 
					+ "; Новая цена: " + Формат(НоваяЦена,"ЧЦ=15; ЧДЦ=2; ЧН=0,00") + " " + СтрВалютаТипаЦен + ";"
					+ " Тип цен: " + СтрТипЦен + ?(обЗначениеНеЗаполнено(УчетЦен), "", "; Учет цен: " + УчетЦен)
					+ ?(обЗначениеНеЗаполнено(ЗначениеУчетаЦен), "", "(" + ЗначениеУчетаЦен + ")")  
					+ ?(обЗначениеНеЗаполнено(Контрагент), "", "; Контрагент: "
					+ СокрЛП(Контрагент.Наименование)), , ДокументОбъект, СтрокаТовар[ИмяРеквизитаНоменклатура]);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// скинем некоторые переменные
	РезультатЗапросаПоТоварам = Неопределено;
	УстанавливатьЦеныУслуг    = Ложь;
	ПроверятьОдинаковыеЦены   = Истина;
	ДатаНачалаДействия        = Неопределено;
	
	// убиваем циклическую ссылку
	ДокументОбъект = Неопределено;  
	
	Возврат Результат;
КонецФункции

// Предопределенная процедура
Процедура ПриЗаписи(Отказ, Замещение)   	
	// Зарегистрируем изменения для узлов РБД
	орРегистрацияИзменений(ЭтотОбъект); 	
КонецПроцедуры	//	ПриЗаписи()


//////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ
//////////////////////////////////////////////////////////////////////////////////

ИмяРеквизитаНоменклатура               = "Номенклатура";
ИмяРеквизитаХарактеристикаНоменклатуры = "ХарактеристикаНоменклатуры";
ИмяРеквизитаЕдиницаИзмерения           = "ЕдиницаИзмерения";
ИмяРеквизитаЦена                       = "ЦенаРозничная";
УстанавливатьЦеныУслуг                 = Ложь;
Контрагент                             = Справочники.Контрагенты.ПустаяСсылка();
ПроверятьОдинаковыеЦены                = Истина;
ПодразделениеКомпании				   = Неопределено;
МожноСделатьОтменуЦены                 = Ложь;
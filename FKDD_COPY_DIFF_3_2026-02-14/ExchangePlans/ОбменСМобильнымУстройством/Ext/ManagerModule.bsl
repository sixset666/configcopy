
///////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Создание узла для устройтва
// &
// Параметры:
//	IdУстройства - Строка - Уникальный идетификатор устройства
// Возвращаемое значение:
//	Возвращает Ложь если не удолась создать новый узел, и Истина если узел с таким id
//	был создан или уже существовал
//
Функция СоздатьНовыйУзелДляУстройтва(IdУстройства, Результат = Неопределено) Экспорт
	
	// если id не указано возрат
	Если НЕ ЗначениеЗаполнено(IdУстройства) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// проверим, а нет ли такого узла
	НайденноеЗначение = ПланыОбмена.ОбменСМобильнымУстройством.НайтиПоРеквизиту("IDУстройства", IdУстройства);
	Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
		Результат = НайденноеЗначение;
		Возврат Истина;
	КонецЕсли;
	
	// создаем новый узел
	НовыйУзелОбмена = ПланыОбмена.ОбменСМобильнымУстройством.СоздатьУзел();
	НовыйУзелОбмена.IDУстройства = IdУстройства;
	НовыйУзелОбмена.Наименование = IdУстройства;
	НовыйУзелОбмена.УстановитьНовыйКод();
	
	Попытка
		НовыйУзелОбмена.Записать();
	Исключение
		Текст = ОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;
	
	Результат = НовыйУзелОбмена.Ссылка;
	
	Возврат Истина;
	
КонецФункции

// Формирование массива Узлов обмена
//
Функция ПолучитьУзлыОбмена() Экспорт
	
	УзлыОбмена = Новый Массив;
	
	Выборка = ПланыОбмена.ОбменСМобильнымУстройством.Выбрать();
	
	ГлавныйУзел = ПланыОбмена.ОбменСМобильнымУстройством.ЭтотУзел();
	
	Пока Выборка.Следующий() Цикл
		Если ГлавныйУзел = Выборка.Ссылка Тогда Продолжить; КонецЕсли;
		
		УзлыОбмена.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат УзлыОбмена;
	
КонецФункции

// Производит регистрацию изменений для всех узлов
//
Процедура ЗарегистрироватьИзмененияДляУзла(Данные = Неопределено, МассивУзлов = Неопределено) Экспорт
	
	Если МассивУзлов = Неопределено Тогда
		МассивУзлов = ПолучитьУзлыОбмена();
	КонецЕсли;
	
	Если ТипЗнч(Данные) = Тип("Массив") Тогда
		Для Каждого записьДанных Из Данные Цикл
			Если ЗначениеЗаполнено(записьДанных) Тогда
				ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, записьДанных);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если ЗначениеЗаполнено(Данные) Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, Данные);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Получение измененных данных по узлу
//
Функция ПолучитьИзменения(Узел, Отбор = Неопределено) Экспорт
	
	// подготовим таблицы данных
	УдаляемыеОбъекты = Новый ТаблицаЗначений;
	УдаляемыеОбъекты.Колонки.Добавить("ТипОбъекта");
	УдаляемыеОбъекты.Колонки.Добавить("Значение");
	
	ИзмененныеОбъекты = Новый ТаблицаЗначений;
	ИзмененныеОбъекты.Колонки.Добавить("ТипОбъекта");
	ИзмененныеОбъекты.Колонки.Добавить("Значение");
	
	СоставCheckList = Новый ТаблицаЗначений;
	СоставCheckList.Колонки.Добавить("Модель"   , Новый ОписаниеТипов("СправочникСсылка.Модели"));
	СоставCheckList.Колонки.Добавить("CheckList", Новый ОписаниеТипов("СправочникСсылка.CheckList"));
	СоставCheckList.Колонки.Добавить("guid"     , новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	
	ЗначенияCheckList = Новый ТаблицаЗначений;
	ЗначенияCheckList.Колонки.Добавить("Заявка"   , Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаРемонт"));
	ЗначенияCheckList.Колонки.Добавить("CheckList", Новый ОписаниеТипов("СправочникСсылка.CheckList"));
	ЗначенияCheckList.Колонки.Добавить("guid"     , новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	
	ПрименяемостьДополнительныхПредложений = Новый ТаблицаЗначений();
	ПрименяемостьДополнительныхПредложений.Колонки.Добавить("ДополнительноеПредложение", Новый ОписаниеТипов("СправочникСсылка.Автоработы"));
	ПрименяемостьДополнительныхПредложений.Колонки.Добавить("Модель"                   , Новый ОписаниеТипов("СправочникСсылка.Модели"));
	ПрименяемостьДополнительныхПредложений.Колонки.Добавить("guid"                     , новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(36)));
	
	ИспользоватьОтбор = НЕ(Отбор = Неопределено);
	
	// Создадим объект сообщений
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ПолучитьИмяВременногоФайла());
	
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, Узел);
	
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель, ЗаписьСообщения.НомерСообщения, Отбор);
	
	ПроверятьПодразделение = (Узел.Подразделения.Количество() > 0);
	
	Пока ВыборкаИзменений.Следующий() Цикл
		Объект = ВыборкаИзменений.Получить(); ТипОбъекта = ТипЗнч(Объект);
		
		Если ТипОбъекта = Тип("УдалениеОбъекта") Тогда
			НоваяСтрока = УдаляемыеОбъекты.Добавить();
			
			Попытка
				НоваяСтрока.Значение   = Объект.Ссылка;
				НоваяСтрока.ТипОбъекта = ТипЗнч(Объект.Ссылка)
			Исключение КонецПопытки;
		ИначеЕсли ТипОбъекта = Тип("РегистрСведенийНаборЗаписей.СоставCheckList") Тогда
			НоваяСтрока = СоставCheckList.Добавить();
			
			НоваяСтрока.Модель    = Объект.Отбор.Модель.Значение;
			НоваяСтрока.CheckList = Объект.Отбор.CheckList.Значение;
			НоваяСтрока.guid      = Объект.Отбор.guid.Значение;
		ИначеЕсли ТипОбъекта = Тип("РегистрСведенийНаборЗаписей.ЗначениеCheckList") Тогда
			НоваяСтрока = ЗначенияCheckList.Добавить();
			
			НоваяСтрока.Заявка    = Объект.Отбор.Объект.Значение;
			НоваяСтрока.CheckList = Объект.Отбор.CheckList.Значение;
			НоваяСтрока.guid      = Объект.Отбор.guid.Значение;
		ИначеЕсли ТипОбъекта = Тип("РегистрСведенийНаборЗаписей.ПрименяемостьДополнительныхПредложенийПоМоделям") Тогда
			НоваяСтрока = ПрименяемостьДополнительныхПредложений.Добавить();
			
			НоваяСтрока.ДополнительноеПредложение = Объект.Отбор.ДополнительноеПредложение.Значение;
			НоваяСтрока.Модель                    = Объект.Отбор.Модель.Значение;
			НоваяСтрока.guid                      = Объект.Отбор.guid.Значение;
		Иначе			
			Если ПроверятьПодразделение Тогда
				Попытка
					Если Узел.Подразделения.Найти(Объект.Ссылка.ПодразделениеКомпании, "ПодразделениеКомпании") = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				Исключение КонецПопытки;
			КонецЕсли;
			
			Попытка
				Если Объект.Ссылка.ПометкаУдаления ИЛИ
					(ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.Автоработы") И НЕ Объект.Ссылка.ЭтоДополнительноеПредложение) Тогда
					
					НоваяСтрока = УдаляемыеОбъекты.Добавить();
					
				Иначе
					НоваяСтрока = ИзмененныеОбъекты.Добавить();
				КонецЕсли;
				
				НоваяСтрока.Значение   = Объект.Ссылка;
				НоваяСтрока.ТипОбъекта = ТипЗнч(Объект.Ссылка);
			Исключение КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	Запись = ИзмененныеОбъекты.Добавить();
	Запись.ТипОбъекта = Тип("РегистрСведенийНаборЗаписей.СоставCheckList");
	Запись.Значение   = СоставCheckList;
	
	Запись = ИзмененныеОбъекты.Добавить();
	Запись.ТипОбъекта = Тип("РегистрСведенийНаборЗаписей.ЗначениеCheckList");
	Запись.Значение   = ЗначенияCheckList;
	
	Запись = ИзмененныеОбъекты.Добавить();
	Запись.ТипОбъекта = Тип("РегистрСведенийНаборЗаписей.ПрименяемостьДополнительныхПредложенийПоМоделям");
	Запись.Значение   = ПрименяемостьДополнительныхПредложений;
	
	Результат = Новый Структура(
		"УдаляемыеОбъекты,ИзмененныеОбъекты, НомерСообщения",
		УдаляемыеОбъекты,
		ИзмененныеОбъекты,
		XMLСтрока(ЗаписьСообщения.НомерСообщения));
	
	// закроем элементы
	ЗаписьСообщения.ЗакончитьЗапись();
	Попытка
		ЗаписьXML.Закрыть();
	Исключение
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Обновление номера принятого сообщения
//
Процедура УстановитьНомерПринятого(Узел, НомерСообщения) Экспорт
	
	УзелОбъект = Узел.Получитьобъект();
	УзелОбъект.НомерПринятого = НомерСообщения;
	
	//УзелОбъект.ОбменДанными.Загрузка = Истина;
	Попытка
		УзелОбъект.Записать();
	Исключение КонецПопытки;
	
КонецПроцедуры

// Получение картинки для узла обмена
//
Функция ПолучитьКартинкуУзлаОбмена(Узел) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КартинкиИФайлы.ИмяФайла,
	|	КартинкиИФайлы.Данные,
	|	КартинкиИФайлы.ДанныеВоВнешнемФайле,
	|	0 КАК ПорядокСортировки
	|ИЗ
	|	РегистрСведений.КартинкиИФайлы КАК КартинкиИФайлы
	|ГДЕ
	|	КартинкиИФайлы.Объект = &Узел
	|	И КартинкиИФайлы.Идентификатор = &ИдентификаторОбъекта
	|
	| //ОбъединениеГруппы
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокСортировки");
	Запрос.УстановитьПараметр("Узел", Узел);
	Запрос.УстановитьПараметр("ИдентификаторОбъекта", "Логотип");
	ДанныеРегистра = Запрос.Выполнить().Выбрать();
	
	Если ДанныеРегистра.Количество() > 0 Тогда
		ДанныеРегистра.Следующий();
		Попытка
			Если ДанныеРегистра.ДанныеВоВнешнемФайле Тогда
				Логотип = Новый ДвоичныеДанные(ДанныеРегистра.ИмяФайла);
			Иначе
				Логотип = ДанныеРегистра.Данные.Получить().ПолучитьДвоичныеДанные();
			КонецЕсли;
		Исключение
		КонецПопытки;
		Если Логотип <> Неопределено И (ТипЗнч(Логотип)=Тип("ДвоичныеДанные")) Тогда
			Возврат Логотип;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции
// Модуль плана обмена Удаленные подразделения

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем ИнициализацияМодуля; 	// Флаг инициализации модуля плана обмена
Перем ЭтотУзелСсылка;		// Ссылка на ЭтотУзел() плана обмена

Перем Лог Экспорт; 			// Используется в качестве флага успешности обмена при вызове из других объектов (робота обмена и т.п.)
Перем КоличествоПопыток; 	// Количество попыток считывания файла сообщения обмена 
Перем ТекПользователь; 		// Текущий пользователь
Перем ИдентификаторИБ; 		// GUID данной информационной базы  XXX

Перем ВестиЛоги Экспорт; 				// Флаг ведения лог-файла при обмене
Перем ВестиЖурнал Экспорт; 				// Флаг ведения журнала регистрации
Перем ФиксироватьПоОбъекту Экспорт; 	// Флаг фиксирования в журнале событий для каждого объекта
Перем КаталогЛогФайлов Экспорт; 		// каталог хранения лог-файлов
Перем ЛогФайлОбъект Экспорт; 			// Текущий лог-файл
Перем ПолноеИмяТекущегоЛога Экспорт; 	// Полное имя текущего лог-файла
Перем КомментироватьХодОбмена Экспорт; 	// Флаг необходимости комментирования хода обмена (при ручном обмене)
Перем ПравилаМиграцииОбъектов Экспорт; 			//кэш правил миграции объектов
Перем СтатусыСобытийПротокола; 			// Перечисление.СтатусыСобытийСистемногоПротокола

Перем КешАвторегистрацииОбъектов Экспорт;// Проверенные типы объектов на авторегистрацию изменений
Перем КешМетаданныхОбъектов      Экспорт;// Закэшированные значение метаданных по типам объектов 

Перем ТемпФайлыДляУдаления; 					// Строка из имен темп-файлов для удаления ХХХ
Перем ПланОбменаМетаданные; 					// кэш метаданных плана обмена
Перем ПрефиксЛога; 								// Префикс лог-файла
Перем ТипУдаление; 								// кэш Тип("УдалениеОбъекта")
Перем ТипВсеДокументы;							// Описание типов все ссылки документов
Перем ТипВсеСправочники;                        // Описание типов все ссылки справочников
Перем ТипВсеПВХ;		                        // Описание типов все ссылки ПВХ
Перем ИдетОбменНастройкамиОбмена Экспорт; 		// флаг отправки настроек обмена
Перем МассивТиповДанныхНастроекОбмена; 	// массив типов ссылок на данные, не подлежащие фильтрации при отправке данных подчиненному
//OkoE - учетка
Перем ПолученныеДанныеОбмена Экспорт; // Фалг на получение данных обмена
Перем ТипПравилаМиграции;
Перем ТипРегистрПрава;
Перем ТипПВХПрава;
Перем ТипПВХПраваОбъект;
Перем ПланОбменаМетаданныеСостав;
Перем РежимЗаписиРегистраПодчинениеРегистратору;


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА  0000

// Получает лог-файл, если не найден, создает новый.
// Если не найдена папка, создает новую.
Функция ПолучитьЛогФайл(НуженНовый = Ложь)
	ЭтотУзелСсылка = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	Если НуженНовый ИЛИ НЕ ЗначениеЗаполнено(ПолноеИмяТекущегоЛога) ИЛИ (ТипЗнч(ЛогФайлОбъект)<>Тип("ЗаписьТекста")) Тогда
		// создаем новый лог-файл
		Если ПустаяСтрока(КаталогЛогФайлов) ИЛИ НЕ одОбменДанными.ЕстьКаталог(КаталогЛогФайлов) Тогда
			КаталогЛогФайлов = КаталогВременныхФайлов();
		КонецЕсли;
		Сч=0;
		Пока Сч<10 Цикл
			Попытка 
				ПолноеИмяТекущегоЛога = КаталогЛогФайлов + СокрЛП(ЭтотУзелСсылка.Ссылка.Код) + "_" + Формат(ТекущаяДата(), "ДФ = гггг-ММ-дд")+ ?(ЗначениеЗаполнено(Сч),"-"+Сч,"") + ".log";
				ЛогФайлОбъект = Новый ЗаписьТекста(ПолноеИмяТекущегоЛога, КодировкаТекста.ANSI,, Истина);
				Прервать;
			Исключение
				Сч=Сч+1;
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	Возврат ЛогФайлОбъект; 
КонецФункции	//	ПолучитьЛогФайл()

// Пишет системный протокол
Процедура ЗаписьЛога(ТипСобытия, СтрСообщения, Знач ОбъектЛога = Неопределено, Знач СтатусСобытия = Неопределено) Экспорт
	УровеньСобытия = СтрЧислоВхождений(ТипСобытия, ".");
	ТекВремяДата = Формат(ТекущаяДата(), "ДФ = 'гггг.ММ.дд ЧЧ:мм:сс'");
	
	СтатусСобытия = ?(СтатусСобытия = Неопределено, Перечисления.СтатусыСобытийСистемногоПротокола.Информация, СтатусСобытия);
	
	Если ОбъектЛога = Неопределено Тогда
		ОбъектЛога = ЭтотУзелСсылка;
	КонецЕсли;
	Если ЛогФайлОбъект = Неопределено Тогда
		ПолучитьЛогФайл();
	КонецЕсли;
	
	Если УровеньСобытия < 3 ИЛИ ФиксироватьПоОбъекту Тогда
		МаскаЛога = ?(ВестиЖурнал, "1", "0") + ?(ВестиЛоги, "1", "0");
		Рез = одОбменДанными.ЗафиксироватьСобытие(ТипСобытия, СтрСообщения, ОбъектЛога, СтатусСобытия, МаскаЛога, ЛогФайлОбъект, ПолноеИмяТекущегоЛога, ТекПользователь);
	КонецЕсли;
	Если Лог = Неопределено Тогда
	    Лог = Новый Структура("Успешность, Сообщение", Истина, "");
	КонецЕсли; 
	Лог.Успешность = Лог.Успешность И (НЕ СтатусСобытия = СтатусыСобытийПротокола.Ошибка);

	Если ФиксироватьПоОбъекту Тогда
		ТекДата = Формат(ТекущаяДата(),"ДФ='HH:mm:ss dd.MM'") + " ";
		Если СтатусСобытия = СтатусыСобытийПротокола.Предупреждение Тогда
			СтатусСообщения_ = СтатусСообщения.Внимание;
		ИначеЕсли СтатусСобытия = СтатусыСобытийПротокола.Ошибка Тогда
			СтатусСообщения_ = СтатусСообщения.Важное;
		Иначе
			СтатусСообщения_ = СтатусСообщения.Информация;
		КонецЕсли;
		СтрИнтСообщения = ТекДата + СтрСообщения;
		Сообщить(СтрИнтСообщения, СтатусСообщения_);
	КонецЕсли;
	
	//блок для внешнего соединения (включая сервис автообменов) и серверного исполнения
	//отписываем в экспортную переменную, для хватания внешним контекстом
	Если УровеньСобытия < 3 ИЛИ ФиксироватьПоОбъекту Тогда
		Лог.Сообщение  = Лог.Сообщение + Рез.Сообщение;
		//Возврат (СтрокаЛога + Символы.ПС);
	КонецЕсли;
КонецПроцедуры	//	ЗаписьЛога()

// Возвращает структуру обязательных / уникальных реквизитов элемента
//
// Возвращаемая структура содержит строковые идентификаторы реквизитов
// Для реквизита значение структуры содержит число: 1 - Обязательный, 2 - Уникальный
Функция ПолучитьОбязательныеРеквизиты() Экспорт
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код", 1);
	ОбязательныеРеквизиты.Вставить("Наименование", 2);
	ОбязательныеРеквизиты.Вставить("Префикс", 2);
		
	Возврат ОбязательныеРеквизиты;
КонецФункции	//	ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
Функция ПроверитьКорректность(Ошибки="") Экспорт
	
	Результат = Истина;
	
	// сначала получим список обязательных у нашего объекта
	Реквизиты = ПолучитьОбязательныеРеквизиты();
	
	// пройдемся по списку реквизитов с проверкой
	Для Каждого Реквизит Из Реквизиты Цикл
		// Сначала проверим заполнение
		Если Реквизит.Значение >= 1 И НЕ ЗначениеЗаполнено(ЭтотОбъект[Реквизит.Ключ]) Тогда
			Результат = Ложь;
			Ошибки = Ошибки + "Реквизит """ + Реквизит.Ключ + """ не заполнен !" + Символы.ПС;
			
			Продолжить; // Если реквизит не заполнен, проверять его уникальность нет смысла
		КонецЕсли;
			
		// Теперь проверим уникальность
		Если Реквизит.Значение = 2 Тогда
			Менеджер = ПланыОбмена[ЭтотОбъект.Метаданные().Имя];
			Если Реквизит.Ключ = "Наименование" Тогда
				НайденнаяСсылка = Менеджер.НайтиПоНаименованию(ЭтотОбъект.Наименование, Истина);
			Иначе
				НайденнаяСсылка = Менеджер.НайтиПоРеквизиту(Реквизит.Ключ, ЭтотОбъект[Реквизит.Ключ]);
			КонецЕсли;
			
			Если НЕ обЗначениеНеЗаполнено(НайденнаяСсылка) И НайденнаяСсылка <> ЭтотОбъект.Ссылка Тогда
				Результат = Ложь;
				Ошибки = Ошибки + "Реквизит """ + Реквизит.Ключ + """ не уникален !" + Символы.ПС;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	//	ПроверитьКорректность()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик отправки данных вверх
// ЭлементДанных - отправляемый объект
// ОтправкаЭлемента - возвращаемый результат (Авто, Игнорировать, Удалить)
Процедура ПриОтправкеДанныхГлавному(ЭлементДанных, ОтправкаЭлемента)
	
	ТипДанных = ТипЗнч(ЭлементДанных);
	ЭтоУдаление = (ТипДанных = ТипУдаление);
	Попытка 
		СсылкаДанных = ЭлементДанных.Ссылка;
	Исключение
		СсылкаДанных = НЕОПРЕДЕЛЕНО;
	КонецПопытки;
	Если ЗначениеЗаполнено(СсылкаДанных) Тогда
		ТипДанных = ТипЗнч(СсылкаДанных);
		Данные = СсылкаДанных; // прочее
	Иначе
		Данные = ЭлементДанных; // регистр или новый документ (ссылки еще нет)
	КонецЕсли;
	
	Если ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО Тогда
		ИдетОбменНастройкамиОбмена = ПараметрыСеанса.ИдетОбменНастройкамиОбмена;
	КонецЕсли;
	Если ИдетОбменНастройкамиОбмена Тогда
		// отправляем только критические данные - ПравилаМиграции, НастройкиДоставки,Структура ИБ	
		Фильтровать = (МассивТиповДанныхНастроекОбмена.Найти(ТипДанных) = Неопределено);
		Если Фильтровать Тогда
			ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;
			// делаем новую регистрацию изменений элемента данных с пустым номером
			ПланыОбмена.ЗарегистрироватьИзменения(Ссылка,ЭлементДанных);
            // ничего не пишем в логи для ускорения работы
			Возврат;
		КонецЕсли; 
	КонецЕсли;
	
	Если ФиксироватьПоОбъекту Тогда
		Представление = одОбменДанными.ПредставлениеДанных(ТипДанных,Данные,ЭтоУдаление);
		ЗаписьЛога(ПрефиксЛога + "Запись","Объект отправлен: " + Представление, ЭлементДанных);
	КонецЕсли;
	счОбъектов = ПараметрыСеанса.СчетчикОбъектов;
	счОбъектов = счОбъектов + 1;
	ПараметрыСеанса.СчетчикОбъектов = счОбъектов;
	Если ТипВсеДокументы.СодержитТип(ТипДанных) Тогда
		счДокументов = ПараметрыСеанса.СчетчикДокументов;
		счДокументов = счДокументов + 1;
		ПараметрыСеанса.СчетчикДокументов = счДокументов;
	ИначеЕсли ТипВсеСправочники.СодержитТип(ТипДанных) Тогда
		счСправочников = ПараметрыСеанса.СчетчикСправочников;
		счСправочников = счСправочников + 1;
		ПараметрыСеанса.СчетчикСправочников = счСправочников;
	КонецЕсли;
	
КонецПроцедуры	//	ПриОтправкеДанныхГлавному()

// Стандартный обработчик отправки данных вниз
// ЭлементДанных - отправляемый объект
// ОтправкаЭлемента - возвращаемый результат (Авто, Игнорировать, Удалить)
Процедура ПриОтправкеДанныхПодчиненному(ЭлементДанных, ОтправкаЭлемента,СозданиеНачальногоОбраза)

	ТипДанных = ТипЗнч(ЭлементДанных);
	ЭтоУдаление = (ТипДанных = ТипУдаление);
	Попытка 
		СсылкаДанных = ЭлементДанных.Ссылка;
	Исключение
		СсылкаДанных = НЕОПРЕДЕЛЕНО;
	КонецПопытки;
	Если ЗначениеЗаполнено(СсылкаДанных) Тогда
		ТипДанных = ТипЗнч(СсылкаДанных);
		Данные = СсылкаДанных; // прочее
	Иначе
		Данные = ЭлементДанных; // регистр или новый документ (ссылки еще нет)
	КонецЕсли;	
	
	Если ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО Тогда
		ИдетОбменНастройкамиОбмена = ПараметрыСеанса.ИдетОбменНастройкамиОбмена;
	КонецЕсли;
	Если ИдетОбменНастройкамиОбмена Тогда
		// отправляем только критические данные - ПравилаМиграции, НастройкиДоставки,Структура ИБ	
		Фильтровать = (МассивТиповДанныхНастроекОбмена.Найти(ТипДанных) = Неопределено);
		Если Фильтровать Тогда
			ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;
			// делаем новую регистрацию изменений элемента данных с пустым номером
			ПланыОбмена.ЗарегистрироватьИзменения(Ссылка,ЭлементДанных);
            // ничего не пишем в логи для ускорения работы
			Возврат;
		КонецЕсли; 
	КонецЕсли;
	Если ФиксироватьПоОбъекту Тогда
		Представление = одОбменДанными.ПредставлениеДанных(ТипДанных,Данные,ЭтоУдаление);
	КонецЕсли;
	// Получим статус авторегистрации объекта
	ЭтоДокумент = Ложь;
	Если ТипВсеДокументы.СодержитТип(ТипДанных) Тогда
		ЭтоДокумент = Истина;
	КонецЕсли;
	СтатусАвторегистрации = одОбменДанными.ОбновитьКешАвторегистрацииОбъектов(ТипДанных,Данные,КешАвторегистрацииОбъектов,КешМетаданныхОбъектов,
		ЭтоДокумент,ПланОбменаМетаданныеСостав, РежимЗаписиРегистраПодчинениеРегистратору);
	// Проверяем статус
	Если СтатусАвторегистрации Тогда
		// если авторегистрация, то отправляем все всегда и всем
	ИначеЕсли СозданиеНачальногоОбраза Тогда
		// у нас идут все объекты и нам нужно фильтровать их согласно правилам
		ФильтроватьОбъект = Истина;
		МетаданныеОбъекта = одОбменДанными.ОбновитьКешМетаданныхОбъектов(ТипДанных,Данные,КешМетаданныхОбъектов);
		// если это не документ, но авторегистрация=ложь - это может быть только регистр, подчиненный регистратору
		Если ЭтоДокумент Тогда
			ФильтроватьОбъект = Истина;
		Иначе
			Попытка
				ЭлементДанных = Данные.Отбор.Регистратор.Значение;
				ТипДанных = ТипЗнч(ЭлементДанных);
				МетаданныеОбъекта = одОбменДанными.ОбновитьКешМетаданныхОбъектов(ТипДанных, ЭлементДанных, КешМетаданныхОбъектов);
			Исключение
				ФильтроватьОбъект = Ложь;
			КонецПопытки;
		КонецЕсли; 
		Если ФильтроватьОбъект Тогда
			МассивДоступныхУзлов = одОбменДанными.ПолучитьМассивУзловОбмена(ЭлементДанных,,,ПланОбменаМетаданные,МетаданныеОбъекта,ЭтоУдаление);
			Если МассивДоступныхУзлов.Найти(Ссылка) = НЕОПРЕДЕЛЕНО Тогда
				// Текущий узел-получатель не обнаружен в списке доступных узлов
				ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать; // Отфильтровываем отправку
				Если ФиксироватьПоОбъекту Тогда
					ЗаписьЛога(ПрефиксЛога + "Запись",
					"Отправка объекта отложена: " + Представление +  " - миграция данного объекта запрещена", ЭлементДанных);
				КонецЕсли;
				Возврат;		
			КонецЕсли;
		КонецЕсли; 
	Иначе
		// все должно быть и так хорошо (зарегистрированы уже только нужные изменения)			 
	КонецЕсли;
	
	Если ФиксироватьПоОбъекту Тогда
		ЗаписьЛога(ПрефиксЛога + "Запись","Объект отправлен: " + Представление, ЭлементДанных);
	КонецЕсли;
	счОбъектов = ПараметрыСеанса.СчетчикОбъектов;
	счОбъектов = счОбъектов + 1;
	ПараметрыСеанса.СчетчикОбъектов = счОбъектов;
	Если ТипВсеДокументы.СодержитТип(ТипДанных) Тогда
		счДокументов = ПараметрыСеанса.СчетчикДокументов;
		счДокументов = счДокументов + 1;
		ПараметрыСеанса.СчетчикДокументов = счДокументов;
	ИначеЕсли ТипВсеСправочники.СодержитТип(ТипДанных) Тогда
		счСправочников = ПараметрыСеанса.СчетчикСправочников;
		счСправочников = счСправочников + 1;
		ПараметрыСеанса.СчетчикСправочников = счСправочников;
	КонецЕсли;
	
КонецПроцедуры	//	ПриОтправкеДанныхПодчиненному()

// Стандартный обработчик получении данных сверху
// ЭлементДанных - получаемый объект
// ПолучениеЭлемента - возвращаемый результат (Авто, Игнорировать, Принять)
// ОтправкаНазад - Признак необходимости регистрации изменений для отправки объекта (ложь по умолчанию)
Процедура ПриПолученииДанныхОтГлавного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	ТипДанных = ТипЗнч(ЭлементДанных);
	ЭтоУдаление = (ТипДанных = ТипУдаление);
	Попытка 
		СсылкаДанных = ЭлементДанных.Ссылка;
	Исключение
		СсылкаДанных = НЕОПРЕДЕЛЕНО;
	КонецПопытки;
	Если ЗначениеЗаполнено(СсылкаДанных) Тогда
		ТипДанных = ТипЗнч(СсылкаДанных);
		Данные = СсылкаДанных; // прочее
	Иначе
		Данные = ЭлементДанных; // регистр или новый документ (ссылки еще нет)
		// все-таки если документ или справ-к новый - получим верный тип данных - не объекта, а именно ссылки
		Если СсылкаДанных = НЕОПРЕДЕЛЕНО Тогда
			ТипДанных = ТипЗнч(ЭлементДанных);
        Иначе
		    ТипДанных = ТипЗнч(СсылкаДанных);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипДанных = ТипПравилаМиграции Тогда
		Если ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО Тогда
			ИдетОбменНастройкамиОбмена = Истина;
			ПараметрыСеанса.ИдетОбменНастройкамиОбмена = ИдетОбменНастройкамиОбмена; // Взвели флаг чтобы отработать после завершения операции обмена
		КонецЕсли; 
		
	ИначеЕсли ТипДанных = ТипРегистрПрава Тогда
		Если ЭлементДанных.Количество()>0 Тогда
			Если ЭлементДанных[0].ПравоНастройка.Родитель = ПланыВидовХарактеристик.ПраваИНастройки.ПравилаМиграцииДокументов Тогда
				Если ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО Тогда
					ИдетОбменНастройкамиОбмена = Истина;
					ПараметрыСеанса.ИдетОбменНастройкамиОбмена = ИдетОбменНастройкамиОбмена; // Взвели флаг чтобы отработать после завершения операции обмена
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
    Если ФиксироватьПоОбъекту Тогда
		Представление = одОбменДанными.ПредставлениеДанных(ТипДанных,Данные,ЭтоУдаление);
	КонецЕсли;
	// вызывается приватный обработчик для каждого объекта (реализовано для БП и задач)
	Попытка
		ЭлементДанных.ПриПолученииДанных(ЭтотОбъект, ПолучениеЭлемента, ОтправкаНазад, Истина);
		Если ФиксироватьПоОбъекту Тогда
			ЗаписьЛога(ПрефиксЛога + "Чтение", "Объект получен: " + Представление, ЭлементДанных);
		КонецЕсли;
		Возврат;
	Исключение
	КонецПопытки;

	// Получим статус авторегистрации объекта
	ЭтоДокумент = Ложь; ЭтоСправочник = Ложь; ЭтоПВХ = Ложь;
	Если ТипВсеДокументы.СодержитТип(ТипДанных) Тогда
		ЭтоДокумент = Истина;
	ИначеЕсли ТипВсеСправочники.СодержитТип(ТипДанных) Тогда
		ЭтоСправочник = Истина;
	ИначеЕсли ТипВсеПВХ.СодержитТип(ТипДанных) Тогда
		ЭтоПВХ = Истина;
	КонецЕсли;
	СтатусАвторегистрации = одОбменДанными.ОбновитьКешАвторегистрацииОбъектов(ТипДанных,Данные,КешАвторегистрацииОбъектов,КешМетаданныхОбъектов,ЭтоДокумент,ПланОбменаМетаданныеСостав);
	
	// для документа далее вызовется его запись и будет использован миникэш документа
	// для регистрации изменений в другие узлы от данного
	
	МетаданныеОбъекта = одОбменДанными.ОбновитьКешМетаданныхОбъектов(ТипДанных,Данные,КешМетаданныхОбъектов);
	Если НЕ СтатусАвторегистрации И ЭтоДокумент Тогда
		одОбменДанными.УстановитьПравилаМиграцииВОбъекте(ТипДанных,ЭлементДанных,МетаданныеОбъекта,ПланОбменаМетаданные,ПравилаМиграцииОбъектов,ЭтоУдаление);
	КонецЕсли;  
	
	// если пришло физическое удаление объект, то удалять объект не будем, а пометим на удаление
	// проверяем для всех, кроме регистров
	Если ЭтоУдаление И НЕ СсылкаДанных = НЕОПРЕДЕЛЕНО Тогда
		Попытка
			Если НЕ СсылкаДанных.Пустая() Тогда
				ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать;
				УдаляемыйОбъект = СсылкаДанных.ПолучитьОбъект();
				// это доп проверка, на случай если объекта уже нет в базе
				Если УдаляемыйОбъект = Неопределено Тогда
					Если ФиксироватьПоОбъекту Тогда
						// у нас этого объекта уже нет 
						ЗаписьЛога(ПрефиксЛога + "Чтение", "Полученный объект " + Представление + " отсутствует в базе 
						|по причине более раннего его удаления." , 
						СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение);
					КонецЕсли;	//!!! а на будущее нужно доработать - "пропустить" пришедшую нам команду на удаление в другие узлы получатели (в наши дочки)
				Иначе
					Если ЭтоДокумент Тогда
						// делаем отметку, что удаляем документ
						// используем именно доп. свойства, а не ОбменДанными
						// так как запись не может выполняться в режиме загрузки данных
						УдаляемыйОбъект.ДополнительныеСвойства.Вставить("Загрузка",Истина); // эмулируем якобы получение объекта через обмены
						УдаляемыйОбъект.ДополнительныеСвойства.Вставить("Отправитель",Ссылка); // устанавливаем отправителя чтобы не отправить назад "эхо"
						УдаляемыйОбъект.ДополнительныеСвойства.Вставить("УдалениеОбъекта",Истина); // устанавливаем флаг отработки команды "удалить объект"
						УдаляемыйОбъект.ПометкаУдаления = Истина;
						
						// Пытаемся пометить на удаление и распровести документ,
						// так как возможны блокировки. 
						КоличествоПопыток = 3;
						ЗаписьВыполнена = Ложь;
						ТекстОшибки = "";
						Для Инд = 1 По КоличествоПопыток Цикл
							Попытка
								Если ЭлементДанных.Ссылка.Проведен Тогда
									УдаляемыйОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения,);  //!! в попытку, 3 раза повторить - не получилось в журнал ОШИБКУ!
								Иначе
									УдаляемыйОбъект.Записать(РежимЗаписиДокумента.Запись,);   //!! в попытку, 3 раза повторить - не получилось в журнал ОШИБКУ!
								КонецЕсли;
								ЗаписьВыполнена = Истина;
								Прервать;
							Исключение
								ТекстОшибки = ОписаниеОшибки();
							КонецПопытки;
						КонецЦикла;
						Если Не ЗаписьВыполнена И ФиксироватьПоОбъекту Тогда
							ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при попытке пометки на удаления документа " + Представление + ":"  
							+ Символы.ПС + ТекстОшибки,СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
						КонецЕсли;
						
					Иначе
						КоличествоПопыток = 3;
						ЗаписьВыполнена = Ложь;
						ТекстОшибки = "";
						Для Инд = 1 По КоличествоПопыток Цикл
							Попытка
								УдаляемыйОбъект.ПометкаУдаления = Истина;
								УдаляемыйОбъект.ДополнительныеСвойства.Вставить("Загрузка",Истина);
								УдаляемыйОбъект.Записать();   //!! в попытку, 3 раза повторить - не получилось в журнал ОШИБКУ
								ЗаписьВыполнена = Истина;
								Прервать;
							Исключение
								ТекстОшибки = ОписаниеОшибки();
							КонецПопытки;
						КонецЦикла;
						Если Не ЗаписьВыполнена И ФиксироватьПоОбъекту Тогда
							ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при попытке пометки на удаления объекта " + Представление + ":"  
							+ Символы.ПС + ТекстОшибки,СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
						КонецЕсли;
					КонецЕсли;
					
					// определимся, есть ли уже изменения - может удалять ничего и не надо
					// игнорировать, как при получении от главного не нужно - от родителя принимаем беспрекословно
					ИзменениеЗарегистрировано = Ложь;
					ЭлементРегистрации =ЭлементДанных;
					Если (ЭтоДокумент ИЛИ ЭтоСправочник ИЛИ ЭтоПВХ)  И НЕ ЭтоУдаление Тогда
						Если ЭлементДанных.ЭтоНовый() Тогда
							ЭлементРегистрации = ЭлементДанных.ПолучитьСсылкуНового();
						КонецЕсли;
					КонецЕсли; 
					ИзменениеЗарегистрировано = ПланыОбмена.ИзменениеЗарегистрировано(Ссылка, ЭлементРегистрации);
					
					Если ИзменениеЗарегистрировано И ЗаписьВыполнена Тогда
						КоличествоПопыток = 3;
						РегистрацияУдалена = Ложь;
						ТекстОшибки = "";
						Для Инд = 1 По КоличествоПопыток Цикл
							Попытка
								ПланыОбмена.УдалитьРегистрациюИзменений(Ссылка, УдаляемыйОбъект.Ссылка);
								РегистрацияУдалена = Истина;
								Прервать;
							Исключение
								ТекстОшибки = ОписаниеОшибки();
							КонецПопытки;
						КонецЦикла;
						Если Не РегистрацияУдалена И ФиксироватьПоОбъекту Тогда
							ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при попытке удаления регистрации объекта " + Представление + ":"  
							+ Символы.ПС + ТекстОшибки,СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		Исключение
			Если ФиксироватьПоОбъекту Тогда
				ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при установке пометки удаления объекта: " + Представление, ЭлементДанных);
			КонецЕсли;
			Возврат;
		КонецПопытки;
	КонецЕсли;	
	
	Если ФиксироватьПоОбъекту Тогда
		ЗаписьЛога(ПрефиксЛога + "Чтение", "Объект получен: " + Представление, ЭлементДанных);
	КонецЕсли;
	
	счОбъектов = ПараметрыСеанса.СчетчикОбъектов;
	счОбъектов = счОбъектов + 1;
	ПараметрыСеанса.СчетчикОбъектов = счОбъектов;
	Если ЭтоДокумент Тогда
		счДокументов = ПараметрыСеанса.СчетчикДокументов;
		счДокументов = счДокументов + 1;
		ПараметрыСеанса.СчетчикДокументов = счДокументов;
	ИначеЕсли ЭтоСправочник Тогда
		счСправочников = ПараметрыСеанса.СчетчикСправочников;
		счСправочников = счСправочников + 1;
		ПараметрыСеанса.СчетчикСправочников = счСправочников;
	КонецЕсли;
	
КонецПроцедуры	//	ПриПолученииДанныхОтГлавного()


// Стандартный обработчик получении данных снизу
// ЭлементДанных - получаемый объект
// ПолучениеЭлемента - возвращаемый результат (Авто, Игнорировать, Принять)
// ОтправкаНазад - Признак необходимости регистрации изменений для отправки объекта (ложь по умолчанию)
Процедура ПриПолученииДанныхОтПодчиненного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	ТипДанных = ТипЗнч(ЭлементДанных);
	ЭтоУдаление = (ТипДанных = ТипУдаление);
	Попытка 
		СсылкаДанных = ЭлементДанных.Ссылка;
	Исключение
		СсылкаДанных = НЕОПРЕДЕЛЕНО;
	КонецПопытки;
	Если ЗначениеЗаполнено(СсылкаДанных) Тогда
		ТипДанных = ТипЗнч(СсылкаДанных);
		Данные = СсылкаДанных; // прочее
	Иначе
		Данные = ЭлементДанных; // регистр или новый документ (ссылки еще нет)
		// все-таки если документ или справ-к новый - получим верный тип данных - не объекта, а именно ссылки
		Если СсылкаДанных = НЕОПРЕДЕЛЕНО Тогда
			ТипДанных = ТипЗнч(ЭлементДанных);
        Иначе
		    ТипДанных = ТипЗнч(СсылкаДанных);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипДанных = ТипПравилаМиграции Тогда
		Если ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО Тогда
			ИдетОбменНастройкамиОбмена = Истина;
			ПараметрыСеанса.ИдетОбменНастройкамиОбмена = ИдетОбменНастройкамиОбмена; // Взвели флаг чтобы отработать после завершения операции обмена
		КонецЕсли; 
		
	ИначеЕсли ТипДанных = ТипРегистрПрава Тогда
		Если ЭлементДанных.Количество()>0 Тогда
			Если ЭлементДанных[0].ПравоНастройка.Родитель = ПланыВидовХарактеристик.ПраваИНастройки.ПравилаМиграцииДокументов Тогда
				Если ИдетОбменНастройкамиОбмена = НЕОПРЕДЕЛЕНО Тогда
					ИдетОбменНастройкамиОбмена = Истина;
					ПараметрыСеанса.ИдетОбменНастройкамиОбмена = ИдетОбменНастройкамиОбмена; // Взвели флаг, чтобы отработать после завершения операции обмена
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	Если ФиксироватьПоОбъекту Тогда
		Представление = одОбменДанными.ПредставлениеДанных(ТипДанных,Данные,ЭтоУдаление);
	КонецЕсли;
	//вызывается приватный обработчик для каждого объекта (реализовано для БП и задач)
	Попытка
		ЭлементДанных.ПриПолученииДанных(ЭтотОбъект, ПолучениеЭлемента, ОтправкаНазад, Ложь);
		Если ФиксироватьПоОбъекту Тогда
			ЗаписьЛога(ПрефиксЛога + "Чтение","Объект получен: " + Представление, ЭлементДанных);
		КонецЕсли;
		Возврат;
	Исключение
	КонецПопытки;
	
	// Получим статус авторегистрации объекта
	ЭтоДокумент = Ложь; ЭтоСправочник = Ложь; ЭтоПВХ = Ложь;
	Если ТипВсеДокументы.СодержитТип(ТипДанных) Тогда
		ЭтоДокумент = Истина;
	ИначеЕсли ТипВсеСправочники.СодержитТип(ТипДанных) Тогда
		ЭтоСправочник = Истина;
	ИначеЕсли ТипВсеПВХ.СодержитТип(ТипДанных) Тогда
		ЭтоПВХ = Истина;
	КонецЕсли;

	СтатусАвторегистрации = одОбменДанными.ОбновитьКешАвторегистрацииОбъектов(ТипДанных,Данные,КешАвторегистрацииОбъектов,КешМетаданныхОбъектов,ЭтоДокумент,ПланОбменаМетаданныеСостав);
	
	// для документа далее вызовется его запись и будет использован миникэш документа
	// для регистрации изменений в другие узлы от данного
	МетаданныеОбъекта = одОбменДанными.ОбновитьКешМетаданныхОбъектов(ТипДанных,Данные,КешМетаданныхОбъектов);
	Если НЕ СтатусАвторегистрации И ЭтоДокумент Тогда
	    одОбменДанными.УстановитьПравилаМиграцииВОбъекте(ТипДанных,ЭлементДанных,МетаданныеОбъекта,ПланОбменаМетаданные,ПравилаМиграцииОбъектов,ЭтоУдаление);
	КонецЕсли;  

	// пришло изменение объекта от дочернего узла, но у нас (в родителе) уже есть изменения этого же объекта
	// поэтому отказываемся от приема этих данных во избежание "перетасовки" - чтобы не вышло
	// что будет не помеченый объект в дочке, а помеченый на удаление в родителе (поменялись местами)
	ИзменениеЗарегистрировано = Ложь;
	СсылкаРегистрации =ЭлементДанных;
	Если (ЭтоДокумент ИЛИ ЭтоСправочник ИЛИ ЭтоПВХ) И НЕ ЭтоУдаление Тогда
	    Если ЭлементДанных.ЭтоНовый() Тогда
	        СсылкаРегистрации = ЭлементДанных.ПолучитьСсылкуНового();
		КонецЕсли;
	КонецЕсли; 
 
	Если ПланыОбмена.ИзменениеЗарегистрировано(Ссылка, СсылкаРегистрации) Тогда 
		// изменения есть, получаемый элемент надо отвергнуть 
		ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать; 
		//ОтправкаНазад = Истина;
		Если ФиксироватьПоОбъекту Тогда
			ЗаписьЛога(ПрефиксЛога + "Чтение", "Получение объекта отменено: " + Представление
					+ ": для объекта уже зарегистрированы изменения. ", ЭлементДанных, СтатусыСобытийПротокола.Предупреждение);
		КонецЕсли;
	    ИзменениеЗарегистрировано = Истина;
		Возврат;
	КонецЕсли;
		
	Если СсылкаДанных = НЕОПРЕДЕЛЕНО Тогда
		// это регистр
		// причина проверки на заполненность - движения могли прийти раньше документа
		// если пришло физическое удаление объект, то удалять объект не будем, а пометим на удаление
		// НаборЗаполнен - на случай, если регистр
		Попытка
			НаборЗаполнен = НЕ (ЭлементДанных.Отбор.Регистратор.Значение.ПолучитьОбъект().Движения[МетаданныеОбъекта.Имя].Количество() = 0);
		Исключение
			НаборЗаполнен = Ложь; // вдруг это константа
		КонецПопытки;
		// если движения по регистру уже заполнены, отказываемся от их повторного приема
		Если НаборЗаполнен Тогда
			ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать; 
			ОтправкаНазад = Истина;
			
			Если ФиксироватьПоОбъекту Тогда
				ЗаписьЛога(ПрефиксЛога + "Чтение", "Получение объекта отменено: " + Представление
				+ ": для объекта уже зарегистрированы изменения. ", ЭлементДанных, СтатусыСобытийПротокола.Предупреждение);
			КонецЕсли;
			Возврат;
		КонецЕсли;
	Иначе	
		Если ЭтоУдаление  Тогда
			Попытка
				Если НЕ СсылкаДанных.Пустая() Тогда
					ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать;
					УдаляемыйОбъект = СсылкаДанных.ПолучитьОбъект();
					// это доп проверка, на случай если объекта уже нет в базе
					Если УдаляемыйОбъект = Неопределено Тогда
						Если ФиксироватьПоОбъекту Тогда
							// у нас этого объекта уже нет 
							ЗаписьЛога(ПрефиксЛога + "Чтение", "Полученный объект " + Представление + " отсутствует в базе 
								|по причине более раннего его удаления." , 
							СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Предупреждение);
						КонецЕсли;
						//!!! а на будущее нужно доработать - "пропустить" пришедшую нам команду на удаление в другие узлы
						//    получатели (в наши дочки)
					Иначе
						Если ЭтоДокумент Тогда
							// делаем отметку, что удаляем документ
							УдаляемыйОбъект.ДополнительныеСвойства.Вставить("Загрузка",Истина); // эмулируем якобы получение объекта через обмены
							УдаляемыйОбъект.ДополнительныеСвойства.Вставить("Отправитель",Ссылка); // устанавливаем отправителя чтобы не отправить назад "эхо"
							УдаляемыйОбъект.ДополнительныеСвойства.Вставить("УдалениеОбъекта",Истина); // устанавливаем флаг отработки команды "удалить объект"
							УдаляемыйОбъект.ПометкаУдаления = Истина;
							
							// Пытаемся пометить на удаление и распровести документ,
							// так как возможны блокировки.
							КоличествоПопыток = 3;
							ЗаписьВыполнена = Ложь;
							ТекстОшибки = "";
							Для Инд = 1 По КоличествоПопыток Цикл
								Попытка
									Если ЭлементДанных.Ссылка.Проведен Тогда
										УдаляемыйОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения,);  //!! в попытку, 3 раза повторить - не получилось в журнал ОШИБКУ!
									Иначе
										УдаляемыйОбъект.Записать(РежимЗаписиДокумента.Запись,);   //!! в попытку, 3 раза повторить - не получилось в журнал ОШИБКУ!
									КонецЕсли;
									ЗаписьВыполнена = Истина;
									Прервать;
								Исключение
									ТекстОшибки = ОписаниеОшибки();
								КонецПопытки;
							КонецЦикла;
							Если Не ЗаписьВыполнена И ФиксироватьПоОбъекту Тогда
								ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при попытке пометки на удаления документа " + Представление + ":"  
								+ Символы.ПС + ТекстОшибки,СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
							КонецЕсли;
							
						Иначе
							КоличествоПопыток = 3;
							ЗаписьВыполнена = Ложь;
							ТекстОшибки = "";
							Для Инд = 1 По КоличествоПопыток Цикл
								Попытка
									УдаляемыйОбъект.ПометкаУдаления = Истина;
									УдаляемыйОбъект.ДополнительныеСвойства.Вставить("Загрузка",Истина);
									УдаляемыйОбъект.Записать();   //!! в попытку, 3 раза повторить - не получилось в журнал ОШИБКУ
									ЗаписьВыполнена = Истина;
									Прервать;
								Исключение
									ТекстОшибки = ОписаниеОшибки();
								КонецПопытки;
							КонецЦикла;
							Если Не ЗаписьВыполнена И ФиксироватьПоОбъекту Тогда
								ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при попытке пометки на удаления объекта " + Представление + ":"  
								+ Символы.ПС + ТекстОшибки,СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
							КонецЕсли;
						КонецЕсли;
						
						КоличествоПопыток = 3;
						РегистрацияУдалена = Ложь;
						ТекстОшибки = "";
						Если ЗаписьВыполнена Тогда
							Для Инд = 1 По КоличествоПопыток Цикл
								Попытка
									ПланыОбмена.УдалитьРегистрациюИзменений(Ссылка, УдаляемыйОбъект.Ссылка);
									РегистрацияУдалена = Истина;
									Прервать;
								Исключение
									ТекстОшибки = ОписаниеОшибки();
								КонецПопытки;
							КонецЦикла;
							Если Не РегистрацияУдалена И ФиксироватьПоОбъекту Тогда
								ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при попытке удаления регистрации объекта " + Представление + ":"  
								+ Символы.ПС + ТекстОшибки,СсылкаДанных,Перечисления.СтатусыСобытийСистемногоПротокола.Ошибка);
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
				КонецЕсли;
			Исключение
				Если ФиксироватьПоОбъекту Тогда
					ЗаписьЛога(ПрефиксЛога + "Чтение", "Ошибка при установке пометки удаления объекта: " + Представление, ЭлементДанных);
				КонецЕсли;
				Возврат;
			КонецПопытки;
		КонецЕсли;	
	КонецЕсли;
	
	Если ФиксироватьПоОбъекту Тогда
		ЗаписьЛога(ПрефиксЛога + "Чтение", "Объект получен: " + Представление, ЭлементДанных);
	КонецЕсли;
	счОбъектов = ПараметрыСеанса.СчетчикОбъектов;
	счОбъектов = счОбъектов + 1;
	ПараметрыСеанса.СчетчикОбъектов = счОбъектов;
	Если ЭтоДокумент Тогда
		счДокументов = ПараметрыСеанса.СчетчикДокументов;
		счДокументов = счДокументов + 1;
		ПараметрыСеанса.СчетчикДокументов = счДокументов;
	ИначеЕсли ЭтоСправочник Тогда
		счСправочников = ПараметрыСеанса.СчетчикСправочников;
		счСправочников = счСправочников + 1;
		ПараметрыСеанса.СчетчикСправочников = счСправочников;
	КонецЕсли;

КонецПроцедуры	//	ПриПолученииДанныхОтПодчиненного()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// Процедура инициализации параметров
Процедура ИнициализацияПараметров() Экспорт
	
	Если ЗначениеЗаполнено(ИнициализацияМодуля) Тогда
		Возврат; 
	КонецЕсли;
	
	ТемпФайлыДляУдаления = "";
	ФайлыДляПеремещения = "";

	КомментироватьХодОбмена = Ложь;
	ФиксироватьПоОбъекту = Ложь;
	
	КешАвторегистрацииОбъектов = Новый Соответствие;
	КешМетаданныхОбъектов = Новый Соответствие;
	ПравилаМиграцииОбъектов = Новый Соответствие;
	
	ЭтотУзелСсылка = ПланыОбмена.УдаленныеПодразделения.ЭтотУзел();
	ПланОбменаМетаданные = Метаданные();
	ПланОбменаМетаданныеСостав = ПланОбменаМетаданные.Состав;
	КоличествоПопыток = 20;
	
	ПараметрыСеанса_ПараметрыОбмена = ПараметрыСеанса.ПараметрыОбменаДанными.Получить();
	ЕстьПараметрыСеансаОбмен = (ПараметрыСеанса_ПараметрыОбмена.Количество() > 0);
	
	Если ЕстьПараметрыСеансаОбмен Тогда
		ВестиЖурнал = ПараметрыСеанса_ПараметрыОбмена.ВестиЖурналРегистрации;
		ФиксироватьПоОбъекту = ПараметрыСеанса_ПараметрыОбмена.ФиксироватьПриемОтправкуОбъектов;
		ВестиЛоги = ПараметрыСеанса_ПараметрыОбмена.ВестиЛогФайл;
		КаталогЛогФайлов = ПараметрыСеанса_ПараметрыОбмена.КаталогЛогФайла;
		КомментироватьХодОбмена = ПараметрыСеанса_ПараметрыОбмена.КомментироватьХодОбмена;
		ПолучитьЛогФайл();
		ИнициализацияМодуля = Истина;
	Иначе
		ВестиЖурнал = Ложь;
		ФиксироватьПоОбъекту = Ложь;
		ВестиЛоги = Ложь;
		КаталогЛогФайлов = "";
		АрхивироватьСообщения = Ложь;
		ПарольАрхивацииЗагрузка = Ложь;
		КомментироватьХодОбмена = Ложь;
	КонецЕсли;
	
	Попытка
		ТекПользователь = ПараметрыСеанса.Пользователь;
	Исключение
		#Если НЕ Клиент Тогда
			ТекПользователь = Справочники.Пользователи.Робот;
		#Иначе 
			ТекПользователь = Справочники.Пользователи.ПустаяСсылка();
		#КонецЕсли
	КонецПопытки;
	
	Лог = Новый Структура("Успешность, Сообщение", Истина, "");
	
	СтатусыСобытийПротокола = Перечисления.СтатусыСобытийСистемногоПротокола;
	
	ПрефиксЛога = "Обмен." + ?(ЭтотОбъект.Ссылка.Пустая(),"","Узел_" + СокрЛП(ЭтотОбъект.Ссылка.Префикс) + ".");
	ТипУдаление = Тип("УдалениеОбъекта");
	ТипПравилаМиграции = Тип("СправочникСсылка.ПравилаМиграцииИДоступа");
	ТипРегистрПрава = Тип("РегистрСведенийНаборЗаписей.ПраваИНастройки");
	ТипВсеДокументы = Документы.ТипВсеСсылки();
	ТипВсеСправочники = Справочники.ТипВсеСсылки();
	ТипВсеПВХ = ПланыВидовХарактеристик.ТипВсеСсылки();
	// массив типов с данными, которые управляют обменами 
	МассивТиповДанныхНастроекОбмена = Новый Массив;
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникСсылка.ПравилаМиграцииИДоступа"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникСсылка.НастройкиДоставкиСообщений"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникСсылка.СтруктураИнформационныхБаз"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникСсылка.ПодразделенияКомпании"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("ПланОбменаСсылка.УдаленныеПодразделения"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("ПланВидовХарактеристикСсылка.ПраваИНастройки"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("РегистрСведенийНаборЗаписей.ПраваИНастройки"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникОбъект.ПравилаМиграцииИДоступа"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникОбъект.НастройкиДоставкиСообщений"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникОбъект.СтруктураИнформационныхБаз"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("СправочникОбъект.ПодразделенияКомпании"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("ПланОбменаОбъект.УдаленныеПодразделения"));
	МассивТиповДанныхНастроекОбмена.Добавить(Тип("ПланВидовХарактеристикОбъект.ПраваИНастройки"));
	
	РежимЗаписиРегистраПодчинениеРегистратору = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору;
КонецПроцедуры	//	ИнициализацияПараметров()

// Стандартный обработчик ПередЗаписью элемента плана обмена
// Параметры:
// ЭлементДанных - ПланОбменаОбъект - Узел плана обмена, прочитанный из сообщения обмена
// Игнорировать - Булево - Если установить Истина, то узел плана обмена в базу данных записан не будет
Процедура ПередЗаписью(Отказ)

	// новый узел может быть только подчиненным узлом
	// т.к. этот узел существует изначально
	// соот-но при соблюдении этого условия - начинает испол-ся РБД
	// поэтому дополнительно на условие количество узлов не проверяем
	Если ЭтоНовый() Тогда
		ПараметрыСеанса.ИспользованиеРБД = Истина;
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	// Проверим правильность заполнения реквизитов
	СтрОшибок = "";
	Если НЕ ПроверитьКорректность(СтрОшибок) Тогда
		#Если Клиент Тогда
			Сообщить("Для узла """ + Наименование + """ обнаружены следующие ошибки: " + СтрОшибок, СтатусСообщения.Внимание);
		#КонецЕсли
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ  = Истина Тогда
	   Возврат;
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ИнициализацияПараметров();
// Процедура заполнения предопределённых элементов справочника
//
// Параметры
//  ЗаполнятьВсе - Булево
//
Процедура ЗаполнитьПредопределенныеЭлементы(ЗаполнятьВсе=Ложь) Экспорт
	// Получим макет настроек по умолчанию
	ИмяМакета = "НастройкиПоУмолчанию";
	Макет = ПланыВидовХарактеристик.ПараметрыПланирования.ПолучитьМакет(ИмяМакета);
	Если Макет = Неопределено Тогда
		Сообщить("Не обнаружен макет начальных настроек!");
		Возврат;
	КонецЕсли;
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		// Считаем из макета Имя, Наименование, Код, Тип, ОписаниеПараметра
		СтрИмя = СокрЛП(Макет.Область(НомерСтроки,1).Текст);
		СтрНаименование = СокрЛП(Макет.Область(НомерСтроки,2).Текст);
		СтрКод = СокрЛП(Макет.Область(НомерСтроки,3).Текст);
		СтрТип = СокрЛП(Макет.Область(НомерСтроки,4).Текст);
		СтрОписаниеПараметра = СокрЛП(Макет.Область(НомерСтроки,5).Текст);
		//будем искать с таким же наименованием
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ПараметрыПланирования.Ссылка КАК ПараметрПланирования
		|ИЗ
		|	ПланВидовХарактеристик.ПараметрыПланирования КАК ПараметрыПланирования
		|ГДЕ
		|	ПараметрыПланирования.Наименование = &Наименование И ПараметрыПланирования.Предопределенный = Ложь 
		|");
		Запрос.УстановитьПараметр("Наименование",СтрНаименование);
		Выборка = Запрос.Выполнить().Выбрать();
		//берем предопределенный элемент
		ПредопределеннаяСтатья = ПланыВидовХарактеристик.ПараметрыПланирования[СтрИмя];
		Если ПредопределеннаяСтатья.Пустая() Тогда
			Сообщить("Ошибка при заполнении плана видов характеристик: '" + СтрНаименование + "'. Указан отсутствующий элемент плана видов характеристик ПараметрыПланирования!");
			Продолжить;
		КонецЕсли;
		ПредопределеннаяСтатьяОбъект = ПредопределеннаяСтатья.ПолучитьОбъект();
		Номер = 1;
		//если с таким именем есть, будем заменять
		Пока Выборка.Следующий() Цикл
			//используем стандартную обработку
			ФормаЗаменаЗначений = Обработки.ПоискИЗаменаЗначений.ПолучитьФорму();
			ФормаЗаменаЗначений.Программно = Истина;
			СтрокаЗамены = ФормаЗаменаЗначений.ЗаменяемыеЗначения.Добавить();
			СтрокаЗамены.ЧтоЗаменять = Выборка.ПараметрПланирования;
			СтрокаЗамены.НаЧтоЗаменять = ПредопределеннаяСтатья;
			СтрокаЗамены.Пометка = Истина;
			Сообщить("План видов характеристик ""ПараметрыПланирования"". Произведена замена ссылок на элемент """ + Выборка.ПараметрПланирования + """ ссылками на элемент """ + ПредопределеннаяСтатья + """");
			ФормаЗаменаЗначений.Открыть();
			//
			ОбъектПараметрыПланирования = Выборка.ПараметрПланирования.ПолучитьОбъект();
			ОбъектПараметрыПланирования.Наименование = СтрНаименование+"_" + Номер;
			ОбъектПараметрыПланирования.Записать();
			Номер = Номер + 1;
		КонецЦикла;
		//перезаполняем реквизиты
		Если ЗаполнятьВсе Тогда
			ПредопределеннаяСтатьяОбъект.Наименование = СтрНаименование;
			ПредопределеннаяСтатьяОбъект.ОписаниеПараметра = СтрОписаниеПараметра;
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(Тип(СтрТип));
			Типы = Новый ОписаниеТипов(МассивТипов,,,);
			ПредопределеннаяСтатьяОбъект.ТипЗначения = Типы;
		Иначе
			Если обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.Наименование) Тогда
				ПредопределеннаяСтатьяОбъект.Наименование = СтрНаименование;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.ТипЗначения) Тогда
				МассивТипов = Новый Массив;
				МассивТипов.Добавить(Тип(СтрТип));
				Типы = Новый ОписаниеТипов(МассивТипов,,,);
				ПредопределеннаяСтатьяОбъект.ТипЗначения = Типы;
			КонецЕсли;
			Если обЗначениеНеЗаполнено(ПредопределеннаяСтатьяОбъект.ОписаниеПараметра) Тогда
				ПредопределеннаяСтатьяОбъект.ОписаниеПараметра = СтрОписаниеПараметра;
			КонецЕсли;
		КонецЕсли;
		//Сохранение ПВХ
		НовыйЭлемент = ПредопределеннаяСтатьяОбъект.ЭтоНовый();
		Если ПредопределеннаяСтатьяОбъект.Модифицированность() Тогда
			Попытка
				ПредопределеннаяСтатьяОбъект.Записать();
				Сообщить("План видов характеристик ""ПараметрыПланирования"". "+?(НовыйЭлемент,"Добавлен","Обновлен")+" элемент """+СокрЛП(ПредопределеннаяСтатьяОбъект.Наименование)+"""");
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПредопределенныеЭлементы()

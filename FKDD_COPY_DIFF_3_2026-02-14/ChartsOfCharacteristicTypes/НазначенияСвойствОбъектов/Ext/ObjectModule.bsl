// Модуль ПВХ НазначенияСвойствОбъекта

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт;	// Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА

// Возвращает структуру обязательных / уникальных реквизитов элемента
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный
//
// Параметры
//	ДляГруппы - Булево
//	ДляЭлемента - Булево
//
// Возвращаемое значение:
//	ОбязательныеРеквизиты - Структура
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты = Новый Структура();
	ОбязательныеРеквизиты.Вставить("Код",1);
	ОбязательныеРеквизиты.Вставить("Наименование",3);

	Если ДляЭлемента Тогда
		ОбязательныеВидыСвойств = Новый Структура();
		ОбязательныеВидыСвойств.Вставить("Свойство",3);
		ОбязательныеРеквизиты.Вставить("ВидыСвойств",ОбязательныеВидыСвойств);
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку описанием возникших ошибок "Ошибки"
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки
// может управляться булевыми флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные)
// Обычно выполняется универсальным обработчиком, но могут быть добавлены доп. проверки
//
// Параметры
//	Ошибки - Строка
// 	ДопРеквизиты - структура
//  Заполнение - Булево
//  Уникальность - Булево
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
    // Предопределенные объекты вообще запрещены на любые изменения,
	// а вот для не предопределенных, добавленных исключительно вводом на основании,
	// необходимо заполнение ссылки на базовое назначение и объекта, к которому относится назначение
	Если НЕ Предопределенный Тогда
		Если ДопРеквизиты = Неопределено Тогда
			ДопРеквизиты = Новый Структура();
		КонецЕсли;
		ДопРеквизиты.Вставить("БазовоеНазначение",1);
		ДопРеквизиты.Вставить("Объект",3);
	КонецЕсли;
	Возврат спПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
КонецФункции // ПроверитьКорректность()
 
// Функция проверяет, допустимо ли изменение объекта
// Возвращает Истина, если изменения возможны, ложь иначе
// Если изменения доступны частично, возвращает ложь и структуру блокируемых на изменение реквизитов,
//
// Параметры
//	БлокироватьРеквизиты - Структура
//
Функция ДоступностьИзменения(БлокироватьРеквизиты=Неопределено) Экспорт
	Возврат Истина;
КонецФункции // ДоступностьИзменения()

// Формирует стандартное наименование назначения свойств
// в формате <ВидОбъекта> - <ПредставлениеОбъекта>
// Используется как при интерактивном, так и при программном создании
//
// Без параметров
//
Процедура СформироватьНаименование() Экспорт
	Если НЕ БазовоеНазначение.Пустая() Тогда
		Наименование = СокрЛП(БазовоеНазначение.Наименование)
		             + " - "+Объект;
	КонецЕсли;
КонецПроцедуры // СформироватьНаименование()

// Обработка изменения реквизитов справочников
//
// Параметры
//  Имя			– Строка			– Имя реквизита справочника с полным путем.
//  ЭтаФорма	– Форма				– Ссылка на форму справочника. Если значение неопределено,
//									  производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части справочника, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей справочников.
//  ДопПараметры– Структура			– Структура, содержащая дополнительные параметры обработки реквизита.
//
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
//
Функция ОбработкаРеквизита(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено) Экспорт
	Возврат спОбработкаРеквизита(ЭтотОбъект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
КонецФункции // ОбработкаРеквизита()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик заполнения при вводе нового на основании
Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Элементы вводятся только на основании предопределенных
	Если НЕ обЗначениеНеЗаполнено(Основание) Тогда
		// Сначала заполним реквизиты
		Родитель = Основание.Родитель;
		Если Основание.Предопределенный Тогда
			БазовоеНазначение = Основание;
		Иначе
			БазовоеНазначение = Основание.БазовоеНазначение;
		КонецЕсли;
		ТипЗначения = Основание.ТипЗначения;
		СформироватьНаименование();
	КонецЕсли;
	Если НЕ спОбработкаЗаполнения(ЭтотОбъект, Основание) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	Если НЕ обЗначениеНеЗаполнено(БазовоеНазначение) Тогда
		// Установим код
		Префикс = Лев(БазовоеНазначение.Код,5);
	КонецЕсли;
КонецПроцедуры // ПриУстановкеНовогоКода()
   
// Стандартный обработчик копирования объекта
Процедура ПриКопировании(ОбъектКопирования)
	Если НЕ ЭтоГруппа Тогда
		Объект = Неопределено;
		// Очищаем табличную часть
		ВидыСвойств.Очистить();
		Если НЕ спПриКопировании(ЭтотОбъект, ОбъектКопирования) Тогда Возврат; КонецЕсли;
		Заполнить(ОбъектКопирования.Ссылка);
	КонецЕсли;
КонецПроцедуры // ПриКопировании()

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	Если НЕ спПередЗаписью(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПриЗаписи элемента справочника
Процедура ПриЗаписи(Отказ)
	Если НЕ спПриЗаписи(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПриЗаписи()

// Стандартный обработчик ПередУдалением элемента
Процедура ПередУдалением(Отказ)
	Если НЕ спПередУдалением(ЭтотОбъект, Отказ) Тогда Возврат; КонецЕсли;
КонецПроцедуры // ПередУдалением()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПрава;
#КонецЕсли